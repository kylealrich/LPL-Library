ConsolidatedVendorItemDetails is a Field

    Representation
        Group Fields
            Vendor
            VendorItem
			UpdatedVendorItem            is a VendorItem 

	Context Fields 
		ContractGroup
		ConsolidatedItemMaster

	Derived Fields 

		DerivedVendorName is a DerivedField
			type is like VendorName 
			return Vendor.VendorName 

		DerivedVendorItemBuyUOM is a DerivedField
			type is Alpha 20 
			if (VendorItemExists)
				return VendorItemRel.VendorBuyUOM 
			else 
				return first ContractLinesByManufacturerRel.UOM.UnitOfMeasure

		DerivedVendorItemManufacturer is a DerivedField
			type is Alpha 25
			if (VendorItemExists)
				return VendorItemRel.Manufacturer 
			else 
				return ConsolidatedItemMaster.Manufacturer

		DerivedVendorItemManufacturerNumber is a DerivedField
			type is Alpha 40
			if (VendorItemExists)
				return VendorItemRel.ManufacturerNumber 
			else 
				return ConsolidatedItemMaster.ManufacturerNumber

	Conditions 

		VendorItemExists
			restricted 
			when (VendorItemRel exists)
	
	Relations 

		ContractLinesByManufacturerRel 
			one-to-many relation to ContractLine 
			Field Mapping uses ByMfgInfoVenItem
				related.ContractGroup 						= ContractGroup
				related.Manufacturer.ManufacturerCode		= ConsolidatedItemMaster.Manufacturer.ManufacturerCode 
				related.Manufacturer.ManufacturerDivision   = ConsolidatedItemMaster.Manufacturer.ManufacturerDivision
				related.ManufacturerNumber                  = ConsolidatedItemMaster.ManufacturerNumber 
			Instance Selection 
				where (related.LineNotClosed
				and    related.Vendor 		= Vendor
				and    related.VendorItem 	= VendorItem)	

		VendorItemRel 
			one-to-one relation to VendorItem 
			Field Mapping uses symbolic key 
				related.ProcurementGroup					= ContractGroup 
				related.Item                                = ConsolidatedItemMaster.InternalItem 
				related.Vendor                              = Vendor 
				related.VendorItem                          = VendorItem 

	
