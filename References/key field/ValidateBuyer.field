ValidateBuyer is a Field

    Representation
        Group Fields
			InPurchasingCompany				is a PurchasingCompany
			InShipToLocation				is like InventoryLocation
			InVendor						is a Vendor
			InPurchaseFromLocation			is like PurchaseFromLocation
			InMajorPurchasingClass			is a MajorPurchasingClass
			InMinorPurchasingClass			is a MinorPurchasingClass
			InItemType						is like ItemType
			InRequestingLocation			is like RequestingLocation
			InBypassPurchasingClass			is Boolean

	Context Fields
		Buyer

	Transient Fields
		ErrorMessage						is Alpha size 100

	Relations
		BuyerLocationsForBuyerRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer			= Buyer

		BuyerLocationsForBuyerAndCompanyRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer			= Buyer
				related.Company			= InPurchasingCompany
		ValidShipToBuyerLocationsForBuyerAndCompanyRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer			= Buyer
				related.Company			= InPurchasingCompany
			Instance Selection
				where (related.Location entered)

		MatchingShipToBuyerLocationsForBuyerAndCompanyRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer			= Buyer
				related.Company			= InPurchasingCompany
			Instance Selection
				where (related.Location = InShipToLocation
				or    (related.Location = blank
				and    related.RequestingLocation = blank))

		ValidRequestingBuyerLocationsForBuyerAndCompanyRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer			= Buyer
				related.Company			= InPurchasingCompany
			Instance Selection
				where (related.RequestingLocation entered)

		MatchingRequestingBuyerLocationsForBuyerAndCompanyRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer			= Buyer
				related.Company			= InPurchasingCompany
			Instance Selection
				where (related.RequestingLocation = InRequestingLocation
				or    (related.RequestingLocation = blank
				and    related.Location = blank))

		BuyerVendorsForBuyerRel
			one-to-many relation to BuyerVendor
			Field Mapping uses symbolic key
				related.HROrganization						= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer								= Buyer

		ValidVendorsForBuyerRel
			one-to-many relation to BuyerVendor
			Field Mapping uses symbolic key
				related.HROrganization							= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer									= Buyer
				related.VendorGroup								= InPurchasingCompany.ProcurementGroup
			Instance Selection
				where (related.BuyerVendor.Vendor				= InVendor)

		ValidBuyerVendorsForBuyerRel
			one-to-many relation to BuyerVendor
			Field Mapping uses symbolic key
				related.HROrganization							= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer									= Buyer
				related.VendorGroup								= InPurchasingCompany.ProcurementGroup
			Instance Selection
				where (related.BuyerVendor.Vendor				= InVendor
				and   (related.BuyerVendor.PurchaseFromLocation = blank
				or     related.BuyerVendor.PurchaseFromLocation = InPurchaseFromLocation))

		BuyerPurchasingClassesForBuyerRel
			one-to-many relation to BuyerPurchasingClass
			Field Mapping uses symbolic key
				related.HROrganization							= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer									= Buyer
			

		ValidBuyerPurchasingClassesForBuyerRel
			one-to-many relation to BuyerPurchasingClass
			Field Mapping uses symbolic key
				related.HROrganization							= InPurchasingCompany.FinanceEnterpriseGroup.HROrganization
				related.Buyer									= Buyer
				related.ItemGroup								= InPurchasingCompany.ProcurementGroup
			Instance Selection
				where (related.BuyerPurchasingClass.MajorPurchasingClass = InMajorPurchasingClass
				and   (related.BuyerPurchasingClass.MinorPurchasingClass = blank
				or     related.BuyerPurchasingClass.MinorPurchasingClass = InMinorPurchasingClass))

    Derived Fields			
		MsgNoPurchaseClass 				is a MessageField 
			"CannotPurchaseItemsWithoutASystemAssignedPurchaseClass"
		MsgPurchaseClassInvalid 		is a MessageField 
			"PurchasingMajorClass<InMajorPurchasingClass>MinorClass<InMinorPurchasingClass>NotValidForBuyer<Buyer>"
		MsgLocationRequired 			is a MessageField 
			"LocationOrRequestingLocationRequired;BuyerLimitsExistAtThatLevel"
		MsgShipToLocationInvalid 		is a MessageField 
			"Company<InPurchasingCompany>Location<InShipToLocation>NotValidForBuyer<Buyer>"
		MsgRequestingLocationInvalid	is a MessageField 
			"Company<InPurchasingCompany>RequestingLocation<InRequestingLocation>NotValidForBuyer<Buyer>"
		MsgVendorInvalid 				is a MessageField 
			"Vendor<InVendor>NotValidForBuyer<Buyer>"
		MsgPurchaseFromLocationInvalid	is a MessageField 
			"Vendor<InVendor>PurchasingFromLocation<InPurchaseFromLocation>NotValidForBuyer<Buyer>"

			
		IsValidBuyer is a DerivedField
			type is Boolean			
			if (not InBypassPurchasingClass)
				if (InItemType entered
				and BuyerPurchasingClassesForBuyerRel exist)
					if (Buyer.RestrictPurchasingClasses)
						if ((InItemType.Special or InItemType.Service)
						or  InMajorPurchasingClass not entered) 
							ErrorMessage = MsgNoPurchaseClass
							return false
						
					if (InMajorPurchasingClass entered)
						if (ValidBuyerPurchasingClassesForBuyerRel not exist)
							ErrorMessage = MsgPurchaseClassInvalid
							return false							
		
			if (InPurchasingCompany entered
			and BuyerLocationsForBuyerRel exist)
				if (BuyerLocationsForBuyerAndCompanyRel not exist)
					if (InShipToLocation 		not entered 
					and InRequestingLocation 	not entered)
						ErrorMessage = MsgLocationRequired
						return false

				if (BuyerLocationsForBuyerAndCompanyRel exist)
					if (InShipToLocation entered
					and ValidShipToBuyerLocationsForBuyerAndCompanyRel exist)
						if (MatchingShipToBuyerLocationsForBuyerAndCompanyRel not exist)
							ErrorMessage = MsgShipToLocationInvalid
							return false

					if (InRequestingLocation entered
					and ValidRequestingBuyerLocationsForBuyerAndCompanyRel exist)
						if (MatchingRequestingBuyerLocationsForBuyerAndCompanyRel not exist)
							ErrorMessage = MsgRequestingLocationInvalid
							return false

			if (InVendor entered
			and BuyerVendorsForBuyerRel exist)
				if (ValidVendorsForBuyerRel not exist)
					ErrorMessage = MsgVendorInvalid
					return false
				if (ValidBuyerVendorsForBuyerRel not exist)
					ErrorMessage = MsgPurchaseFromLocationInvalid
					return false
					
			return true

			
