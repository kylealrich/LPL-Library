PersonName is a Field
	sql prefix is PNNM

	Representation
		Group Fields
			FormOfAddress			is Alpha up to 50  
				translatable 
			GivenName				is Alpha up to 50  
				translatable 
			PreferredGivenName		is Alpha up to 50  
				translatable 
			MiddleName				is Alpha up to 50
				translatable 
			FamilyNamePrefix		is Alpha up to 20  
				translatable 
			FamilyName				is Alpha up to 50  
				translatable 
			PreferredFamilyName     is Alpha up to 50  
				translatable 
			Affix					is Alpha up to 50  
				translatable 
			Qualification			is Alpha up to 50  
				translatable 
			MaidenName				is Alpha up to 50
				translatable 
			FormerName				is Alpha up to 50
				translatable 
			Alias					is Alpha up to 60  
				translatable 

	Patterns
		implements CompactFormat
	
	Derived Fields

		PresentationName is a DerivedField
		    type is Alpha size 153
			PresentationName = FullFamilyName
			if (GivenName entered
			or  MiddleName entered)
				PresentationName += ", "
			PresentationName += GivenName
			PresentationName += " "
			PresentationName += MiddleName			
        	
		PreferredPresentationName is a DerivedField
		    type is Alpha size 153
			PreferredPresentationName = PreferredFamilyName
			if (PreferredFamilyName entered)
				PreferredPresentationName += ", "
			PreferredPresentationName += PreferredGivenName
			PreferredPresentationName += " "
			PreferredPresentationName += MiddleName  
			      	
		FullPresentationName is a DerivedField
		    type is Alpha size 329
			FullPresentationName = FullFamilyNamePlus
			if (GivenName entered)
				FullPresentationName += ", "
			FullPresentationName += FormOfAddress
			FullPresentationName += " "
			FullPresentationName += GivenName
			FullPresentationName += " "
			FullPresentationName += MiddleName
                    	
		PreferredFullPresentationName is a DerivedField
		    type is Alpha size 329
			PreferredFullPresentationName = PreferredFamilyName
			if (PreferredGivenName entered)
				PreferredFullPresentationName += ", "
			PreferredFullPresentationName += FormOfAddress
			PreferredFullPresentationName += " "
			PreferredFullPresentationName += PreferredGivenName
			PreferredFullPresentationName += " "
			PreferredFullPresentationName += MiddleName
			                           	
		FullNormalPresentationName is a StringField
		    type is Alpha size 328
			FormOfAddress
			" "
			GivenName
			MiddleNamePlus
			" "
			FullFamilyNamePlus

		PreferredFullNormalPresentationName is a StringField
		    type is Alpha size 328
			FormOfAddress
			" "
			PreferredGivenName
			MiddleNamePlus
			" "
			PreferredFamilyName
			
		SimplePresentationName is a DerivedField
		    type is Alpha size 230
			SimplePresentationName = FullFamilyNamePlus
			if (GivenName entered)
				SimplePresentationName += ", "
			SimplePresentationName += GivenName
			SimplePresentationName += MiddleInitialPlus

		PreferredSimplePresentationName is a DerivedField
		    type is Alpha size 230
			PreferredSimplePresentationName += PreferredFamilyName
			if (PreferredGivenName entered)
				PreferredSimplePresentationName += ", "
			PreferredSimplePresentationName += PreferredGivenName
			PreferredSimplePresentationName += MiddleInitialPlus

		SimpleCorrespondenceName is a StringField
			type is Alpha size 229
			GivenName
			MiddleInitialPlus
			" "
			FullFamilyNamePlus

		PreferredSimpleCorrespondenceName is a StringField
			type is Alpha size 229
			PreferredGivenName
			MiddleInitialPlus
			" "
			PreferredFamilyName

		MiddleInitialPlusDot is a StringField
		    type is Alpha size 3
		    " "
			MiddleInitial
			"."

		MiddleInitialPlus is a DerivedField
		    type is Alpha size 3
			if (MiddleInitial entered)
	    		return MiddleInitialPlusDot

		FirstAndLastName is a StringField
		    type is Alpha size 101
			GivenName
			" "
			FullFamilyName  
			
		PreferredFirstAndLastName is a StringField
		    type is Alpha size 101
			PreferredGivenName
			" "
			PreferredFamilyName  			
			  	


		Initials is a StringField
			type is AlphaUpper size 3
			GivenNameInitial
			MiddleInitial
			FamilyNameInitial

		FamilyNameInitial is a StringField
			type is AlphaUpper size 1
			FullFamilyNamePlus

		MiddleNamePlus is a DerivedField
			type is Alpha size 51
			if (MiddleName entered)
				return " " + MiddleName
			
		MiddleInitial is a StringField
			type is AlphaUpper size 1
			MiddleName

		GivenNameInitial is a StringField
			type is AlphaUpper size 1
			GivenName

		FamilyNameWithPrefix is a StringField
		    type is Alpha size 71
			FamilyNamePrefix
			" "
			FamilyName

		FullFamilyName is a ConditionalField
		    type is Alpha size 71
			if (FamilyNamePrefix entered)
				FamilyNameWithPrefix
			else
				FamilyName

		FullFamilyNameWithAffix is a DerivedField
		    type is Alpha size 123
			FullFamilyNameWithAffix = FullFamilyName
			if (Affix entered)
				FullFamilyNameWithAffix += ", "
				FullFamilyNameWithAffix += Affix

		FullFamilyNameWithQualification is a DerivedField
		    type is Alpha size 123
			FullFamilyNameWithQualification = FullFamilyName
			if (Qualification entered)
				FullFamilyNameWithQualification += ", "
				FullFamilyNameWithQualification += Qualification

		FullFamilyNameWithAffixAndQual is a DerivedField
		    type is Alpha size 175
			FullFamilyNameWithAffixAndQual = FullFamilyNameWithAffix
			if (Qualification entered)
				FullFamilyNameWithAffixAndQual += ", "
				FullFamilyNameWithAffixAndQual += Qualification

		FullFamilyNamePlus is a ConditionalField
		    type is Alpha size 175
			if (Affix entered
			and Qualification entered)
				FullFamilyNameWithAffixAndQual
			else
			if (Affix entered)
				FullFamilyNameWithAffix
			else
			if (Qualification entered)
				FullFamilyNameWithQualification
			else
				FullFamilyName

		FormOfAddressHelp is a MessageField
			"Mr.,Mrs.,Ms.,General,Reverend,etc." 

		GivenNameHelp is a MessageField
			"FirstName"
			
		PreferredGivenNameHelp is a MessageField
			"Nickname"

		FamilyNamePrefixHelp is a MessageField
			"Von,di,Van,da,de,del,etc."
		
		FamilyNameHelp is a MessageField
			"LastName"
			
		AffixHelp is a MessageField
			"Sr.,Jr.,etc."
			
		QualificationHelp is a MessageField
			"MD,CPA,PhD,etc."

		AliasHelp is a MessageField
			"StageName,etc."

		UiDisplayName is a DerivedField
		    type is Alpha size 230
			if (FormOfAddress entered)
				UiDisplayName += FormOfAddress
				UiDisplayName += " "
			if (PreferredGivenName entered)
				UiDisplayName += PreferredGivenName
			else
				UiDisplayName += GivenName
			UiDisplayName += " "
			if (PreferredFamilyName entered)
				UiDisplayName += PreferredFamilyName
			else
				UiDisplayName += FamilyName
			
	Field Rules

		GivenName
			if (GivenName changed)
				if (old GivenName = old PreferredGivenName
				and PreferredGivenName not changed)
					PreferredGivenName = GivenName
			required
			
		PreferredGivenName
			default to GivenName

		FamilyName
			if (FamilyName changed)
				if (old FamilyName = old PreferredFamilyName
				and PreferredFamilyName not changed)
					PreferredFamilyName = FullFamilyName		
			required
			
		PreferredFamilyName
			default to FullFamilyName
