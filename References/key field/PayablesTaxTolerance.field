PayablesTaxTolerance is a Field


    Representation
        Group Fields
            InvoiceTaxAmount        is an InternationalAmount	
            CalculatedTaxAmount     is an InternationalAmount	
            OverchargeLimitAmount   is an InternationalAmount	
            UnderchargeLimitAmount  is an InternationalAmount	
            OverchargeLimitPercent  is Percent size 5.2			
            UnderchargeLimitPercent is Percent size 5.2			
            OverchargeRatePercent   is Decimal size 6.3			
            UnderchargeRatePercent  is Decimal size 6.3			
			AllowZeroTaxOverride    is Boolean					
            TaxSystemTaxRate		is a TaxRateValue			
			VendorChargedTaxRate	is a TaxRateValue			


	Transient Fields			 
		InTolerance			is Boolean		
		ZeroTaxOverride		is Boolean		
		BypassEdits			is Boolean		
		
	
	
	Context Fields
	
	
	Local Fields
		LocalAmountVariance				is an InternationalAmount
		LocalVarianceAbsolute			is an InternationalAmount
		LocalPercentVariance			is Percent size 5.2
		LocalTaxRateOverPctVariance		is a TaxRateValue		
		LocalTaxRateUnderPctVariance	is a TaxRateValue		
		LocalUnderchargeAmountEdit		is Numeric size 1
			States
				None	value is 0
				In		value is 1
				Out		value is 2
		LocalOverchargeAmountEdit		is Numeric size 1
			States
				None	value is 0
				In		value is 1
				Out		value is 2
		LocalUnderchargePercentEdit		is Numeric size 1
			States
				None	value is 0
				In		value is 1
				Out		value is 2
		LocalOverchargePercentEdit		is Numeric size 1
			States
				None	value is 0
				In		value is 1
				Out		value is 2
				
	
	Field Rules
	
	
	Conditions
	
	
	Relations	


	Derived Fields
		VerifyTaxWithinTolerance is a DerivedField
			type is Alpha size 1
			
			
			initialize ZeroTaxOverride
			initialize InTolerance
			initialize LocalOverchargeAmountEdit
			initialize LocalUnderchargeAmountEdit
			initialize LocalOverchargePercentEdit
			initialize LocalUnderchargePercentEdit

			
		
			if (AllowZeroTaxOverride
			and CalculatedTaxAmount = 0
			and !BypassEdits)		
				ZeroTaxOverride = true
				return "Z"
			
			if (InvoiceTaxAmount = CalculatedTaxAmount)
				InTolerance = true
				return "Y"
			
			if ((InvoiceTaxAmount not entered
			or   CalculatedTaxAmount not entered)
			and  !BypassEdits)		
				InTolerance = true
				return "Y"
			
				
			LocalAmountVariance	= (InvoiceTaxAmount - CalculatedTaxAmount)
		
			if (LocalAmountVariance < 0)	
				LocalVarianceAbsolute	= LocalAmountVariance * -1
			else
				LocalVarianceAbsolute	= LocalAmountVariance
			
			LocalPercentVariance = (LocalVarianceAbsolute / CalculatedTaxAmount)
			
			LocalTaxRateOverPctVariance		= (TaxSystemTaxRate + (OverchargeRatePercent  / 100))	
			LocalTaxRateUnderPctVariance	= (TaxSystemTaxRate - (UnderchargeRatePercent / 100))	

			if (LocalAmountVariance > 0)			
				if (OverchargeLimitAmount entered)		
					LocalOverchargeAmountEdit 	= DoOverchargeAmountEdit
				if (OverchargeRatePercent entered)
					LocalOverchargePercentEdit 	= DoRateOverPercentEdit		
			
			if (LocalAmountVariance < 0)			
				if (UnderchargeLimitAmount entered)		
					LocalUnderchargeAmountEdit 	= DoUnderchargeAmountEdit
				if (UnderchargeRatePercent entered)
					LocalUnderchargePercentEdit = DoRateUnderPercentEdit	
			



			if (LocalOverchargeAmountEdit.In
			or  LocalUnderchargeAmountEdit.In
			or  LocalOverchargePercentEdit.In		
			or  LocalUnderchargePercentEdit.In)		
				InTolerance = true
				return "Y"
				
			if (LocalOverchargePercentEdit.Out
			or  LocalUnderchargePercentEdit.Out
			or	LocalOverchargeAmountEdit.Out
			or  LocalUnderchargeAmountEdit.Out)
				return "N"
			
			InTolerance = true
			return "Y"
			

		DoOverchargeAmountEdit is a DerivedField
			type is Alpha size 1
			restricted
			if (LocalVarianceAbsolute <= OverchargeLimitAmount)
				return 1
			else
				return 2
					

		DoUnderchargeAmountEdit is a DerivedField
			type is Alpha size 1
			restricted
			if (LocalVarianceAbsolute <= UnderchargeLimitAmount)
				return 1
			else
				return 2
		

		DoRateOverPercentEdit is a DerivedField			
			type is Alpha size 1
			restricted
			if (VendorChargedTaxRate <= LocalTaxRateOverPctVariance)
				return 1
			else
				return 2


		DoRateUnderPercentEdit is a DerivedField			
			type is Alpha size 1
			restricted
			if (VendorChargedTaxRate >= LocalTaxRateUnderPctVariance)
				return 1
			else
				return 2										
											
		
		DoOverchargePercentEdit is a DerivedField		
			type is Alpha size 1
			restricted
			return 0


		DoUnderchargePercentEdit is a DerivedField		
			type is Alpha size 1
			restricted
			return 0
					

				
				
				
				
				
				
				
				
				
				
				
				
				
				
				
