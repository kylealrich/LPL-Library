VerifyPayGroupAccess is a Field		

	Representation
		Group Fields
			VendorGroup
			PayGroup
			SystemCode		is a GeneralLedgerSystemCode
			PayablesUserClass
			PayablesUser
			AccessCode		is Alpha size 1
	            States
	            	Add			value is "A"
	            	Change		value is "C"
	            	Delete		value is "D"
	            

	Transient Fields
		OutputErrorNumber		is Numeric size 3
		OutputErrorMessage		is Alpha size 100
          

	Derived Fields	 
		PerformVerifyPayGroupAccess	is a DerivedField			
			type is Boolean
			initialize OutputErrorNumber
			initialize OutputErrorMessage
			if (VerifyPayGroupAccess.PayGroup not entered)
				OutputErrorMessage 	= "PayGroupDoesNotExist"							
				OutputErrorNumber	= 101
				return false
			if ((VerifyPayGroupAccess.SystemCode = "AP"
			or  VerifyPayGroupAccess.SystemCode = "AP")
			and  VerifyPayGroupAccess.PayGroup.PayablesUserClass entered)
				if (PayablesUserClassRel not exists)
					OutputErrorMessage 	= "PayGroupUserClassDoesNotExist"				
					OutputErrorNumber	= 101
					return false
				if (PayablesUserRel not exists)
					OutputErrorMessage 	= "UserDoesNotHaveAccessRights"					
					OutputErrorNumber	= 101
					return false
				if (!PayablesUserRel.InquireAccess
				and !PayablesUserRel.UpdateAccess)
					OutputErrorMessage 	= "UserDoesNotHaveAccessRights"					
					OutputErrorNumber	= 101
					return false
				if (VerifyPayGroupAccess.AccessCode.Add
				or  VerifyPayGroupAccess.AccessCode.Change
				or  VerifyPayGroupAccess.AccessCode.Delete)
					if (!PayablesUserRel.UpdateAccess)
						OutputErrorMessage 	= "UserNotAuthorizedToPerformThisFunction"	
						OutputErrorNumber	= 101
						return false
			
			return true
		

	Relations	 
		PayablesUserClassRel
			one-to-one relation to PayablesUserClass
			Field Mapping uses symbolic key
				related.VendorGroup			= VendorGroup
				related.PayablesUserClass	= VerifyPayGroupAccess.PayGroup.PayablesUserClass
				
		PayablesUserRel
			one-to-one relation to PayablesUser
			Field Mapping uses symbolic key
				related.VendorGroup			= VendorGroup
				related.PayablesUserClass	= VerifyPayGroupAccess.PayGroup.PayablesUserClass
				related.PayablesUser		= VerifyPayGroupAccess.PayablesUser




				       
