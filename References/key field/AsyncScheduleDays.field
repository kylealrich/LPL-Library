AsyncScheduleDays is a Field
	default label is "ScheduleDays"
	 
    Representation
		AsyncScheduleDaySelect
        	occurs 10 times

	Local Fields
    	LocalCounter	is Numeric 1
    	LoopCounter		is Numeric 1
    	
	Context Fields
		AsyncActionRequest    	
		AsyncScheduleTemplate
		
	Conditions
		IsRequest
			default label is untranslatable
			when (AsyncActionRequest in context and (parentcontext.name = "AsyncActionRequest" or not IsTemplate)) 
			
		IsTemplate
			default label is untranslatable
			when (AsyncScheduleTemplate in context and parentcontext.name = "AsyncScheduleTemplate")
						
		DisplayRangeSeparator
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleDay.Range))
			
		DisplayElement2
			default label is untranslatable
			when ((IsRequest and (AsyncActionRequest.ScheduleDay.Range or AsyncActionRequest.ScheduleDay.Multiple)) or (IsTemplate and (AsyncScheduleTemplate.ScheduleDay.Range or AsyncScheduleTemplate.ScheduleDay.Multiple)))
			
		DisplayOtherElements
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleDay.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleDay.Multiple))
			
		IsSpecificDay
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleDay > 0) or (IsTemplate and AsyncScheduleTemplate.ScheduleDay > 0))		
			
		IsDayRange
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleDay.Range))			
						
			
	Derived Fields
		SlotOneRangeMessage is a MessageField
			"Range"
						
		DaySlotOneLabel is a DerivedField
			type is MessageField
			default label is untranslatable
			if ((IsRequest and AsyncActionRequest.ScheduleDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleDay.Range))
				return SlotOneRangeMessage
			else
				return blank
			  	                		
	Field Rules

		AsyncScheduleDays
			edit contiguous
			no duplicates
			
			if ((IsRequest and AsyncActionRequest.ScheduleDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleDay.Range))
				constraint (AsyncScheduleDays.AsyncScheduleDaySelect[1] entered and AsyncScheduleDays.AsyncScheduleDaySelect[2] entered)
					"DayRangeValuesRequired" 
			
			LoopCounter = 1
			while LoopCounter < 10
			    LocalCounter = LoopCounter + 1 
				
				if (AsyncScheduleDays.AsyncScheduleDaySelect[LocalCounter] entered)
					if (IsSpecificDay
					or (IsDayRange
					and LocalCounter > 2))
						AsyncScheduleDays.AsyncScheduleDaySelect[LocalCounter] = blank
					else
						constraint (AsyncScheduleDays.AsyncScheduleDaySelect[LoopCounter] entered)
							"ValuesMustBeEnteredFromFirstToLastInAscendingOrder"
					
						constraint(AsyncScheduleDays.AsyncScheduleDaySelect[LocalCounter] > AsyncScheduleDays.AsyncScheduleDaySelect[LoopCounter])
							"ValuesMustBeEnteredInAscendingOrder"

				LoopCounter += 1
