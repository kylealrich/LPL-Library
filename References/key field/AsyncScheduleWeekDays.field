AsyncScheduleWeekDays is a Field
	default label is "ScheduleDaysOfWeek"
	 
    Representation
		AsyncScheduleWeekDaySelect
        	occurs 6 times

	Local Fields
    	LocalCounter	is Numeric 1
    	LoopCounter		is Numeric 1
    	
	Context Fields
		AsyncActionRequest  
		AsyncScheduleTemplate  	
		
	Conditions
		IsRequest
			default label is untranslatable
			when (AsyncActionRequest in context and (parentcontext.name = "AsyncActionRequest" or not IsTemplate)) 
			
		IsTemplate
			default label is untranslatable
			when (AsyncScheduleTemplate in context and parentcontext.name = "AsyncScheduleTemplate")
		
		DisplayRangeSeparator
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDay.Range))
			
		DisplayElement2
			default label is untranslatable
			when ((IsRequest and (AsyncActionRequest.ScheduleWeekDay.Range or AsyncActionRequest.ScheduleWeekDay.Multiple)) or (IsTemplate and (AsyncScheduleTemplate.ScheduleWeekDay.Range or AsyncScheduleTemplate.ScheduleWeekDay.Multiple)))
			
		DisplayOtherElements
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDay.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDay.Multiple))
			
		IsSpecificWeekDay
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDay > 0) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDay > 0))		
			
		IsWeekDayRange
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDay.Range))						
			
	Derived Fields
		SlotOneRangeMessage is a MessageField
			"Range"
						
		DayOfWeekSlotOneLabel is a DerivedField
			type is MessageField
			default label is untranslatable
			if ((IsRequest and AsyncActionRequest.ScheduleWeekDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDay.Range))
				return SlotOneRangeMessage
			else
				return blank
			  	                		
	Field Rules

		AsyncScheduleWeekDays
			edit contiguous
			no duplicates
			
			if ((IsRequest and AsyncActionRequest.ScheduleWeekDay.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDay.Range))
				constraint (AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[1] entered and AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[2] entered)
					"DayOfWeekRangeValuesRequired" 
			
			LoopCounter = 1
			while LoopCounter < 6
			    LocalCounter = LoopCounter + 1 
				
				if (AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[LocalCounter] entered)
					if (IsSpecificWeekDay
					or (IsWeekDayRange
					and LocalCounter > 2))
						AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[LocalCounter] = blank
					else
						constraint (AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[LoopCounter] entered)
							"ValuesMustBeEnteredFromFirstToLastInAscendingOrder"
					
						constraint(AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[LocalCounter] > AsyncScheduleWeekDays.AsyncScheduleWeekDaySelect[LoopCounter])
							"ValuesMustBeEnteredInAscendingOrder"

				LoopCounter += 1
