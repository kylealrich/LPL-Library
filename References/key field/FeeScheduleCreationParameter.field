FeeScheduleCreationParameter is a Field

    Representation
    	Group Fields
	        PaymentFrequency			is Numeric 2
				States
					Daily			value is 1 
					Weekly			value is 2 
					SemiMonthly		value is 3 
					Monthly			value is 4 
					Quarterly		value is 5 
					SemiAnnually	value is 6 
					Annually		value is 7 
					OneTime			value is 8
			NumberOfDays				is Numeric 3
	        DayOfWeek 					is Numeric 1
        		States
        			Sunday 	  		value is 1
        			Monday	  		value is 2
        			Tuesday   		value is 3
        			Wednesday 		value is 4
        			Thursday  		value is 5
        			Friday	  		value is 6
        			Saturday  		value is 7
        	DayOfMonthArray
	        PaymentBasis				is Numeric 2
		    	States
		    		Amount			value is 1
		    		BasisPoints		value is 2 
		    		Percentage		value is 3
	        PaymentAmount				is an InternationalAmount
    		PaymentBasisPoints			is Numeric 5
    		PaymentPercentage			is Percent 6.3
    		
    Local Fields
    	LocalPaymentDate				is Date
    
    Context Fields
    	DebtInstrumentIdentifierAmortizationSchedule
    	DebtInstrumentFeeSchedule
    			
    Derived Fields
    			
    	DerivedPercentage is a DerivedField
    		type is Percent 6.3
    		if (PaymentBasis.BasisPoints)
    			return (PaymentBasisPoints * .01)
    		else
    			return PaymentPercentage
    			
    	CalculatedPaymentAmount	is a DerivedField
    		type is like InternationalAmount 
    		if (PaymentBasis.Amount)
    			return PaymentAmount
    			    
    Relations
	
	Conditions
    	RequiresNumberOfDays
    		when (FeeScheduleCreationParameter.PaymentFrequency.Daily)
    		
    	RequiresDayOfWeek
    		when (FeeScheduleCreationParameter.PaymentFrequency.Weekly)
    		
    	RequiresDayOfMonth
    		when (FeeScheduleCreationParameter.PaymentFrequency.Monthly
	    	or	  FeeScheduleCreationParameter.PaymentFrequency.SemiMonthly)
	    						
    Field Rules
    	NumberOfDays
    		initial value is 1
    		
    		if (PaymentFrequency.Daily)
    			required
    		else
    			initialize
    			
        DayOfWeek
        	if (PaymentFrequency.Weekly)
        		required
        	else
        		initialize
        		
    	DayOfMonthArray
    		if (PaymentFrequency.Monthly
    		or  PaymentFrequency.SemiMonthly)
        		required

				if (PaymentFrequency.SemiMonthly)
					constraint (DayOfMonthArray.DayOfMonth[2] entered)
						"MustEnter2DaysWhenFrequencyIsSemiMonthly"
        	else
        		initialize
    			
		PaymentBasis
			initial value is 1 
			required
						
    	PaymentAmount
    		if (PaymentBasis.Amount)
    			required
    		else
    			initialize
		
		PaymentBasisPoints
			if (PaymentBasis.BasisPoints)
    			required
    		else
    			initialize
    			
    	PaymentPercentage
    		if (PaymentBasis.Percentage)
    			required
    		else
    			initialize
    		
