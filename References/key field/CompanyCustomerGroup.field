CompanyCustomerGroup is a Field

    Representation
        Group Fields
            ReceivableCompany
            Customer
			CompanyCustomer   
			InvoiceTypeCode			
			CustomerBillTo			  
            ReceivableProcessLevel
			ReceivableGeneralLedgerCode
				default label is "ReceivableGlobalLedgerCode"
       	    TaxExempt						is Boolean	
				default label is "PreventTaxCodeDefaulting"
			TaxCode
			ProjectServiceLocation
									      
    Context Fields
    	FinanceEnterpriseGroup
		FinanceDimension2
		FinanceDimension2Upload		
		BypassEdits
		ProjectFundingType
								
    Derived Fields
		DerivedReceivableCompany is a DerivedField
			type is like ReceivableCompany
			if (FinanceDimension2Upload.CompanyCustomerGroup.ReceivableCompany entered)
				return FinanceDimension2Upload.CompanyCustomerGroup.ReceivableCompany
			else
				return FinanceDimension2.CompanyCustomerGroup.ReceivableCompany
				
		DerivedCustomer is a DerivedField
			type is like Customer
			if (FinanceDimension2Upload.CompanyCustomerGroup.Customer entered)
				return FinanceDimension2Upload.CompanyCustomerGroup.Customer
			else
				return FinanceDimension2.CompanyCustomerGroup.Customer	
				
		DerivedInvoiceTypeCode is a DerivedField
			type is like InvoiceTypeCode
			if (FinanceDimension2Upload.CompanyCustomerGroup.InvoiceTypeCode entered)
				return FinanceDimension2Upload.CompanyCustomerGroup.InvoiceTypeCode
			else
				return FinanceDimension2.CompanyCustomerGroup.InvoiceTypeCode	
					
		DerivedCustomerBillTo is a DerivedField
			type is like CustomerBillTo
			if (FinanceDimension2Upload.CompanyCustomerGroup.CustomerBillTo entered)
				return FinanceDimension2Upload.CompanyCustomerGroup.CustomerBillTo
			else
				return FinanceDimension2.CompanyCustomerGroup.CustomerBillTo	
				
		DerivedReceivableProcessLevel is a DerivedField
			type is like ReceivableProcessLevel
			if (FinanceDimension2Upload.CompanyCustomerGroup.ReceivableProcessLevel entered)				
				return FinanceDimension2Upload.CompanyCustomerGroup.ReceivableProcessLevel
			else	
				return FinanceDimension2.CompanyCustomerGroup.ReceivableProcessLevel	
							
		DerivedReceivableGeneralLedgerCode	is a DerivedField   
    		type is like ReceivableGeneralLedgerCode
    		restricted
			if  (FinanceDimension2.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode not entered
			and  FinanceDimension2.CompanyCustomerGroup.ReceivableProcessLevel.ReceivableGeneralLedgerCode not entered
			and !FinanceDimension2.CompanyCustomerGroup.ReceivableCompany.DetailedInvoiceAccrualDistributions)
				if (FinanceDimension2Upload.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode entered)
					return FinanceDimension2Upload.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode
	    		else
	    			return FinanceDimension2Upload.CompanyCustomerGroup.ReceivableProcessLevel.ReceivableGeneralLedgerCode
    		else	
			if  (FinanceDimension2Upload.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode not entered
    		and  FinanceDimension2Upload.CompanyCustomerGroup.ReceivableProcessLevel.ReceivableGeneralLedgerCode not entered
			and !FinanceDimension2Upload.CompanyCustomerGroup.ReceivableCompany.DetailedInvoiceAccrualDistributions)
				if (FinanceDimension2.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode entered)
					return FinanceDimension2.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode
				else
					return FinanceDimension2.CompanyCustomerGroup.ReceivableProcessLevel.ReceivableGeneralLedgerCode
			else
			if  (FinanceDimension2Upload.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode not entered
    		and  FinanceDimension2Upload.CompanyCustomerGroup.ReceivableProcessLevel.ReceivableGeneralLedgerCode not entered
			and  FinanceDimension2.CompanyCustomerGroup.CompanyCustomer.ReceivableGeneralLedgerCode not entered
			and  FinanceDimension2.CompanyCustomerGroup.ReceivableProcessLevel.ReceivableGeneralLedgerCode not entered)
				if (!ReceivableCompany.DetailedInvoiceAccrualDistributions)
					if (CompanyCustomer.ReceivableGeneralLedgerCode entered)
						return CompanyCustomer.ReceivableGeneralLedgerCode
					else			
						return ReceivableProcessLevel.ReceivableGeneralLedgerCode
				
    Field Rules		
    
		ReceivableCompany
			if (!BypassEdits)		
				if (IsProjectInvoicingExternal)
					default to DerivedReceivableCompany	
					required
						"ReceivableCompanyRequiredForProjectExternalInvoicing"
					constraint (ReceivableCompany.ProjectCreditMemoPrefix      entered
					and         ReceivableCompany.ProjectInvoicePrefix	       entered
					and         ReceivableCompany.ProjectCreditDebitMemoReason entered)				
						"CreditMemoPrefix,InvoicePrefixAndCreditReasonCodeRequiredForReceivableCompany<ReceivableCompany>"
					constraint (ReceivableCompany.Company.GeneralLedgerCompany.FinanceEnterpriseGroup = FinanceEnterpriseGroup)
						"ReceivableCompanyMustBelongToFinanceEnterpriseGroup<FinanceEnterpriseGroup>"
				else
				if (ProjectFundingType.IntercompanyBilling)
					default to DerivedReceivableCompany
					required
						"ReceivableCompanyRequiredForIntercompanyBilling"
					constraint (ReceivableCompany.AnyIntercompanyBillingFieldEntered)
						"ReceivableCompanyMustBeSetUpForIntercompanyBilling"
				else
					cannot be entered
						"ReceivableCompanyOnlyValidForProjectInvoicingOrIntercompanyBilling"

		Customer
			if (!BypassEdits)		
				if (IsProjectInvoicingExternal)
					default to DerivedCustomer			
					required
						"CustomerRequiredForProjectExternalInvoicing"
					constraint (CompanyCustomer exists)
						"CustomerInvalidForCompany"				
				else
					cannot be entered
						"CustomerOnlyValidForProjectExternalInvoicing"	
								
 		InvoiceTypeCode
			if (!BypassEdits) 		
				if (IsProjectInvoicingExternal)
					default to DerivedInvoiceTypeCode		
				else
					cannot be entered
						"InvoiceTypeOnlyValidForProjectExternalInvoicing"				
 		
		CustomerBillTo
			if (!BypassEdits)		
				if (IsProjectInvoicingExternal)
					default to DerivedCustomerBillTo

		ReceivableProcessLevel
			if (!BypassEdits)		
				if (IsProjectInvoicingExternal)
					default to DerivedReceivableProcessLevel				
					required
						"ReceivableProcessLevelRequiredForProjectExternalInvoicing"				
				else
					cannot be entered
						"ReceivableProcessLevelOnlyValidForProjectExternalInvoicing"				
				
		ReceivableGeneralLedgerCode
			if (!BypassEdits)		
				if (IsProjectInvoicingExternal)
					if (!ReceivableCompany.DetailedInvoiceAccrualDistributions)
						default to DerivedReceivableGeneralLedgerCode
						required
							"ReceivableGlobalLedgerCodeRequiredForProjectExternalInvoicing"
						constraint (ReceivableGeneralLedgerCode.DistributionAccount.Ledger = FinanceEnterpriseGroup.CoreLedger)
							"ReceivableGlobalLedgerCodeLedgerMustBe<FinanceEnterpriseGroup.CoreLedger>"
				else
					cannot be entered
						"ReceivableGlobalLedgerCodeOnlyValidForProjectExternalInvoicing"	
								
	Conditions				
		IsProjectInvoicingExternal
			restricted
			when (ProjectFundingType.ExternalGrant
			or    ProjectFundingType.ExternalCustomer)

		IsProjectInvoicingNotExternal
			restricted
			when (FinanceEnterpriseGroup.ProjectInvoicing
			and   FinanceDimension2.DimensionType.Posting
			and  !FinanceDimension2.FundingType.InternalFund)
			
