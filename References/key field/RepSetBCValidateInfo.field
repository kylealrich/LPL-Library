RepSetBCValidateInfo is a Field
	default label is "ValidationInfo"

	Representation
    	Group Fields
    		ValidationDateField	is a RepSetBCFieldChoice
    		ValidateAllData		is Boolean
    		OffsetOptions 		is a RepSetBCValidateOffset
    		DisableCondition	is Boolean
    		ConditionOverride 	is LPLText	
				is condition for RepSetBC.BusinessClass
    
    Context Fields
    	RepSetBC
    	
    Local Fields
    	LocalErrorField is Text
    	
    Field Rules
    	ValidationDateField
    		if (ValidateAllData)
    			initialize ValidationDateField
    		else
    			required
    				"MustSelectAValidationDateFieldOrSpecifyToValidateAllData"
    		
				constraint (ValidationDateField.DerivedRepresentationType = BusinessFieldRepresentationType.Date) 
					"FieldMustBeADateTypeField"
					
				constraint (ValidationDateField.IsResident) 
					"FieldMustBeResidentOnTheBusinessClass.RelatedFieldsAreNotValid"
					
				constraint (not ValidationDateField.IsUserField)
					"FieldCannotBeAUserField"	
		
		ValidateAllData
			if (ValidateAllData
			and  ValidateAllData changed)
				confirmation required
					"AreYouSureYouWantToValidateAllData?"
    	
    	OffsetOptions
    		if (not ValidateAllData)
    			required
    		
    	DisableCondition
    		if (DisableCondition changed or DisableCondition not changed) 
    			if (not DisableCondition)
    				constraint (not RepSetBC.Condition entered or RepSetBC.ValidCondition)
    					"ConditionReferencesFieldsThatAreNotDeliveredPersistentFieldsOrConstants.YouMustDisableConditonsAndCreateANewValidConditionToUseForValidation.FirstError:<LocalErrorField>."
    				
    				constraint (not RepSetBC.AdditionalCondition entered or RepSetBC.ValidAdditionalCondition)
    					"AddtionalConditionReferencesFieldsThatAreNotDeliveredPersistentFieldsOrConstants.YouMustDisableConditonsAndCreateANewValidConditionToUseForValidation.FirstError:<LocalErrorField>."
    		
    		if (DisableCondition and not ConditionOverride entered and (RepSetBC.Condition entered or RepSetBC.AdditionalCondition entered))
    			confirmation required
	    				"ReplicationSetBusinessClassHasConditions.DoYouWantToProceedWithoutSpecifyingACondition?"
    		
    	ConditionOverride
    		if (DisableCondition)
    	    	if (ConditionOverride entered)
	    			constraint (ValidCondition)
	    				"ConditionCanOnlyReferenceDeliveredPersistentFieldsAndConstants.FirstError:<LocalErrorField>."
	    			

	Derived Fields
		ValidCondition is a NativeField
			type is Boolean
		
		DisplayBlankMessage is a DerivedField
			type is Boolean
			default label is untranslatable
			if ((ValidationDateField.IsBaseField or ValidationDateField.IsClientField) and not ValidationDateField.IsAlwaysRequired)
				return true
			
			return false
