UnsignedUnitCost is a Field

    Representation
        type is Unsigned Decimal size 21.8

	Context Fields
		ContractGroup
		InventoryCompany
		PurchasingCompany
		SourcingCompany
		BillingCompany
		Item
		ItemType
		BillingLineType

	Local Fields
		RoundedValue
		LocalItemGroup is an ItemGroup
		LocalItem is an Item

	Derived Fields
        DerivedNumberOfDecimalsCost is a DerivedField
            type is Numeric size 1
            if (ItemType.Service or ItemType.Special
            or	BillingLineType.Special)
                if (PurchasingCompany entered
                and PurchasingCompany.InventoryCompanyRel exists)
                    return PurchasingCompany.InventoryCompanyRel.NumberOfDecimalsCost
                else
                if (SourcingCompany entered
                and SourcingCompany.InventoryCompanyRel exists)
                    return SourcingCompany.InventoryCompanyRel.NumberOfDecimalsCost
                else                
                if (BillingCompany entered
                and BillingCompany.InventoryCompanyRel exists)
                	return BillingCompany.InventoryCompanyRel.NumberOfDecimalsCost
                else
                if (InventoryCompany entered)
                    return InventoryCompany.NumberOfDecimalsCost
                else
                	return 8
            else
                if (LocalItemGroup not entered)
                    if (PurchasingCompany entered
                    and PurchasingCompany.InventoryCompanyRel exists)
                        LocalItemGroup = PurchasingCompany.InventoryCompanyRel.ItemGroup
                    else                    
                    if (BillingCompany entered
                    and BillingCompany.InventoryCompanyRel exists)
                    	LocalItemGroup = BillingCompany.InventoryCompanyRel.ItemGroup
                    else
                    if (ContractGroup entered)
                        LocalItemGroup = ContractGroup
                    else
                    if (InventoryCompany entered)
                        LocalItemGroup = InventoryCompany.ItemGroup

                LocalItem = Item
                if (LocalItem exists)
	                if (BillingCompany entered)
	                	return LocalItem.NumberOfDecimalsPrice
	                else
	                	return LocalItem.NumberOfDecimalsCost
	            else
	            	return 8

		DerivedRoundTo			is a DerivedField
			type is Unsigned Decimal 21.8
			if (DerivedNumberOfDecimalsCost = 8)
				return .00000001
			else
			if (DerivedNumberOfDecimalsCost = 7)
				return .0000001
			else
			if (DerivedNumberOfDecimalsCost = 6)
				return .000001
			else
			if (DerivedNumberOfDecimalsCost = 5)
				return .00001
			else
			if (DerivedNumberOfDecimalsCost = 4)
				return .0001
			else
			if (DerivedNumberOfDecimalsCost = 3)
				return .001
			else
			if (DerivedNumberOfDecimalsCost = 2)
				return .01
			else
			if (DerivedNumberOfDecimalsCost = 1)
				return .1
			else
			if (DerivedNumberOfDecimalsCost = 0)
				return 1
			else
				return 0


	Field Rules
		UnsignedUnitCost
			initialize RoundedValue
			RoundedValue.RoundInput			= UnsignedUnitCost
			RoundedValue.RoundingType		= RoundedValue.RoundingType.Normal
			RoundedValue.RoundTo			= DerivedRoundTo
			RoundedValue.RoundingMethodFW	= RoundedValue.RoundingMethodFW.MultipleOf
			UnsignedUnitCost 				= RoundedValue.RoundResult
			constraint (UnsignedUnitCost decimals <= DerivedNumberOfDecimalsCost)
				"TooManyDecimalDigitsEnteredForUnitCost:MaxIs<DerivedNumberOfDecimalsCost>"
