ForecastCreationParameter is a Field

    Representation
    	Group Fields
	        ForecastCalculation
	        
	        ActualDateRange		 		is a DateRange
	        HistoricalCalculationOption
			CalculationAdjustment		is Percent 5.2
			
			ScheduledCalculationOption
			DebtInstrument
			ForecastDebtComponent
			
			EnteredCalculationOption
	        DayOfWeek 					is Numeric 1
        		States
        			Sunday 	  				value is 1
        			Monday	  				value is 2
        			Tuesday   				value is 3
        			Wednesday 				value is 4
        			Thursday  				value is 5
        			Friday	  				value is 6
        			Saturday  				value is 7
        	DayOfMonth					is Numeric 2 	
	        CalculationMethod			is Numeric 1
	        	States
	        		Whole					value is 1
	        		Spread					value is 2
	        ForecastDateArray
	        	default label is "ForecastDate"
	        ForecastDateRange			is a DateRange
	        ForecastAmount				is an InternationalAmount

			LastRollingDate				    is Date
			LastRollingActualDate			is Date
			RollingFrequency				is Numeric 2
				States
					Weekly						value is 2
					Monthly						value is 3
					Annually					value is 4
			RollingFrequencyDuration		is Numeric 3
				default label is "RollingDuration"			
    
    Local Fields
    	ForecastDateArrayCounter		is Numeric 1
    	LocalForecastDate				is Date
    	LocalForecastCalendar			is a SystemCalendar
    	
    Derived Fields
    	NumberOfDatesEntered is a DerivedField
    		type is Numeric 1
    		if (ForecastDateArray.ForecastDate[4] entered)
    			return 4
    		else
    		if (ForecastDateArray.ForecastDate[3] entered)
    			return 3
    		else
    		if (ForecastDateArray.ForecastDate[2] entered)
    			return 2
    		else
    		if (ForecastDateArray.ForecastDate[1] entered)
    			return 1
    
    	ActualDateRangeBeginMonth is a DerivedField
			type is Alpha 2
			return ActualDateRange.Begin month
		
		ActualDateRangeEndMonth is a DerivedField
			type is Alpha 2
			return ActualDateRange.End month
				
		ActualBeginDate is a DerivedField
			type is Alpha up to 15
			return ActualDateRangeBeginMonth + "/" + ActualDateRange.Begin day + "/" + ActualDateRange.Begin year
		
		ActualEndDate is a DerivedField
			type is Alpha up to 15
			return ActualDateRangeEndMonth + "/" + ActualDateRange.End day + "/" + ActualDateRange.End year
				
		DerivedActualDateRange is a DerivedField
			type is Alpha up to 30
			default label is "ActualDateRange"
			return ActualBeginDate + " - " + ActualEndDate
			
    	ForecastDateRangeBeginMonth is a DerivedField
			type is Alpha 2
			return ForecastDateRange.Begin month
		
		ForecastDateRangeEndMonth is a DerivedField
			type is Alpha 2
			return ForecastDateRange.End month
				
		ForecastBeginDate is a DerivedField
			type is Alpha up to 15
			return ForecastDateRangeBeginMonth + "/" + ForecastDateRange.Begin day + "/" + ForecastDateRange.Begin year
		
		ForecastEndDate is a DerivedField
			type is Alpha up to 15
			return ForecastDateRangeEndMonth + "/" + ForecastDateRange.End day + "/" + ForecastDateRange.End year
				
		DerivedForecastDateRange is a DerivedField
			type is Alpha up to 30
			default label is "ForecastDateRange"
			return ForecastBeginDate + " - " + ForecastEndDate

		RollingFrequencyDurationLabel is a DerivedField
			type is Alpha up to 10
			restricted
			if (RollingFrequency.Weekly)
				return WeeksMessage
			else
			if (RollingFrequency.Monthly)
				return MonthsMessage
			else
			if (RollingFrequency.Annually)
				return YearsMessage
			else
				return blank

		DaysMessage is a MessageField
			"Days"

		WeeksMessage is a MessageField
			"Weeks"

		MonthsMessage is a MessageField
			"Months"
		
		YearsMessage is a MessageField
			"Years"
						    
    Context Fields
    	CashManagementGroup
    	CashForecast
    
    Conditions
    	ShowDateRange
    		when (EnteredCalculationOption entered
			and	  !EnteredCalculationOption.Date)
					
    Field Rules
    	ForecastCalculation
    		initial value is 1 
			default to 1 
			required
		
		ActualDateRange
			if (ForecastCalculation.Historical)
    			required
	  				"ActualDateRangeIsRequired"
	  				
	  			constraint (ActualDateRange.Begin entered)
  					"ActualDateRangeBeginDateIsRequired"
  				
	  			constraint (ActualDateRange.End entered)
	  				"ActualDateRangeEndDateIsRequired"
    		else
    			initialize
    			
		HistoricalCalculationOption
			if (ForecastCalculation.Historical)
    			required
    		else
    			initialize
    	
    	ScheduledCalculationOption
    		initial value is 1 
    		
    	
    	DebtInstrument
    		if (ScheduledCalculationOption.Debt)
    			required
    		else
    			initialize
    			
    	ForecastDebtComponent
    		if (ScheduledCalculationOption.Debt)
    			required
    		else
    			initialize
    									
		CalculationAdjustment
			initial value is CashForecast.ForecastCreationParameter.CalculationAdjustment
			
			if (CalculationAdjustment changed)
				constraint (!CashForecast.HaveBuiltForecast)
					"CannotChange;ForecastHasAlreadyBeenBuiltUsingThisCriteria"
				
			if (!ForecastCalculation.Historical)
				initialize
				
		EnteredCalculationOption
    		if (ForecastCalculation.Calculated)
    			required
    		else
    			initialize
    		
    	DayOfWeek
    		if (ForecastCalculation.Calculated
    		and EnteredCalculationOption.DayOfWeek)
    			required
    			
    		if (!ForecastCalculation.Calculated)
				initialize
    			
    	DayOfMonth
    		if (ForecastCalculation.Calculated
    		and EnteredCalculationOption.DayOfMonth)
    			required
    			
    		constraint (DayOfMonth > 0 
    		and			DayOfMonth <= 31)
    			"DayOfMonthNumberMustBeLessThan31"
    		
    		if (!ForecastCalculation.Calculated)
				initialize
					
    	CalculationMethod
    		initial value is 1 
    			
    		if (ForecastCalculation.Calculated)
    			required
    			
    		if (!ForecastCalculation.Calculated)
				initialize 
    			
    	ForecastDateArray
    		if (ForecastCalculation.Calculated
    		and EnteredCalculationOption.Date)
    			required
    		
    		ForecastDateArrayCounter = 1
			while (ForecastDateArrayCounter <= 4)
				if (ForecastDateArray.ForecastDate[ForecastDateArrayCounter] entered)
					LocalForecastDate = ForecastDateArray.ForecastDate[ForecastDateArrayCounter]
					constraint (LocalForecastDate within CashForecast.ForecastDateRange)
	    				"ForecastDate:<LocalForecastDate>NotWithinForecastDateRangeOnForecast<CashForecast>"
	    			
	    			if (CashForecast.Status.Active)
	  					constraint (LocalForecastDate >= current date)
	  						"ForecastDatesMustBeTodayOrLaterForAnActiveForecast"
	  					
		    		for each CashForecast.CashForecastAccount set
		    			LocalForecastCalendar = each.ForecastCalendar	
		    			constraint (ForecastDateBankDayRel.IsBankDay)
		    				"ForecastDate:<LocalForecastDate>MustBeABankDayInCalendarForAccount<each.CashForecastAccount.CashManagementAccount>"
	    				
				ForecastDateArrayCounter += 1
												
			if (!ForecastCalculation.Calculated)
				initialize
    			
    	ForecastDateRange
			initial value is CashForecast.ForecastDateRange
			default to CashForecast.ForecastDateRange

    		if (ForecastCalculation.Calculated
    		and !EnteredCalculationOption.Date)
    			required
    				"ForecastDateRangeBeginDateIsRequired"
    				
    			constraint (ForecastDateRange.Begin entered)
  					"ForecastDateRangeBeginDateIsRequired"
  				
	  			constraint (ForecastDateRange.End entered)
	  				"ForecastDateRangeEndDateIsRequired"
	  			
	  			if (CashForecast.Status.Active)
	  				constraint (ForecastDateRange.Begin >= current date)
	  					"ForecastDateRangeBeginDateMustBeTodayOrLaterForAnActiveForecast"
	  						
	    		constraint (ForecastDateRange.Begin within CashForecast.ForecastDateRange
	    		and			ForecastDateRange.End within CashForecast.ForecastDateRange)
	    			"ForecastDateRangeNotWithinForecastDateRangeOnForecast<CashForecast>"
    		
    		if (!ForecastCalculation.Calculated)
				initialize
					
    	ForecastAmount
    		if (ForecastCalculation.Calculated)
    			required
    		else
				initialize

		LastRollingDate
			default to CashForecast.ForecastDateRange.End

		LastRollingActualDate
			default to ActualDateRange.End
		
		RollingFrequency
			initial value is CashForecast.ForecastCreationParameter.RollingFrequency
			default to CashForecast.ForecastCreationParameter.RollingFrequency
			
			if (CashForecast.RollingForecast)
				required	
					"RollingFrequencyIsRequired"
			else
				initialize

		RollingFrequencyDuration
			initial value is 1
			default to 1

			if (RollingFrequency entered)
				required	
					"RollingDurationIsRequired"
			else
				initialize
    
    Relations
    	ForecastDateBankDayRel
    		one-to-one relation to SystemCalendarDate
    		Field Mapping uses symbolic key
    			related.EnterpriseGroup		= CashManagementGroup.EnterpriseGroup
				related.SystemCalendar		= LocalForecastCalendar
				related.SystemCalendarDate	= LocalForecastDate
