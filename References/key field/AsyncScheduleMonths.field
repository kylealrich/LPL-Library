AsyncScheduleMonths is a Field
	default label is "ScheduleMonths"
	 
    Representation
		AsyncScheduleMonthSelect
        	occurs 10 times

	Local Fields
    	LocalCounter	is Numeric 1
    	LoopCounter		is Numeric 1
    	
	Context Fields
		AsyncActionRequest 
		AsyncScheduleTemplate   	
		
	Conditions
		IsRequest
			default label is untranslatable
			when (AsyncActionRequest in context and (parentcontext.name = "AsyncActionRequest" or not IsTemplate)) 
			
		IsTemplate
			default label is untranslatable
			when (AsyncScheduleTemplate in context and parentcontext.name = "AsyncScheduleTemplate")
			
		DisplayRangeSeparator
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleMonth.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleMonth.Range))
			
		DisplayElement2
			default label is untranslatable
			when ((IsRequest and (AsyncActionRequest.ScheduleMonth.Range or AsyncActionRequest.ScheduleMonth.Multiple)) or (IsTemplate and (AsyncScheduleTemplate.ScheduleMonth.Range or AsyncScheduleTemplate.ScheduleMonth.Multiple)))
			
		DisplayOtherElements
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleMonth.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleMonth.Multiple))
			
		IsSpecificMonth
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleMonth > 0) or (IsTemplate and AsyncScheduleTemplate.ScheduleMonth > 0))		
			
		IsMonthRange
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleMonth.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleMonth.Range))			
			
	Derived Fields
			
		SlotOneRangeMessage is a MessageField
			"Range"
						
		MonthSlotOneLabel is a DerivedField
			type is MessageField
			default label is untranslatable
			if ((IsRequest and AsyncActionRequest.ScheduleMonth.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleMonth.Range))
				return SlotOneRangeMessage
			else
				return blank
			  	                		
	Field Rules

		AsyncScheduleMonths
			edit contiguous
			no duplicates
			
			if ((IsRequest and AsyncActionRequest.ScheduleMonth.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleMonth.Range))
				constraint (AsyncScheduleMonths.AsyncScheduleMonthSelect[1] entered and AsyncScheduleMonths.AsyncScheduleMonthSelect[2] entered)
					"MonthRangeValuesRequired" 
			
			LoopCounter = 1
			while LoopCounter < 10
			    LocalCounter = LoopCounter + 1 
				
				if (AsyncScheduleMonths.AsyncScheduleMonthSelect[LocalCounter] entered)
					if (IsSpecificMonth
					or (IsMonthRange
					and LocalCounter > 2))
						AsyncScheduleMonths.AsyncScheduleMonthSelect[LocalCounter] = blank
					else
						constraint (AsyncScheduleMonths.AsyncScheduleMonthSelect[LoopCounter] entered)
							"ValuesMustBeEnteredFromFirstToLastInAscendingOrder"
					
						constraint(AsyncScheduleMonths.AsyncScheduleMonthSelect[LocalCounter] > AsyncScheduleMonths.AsyncScheduleMonthSelect[LoopCounter])
							"ValuesMustBeEnteredInAscendingOrder"

				LoopCounter += 1
