SetActionStats is a Field
    Representation
        Group Fields
			SetRecords				is Numeric size 9
				default label is "SetRecordsProcessed"
			SetCommits				is Numeric size 9
				default label is "NumberOfCommits"
			SetCommitObjects		is Numeric size 12
				default label is "ObjectsCommitted"
			SetCommitHighWater		is Numeric size 9
				default label is "HighWater"
			SetFailedObjects		is Numeric size 9
				default label is "FailedPendingCommit"
			CheckPointDuration  	is Numeric size 12
				default label is "CheckpointDuration"
			CheckPointHighWater		is Numeric size 12
				default label is "CheckpointHighWater"
			EstimatedSetSize 		is Numeric size 9 
		
	Context Fields
		AsyncActionTrigger			
			
	Conditions
		EstimatedSetSizeComputed
			when (EstimatedSetSize > 0 or EstimatedSetSize < 0 or (AsyncActionTrigger in context and (AsyncActionTrigger.Status.Finished or AsyncActionTrigger.PendingStatusInfo.PendingStatus.Complete))) 

		EstimatedSizeDiscrepancy
			when (EstimatedSetSize > 0 and EstimatedSetSize != SetRecords and AsyncActionTrigger in context and (AsyncActionTrigger.Status.Finished or AsyncActionTrigger.PendingStatusInfo.PendingStatus.Complete))  			
		
	Derived Fields
		SetAverageObjectPerCommit is a DerivedField
			type is Decimal size 9.2
			default label is "AverageObjectsPerCommit"
			return (SetCommitObjects / SetCommits)  				
			
		CheckPointDurationSeconds is a DerivedField
			type is Numeric size 12
			default label is "CheckpointDurationSeconds"
			return (CheckPointDuration / 1000)
					
		SetAverageCheckPointDurationMs is a DerivedField	 			
			type is Numeric size 12
			default label is "SetAverageCheckpointDurationMs"
			return (CheckPointDuration / SetCommits)
			
		SetAverageCheckPointDurationSeconds	is a DerivedField			
			type is Numeric size 12
			default label is "AverageCheckpointDurationSeconds"
			return (CheckPointDurationSeconds / SetCommits)			
			
		CheckPointHighwaterSeconds is a DerivedField
			type is Numeric size 12
			default label is "CheckpointHighwaterSeconds"
			return (CheckPointHighWater / 1000)		
			
		DerEstimatedSetSize is a DerivedField
			type is Numeric size 9
			default label is "EstimatedSetSize"
			
			if (AsyncActionTrigger in context
    		and (AsyncActionTrigger.Status.Finished
    		or   AsyncActionTrigger.PendingStatusInfo.PendingStatus.Complete))
    			return SetRecords
    			
			if (EstimatedSetSize < 0)
				return 0
				
			return EstimatedSetSize
			
		DerEstimatedSetSizeUnknown is a DerivedField
			type is Alpha size 10
			default label is "EstimatedSetSize"
			
			if (EstimatedSetSize = 0) 
				return "Unknown"
			return ""
			
		EstimatedSetComplete is a DerivedField
			type is Percent 5.2		
			default label is "EstimatedPercentComplete"	
			
			if (AsyncActionTrigger in context
    		and (AsyncActionTrigger.Status.Finished
			or   AsyncActionTrigger.PendingStatusInfo.PendingStatus.Complete))
    			return 1
    						
			if (EstimatedSetSize <= 0)
				return 0
			return (SetRecords / EstimatedSetSize)
			
		SetSizeMsg is a MessageField
			restricted
			"SetSize"	
			
		EstimatedSetSizeMsg is a MessageField
			restricted
			"EstimatedSetSize"						
			
		SetSizeMessage is a DerivedField
    		type is MessageField
    		
    		if (AsyncActionTrigger in context
    		and (AsyncActionTrigger.Status.Finished
    	    or   AsyncActionTrigger.PendingStatusInfo.PendingStatus.Complete))
    			return SetSizeMsg
			else    			
    			return EstimatedSetSizeMsg			
			
		PercentCompleteMsg is a MessageField
			restricted
			"PercentComplete"
			
		EstimatedPercentCompleteMsg is a MessageField
			restricted
			"EstimatedPercentComplete"						
			
		PercentCompleteMessage is a DerivedField
    		type is MessageField
    		
    		if (AsyncActionTrigger in context
    		and (AsyncActionTrigger.Status.Finished
    		or   AsyncActionTrigger.PendingStatusInfo.PendingStatus.Complete))
    			return PercentCompleteMsg
			else    			
    			return EstimatedPercentCompleteMsg
    			
		EstimatedSizeDiscrepancyMessage is a MessageField
			"InitialEstimateWas<EstimatedSetSize>"    			
    					
		
