IntrastatProcessing is a Field

    Representation
		Group Fields	
			InputErrorCategory				is Alpha 5
 			HeaderFc	is Alpha 1
				States
			        Create 			value is "C"			 
			        Update	   		value is "U"
			        Delete	   		value is "D"		
    		InFinanceEnterpriseGroup is like FinanceEnterpriseGroup	
				disable surrogates
			InvoiceCompany			is a GeneralLedgerCompany		
    			disable surrogates 
    		TaxEntity	
           	FiscalYear
           	ReportYear				is a Year
           	ReportPeriod			is a Period
			IntrastatNumber
			TransactionType			is Alpha 1
				States
			        Purchase   		value is "P"			 
			        Sales	   		value is "S"					
			SystemCode				is a GeneralLedgerSystemCode
			CustomerGrp				is like CustomerGroup
			Customer				is like Customer
			VendorGrp				is like VendorGroup
			Vendor					is like Vendor
			DocumentNumber
			NatureOfTransactionCode is a IntrastatNatureOfTransactionCode
			InvoiceDate				is Date
			CustomerVATRegCountry   is like Country
			CustomerVATRegNumber 	is a VATRegistrationNumber
			VendorVATRegCountry     is like Country
			VendorVATRegNumber 		is a VATRegistrationNumber
			CustomerCurrency		is a Currency 
			VendorCurrency			is a Currency 
			CurrencyRate
			OriginCountry 			is a Country
			OriginRegion			is a Region
			LoadingPort				is an IntrastatPortCode
			DestinationCountry 		is like Country
			DestinationRegion		is like Region
			UnloadingPort			is an IntrastatPortCode
			TransportMode	
			DeliveryTerms           is like ShipTerm
   			StatisticalProcedure    is a IntrastatStatisticalProcedure
   			DropShip 				is a DropShipInd
   			DeliveryDate            is Date
        	ShippedDate				is Date
        	PurchaseOrder
            GoodsOrServices  		is Alpha 1
				States
			        Goods   		value is "G"			 
			        Services   		value is "S"
            BranchID				is Alpha 3      				
			InhStatus				is Numeric 1	
			RelatedObjectReference	is BusinessObjectReference
			DetailFc				is Alpha 1
				States
			        Create 			value is "C"			 
			        Update	   		value is "U"
			        Delete	   		value is "D"					
			LineNumber 				is a DistributionSequence 
			BaseAmount 				is an InternationalAmount
			ICNCode
            StockWeight
       		ReceivedAmount        	is an InternationalAmount
            SupplementaryUOM      	is an IntrastatUnitOfMeasure
            SupplementaryQuantity	is a Quantity
            DoNotDeleteHdrIfNoDtl	is Boolean				
            NoMoreDetailsExist		is Boolean				
                            			
	Transient Fields			 
		ReturnedErrorNumber 		is Numeric 3
		OutputIntrastatNumber		is an IntrastatNumber
 		SoftMessageNumber  			is Numeric 3			
 		DoIntrastatReporting		is Boolean	
 		
	Local Fields
		LocalErrorCategory				is Alpha 5
		LocalErrorNumber 				is Numeric 3
		LocalTrigger					is Alpha 1
		LocalCountry					is like Country
		LocalRegion						is like Region
		LocalFiscalYear					is a Year
		LocalAccountingEntityYearPeriod is an AccountingEntityYearPeriod
		LocalDescType					is Alpha 1
			States
		        Header 			value is "H"			 
		        Detail	   		value is "D"
		        Blank			value is blank		

    Relations

		IntrastatHeaderRel				 
            one-to-one relation to IntrastatHeader
  			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
				related.TaxEntity							= TaxEntity 			
				related.IntrastatHeader.FiscalYear			= FiscalYear
				related.IntrastatHeader.IntrastatNumber 	= IntrastatNumber   
				
        IntrastatDtlRel
            one-to-many relation to IntrastatDetail
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
				related.TaxEntity							= TaxEntity 	            
            Instance Selection
                where (related.IntrastatHeader.FiscalYear 		= FiscalYear
                and   related.IntrastatHeader.IntrastatNumber 	= IntrastatNumber)

       	IntrastatDetailSingleRecRel
            one-to-one relation to IntrastatDetail
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
				related.TaxEntity							= TaxEntity 	            
                related.IntrastatHeader.FiscalYear 			= FiscalYear
                related.IntrastatHeader.IntrastatNumber 	= IntrastatNumber
                related.IntrastatDetail						= LineNumber
                		
		IntrastatNumberRel				 
            one-to-one relation to IntrastatSequenceNumber
  			Field Mapping uses symbolic key
  				related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
				related.TaxEntity							= TaxEntity 	
				related.FiscalYear							= FiscalYear   

		DestinationCountryRel
           	one-to-one relation to Country
  			Field Mapping uses symbolic key
                related.Country								= DestinationCountry
						
		RegionRel				 
            one-to-one relation to Region
  			Field Mapping uses symbolic key
                related.Country 							= LocalCountry
				related.Region								= LocalRegion   

	Derived Fields	
		MainTrigger	is a DerivedField	 
			type is Alpha 1	
			LocalTrigger = EditInput
			if (LocalErrorNumber entered
			or !TaxEntity.IntrastatUsed)
				return blank
							
			if (InFinanceEnterpriseGroup not entered)
				if (TaxEntity entered)
					InFinanceEnterpriseGroup = TaxEntity.FinanceEnterpriseGroup
				else	
					InFinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
						
			if (HeaderFc entered)
				LocalTrigger = DoIntrastatHeader
				if (LocalErrorNumber entered)
					return blank
	
			if (DetailFc entered)
				LocalTrigger = DoIntrastatDetail
				if (LocalErrorNumber entered)
					return blank
							
			return blank		

		GetNextIntrastatNumber	is a DerivedField
			type is like IntrastatNumber
			initialize ReturnedErrorNumber
			if (LocalErrorNumber entered)
				return blank	
			if (IntrastatNumberRel not exist)
				invoke IncrementLastIntrastatNumber TaxEntity
				invoke Create IntrastatSequenceNumber
					invoked.FinanceEnterpriseGroup	= InFinanceEnterpriseGroup
					invoked.TaxEntity				= TaxEntity 	
					invoked.FiscalYear				= FiscalYear	
					invoked.LastIntrastatNumber 	= TaxEntity.LastIntrastatNumber 	
			else	
				invoke IncrementLastIntrastatNumber TaxEntity
				invoke Update IntrastatNumberRel
					invoked.LastIntrastatNumber	    = TaxEntity.LastIntrastatNumber	
					       				       				
			OutputIntrastatNumber = TaxEntity.LastIntrastatNumber	
			return OutputIntrastatNumber 

		EditInput	is a DerivedField	 
			type is Alpha 1
			initialize LocalErrorNumber
			initialize LocalErrorCategory
 			if (HeaderFc entered)
	 			if (HeaderFc != "C"
	 			and HeaderFc != "U"
	 			and HeaderFc != "D")
	 				LocalErrorNumber = 004
	 				return blank 
 			if (DetailFc entered)
	 			if (DetailFc != "C"
	 			and DetailFc != "U"
	 			and DetailFc != "D")
	 				LocalErrorNumber = 005
	 				return blank 
	 				 	
			if (TaxEntity not entered)
				LocalErrorNumber	= 001
			if (FiscalYear not entered)
				FiscalYear = DerivedFiscalYear
				if (LocalErrorNumber entered)
					return blank
						
			if (!TaxEntity.IntrastatUsed)
				SoftMessageNumber 	= 001
				return blank
				
			LocalTrigger = CheckCountries
			if (LocalErrorNumber entered
			or !DoIntrastatReporting)
				return blank
								
			return blank 

		DerivedFiscalYear	is a DerivedField	 
			type is like FiscalYear
			if (FiscalYear not entered)
				if (InvoiceDate > DeliveryDate)
					LocalAccountingEntityYearPeriod.InputDate   = InvoiceDate
				else
				if (DeliveryDate entered)	
					LocalAccountingEntityYearPeriod.InputDate   = DeliveryDate
				else
					LocalErrorNumber	= 002	
					return blank 				

				LocalFiscalYear = LocalAccountingEntityYearPeriod.AccountingEntityYear
				
			if (LocalFiscalYear not entered)
				LocalErrorNumber	= 002	
				return blank 
				
			return LocalFiscalYear															

		DoIntrastatHeader	is a DerivedField	
			type is Alpha 1
			initialize LocalErrorNumber
			initialize SoftMessageNumber
			LocalErrorCategory = "ININH"
			LocalDescType	= LocalDescType.Header

			if (IntrastatNumber not entered)
				if (HeaderFc.Delete
				or  HeaderFc.Update)
					LocalErrorNumber = 101
					return blank
				
			if (HeaderFc.Create)
				if (IntrastatNumber entered)
					LocalErrorNumber = 100
					
				invoke Create IntrastatHeader
					invoked.IntrastatHeader.IntrastatNumber	= GetNextIntrastatNumber						
					invoked.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup
					invoked.TaxEntity						= TaxEntity
					invoked.IntrastatHeader.FiscalYear		= FiscalYear
			        invoked.BypassCheckCountries			= true
			        invoked.BypassNumberGeneration			= true
			        invoked.ReportYear                      = blank
			        invoked.ReportPeriod                    = blank
			        invoked.TransactionType                 = TransactionType
			        invoked.SystemCode                      = SystemCode
			        invoked.CustomerGroup					= CustomerGrp
			        invoked.Customer						= Customer
			        invoked.VendorGroup						= VendorGrp
			        invoked.Vendor							= Vendor
			        invoked.DocumentNumber					= DocumentNumber
			        invoked.NatureOfTransactionCode 		= NatureOfTransactionCode
			        invoked.TaxPointDate                    = InvoiceDate
			        invoked.CustomerVATCountry  			= CustomerVATRegCountry              
			        invoked.CustomerVATRegistrationNumber  	= CustomerVATRegNumber   
			        invoked.CustomerCurrencyCode            = CustomerCurrency 
			        invoked.VendorVATCountry    			= VendorVATRegCountry              
			        invoked.VendorVATRegistrationNumber     = VendorVATRegNumber
			        invoked.VendorCurrencyCode              = VendorCurrency 
			        invoked.CurrencyRate					= CurrencyRate
			        invoked.OriginCountry                   = OriginCountry 
			        invoked.OriginRegion                    = OriginRegion 
			        invoked.LoadingPort                     = LoadingPort 
			        invoked.DestinationCountry              = DestinationCountry 
			        invoked.DestinationRegion               = DestinationRegion 
			        invoked.UnloadingPort                   = UnloadingPort 
			        invoked.TransportMode					= TransportMode
			        invoked.DeliveryTerms                   = DeliveryTerms 
			        invoked.StatisticalProcedure			= StatisticalProcedure
			        invoked.DropShipIndicator               = DropShip 
			        invoked.DeliveryDate  					= DeliveryDate                    
			        invoked.PurchaseOrder					= PurchaseOrder
			        invoked.PurchasingCompany				= PurchaseOrder.Company
			        invoked.RelatedObjectReference			= RelatedObjectReference
					invoked.Status                          = 1		
		        SoftMessageNumber 	= 190
		    else    
			if (HeaderFc.Update)
				if (IntrastatHeaderRel not exist)
					LocalErrorNumber = 104
					return blank
				if (IntrastatHeaderRel.Status.Reported)
					LocalErrorNumber = 102
					return blank
				
				invoke Update IntrastatHeaderRel
					invoked.ReportYear                       = ReportYear
			        invoked.ReportPeriod                     = ReportPeriod
			        invoked.TransactionType                  = TransactionType
			        invoked.SystemCode                       = SystemCode
			        invoked.Customer						 = Customer
			        invoked.Vendor							 = Vendor
			        invoked.DocumentNumber					 = DocumentNumber
			        invoked.NatureOfTransactionCode 		 = NatureOfTransactionCode
			        invoked.TaxPointDate                     = InvoiceDate					
			        invoked.CustomerVATCountry  			 = CustomerVATRegCountry              
			        invoked.CustomerVATRegistrationNumber  	 = CustomerVATRegNumber   
			        invoked.CustomerCurrencyCode             = CustomerCurrency 
			        invoked.VendorVATCountry    			 = VendorVATRegCountry              
			        invoked.VendorVATRegistrationNumber      = VendorVATRegNumber
			        invoked.VendorCurrencyCode               = VendorCurrency 
			        invoked.CurrencyRate					 = CurrencyRate
			        invoked.OriginCountry                    = OriginCountry 
			        invoked.OriginRegion                     = OriginRegion 
			        invoked.LoadingPort                      = LoadingPort 
			        invoked.DestinationCountry               = DestinationCountry 
			        invoked.DestinationRegion                = DestinationRegion 
			        invoked.UnloadingPort                    = UnloadingPort 
			        invoked.TransportMode					 = TransportMode
			        invoked.DeliveryTerms                    = DeliveryTerms 
			        invoked.StatisticalProcedure			 = StatisticalProcedure
			        invoked.DropShipIndicator                = DropShip 
			        invoked.DeliveryDate  					 = DeliveryDate                    
			        invoked.PurchaseOrder					 = PurchaseOrder
			        invoked.Status                           = InhStatus
			    SoftMessageNumber 	= 191    
			else		
			if (HeaderFc.Delete)	 
				if (first IntrastatDtlRel exist)
					LocalErrorNumber = 103				
					return blank
										
				invoke Delete IntrastatHeaderRel	
				SoftMessageNumber 	= 192	

			return blank

		DoIntrastatDetail	is a DerivedField
			type is like IntrastatDetail
			initialize LocalErrorNumber
			initialize SoftMessageNumber
			LocalErrorCategory 	= "INING"
			LocalDescType		= LocalDescType.Header
			if (IntrastatNumber not entered)
				if (DetailFc.Delete
				or  DetailFc.Update)
					LocalErrorNumber = 101
					return blank
					
			if (DetailFc entered
			and IntrastatHeaderRel exist)
				if (IntrastatHeaderRel.Status.Reported)
					LocalErrorNumber = 106					
					return blank					
						
			if (DetailFc.Update)
				if (LineNumber not entered)
					LocalErrorNumber = 102
					return blank
					
				if (IntrastatDetailSingleRecRel not exist)
					LocalErrorNumber = 104
					return blank
										
			if (DetailFc.Create)
				if (IntrastatDetailSingleRecRel exist)
					LocalErrorNumber = 103
					return blank
						 
				if (IntrastatHeaderRel not exist)
					LocalErrorNumber = 105
					return blank

					if (FiscalYear not entered)
						FiscalYear = IntrastatHeaderRel.IntrastatHeader.FiscalYear
											
				invoke Create IntrastatDetail
					invoked.IntrastatDetail					= LineNumber
					invoked.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup
					invoked.TaxEntity						= TaxEntity
					invoked.IntrastatHeader.FiscalYear		= FiscalYear
					invoked.IntrastatHeader.IntrastatNumber	= IntrastatNumber
				    invoked.BaseAmount            			= BaseAmount
				    invoked.ICNCode				  			= ICNCode
				    invoked.StockWeight			  			= StockWeight
				    invoked.ReceivedAmount        			= ReceivedAmount
				    invoked.SupplementaryUOM      			= SupplementaryUOM
				    invoked.SupplementaryQuantity 			= SupplementaryQuantity
			else	    				
			if (DetailFc.Update)
				if (IntrastatDetailSingleRecRel	exist)	 
					invoke Update IntrastatDetailSingleRecRel
					    invoked.BaseAmount            		= BaseAmount
					    invoked.ICNCode				  		= ICNCode
					    invoked.StockWeight			  		= StockWeight
					    invoked.ReceivedAmount        		= ReceivedAmount
					    invoked.SupplementaryUOM      		= SupplementaryUOM
					    invoked.SupplementaryQuantity 		= SupplementaryQuantity
					SoftMessageNumber 	= 191    
			else		
			if (DetailFc.Delete)
				if (IntrastatDetailSingleRecRel exist)			
					invoke Delete IntrastatDetailSingleRecRel	
					SoftMessageNumber 	= 192
				if (DoNotDeleteHdrIfNoDtl)
					DoNotDeleteHdrIfNoDtl = false	
				else	
				if (!IntrastatDtlRel exist)		
					invoke Delete IntrastatHeaderRel
					initialize IntrastatNumber
					 								
			return blank 

		CheckCountries	is a DerivedField
			type is Alpha 1
			initialize LocalErrorNumber
			LocalErrorCategory 	= "TXINT"
			DoIntrastatReporting 	= false
			if (!TaxEntity.IntrastatUsed)
				SoftMessageNumber 	= 001
				return blank
			
			if (OriginCountry not entered)
				LocalErrorNumber	= 108		
				return blank		
			if (DestinationCountry not entered)
				LocalErrorNumber	= 109		
				return blank	
								
			if (InFinanceEnterpriseGroup not entered)
				if (TaxEntity entered)
					InFinanceEnterpriseGroup = TaxEntity.FinanceEnterpriseGroup
				else	
					InFinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				
			if (DestinationCountryRel not exist)
				LocalErrorNumber	= 102		
				return blank						

			if (!OriginCountry.MemberOfEC)
				LocalCountry		= OriginCountry
				LocalErrorNumber	= 003
				SoftMessageNumber 	= 050		
				return blank				

			LocalCountry 			= OriginCountry
			LocalRegion				= OriginRegion
			if (LocalRegion entered)
				if (RegionRel not exist)
					LocalErrorNumber 	= 114
					return blank				
				if (RegionRel.ExcludeFromIntrastat)
					initialize LocalErrorCategory
					SoftMessageNumber 	= 004	
					return blank
					
			LocalCountry 	= DestinationCountry
			LocalRegion		= DestinationRegion
			if (LocalRegion entered)
				if (RegionRel not exist)
					LocalErrorNumber 		= 115				
					return blank
				if (RegionRel.ExcludeFromIntrastat)
					initialize LocalErrorCategory
					SoftMessageNumber 		= 004
					return blank

			if (OriginCountry = DestinationCountry)
				LocalErrorNumber = 119	
				SoftMessageNumber = 119	
				return blank
			else
			if (!OriginCountry.MemberOfEC
			or !DestinationCountryRel.MemberOfEC)	
				LocalErrorNumber = 138				
				SoftMessageNumber = 119
				return blank	
			
			if (BranchID not entered)
				BranchID	= TaxEntity.BranchID
			
			DoIntrastatReporting 	= true
			initialize LocalErrorCategory
			return blank 
														       								


    
		OutputErrorNumber is a DerivedField	 			 
			type is Numeric 3
			if (LocalErrorNumber entered)		
				return LocalErrorNumber  													 
			return blank		         

		OutputErrorMessage is a DerivedField	  
			type is MessageField
			if (SoftMessageNumber entered
			and LocalErrorCategory not entered)
				return blank
			if (LocalErrorCategory = "ININH")
				if (LocalErrorNumber = 100)
					return Ininh100					 
				if (LocalErrorNumber = 101)			
					return DtlMsg101
				if (LocalErrorNumber = 102)	
					return Ininh102
				if (LocalErrorNumber = 103)
					return Ininh103	
				if (LocalErrorNumber = 104)			
					return Ininh104									
			else		
			if (LocalErrorCategory = "INING")
				if (LocalErrorNumber = 101)			
					return DtlMsg101					 
				if (LocalErrorNumber = 102)
					return DtlMsg102
				if (LocalErrorNumber = 103)
					return DtlMsg103
				if (LocalErrorNumber = 104)
					return DtlMsg104													
				if (LocalErrorNumber = 105)
					return DtlMsg105
				if (LocalErrorNumber = 106)
					return DtlMsg106											
			else
			if (LocalErrorCategory = "TXINT")
				if (LocalErrorNumber = 101)			
					return Txint101	
				if (LocalErrorNumber = 102)			
					return Txint102												 
				if (LocalErrorNumber = 108)			
					return Txint108		
				if (LocalErrorNumber = 109)			
					return Txint109
				if (LocalErrorNumber = 114)
					return Txint114	
				if (LocalErrorNumber = 115)
					return Txint115
				if (LocalErrorNumber = 119)	
					SoftMessageNumber = LocalErrorNumber
					initialize LocalErrorNumber									
					return Txint119
				if (LocalErrorNumber = 003)
					return Error003															
				if (LocalErrorNumber = 138)			
					return Error138						
			else		 			 
			if (LocalErrorCategory not entered)			
				if (LocalErrorNumber = 001)
					return Error001	
				if (LocalErrorNumber = 002)
					return Error002	
				if (LocalErrorNumber = 004)
					return Error004	
				if (LocalErrorNumber = 005)
					return Error005											
			else			
			if (SoftMessageNumber entered)
				return SoftErrorMessages
																												
			return blank	

		SoftErrorMessages is a DerivedField	  
			type is MessageField
			if (SoftMessageNumber entered)
				if (SoftMessageNumber = 190)
					return Msg190
				if (SoftMessageNumber = 191)
					return Msg191
				if (SoftMessageNumber = 192)
					return Msg192								
				if (SoftMessageNumber = 001)
					return Msg001	
				if (SoftMessageNumber = 004)
					return Msg004					
				if (SoftMessageNumber = 119)	 							
					return Msg119
			return blank
						
		FileDesc is a DerivedField	  
			type is MessageField
			if (LocalDescType.Header)
				return HeaderText
				
			if (LocalDescType.Detail)
				return DetailText	
			
			return blank
		HeaderText is a MessageField
			"Header"	

		DetailText is a MessageField
			"Detail"	
			
		Error001 is a MessageField
			"TaxEntityRequired"	

		Error002 is a MessageField
			"FiscalYearRequired"	

		Error003 is a MessageField
			"Country/\Jurisdiction<LocalCountry>IsNotAMemberOfTheEuropeanCommunity"	

		Error004 is a MessageField
			"HeaderFunctionCodeInvalid;MustBe/C,/DOr/U"	
		Error005 is a MessageField
			"DetailFunctionCodeInvalid;MustBe/C,/DOr/U"				
			
		Error138 is a MessageField
			"IntrastatReportsAreNotRequiredBetweenTheseCountries"				
		Ininh100 is a MessageField
			"CannotSpecifyIntrastatNumberOnAdd"		 

		Ininh102 is a MessageField
			"CannotChangeHeaderAfterItHasBeenReported"	

		Ininh103 is a MessageField
			"CannotDeleteIntrastatHeaderRecord;DetailRecordsExist"		
				
		Ininh104 is a MessageField
			"IntrastatHeaderDoesNotExist"	
			
		DtlMsg101 is a MessageField
			"CannotAdd,ChangeOrDelete:IntrastatNumberNotSpecified"	

		DtlMsg102 is a MessageField
			"CannotProcess,LineNumberNotSpecified"

		DtlMsg103 is a MessageField
			"DetailRecordDoesAlreadyExist"
			
		DtlMsg104 is a MessageField
			"DetailRecordDoesNotExist"
			
		DtlMsg105 is a MessageField
			"CannotAddOrChange,IntrastatHeaderRecordDoesNotExist"

		DtlMsg106 is a MessageField
			"CannotAddChangeOrDeleteIntrastatRecordAfterReporting"
					
		Txint101 is a MessageField
			"FromCountry/\jurisdiction<OriginCountry>DoesNotExist"		 

		Txint102 is a MessageField
			"ToCountry/\jurisdiction<DestinationCountry>DoesNotExist"	
			
		Txint108 is a MessageField
			"FromCountry/\jurisdictionRequired"	

		Txint109 is a MessageField
			"ToCountry/\jurisdictionRequired"	

		Txint114 is a MessageField
			"OriginRegionInvalidForCountry/\jurisdiction"	

		Txint115 is a MessageField
			"DestinationRegionInvalidForCountry/\jurisdiction"	

		Txint119 is a MessageField
			"IntrastatRecordDoesNotNeedToBeCreated"	
																		
		Msg001 is a MessageField
			"TaxEntityNotSetupToUseIntrastatReporting"		 

		Msg004 is a MessageField
			"Region<LocalRegion>IsExcludedFromIntrastatReporting"	
		Msg119 is a MessageField
			"IntrastatRecordDoesNotNeedToBeCreated"							
		Msg190 is a MessageField
			"Intrastat<FileDesc>RecordCreated"									

		Msg191 is a MessageField
			"Intrastat<FileDesc>RecordUpdated"									

		Msg192 is a MessageField
			"Intrastat<FileDesc>RecordDeleted"									

