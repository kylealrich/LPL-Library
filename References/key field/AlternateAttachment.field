AlternateAttachment is a Field

    Representation
        Group Fields
        	Title						is DocumentTitle size 100
        		disable Auditing
        	File						is BinaryDocument
        		enable alternate document location
        			document is FileADL
        	MimeType					is MimeType
        		disable Auditing

			FSMAttachmentIDM	
				disable Auditing 		
				delete ignored
				protected
			
	Patterns
		implements CompactFormat

    Context Fields
		MaxAttachmentSize	
	
	Local Fields
		LocalFSMAttachmentIDMView 	 is an FSMAttachmentIDM view
		LocalBusinessObjectReference is BusinessObjectReference
		LocalErrorMessage	    	 is Text
		LocalUpdateNeeded			 is Boolean  
		BypassIDMUpload				 is Boolean  
		ExecuteUploadToIDM			 is Boolean 
		ExternalAttachment	   	 	 is an IDMAttachment
		IDMDocumentTypeXML			 is XMLDocument
		IDMSingleValueAttribute 	 is an IDMAttributeOccurs
		IDMDocumentType
		IDMItem
		AttributeCount				 is Numeric 2
		LocalPurgeTriggered			 is Boolean

    Derived Fields
		FileSizeBytes is a DerivedField
			type is Numeric 11
            return (File size)
            
        FileSizeMB is a DerivedField
            type is Decimal 13.2
            	precision is 2
            return ((FileSizeBytes / 1024) / 1024)

        MaxAttachmentSizeMB is a DerivedField
            type is Decimal 13.2
            	precision is 2
            return ((MaxAttachmentSize / 1024) / 1024)	

		FileADL is a DerivedField
			type is BinaryDocument
			if (IsExternal
			and FSMAttachmentIDM.IDMUniqueId entered)
				invoke GetFileInIDM FSMAttachmentIDM
				return FSMAttachmentIDM.LocalExternalAttachment.File
			else
				return File

		UserTemplateName is a StringField
			type is Text
			restricted
			"FSM_AttachmentIDM_"
			LocalBusinessObjectReference.BusinessClassName
			"_ST"

	Conditions
	
		ValidForIDMUpload	
			restricted
			when (IsLocal)
	
		IsExternal
			restricted
			when (MimeType entered
			and File document location = document location.External)

		IsLocal
			restricted
			when (MimeType entered
			and File document location = document location.Local)		

		AttachmentExist
			restricted
			when (MimeType entered)
			
    Field Rules
        Title
			if (MimeType entered)
				required
			
			if (Title changed)
				ExecuteUploadToIDM = true

        File
			if (Title entered
			or MimeType entered)
				required

			if (MaxAttachmentSize in context
            and MaxAttachmentSize entered)
				constraint (FileSizeBytes <= MaxAttachmentSize)
					"AttachmentSizeCannotBeGreaterThan<MaxAttachmentSize>Bytes_(<MaxAttachmentSizeMB>MB).YourAttachmentSizeIs<FileSizeBytes>Bytes_(<FileSizeMB>MB)."
			
			if (File local document changed)
				ExecuteUploadToIDM = true

		FSMAttachmentIDM
			if (MimeType not entered
			and FSMAttachmentIDM entered)
				File document location = document location.Local 
				if (FSMAttachmentIDM.IDMUniqueId entered)
					invoke DeleteOnIDM FSMAttachmentIDM
						resume on error	
				else
					invoke Delete FSMAttachmentIDM
						resume on error			
				initialize ExecuteUploadToIDM
				initialize FSMAttachmentIDM				
