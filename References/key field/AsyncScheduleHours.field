AsyncScheduleHours is a Field
	default label is "ScheduleHour"
	 
    Representation
		AsyncScheduleHourSelect
        	occurs 10 times

	Local Fields
    	LocalCounter	is Numeric 1
    	LoopCounter		is Numeric 1
    	LocalBlankOnly  is Boolean
    	
	Context Fields
		AsyncActionRequest  
		AsyncScheduleTemplate  	
		
	Conditions
		IsRequest
			default label is untranslatable
			when (AsyncActionRequest in context and (parentcontext.name = "AsyncActionRequest" or not IsTemplate)) 
			
		IsTemplate
			default label is untranslatable
			when (AsyncScheduleTemplate in context and parentcontext.name = "AsyncScheduleTemplate")
			
		DisplayRangeSeparator
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleHour.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleHour.Range))
			
		DisplayElement2
			default label is untranslatable
			when ((IsRequest and (AsyncActionRequest.ScheduleHour.Range or AsyncActionRequest.ScheduleHour.Multiple)) or (IsTemplate and (AsyncScheduleTemplate.ScheduleHour.Range or AsyncScheduleTemplate.ScheduleHour.Multiple)))
			
		DisplayOtherElements
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleHour.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleHour.Multiple))
			
		IsMultipleScheduleHour
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleHour.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleHour.Multiple))		
			
		IsScheduleHourRange
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleHour.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleHour.Range))							
			
	Derived Fields
			
		SlotOneRangeMessage is a MessageField
			"Range"
						
		HourSlotOneLabel is a DerivedField
			type is MessageField
			default label is untranslatable
			if ((IsRequest and AsyncActionRequest.ScheduleHour.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleHour.Range))
				return SlotOneRangeMessage
			else
				return blank
			  	                		
	Field Rules

		AsyncScheduleHours
			no duplicates
			
			if ((IsRequest and AsyncActionRequest.ScheduleHour.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleHour.Range))
				constraint (AsyncScheduleHours.AsyncScheduleHourSelect[2] entered) 
					"HourRangeValuesRequired" 
			
			LocalBlankOnly = false
			LoopCounter = 1
			while LoopCounter < 10
			    LocalCounter = LoopCounter + 1 
				
				if ((not IsScheduleHourRange
				and not IsMultipleScheduleHour)
				or (IsScheduleHourRange
				and LocalCounter > 2))
					AsyncScheduleHours.AsyncScheduleHourSelect[LocalCounter] = blank
				else
					if (LocalBlankOnly) 
						constraint (AsyncScheduleHours.AsyncScheduleHourSelect[LocalCounter] = blank)
							"ValuesMustBeEnteredInAscendingOrder"
				
					constraint(AsyncScheduleHours.AsyncScheduleHourSelect[LocalCounter] = 0 or AsyncScheduleHours.AsyncScheduleHourSelect[LocalCounter] > AsyncScheduleHours.AsyncScheduleHourSelect[LoopCounter])
						"ValuesMustBeEnteredInAscendingOrder"

					if (not LocalBlankOnly
					and AsyncScheduleHours.AsyncScheduleHourSelect[LocalCounter] = 0
					and AsyncScheduleHours.AsyncScheduleHourSelect[LoopCounter] > 0)
						LocalBlankOnly = true
						
				LoopCounter += 1
