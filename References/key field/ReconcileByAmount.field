ReconcileByAmount is a Field

    Representation
        Group Fields
			ReconciliationMatchType
			StatementLineOccurs 
			NumberOfStatementRecords	is Numeric 5
			TransactionLineOccurs 
			NumberOfTransactionRecords	is Numeric 5
			TransactionLineTotal		is an InternationalAmount
			TolerancePercent 			is Percent 3
			ToleranceAmount				is an InternationalAmount

    Context Fields
    	CashManagementGroup
    	CashManagementAccount
    	BankStatement

	Local Fields
		LocalMatchAmount					is an InternationalAmount
		LocalMaximimumGroupAmount			is an InternationalAmount
		LocalCurrentRotationAmount			is an InternationalAmount
		LocalCurrentStatementRotationAmount	is an InternationalAmount
		LocalStartingArrayField				is Numeric 5
		LocalCombinationCounter				is Numeric 5
		LocalRecalcCombinationCounter		is Numeric 5
		LocalTransactionCounter				is Numeric 5
		LocalMaximumGroupSizeCalculation	is Numeric 5
		LocalMinimumCalculationField		is Numeric 5
		LocalStatementCounter				is Numeric 4
		LocalNextUnmatchedRecord			is Numeric 5
		LocalNextUnmatchedStmtLine			is Numeric 5
		LocalPreviousCombinationField		is Numeric 5
		LocalPreviousTransactionCombination	is Numeric 5
		LocalStatementGroupSize				is Numeric 4
		LocalIncreaseStatementGroupSize		is Boolean
		LocalCurrentStatementRotation		is Numeric 4
		StatementCombinationOccurs			
		StatementReconciliationOccurs
		LocalTransactionGroupSize			is Numeric 5
		LocalIncreaseTransactionGroupSize	is Boolean
		LocalCurrentTransactionRotation		is Numeric 5
		TransactionCombinationOccurs		
		TransactionReconciliationOccurs
		LocalMinimumAmounts					is a TransactionCombinationOccurs
		LocalMatchFound						is Boolean
		LocalAllStatementLinesProcessed		is Boolean
		LocalAllTransactionsProcessed		is Boolean
		LocalTotalTransactionAmount			is an InternationalAmount
		LocalMatchPass						is Numeric 1
			States
				FirstPass 	value is 1
				SecondPass	value is 2
		SkipCurrentRotation					is Boolean
		IncreasePreviousField				is Boolean
		LocalNewTransactionCounter			is Numeric 5				
		LocalNewTransactionAmount			is an InternationalAmount	
		LocalNewStatementCounter			is Numeric 5				
		LocalAccumStatementAmount			is an InternationalAmount	
		LocalNewIncreaseStatementGroupSize	is Numeric 5				
		LocalRemainingStatementTotal		is an InternationalAmount	
		StartOverWithNextStatementLine		is Boolean					
		LocalDropStmtRecord					is Numeric 5				
		LocalSkipStmtAmount					is an InternationalAmount	
		LocalDerivedStmtAmount				is an InternationalAmount	
		LocalTransactionCounterCheck		is Numeric 5				
		MTOMatchFound						is Boolean					
 		
    Derived Fields
		MatchFound	is a DerivedField
			type is Boolean
			initialize LocalAllStatementLinesProcessed
			initialize LocalMatchFound
			initialize StatementCombinationOccurs
			LocalMatchPass					= 1 
			LocalCurrentStatementRotation	= 1
			LocalStatementGroupSize			= 1
			while (LocalStatementGroupSize <= NumberOfStatementRecords)
				if (LocalAllStatementLinesProcessed)
					end while
				if (LocalMatchFound
				or  LocalMatchPass.SecondPass)
					initialize LocalMatchFound
					initialize StatementCombinationOccurs
					initialize LocalCurrentStatementRotationAmount
					LocalCurrentStatementRotation	= 1
					LocalStatementGroupSize			= 1
				LocalCombinationCounter	= LocalStatementGroupSize - 1
				if  (LocalMatchPass !entered	
				and (ReconciliationMatchType.ManyToOne
				or   ReconciliationMatchType.ManyToMany))
					if (ReconciliationMatchType.ManyToOne)
						if (LocalCombinationCounter = -1)
							LocalCombinationCounter = 0
					LocalStatementCounter	= StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]
					initialize LocalAllTransactionsProcessed
					if (ReconciliationMatchType.ManyToOne
					and LocalTransactionCounter = NumberOfTransactionRecords)
						end while
					if (LocalCombinationCounter	entered
					and StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]	< (NumberOfStatementRecords - 1)
					and NextUnmatchedStatementLine													entered
					and NextUnmatchedStatementLine													<= (NumberOfStatementRecords - 1))
						StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]	= NextUnmatchedStatementLine
						LocalCurrentStatementRotation	= NextUnmatchedStatementLine + 1
					else
						if (ReconciliationMatchType.ManyToOne)
							initialize LocalDropStmtRecord
							LocalAccumStatementAmount = DerivedRemainingStatementTotal
							LocalNewTransactionCounter = DerivedTransactionMatchAmountCheck
							if (PreviousStatementCombinationField <= 0
							or  LocalIncreaseStatementGroupSize)
								initialize LocalIncreaseStatementGroupSize
								LocalStatementGroupSize	+= 1
								LocalStatementCounter	= 0
								if (LocalCombinationCounter = 0)		
									if (LocalNewTransactionCounter !entered)
										LocalNewTransactionCounter = LocalTransactionCounter
										while (LocalNewTransactionAmount !entered)
											if (LocalNewTransactionCounter <= NumberOfTransactionRecords)
												LocalNewTransactionCounter += 1
												if (!TransactionLineOccurs.StatementReconciliationAmount[LocalNewTransactionCounter].Matched)
													LocalNewTransactionAmount = TransactionLineOccurs.StatementReconciliationAmount[LocalNewTransactionCounter].ReconciliationAmount
											else
												end while
									initialize LocalNewStatementCounter
									initialize LocalAccumStatementAmount
									initialize LocalNewIncreaseStatementGroupSize
									LocalStatementCounter = NextCalculatedUnmatchedStatementLine -1
									if (LocalStatementCounter = -1)
										LocalTransactionCounter += 1
										LocalAllTransactionsProcessed = true
									LocalNewStatementCounter = LocalStatementCounter
									while (LocalAccumStatementAmount < LocalNewTransactionAmount)
										if (LocalNewStatementCounter <= NumberOfStatementRecords)
											LocalNewStatementCounter += 1
											if (!StatementLineOccurs.StatementReconciliationAmount[LocalNewStatementCounter].Matched)
												LocalAccumStatementAmount += StatementLineOccurs.StatementReconciliationAmount[LocalNewStatementCounter].ReconciliationAmount
												LocalNewIncreaseStatementGroupSize += 1
										else
											end while
									LocalStatementGroupSize = LocalNewIncreaseStatementGroupSize
									if (LocalStatementGroupSize <= 2)
										LocalStatementGroupSize = 2
									initialize LocalAccumStatementAmount
									while (LocalAccumStatementAmount < LocalNewTransactionAmount)
										if (LocalAllTransactionsProcessed)
											LocalIncreaseStatementGroupSize = true
											LocalStatementGroupSize = 1
											initialize LocalCombinationCounter
											end while
										if (StartOverWithNextStatementLine)
											initialize StartOverWithNextStatementLine
											StatementCombinationOccurs.StatementCombination[1]	= NextUnmatchedStatementLine
											LocalStatementCounter	= NextUnmatchedStatementLine
											LocalAccumStatementAmount = StatementLineOccurs.StatementReconciliationAmount[LocalStatementCounter].ReconciliationAmount
											LocalRemainingStatementTotal = DerivedRemainingStatementTotal
											LocalNewStatementCounter	= LocalStatementCounter
											if (LocalAccumStatementAmount + LocalRemainingStatementTotal < LocalNewTransactionAmount)
												LocalStatementGroupSize = 1
												initialize LocalAccumStatementAmount
												LocalTransactionCounter = LocalNewTransactionCounter
											else
												LocalCombinationCounter = 2
												LocalStatementGroupSize = DerivedNewStatementGroupSize
										else
											StatementCombinationOccurs.StatementCombination[1]	= NextUnmatchedStatementLine
											LocalStatementCounter	= NextUnmatchedStatementLine
											LocalAccumStatementAmount = StatementLineOccurs.StatementReconciliationAmount[LocalStatementCounter].ReconciliationAmount		
											LocalRemainingStatementTotal = DerivedRemainingStatementTotal
											LocalNewStatementCounter	= LocalStatementCounter
											LocalCombinationCounter = 2
											if (LocalStatementGroupSize !entered)
												LocalStatementGroupSize = DerivedNewStatementGroupSize
										while (LocalCombinationCounter < NumberOfStatementRecords)
											if (LocalAccumStatementAmount + LocalRemainingStatementTotal >= LocalNewTransactionAmount)
												while (LocalCombinationCounter < LocalStatementGroupSize)
													if (NextCalculatedUnmatchedStatementLine entered)
														StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]	= NextCalculatedUnmatchedStatementLine
														initialize LocalSkipStmtAmount
														LocalStatementCounter	= NextCalculatedUnmatchedStatementLine
														LocalAccumStatementAmount += StatementLineOccurs.StatementReconciliationAmount[LocalStatementCounter].ReconciliationAmount		
														LocalRemainingStatementTotal -= LocalSkipStmtAmount + StatementLineOccurs.StatementReconciliationAmount[LocalStatementCounter].ReconciliationAmount
														LocalCombinationCounter +=1
													else
														if (LocalStatementGroupSize	< NumberOfStatementRecords)
															LocalIncreaseStatementGroupSize	= true
														else
															LocalAllStatementLinesProcessed		= true
														end while
												if (NextCalculatedUnmatchedStatementLine	entered)	
													initialize LocalSkipStmtAmount
													LocalCurrentStatementRotation		= NextCalculatedUnmatchedStatementLine
													LocalAccumStatementAmount += StatementLineOccurs.StatementReconciliationAmount[LocalCurrentStatementRotation].ReconciliationAmount		
													LocalRemainingStatementTotal -= LocalSkipStmtAmount + StatementLineOccurs.StatementReconciliationAmount[LocalCurrentStatementRotation].ReconciliationAmount		
													LocalCombinationCounter +=1
													if (LocalAccumStatementAmount != LocalNewTransactionAmount)
														if (LocalAccumStatementAmount + LocalRemainingStatementTotal < LocalNewTransactionAmount) 
															initialize LocalAccumStatementAmount
															LocalDropStmtRecord = StatementCombinationOccurs.StatementCombination[2]
															initialize StatementCombinationOccurs
															LocalStatementCounter = LocalNewStatementCounter
															StatementCombinationOccurs.StatementCombination[1]	= LocalStatementCounter
															LocalAccumStatementAmount += StatementLineOccurs.StatementReconciliationAmount[LocalStatementCounter].ReconciliationAmount		
															LocalStatementCounter = LocalDropStmtRecord
															LocalRemainingStatementTotal = DerivedRemainingStatementTotal
															if (LocalAccumStatementAmount + LocalRemainingStatementTotal < LocalNewTransactionAmount)
																StartOverWithNextStatementLine = true
																LocalStatementCounter = LocalNewStatementCounter
																initialize LocalAccumStatementAmount
																initialize LocalDropStmtRecord
																end while
															LocalCombinationCounter = 2
															LocalStatementGroupSize = DerivedNewStatementGroupSize
														else
															initialize LocalAccumStatementAmount
															initialize StatementCombinationOccurs
															LocalStatementCounter = LocalNewStatementCounter
															StatementCombinationOccurs.StatementCombination[1]	= LocalStatementCounter
															LocalAccumStatementAmount += StatementLineOccurs.StatementReconciliationAmount[LocalStatementCounter].ReconciliationAmount		
															if (LocalDropStmtRecord entered)
																LocalStatementCounter = LocalDropStmtRecord
															LocalRemainingStatementTotal = DerivedRemainingStatementTotal
															LocalCombinationCounter = 2
															LocalStatementGroupSize += 1
													else
														initialize LocalDropStmtRecord
														end while
												else
													if (LocalAccumStatementAmount + LocalRemainingStatementTotal < LocalNewTransactionAmount)
														StartOverWithNextStatementLine = true
													else	
														LocalNewTransactionCounter = NextUnmatchedRecord
														LocalNewTransactionAmount = TransactionLineOccurs.StatementReconciliationAmount[LocalNewTransactionCounter].ReconciliationAmount
														LocalTransactionCounter = LocalNewTransactionCounter
														LocalStatementGroupSize = 1
														initialize LocalStatementCounter
														initialize LocalDropStmtRecord
														end while
											else	
												if (NextUnmatchedRecord entered)
													LocalNewTransactionCounter = NextUnmatchedRecord
													LocalNewTransactionAmount = TransactionLineOccurs.StatementReconciliationAmount[LocalNewTransactionCounter].ReconciliationAmount
													LocalTransactionCounter = LocalNewTransactionCounter
													LocalStatementGroupSize = 1
													initialize LocalStatementCounter
													initialize LocalDropStmtRecord
													end while
												else
													if (LocalNewTransactionCounter = NumberOfTransactionRecords)
														LocalAllTransactionsProcessed = true
													end while
							else
								LocalCombinationCounter	= PreviousStatementCombinationField
								LocalStatementCounter	= StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]
								while (LocalCombinationCounter < LocalStatementGroupSize)
									if (NextUnmatchedStatementLine	entered)
										StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]	= NextUnmatchedStatementLine
										LocalStatementCounter	= NextUnmatchedStatementLine
										LocalCombinationCounter +=1
									else
										if (LocalStatementGroupSize	< NumberOfStatementRecords)
											LocalIncreaseStatementGroupSize	= true
										else
											LocalAllStatementLinesProcessed		= true
										end while
								if (NextUnmatchedStatementLine	entered)
									LocalCurrentStatementRotation		= NextUnmatchedStatementLine
								else
									if (LocalStatementGroupSize	< NumberOfStatementRecords)
										LocalIncreaseStatementGroupSize	= true
									else
										LocalAllStatementLinesProcessed		= true
						else
							if (PreviousStatementCombinationField <= 0
							or  LocalIncreaseStatementGroupSize)
								initialize LocalIncreaseStatementGroupSize
								LocalStatementGroupSize	+= 1
								LocalStatementCounter	= 0
								if (LocalCombinationCounter = 0)		
									initialize LocalNewTransactionCounter
									initialize LocalNewTransactionAmount
									initialize LocalNewStatementCounter
									initialize LocalAccumStatementAmount
									initialize LocalNewIncreaseStatementGroupSize
									while (LocalNewTransactionAmount !entered)
										if (LocalNewTransactionCounter <= NumberOfTransactionRecords)
											LocalNewTransactionCounter += 1
											if (!TransactionLineOccurs.StatementReconciliationAmount[LocalNewTransactionCounter].Matched)
												LocalNewTransactionAmount = TransactionLineOccurs.StatementReconciliationAmount[LocalNewTransactionCounter].ReconciliationAmount
										else
											end while
									while (LocalAccumStatementAmount <= LocalNewTransactionAmount)
										if (LocalNewStatementCounter <= NumberOfStatementRecords)
											LocalNewStatementCounter += 1
											if (!StatementLineOccurs.StatementReconciliationAmount[LocalNewStatementCounter].Matched)
												LocalAccumStatementAmount += StatementLineOccurs.StatementReconciliationAmount[LocalNewStatementCounter].ReconciliationAmount
												LocalNewIncreaseStatementGroupSize += 1
											
										else
											end while
									LocalStatementGroupSize = (LocalNewIncreaseStatementGroupSize - 1)
									if (LocalStatementGroupSize <= 2)
										LocalStatementGroupSize = 2
								if (NextUnmatchedStatementLine	entered)
									StatementCombinationOccurs.StatementCombination[1]	= NextUnmatchedStatementLine
									LocalCombinationCounter = 2
									LocalStatementCounter	= NextUnmatchedStatementLine
									while (LocalCombinationCounter < LocalStatementGroupSize)
										if (NextUnmatchedStatementLine	entered)
											StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]	= NextUnmatchedStatementLine
											LocalStatementCounter	= NextUnmatchedStatementLine
											LocalCombinationCounter +=1
										else
											if (LocalStatementGroupSize	< NumberOfStatementRecords)
												LocalIncreaseStatementGroupSize	= true
											else
												LocalAllStatementLinesProcessed		= true
											end while
									if (NextUnmatchedStatementLine	entered)
										LocalCurrentStatementRotation		= NextUnmatchedStatementLine
									else
										if (LocalStatementGroupSize	< NumberOfStatementRecords)
											LocalIncreaseStatementGroupSize	= true
										else
											LocalAllStatementLinesProcessed		= true
								else
									if (LocalStatementGroupSize	< NumberOfStatementRecords)
										LocalIncreaseStatementGroupSize	= true
									else
										LocalAllStatementLinesProcessed		= true
							else
								LocalCombinationCounter	= PreviousStatementCombinationField
								LocalStatementCounter	= StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]
								while (LocalCombinationCounter < LocalStatementGroupSize)
									if (NextUnmatchedStatementLine	entered)
										StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]	= NextUnmatchedStatementLine
										LocalStatementCounter	= NextUnmatchedStatementLine
										LocalCombinationCounter +=1
									else
										if (LocalStatementGroupSize	< NumberOfStatementRecords)
											LocalIncreaseStatementGroupSize	= true
										else
											LocalAllStatementLinesProcessed		= true
										end while
								if (NextUnmatchedStatementLine	entered)
									LocalCurrentStatementRotation		= NextUnmatchedStatementLine
								else
									if (LocalStatementGroupSize	< NumberOfStatementRecords)
										LocalIncreaseStatementGroupSize	= true
									else
										LocalAllStatementLinesProcessed		= true

					if (!LocalAllStatementLinesProcessed
					and !LocalIncreaseStatementGroupSize)
						LocalCombinationCounter	= 1
						initialize LocalCurrentStatementRotationAmount
						while (LocalCombinationCounter < LocalStatementGroupSize)
							LocalCurrentStatementRotationAmount	+= StatementLineOccurs.StatementReconciliationAmount[StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]].ReconciliationAmount
							LocalCombinationCounter	+= 1

				if (!LocalAllStatementLinesProcessed
				and !LocalIncreaseStatementGroupSize)
					while (LocalCurrentStatementRotation <= NumberOfStatementRecords)
						if  (LocalMatchFound
						and  LocalStatementGroupSize > 1)
							end while
						else
							initialize LocalMatchFound
						if (LocalCurrentStatementRotation not entered)
								LocalCurrentStatementRotation	= 1
						StatementCombinationOccurs.StatementCombination[LocalStatementGroupSize]	= LocalCurrentStatementRotation
						if  (LocalCurrentStatementRotation = NumberOfStatementRecords)
							if (ReconciliationMatchType.OneToOne
							or   ReconciliationMatchType.OneToMany)
								LocalAllStatementLinesProcessed	= true
							if (LocalMatchPass.FirstPass)
								LocalMatchPass		= 2 
							else
								initialize LocalMatchPass
						if  (!StatementLineOccurs.StatementReconciliationAmount[LocalCurrentStatementRotation].Matched
						and (LocalCurrentStatementRotationAmount + StatementLineOccurs.StatementReconciliationAmount[LocalCurrentStatementRotation].ReconciliationAmount) <= TransactionLineTotal)
							LocalMatchAmount	= (LocalCurrentStatementRotationAmount + StatementLineOccurs.StatementReconciliationAmount[LocalCurrentStatementRotation].ReconciliationAmount)
	
							initialize LocalCurrentRotationAmount
							initialize TransactionCombinationOccurs
							initialize LocalAllTransactionsProcessed
							initialize IncreasePreviousField
							initialize SkipCurrentRotation
							if (LocalMatchPass !entered
							and ReconciliationMatchType.ManyToOne)
								LocalCurrentTransactionRotation = LocalNewTransactionCounter
							else
								LocalCurrentTransactionRotation	= 1
							LocalTransactionGroupSize		= 1
							while (LocalTransactionGroupSize <= MaximumGroupSize) 
								if (LocalMatchFound
								or  LocalAllTransactionsProcessed)
									end while
								initialize SkipCurrentRotation
								LocalCombinationCounter	= LocalTransactionGroupSize - 1
								if  (TransactionCombinationOccurs.TransactionCombination[1] entered
								and (ReconciliationMatchType.OneToMany
								or   ReconciliationMatchType.ManyToMany)
								and  !LocalMatchPass.FirstPass)
									LocalTransactionCounter	= TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]
									if (!LocalIncreaseTransactionGroupSize
									and !IncreasePreviousField
									and LocalCombinationCounter	entered
									and TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]	< (NumberOfTransactionRecords - 1)
									and NextUnmatchedRecord																entered
									and NextUnmatchedRecord																<= (NumberOfTransactionRecords - 1))
										TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]	= NextUnmatchedRecord
										LocalCurrentRotationAmount	-= TransactionLineOccurs.StatementReconciliationAmount[LocalTransactionCounter].ReconciliationAmount 
										LocalCurrentRotationAmount	+= TransactionLineOccurs.StatementReconciliationAmount[NextUnmatchedRecord].ReconciliationAmount 
										LocalCurrentTransactionRotation	= NextUnmatchedRecord + 1
									else
										initialize IncreasePreviousField
										LocalPreviousTransactionCombination		= PreviousCombinationField
										if (LocalPreviousTransactionCombination <= 0
										or  LocalIncreaseTransactionGroupSize)
											initialize LocalCurrentRotationAmount
											initialize LocalIncreaseTransactionGroupSize
											LocalTransactionGroupSize		+= 1
											LocalTransactionCounter			= (LocalStartingArrayField - 1)
											if (NextUnmatchedRecord	entered)
												TransactionCombinationOccurs.TransactionCombination[1]	= NextUnmatchedRecord
												LocalCurrentRotationAmount	+= TransactionLineOccurs.StatementReconciliationAmount[NextUnmatchedRecord].ReconciliationAmount
												LocalCombinationCounter = 2
												LocalTransactionCounter	= NextUnmatchedRecord
												while (LocalCombinationCounter < LocalTransactionGroupSize)
													if (NextUnmatchedRecord	entered)
														TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]	= NextUnmatchedRecord
														LocalCurrentRotationAmount	+= TransactionLineOccurs.StatementReconciliationAmount[NextUnmatchedRecord].ReconciliationAmount 
														LocalTransactionCounter	= NextUnmatchedRecord
														LocalCombinationCounter +=1
													else
														LocalAllTransactionsProcessed		= true
														end while
												if (NextUnmatchedRecord	entered)
													LocalCurrentTransactionRotation		= NextUnmatchedRecord
													if (LocalMatchAmount	> (LocalCurrentRotationAmount + TransactionLineOccurs.StatementReconciliationAmount[LocalCurrentTransactionRotation].ReconciliationAmount))
														if (LocalTransactionGroupSize	< MaximumGroupSize) 
															LocalIncreaseTransactionGroupSize	= true
														else
															LocalAllTransactionsProcessed		= true
													else
														if (LocalMatchAmount	< (LocalCurrentRotationAmount + TransactionLineOccurs.StatementReconciliationAmount[NumberOfTransactionRecords].ReconciliationAmount))
															SkipCurrentRotation	= true	
												else
													LocalAllTransactionsProcessed		= true
											else
												LocalAllTransactionsProcessed		= true
										else
											LocalCombinationCounter	= LocalPreviousTransactionCombination
											LocalTransactionCounter	= TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]
											while (LocalCombinationCounter < LocalTransactionGroupSize)
												if (NextUnmatchedRecord	entered)
													TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]	= NextUnmatchedRecord
													LocalCurrentRotationAmount	+= TransactionLineOccurs.StatementReconciliationAmount[NextUnmatchedRecord].ReconciliationAmount 
													LocalTransactionCounter	= NextUnmatchedRecord
													LocalCombinationCounter +=1
												else
													if (LocalTransactionGroupSize	< MaximumGroupSize) 
														LocalIncreaseTransactionGroupSize	= true
													else
														LocalAllTransactionsProcessed		= true
													end while
											if (NextUnmatchedRecord	entered)
												LocalCurrentTransactionRotation		= NextUnmatchedRecord
											else
												if (LocalTransactionGroupSize	< MaximumGroupSize) 
													LocalIncreaseTransactionGroupSize	= true
												else
													LocalAllTransactionsProcessed		= true


									if (!LocalAllTransactionsProcessed
									and !LocalIncreaseTransactionGroupSize)
										if (LocalMatchAmount	> (LocalCurrentRotationAmount + TransactionLineOccurs.StatementReconciliationAmount[LocalCurrentTransactionRotation].ReconciliationAmount))						
											IncreasePreviousField	= true
											SkipCurrentRotation		= true
										if (LocalMatchAmount	< (LocalCurrentRotationAmount + TransactionLineOccurs.StatementReconciliationAmount[NumberOfTransactionRecords].ReconciliationAmount))	
											SkipCurrentRotation		= true	
	
								if (!SkipCurrentRotation
								and !LocalAllTransactionsProcessed
								and !LocalIncreaseTransactionGroupSize)
									while (LocalCurrentTransactionRotation <= NumberOfTransactionRecords)
										if (LocalCurrentTransactionRotation not entered)
											LocalCurrentTransactionRotation	= 1
										TransactionCombinationOccurs.TransactionCombination[LocalTransactionGroupSize]	= LocalCurrentTransactionRotation

										if  (LocalCurrentTransactionRotation = NumberOfTransactionRecords
										and (ReconciliationMatchType.OneToOne
										or   ReconciliationMatchType.ManyToOne)
										or   LocalMatchPass.FirstPass)
											LocalAllTransactionsProcessed	= true
										LocalTotalTransactionAmount	= (LocalCurrentRotationAmount + TransactionLineOccurs.StatementReconciliationAmount[LocalCurrentTransactionRotation].ReconciliationAmount)
										if (!TransactionLineOccurs.StatementReconciliationAmount[LocalCurrentTransactionRotation].Matched)
											if (LocalMatchAmount	= LocalTotalTransactionAmount)
										 		LocalMatchFound	= true
												TransactionLineTotal	-= LocalMatchAmount
												LocalCombinationCounter	= 1
												while (LocalCombinationCounter <= LocalStatementGroupSize)
													StatementLineOccurs.StatementReconciliationAmount[StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]].Matched	= true
													StatementReconciliationOccurs.BankStatementReconciliation[LocalCombinationCounter]	= StatementLineOccurs.StatementReconciliationAmount[StatementCombinationOccurs.StatementCombination[LocalCombinationCounter]].BankStatementReconciliation
													LocalCombinationCounter	+= 1
							
												LocalCombinationCounter	= 1
												while (LocalCombinationCounter <= LocalTransactionGroupSize)
													TransactionLineOccurs.StatementReconciliationAmount[TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]].Matched	= true
													TransactionReconciliationOccurs.BankStatementReconciliation[LocalCombinationCounter]	= TransactionLineOccurs.StatementReconciliationAmount[TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]].BankStatementReconciliation
													LocalCombinationCounter	+= 1
							
												invoke ReconcileMatchedAmounts BankStatementRel
													invoked.PrmStatementReconciliationOccurs	= StatementReconciliationOccurs
													invoked.PrmStatementGroupSize				= LocalStatementGroupSize
													invoked.PrmTransactionReconciliationOccurs	= TransactionReconciliationOccurs
													invoked.PrmTransactionGroupSize				= LocalTransactionGroupSize
										 		end while
										 		return true
									 		else
												if  ((ReconciliationMatchType.OneToMany
												or    ReconciliationMatchType.ManyToMany)
												and   LocalMatchAmount	> LocalTotalTransactionAmount)
													if (LocalTransactionGroupSize	= 1)
														LocalStartingArrayField	= 	LocalCurrentTransactionRotation	
									 				end while
									 			else	
									 				LocalCurrentTransactionRotation	+= 1
										else
								 			LocalCurrentTransactionRotation	+= 1
						if (ReconciliationMatchType.ManyToOne)
							if (LocalMatchPass !entered)
								if (AllStatementLinesProcessed
								or  LocalNewTransactionCounter = NumberOfTransactionRecords)		
									LocalAllStatementLinesProcessed = true
									end while
								else
									LocalCurrentStatementRotation	+= 1
							else
								if (LocalCurrentStatementRotation = NumberOfStatementRecords		
								and NextUnmatchedStatementLine !entered)
									LocalAllStatementLinesProcessed = true
									end while
								else
									LocalCurrentStatementRotation	+= 1
						else
							LocalCurrentStatementRotation	+= 1


		NextUnmatchedStatementLine is a DerivedField
			type is Numeric 4
			LocalNextUnmatchedRecord	= LocalStatementCounter + 1
			while (LocalNextUnmatchedRecord <= NumberOfStatementRecords)
				if (StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedRecord].Matched)
					LocalNextUnmatchedRecord += 1
				else
					return LocalNextUnmatchedRecord
					end while

		AllStatementLinesProcessed is a DerivedField
			type is Boolean
			LocalNextUnmatchedRecord	= 1
			while (LocalNextUnmatchedRecord <= NumberOfStatementRecords)
				if (!StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedRecord].Matched)
					return false
					end while
				else
					LocalNextUnmatchedRecord += 1
			return true

		DerivedRemainingStatementTotal is a DerivedField		
			type is like InternationalAmount
			LocalNextUnmatchedRecord	= LocalStatementCounter
			while (LocalNextUnmatchedRecord <= NumberOfStatementRecords)
				LocalNextUnmatchedRecord += 1
				if (!StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedRecord].Matched)
					DerivedRemainingStatementTotal += StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedRecord].ReconciliationAmount
			return DerivedRemainingStatementTotal

		NextCalculatedUnmatchedStatementLine is a DerivedField		
			type is Numeric 5
			LocalNextUnmatchedStmtLine	= LocalStatementCounter + 1
			while (LocalNextUnmatchedStmtLine <= NumberOfStatementRecords)
				if (StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedStmtLine].Matched)
					LocalNextUnmatchedStmtLine += 1
				else
					if (LocalAccumStatementAmount + StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedStmtLine].ReconciliationAmount > LocalNewTransactionAmount)
						LocalSkipStmtAmount += StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedStmtLine].ReconciliationAmount		
						LocalNextUnmatchedStmtLine  += 1
					else
						return LocalNextUnmatchedStmtLine
						end while

		DerivedNewStatementGroupSize is a DerivedField		
			type is Numeric 5
			LocalNewIncreaseStatementGroupSize = 1
			LocalNextUnmatchedStmtLine = LocalStatementCounter
			LocalDerivedStmtAmount = LocalAccumStatementAmount
			while (LocalDerivedStmtAmount < LocalNewTransactionAmount)
				LocalNextUnmatchedStmtLine += 1
				if (LocalNextUnmatchedStmtLine <= NumberOfStatementRecords)
					if (!StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedStmtLine].Matched)
						LocalDerivedStmtAmount += StatementLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedStmtLine].ReconciliationAmount
						LocalNewIncreaseStatementGroupSize += 1
				else
					end while
			if (LocalNewIncreaseStatementGroupSize <= 2)
				LocalNewIncreaseStatementGroupSize = 2
			return LocalNewIncreaseStatementGroupSize

		DerivedTransactionMatchAmountCheck is a DerivedField		
			type is Numeric 5
			initialize MTOMatchFound
			while (LocalTransactionCounterCheck <= NumberOfTransactionRecords)
				LocalTransactionCounterCheck += 1
				if (!TransactionLineOccurs.StatementReconciliationAmount[LocalTransactionCounterCheck].Matched)
					LocalNewTransactionAmount = TransactionLineOccurs.StatementReconciliationAmount[LocalTransactionCounterCheck].ReconciliationAmount
					if (LocalNewTransactionAmount = LocalAccumStatementAmount)
						MTOMatchFound = true
						end while
			if (MTOMatchFound)
				return LocalTransactionCounterCheck
			initialize LocalTransactionCounterCheck
			initialize LocalNewTransactionAmount
			return blank

		PreviousStatementCombinationField is a DerivedField
			type is Numeric 4
			LocalPreviousCombinationField	= LocalCombinationCounter -1
			while (LocalPreviousCombinationField > 0)
				LocalStatementCounter	= StatementCombinationOccurs.StatementCombination[LocalPreviousCombinationField]
				if (NextUnmatchedStatementLine <= (NumberOfStatementRecords - LocalStatementGroupSize + LocalPreviousCombinationField))
					return LocalPreviousCombinationField
					end while
				else
					LocalPreviousCombinationField -= 1

		NextUnmatchedRecord is a DerivedField
			type is Numeric 5
			LocalNextUnmatchedRecord	= LocalTransactionCounter + 1
			while (LocalNextUnmatchedRecord <= NumberOfTransactionRecords)
				if (TransactionLineOccurs.StatementReconciliationAmount[LocalNextUnmatchedRecord].Matched)
					LocalNextUnmatchedRecord += 1
				else
					return LocalNextUnmatchedRecord
					end while

		PreviousCombinationField is a DerivedField
			type is Numeric 5
			LocalPreviousCombinationField	= LocalCombinationCounter -1
			LocalCurrentRotationAmount		-= TransactionLineOccurs.StatementReconciliationAmount[TransactionCombinationOccurs.TransactionCombination[LocalCombinationCounter]].ReconciliationAmount
			while (LocalPreviousCombinationField > 0)
				LocalTransactionCounter			= TransactionCombinationOccurs.TransactionCombination[LocalPreviousCombinationField]
				LocalCurrentRotationAmount		-= TransactionLineOccurs.StatementReconciliationAmount[LocalTransactionCounter].ReconciliationAmount
				LocalRecalcCombinationCounter	= LocalPreviousCombinationField
				LocalMinimumCalculationField	= (LocalTransactionGroupSize - LocalPreviousCombinationField + 1)
				if (NextUnmatchedRecord <= (NumberOfTransactionRecords - LocalTransactionGroupSize + LocalPreviousCombinationField)
				and LocalMatchAmount >= (LocalCurrentRotationAmount + LocalMinimumAmounts.TransactionCombination[LocalMinimumCalculationField]))
					return LocalPreviousCombinationField
					end while

				else
					LocalPreviousCombinationField -= 1

		MaximumGroupSize is a DerivedField
			type is Numeric 5
			LocalCombinationCounter	= NumberOfTransactionRecords
			initialize LocalMaximimumGroupAmount
			initialize LocalMaximumGroupSizeCalculation
			while (LocalCombinationCounter >= 0)
				if (!TransactionLineOccurs.StatementReconciliationAmount[LocalCombinationCounter].Matched)
					LocalMaximumGroupSizeCalculation	+= 1
					LocalMaximimumGroupAmount	+= TransactionLineOccurs.StatementReconciliationAmount[LocalCombinationCounter].ReconciliationAmount
					LocalMinimumAmounts.TransactionCombination[LocalMaximumGroupSizeCalculation]	= LocalMaximimumGroupAmount
					if (LocalMaximimumGroupAmount >= LocalMatchAmount)
						end while
					else
						LocalCombinationCounter		-= 1
				else
					LocalCombinationCounter		-= 1
			return LocalMaximumGroupSizeCalculation 



	Relations	 
        BankStatementRel
            one-to-one relation to BankStatement
            Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
				related.CashManagementAccount	= CashManagementAccount
				related.BankStatement			= BankStatement
		
			
