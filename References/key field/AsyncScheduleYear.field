AsyncScheduleYear is a Field
	default label is "ScheduleYear"
	 
    Representation
		ScheduleYear is Year
        	occurs 5 times

	Local Fields
    	LocalCounter	is Numeric 1
    	LoopCounter		is Numeric 1
    	
	Context Fields
		AsyncActionRequest   
		AsyncScheduleTemplate 	
		
	Conditions
		IsRequest
			default label is untranslatable
			when (AsyncActionRequest in context and (parentcontext.name = "AsyncActionRequest" or not IsTemplate)) 
			
		IsTemplate
			default label is untranslatable
			when (AsyncScheduleTemplate in context and parentcontext.name = "AsyncScheduleTemplate")
			
		DisplayRangeSeparator
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleYearType.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleYearType.Range))
			
		DisplayElement2
			default label is untranslatable
			when ((IsRequest and (AsyncActionRequest.ScheduleYearType.Range or AsyncActionRequest.ScheduleYearType.Multiple)) or (IsTemplate and (AsyncScheduleTemplate.ScheduleYearType.Range or AsyncScheduleTemplate.ScheduleYearType.Multiple)))
			
		DisplayOtherElements
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleYearType.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleYearType.Multiple))
			
		IsSpecificYear
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleYearType.SpecificYear) or (IsTemplate and AsyncScheduleTemplate.ScheduleYearType.SpecificYear))
		
		IsYearRange
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleYearType.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleYearType.Range))
			
	Derived Fields
		SlotOneRangeMessage is a MessageField
			"Range"
						
		YearSlotOneLabel is a DerivedField
			type is MessageField
			default label is untranslatable
			if ((IsRequest and AsyncActionRequest.ScheduleYearType.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleYearType.Range))
				return SlotOneRangeMessage
			else
				return blank
			  	                		
	Field Rules

		AsyncScheduleYear
			edit contiguous
			no duplicates
			
			if ((IsRequest and AsyncActionRequest.ScheduleYearType.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleYearType.Range))
				constraint (AsyncScheduleYear.ScheduleYear[1] entered and AsyncScheduleYear.ScheduleYear[2] entered)
					"YearRangeValuesRequired" 
			
			LoopCounter = 1
			while LoopCounter < 5
			    LocalCounter = LoopCounter + 1 
				
				if (AsyncScheduleYear.ScheduleYear[LocalCounter] entered)
					if ((IsSpecificYear
					and LocalCounter > 1)
					or (IsYearRange
					and LocalCounter > 2))
						AsyncScheduleYear.ScheduleYear[LocalCounter] = blank
					else
						constraint (AsyncScheduleYear.ScheduleYear[LoopCounter] entered)
							"ValuesMustBeEnteredFromFirstToLastInAscendingOrder"
					
						constraint(AsyncScheduleYear.ScheduleYear[LocalCounter] > AsyncScheduleYear.ScheduleYear[LoopCounter])
							"ValuesMustBeEnteredInAscendingOrder"
								
				LoopCounter += 1
