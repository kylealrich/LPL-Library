POLineLookup is a Field	

	Representation
		Group Fields	
			Company					is a PurchasingCompany
			PurchaseOrder			is like PurchaseOrder			
			PurchaseOrderLine		is like PurchaseOrderLine
			Item					is like Item
			VendorItem				is like VendorItem
			Description
			Location				is like InventoryLocation
			AllowMatchItemOrVendorItem is Boolean
			InterfaceType			is Alpha size 1
				States
					Invoice value is "I"
					Receipt value is "R"
			
			
	Local Fields
		LocalVendor				is like Vendor			
	Transient Fields
		OutputErrorNumber			is Numeric 3

	Derived Fields	 
		DerivedPoolOption is a DerivedField
			type is like PoolOption
			LocalVendor = PurchaseOrderRel.Vendor
			if (!LocalVendorRel.PoolOption.UseCompany)
				return LocalVendorRel.PoolOption
			return MatchCompanyRel.PoolOption

		ItemCount is a DerivedField
			type is Numeric 4
			restricted
			return instance count of POLineByItemRel

		VendorItemCount	is a DerivedField
			type is Numeric 4
			restricted
			return instance count of POLineByVendorItemRel
			
		ExecuteLookup is a DerivedField		
			type is Numeric 1



			if  (AllowMatchItemOrVendorItem)
				if  (VendorItem entered
				and  Item entered
				and (PurchaseOrderLine not entered
				or   PurchaseOrderLineRel.Item != Item
				or   PurchaseOrderLineRel.VendorItem != VendorItem))
				
	
					if  (PurchaseOrderLine entered
					and  PurchaseOrderLineRel.Item = Item
					and  PurchaseOrderLineRel.VendorItem != VendorItem)
					    initialize VendorItem
					else
					if  (PurchaseOrderLine entered
					and  PurchaseOrderLineRel.Item != Item
					and  PurchaseOrderLineRel.VendorItem = VendorItem)
					    initialize Item
					else
					if (first POLineByVendorItemRel not exists
					and first POLineByItemRel exists)
					    initialize VendorItem	
					else
					if (first POLineByVendorItemRel exists
					and first POLineByItemRel not exists)
					    initialize Item	
	
			if  (Item not entered
			and  Description not entered
			and  VendorItem not entered
			and  PurchaseOrderLine not entered)
				OutputErrorNumber			= 101
				return 1					
			else
			if  (PurchaseOrderLine entered
			and  PurchaseOrderLineRel exists
			and (not PurchaseOrderLineRel.QuantityFullyCancelled
			or   PurchaseOrderLineRel.ReceivedQuantity entered  
			or  (PurchasingCompanyRel.ReopenPurchaseOrderLine
			and  InterfaceType = "R"))
			and  PurchaseOrderLineRel.Item = Item
			and  PurchaseOrderLineRel.VendorItem = VendorItem
			and (!DerivedPoolOption.CompanyVendorPoShipTo 
			or   PurchaseOrderLineRel.ShipToLocation = Location))
				return blank					
			else
			if  (PurchaseOrderLine entered
			and  PurchaseOrderLineRel exists
			and (not PurchaseOrderLineRel.QuantityFullyCancelled
			or   PurchaseOrderLineRel.ReceivedQuantity entered  
			or  (PurchasingCompanyRel.ReopenPurchaseOrderLine
			and  InterfaceType = "R"))
			and  PurchaseOrderLineRel.Item = Item
			and  VendorItem = blank
			and (!DerivedPoolOption.CompanyVendorPoShipTo 
			or   PurchaseOrderLineRel.ShipToLocation = Location))
				VendorItem 	= PurchaseOrderLineRel.VendorItem
				return blank					
			else
			if  (PurchaseOrderLine entered
			and  PurchaseOrderLineRel exists
			and (not PurchaseOrderLineRel.QuantityFullyCancelled
			or   PurchaseOrderLineRel.ReceivedQuantity entered   
			or  (PurchasingCompanyRel.ReopenPurchaseOrderLine
			and  InterfaceType = "R"))
			and  Item = blank
			and  PurchaseOrderLineRel.VendorItem = VendorItem
			and (!DerivedPoolOption.CompanyVendorPoShipTo 
			or   PurchaseOrderLineRel.ShipToLocation = Location))
				Item = PurchaseOrderLineRel.Item
				return blank					
			else
			if  (Location not entered)
				if  (PurchaseOrderLine entered
				and  PurchaseOrderLineRel exists
				and (not PurchaseOrderLineRel.QuantityFullyCancelled
				or   PurchaseOrderLineRel.ReceivedQuantity entered  
				or  (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and  InterfaceType = "R"))
				and  Item = blank
				and  VendorItem = blank)
					Item 			= PurchaseOrderLineRel.Item
					VendorItem 	= PurchaseOrderLineRel.VendorItem
					return blank					
				else
				if  (Item entered
				and  VendorItem entered
				and  instance count of POLineByItemAndVendorItemRel = 1)
					PurchaseOrderLine			= first POLineByItemAndVendorItemRel.PurchaseOrderLine
					return blank
				else
				if  (Item entered
				and  VendorItem not entered
				and  instance count of POLineByItemRel = 1)
					PurchaseOrderLine			= first POLineByItemRel.PurchaseOrderLine
					VendorItem					= first POLineByItemRel.VendorItem
					if  (Description not entered) 
						Description				= first POLineByItemRel.Description 
					return blank
				else
				if  (VendorItem entered
				and  Item not entered
				and  instance count of POLineByVendorItemRel = 1)
					PurchaseOrderLine			= first POLineByVendorItemRel.PurchaseOrderLine 
					Item						= first POLineByVendorItemRel.Item 
					if  (Description not entered) 
						Description				= first POLineByVendorItemRel.Description 
					return blank
				else
				if  (Description entered
				and  Item not entered
				and  VendorItem not entered
				and  instance count of POLineByMatchDetailKeyRel = 1)
					PurchaseOrderLine			= first POLineByMatchDetailKeyRel.PurchaseOrderLine 
					Item						= first POLineByMatchDetailKeyRel.Item 
					VendorItem					= first POLineByMatchDetailKeyRel.VendorItem 
					return blank
				else
					if (CancelledPOLineItemRel exists
					or  CancelledPOLineVendorItemRel exists)
					    OutputErrorNumber = 103
					else
					if (Item entered
					and VendorItem entered
					and instance count of POLineByItemAndVendorItemRel > 1)
						OutputErrorNumber = 104
					else
					if (Item entered
					and VendorItem not entered
					and instance count of POLineByItemRel > 1)
						OutputErrorNumber = 104
					else
					if (VendorItem entered
					and Item not entered
					and instance count of POLineByVendorItemRel > 1)
						OutputErrorNumber = 104
					else
						OutputErrorNumber			= 102
					return 1					
			else
			if  (Location entered)
				if  (Item entered
				and  VendorItem entered
				and  instance count of POLineByItemVendorItemLocRel = 1)
					PurchaseOrderLine			= first POLineByItemVendorItemLocRel.PurchaseOrderLine
					return blank
				else
				if  (Item entered
				and  VendorItem not entered
				and  instance count of POLineByItemAndLocationRel = 1)
					PurchaseOrderLine			= first POLineByItemAndLocationRel.PurchaseOrderLine
					VendorItem					= first POLineByItemAndLocationRel.VendorItem
					if  (Description not entered) 
						Description				= first POLineByItemAndLocationRel.Description 
					return blank
				else
				if  (VendorItem entered
				and  Item not entered
				and  instance count of POLineByVendorItemLocationRel = 1)
					PurchaseOrderLine			= first POLineByVendorItemLocationRel.PurchaseOrderLine 
					Item						= first POLineByVendorItemLocationRel.Item 
					if  (Description not entered) 
						Description				= first POLineByVendorItemLocationRel.Description 
					return blank
				else
				if  (Description entered
				and  Item not entered
				and  VendorItem not entered
				and  instance count of POLineByMatchDetailKeyAndLocationRel = 1)
					PurchaseOrderLine			= first POLineByMatchDetailKeyAndLocationRel.PurchaseOrderLine 
					Item						= first POLineByMatchDetailKeyAndLocationRel.Item 
					VendorItem					= first POLineByMatchDetailKeyRel.VendorItem 
					return blank
				else
				if  (PurchaseOrderLine entered
				and  PurchaseOrderLineRel exists
				and (not PurchaseOrderLineRel.QuantityFullyCancelled
				or   PurchaseOrderLineRel.ReceivedQuantity entered  
				or  (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and  InterfaceType = "R"))
				and  PurchaseOrderLineRel.ShipToLocation = Location
				and  Item = blank
				and  VendorItem = blank)
					Item 			= PurchaseOrderLineRel.Item
					VendorItem 		= PurchaseOrderLineRel.VendorItem
					return blank					
				else
					if (CancelledPOLineItemRel exists
					or  CancelledPOLineVendorItemRel exists)
					    OutputErrorNumber = 103
					else
					if (Item entered
					and VendorItem entered
					and instance count of POLineByItemVendorItemLocRel > 1)
						OutputErrorNumber = 104
					else
					if (Item entered
					and VendorItem not entered
					and instance count of POLineByItemAndLocationRel > 1)
						OutputErrorNumber = 104
					else
					if (VendorItem entered
					and Item not entered
					and instance count of POLineByVendorItemLocationRel > 1)
						OutputErrorNumber = 104
					else
						OutputErrorNumber			= 102
					return 1					


		OutputErrorMessage is a DerivedField
			type is MessageField
			if (OutputErrorNumber = 101)
				return Msg101
			else
			if (OutputErrorNumber = 102)
				return Msg102
			else
			if (OutputErrorNumber = 103)
				return Msg103
			else
			if (OutputErrorNumber = 104)
				return Msg104

		Msg101 is a MessageField
			restricted
			"Line,Item,VendorItem,AndDescriptionAreNotEntered"

		Msg102 is a MessageField
			restricted
			"PurchaseOrderLineNotFoundForItem,VendorItemOrDescription"

		Msg103 is a MessageField
			restricted
			"PurchaseOrderLineHasBeenCancelled"

		Msg104 is a MessageField
			restricted
			"MultipleLinesWithSameItemOrVendorItem,OriginalPOLineRequiredAsLineNumberOnInvoiceDetails"

	Conditions	 

	Relations	 

        MatchCompanyRel
            one-to-one relation to MatchCompany
            Field Mapping uses symbolic key
				related.Company								= Company

        PurchasingCompanyRel
            one-to-one relation to PurchasingCompany
            Field Mapping uses symbolic key
				related.Company								= Company

        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
				related.Company								= Company

        PurchaseOrderRel
            one-to-one relation to PurchaseOrder
            Field Mapping uses symbolic key
                related.Company								= Company
                related.PurchaseOrder						= PurchaseOrder 

		LocalVendorRel
            one-to-one relation to Vendor
            Field Mapping uses symbolic key
				related.VendorGroup							= PayablesCompanyRel.VendorGroup
				related.Vendor								= LocalVendor

        PurchaseOrderLineRel
            one-to-one relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.PurchaseOrderLine				= PurchaseOrderLine

		POLineByItemAndVendorItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder
                related.Item							= Item
            Instance Selection
            	where (related.VendorItem				= VendorItem
				and   (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine)
				and     InterfaceType.Receipt))
            	
		POLineByItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder
                related.Item							= Item
            Instance Selection
                where (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt))

        POLineByVendorItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set15
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.VendorItem						= VendorItem
            Instance Selection
				where  (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt))

        POLineByMatchDetailKeyRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set11
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.MatchDetailKey					= Description
            Instance Selection
				where   (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or      (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt))

		POLineByItemVendorItemLocRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder
                related.Item							= Item
            Instance Selection
            	where (related.VendorItem				= VendorItem
				and    related.ShipToLocation			= Location
				and   (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt)))
            	
		POLineByItemAndLocationRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder
                related.Item							= Item
			Instance Selection
				where (related.ShipToLocation			= Location
				and   (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered 
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt)))

        POLineByVendorItemLocationRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set15
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.VendorItem						= VendorItem
			Instance Selection
				where (related.ShipToLocation			= Location
				and   (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt)))

		POLineByMatchDetailKeyAndLocationRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set11
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.MatchDetailKey					= Description
			Instance Selection
				where (related.ShipToLocation			= Location
				and   (!related.QuantityFullyCancelled
				or      related.ReceivedQuantity entered  
				or     (PurchasingCompanyRel.ReopenPurchaseOrderLine
				and     InterfaceType.Receipt)))

		CancelledPOLineItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder
                related.Item							= Item
            Instance Selection
                where (related.QuantityFullyCancelled)

        CancelledPOLineVendorItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set15
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.VendorItem						= VendorItem
            Instance Selection
                where (related.QuantityFullyCancelled)


				
				
