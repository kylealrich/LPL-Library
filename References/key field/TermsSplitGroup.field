TermsSplitGroup is a Field

    Representation
        Group Fields
            SplitBegDay  is Numeric size 2
            SplitEndDay  is Numeric size 2
            SplitDiscDay is Numeric size 2
            SplitNetDay  is Numeric size 2
            SplitEomFlag is Boolean
            SplitMoForw  is Numeric size 2

    Patterns
        implements CompactFormat

	Context Fields
		TermsCode
		
	Field Rules
		SplitNetDay
			if (TermsCode.IsSplitTerms)
				if (SplitNetDay entered)
					constraint (SplitNetDay >= 1
		            and SplitNetDay <= 28)
		                "DueOnValueMustBeBetween1And28"

				if (SplitNetDay entered
				and SplitDiscDay entered)
					constraint (SplitNetDay > SplitDiscDay)
							"DueDateMustBeGreaterThanDiscountDate"	                 

				if (SplitBegDay entered
				and (SplitNetDay not entered
				and  SplitEomFlag not entered
				and TermsCode.FirstNetDays not entered))
					required
						"SelectDueDateOption"                                   

		SplitDiscDay
			if (TermsCode.IsSplitTerms)           
				if (SplitDiscDay entered)
					constraint (SplitDiscDay >= 1
					and SplitDiscDay <= 28)
						"DiscountOnValueMustBeBetween1And28"

				if (SplitNetDay entered
				and SplitDiscDay entered)
					if (SplitMoForw not entered)
						constraint (SplitDiscDay > SplitEndDay)
							"DiscountDayMustBeGreaterThanEndingDateRange"           

		SplitEomFlag
			if (TermsCode.IsSplitTerms) 
				if (SplitNetDay entered)
					constraint (!SplitEomFlag)
						"CannotEnterDueDateAnd;EndOfMonth_=_\Yes"                   

		SplitBegDay
			if (TermsCode.IsSplitTerms)
				if (SplitNetDay entered
				and SplitDiscDay entered)
					if (SplitMoForw not entered)
						constraint (SplitNetDay > SplitEndDay)
							"FromDateMustBeLargerThanPrecedingDueDate"              
							
