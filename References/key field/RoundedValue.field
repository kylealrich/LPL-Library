RoundedValue is a Field

    Representation
    	Group Fields
			RoundingMethodFW
			RoundingType
			RoundTo
			RoundInput

	Patterns
		disable StaticTranslations


	Local Fields
		TempRoundInput      is Decimal size 21.8
		
	Derived Fields
		WorkFieldNormal is a DerivedField
			type is Numeric size 18
			return (RoundInput / RoundTo)
		WorkFieldLow is a DerivedField
			type is Numeric size 18
			return ((RoundInput - (RoundTo / 2)) / RoundTo) 
		WorkFieldHigh is a DerivedField
			type is Numeric size 18
			return ((RoundInput  + (RoundTo / 2)) / RoundTo)
		WorkFieldTemp is a DerivedField
			type is Decimal size 21.8
			return (RoundInput / RoundTo)				
		
		NormalRound is a DerivedField 
			type is Decimal size 21.8
			return (WorkFieldNormal * RoundTo)
		LowRound is a DerivedField 
			type is Decimal size 21.8
			return (WorkFieldLow * RoundTo)				
		HighRound is a DerivedField
 			type is Decimal size 21.8
			return (WorkFieldHigh * RoundTo)
				
		RoundResult is a DerivedField
			type is Decimal size 21.8
			if (RoundingMethodFW.MultipleOf)
				if (RoundingType.Normal)
					return NormalRound
				else
				if (RoundingType.High)
					if (WorkFieldTemp decimals = 0)
						return NormalRound
					else
						return HighRound
				else
				if (RoundingType.Low)
					return LowRound
				else
					return RoundInput
			else
				TempRoundInput = RoundInput
				if (RoundingType.Normal)
					round TempRoundInput to nearest RoundTo
				else
				if (RoundingType.Low)
					round TempRoundInput down to nearest RoundTo
				else
					round TempRoundInput up to nearest RoundTo
				return TempRoundInput
    Field Rules
				
		RoundTo
			required
				"RoundToValueIsRequiredWhenARoundingMethodIsSelected"

					
				
							
				
