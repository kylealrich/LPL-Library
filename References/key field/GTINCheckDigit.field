GTINCheckDigit is a Field

    Representation
        Group Fields
            ItemGTIN
            
    Local Fields
    	LocalAlphaGTIN					is like ItemGTIN
		LocalGTINSize					is Numeric 2
		LocalStructureSize				is Numeric 2
		LocalGTINDigit					is Numeric 1
		LocalGTINMultiplier				is Numeric 1
		LocalCheckDigitTotal			is Decimal 6.1
		LocalCheckDigitTotalRoundedUp	is Decimal 6.1
		LocalCheckDigit					is Numeric 1
		I1								is Numeric 2
		LocalRightJustGTIN				is like ItemGTIN
		TempResult						is Decimal 4.2
		RealResult						is Decimal 4.2
    
    Derived Fields
    
    	Result							is a DerivedField
			type is Numeric 18
			restricted
			TempResult = (I1 / 2)
			round TempResult down to nearest 1
			return TempResult

		Remainder 						is a DerivedField
			type is Numeric 18
			restricted
			RealResult = (I1 / 2)
			RealResult -= Result
			return (RealResult * 2)

		CheckDigitTotalRoundedUp		is a DerivedField
			type is Decimal 6.1
			restricted
			LocalCheckDigitTotalRoundedUp = LocalCheckDigitTotal
			round LocalCheckDigitTotalRoundedUp up to nearest 10
			return LocalCheckDigitTotalRoundedUp
    	
    	ValidCheckDigit is a DerivedField
    		type is Boolean
    	
    		LocalAlphaGTIN = ItemGTIN
			if (LocalAlphaGTIN size = 8)
				LocalStructureSize = 8
			else
			if (LocalAlphaGTIN size = 12)
				LocalStructureSize = 12
			else
			if (LocalAlphaGTIN size = 13)
				LocalStructureSize = 13
			else
			if (LocalAlphaGTIN size = 14)
				LocalStructureSize = 14
			LocalGTINSize  = LocalAlphaGTIN size

			if (LocalStructureSize = 8)
				LocalRightJustGTIN = "000000" + LocalAlphaGTIN[1:LocalGTINSize]
			else
			if (LocalStructureSize = 12)
				LocalRightJustGTIN = "00" + LocalAlphaGTIN[1:LocalGTINSize]
			else
			if (LocalStructureSize = 13)
				LocalRightJustGTIN = "0" + LocalAlphaGTIN[1:LocalGTINSize]
			else
				LocalRightJustGTIN = LocalAlphaGTIN[1:LocalGTINSize]						
	
			I1 = 1
			while (I1 < 14)
				LocalGTINDigit = LocalRightJustGTIN[I1]		
			
				if (Remainder = 0)
					LocalGTINMultiplier = 1
				else
					LocalGTINMultiplier = 3
	
				LocalCheckDigitTotal += (LocalGTINDigit * LocalGTINMultiplier)
				I1 += 1
	
			LocalCheckDigit = (CheckDigitTotalRoundedUp - LocalCheckDigitTotal)
			display "LocalCheckDigit<LocalCheckDigit>"
			if (LocalRightJustGTIN[14] = LocalCheckDigit)
				return true
			else
				return false
