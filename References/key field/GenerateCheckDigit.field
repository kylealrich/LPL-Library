GenerateCheckDigit is a Field

    Representation
		Group Fields
	        CheckDigitType          	is AlphaUpper size 1
	            States
	                MOD10       		value is "T"
			NumberToGenerate			is Alpha size 100

	Local Fields
		LocalCounter 			is Numeric size 2
		LocalNumberAlpha 		is Alpha size 30
		LocalNumberAlphaSize 	is Numeric size 2
		LocalCheckDigit			is Numeric size 2
		LocalTwoDigitAlpha 		is Alpha size 2
		LocalTotal 				is Numeric size 2
		LocalNumberDigit 		is Numeric size 1
									
	Derived Fields	 
		DerivedCheckDigit is a DerivedField
			type is Numeric 1
			initialize LocalCounter
			initialize LocalNumberAlpha
			initialize LocalNumberAlphaSize
			initialize LocalCheckDigit
			initialize LocalTwoDigitAlpha
			initialize LocalTotal
			initialize LocalNumberDigit
	
			while (LocalCounter < NumberToGenerate size)
				LocalCounter += 1
				if (NumberToGenerate[LocalCounter] is numeric)
					LocalNumberAlpha += NumberToGenerate[LocalCounter]

			LocalNumberAlphaSize = LocalNumberAlpha size

			if (CheckDigitType.MOD10)
			
				if (LocalNumberAlphaSize > 0)
				
					LocalCounter 	= LocalNumberAlpha size
					
					while (LocalCounter > 0)
						LocalNumberDigit   = LocalNumberAlpha[LocalCounter]
						LocalTwoDigitAlpha = LocalNumberDigit * 2
						if (LocalTwoDigitAlpha size = 2)
							LocalNumberDigit = LocalTwoDigitAlpha[2]
							LocalTotal 		+= LocalNumberDigit
							
						LocalNumberDigit = LocalTwoDigitAlpha[1]
						LocalTotal 		+= LocalNumberDigit
						if (LocalCounter > 1)
							LocalCounter -= 1
							LocalNumberDigit = LocalNumberAlpha[LocalCounter]
							LocalTotal 		+= LocalNumberDigit
						LocalCounter -= 1
					
					LocalTwoDigitAlpha = LocalTotal
					LocalNumberDigit = LocalTwoDigitAlpha[LocalTwoDigitAlpha size]
					
					if (LocalNumberDigit = 0)
						DerivedCheckDigit = LocalNumberDigit
					else
						DerivedCheckDigit = 10 - LocalNumberDigit
