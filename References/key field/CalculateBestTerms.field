CalculateBestTerms is a Field

	Representation
        Group Fields
			InvoiceDate 		is Date
			PODate				is Date
			InvoiceAmount		is an InternationalAmount
			POAmount			is an InternationalAmount
			InterestRate1		is an InternationalCost
			TermsCode
			InvoiceTermsCode	is like TermsCode
			POTermsCode			is like TermsCode
			ContextTermsCode	is a TermsCode
			
	Local Fields
		LocalTermsCalTrigger	is Alpha size 1
		CalculateTerms
		LocalDays1				is Numeric 5
		LocalDays2				is Numeric 5
		LocalValue1				is Decimal size 6.6
		LocalValue2				is Decimal size 6.6
				
	Derived Fields	
		BestTermsCode is a DerivedField	 
			type is like TermsCode

			initialize CalculateTerms
			ContextTermsCode							= InvoiceTermsCode
			CalculateTerms.TransactionDate 				= InvoiceDate
			CalculateTerms.TransactionAmount 			= InvoiceAmount
			LocalTermsCalTrigger 						= CalculateTerms.TermsCalTrigger

			if  (CalculateTerms.ProcIndicator = "M"
			or   CalculateTerms.ProcIndicator = "F")
				return InvoiceTermsCode
			else
				LocalDays1								= InvoiceDate - CalculateTerms.CalculateTermsTable.CalculateTermsOutput[1].DueDate as days
				LocalValue1								= (((LocalDays1 / 365) * InterestRate1) + CalculateTerms.CalculateTermsTable.CalculateTermsOutput[1].DiscountPercent)

				initialize CalculateTerms
				ContextTermsCode						= POTermsCode
				CalculateTerms.TransactionDate 			= PODate
				CalculateTerms.TransactionAmount 		= POAmount
				LocalTermsCalTrigger 					= CalculateTerms.TermsCalTrigger
	
				if  (CalculateTerms.ProcIndicator = "M"
				or   CalculateTerms.ProcIndicator = "F")
					return InvoiceTermsCode
				else
					LocalDays2							= PODate - CalculateTerms.CalculateTermsTable.CalculateTermsOutput[1].DueDate as days
					LocalValue2							= (((LocalDays2 / 365) * InterestRate1) + CalculateTerms.CalculateTermsTable.CalculateTermsOutput[1].DiscountPercent)

					if  (LocalValue1 > LocalValue2
					or  (LocalValue1 = LocalValue2
					and  LocalDays1 >= LocalDays2))
						return InvoiceTermsCode
					else
						return POTermsCode
						
