AsyncScheduleWeekDayOccurences is a Field
	default label is "ScheduleWeekOccurrence"
	 
    Representation
		AsyncScheduleWeekDayOccurenceSelect
        	occurs 5 times

	Local Fields
    	LocalCounter	is Numeric 1
    	LoopCounter		is Numeric 1
    	
	Context Fields
		AsyncActionRequest
		AsyncScheduleTemplate	
		
	Conditions
		IsRequest
			default label is untranslatable
			when (AsyncActionRequest in context and (parentcontext.name = "AsyncActionRequest" or not IsTemplate)) 
			
		IsTemplate
			default label is untranslatable
			when (AsyncScheduleTemplate in context and parentcontext.name = "AsyncScheduleTemplate")
		
		DisplayRangeSeparator
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDayOccurence.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDayOccurence.Range))
			
		DisplayElement2
			default label is untranslatable
			when ((IsRequest and (AsyncActionRequest.ScheduleWeekDayOccurence.Range or AsyncActionRequest.ScheduleWeekDayOccurence.Multiple)) or (IsTemplate and (AsyncScheduleTemplate.ScheduleWeekDayOccurence.Range or AsyncScheduleTemplate.ScheduleWeekDayOccurence.Multiple)))
			
		DisplayOtherElements
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDayOccurence.Multiple) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDayOccurence.Multiple))
			
		IsSpecificWeekDayOccurence
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDayOccurence > 0) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDayOccurence > 0))		
			
		IsWeekDayOccurenceRange
			default label is untranslatable
			when ((IsRequest and AsyncActionRequest.ScheduleWeekDayOccurence.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDayOccurence.Range))					
			
	Derived Fields
		SlotOneRangeMessage is a MessageField
			"Range"
						
		WeekDayOccurenceSlotOneLabel is a DerivedField
			type is MessageField
			default label is untranslatable
			if ((IsRequest and AsyncActionRequest.ScheduleWeekDayOccurence.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDayOccurence.Range))
				return SlotOneRangeMessage
			else
				return blank
			  	                		
	Field Rules

		AsyncScheduleWeekDayOccurences
			edit contiguous
			no duplicates
			
			if ((IsRequest and AsyncActionRequest.ScheduleWeekDayOccurence.Range) or (IsTemplate and AsyncScheduleTemplate.ScheduleWeekDayOccurence.Range))
				constraint (AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[1] entered and AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[2] entered)
					"WeekDayOccurrenceRangeValuesRequired" 
			
			LoopCounter = 1
			while LoopCounter < 5
			    LocalCounter = LoopCounter + 1 
				
				if (AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[LocalCounter] entered)
					if (IsSpecificWeekDayOccurence
					or (IsWeekDayOccurenceRange
					and LocalCounter > 2))
						AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[LocalCounter] = blank
					else
						constraint (AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[LoopCounter] entered)
							"ValuesMustBeEnteredFromFirstToLastInAscendingOrder"
					
						constraint(AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[LocalCounter] > AsyncScheduleWeekDayOccurences.AsyncScheduleWeekDayOccurenceSelect[LoopCounter])
							"ValuesMustBeEnteredInAscendingOrder"

				LoopCounter += 1
