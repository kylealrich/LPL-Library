CalculateExpenseManagementFinanceStructure is a Field

	Representation
        Group Fields
			InputVendorGroup						is a VendorGroup
			InputExpenseManagementInterfaceHeader	is a ExpenseManagementInterfaceHeader
			InputExpenseManagementInterfaceDetail	is a ExpenseManagementInterfaceDetail
			InputFinanceCodeBlock					is a FinanceCodeBlock
			OutputErrorNumber						is Numeric size 3
			OutputErrorMessage						is Alpha size 100
			OutputFinanceCodeBlock					is a FinanceCodeBlock
			
	Local Fields
		LocalAnswer								is Boolean
		LocalSetFinanceCodeBlockComponents		is a SetFinanceCodeBlockComponents

	Context Fields
		FinanceEnterpriseGroup

    Field Rules
		InputVendorGroup
			required
		InputExpenseManagementInterfaceHeader
			required
		InputExpenseManagementInterfaceDetail
			required
		

	Relations
		ExpenseManagementFinanceStructureRuleRel
			one-to-many relation to ExpenseManagementFinanceStructureRule
			Field Mapping uses ByPriorityActive
				related.FinanceEnterpriseGroup	= InputVendorGroup.BusinessGroup.FinanceEnterpriseGroup
				related.Active					= true

	Derived Fields	 
		CalculateExpenseManagementFinanceStructure is a DerivedField
			type is Alpha size 1
			default label is untranslatable
			LocalAnswer = SetFinanceCodeBlock

			return blank

		SetFinanceCodeBlock is a DerivedField
			type is Boolean
			for each ExpenseManagementFinanceStructureRuleRel
				if (InputExpenseManagementInterfaceDetail within each.DistributionRule)
					initialize LocalSetFinanceCodeBlockComponents
					LocalSetFinanceCodeBlockComponents.InputFinanceCodeBlock	= OutputFinanceCodeBlock
					LocalSetFinanceCodeBlockComponents.DefaultFinanceCodeBlock	= each.FinanceCodeBlock
					LocalAnswer = LocalSetFinanceCodeBlockComponents.SetIndividualFields
					OutputFinanceCodeBlock										= LocalSetFinanceCodeBlockComponents.OutputFinanceCodeBlock

			if (OutputFinanceCodeBlock.GeneralLedgerChartAccount not entered)
				OutputFinanceCodeBlock.GeneralLedgerChartAccount				= InputFinanceCodeBlock.GeneralLedgerChartAccount

			initialize LocalSetFinanceCodeBlockComponents
			LocalSetFinanceCodeBlockComponents.InputFinanceCodeBlock				= OutputFinanceCodeBlock
			LocalSetFinanceCodeBlockComponents.DefaultFinanceCodeBlock				= InputExpenseManagementInterfaceDetail.FinanceStructure
			LocalAnswer = LocalSetFinanceCodeBlockComponents.SetIndividualFields
			OutputFinanceCodeBlock													= LocalSetFinanceCodeBlockComponents.OutputFinanceCodeBlock

			for each ExpenseManagementFinanceStructureRuleRel
				if (InputExpenseManagementInterfaceDetail within each.DistributionRule)

					if (each.FinanceCodeBlockSetBlanks.SetAccountingUnitBlank)
						OutputFinanceCodeBlock.AccountingUnit 					= blank
					if (each.FinanceCodeBlockSetBlanks.SetProjectBlank)
						OutputFinanceCodeBlock.Project 							= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension1Blank)
						OutputFinanceCodeBlock.FinanceDimension1 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension2Blank)
						OutputFinanceCodeBlock.FinanceDimension2 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension3Blank)
						OutputFinanceCodeBlock.FinanceDimension3 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension4Blank)
						OutputFinanceCodeBlock.FinanceDimension4 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension5Blank)
						OutputFinanceCodeBlock.FinanceDimension5 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension6Blank)
						OutputFinanceCodeBlock.FinanceDimension6 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension7Blank)
						OutputFinanceCodeBlock.FinanceDimension7 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension8Blank)
						OutputFinanceCodeBlock.FinanceDimension8 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension9Blank)
						OutputFinanceCodeBlock.FinanceDimension9 				= blank
					if (each.FinanceCodeBlockSetBlanks.SetFinanceDimension10Blank)
						OutputFinanceCodeBlock.FinanceDimension10 				= blank

			return true

