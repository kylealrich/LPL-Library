PriceBreak is a Field

    Representation
    	QuantityPercentPrice
        	occurs 5 times
        		
	Patterns
		implements CompactFormat
			delimiter is "-"

	Context Fields
		Contract
    	ContractLine
		
	Local Fields	
		LastQuantity    is Numeric size 13
		LastUnitPrice   is a UnitPrice
		LastUnitPercent is a UnitPercent
		HasUnitPercent  is Boolean
		HasUnitPrice  	is Boolean
		
	Field Rules	
		PriceBreak
			edit contiguous
				"PriceBreakEntriesMustBeContiguous"
			
			LastUnitPrice   = ContractLine.BaseCost
			LastUnitPercent = ContractLine.BasePercent
			HasUnitPercent  = false
			HasUnitPrice    = false
			for each PriceBreak.QuantityPercentPrice
				if (each.UnitPercent > 0)
					HasUnitPercent = true
				else
				if (each.UnitPrice > 0)
					HasUnitPrice = true
					
				if (HasUnitPercent or HasUnitPrice)
					constraint ((HasUnitPercent and !HasUnitPrice) 
							or (!HasUnitPercent and HasUnitPrice))
								"MustEnterAllPercentOrAllCostTypeBreaksForALineItem"
				
			for each PriceBreak.QuantityPercentPrice
				if (each.Quantity entered)
					constraint (each.Quantity > LastQuantity)
						"BreakQuantitiesMustBeGreaterThanTheLastQuantity"
				if (!Contract.LineLevelBreaks.OrderTotals)					
					if (each.UnitPrice entered)
						constraint (each.UnitPrice < LastUnitPrice)
							"EachHigherPriceBreakMustShowASmallerUnitPrice"
				if (each.UnitPercent entered)
					constraint (each.UnitPercent > LastUnitPercent)
						"EachHigherPriceBreakMustShowALargerUnitPercent"	
				LastQuantity    = each.Quantity
				LastUnitPrice   = each.UnitPrice
				LastUnitPercent = each.UnitPercent
    		
