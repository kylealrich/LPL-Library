CalendarQuery is a Field

	
    Representation
        Group Fields
        	QueryDate  	    is Date
        	QueryDateEnd 	is Date
        	NthDay     is Numeric size 6
        	Occurrence is Numeric size 1
        		States
	        		First	 value is 1
    	    		Last	 value is 2
        			Next	 value is 3
        			Previous value is 4
        	WorkingDay 		is Boolean
        	BankDay	   		is Boolean
        	GoodsReceiptDay	is Boolean
			DeliveryDay		is Boolean
        	Holiday   		is Boolean
        	TimeEntryDay    is Boolean
        	JobScheduleDay  is Boolean
        	TimePeriod 		is Numeric size 1
        		States
        			Any   value is 1
        			Year  value is 2
        			Month value is 3
        			Week  value is 4
        	DayOfWeek is Numeric size 1
        		States
        			Sunday 	  value is 1
        			Monday	  value is 2
        			Tuesday   value is 3
        			Wednesday value is 4
        			Thursday  value is 5
        			Friday	  value is 6
        			Saturday  value is 7
			        			
    Context Fields
    	EnterpriseGroup
    	SystemCalendar	
    	SystemCalendarPeriodGroup
    
    Local Fields
    	EmptyDate		is Date
    	StartDate	 	is a SystemCalendarDate
    	LimitDate		is Date
    	TempDate 	 	is a SystemCalendarDate
    	TempDays 		is Numeric size 6
    	DayCount 	   	is Numeric size 6
    	TempMonth 	   	is Numeric size 1 
    	DateFound      	is Boolean  	
    	TempDayNumber   is Numeric 8
    	
	Relations
		DateForWorkingDayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByWorkingDayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar 	= SystemCalendar
				related.WorkingDayNumber 	= TempDayNumber
				related.SystemCalendarDate >= TempDate
		
		DateForBankDayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByBankDayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar     = SystemCalendar
				related.BankDayNumber       = TempDayNumber
				related.SystemCalendarDate >= TempDate				

		DateForHolidayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByHolidayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar     = SystemCalendar
				related.HolidayNumber       = TempDayNumber
				related.SystemCalendarDate >= TempDate
								
		DateForGoodsRecDayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByGoodsRecDayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar     = SystemCalendar
				related.GoodsRecDayNumber   = TempDayNumber
				related.SystemCalendarDate >= TempDate				
				
		DateForDeliveryDayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByDeliveryDayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar     = SystemCalendar
				related.DeliveryDayNumber   = TempDayNumber				
				related.SystemCalendarDate >= TempDate
				
		DateForJobSchdDayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByJobSchdDayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar     = SystemCalendar
				related.JobSchdDayNumber    = TempDayNumber
				related.SystemCalendarDate >= TempDate
								
		DateForTimeEntryDayNumber
			one-to-many relation to SystemCalendarDate
			Field Mapping uses ByTimeEntryDayNumber
				related.EnterpriseGroup     = EnterpriseGroup
				related.SystemCalendar     = SystemCalendar
				related.TimeEntryDayNumber  = TempDayNumber
				related.SystemCalendarDate >= TempDate								
													   	
    Derived Fields
 
    	OutputDate is a DerivedField
    		type is Date
    		TempMonth    = 1
    		StartDate    = QueryDate
    		TempDays     = 0 
    		DayCount     = 0
    		DateFound    = false
    		if (TimePeriod not entered
    		or  Occurrence.Next
    		or  Occurrence.Previous)
    			TimePeriod = TimePeriod.Any
    		if (QueryDate  not entered
    		or  Occurrence not entered)
    			display "NoInputGiven"
    			return 0

    		if (TimePeriod.Year)
    			if (Occurrence.First)
    				StartDate -= (StartDate year day - 1)
    				LimitDate = (StartDate + StartDate days in year - StartDate year day)
    			else
    			if (Occurrence.Last)
    				StartDate += (StartDate days in year - StartDate year day)
    				LimitDate = (StartDate - StartDate year day + 1)
    		else
    		if (TimePeriod.Month)
    			if (Occurrence.First)
    				StartDate -= (StartDate day - 1)
    				LimitDate = (StartDate + StartDate days in month - StartDate day)
    			else
    			if (Occurrence.Last)
    				StartDate += (StartDate days in month - StartDate day)
    				LimitDate = (StartDate - StartDate day + 1)
    		else
    		if (TimePeriod.Week)
    			if (Occurrence.First)
    				StartDate -= (StartDate week day - 1)
    				LimitDate = (StartDate + 7 - StartDate week day)
    			else
    			if (Occurrence.Last)    			
    				StartDate += (7 - StartDate week day)
    				LimitDate = (StartDate - StartDate week day + 1)
			else
			if (!Occurrence.Previous
			and !DayOfWeek entered)
	    		if (WorkingDay)
	    			TempDayNumber = (StartDate.WorkingDayNumber + NthDay)
	    			StartDate = first DateForWorkingDayNumber.SystemCalendarDate
	    		else
	    		if (BankDay)
	    			TempDayNumber = (StartDate.BankDayNumber + NthDay)
	    			StartDate = first DateForBankDayNumber.SystemCalendarDate
	    		else
	    		if (GoodsReceiptDay)
	    			TempDayNumber = (StartDate.GoodsRecDayNumber + NthDay)
	    			StartDate = first DateForGoodsRecDayNumber.SystemCalendarDate
	    		else
	     		if (DeliveryDay)
	    			TempDayNumber = (StartDate.DeliveryDayNumber + NthDay)
	    			StartDate = first DateForDeliveryDayNumber.SystemCalendarDate
	    		else
	    		if (JobScheduleDay)
	    			TempDayNumber = (StartDate.JobSchdDayNumber + NthDay)
	    			StartDate = first DateForJobSchdDayNumber.SystemCalendarDate
	    		else    		
	    		if (TimeEntryDay)
	    			TempDayNumber = (StartDate.TimeEntryDayNumber + NthDay)
	    			StartDate = first DateForTimeEntryDayNumber.SystemCalendarDate 
	    		else
	    		if (Holiday)
	    			TempDayNumber = (StartDate.HolidayNumber + NthDay)
	    			StartDate = first DateForHolidayNumber.SystemCalendarDate
	    			    	  						    				
    		while (!DateFound)
				if (Occurrence.Last
				or  Occurrence.Previous)
					TempDate = (StartDate - TempDays)
				else
					TempDate = (StartDate + TempDays)
				if (TimePeriod.Week
				or	TimePeriod.Month
				or  TimePeriod.Year)
					if ((Occurrence.First
					and  TempDate > LimitDate)
					or  (Occurrence.Last
					and  TempDate < LimitDate))
						display "DateOutsideLimit<LimitDate>"
						return EmptyDate
						end while	  		
    			if (!TempDate exists)
					display "CouldNotFindDateOnCalendar"
					return EmptyDate
					end while  
									
    			DateFound = true
    			if (WorkingDay)
    				if (!TempDate.IsWorkingDay)
    					DateFound = false
				else
				if (BankDay)
					if (!TempDate.IsBankDay)
						DateFound = false
				else
				if (GoodsReceiptDay)
					if (!TempDate.IsGoodsReceiptDay)
						DateFound = false
				else
				if (DeliveryDay)
					if (!TempDate.IsDeliveryDay)
						DateFound = false										
				else
				if (Holiday)
					if (!TempDate.IsHoliday)
						DateFound = false
				else
				if (TimeEntryDay)
					if (!TempDate.TimeEntryAllowed)
						DateFound = false
				else
				if (JobScheduleDay)
					if (!TempDate.JobSchedulingAllowed)
						DateFound = false
						
				if (DayOfWeek entered
				and TempDate week day != DayOfWeek)
					DateFound = false
															
				if (Occurrence.Next)
					if ((DayOfWeek entered
					and TempDate = StartDate)
					or (DayOfWeek not entered
					and TempDate = (StartDate - NthDay)))
						DateFound = false
						
				if (DateFound)
					if (DayOfWeek entered
					or  Occurrence.Previous)
						if (NthDay entered)		
							DayCount +=1
							if (DayCount < NthDay)
								DateFound = false
							
				if (Occurrence.Previous
				and TempDate = StartDate)
					DateFound = false
													
				if (DateFound)
					return TempDate	    				
				else
					TempDays +=1
										
		OutputDays is a DerivedField
			type is Numeric 6
			if (QueryDate    not entered
			or  QueryDateEnd not entered)
				return 0
			if (QueryDate < QueryDateEnd)
				StartDate = QueryDate
				LimitDate = QueryDateEnd
			else
				StartDate = QueryDateEnd
				LimitDate = QueryDate
    		TempDate = (StartDate + 1)
    		DayCount = 0   		
			while (TempDate <= LimitDate)
    			DateFound = true
    			if (WorkingDay)
    				if (!TempDate.IsWorkingDay)
    					DateFound = false
				else
				if (BankDay)
					if (!TempDate.IsBankDay)
						DateFound = false
				else
				if (GoodsReceiptDay)
					if (!TempDate.IsGoodsReceiptDay)
						DateFound = false
				else
				if (DeliveryDay)
					if (!TempDate.IsDeliveryDay)
						DateFound = false										
				else
				if (Holiday)
					if (!TempDate.IsHoliday)
						DateFound = false
				else
				if (TimeEntryDay)
					if (!TempDate.TimeEntryAllowed)
						DateFound = false
				else
				if (JobScheduleDay)
					if (!TempDate.JobSchedulingAllowed)
						DateFound = false

				if (DayOfWeek entered
				and TempDate week day != DayOfWeek)
					DateFound = false
				if (DateFound)	
					DayCount +=1
				TempDate +=1
			return DayCount				
								
	Field Rules
		QueryDate
			default to current date
			
		QueryDateEnd
			default to QueryDate			
			
		Occurrence
			default to Occurrence.First
		
		TimePeriod
			default to TimePeriod.Any
			
