AssetTransferTo is a Field

    Representation
        Group Fields
			ToAsset							is an Asset
			ToAssetCompany					is an AssetCompany
			ToTagNumber						is a TagNumber
			ToAssetGroup					is an AssetGroup
			ToAssetObjectID					is an AssetObjectID
			ToAssetType						is an AssetType
			ToAssetAccountGroup				is an AssetAccountGroup
			ToAssetAccountingUnit         	is a FinanceCodeBlockNoAccountFull
            	default label is "ToDefaultTransactionDimensions"
			ToAssetAccountingUnitGroup		is an AssetAccountingUnitGroup
            	default label is "ToAssetDimensionGroup"
			ToAssetDivision					is an AssetDivision
			ToAssetLocation					is an AssetLocation
			ToAssetLocationDetail			is an AssetLocationDetail
			ToTotalItemQuantity				is a TotalItemQuantity
			ToItemTotalBaseCost        	 	is an InternationalAmount
			ToItemTotalBaseTax          	is an InternationalAmount
			ToItemTotalTransactionCost  	is an InternationalAmount
			ToItemTotalTransactionTax   	is an InternationalAmount
			ToCurrency						is a Currency
			ToLastAssetItemPurchaseDate 	is Date
			ToLastSequenceNumber        	is a Sequence
			ToAssetProject					is a FinanceCodeBlockProjectOnly
        	ToDepreciationExpenseProject	is a FinanceCodeBlockProjectOnly 
        	ToAssetOwner					is an Employee

	Context Fields
		Asset			

	Conditions
        HasAssetAU
        	restricted   
        	when (ToAssetAccountingUnitGroup.AssetAccountingUnit entered)

        HasAccumulatedDepreciationAU
        	restricted 
        	when (ToAssetAccountingUnitGroup.AccumulatedDepreciationAccountingUnit entered)
    	
        HasDepreciationExpenseAU
        	restricted
        	when (ToAssetAccountingUnitGroup.DepreciationExpenseAccountingUnit entered)

        HasClearingAU
        	restricted 
        	when (ToAssetAccountingUnitGroup.ClearingAccountingUnit entered)
             
        HasProceedsAU
        	restricted     
        	when (ToAssetAccountingUnitGroup.ProceedsAccountingUnit entered)

       	HasDisposalExpenseAU
       		restricted     
       	   	when (ToAssetAccountingUnitGroup.DisposalExpenseAccountingUnit entered)
 	       	   	
        HasGainAU
        	restricted     
          	when (ToAssetAccountingUnitGroup.GainAccountingUnit entered)
   	       
        HasLossAU
        	restricted   
        	when (ToAssetAccountingUnitGroup.LossAccountingUnit entered)

        HasDeferredGainAU
        	restricted     
        	when (ToAssetAccountingUnitGroup.DeferredGainAccountingUnit entered)

        HasDeferredLossAU
        	restricted  
        	when (ToAssetAccountingUnitGroup.DeferredLossAccountingUnit entered)
 
        HasRevalueSurplusAU
        	restricted  
			when ((ToAssetCompany.Revalue 
			and ToAssetAccountingUnitGroup.RevalueSurplusAccountingUnit entered)
			or !ToAssetCompany.Revalue)
        
        HasRevalueLossAU
        	restricted          
			when ((ToAssetCompany.Revalue 
			and ToAssetAccountingUnitGroup.RevalueLossAccountingUnit entered)
			or !ToAssetCompany.Revalue)

		TransferFieldsSame
			restricted
			when (Asset.Company 					= AssetTransferTo.ToAssetCompany
			and   Asset.TagNumber 					= AssetTransferTo.ToTagNumber
			and   Asset.AssetGroup 					= AssetTransferTo.ToAssetGroup
			and   Asset.AssetLocation 				= AssetTransferTo.ToAssetLocation
			and   Asset.AssetDivision 				= AssetTransferTo.ToAssetDivision
			and   Asset.AssetType 					= AssetTransferTo.ToAssetType
			and   Asset.AssetAccountGroup 			= AssetTransferTo.ToAssetAccountGroup
			and   Asset.AssetAccountingUnitGroup 	= AssetTransferTo.ToAssetAccountingUnitGroup
			and   Asset.AssetAccountingUnit 		= AssetTransferTo.ToAssetAccountingUnit
			and	  Asset.AssetOwner					= AssetTransferTo.ToAssetOwner)
		
		HasAllRequiredAccountingUnits
			restricted
			when (ToAssetAccountingUnit entered
			or (HasAccumulatedDepreciationAU 
        	and HasDepreciationExpenseAU
        	and HasClearingAU 
        	and HasProceedsAU     
        	and	HasDisposalExpenseAU     
       	   	and HasGainAU       
          	and HasLossAU   
        	and HasDeferredGainAU     
            and HasDeferredLossAU  
            and HasRevalueSurplusAU  
        	and HasRevalueLossAU))	
  
  		DimensionsHaveChanged
  			restricted 
  			when (ToAssetType changed
  			or	ToAssetAccountGroup changed
  			or 	ToAssetAccountingUnitGroup changed
			or ToAssetAccountingUnit changed)
		
	Relations
		FromAssetTypeRel
			one-to-one relation to AssetType
			Field Mapping uses symbolic key
				related.Company							= Asset.Company
				related.AssetType.Type					= Asset.AssetType.Type
				related.AssetType.SubType				= Asset.AssetType.SubType

		ToAssetTypeRel
			one-to-one relation to AssetType
			Field Mapping uses symbolic key
				related.Company							= ToAssetCompany
				related.AssetType.Type					= ToAssetType.Type
				related.AssetType.SubType				= ToAssetType.SubType	
															        	
	Field Rules
		ToTagNumber
			initial value is Asset.TagNumber

		ToAssetGroup
			initial value is Asset.AssetGroup

		ToAssetCompany
			initial value is Asset.Company
			required
				"ToCompanyIsRequired"
			if (Asset.AssetSummaryGroup entered
			and Asset.AssetSummaryGroup.MemberCount > 1)
				constraint (Asset.Company = ToAssetCompany)
					"IntercompanyTransferIsNotAllowedOnAssetsHavingASummaryGroupAssignedToThem;AllMembersOfASummaryGroupMustBeInTheSameCompany"	
								
		ToAssetType
			initial value is Asset.AssetType
			if (Asset.HasImpairment)
				constraint (FromAssetTypeRel.Impairment = ToAssetTypeRel.Impairment)
					"FromAssetTypeAndToAssetTypeMustHaveTheSameImpairmentOptionSelected"
			required
				"ToAssetTypeIsRequired"
				
		ToAssetAccountGroup
			initial value is Asset.AssetAccountGroup
			required
				"ToAssetAccountGroupIsRequired"
				
		ToAssetAccountingUnit
			initial value is Asset.AssetAccountingUnit

		ToAssetAccountingUnitGroup
			initial value is Asset.AssetAccountingUnitGroup
			if (ToAssetAccountingUnit not entered)
				required
					"DefaultAssetDimensionsOrDefaultDimensionGroupRequired"
					
				constraint (HasAllRequiredAccountingUnits)
					"RequiredProcessingDimensionAreNotDefinedInAssetDimensionGroup"	
			
		ToAssetDivision
			initial value is Asset.AssetDivision

		ToAssetLocation
			initial value is Asset.AssetLocation
			
		ToTotalItemQuantity
			initial value is Asset.TotalItemQuantity
			default to Asset.TotalItemQuantity

		ToAssetOwner
			initial value is Asset.AssetOwner

		ToAssetProject
			initial value is Asset.AssetProject

		ToCurrency
			initial value is Asset.Currency
			default to Asset.Currency
						
		ToDepreciationExpenseProject
			initial value is Asset.DepreciationExpenseProject
