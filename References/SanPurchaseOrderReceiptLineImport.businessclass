SanPurchaseOrderReceiptLineImport is a BusinessClass
    prefix is cfg

    Ontology
        symbolic key is SanPurchaseOrderReceiptLineImport

    Persistent Fields
        RunGroup
        Location is Alpha size 15
        EnteredReceivedQuantity is a Quantity
        ReceivedUOM is a UnitOfMeasure
        OriginalUnitCost is Decimal size 17.3
        CancelBackOrder is Boolean
        RecComments is Text
        ErrorMessage is Text

    Field Rules
        SanPurchaseOrderReceiptImport
            required

        Company
            required

        RunGroup
            required

    Relations
        PurchaseOrderLineRel
            one-to-one relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company = Company
                related.PurchaseOrder = SanPurchaseOrderReceiptImport.PurchaseOrder
                related.PurchaseOrderLine = SanPurchaseOrderReceiptLineImport.LineNumber

    Conditions
        IsReleased
            when (PurchaseOrderLineRel.Released)

    Actions
        BatchUpdatePurchaseOrderLine is a Set Action
            restricted
            Parameters
                PrmCompany is Alpha size 50
                PrmPurchaseOrder is Alpha size 50

            Parameter Rules
                PrmCompany
                    required
                PrmPurchaseOrder
                    required

            Instance Selection
                where (Company = PrmCompany
                and SanPurchaseOrderReceiptImport.PurchaseOrder = PrmPurchaseOrder)

            Action Rules
                Instance Rules
                    invoke UpdatePurchaseOrderLine
                    
        UpdatePurchaseOrderLine is an Instance Action
            restricted
            Action Rules
                if (PurchaseOrderLineRel not exists)
                    ErrorMessage = "Purchase Order Line does not exist."

                if (PurchaseOrderLineRel exists and !IsReleased)
                    ErrorMessage = "Purchase Order Line is not in released status."

                if (PurchaseOrderLineRel exists and IsReleased)
                    invoke Released.Update PurchaseOrderLineRel
                        resume on error
                            ErrorMessage = error message
                        invoked.Quantity = EnteredReceivedQuantity
                        if (PurchaseOrderLineRel.EnteredBuyUOM != ReceivedUOM)
                            invoked.EnteredBuyUOM = ReceivedUOM
                        if (OriginalUnitCost > 0)
                            invoked.VendorBuyUnitCost = OriginalUnitCost

        Create is a Create Action

        Update is an Update Action

        Delete is a Purge Action