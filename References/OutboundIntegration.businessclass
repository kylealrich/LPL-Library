OutboundIntegration is a BusinessClass
    prefix is cfg

    Ontology
        symbolic key is OutboundIntegration

    Persistent Fields
        IntegrationName is Alpha size 100
        Description
		UseDefaultProcess is Boolean
		Process is a PfiFlowDefinition
        OutputDirectory is Alpha size 200
			default label is untranslatable:"Output File Location"
		DeleteSourceFile is Boolean
		Filename is Alpha 150
			default label is untranslatable:"File Name"
			Text Variables
				FileTimeStamp
		Archive is Boolean
		ArchiveDirectory is Alpha size 200
			default label is untranslatable:"Archive File Location"
		ArchiveFilename is Alpha size 200
		Encrypt is Boolean
		EncryptionServiceName is a Service
        Delimiter is Alpha size 1
            States
                Comma value is ","
                Pipe value is "|"
                Tilde value is "~"
        Header is Boolean
        HeaderValue is Text
        Solution is Numeric size 1
            States
                DBExport value is 1
                DataLake value is 2
		DbBusinessClass is a BusinessClass
			default label is untranslatable:"Business Class"
		DbFields is Text
			default label is untranslatable:"Fields"
		DbFilter is Text
			default label is untranslatable:"LPL Filter"
		DbLimit is Numeric size 10
			default label is untranslatable:"Limit"
		DbShowAdvanceControls is Boolean
			default label is untranslatable:"Show Advance Controls"
		DbPaging is Alpha size 10
			default label is untranslatable:"Paging"
			States
				Next value is NEXT
				Previous value is PREVIOUS
		DbLinks is Boolean
			default label is untranslatable:"Links"
		DbPageNav is Boolean
			default label is untranslatable:"Page Nav"
		DbOutputFormat is Alpha size 5
			default label is untranslatable:"Output Format"
			States
				XML value is XML
				CSV value is CSV
				JSON value is JSON
		DbFlatten is Boolean
			default label is untranslatable:"Flatten"
		DlSQLQuery is Text
			default label is untranslatable:"SQL Query"
		RemoveQuotes is Boolean
			default label is untranslatable:"Remove Quotes"
        FailureEmail is Boolean
        FailureEmailTo is an EmailAddressMulti
		FailureEmailCc is an EmailAddressMulti
        FailureEmailFrom is an EmailAddress
        FailureEmailSubject is Alpha size 200
        FailureEmailBody is Text
        SuccessEmail is Boolean
        SuccessEmailTo is an EmailAddressMulti
		SuccessEmailCc is an EmailAddressMulti
        SuccessEmailFrom is an EmailAddress
        SuccessEmailSubject is Alpha size 200
        SuccessEmailBody is Text
		AddFooter is Boolean
		FooterEmailBody is Text
		Status is Numeric size 1
			default label is untranslatable:"Last Run Status"
			States
				Ready value is 0
				ReplicateInProgress value is 1
				GeneratingOutputFile value is 2
				Completed value is 3
				Error value is 4
				Cancelled value is 5
		FileNameTimeStampFormat is Alpha 40
			default label is untranslatable:"Time Stamp Format (Java)"
		AppendTimestampToFileName is Boolean
		BuildTimeStampFormat is Boolean
		DateFormat is Numeric 1
			States
				YYMMDD value is 0
					default label is untranslatable
				MMDDYY value is 1
					default label is untranslatable
				DDMMYY value is 2
					default label is untranslatable
		IncludeCentury is Boolean	
		IncludeHours is Boolean
		IncludeMinutes is Boolean
		IncludeSeconds is Boolean
		IncludeMilliseconds is Boolean
		Replicate is Boolean
			default label is untranslatable:"Auto-Replicate"
		ReplicationSet
		PfiWorkunit
			default label is untranslatable:"Last Work Unit"
		TimeZone

	Local Fields
		LocalAsyncRequest is a AsyncActionRequest
		LocalJavaFormat is Alpha 40
		CurrentSystemTimeStamp is TimeStamp
		LocalAlpha is Alpha 100
		TempFileName is Text
			Text Variables
				FileTimeStamp value is LocalAlpha
		LocalTimeFormat is Alpha 40
		I1 is Numeric 4
		LocalReplicationSet is a ReplicationSet
		LocalIntegrationName is Alpha size 100

	Conditions
		GreenAlert
			when (Status = 0 or Status = 3)

		YellowAlert
			when (Status = 1 or Status = 2 or Status = 5)

		RedAlert
			when (Status = 4)

	Field Rules
		OutboundIntegration
			autosequence
				minimize contention
				
		IntegrationName
			required

		UseDefaultProcess
			initial value is true

		Process
			if (!UseDefaultProcess)
				required

		OutputDirectory
			required
			
		Filename
			required

		Delimiter
			required
			initial value is Delimiter.Comma
			default to Delimiter.Comma

		Solution
			if (UseDefaultProcess)
				required

		DbBusinessClass
			if (Solution.DBExport)
				required
			
		DbFields
			if (Solution.DBExport)
				required
			initial value is "_all"
			default to "_all"

		DbLimit
			initial value is 5

		DbPaging
			initial value is "NEXT"
			default to "NEXT"

		DbLinks
			initial value is false
			default to false

		DbPageNav
			initial value is true
			default to true

		DbOutputFormat
			initial value is "CSV"
			default to "CSV"

		DbFlatten
			initial value is false
			default to false
			
		RemoveQuotes
			initial value is true

		FailureEmailFrom
			initial value is EmailFromRel.Value
			default to EmailFromRel.Value

		SuccessEmailFrom
			initial value is EmailFromRel.Value
			default to EmailFromRel.Value
			
		AddFooter
			initial value is true

		Status
			initial value is 0
			default to 0
			
		ReplicationSet
			if (Replicate)
				required

		Header
			initial value is true

		DlSQLQuery
			initial value is "SELECT\u000D\u000AFROM\u000D\u000AWHERE"

		FailureEmailSubject
			initial value is "<prefix> [Failed] – <integrationName> to <customer> – Process Date: <processDate>"

		SuccessEmailSubject
			initial value is "<prefix> [Success] – <integrationName> to <customer> – Process Date: <processDate>"

		FailureEmailBody
			initial value is "<p>Data extract <integrationName> encountered an error while processing.</p>"

		SuccessEmailBody
			initial value is "<p>Data extract <integrationName> has completed successfully.</p><head><style>table.header, table.header th, table.header td{padding: 5px;border: 1px solid black;border-collapse: collapse;}table.header th{color: white;background-color: #0000FF;}table.header tr:nth-child(even){background-color: #F0FFFF;}</style></head><body><table class=\"header\"; style=\"width:100%\"><tr><td>Output File name</td><td><filename></td></tr><tr><td>File Date</td><td><processDate></td></tr><tr><td>File Location</td><td><outputDirectory></td></tr></table></body>"

		FooterEmailBody
			initial value is "<table width=\"100%\" style=\"font-family:Calibri;font-size:10pt;\"><td><hr style=\"color:blue\"/>The following information is for Infor Support reference purposes only:</td></table><table style=\"font-family:Calibri;font-size:10pt;\"><tr><th align=\"left\">Environment:</th><td><appProdline></td></tr><tr><th align=\"left\">Process Name:</th><td><ProcessName></td></tr><th align=\"left\">Workunit/Reference:</th><td><WorkUnit></td></tr></table>"
			
		TimeZone
			initial value is "UTC"
			default to "UTC"

	Relations
		EmailFromRel
			one-to-one relation to PfiConfigurationProperty
    		Field Mapping uses symbolic key
        		related.PfiConfiguration = "system"
        		related.PfiConfigurationProperty = "mailFrom"

		PfiFlowVersionRel
			one-to-many relation to PfiFlowVersion
			Field Mapping uses symbolic key
				related.PfiFlowDefinition = Process

		ReplicationSetRel
			one-to-one relation to ReplicationSet
			Field Mapping uses symbolic key
				related.ReplicationSet = ReplicationSet

		ReplicationSetInProcessRel
			one-to-many relation to ReplicationSet
			Field Mapping uses symbolic key
				related.ReplicationSet = ReplicationSet
			Instance Selection
				where (related.InProcess = true)

		PfiWorkunitRel
			one-to-one relation to PfiWorkunit
			Field Mapping uses symbolic key
				related.PfiWorkunit = PfiWorkunit
				
		LocalReplicationSetRel
			one-to-many relation to ReplicationSet
			Field Mapping uses symbolic key
				related.ReplicationSet = LocalReplicationSet
			Instance Selection
				where (related.AnyDelivered  = true)

		LocalIntegrationNameRel
			one-to-many relation to OutboundIntegration
			Field Mapping uses ByIntegrationName
				related.IntegrationName = LocalIntegrationName

    Derived Fields
		DerivedLastPfiFlowVersion is a DerivedField
			type is Numeric size 5
			return last PfiFlowVersionRel.PfiFlowVersion

		CalculatedFileName is a DerivedField
			type is Text
			default label is "Sample File Name"
			if (FileNameTimeStampFormat entered)
				LocalJavaFormat = FileNameTimeStampFormat
			else
				LocalJavaFormat = "%1$tY%1$tm%1$td%1$tH%1$tM%1$tS%1$tL"
			CurrentSystemTimeStamp = system current timestamp
			LocalAlpha = CurrentSystemTimeStamp using "<LocalJavaFormat>"		
			if (AppendTimestampToFileName)
				return FileNameOnly + "_" + LocalAlpha + FileNameExtention
			else
				TempFileName = Filename
				return TempFileName text

		CalculateTimeStampFormat is a DerivedField
			type is Alpha 40
			restricted

			initialize LocalJavaFormat

			if (IncludeCentury)
				if (DateFormat.YYMMDD)
					LocalJavaFormat = "%1$tY%1$tm%1$td"
				else
				if (DateFormat.MMDDYY)
					LocalJavaFormat = "%1$tm%1$td%1$tY"
				else
				if (DateFormat.DDMMYY)
					LocalJavaFormat = "%1$td%1$tm%1$tY"
			else
				if (DateFormat.YYMMDD)
					LocalJavaFormat = "%1$ty%1$tm%1$td"
				else
				if (DateFormat.MMDDYY)
					LocalJavaFormat = "%1$tm%1$td%1$ty"
				else
				if (DateFormat.DDMMYY)
					LocalJavaFormat = "%1$td%1$tm%1$ty"

			initialize LocalTimeFormat
			if (IncludeHours)
				LocalTimeFormat = "%1$tH"
			if (IncludeMinutes)
				LocalTimeFormat += "%1$tM"
			if (IncludeSeconds)
				LocalTimeFormat += "%1$tS"
			if (IncludeMilliseconds)
				LocalTimeFormat += "%1$tL"
					
			LocalJavaFormat += LocalTimeFormat
			return LocalJavaFormat

		FileNameOnly is a DerivedField
			type is Alpha 150
			restricted
			initialize I1
			while (I1 <= 70)
				if (Filename[I1] = ".")
					return Filename[1:I1-1]
				if (Filename[I1] = " ")
					return Filename
				increment I1
			return Filename
			
		FileNameExtention is a DerivedField
			type is Alpha 10
			restricted
			initialize I1
			while (I1 <= 50)
				if (Filename[I1] = ".")
					return Filename[I1:50]
				increment I1
			return blank

		DerivedLastRunStartDate is a DerivedField
			type is Alpha 8
			return PfiWorkunit.ExecutionStartDate

		DerivedLastRunStartTimeStamp is a DerivedField
			type is TimeStamp
			return PfiWorkunit.ExecutionStartDate

		DerivedLastRunClosedDate is a DerivedField
			type is Alpha 8
			return PfiWorkunit.CloseDate

		DerivedLastRunClosedTimeStamp is a DerivedField
			type is TimeStamp
			return PfiWorkunit.CloseDate

	Sets
		ByIntegrationName
			indexed
			Sort Order
				IntegrationName

    Actions
        CreateNew is a Create Action
            Parameters
				IntegrationName is Alpha size 50
				Description
				UseDefaultProcess is Boolean
				Process is a PfiFlowDefinition
				OutputDirectory is Alpha size 200
					default label is untranslatable:"Output File Location"
				DeleteSourceFile is Boolean
				Filename is Alpha 150
					default label is untranslatable:"File Name"
					Text Variables
						FileTimeStamp
				Archive is Boolean
				ArchiveDirectory is Alpha size 200
					default label is untranslatable:"Archive File Location"
				ArchiveFilename is Alpha size 200
				Encrypt is Boolean
				EncryptionServiceName is a Service
				Delimiter is Alpha size 1
					States
						Comma value is ","
						Pipe value is "|"
						Tilde value is "~"
				Header is Boolean
				HeaderValue is Text
				Solution is Numeric size 1
					States
						DBExport value is 1
						DataLake value is 2
				DbBusinessClass is a BusinessClass
					default label is untranslatable:"Business Class"
				DbFields is Text
					default label is untranslatable:"Fields"
				DbFilter is Text
					default label is untranslatable:"LPL Filter"
				DbLimit is Numeric size 10
					default label is untranslatable:"Limit"
				DbShowAdvanceControls is Boolean
					default label is untranslatable:"Show Advance Controls"
				DbPaging is Alpha size 10
					default label is untranslatable:"Paging"
					States
						Next value is NEXT
						Previous value is PREVIOUS
				DbLinks is Boolean
					default label is untranslatable:"Links"
				DbPageNav is Boolean
					default label is untranslatable:"Page Nav"
				DbOutputFormat is Alpha size 5
					default label is untranslatable:"Output Format"
					States
						XML value is XML
						CSV value is CSV
						JSON value is JSON
				DbFlatten is Boolean
					default label is untranslatable:"Flatten"
				DlSQLQuery is Text
					default label is untranslatable:"SQL Query"
				RemoveQuotes is Boolean
					default label is untranslatable:"Remove Quotes"
				FailureEmail is Boolean
				FailureEmailTo is an EmailAddressMulti
				FailureEmailCc is an EmailAddressMulti
				FailureEmailFrom is an EmailAddress
				FailureEmailSubject is Alpha size 200
				FailureEmailBody is Text
				SuccessEmail is Boolean
				SuccessEmailTo is an EmailAddressMulti
				SuccessEmailCc is an EmailAddressMulti
				SuccessEmailFrom is an EmailAddress
				SuccessEmailSubject is Alpha size 200
				SuccessEmailBody is Text
				AddFooter is Boolean
				FooterEmailBody is Text
				FileNameTimeStampFormat is Alpha 40
					default label is "TimeStampFormat"
				AppendTimestampToFileName is Boolean
				BuildTimeStampFormat is Boolean
				DateFormat is Numeric 1
					States
						YYMMDD value is 0
							default label is untranslatable
						MMDDYY value is 1
							default label is untranslatable
						DDMMYY value is 2
							default label is untranslatable
				IncludeCentury is Boolean	
				IncludeHours is Boolean
				IncludeMinutes is Boolean
				IncludeSeconds is Boolean
				IncludeMilliseconds is Boolean
				Replicate is Boolean
					default label is untranslatable:"Auto-Replicate"
				ReplicationSet
				TimeZone

			Parameter Rules
				IntegrationName
					required

				UseDefaultProcess
					initial value is true

				Process
					if (!UseDefaultProcess)
						required

				OutputDirectory
					required
					
				Filename
					required

				Solution
					if (UseDefaultProcess)
						required

				Delimiter
					required
					initial value is Delimiter.Comma
					default to Delimiter.Comma

				Header
					initial value is true

				DbFields
					initial value is "_all"

				DbLimit
					initial value is 5

				DbPaging
					initial value is "NEXT"
					default to "NEXT"

				DbLinks
					initial value is false
					default to false

				DbPageNav
					initial value is true
					default to true

				DbOutputFormat
					initial value is "CSV"
					default to "CSV"

				DbFlatten
					initial value is false
					default to false
			
				RemoveQuotes
					initial value is true

				DlSQLQuery
					initial value is "SELECT\u000D\u000AFROM\u000D\u000AWHERE"

				FailureEmailFrom
					initial value is EmailFromRel.Value
					default to EmailFromRel.Value

				SuccessEmailFrom
					initial value is EmailFromRel.Value
					default to EmailFromRel.Value
					
				AddFooter
					initial value is true
					
				ReplicationSet
					if (Replicate)
						required

				FailureEmailSubject
					initial value is "<prefix> [Failed] – <integrationName> to <customer> – Process Date: <processDate>"

				SuccessEmailSubject
					initial value is "<prefix> [Success] – <integrationName> to <customer> – Process Date: <processDate>"

				FailureEmailBody
					initial value is "<p>Data extract <integrationName> encountered an error while processing.</p>"

				SuccessEmailBody
					initial value is "<p>Data extract <integrationName> has completed successfully.</p><head><style>table.header, table.header th, table.header td{padding: 5px;border: 1px solid black;border-collapse: collapse;}table.header th{color: white;background-color: #0000FF;}table.header tr:nth-child(even){background-color: #F0FFFF;}</style></head><body><table class=\"header\"; style=\"width:100%\"><tr><td>Output File name</td><td><filename></td></tr><tr><td>File Date</td><td><processDate></td></tr><tr><td>File Location</td><td><outputDirectory></td></tr></table></body>"

				FooterEmailBody
					initial value is "<table width=\"100%\" style=\"font-family:Calibri;font-size:10pt;\"><td><hr style=\"color:blue\"/>The following information is for Infor Support reference purposes only:</td></table><table style=\"font-family:Calibri;font-size:10pt;\"><tr><th align=\"left\">Environment:</th><td><appProdline></td></tr><tr><th align=\"left\">Process Name:</th><td><ProcessName></td></tr><th align=\"left\">Workunit/Reference:</th><td><WorkUnit></td></tr></table>"
					
				TimeZone
					initial value is "UTC"
					default to "UTC"

            Action Rules
                invoke Create this instance
					invoked.IntegrationName = IntegrationName
					invoked.Description = Description
					invoked.UseDefaultProcess = UseDefaultProcess
					invoked.Process = Process
					invoked.OutputDirectory = OutputDirectory
					invoked.DeleteSourceFile = DeleteSourceFile
					invoked.Filename = Filename
					invoked.Archive = Archive
					invoked.ArchiveDirectory = ArchiveDirectory
					invoked.ArchiveFilename = ArchiveFilename
					invoked.Encrypt = Encrypt
					invoked.EncryptionServiceName = EncryptionServiceName
					invoked.Delimiter = Delimiter
					invoked.Header = Header
					invoked.HeaderValue = HeaderValue
					invoked.Solution = Solution
					invoked.DbBusinessClass = DbBusinessClass
					invoked.DbFields = DbFields
					invoked.DbFilter = DbFilter
					invoked.DbLimit = DbLimit
					invoked.DbShowAdvanceControls = DbShowAdvanceControls
					invoked.DbPaging = DbPaging
					invoked.DbLinks = DbLinks
					invoked.DbPageNav = DbPageNav
					invoked.DbOutputFormat = DbOutputFormat
					invoked.DbFlatten = DbFlatten
					invoked.DlSQLQuery = DlSQLQuery
					invoked.RemoveQuotes = RemoveQuotes
					invoked.FailureEmail = FailureEmail
					invoked.FailureEmailTo = FailureEmailTo
					invoked.FailureEmailCc = FailureEmailCc
					invoked.FailureEmailFrom = FailureEmailFrom
					invoked.FailureEmailSubject = FailureEmailSubject
					invoked.FailureEmailBody = FailureEmailBody
					invoked.SuccessEmail = SuccessEmail
					invoked.SuccessEmailTo = SuccessEmailTo
					invoked.SuccessEmailCc = SuccessEmailCc
					invoked.SuccessEmailFrom = SuccessEmailFrom
					invoked.SuccessEmailSubject = SuccessEmailSubject
					invoked.SuccessEmailBody = SuccessEmailBody
					invoked.AddFooter = AddFooter
					invoked.FooterEmailBody = FooterEmailBody
					invoked.FileNameTimeStampFormat = FileNameTimeStampFormat
					invoked.AppendTimestampToFileName = AppendTimestampToFileName
					invoked.BuildTimeStampFormat = BuildTimeStampFormat
					invoked.DateFormat = DateFormat
					invoked.IncludeCentury = IncludeCentury
					invoked.IncludeHours = IncludeHours
					invoked.IncludeMinutes = IncludeMinutes
					invoked.IncludeSeconds = IncludeSeconds
					invoked.IncludeMilliseconds = IncludeMilliseconds
					invoked.Replicate = Replicate
					invoked.ReplicationSet = ReplicationSet
					invoked.TimeZone = TimeZone

			Exit Rules
				LocalReplicationSet = ReplicationSet
				if (Replicate)
					confirmation required
						untranslatable:"Auto Replication is for custom Replication Sets that are incremental and used by a single process."

				constraint (LocalReplicationSetRel not exists)
					untranslatable:"Standard Replication Sets cannot be selected to auto-replicate."

		CopyFromIntegration is a Create Action
			Parameters
				PrmOutboundIntegration is an OutboundIntegration
					default label is untranslatable:"Outbound Integration
				PrmIntegrationName is Alpha size 50
					default label is untranslatable:"Integration Name"
				PrmDescription is a Description
					default label is untranslatable:"Description"

			Parameter Rules
				PrmOutboundIntegration
					required
					constraint (PrmOutboundIntegration exists)
						"CopyFromOutboundIntegration<PrmOutboundIntegration>DoesNotExist"
						
				PrmIntegrationName
					required

			Action Rules
				invoke Create this instance
					fill in fields from PrmOutboundIntegration
						except invoked.IntegrationName
						except invoked.Description
						except invoked.PfiWorkunit
						except invoked.Status
					invoked.IntegrationName = PrmIntegrationName
					invoked.Description = PrmDescription

			Exit Rules
				LocalIntegrationName = PrmIntegrationName
				constraint (LocalIntegrationNameRel not exists)
					untranslatable:"Integration Name already exists."

		Trigger is an Instance Action
			valid when (!YellowAlert)
			Action Rules
				if (Solution.DataLake and Replicate)
					if (ReplicationSetInProcessRel not exists)
						Status = 1
						invoke Replicate ReplicationSetRel

					if (ReplicationSetInProcessRel exists)
						Status = 1
					
				else
					invoke RunProcessFlow

			Exit Rules
				confirmation required
					untranslatable:"<IntegrationName> is about to run. Proceed?"

		TriggerSet is a Set Action
			default label is untranslatable:"Schedule Trigger"
			Parameters
				PrmOutboundIntegration is a OutboundIntegration
					default label is untranslatable:"Integration Number"

			Parameter Rules
				PrmOutboundIntegration
					required

			Instance Selection
				where (OutboundIntegration = PrmOutboundIntegration)

			Action Rules
				Instance Rules
					invoke Trigger OutboundIntegration

		AdminTriggerSet is a Set Action
			restricted
			Parameters
				PrmOutboundIntegration is a OutboundIntegration
					default label is untranslatable:"Integration Number"

			Parameter Rules
				PrmOutboundIntegration
					required

			Instance Selection
				where (OutboundIntegration = PrmOutboundIntegration)

			Action Rules
				Instance Rules
					if (Solution.DataLake and Replicate)
						if (ReplicationSetInProcessRel not exists)
							Status = 1
							invoke Replicate ReplicationSetRel

						if (ReplicationSetInProcessRel exists)
							Status = 1
						
					else
						invoke RunProcessFlow

		RunProcessFlowDataLakeInProgress is a Set Action
			restricted
			Instance Selection
				where (Status = 1)

			Action Rules
				Instance Rules
					invoke RunProcessFlow

		RunProcessFlowSet is a Set Action
			restricted
			Parameters
				PrmOutboundIntegration is a OutboundIntegration

			Instance Selection
				where (OutboundIntegration = PrmOutboundIntegration)

			Action Rules
				Instance Rules
					invoke RunProcessFlow
							
		RunProcessFlow is an Instance Action
			restricted
			Action Rules
				Status = 2
				initiate OutboundIntegrationService process
					title is untranslatable:"Outbound Integration: <IntegrationName>"
					Variables
						OutboundIntegration
						IntegrationName
						Description
						UseDefaultProcess
						Process
						DerivedLastPfiFlowVersion
						OutputDirectory
						DeleteSourceFile
						CalculatedFileName
						AppendTimestampToFileName
						Archive
						ArchiveDirectory
						ArchiveFilename
						Encrypt
						EncryptionServiceName
						Delimiter
						Header
						HeaderValue
						Solution
						DbBusinessClass
						DbFields
						DbFilter
						DbLimit
						DbShowAdvanceControls
						DbPaging
						DbLinks
						DbPageNav
						DbOutputFormat
						DbFlatten
						DlSQLQuery
						RemoveQuotes
						FailureEmail
						FailureEmailTo
						FailureEmailCc
						FailureEmailFrom
						FailureEmailSubject
						FailureEmailBody
						SuccessEmail
						SuccessEmailTo
						SuccessEmailCc
						SuccessEmailFrom
						SuccessEmailSubject
						SuccessEmailBody
						AddFooter
						FooterEmailBody
						TimeZone
						DateFormat
						IncludeCentury
						IncludeHours
						IncludeMinutes
						IncludeSeconds
						IncludeMilliseconds

		CancelWorkUnit is an Instance Action
			valid when (PfiWorkunit.Status = 9
			or PfiWorkunit.Status = 1
			or PfiWorkunit.Status = 2
			or PfiWorkunit.Status = 10)
			Action Rules
				invoke Cancel PfiWorkunitRel
				Status = 5

		Reset is an Instance Action
			Action Rules
				Status = 0

        Create is a Create Action
            restricted
			Exit Rules
				FileNameTimeStampFormat = CalculateTimeStampFormat

        Update is an Update Action
			Exit Rules
				if (Replicate and (Replicate changed or ReplicationSet changed))
					confirmation required
						untranslatable:"Auto Replication is for Replication Sets that are incremental and used by a single process."

        Delete is a Purge Action