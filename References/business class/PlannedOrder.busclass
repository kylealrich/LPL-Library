PlannedOrder is a BusinessClass
    owned by ic
    prefix is IPL
    classic name is ICPLANORD

    Ontology
        symbolic key is PlannedOrder
            classic set name is IPLSET1
            classic name for PlannedOrderPeriodDate is PERIOD-DT
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        PlannedReceiveDate       is Date
            classic name is PLAN-RECV-DT
        PlannedShipDate          is Date
            classic name is PLAN-SHIP-DT
        Vendor
        FromLocation             is an InventoryLocation
            classic name is FR-LOCATION
        CreatedDate              is TimeStamp
            classic name is CREATED-DT
        Buyer
            classic name is BUYER-CODE
        FirmDemand               is Decimal size 13.4
        RemainingForecast        is Decimal size 13.4
            classic name is REMAIN-FORCST
        ScheduledReceipt         is Decimal size 13.4
            classic name is SCHED-RECEIPT
        ProjectedAvailable       is Decimal size 13.4
            classic name is PROJ-AVAIL
 		PlannedOrderQuantity     is a Quantity
            classic name is PLAN-ORDER
        FirmPlannedOrderQuantity is a Quantity      
            classic name is FIRM-PLAN-ORD

	
    Conditions

        HasPlannedShipDate
            classic name is IPLSET2
            restricted
            when (FromLocation entered
            and   PlannedShipDate >  blank)

        HasPlannedReceiveDate
            classic name is IPLSET3
            restricted
            when (PlannedReceiveDate >  blank)

    Relations

        ItemLocationRel
            classic name is ITEMLOC
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = InventoryLocation
                related.Item              = Item

    Sets

        Set2
            indexed
            Instance Selection
                where (HasPlannedShipDate)
            Sort Order
                Company
                FromLocation
                Item
                PlannedShipDate
                InventoryLocation

        Set3
            indexed
            Instance Selection
                where (HasPlannedReceiveDate)
            Sort Order
                Company
                InventoryLocation
                Item
                PlannedReceiveDate


    Field Rules

        PlannedOrderQuantity
            required

        PlannedShipDate
            required
        		"PlannedShipDateShouldBeEntered"     
        		       
        PlannedReceiveDate 
        	required
        		"PlannedReceivedDateShouldBeEntered"
            constraint(PlannedReceiveDate > PlannedShipDate)
        		"PlannedReceivedDateCannotBeBeforePlannedShipDate"        		
        		
		Item
			constraint (ItemLocationRel exists)
				"Item<Item>DoesNotExistsAtLocation<InventoryLocation>"


    Actions
    			
        Create is a Create Action
        	restricted
        	
        Update is an Update Action

        Delete is a Delete Action
        	restricted	
