ProjectContractComment is a BusinessClass
	owned by Projects
	prefix is Prcc
	
	Ontology
		symbolic key is ProjectContractComment
		
	
	Persistent Fields
		Subject	 			is a CommentName
        	translatable		
    	Comment				is Text
		From				is Alpha size 230
        	translatable		
			default label is "CommentMadeBy"
		CommentDate			is TimeStamp
    		default label is "Date"
#ifdef module idm
    	Attachment				is an AlternateAttachment
#endif
#ifndef module idm
		Attachment
#endif
		PrintOnInvoice      is Boolean  
    	DocumentURL			is URL
    	SendToCustomer		is Boolean	
    	
    Rule Blocks
    	AttachmentsSizeTotal
    		if (Attachment entered)
    			LocalAttachmentSizeForProjectContract = DerivedCurrentAttachmentSizeForProjectContract + Attachment.FileSizeBytes
    Derived Fields
    	DerivedCurrentAttachmentSizeForProjectContract	is a DerivedField
    		type is Numeric size 11
    		restricted
    		return (sum ProjectContractCommentAttachmentsRel.Attachment.FileSizeBytes)
    	
    	DerivedCommentDate is a DerivedField
            type is Date
            return (CommentDate date)
    
    Local Fields
    	LocalAttachmentSizeForProjectContract	is Numeric size 11
	    LocalAttributeCtr   is Numeric 2	
		
	Field Rules
		Subject
    		required
    			"SubjectIsRequired"
    				
    	Comment 
    		required
    			"CommentIsRequired"
    	
    	From
			initial value is actor
			required
    	
    	CommentDate
    		default to current timestamp
    		
    	SendToCustomer
    		constraint (LocalAttachmentSizeForProjectContract <= 10485760) 
    			"10MBMaxAttachmentLimitExceeded.CannotSendToCustomer."
		
	Conditions
    	HasAttachment
			restricted    	
    		when (Attachment entered)
    	
    	HasComment
			restricted    	
    		when (CommentDate entered)
    		
		HasURL
			restricted
			when (DocumentURL entered)
    	    		
	Relations
    	ProjectContractCommentAttachmentsRel
			one-to-many relation to ProjectContractComment
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectContract			= ProjectContract
			Instance Selection
				where (related.Attachment entered
				and	   related.SendToCustomer)
   	
	Sets
		ByCommentDate
			Sort Order
				FinanceEnterpriseGroup
				ProjectContract
				CommentDate
				ProjectContractComment
					
#ifdef module idm		
	Create Rules   
		include IDM.CreateRules 
			replace AttachmentField with Attachment	
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment
	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment			

#endif				

	Actions
		Create is a Create Action
			Action Rules
				include AttachmentsSizeTotal
				
		Update is an Update Action
			Action Rules
				include AttachmentsSizeTotal
		
		Delete is a Delete Action	

#ifdef module idm
		UploadToIDM is an Instance Action
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)
			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
						
#endif
