RecurringInvoiceLine is a BusinessClass
    owned by bl
    prefix is BML
    classic name is BLRIMLINE

    Ontology
        symbolic key is RecurringInvoiceLine
            classic set name is BMLSET1
            classic name is LINE-NBR
            classic name for RecurringInvoiceParameter is RECUR-INVOICE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Item
        Quantity
        SellingUnitOfMeasure       is a UnitOfMeasure
            classic name is SELL-UOM
        UnitPrice				   is an UnsignedUnitCost                  
    		precision is DerivedNumberOfDecimalsPrice        
        SellingPriceUnitOfMeasure  is a UnitOfMeasure
            classic name is SPR-UOM
        ZeroAfterUse			   is Boolean
        StartDate
        StopDate                   is Date
        Territory
        SalesRepresentative
            classic name is SALESMAN
		SalesRepresentative2 	   is a SalesRepresentative
		CommissionRate1            is a ChrgRate
            classic name is COMM-RATE-1
		CommissionRate2            is a ChrgRate
            classic name is COMM-RATE-2
		CommissionSplitPercentage
            classic name is COMM-SPLIT
        StandardCommentCode1       is a BillingStandardCommentHeader 
            classic name is STD-COMM1
        StandardCommentCode2       is a BillingStandardCommentHeader 
            classic name is STD-COMM2
        StandardCommentCode3       is a BillingStandardCommentHeader 
            classic name is STD-COMM3
        LastAddOnChargeSequence    is a Sequence3
            classic name is LAST-MISC-SEQ
        LastCommentSequence        is a Sequence3
            classic name is LAST-COMM-SEQ
		HSNSACCode
        TaxCode
        TaxExemptCode
            classic name is TAX-EXEMPT-CD
        Project
            classic name is ACTIVITY
        ItemDescription            is a Description
            classic name is ITEM-DESC
        LineType		           is a BillingLineType				

	Derived Fields
		DerivedRecurringInvoiceLine is a DerivedField
			type is like RecurringInvoiceLine
			return RecurringInvoiceLine

		DerivedNumberOfDecimalsPrice is a ConditionalField
			type is Numeric size 1
			restricted
			if (LineType.NonStock
			or  LineType.Inventoried)
				Item.NumberOfDecimalsPrice
			else
			if (LineType.Special)
				InventoryCompanyRel.NumberOfDecimalsCost
			else
				8
				
		DerivedFormTitleForInvLine is a DerivedField
			type is MessageField
			if (RecurringInvoiceLine entered)
				return InvoiceLineFormTitleWithLine
			else	
				return InvoiceLineFormTitleWithoutLine	
				
		InvoiceLineFormTitleWithLine is a LabelField
			restricted
			"RecurringInvoiceLine_<RecurringInvoiceLine>"

		InvoiceLineFormTitleWithoutLine is a LabelField
			restricted
			"RecurringInvoiceLine"

    Conditions
		IsHSNSACCodeEnabled
			restricted
			when (Company.GeneralLedgerCompany.RequireHSNSACCode)
			
		IsItemExists
			restricted
			when (Item exist)
		
		IsRequireUnitOfMeasure
			restricted
			when (Company.UnitOfMeasureRequired
			and   !LineType.Special)
			
    Relations
		InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = Company

		CustomerRecurringInvoiceLinesRel
            one-to-many relation to CustomerRecurringInvoiceLine
            Field Mapping uses Set2
                related.Company                   = Company
                related.RecurringInvoiceParameter = RecurringInvoiceParameter
                related.MasterLineNumber          = RecurringInvoiceLine

		RecurringInvoiceCommentsRel
            one-to-many relation to RecurringInvoiceComment
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.RecurringInvoiceParameter = RecurringInvoiceParameter
            Instance Selection
                where (related.RecurringInvoiceLine = RecurringInvoiceLine)

		RecurringInvoiceAddOnChargesRel is a RecurringInvoiceAddOnCharge set
		
		RecurringInvoiceLineAttachmentsRel is a RecurringInvoiceLineAttachment set

		ItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation	= RecurringInvoiceParameter.Location
				related.Item				= Item
		
		SellingItemUOMRel
			one-to-many relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup			= InventoryCompanyRel.ItemGroup
				related.Item				= Item
				related.UnitOfMeasure		= SellingUnitOfMeasure
			Instance Selection
				where (related.ValidForSelling.Default
				or 	   related.ValidForSelling.Valid)
		
		SellingPriceItemUOMRel
			one-to-many relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup			= InventoryCompanyRel.ItemGroup
				related.Item				= Item
				related.UnitOfMeasure		= SellingPriceUnitOfMeasure
			Instance Selection
				where (related.ValidForSellPrice.Default
				or 	   related.ValidForSellPrice.Valid)			

		BillingItemGeneralLedgerAccountRel
			one-to-many relation to BillingItemGeneralLedgerAccount
			Field Mapping uses symbolic key
				related.Company														= Company
			Instance Selection	
				where (related.BillingItemGeneralLedgerAccount.BillingProcessLevel 	= RecurringInvoiceParameter.BillingProcessLevel
				and	   related.BillingItemGeneralLedgerAccount.MajorSalesClass 		= Item.MajorSalesClass
				and   (related.BillingItemGeneralLedgerAccount.MinorSalesClass 		= Item.MinorSalesClass
				or 	   related.BillingItemGeneralLedgerAccount.MinorSalesClass  	= blank))
    
    Field Rules

        Item
           	if (!LineType.Special)
	            required
           		constraint (Item exist)
           			"Item<Item>DoesNotExist"									
           		
           		constraint (BillingItemGeneralLedgerAccountRel exist)
           			"SalesAccountDoesNotExistForItem<Item>"				
					           		
            if (LineType.Inventoried)
            	constraint (ItemLocationRel exist)
            		"ItemLocationDoesNotExistForItem<Item>"						
			
			if (RecurringInvoiceParameter.HasBillingInvoices)
				cannot be changed					
				
		ItemDescription
			if (LineType.Special)
				required
					"ItemDescriptionIsRequiredForSpecialItem<Item>"								
			else
				default to Item.Description
						
		Quantity
			if (!ZeroAfterUse)
				required
					"QuantityIsRequiredWhenItIsNotZeroAfterUse"				
			else	
				cannot be entered
					"CannotEnterQuantityWhenZeroAfterUse"					

			if (RecurringInvoiceParameter.HasBillingInvoices)						
				cannot be changed					
       
		ZeroAfterUse
			if (RecurringInvoiceParameter.HasBillingInvoices)
				cannot be changed
					
		UnitPrice
			if (RecurringInvoiceParameter.Currency not entered)
				cannot be entered
					"CannotEnterUnitPrice;CurrencyCodeIsNotEnteredInInvoice"		
					
		SalesRepresentative
			default to RecurringInvoiceParameter.SalesRepresentative
			if (SalesRepresentative2 entered)
				required												
		
		SalesRepresentative2
			default to RecurringInvoiceParameter.SalesRepresentative2
			constraint (SalesRepresentative != SalesRepresentative2)
				"SalesRepresentativeCannotBeSameAsSalesman"				

        CommissionRate1
        	default to SalesRepresentative.CommisionPercent
        	if (SalesRepresentative not entered)
        		cannot be entered
        			"CannotEnterCommisionRate1IfSalesRepresentiveIsNotEntered"		
        	
			constraint (CommissionRate1 <= 100%)
				"CommisionRateForSalesRepresentativeShouldNotBeGreaterThan100Percent"  
        
        CommissionRate2	
        	default to SalesRepresentative2.CommisionPercent
        	if (SalesRepresentative2 not entered)
        		cannot be entered
        			"CannotEnterCommisionRate2IfSalesRepresentiveIsNotEntered"		
        	
        	constraint ((CommissionRate1 + CommissionRate2) <= 100%) 
        		"CommisionRateForSalesRepresentativesShouldNotBeGreaterThan100Percent"  

        	if (SalesRepresentative2 entered)
				default to RecurringInvoiceParameter.CommissionSplitPercentage
        		required															
        	else
        		cannot be entered													
		
		SellingUnitOfMeasure
			if (LineType.Special
			or  IsRequireUnitOfMeasure)										
				required
			
			if (!LineType.Special)
				constraint (SellingItemUOMRel exist)
					"InvalidSellingUnitOfMeasure"										
				
		SellingPriceUnitOfMeasure
			if (LineType.Special)
				default to SellingUnitOfMeasure
				constraint (SellingPriceUnitOfMeasure = SellingUnitOfMeasure)
					"SellingPriceUnitOfMeasureAndSellingUnitOfMeasureShouldBeEqualForSpecialItem"	
			
			if (!LineType.Special)
				constraint (SellingPriceItemUOMRel exist)
					"InvalidSellingPriceUnitOfMeasure"													
        
        StartDate
			default to RecurringInvoiceParameter.StartDate            
			required	
				"StartDateIsRequired"								
			
			constraint (StartDate >= RecurringInvoiceParameter.StartDate)
				"StartDateCannotBeGreaterThanInvoiceStartDate"			
		
		StopDate
			default to RecurringInvoiceParameter.StopDate
			if (RecurringInvoiceParameter.StopDate entered)
				required
			
			constraint (StopDate >= StartDate)
				"StartDateCannotBeGreaterThanStopDate"					
			
			constraint (StopDate <= RecurringInvoiceParameter.StopDate)
				"StopDateDateCannotBeGreaterThanInvoiceStopDate"			
		HSNSACCode
			if (IsHSNSACCodeEnabled)
    			if (!LineType.Special)
					default to Item.HSNSACCode
				required
					"HSN_/SACCodeIsRequired"
			else
    			cannot be entered	
    				"CannotEnter,HSN_/SACCodeFlagIsNotSetAtGlobalLedgerCompany"
			
		TaxCode
			if (TaxExemptCode.Exempt)
				cannot be entered	

			if (TaxExemptCode.Taxable)
				default to RecurringInvoiceParameter.TaxCode
				required
					"TaxCodeIsRequiredForTaxableInvoice"		
		
		TaxExemptCode
			default to RecurringInvoiceParameter.TaxExemptCode
			if (RecurringInvoiceParameter.TaxExemptCode.Taxable)
				constraint (TaxExemptCode.Taxable)
					"InvoiceLineMustBeTaxableIfInvoiceIsTaxable"								
			
			if (TaxCode entered)
				constraint (TaxExemptCode.Taxable)
					"TaxExemptCodeShouldBeTaxableIfTaxCodeIsEntered"							
		
		Territory
			initial value is RecurringInvoiceParameter.Territory
			default to RecurringInvoiceParameter.Territory
				
		Project
			initial value is RecurringInvoiceParameter.Project
			default to RecurringInvoiceParameter.Project
		
		LineType
			default to LineType.NonStock
																	
	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Delete Action
