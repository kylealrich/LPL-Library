LeaseTemplate is a BusinessClass
    owned by lm
    prefix is LTM

    Ontology
        symbolic key is LeaseTemplate

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields 
        Description
        Company is a PayablesCompany
        LeaseType
        PaymentTiming
            classic name is PMT-TIMING   
        LeaseCategory												
			default label is "Category"    
        Currency					is a FromCurrency  
            classic name is CURRENCY-CODE
        Fas13Flag                  is AlphaUpper size 1
            classic name is FAS13-FLAG
            States
                Yes value is "Y"
                No  value is "N"    
        LeaseClassification
            classic name is LEASE-CLASS 
        InterestRate
        ImplicitInterestRate
            classic name is IMPLICIT-RATE 
        OverrideShortTermLease			 is Boolean        
        Lessor                           is a Vendor
        TerminationOption	             is Boolean
        TerminationGainLossAccount       is a FinanceCodeBlockFull
        TerminationNonLeaseComponent	 is an ExecutoryCostCode
        ExtensionOption		             is Boolean
        NumberOfExtensionOptions         is Numeric 2
        LengthOfExtension                is Numeric 2
        Ownership              	         is Boolean 	
        SpecializedAsset				 is Boolean 
        ExercisePurchase				 is Boolean
        BargainPurchaseAmount           is a CurrencyExchangeGroup
        LandAndBuilding					is Numeric 1 
			States
				LandOnly				value is 1	
				LandAndBuildingLease	value is 2
				Neither					value is 0
        LandValue                  is a CurrencyExchangeGroup
        BuildingValue              is a CurrencyExchangeGroup
            classic name is BLDG-VALUE
        DebtAccount                      is a FinanceCodeBlockFull
            classic name for DebtAccount.ToAccountingEntity is DEBT-COMPANY
            classic name for DebtAccount.AccountingUnit is DEBT-ACCT-UNIT
            classic name for DebtAccount.GeneralLedgerChartAccount is DEBT-ACCOUNT
            classic name for DebtAccount.Project is DEBT-ACTIVITY
        InterestAccount                is a FinanceCodeBlockFull
            classic name for InterestAccount.ToAccountingEntity is INT-COMPANY
            classic name for InterestAccount.AccountingUnit is INT-ACCT-UNIT
            classic name for InterestAccount.GeneralLedgerChartAccount is INT-ACCOUNT
            classic name for InterestAccount.Project is INT-ACTIVITY
        DeferredRentAccount             is a FinanceCodeBlockFull 
        LongTermOperatingLeaseExpense	is a FinanceCodeBlockFull
            default label is "OperatingLeaseExpense"
		ShortTermLiabilityAccount    	is a FinanceCodeBlockFull
		LongTermLiabilityAccount     	is a FinanceCodeBlockFull
		SkipFMVTest                     is Boolean
			default label is "SkipFairMarkValueTest"
        LeaseAccountingStatement 		is Numeric 1
			States
				842						value is 1
				87						value is 2
				96						value is 3

    Conditions

        IsValidForActorContext
			restricted
			when (Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

        LeaseTemplateExists
            restricted
            when (LeaseTemplate exists)

		ShortTermLiabilityAccounting
			restricted
			when (actor.context.FinanceEnterpriseGroup.ShortTermLiabilityAccounting)

    Field Rules

    	Currency
			default to Company.Currency

        LeaseCategory
			initial value is "D"

        DebtAccount
			if (!ShortTermLiabilityAccounting)
				required
					"LeaseLiabilityAccountRequired"
		ShortTermLiabilityAccount
			if (ShortTermLiabilityAccounting)
				required
					"ShortTermLiablityAccountRequired"
			else
				cannot be entered
					"CannotHaveShortTermLiabilityAccountIfShortTermLiabilityAccountingIsNotSet"
		
		LongTermLiabilityAccount
			if (ShortTermLiabilityAccounting)
				required
					"LongTermLiablityAccountRequired"
			else
				cannot be entered
					"CannotHaveLongTermLiabilityAccountIfShortTermLiabilityAccountingIsNotSet"

    Actions
        
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action
