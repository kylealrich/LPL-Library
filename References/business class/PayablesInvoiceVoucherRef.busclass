PayablesInvoiceVoucherRef is a BusinessClass
	owned by ap
	
	prefix is PIVR
	
	Ontology
		symbolic key is PayablesInvoiceVoucherRef
		
	Patterns
        disable AuditIndex
        disable Auditing 
        disable EffectiveDated
        implements StaticJava         
        
	Persistent Fields
		PayablesInvoiceSeq			  is Numeric 18
			protected
			restricted
		Invoice
		Vendor
		InvoiceType					  is a PayablesInvoiceType
        Status                        is Numeric size 1
            States
                Unreleased	    value is 0
                Released		value is 1
                Approved		value is 2
                PendingApproval	value is 3
                Rejected      	value is 4
                Amended			value is 5 
                Cancelled		value is 6		
                Paid			value is 8
                Historical		value is 9		
		VoucherReferenceNumber 		  is AlphaUpper 50
			protected
		InvoiceDate                   is an ExchangeDate	
		DueDate	
		DistributionDate			  is Date
        InvoiceAmount                 is an InternationalAmount
            default label is "InvoiceAmount"							

	Local Fields
		LocalSequenceNum6			  is AlphaUpper 6
		LocalSequenceNum12			  is AlphaUpper 12
		LocalTempJournalReference	  is AlphaUpper 50
    		Text Variables
				YearYYYY	    value is DistributionDate year
				YearYY		    value is DerivedYear[3:4]
				MonthMM		    value is DerivedPaddedMonth
				999999999999    value is LocalSequenceNum12
				000000000009    value is DerivedZeroPaddedSequenceNum12
				999999		    value is LocalSequenceNum6
				000009		    value is DerivedZeroPaddedSequenceNum6

	Transient Fields
		TransientYear				  is Year
			default label is "Year"
			derive value from DerivedYear

	Relations
		PayablesInvoiceRel
			one-to-one relation to PayablesInvoice
        	Field Mapping uses symbolic key
        		related.Company		  	= Company
        		related.PayablesInvoice = PayablesInvoiceVoucherRef	
            
		PayablesInvoiceHistoryRel
			one-to-many relation to PayablesInvoiceHistory
			Field Mapping uses ByPayablesInvoiceNumber
				related.Company 			  = Company
				related.PayablesInvoiceNumber = PayablesInvoiceVoucherRef
			Instance Selection
				where (related.PayablesInvoiceHistory.Invoice = Invoice)
						
	Field Rules
	
	Sets
		ByDistributionDate
			Sort Order
				Company
				DistributionDate 
				InvoiceDate
				PayablesInvoiceVoucherRef

	Derived Fields
 		DerivedSystem	is a DerivedField
 			type is like GeneralLedgerSystemCode
 			restricted
			return "AP"
 	
		DerivedYear is a DerivedField
			type is AlphaUpper size 4
			restricted
			return DistributionDate year

		DerivedMonth is a DerivedField
			type is Numeric size 2
			restricted
			return DistributionDate month
			
		DerivedSequence6 is a DerivedField
			type is AlphaRight size 6
			restricted
			return LocalSequenceNum6

		DerivedSequence12 is a DerivedField
			type is AlphaRight size 12
			restricted
			return LocalSequenceNum12

 		DerivedPaddedMonth is a DerivedField
			type is AlphaUpper 2
			restricted
			if (DistributionDate month < 10)
				DerivedPaddedMonth	= "0" + DerivedMonth
			else
				return DistributionDate month
            
 		DerivedZeroPaddedSequenceNum12 is a DerivedField
 			type is AlphaUpper size 12
 			restricted
 			return com.lawson.apps.procurement.base.StringUtils.leadingZeros(DerivedSequence12)
 			
		DerivedZeroPaddedSequenceNum6 is a DerivedField
 			type is AlphaUpper size 6
  			restricted
 			return com.lawson.apps.procurement.base.StringUtils.leadingZeros(DerivedSequence6)

  		DerivedStructuredJournalReferenceNumber is a DerivedField
 			type is AlphaUpper size 50 		
  			restricted
  			DerivedStructuredJournalReferenceNumber = LocalTempJournalReference text	

	Conditions
		HasVoucherReferenceNumber
			restricted
			when (VoucherReferenceNumber entered)

		HasPayablesInvoice
			restricted
			when (PayablesInvoiceRel exists)
			
		HasPayablesInvoiceHistory
			restricted
			when (PayablesInvoiceHistoryRel exists)

	Actions
		Create is an Action
			restricted
			
		Update is an Action
			restricted
			
		Delete is an Action
			restricted	

		AssignVoucherReferenceNumber is a Set Action
			restricted
			run in background

			Parameters
				PrmCompany 		  	 is a PayablesCompany
				PrmPeriodStartDate 	 is Date
				PrmPeriodEndDate     is Date
										
			Local Fields
    			LocalLineCounter12	 is Numeric size 12
    			LocalLineCounter6	 is Numeric size 6	

			Instance Selection		
				where (Company 			 = PrmCompany
				and   (DistributionDate >= PrmPeriodStartDate 
				and    DistributionDate <= PrmPeriodEndDate))

			Sort Order
				Company
				DistributionDate
				InvoiceDate
				PayablesInvoiceVoucherRef
            	
			Action Rules						
				Instance Rules
					LocalLineCounter6  += 1
					LocalSequenceNum6   = LocalLineCounter6
					LocalLineCounter12 += 1	
					LocalSequenceNum12  = LocalLineCounter12
					LocalTempJournalReference = PrmCompany.VoucherReferenceFormat 
					
					invoke Update 
						invoked.VoucherReferenceNumber = DerivedStructuredJournalReferenceNumber
