ArchivedBusinessClassValidationResult is a BusinessClass
	sql name is "ArchivedBusClassValResult"
	prefix is ABVR
	owned by la
	default label is "ValidationResult"
	
	Patterns
		disable Auditing
    	disable EffectiveDated
		implements CreateStamp
		implements UpdateStamp
	
	Ontology
		symbolic key is ArchivedBusinessClassValidationResult
		
	Persistent Fields
		ExpectedInArchiveCount			is Numeric size 12   			
		ExpectedInArchiveCheckSum 		is a PurgeCheckSumResultsArray 	
			default label is "ExpectedInArchiveCountCheckSum"
		ValidatedInArchiveCount			is Numeric size 12   			
		ValidatedInArchiveCheckSum		is a PurgeCheckSumValueArray  
		
	Derived Fields
		ArchiveCountDiscrepancy is a DerivedField
			type is Numeric size 12 
			return (ExpectedInArchiveCount - ValidatedInArchiveCount)
		
		CheckSumDiscrepancy1 is a DerivedField
			type is Numeric size 12 
			return (ExpectedInArchiveCheckSum.CheckSumField[1].CheckSumValue - ValidatedInArchiveCheckSum.CheckSumValue[1])
			
		CheckSumDiscrepancy2 is a DerivedField
			type is Numeric size 12 
			return (ExpectedInArchiveCheckSum.CheckSumField[2].CheckSumValue - ValidatedInArchiveCheckSum.CheckSumValue[2])
			
		CheckSumDiscrepancy3 is a DerivedField
			type is Numeric size 12 
			return (ExpectedInArchiveCheckSum.CheckSumField[3].CheckSumValue - ValidatedInArchiveCheckSum.CheckSumValue[3])
			
		CheckSumDiscrepancy4 is a DerivedField
			type is Numeric size 12 
			return (ExpectedInArchiveCheckSum.CheckSumField[4].CheckSumValue - ValidatedInArchiveCheckSum.CheckSumValue[4])
			
		ValidationConsistentMessage is a MessageField
			restricted
			"<ArchivedBusinessClass>ValidationDataIsConsistent"
	
		ValidationInconsistentMessage is a MessageField
			restricted
			"<ArchivedBusinessClass>ValidationDataIsInconsistent"
			
		ValidationSingleConsistentMessage is a MessageField
			restricted
			"<ArchivedBusinessClass>ValidationDataFor<LocCheckSumName>IsConsistent"
	
		ValidationSingleInconsistentMessage is a MessageField
			restricted
			"<ArchivedBusinessClass>ValidationDataFor<LocCheckSumName>IsInconsistent"
			
		ValidationMessage is a DerivedField
    		type is MessageField
    		
    		if (AnyDiscrepancy)
    			if (LocCheckSumName entered and AnyCheckSumDiscrepancy)
    				return ValidationSingleInconsistentMessage

    			return ValidationInconsistentMessage
    		
    		if (LocCheckSumName entered)
				return ValidationSingleConsistentMessage
				
    		return ValidationConsistentMessage
		
	Conditions
		CountDiscrepancy
			default label is untranslatable
			when (ArchiveCountDiscrepancy != 0)
			
		CheckSum1InDiscrepancy
			default label is untranslatable
			when (CheckSumDiscrepancy1 != 0)
			
		CheckSum2InDiscrepancy
			default label is untranslatable
			when (CheckSumDiscrepancy2 != 0)
			
		CheckSum3InDiscrepancy
			default label is untranslatable
			when (CheckSumDiscrepancy3 != 0)
			
		CheckSum4InDiscrepancy
			default label is untranslatable
			when (CheckSumDiscrepancy4 != 0)
			
		AnyCheckSumDiscrepancy
			default label is untranslatable
			when (CheckSum1InDiscrepancy or CheckSum2InDiscrepancy or CheckSum3InDiscrepancy or CheckSum4InDiscrepancy)
		
		AnyDiscrepancy
			default label is untranslatable
			when (CountDiscrepancy or AnyCheckSumDiscrepancy)
		
		
	Local Fields
		I is Numeric 2
		J is Numeric 2
		LocCheckSumName is like ArchivedBusinessClassCheckSum
		
	Create Exit Rules
		I = instance count of ArchivedBusinessClass.ArchivedBusinessClassValidationResult set
		
		if (I > 5)
			for each ArchivedBusinessClass.ArchivedBusinessClassValidationResult set 
				invoke Delete each
				decrement I
				
				if (I <= 5)
					end for each
				
	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
		
		SendNotification is an Instance Action
			restricted
			
			Parameters
				ParamCheckSumName is like ArchivedBusinessClassCheckSum
					default label is untranslatable
			
			Local Fields
				LocActor is an Actor
			
			Action Rules
				LocActor = actor
				LocCheckSumName = ParamCheckSumName
				
				send notification
					to LocActor
					description is untranslatable:"<ValidationMessage>"
					linkback (webapp is AdministratorConsole navigation is ViewValidationResult text is "ViewValidationResult")
		
