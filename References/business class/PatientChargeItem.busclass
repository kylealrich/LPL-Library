PatientChargeItem is a BusinessClass
    owned by ic
    prefix is HCI
    classic name is HCCHRGITEM

    Ontology
        symbolic key is PatientChargeItem
            classic set name is HCISET1
            classic name for PatientChargeItem.RevenueCenter is REV-CENTER
            classic name for PatientChargeItem.ChargeNumber is CHARGE-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        PriceClass		is a PatientPriceClass
        Price			is an InternationalAmount
        UpdateDate		is Date
 		ContractGroup   is like ContractGroup
        Contract
        	delete ignored
        ContractLine
        	delete ignored

    Local Fields
    	LocalItem        is like Item
    
    Conditions

        HasPriceClass
            classic name is PRICE-CLASS
            restricted
            when (PriceClass entered)

    	NotFromContract
    		restricted
    		when (Contract !entered)
    		
    	FromContract
    		restricted
    		when (Contract entered)
    	
    	DoNotDisplay
    		restricted
    		when (Company !entered
    		and   Company entered)

    	RecordExists
    		restricted
    		when(PatientChargeItem exists)
    
    Relations

				
		PatientChargeItemRel
			one-to-many relation to PatientChargeItem
			Field Mapping uses symbolic key
				related.Company							= Company
			Instance Selection
				where	(related.PatientChargeItem.Item			= PatientChargeItem.Item
				and 	related.PatientChargeItem.ChargeNumber = PatientChargeItem.ChargeNumber)		
				
		NewPatientChargeItemRel
			one-to-many relation to PatientChargeItem
			Field Mapping uses symbolic key
				related.Company							= Company
			Instance Selection
				where	(related.PatientChargeItem.Item			= LocalItem
				and 	related.PatientChargeItem.ChargeNumber = PatientChargeItem.ChargeNumber)	

		PatientChargeItemMasterRel
			one-to-one relation to Item
			Field Mapping uses symbolic key
				related.ItemGroup                       = Company.ItemGroup
				related.Item                            = PatientChargeItem.Item
		
		PatientChargeContractCompanyRel
			one-to-one relation to ContractGroup
			Field Mapping uses symbolic key
				related.ContractGroup                   = Company.ItemGroup 

		ContractLineRel
			one-to-one relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.ContractLine               = ContractLine

		PatientChargesItemRel
            one-to-many relation to PatientChargeItem
            Field Mapping uses Set2
                related.Company                         = Company
                related.PatientChargeItem.Item          = PatientChargeItem.Item
                related.PatientChargeItem.RevenueCenter = PatientChargeItem.RevenueCenter

    Sets

        Set2
            indexed
            Sort Order
                Company
                PatientChargeItem.Item
                PatientChargeItem.RevenueCenter
				ContractGroup
				Contract
				ContractLine

        Set3
            indexed
            Sort Order
                Company
                PatientChargeItem.RevenueCenter
                PatientChargeItem.Item
                ContractGroup
                Contract
                ContractLine
                
    	ByContractLine
    		Sort Order
    			ContractGroup
    			Contract
    			ContractLine
    			Company
                PatientChargeItem.Item
                PatientChargeItem.RevenueCenter
                PatientChargeItem.ChargeNumber
    
    Field Rules
    	
    	PatientChargeItem
    	
    		constraint (PatientChargeItem.RevenueCenter entered)
    			"RevenueCenterIsRequired"
    			
    		constraint (PatientChargeItem.ChargeNumber entered)
    			"ChargeNumberIsRequired"
    		
    		constraint (PatientChargeItem.Item entered)
    			"ItemIsRequired"
    			
		UpdateDate
		
			default to current corporate date
			
	Actions
	
		Create is a Create Action
		
			Action Rules
			
				constraint (PatientChargeItemMasterRel exists)
					"ItemDoesNotExist"
				
				constraint (PatientChargesItemRel not exists)
					"ItemCanOnlyBeAssignedToOneChargeNumber;Item<PatientChargeItem.Item>IsAssociatedWith<first PatientChargesItemRel.PatientChargeItem.ChargeNumber>ChargeNumber"			

		CreateFromContract is a Create Action
			Action Rules
				
				constraint (PatientChargeContractCompanyRel exists)
					"CompanyNotValidForContractGroup"

				ContractGroup          = ContractGroup
				PatientChargeItem.Item = ContractLineRel.ItemNumber
		
		ChangeItemNumber is an Instance Action
			restricted
			Parameters
				NewItem        is like Item
				
			Action Rules
				
				LocalItem    = NewItem
				
				if (NewPatientChargeItemRel !exists)
					invoke CreateFromContract
						invoked.ContractGroup                   = ContractGroup
						invoked.Contract        				= Contract
						invoked.ContractLine    				= ContractLine
						invoked.Company                         = Company
						invoked.PatientChargeItem.RevenueCenter = PatientChargeItem.RevenueCenter
						invoked.PatientChargeItem.ChargeNumber  = PatientChargeItem.ChargeNumber
						invoked.Price                           = Price
						invoked.PriceClass                      = PriceClass
						invoked.PatientChargeItem.Item         	= NewItem 

				invoke Delete
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
			
