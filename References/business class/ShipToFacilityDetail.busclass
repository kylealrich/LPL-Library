ShipToFacilityDetail is a BusinessClass
    owned by ic
    prefix is FAC
    classic name is ICFACILITY

    Ontology
        symbolic key is ShipToFacilityDetail
            classic set name is FACSET0
            classic name for ShipToFacilityDetail.InventoryLocation is FACILITY-LOC
            classic name for ShipToFacility is ST-FACILITY

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Name
        FacilityLocationType is Numeric size 1
            classic name is FAC-LOC-TYPE
            States
                InventoryLocation  value is 0
                RequestingLocation value is 1

    Conditions

        IsInventoryLocation
            classic name is IC-LOCATION
            restricted
            when (FacilityLocationType.InventoryLocation)

        IsRequestingLocation
            classic name is REQ-LOCATION
            restricted
            when (FacilityLocationType.RequestingLocation)

    Relations

        ManufacturerIDShipToFacilitiesRel
            classic name is MFGSHPFAC
            one-to-many relation to ManufacturerIDShipToFacility
            Field Mapping uses Set2
                related.ItemGroup      = ItemGroup
                related.ShipToFacility = ShipToFacility

        RequestingLocationRel
            classic name is RQLOC
            one-to-one relation to RequestingLocation
            valid when (IsRequestingLocation)
            Field Mapping uses symbolic key
                related.Company            = ShipToFacilityDetail.Company
                related.RequestingLocation = ShipToFacilityDetail.InventoryLocation

		InventoryLocationForRequestingLocationRel
			one-to-many relation to InventoryLocation
			Field Mapping uses symbolic key
			Instance Selection
				where	(related.Company									=	ShipToFacilityDetail.Company	
				and		related.InventoryLocation							=	ShipToFacilityDetail.RequestingLocation
				and		related.LocationType.InventoryLocation)
			
				
		OtherShipToFacilitiesForCompanyLocationRel											
			one-to-many relation to ShipToFacilityDetail
			Field Mapping uses ByCompanyLocation
				related.ItemGroup											=	ItemGroup
			Instance Selection
				where	(related.ShipToFacilityDetail.Company				=	ShipToFacilityDetail.Company
				and		related.ShipToFacilityDetail.InventoryLocation		=	ShipToFacilityDetail.InventoryLocation
				and		related.ShipToFacilityDetail.RequestingLocation		=	ShipToFacilityDetail.RequestingLocation
				and		related.ShipToFacility								!=	ShipToFacility)
				
				
		OtherShipToFacilitiesForCompanyForAllLocationsRel										
			one-to-many relation to ShipToFacilityDetail
			Field Mapping uses ByCompanyLocation
				related.ItemGroup											=	ItemGroup
			Instance Selection
				where	(related.ShipToFacilityDetail.Company				=	ShipToFacilityDetail.Company
				and		(related.ShipToFacilityDetail.InventoryLocation		!=	ShipToFacilityDetail.InventoryLocation
				or		related.ShipToFacilityDetail.RequestingLocation		!=	ShipToFacilityDetail.RequestingLocation)
				and		related.ShipToFacility								!=	ShipToFacility)
	
	
		OtherShipToFacilitiesForCompanyForAnyLocationRel										
			one-to-many relation to ShipToFacilityDetail
			Field Mapping uses ByCompanyLocation
				related.ItemGroup											=	ItemGroup
			Instance Selection
				where	(related.ShipToFacilityDetail.Company				=	ShipToFacilityDetail.Company
				and		(related.ShipToFacilityDetail.InventoryLocation not entered
				or       related.ShipToFacilityDetail.RequestingLocation not entered))
	
		OtherShipToFacilitiesForCompanyWithNoLocationsRel										
			one-to-many relation to ShipToFacilityDetail
			Field Mapping uses ByCompanyLocation
				related.ItemGroup											=	ItemGroup
			Instance Selection
				where	(related.ShipToFacilityDetail.Company				=	ShipToFacilityDetail.Company
				and		 related.ShipToFacilityDetail.InventoryLocation not entered
				and		 related.ShipToFacilityDetail.RequestingLocation not entered)
	
	Sets
		ByCompanyLocation
			Sort Order
				ItemGroup
				ShipToFacilityDetail.Company
				ShipToFacilityDetail.InventoryLocation
				ShipToFacilityDetail.RequestingLocation
				ShipToFacility
				
    Field Rules

		Name
            required	
			initial value is ShipToFacility.Name
			default to ShipToFacility.Name												

	
		ShipToFacilityDetail
			constraint (ShipToFacilityDetail.Company.ItemGroup = ItemGroup)
				"CompanyIsNotAMemberOfThe_\Item_\Group"																    
			
				
			if (ShipToFacilityDetail.InventoryLocation not entered
			and ShipToFacilityDetail.RequestingLocation not entered)
				constraint (OtherShipToFacilitiesForCompanyForAnyLocationRel not exist)
					"CompanyAlreadyAssociatedWithAFacility_(WithOrWithOutALocation)"															      
   					
			if (ShipToFacilityDetail.InventoryLocation entered)
				constraint (ShipToFacilityDetail.RequestingLocation not entered)
					"BothInventoryLocationAndRequestingLocationCannotBeEnteredTogether"	
						
				constraint (OtherShipToFacilitiesForCompanyLocationRel not exist) 			
					"Company/\LocationAlreadyAssociatedWithAnotherFacility"																   
				
				constraint (OtherShipToFacilitiesForCompanyWithNoLocationsRel not exist) 	
					"CompanyAlreadyAssociatedWithAFacilityWithoutALocation"                   
					
   				constraint (ShipToFacilityDetail.InventoryLocation.LocationType.InventoryLocation)
   					"InventoryLocationMustBeARegularLocation,NotExchangeCartOrParLocation"

   			if (ShipToFacilityDetail.RequestingLocation entered)
   				constraint (ShipToFacilityDetail.InventoryLocation not entered)
					"BothInventoryLocationAndRequestingLocationCannotBeEnteredTogether"	
   				
   				constraint (OtherShipToFacilitiesForCompanyLocationRel not exist) 			
					"Company/\LocationAlreadyAssociatedWithAnotherFacility"																   
				
				constraint (OtherShipToFacilitiesForCompanyWithNoLocationsRel not exist) 	
					"CompanyAlreadyAssociatedWithAFacilityWithoutALocation"                   
					
				constraint (InventoryLocationForRequestingLocationRel not exist)
					"ThisIsAInventoryLocationAsWell,CannotBeEnteredAsARequestingLocation"



  	Actions
  	
        Create is a Create Action

        Update is an Update Action
	
        Delete is a Delete Action
        	Action Rules
        		constraint (ManufacturerIDShipToFacilitiesRel not exist)
        			"CannotDeleteRecord;RecordAssociatedToAManufacture"

