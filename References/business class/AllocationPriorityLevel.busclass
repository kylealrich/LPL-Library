AllocationPriorityLevel is a BusinessClass 
    owned by rq 
    prefix is ALPRT 
    
    Ontology
        symbolic key is AllocationPriorityLevel 

    Patterns 
        disable AuditIndex 
    
    Persistent Fields 
        Description  
        UseAsDefault is Boolean
        PriorityCategoryType is AlphaUpper size 1
            protected
            States
                System 				value is "S"
                User   				value is "U"

    Field Rules 
        AllocationPriorityLevel
            initial value is 50
            default to 50
        Description
            required
        PriorityCategoryType
            default to PriorityCategoryType.User
            initial value is PriorityCategoryType.User
    
    Derived Fields 
        DerivedCurrentAllocationPriorityLevel is a DerivedField
            type is like AllocationPriorityLevel
            restricted
            if (CurrentDefaultPriorityRel exists)
                return first CurrentDefaultPriorityRel.AllocationPriorityLevel

    Relations 
        CurrentDefaultPriorityRel  
            one-to-many relation to AllocationPriorityLevel
			Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.UseAsDefault)
        
        SystemDefaultPriorityLevelRel 
            one-to-many relation to AllocationPriorityLevel
			Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.PriorityCategoryType = "S")

    Actions
        Create is a Create Action
            Entrance Rules
                constraint (Description entered)
                    "EnterADescriptionForThis_Priority_Level."
                constraint (AllocationPriorityLevel > 0)
                    "ValueMustBeBetween1And99."
            
            Action Rules
                if(UseAsDefault
                and CurrentDefaultPriorityRel exists)
                    confirmation required
                        "Warning:_Default_PriorityWillBeChangedFrom<DerivedCurrentAllocationPriorityLevel>To<AllocationPriorityLevel>._Proceed?"
                    invoke FastUpdate CurrentDefaultPriorityRel


		Update is an Update Action
            Entrance Rules
                constraint (Description entered)
                    "EnterADescriptionForThis_Priority_Level."
    
            Action Rules
                if(UseAsDefault changed
                and UseAsDefault
                and CurrentDefaultPriorityRel exists)
                    confirmation required
                        "Warning:_Default_PriorityWillBeChangedFrom<DerivedCurrentAllocationPriorityLevel>To<AllocationPriorityLevel>._Proceed?"
                    invoke FastUpdate CurrentDefaultPriorityRel

            Exit Rules
                constraint (CurrentDefaultPriorityRel exists)
                    "AtLeastOneEntryMustBeSetToDefault."
        
        Delete is a Delete Action
            Action Rules
                constraint (PriorityCategoryType.User)
                    "CannotDelete_SystemGenerated_Allocation_Priority_LevelValue."
                if (UseAsDefault)
                    invoke FastUpdate SystemDefaultPriorityLevelRel
                
        FastUpdate is an Update Action
            restricted
            bypass field rules

            Action Rules 
                if (UseAsDefault = false)
                    UseAsDefault = true
                else
                    UseAsDefault = false
