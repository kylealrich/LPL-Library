PoInvLineFact is a BusinessClass
    owned by procurement
    prefix is PCF

	Ontology
		symbolic key is PoInvLineFact
		
    Patterns
        disable Auditing
        disable EffectiveDated
        implements DynamicCreation
        implements ContextualParent

	Persistent Fields

		MatchedQty				is Decimal size 13.4
		MatchUnitCst			is an InternationalCost	
		VpriUomMult				is Decimal size 13.7
		TotDistAmt				is an InternationalAmount
		PoUnitCost				is an InternationalCost

		ItemGroup				
		Item														
		VendorItem				is AlphaUpper size 32							
		InvoiceDte				is Date
		VendorGroup				
		PurchFrLoc				is like PurchaseFromLocation 
		ContractGroup			is AlphaUpper size 4
		Contract				is like Contract
		PoDate					is Date
    	ProcureGroup			is a ProcurementGroup
    	MfgContract				is Numeric 15
		Location				is an InventoryLocation
		EntBuyUom				is Alpha size 4
		PreferredFlag			is Numeric size 1
		VendorVname				is a Name	 
			holds pii
		ContractName			is a Description
    	ManufacturerCode		is Alpha 4
    	ManufacturerDivision	is Alpha 4
    	ManufacturerNumber
    	PurchMajcl				is AlphaUpper size 4
    	PurchMincl				is AlphaUpper size 4
		UNSPSCCode
		CommodityCode           is a CommCodes
		ItemOnContract			is Numeric size 1
		CurrencyCode			is a Currency

	Context Fields
		SupplyManagementReportContext	is a SupplyManagementReport

		
	Local Fields
		LocalContractGroup		is AlphaUpper size 4
		LocalContract			is Numeric size 15
		LocalOnContractFlag 	is Numeric size 1
		
	Derived Fields
	
		DerivedExtendedInvoiceAmt		is a DerivedField                    
			type is like InternationalCost
			return (MatchedQty * MatchUnitCst)
			
		DerivedExtendedAmountFromPo		is a DerivedField                    
			type is like InternationalCost
			return (MatchedQty * PoUnitCost)
    	
		DerivedExtendedVarianceAmt		is a DerivedField                    
			type is like InternationalCost
			return (MatchedQty * (MatchUnitCst - PoUnitCost))
			
		DerivedUnitCostVariance			is a DerivedField
			type is like InternationalCost
			return (MatchUnitCst - PoUnitCost)
		
		InvOverPOUnitCostAmt 			is a DerivedField
			type is like InternationalCost
			if (MatchUnitCst < PoUnitCost)
				return 0
			else
				return DerivedUnitCostVariance
				
		UnitCostVarPct					is a ComputeField
			type is Percent size 5.2
			(((MatchUnitCst - PoUnitCost) / PoUnitCost))
			
		InvOverPOUnitCostPct  			is a ConditionalField
			type is Decimal size 5.2
			if (MatchUnitCst < PoUnitCost) 
				0
			else
				UnitCostVarPct	



		SumPOLineQuantityByVendor is a DerivedField
			type is Decimal size 13.2
			return POInvoiceByItemVendorDetailRel.SumPOLineQuantity

		SumDerivedExtendedAmountFromPoByVendor is a DerivedField
			type is Decimal size 21.2
			return sum POInvoiceByItemVendorDetailRel.DerivedExtendedAmountFromPo
		
		SumMatchedQtyByVendor is a DerivedField
			type is Decimal size 13.2
			return sum POInvoiceByItemVendorDetailRel.MatchedQty
			
		SumDerivedExtendedInvoiceAmtByVendor is a DerivedField
			type is Decimal size 21.2
			return sum POInvoiceByItemVendorDetailRel.DerivedExtendedInvoiceAmt

		ReportPOLineQuantity is a DerivedField
			type is Decimal size 13.2
			return PurchaseOrderLineRel.Quantity
		
		ReportPOLineUnitCost is a DerivedField
			type is Decimal size 21.2
			return PoUnitCost
			
		ReportDerivedExtendedAmountFromPo is a DerivedField
			type is Decimal size 21.2
			return DerivedExtendedAmountFromPo
		
		ReportMatchedQty is a DerivedField
			type is Decimal size 13.2
			return MatchedQty
		
		ReportMatchUnitCost is a DerivedField
			type is Decimal size 21.2	
			return MatchUnitCst

		ReportDerivedExtendedInvoiceAmt is a DerivedField
			type is Decimal size 21.2	
			return DerivedExtendedInvoiceAmt
		
		SumPOLineQuantity is a DerivedField
			type is Decimal size 13.2
			restricted
			return PurchaseOrderLineRel.Quantity

    Sets
       	ByItem
       		duplicates
       		indexed
       		Sort Order
       			ProcureGroup
       			Item
       			EntBuyUom
       			Contract
       			MfgContract
       	
       	ByItemVendor
       		duplicates
       		indexed
       		Sort Order
       			ProcureGroup
       			Item
				PoInvLineFact.Vendor
				PoInvLineFact.Invoice
				PoInvLineFact.Suffix
				PoInvLineFact.PoNumber
				PoInvLineFact.PoRelease
				PoInvLineFact.PoCode
				PoInvLineFact.LineNbr
				PoInvLineFact.SeqNbr
       	
        ByVendorGroup
        	duplicates
            indexed
            Sort Order
            	VendorGroup
                InvoiceDte

		ByManufacturerInformationVendorItem 
			Sort Order 
				ContractGroup
				ManufacturerCode 
				ManufacturerDivision 
				ManufacturerNumber 
				VendorItem 				
				PoInvLineFact.Vendor
				PoInvLineFact.Invoice
				PoInvLineFact.Suffix
				PoInvLineFact.PoNumber
				PoInvLineFact.PoRelease
				PoInvLineFact.PoCode
				PoInvLineFact.LineNbr
				PoInvLineFact.SeqNbr

		ByItemVendorItem 
			Sort Order 
				ContractGroup
				Item 
				VendorItem 		
				PoInvLineFact.Vendor
				PoInvLineFact.Invoice
				PoInvLineFact.Suffix
				PoInvLineFact.PoNumber
				PoInvLineFact.PoRelease
				PoInvLineFact.PoCode
				PoInvLineFact.LineNbr
				PoInvLineFact.SeqNbr			
           
	Relations
		PurchaseOrderLineRel
			one-to-one relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company					= PoInvLineFact.Company
				related.PurchaseOrder			= PoInvLineFact.PoNumber
				related.PurchaseOrderLine		= PoInvLineFact.LineNbr
				
		CommodityCodeRel
			one-to-one relation to CommodityCode
			Field Mapping uses symbolic key
				related.ItemGroup					= ItemGroup
				related.CommodityCode				= PurchaseOrderLineRel.CommodityCode
				
		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup		= VendorGroup
				related.Vendor			= PoInvLineFact.Vendor	
				
		PayablesInvoiceRel
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByCompanyVendorInvoice
				related.Company			= PoInvLineFact.Company
				related.Vendor			= PoInvLineFact.Vendor
				related.Invoice			= PoInvLineFact.Invoice
				related.Suffix			= PoInvLineFact.Suffix
				related.CancelSequence	= 0
				
		ContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup				= ContractGroup
				related.Contract					= Contract

		SupplyManagementReportRel
			one-to-one relation to SupplyManagementReport
			Field Mapping uses symbolic key
				related.ItemGroup	= ProcureGroup
				related.SupplyManagementReport	= SupplyManagementReportContext

		POInvoiceByItemVendorDetailRel
			one-to-many relation to PoInvLineFact
			Field Mapping uses ByItemVendor
				related.ProcureGroup			= ProcureGroup
				related.Item					= Item
				related.PoInvLineFact.Vendor	= PoInvLineFact.Vendor
			Instance Selection	
				where	((SupplyManagementReportContext.ManufacturerCode not entered
				or		related.ManufacturerCode	= SupplyManagementReportContext.ManufacturerCode)
				and		(SupplyManagementReportContext.ManufacturerDivision not entered
				or		related.ManufacturerDivision	= SupplyManagementReportContext.ManufacturerDivision)
				and		(SupplyManagementReportContext.CommodityCode not entered
				or		related.CommodityCode		= SupplyManagementReportContext.CommodityCode)
				and		(SupplyManagementReportContext.PurchasingMajorClass not entered
				or		related.PurchMajcl			= SupplyManagementReportContext.PurchasingMajorClass)
				and		(SupplyManagementReportContext.PurchasingMinorClass not entered
				or		related.PurchMincl			= SupplyManagementReportContext.PurchasingMinorClass)
				and		(SupplyManagementReportContext.UNSPSCCode not entered
				or		related.UNSPSCCode			= SupplyManagementReportContext.UNSPSCCode)
				and		(SupplyManagementReportContext.FromDate not entered
				or		related.PoDate				>= SupplyManagementReportContext.FromDate)
				and		(SupplyManagementReportContext.ThruDate not entered
				or		related.PoDate				<= SupplyManagementReportContext.ThruDate)
				and		(SupplyManagementReportContext.IncludeDetails
				or		not related.IncludeDetails))

		POInvoiceByItemVendorHeaderRel
			one-to-many relation to PoInvLineFact
			Field Mapping uses ByItemVendor
				related.ProcureGroup			= ProcureGroup
				related.Item					= Item
				related.PoInvLineFact.Vendor	= PoInvLineFact.Vendor 
			Instance Selection	
				where	(related.UniqueID < UniqueID)
				
	Conditions
		IncludeDetails
			restricted
			when (UNSPSCCode entered)
		
		ByItemVendorHeader
			restricted
			when (POInvoiceByItemVendorHeaderRel not exist)

	Actions
		Create is a Create Action
			restricted
			
		Delete is a Delete Action
			restricted
			
        Purge is a Purge Action
            restricted
            
		Update is an Update Action
			restricted
			
        PurgePoInvLineFact is a Set Action
            restricted
            completion message is "PurgeInvoiceInformationForTierReportingIsComplete"
            Parameters
                PrmVendorGroup                    is a VendorGroup
                PrmBeginDate                    is Date
                PrmEndDate                      is Date
                
            Parameter Rules
                PrmVendorGroup
                    required
                        "VendorGroupRequired"
                
                PrmBeginDate
                    required
                        "BeginDateIsRequired"
                        
                PrmEndDate
                    default to current corporate date
                    constraint (PrmEndDate >= PrmBeginDate)
                    	"EndDateCannotPrecedeBeginDate"
                    
            Instance Selection
                where (VendorGroup            = PrmVendorGroup
                and   (InvoiceDte           >= PrmBeginDate
                and    InvoiceDte           <= PrmEndDate))
                
            Sort Order
                VendorGroup
                InvoiceDte
                
            Action Rules
                Instance Rules
                    invoke Purge             
