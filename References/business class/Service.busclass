Service is a BusinessClass
    owned by security
    prefix is SVC
    stored in environment
	contains environment data
	disable data area copy
		preserve target data

    Ontology
        symbolic key is Service
        
	Patterns
        implements SecurityCache
        disable EffectiveDated
        disable AsOfDateProcessing

    Persistent Fields
    	Description is a LSDescription
        ServiceType
        Version
        LoginScheme
        LoginSchemeExpression		is Alpha size 1000
        IsReference                 is Boolean
			default label is "Reference"
        ReferenceServiceName        is a Service
        AccountLockoutPolicy
        PasswordResetPolicy
        AllowsAnonymous             is Boolean
        DeleteConstraints
        AssignmentConstraints
        PasswordPolicy
        ServiceProtocol
        HasOwnSSOPages              is Boolean
			default label is "OwnSSOPagesExist"
        LastSecurityQuestion        is Numeric size 6
        TrackLastSuccessfulLogin    is Boolean

    Field Rules
        Version
            default to Version.Version020
        ReferenceServiceName
            constraint (IsReference)
                "CannotSetReferenceServiceNameUnlessServiceIsAReferenceService"
        ServiceProtocol
            default to ServiceProtocol.HTTPOnly

	Derived Fields
		HasExternalEntityIdValue is a DerivedField
			type is Boolean
			default label is untranslatable
			for each Service.ServicePropertyEntityCodeRel 
				if (each.PropertyValue != blank)
					return true
			return false
			
		Productline is a DerivedField
			type is Alpha size 10
			return first DataAreaRel.ProductLine
		
	Conditions
		IsGreyBox
			default label is "GreyBox"
            when (LoginScheme.SchemeType = "GreyBoxUPLogin")
        
        IsExternalEntityIdConfigured
            default label is untranslatable
        	when (HasExternalEntityIdValue)

	Sets
		ByServiceUpdateStamp
			indexed
			duplicates
			Sort Order
				update stamp descending
				
	Relations






		ServicePropertyEntityCodeRel is a ServiceProperty set
			Instance Selection
				where (related.ServiceProperty = "ExternalEntityId")
				
		DataAreaRel is a DataArea set
			Instance Selection
				where related.DataArea = parentcontext.dataarea
				
	Transient Fields
		SSODomain
    
    Actions
        Create is a Create Action
			Action Rules				 
				if (IsReference)
					constraint(ReferenceServiceName entered)
						"ReferenceServiceIsRequired"
					constraint(ReferenceServiceName != Service)
						"ReferenceServiceCannotReferenceItself"
						
        Delete is a Delete Action

        Update is an Update Action
        	Action Rules				 
				if (IsReference)
					constraint(ReferenceServiceName entered)
						"ReferenceServiceIsRequired"
					constraint(ReferenceServiceName != Service)
						"ReferenceServiceCannotReferenceItself"
						
        ViewFullSummary is an Update Action
			     
		GeneratePGPKeyPair is an Instance Action
			Parameters
				KeyExpiryInYears is Numeric size 2
			Parameter Rules
				KeyExpiryInYears
                    if (KeyExpiryInYears entered)
                        constraint (KeyExpiryInYears >= 0 and KeyExpiryInYears <= 10)
                            "KeyExpiryMustBeFrom0To10"
					
		DeletePGPKeyPair is an Instance Action
			
