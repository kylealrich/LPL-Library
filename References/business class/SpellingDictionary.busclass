SpellingDictionary is a BusinessClass
	owned by la
	prefix is SDICT
    
	Ontology
		symbolic key is SpellingDictionary
    
	Patterns
		disable Auditing
		disable EffectiveDated
		implements CRUD
		
	
	Persistent Fields
		Description			is Alpha 50
  		Type				is Numeric size 1
	  		States
	  			Locale			value is 1
	  			Alternate		value is 2
	  			User			value is 3
		Locale  		
		Dictionary 			is Text
		Version				is Numeric size 6	
	
	Transient Fields
		MimeType			is MimeType
			default label is "MIME_Type"
		DocDictionary		is BinaryDocument
	
	Derived Fields
		IsEnvironment is a NativeField
			type is Boolean
			default label is untranslatable
					
		CanEditUser is a DerivedField
			type is Boolean
			default label is untranslatable
			return (Type.User and IsEnvironment) 
			
	Field Rules
		SpellingDictionary
			if  (Type.Locale)
				if (SpellingDictionary not entered)
					default to Locale
			
				constraint (SpellingDictionary = Locale)
					"NameMustEqualLocaleForLocaleType"
			
		Type
			required
			
		Locale
			if (Type.Locale)
				required	
			else
			if (Type.User)
				cannot be entered
					"LocaleMustBeBlankForUserType"

		
		Dictionary
			if (DocDictionary entered)
				Dictionary = DocDictionary
			
			if (not Type.User)
				required	
  		
  		Version
  			if (Version = blank
  			or  Dictionary changed)
  				Version = Version + 1
  		
  		MimeType
  			initial value is "text/plain"
  			default to "text/plain"
  			constraint (MimeType = "text/plain")
  				"DictionariesMustBePlainText"
  			
  	Conditions
    	IsLocale
    		default label is untranslatable
    		when (Type.Locale) 
    	IsAlternate
    		default label is untranslatable
    		when (Type.Alternate) 
    		
	Actions
		Create is a Create Action
			Action Rules
				if (Type.User
				and not IsEnvironment)
					confirmation required
						"UserDictionariesNormallyResideInTheEnvironmentDataAreaAndThisVersionWillNotBeUsed.AreYouSureYouWantToCreate?"
		
		
		Update is an Update Action
		
		Delete is a Delete Action			
  			
	Sets
		ByTypeLocale
			indexed
			Sort Order
				Type
				Locale
				SpellingDictionary
				
