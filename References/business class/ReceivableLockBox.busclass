ReceivableLockBox is a BusinessClass
	owned by ar
	prefix is ARL
	classic name is ARLOCKBOX
	default label is "ReceivableLockbox"	

	Ontology
		symbolic key is ReceivableLockBox
			classic set name is ARLSET1
			classic name is LOCK-BOX

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		Company				is a ReceivableCompany
		ReceivableProcessLevel
			classic name is PROCESS-LEVEL
		CashCode
		Name
		PostalAddress		is a PostalAddressV2	
			holds pii
			classic name for PostalAddress.DeliveryAddress.AddressLine1 is REMIT-ADDR1
			classic name for PostalAddress.DeliveryAddress.AddressLine2 is REMIT-ADDR2
			classic name for PostalAddress.DeliveryAddress.AddressLine3 is REMIT-ADDR3
			classic name for PostalAddress.DeliveryAddress.AddressLine4 is REMIT-ADDR4
			classic name for PostalAddress.Municipality is CITY
			classic name for PostalAddress.StateProvince is STATE
			classic name for PostalAddress.PostalCode is ZIP
			classic name for PostalAddress.Country is COUNTRY-CODE
		UsedFlag			is Boolean
			classic name is USED-FL
		JournalBook
			classic name is JRNL-BOOK-NBR
			restricted
		Currency
			classic name is CURRENCY-CD
		CompanyCashCode


	Relations
		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company


	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		ReceivableLockBoxExists									
			restricted
			when (ReceivableLockBox exists)

	Derived Fields

		ReceivableLockBoxTitle is a DerivedField				
			type is Alpha size up to 80
			if (ReceivableLockBoxExists)
				return ReceivableLockBoxTitleWithRecord
			else
				return ReceivableLockBoxTitleNoRecord
		
		ReceivableLockBoxTitleNoRecord is a LabelField			
			restricted
			"ReceivableLockbox"	
		
		ReceivableLockBoxTitleWithRecord is a LabelField		
			restricted
			"ReceivableLockbox<ReceivableLockBox>_forCompany<Company>_-<Company.Name>"	

	Sets

		ByCompanyProcessLevel
			not indexed
			Sort Order
				Company
				ReceivableProcessLevel
				ReceivableLockBox

	Field Rules

		Company
			required

		Name
			required

		ReceivableProcessLevel
			required

		CashCode
			default to ReceivableProcessLevel.CashCode

		CompanyCashCode
			required

		Currency
			default to CashCode.Currency
			if (!Company.MultiCurrencyProcessing)
				constraint (Currency = Company.Currency)
					"CompanyIsNotMultiCurrency,CurrCdMust_=Company"		
			constraint (Currency = Company.Currency
			or			Currency =  CashCode.Currency)
				"CurrencyMustEqualEitherCashCodeOrCompanyCurrency."

	Actions
		Create is a Create Action
			completion message is "ReceivableLockboxCreated"	

		Update is an Update Action
			completion message is "ReceivableLockboxUpdated"	

		Delete is a Delete Action
			completion message is "ReceivableLockboxDeleted"	

