ADBDailyAmount is a BusinessClass
    owned by ad
    prefix is ADAMT

    Ontology
    	symbolic key is ADBDailyAmount

    Patterns
 		implements DynamicCreation
		implements LightweightAuditing
		disable AuditIndex
 		disable EffectiveDated
		disable AsOfDateProcessing 		

    Persistent Fields
       	AccountingUnit
		FinanceDimension1
		Amount							is an InternationalAmount		
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
		AdjustmentAmount				is an InternationalAmount		
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
		BalanceOnPostDate				is an InternationalAmount		
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
		BalanceOnTransactionDate		is an InternationalAmount		
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals

	Transient Fields
		PrmAmount						is an InternationalAmount
		PrmAdjustmentAmount				is an InternationalAmount
		PrmBalanceOnPostDate			is an InternationalAmount
		PrmBalanceOnTransactionDate		is an InternationalAmount


	Derived Fields

	Local Fields

	Relations

	Conditions
		DisplayADBInTransactionDate
			restricted
			when (config.UsePostDateAndTranDateInAdb = true)

	Sets				
		ByEntityAccountRunDate
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				AccountingEntity
				ADBDailyAmount.ADBOrganizationDimension
				ADBDailyAmount.GeneralLedgerChartAccount
				ADBDailyAmount.RunDate

		ByBasisRunDate
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				ADBDailyAmount.RunDate
				AccountingEntity
				ADBDailyAmount.ADBOrganizationDimension
				ADBDailyAmount.GeneralLedgerChartAccount

	Field Rules

	Rule Blocks
		UpdateDailyAmount
			Amount						+= PrmAmount
			BalanceOnPostDate			+= PrmBalanceOnPostDate
			AdjustmentAmount			+= PrmAdjustmentAmount
			BalanceOnTransactionDate	+= PrmBalanceOnTransactionDate
			initialize PrmAmount
			initialize PrmAdjustmentAmount
			initialize PrmBalanceOnPostDate
			initialize PrmBalanceOnTransactionDate

	Actions
		Create is a Create Action
			restricted
			bypass field rules
			Action Rules
				if (FinanceEnterpriseGroup.ADBSelection.AccountAndAccountingUnit)
					AccountingUnit			= ADBDailyAmount.ADBOrganizationDimension
				if (FinanceEnterpriseGroup.ADBSelection.AccountAndDimension1)
					FinanceDimension1		= ADBDailyAmount.ADBOrganizationDimension
				include UpdateDailyAmount

		Update is an Update Action
			restricted
			bypass field rules
			Action Rules
				include UpdateDailyAmount

		Delete is a Delete Action
			restricted
			Action Rules

		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis			is like ReportingBasis
					default label is "ReportingBasis"
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and	   ReportingBasis				= PrmReportingBasis)
			Set Is
				PrmFinanceEnterpriseGroup
				PrmReportingBasis
			Action Rules
				Empty Set Rules
				Instance Rules
					invoke Delete 

		Purge is a Purge Action
			restricted

		PurgeData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmReportingBasis				is a ReportingBasis
				PrmAccount						is like GeneralLedgerChartAccount
				PrmStartDate					is Date
				PrmEndDate						is Date
				PrmAccountingEntity						is like AccountingEntity
				PrmADBOrganizationDimension				is like AccountingUnit

			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    (PrmReportingBasis           not entered or PrmReportingBasis = ReportingBasis)
				and    (PrmAccountingEntity         not entered or PrmAccountingEntity = AccountingEntity)
				and    (PrmADBOrganizationDimension not entered or PrmADBOrganizationDimension = ADBDailyAmount.ADBOrganizationDimension)
				and    (PrmAccount                  not entered or PrmAccount = ADBDailyAmount.GeneralLedgerChartAccount)
				and    (PrmStartDate                not entered or ADBDailyAmount.RunDate >= PrmStartDate)
				and    (PrmEndDate                  not entered or ADBDailyAmount.RunDate <= PrmEndDate))

			Action Rules
				Instance Rules
					invoke Purge
