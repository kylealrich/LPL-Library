ProjectSalaryCap is a BusinessClass
    owned by Projects
    prefix is PRJSC

    Ontology
        symbolic key is ProjectSalaryCap

    Persistent Fields
        Description  
		Currency				is a FromCurrency
		MaximumAmount			is a CurrencyAmount
			precision is Currency.NumberOfDecimals
			
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforProjectSalaryCap"
			
		ContextMessageText is a StringField
			type is Alpha 30
			restricted
			"EffectiveDate<Effective Date>"      	

	Relations
		OtherVersionsRel
			one-to-many relation to ProjectSalaryCap
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectSalaryCap		= ProjectSalaryCap
			Instance Selection
				where (related.EffectiveDate != EffectiveDate)
					        
    Field Rules
        Description
            required

		Currency
			required
			if (OtherVersionsRel exists)
				constraint (Currency = first OtherVersionsRel.Currency)
					"CurrencyMustBe<first OtherVersionsRel.Currency>"
			if (Project set exists)
				cannot be changed
					"CannotChangeCurrency;SalaryCapAttachedToProject"
							
		MaximumAmount
			required
			constraint (MaximumAmount > 0)
				"MaximumAmountMustBePositive"

    Actions
        Create is a Create Action
        Update is an Update Action
        Delete is a Delete Action
        
        CreateNewVersion is an Instance Action
        	Parameters
        		NewMaximumAmount 	is an InternationalAmount
        			default label is "MaximumAmount"
				NewEffectiveDate	is Date
					default label is "EffectiveDate"
			Parameter Rules
				NewMaximumAmount
					initial value is MaximumAmount
					required
				NewEffectiveDate
					initial value is current corporate date
					required
			Action Rules
				invoke Create
					fill in fields from this instance
					invoked.EffectiveDate	= NewEffectiveDate
					invoked.MaximumAmount	= NewMaximumAmount



