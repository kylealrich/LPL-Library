ContractType is a BusinessClass
    owned by po
    prefix is CTYP

    Ontology
    	symbolic key is ContractType
   
    Patterns
    	implements StaticJava 	

	Persistent Fields
		Description			 		is a Description4
		PurchaseType
    	CanUseForCostDefaultingOptions is Numeric size 1    
    		default label is "UseForCostDefaultingOptions"
    		States
    			UseAllItems             	value is 1
    			UseOnlyItemMasterItems   	value is 2
    			DoNotUseForCostDefaulting 	value is 3
		Active				 		is Boolean
		SpecialItemOptions			is Numeric size 1			
			States
				KeepAsSpecialItem       value is 1
				CreateItemOnActivation  value is 2
				CreateItemWithProcess   value is 3
		ItemCreationMethod    is Numeric size 1    
            States
                Sequential  value is 1
                Assign      value is 2
                ManualInput value is 3
        AssignItemNumberUsing is Numeric size 1
        	States
        		VendorItem  		value is 1
        		ManufacturerNumber 	value is 2
        		ItemGTIN            value is 3
        		ItemUPC             value is 4
        		ItemSKU             value is 5
        		HIBCCItemUPN        value is 6
        		UCCEANItemUPN       value is 7
        		NationalDrugCode    value is 8
        AllowNoMaxServiceItems is Boolean
			default label is "AllowQuantityServicesOnServiceContractsToBeCreatedWithNoMaximumQuantity"
		
	Conditions
		NotCatalogQuotePurchaseType
			restricted
			when (PurchaseType.Contract
			or	  PurchaseType.Blanket
			or    PurchaseType.Service					
			or    PurchaseType.Standing)
		HasAPurchaseType
			restricted
			when (PurchaseType.Contract
			or	  PurchaseType.Blanket
			or    PurchaseType.Service					
			or    PurchaseType.Standing
			or    PurchaseType.CatalogQuote)
		
		NonServicePurchaseType
			restricted
			when (PurchaseType.Contract
			or	  PurchaseType.Blanket
			or    PurchaseType.Standing
			or    PurchaseType.CatalogQuote)
			
		BlanketOrStandingOrServicesType
			restricted
			when (PurchaseType.Blanket
			or    PurchaseType.Service					
			or    PurchaseType.Standing)
		
		BlanketOrStandingType
			restricted
			when (PurchaseType.Blanket
			or    PurchaseType.Standing)
		
		StandingOrServiceType
			restricted
			when (PurchaseType.Standing
			or    PurchaseType.Service)
		
		ServiceType
			restricted
			when (PurchaseType.Service)
		
		StandingType
			restricted
			when (PurchaseType.Standing)
		
		ContractPurchaseType
			restricted
			when (PurchaseType.Contract)
			
		NoPurchaseType
			restricted
			when (!PurchaseType.Contract
			and	  !PurchaseType.Blanket
			and   !PurchaseType.Service					
			and   !PurchaseType.Standing
			and   !PurchaseType.CatalogQuote)
			
		BlanketType
			restricted
			when (PurchaseType.Blanket)
		
		CatalogQuoteType
			restricted
			when (PurchaseType.CatalogQuote)
		
		ContractOrCatalogType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.CatalogQuote) 

		ContractOrBlanketType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket)
			
		ContractOrBlanketOrServiceType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket
			or    PurchaseType.Service)
			
		ContractOrBlanketOrCatalogType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket
			or    PurchaseType.CatalogQuote)
			
		HasASpecialItemOption
			restricted
			when (SpecialItemOptions.KeepAsSpecialItem
			or    SpecialItemOptions.CreateItemOnActivation
			or    SpecialItemOptions.CreateItemWithProcess)
		
	Relations				
		TypeToContractsRel 
			one-to-many relation to Contract
			Field Mapping uses ByContractType
				related.ContractType          = ContractType
		
		NonClosedContractsForThisTypeRel
			one-to-many relation to Contract
			Field Mapping uses ByContractType
				related.ContractType          = ContractType
			Instance Selection
				where (!related.Contract.ContractStatus.Closed)
		
		ItemGroupRel
			one-to-one relation to ItemGroup
			Field Mapping uses symbolic key
				related.ItemGroup             = ContractGroup
						
	Field Rules
		PurchaseType
					
			if (NonServicePurchaseType)
				constraint (CanUseForCostDefaultingOptions > 0)
					"MustSelectACostDefaultingOption"
					
		Description
			required
		
		CanUseForCostDefaultingOptions
					
			if (!NonServicePurchaseType)
				force default to 3

		Active
			default to true

		SpecialItemOptions
			if  (!NonServicePurchaseType)
				initialize SpecialItemOptions
				
			if  (NonServicePurchaseType)
				default to SpecialItemOptions.KeepAsSpecialItem

			if (CanUseForCostDefaultingOptions.UseOnlyItemMasterItems
			or  CanUseForCostDefaultingOptions.DoNotUseForCostDefaulting)
				force default to SpecialItemOptions.KeepAsSpecialItem 
			
		ItemCreationMethod
			if (ItemGroupRel.AutomaticItemNumbering = true)
				constraint (ItemCreationMethod.Sequential)
					"ItemCreationMethodMustBeSequential;ItemGroupUsesAutomaticItemNumbering"
			else
			if (ItemGroupRel.AutomaticItemNumbering = false)
				constraint (!ItemCreationMethod.Sequential)
					"ItemCreationMethodCannotBeSequential;ItemGroupDoesNotUseAutomaticItemNumbering"
					
			if (SpecialItemOptions.CreateItemOnActivation)
				constraint (ItemCreationMethod > 0)
					"MustSelectAnItemCreationMethodIfSpecialItemOptionsIsCreateItemOnActivation"
			else
			if (!SpecialItemOptions.CreateItemOnActivation)
				force default to 0
				
		AssignItemNumberUsing
			if (ItemCreationMethod.Assign)
				constraint (AssignItemNumberUsing > 0)
					"MustSelectAnAssignItemNumberUsingValueIfItemCreationMethodIsAssign"
			else
			if (!ItemCreationMethod.Assign)
				force default to 0
				
			if (AssignItemNumberUsing = 5)
				constraint (ItemGroupRel.NumericItemNumber = false)
					"CannotAssignItemNumberUsingSKU;ItemGroupRequiresItemNumberToBeNumeric"
						
	Attach Rules
		constraint (Active)
			"ContractTypeIsNotActive"
			 
	Actions
		Create is a Create Action
			Field Rules
				CanUseForCostDefaultingOptions
					if (NonServicePurchaseType)
						default to 1
			
			Action Rules
			
					
		Update is an Update Action	
			Action Rules
			
				if (PurchaseType changed) 
					constraint (!NonClosedContractsForThisTypeRel exists)
						"CannotChangePurchaseType;ContractsExistForThisContractTypeThatAreNotClosed"
					if (!PurchaseType.Service)
						AllowNoMaxServiceItems = false
						
				if (AllowNoMaxServiceItems changed)
					if (AllowNoMaxServiceItems = false)
						constraint (!NonClosedContractsForThisTypeRel exists)
							"CannotSetAllowQuantityServicesOnServiceContractsToBeCreatedWithNoMaximumQuantityToFalse;ContractsExistForThisContractTypeThatAreNotClosed"

				if (Active changed)
					for each ContractSubtype set
						invoke Update each
							invoked.Active = Active
				
		Delete is a Delete Action
			Action Rules
				constraint (!TypeToContractsRel exists)
					"CannotDelete;ContractsExistForThisContractType"
								
