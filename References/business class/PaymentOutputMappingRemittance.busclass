PaymentOutputMappingRemittance is a BusinessClass
    owned by cb
    prefix is POFMR

    Ontology
        symbolic key is PaymentOutputMappingRemittance

    Patterns
        disable AuditIndex
		implements Resequence on Position
			new sequence field is NewPosition
			set is ByPosition
			
    Persistent Fields
		Position		is a DisplayOrder
		MappingType		is Numeric 2
			States
				Field		value is 0
				Literal		value is 1
				Blank		value is 2
		Field			is LPLName
			is related value for "PaymentOutputFileRemittance"
		LiteralName		is a FieldName
			default label is "Name"
		LiteralValue	is Alpha up to 100
		BlankName		is a FieldName
			default label is "Name"
		BlankValue		is Boolean
		OverrideFieldName  is a FieldName
     		default label is "OverrideName"
		
	Local Fields
		LocalRemoveExpression	is Alpha 200		

	Transient Fields
		NewPosition			is a DisplayOrder

	Derived Fields
		DoubleQuote        is a StringField			
            type is Alpha 2
            restricted
            "\""

		DerivedUsingExpression is a StringField		
			type is Alpha 40
			restricted
			" using "
			DoubleQuote
			"<"
			"NumberOfDecimals"
			">"
			DoubleQuote
		
		DerivedRemoveAfter is a StringField								
			type is Alpha up to 30
            restricted
			"\Q"
			"using"
			"\E"
			".*$"
		
		DerivedRemoveString is a StringField							
			type is Alpha up to 30
            restricted
			"\Q"
			"using"
			"\E"

		DerivedTextWithoutExpression is a DerivedField
			type is Alpha up to 200
			if (UsingExpression)
				LocalRemoveExpression = Field
				LocalRemoveExpression -= DerivedRemoveAfter
				LocalRemoveExpression -= DerivedRemoveString
				return LocalRemoveExpression
			return Field

		AddExpressionForNumberOfDecimalsLabel is a LabelField			
			restricted
			"AddExpressionForNumberOfDecimals"

		RemoveExpressionLabel is a LabelField							
			restricted
			"RemoveExpression"

		AddExpressionCheckControlLabel	is a DerivedField				
			type is Alpha 40
			if (UsingExpression)
				return RemoveExpressionLabel
			return AddExpressionForNumberOfDecimalsLabel

	Field Rules
		Position
			autosequence using ByPosition
			
		Field
			if (MappingType.Field)
				required
					"FieldIsRequired"
			else
				initialize
			
			constraint (!DuplicatePaymentOutputMappingRemittanceRel exists)
				"RemittanceField<Field>IsAlreadyDefinedForPaymentOutputMapping<PaymentOutputMapping>"

			if ((Field like "*NetPaymentAmount*"			
			or   Field like "*PaymentAmount*"
			or   Field like "*DiscountAmount*"
			or   Field like "*VATSplitAmount*"
			or   Field like "*UnsignedNetPaymentAmount*"
			or   Field like "*UnsignedPaymentAmount*"
			or   Field like "*UnsignedDiscountAmount*"
			or   Field like "*UnsignedVATSplitAmount*")
			and  !UsingExpression)
				Field += DerivedUsingExpression

		LiteralName
			if (MappingType.Literal)
				required
					"NameIsRequired"
			else
				initialize
		
		LiteralValue
			if (MappingType.Literal)
				required
					"ValueIsRequired"
			else
				initialize

		BlankName
			if (MappingType.Blank)
				required
					"NameIsRequired"
			else
				initialize
		
		BlankValue
			if (MappingType.Blank)
				BlankValue = true
			else
				BlankValue = false

		OverrideFieldName
			if (MappingType.Blank
			or	MappingType.Literal)
				initialize

	Conditions
		UsingExpression										
			restricted
			when (Field like "*using*")
				
		AddExpressionValid									
			restricted
			when (IsUserField
			and   !UsingExpression)

		RemoveExpressionValid								
			restricted
			when (IsUserField
			and   UsingExpression)

		IsUserField											
			restricted
			when (UserFieldRel exists)

	Relations
		DuplicatePaymentOutputMappingRemittanceRel
			one-to-many relation to PaymentOutputMappingRemittance
			Field Mapping uses ByField
				related.CashManagementGroup		= CashManagementGroup
 				related.PaymentOutputMapping	= PaymentOutputMapping
 				related.Field					= Field
 			Instance Selection
 				where (related.PaymentOutputMappingRemittance != PaymentOutputMappingRemittance)
							
		UserFieldRel										
			one-to-one relation to UserField
			Field Mapping uses ByClassField
				related.BusinessClass	= "PaymentOutputFileRemittance"
				related.FieldName		= DerivedTextWithoutExpression

	Sets
		ByPosition
			duplicates
			Sort Order
 				CashManagementGroup
 				PaymentOutputMapping
 				Position
 		
 		ByField
			Sort Order
 				CashManagementGroup
 				PaymentOutputMapping
 				Field
 				PaymentOutputMappingRemittance
 						
	Actions
        AddExpression is an Instance Action			
        	default label is "AddExpressionForNumberOfDecimals"
        	valid when (AddExpressionValid)
        	Action Rules
				if (!UsingExpression)
					Field += DerivedUsingExpression
        		
        RemoveExpression is an Instance Action		
        	valid when (RemoveExpressionValid)
        	Action Rules
				if (UsingExpression)
					LocalRemoveExpression = Field
					LocalRemoveExpression -= DerivedRemoveAfter
					LocalRemoveExpression -= DerivedRemoveString
					Field = LocalRemoveExpression
        		
        Create is a Create Action

        Update is an Update Action
							
        Delete is a Purge Action
