ThirdPartyApplication is a BusinessClass
    owned by ws
    prefix is TPAP
    stored in environment
    contains environment data
	disable data area copy
		preserve target data

	Ontology
		symbolic key is ThirdPartyApplication

	Patterns
        implements CRUD
        disable Auditing
        disable EffectiveDated

    Persistent Fields
        Name                        is Alpha size 100
		URL                         is URL
        OAuthKey                    is Alpha size 200
            default label is "O\AuthKey"
        OAuthSecret                 is Password
            default label is "O\AuthSecret"
        ApplicationType             is Numeric size 2
            States
                InterviewScheduling		        value is 1
        Verified                    is Boolean
        ContactEmail                is Alpha size 100
        DisplayName                 is Alpha size 200
        RedirectURI1                is URL
        RedirectURI2                is URL
        RedirectURI3                is URL

	Transient Fields
	    JSONResult                  is JSONObject
	    AdminKey                    is Alpha size 100
	    OAuthSecretTemp             is Alpha size 100
            default label is "TemporaryO\AuthSecret"


	Derived Fields
        CreateApplicationJSONRequest is a DerivedField
            type is JSONObject
            CreateApplicationJSONRequest = template.InterviewSchedulingCreateApplicationRequest document for this instance

        VerifyApplicationJSONRequest is a DerivedField
            type is JSONObject
            VerifyApplicationJSONRequest = template.InterviewSchedulingVerifyApplicationRequest document for this instance

	Actions
        Create is a Create Action
            Action Rules
                invoke CreateApplication InterviewScheduling
                    invoked.INTERVIEWSCHEDULINGWSIHTTPHEADERAuthorization   = "Bearer " + AdminKey
                    invoked.INTERVIEWSCHEDULINGWSIHTTPHEADERContentType     = "application/json"
                    invoked.CreateApplicationRequest            = CreateApplicationJSONRequest
                    JSONResult                                  = result.CreateApplicationResponse
                Name                = JSONResult select "$.oauth_client.name"
                OAuthKey            = JSONResult select "$.oauth_client.client_id"
                OAuthSecretTemp     = JSONResult select "$.oauth_client.client_secret"
                OAuthSecret         = OAuthSecretTemp







        Update is an Update Action
        Delete is a Delete Action
        VerifyInterviewSchedulingApplication is an Instance Action
            Action Rules
                invoke VerifyApplication InterviewScheduling
                    invoked.INTERVIEWSCHEDULINGWSIHTTPHEADERAuthorization   = "Bearer " + OAuthSecretTemp
                    invoked.INTERVIEWSCHEDULINGWSIHTTPHEADERContentType     = "application/json"
                    invoked.VerifyApplicationRequest            = VerifyApplicationJSONRequest
                    JSONResult                                  = result.VerifyApplicationResponse
                    Verified                                    = true





