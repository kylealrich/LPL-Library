SourcingEventTermAndConditionAttachment is a BusinessClass
    owned by ss
    prefix is SETA
    sql name is SETERMATTACHMENT

	Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Ontology
    	symbolic key is SourcingEventTermAndConditionAttachment

    Persistent Fields
		Attachment
		AttachmentReference is a Description
		DisplayOrder

	Transient Fields
		NewDisplayOrder			is a DisplayOrder
		
	Local Fields
		LocalUserTemplate				 is like UserTemplate
    	
	Derived Fields
		MissingTemplateMsg is a MessageField
			"TheTemplateCouldNotBeFound"
			
		AttachmentTemplate is a DerivedField
			type is XMLDocument
			default label is untranslatable
			restricted
			LocalUserTemplate = "SourcingEventTermsAndConditionsAttachments_ST"
			if (UserTemplateRel exists)
				AttachmentTemplate = template.SourcingEventTermsAndConditionsAttachments_ST document for this instance
			else
				AttachmentTemplate = MissingTemplateMsg 
			return AttachmentTemplate

    Field Rules
    	Attachment
    		required

	Conditions
		HasAttachment
			restricted
			when (Attachment entered)

 	Sets
 		ByDisplayOrder
 			duplicates
 			Sort Order
 				Company
 				SourcingEvent
 				SourcingEventArticle
 				SourcingEventTermAndCondition
 				DisplayOrder
 				SourcingEventTermAndConditionAttachment
 				
 	Relations
		UserTemplateRel
			one-to-one relation to UserTemplate
			Field Mapping uses symbolic key
		 		related.UserTemplate = LocalUserTemplate
    
    Actions
    	Create is a Create Action
			Field Rules
	 			DisplayOrder
	 				autosequence using SourcingEventTermAndCondition.LastAttachmentDisplayOrder 

			Action Rules
    			constraint (SourcingEventTermAndCondition.AllowModification)
					"TermCannotBeModified"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
		
		CreateFromTerm is a Create Action
			restricted
					
    	Update is an Update Action
    		Action Rules
    			constraint (SourcingEventTermAndCondition.AllowModification)
					"TermCannotBeModified"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
		
		Delete is a Delete Action
			Action Rules
				constraint (SourcingEventTermAndCondition.AllowModification)
					"TermCannotBeModified"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
