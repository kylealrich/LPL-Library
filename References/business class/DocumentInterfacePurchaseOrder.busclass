DocumentInterfacePurchaseOrder is a BusinessClass
	owned by ma
	prefix is DIPO

	Ontology
		symbolic key is DocumentInterfacePurchaseOrder

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

	Persistent Fields
        PurchaseOrder					is like PurchaseOrder
		InterfacedPurchaseOrder		  	is like PurchaseOrderImport
		InterfacedPOCode			  	is like POCode
 		CreateDetails					is Numeric 1
 			default label is "CreateDetails"
 			States
 				FromPurchaseOrder				value is 2
 				FromReceipt						value is 3
				FromReceiptThenFromPO			value is 4

	Local Fields
		ErrorMessage				  is Alpha 150
		NewMatchPurchaseOrderInvoice  is a MatchPurchaseOrderInvoice view
		LocalPurchaseOrder            is like PurchaseOrder
	
	Derived Fields

		PurchaseOrderNotFoundMsg is a MessageField
			restricted
			"PurchaseOrderNotFound"

		CannotSelectCreateDetailsMsg is a MessageField
			restricted
			"CannotSelectCreateDetailsIfInterfaceDetailsExist"

		DerivedPurchaseOrder is a DerivedField
			type is like PurchaseOrder

			if  (PurchaseOrder entered)
				return PurchaseOrder
			else
			if (InterfacedPurchaseOrder entered)
				return InterfacedPurchaseOrderRel.PurchaseOrder

	Conditions
		RecordInError
			when (DocumentInterfaceErrorRel exists)

	Relations
		MatchCompanyRel
			one-to-one relation to MatchCompany
			Field Mapping uses symbolic key
				related.Company							= DocumentInterfaceInvoice.Company
				
		PayablesCompanyRel
			one-to-one relation to PayablesCompany
			Field Mapping uses symbolic key
				related.Company							= DocumentInterfaceInvoice.Company

		DocumentInterfaceErrorRel
			one-to-many relation to DocumentInterfaceError
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice
				related.DocumentInterfaceDetail			= blank
				related.DocumentInterfaceDistribution	= blank
				related.DocumentInterfaceAddOnCharge	= blank
				related.DocumentInterfaceComment		= blank
				related.DocumentInterfacePurchaseOrder  = DocumentInterfacePurchaseOrder

		DocumentInterfaceDetailRel
			one-to-many relation to DocumentInterfaceDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice
			Instance Selection
				where (related.PurchaseOrder			= PurchaseOrder
				and    related.InterfacedPurchaseOrder  = InterfacedPurchaseOrder)
				
		DocumentInterfaceAddOnChargeRel
			one-to-many relation to DocumentInterfaceAddOnCharge
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice				
			Instance Selection
				where (related.PurchaseOrder			= PurchaseOrder
				and    related.InterfacedPurchaseOrder  = InterfacedPurchaseOrder)

		PurchaseOrderRel
			one-to-one relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company					= DocumentInterfaceInvoice.Company
				related.PurchaseOrder			= PurchaseOrder

		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
				related.Company						= DocumentInterfaceInvoice.Company
                related.PurchaseOrderImport 		= InterfacedPurchaseOrder
                related.POCode						= InterfacedPOCode


	Sets
		ByPurchaseOrder
			Sort Order
				PurchaseOrder
				DocumentInterfaceInvoice
				DocumentInterfacePurchaseOrder
				
	Field Rules


	Rule Blocks

	Actions
		Create is a Create Action


		Update is an Update Action

		Delete is a Delete Action

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		InterfacePurchaseOrders is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmCompany					is like PayablesCompany
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add            value is "A"
		                MatchSubmitOrRelease value is "R"
				PrmProcessType				is AlphaUpper size 1
		            States
		                MatchInvoices    	value is "M"
		                ExpenseInvoices		value is "E"
		                Both				value is "B"
				PrmVendorClass				is like VendorClass		                
				PrmDocumentInterfaceInvoice is like DocumentInterfaceInvoice
				PrmInterfaceRun				is a PayablesInvoiceInterfaceResult
		 		AutoCreateDetails					is Numeric 1
		 			States
		 				FromPurchaseOrder				value is 2
		 				FromReceipt						value is 3
		 				FromReceiptThenFromPO			value is 4
				AllowMatchItemOrVendorItem	is Boolean
				ErrorOccurred					is Boolean

			Local Fields
				LocalInterfacedPurchaseOrderCount			is Numeric 10
			
			Sort Order
				FinanceEnterpriseGroup
				DocumentInterfaceInvoice
				DocumentInterfacePurchaseOrder
				
			Instance Selection
				where (DocumentInterfaceInvoice.InterfaceRunID = PrmInterfaceRun
				and    DocumentInterfaceInvoice.SystemAssignedInvoice entered)

			Rule Blocks
				SetError
					invoke Create DocumentInterfaceError
						invoked.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.DocumentInterfaceInvoice 		= DocumentInterfaceInvoice
						invoked.DocumentInterfacePurchaseOrder 	= DocumentInterfacePurchaseOrder
						invoked.ErrorMessage					= ErrorMessage
					invoke Update PrmInterfaceRun
						invoked.Status 							= 2
			
			Action Rules
				Empty Set Rules
						
					invoke InterfaceContracts DocumentInterfaceServiceContract
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmCompany					= PrmCompany
						invoked.InvoiceProcess				= InvoiceProcess
						invoked.PrmProcessType				= PrmProcessType
						invoked.PrmVendorClass				= PrmVendorClass
						invoked.PrmDocumentInterfaceInvoice = PrmDocumentInterfaceInvoice
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem

				Set Rules
						
					Exit Rules
						invoke Update PrmInterfaceRun
							invoked.InterfacedPurchaseOrdersCount	+= LocalInterfacedPurchaseOrderCount

						invoke InterfaceContracts DocumentInterfaceServiceContract 
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmCompany					= PrmCompany
							invoked.InvoiceProcess				= InvoiceProcess
							invoked.PrmProcessType				= PrmProcessType
							invoked.PrmVendorClass				= PrmVendorClass
							invoked.PrmDocumentInterfaceInvoice = PrmDocumentInterfaceInvoice
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem

				DocumentInterfaceInvoice Set Rules
				
					Entrance Rules
						ErrorOccurred = false
						if (DocumentInterfaceInvoice.RecordInError)
							ErrorOccurred = true
						

				Instance Rules


					LocalPurchaseOrder = DerivedPurchaseOrder
					
					if  (LocalPurchaseOrder = DocumentInterfaceInvoice.PayablesInvoiceRel.FirstPurchaseOrder)
						invoke FastDelete
					else
					if  (!ErrorOccurred)
						LocalInterfacedPurchaseOrderCount			+= 1
						invoke Create MatchPurchaseOrderInvoice
							assign result to NewMatchPurchaseOrderInvoice
							resume on error
								ErrorOccurred			= true
								ErrorMessage 			= error message
								
							invoked.Company						= DocumentInterfaceInvoice.Company 
							invoked.PayablesInvoice				= DocumentInterfaceInvoice.SystemAssignedInvoice
							invoked.PurchaseOrder				= LocalPurchaseOrder
							invoked.Vendor						= DocumentInterfaceInvoice.Vendor


						if (ErrorOccurred)

							include SetError
						else
							display "MPOI:CreateDetails<DocumentInterfaceInvoice.CreateDetails>AutoCreateDetails<AutoCreateDetails>"
							if ((DocumentInterfaceInvoice.CreateDetails.FromPurchaseOrder
							or  DocumentInterfaceInvoice.CreateDetails.FromReceipt
							or  DocumentInterfaceInvoice.CreateDetails.FromReceiptThenFromPO
							or  AutoCreateDetails.FromPurchaseOrder
							or  AutoCreateDetails.FromReceipt
							or  AutoCreateDetails.FromReceiptThenFromPO)
							and  (!DocumentInterfaceDetailRel exists))


								if (DocumentInterfaceInvoice.CreateDetails.FromPurchaseOrder
								or (DocumentInterfaceInvoice.CreateDetails not entered
								and AutoCreateDetails.FromPurchaseOrder))
									display "FromPO"
									invoke CreateDetailFromPurchaseOrder NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
								else
								if (DocumentInterfaceInvoice.CreateDetails.FromReceipt
								or (DocumentInterfaceInvoice.CreateDetails not entered
								and AutoCreateDetails.FromReceipt))
									display "FromRcpt"
									invoke CreateDetailFromReceipt NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
								if (DocumentInterfaceInvoice.CreateDetails.FromReceiptThenFromPO	
								or (DocumentInterfaceInvoice.CreateDetails not entered
								and AutoCreateDetails.FromReceiptThenFromPO))
									if (DocumentInterfaceInvoice.PayablesInvoiceRel.OpenReceiptsPool2Rel exists
									or  DocumentInterfaceInvoice.PayablesInvoiceRel.OpenReceiptsPool3Rel exists)
										display "FromRcpt"
										invoke CreateDetailFromReceipt NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
									else
										display "FromPO"
										invoke CreateDetailFromPurchaseOrder NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice

							invoke FastDelete

