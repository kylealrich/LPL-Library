GeneralLedgerSystemCode is a BusinessClass
	default label is "GlobalLedgerSystemCode"
	owned by GeneralLedger
	prefix is GLSYS
	representative text is "<GeneralLedgerSystemCode>-<Description>"	   

	Ontology
		symbolic key is GeneralLedgerSystemCode           	

    Patterns
        implements StaticJava
        implements BODId
	
	Persistent Fields
		Description
		RecordType
		EncumbranceOption			is Numeric 2
			States
				NotUsed					value is 0
				Track					value is 1
				TrackAndEdit			value is 2
		LastJournalizeGroup			is Numeric 12
			disable Auditing
		LastCommitment				is Numeric 16
			disable Auditing

	Derived Fields

		DerivedJournalizeGroup is a DerivedField
			type is AlphaUpper 30
			restricted
			if (LastJournalizeGroup > 99999999999)
				LocalAlphaJournalizeGroup = LastJournalizeGroup
			else
			if (LastJournalizeGroup > 9999999999)
				LocalAlphaJournalizeGroup = "0" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 999999999)
				LocalAlphaJournalizeGroup = "00" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 99999999)
				LocalAlphaJournalizeGroup = "000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 9999999)
				LocalAlphaJournalizeGroup = "0000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 999999)
				LocalAlphaJournalizeGroup = "00000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 99999)
				LocalAlphaJournalizeGroup = "000000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 9999)
				LocalAlphaJournalizeGroup = "0000000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 999)
				LocalAlphaJournalizeGroup = "00000000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 99)
				LocalAlphaJournalizeGroup = "000000000" + LastJournalizeGroup
			else
			if (LastJournalizeGroup > 9)
				LocalAlphaJournalizeGroup = "0000000000" + LastJournalizeGroup
			else
				LocalAlphaJournalizeGroup = "00000000000" + LastJournalizeGroup	
															
			return GeneralLedgerSystemCode + LocalAlphaJournalizeGroup
									
		DerivedCommitmentGroup is a DerivedField
			type is AlphaUpper 30
			restricted
			if (LastCommitment > 99999999999)
				LocalAlphaCommitmentGroup = LastCommitment
			else
			if (LastCommitment > 9999999999)
				LocalAlphaCommitmentGroup = "0" + LastCommitment
			else
			if (LastCommitment > 999999999)
				LocalAlphaCommitmentGroup = "00" + LastCommitment
			else
			if (LastCommitment > 99999999)
				LocalAlphaCommitmentGroup = "000" + LastCommitment
			else
			if (LastCommitment > 9999999)
				LocalAlphaCommitmentGroup = "0000" + LastCommitment
			else
			if (LastCommitment > 999999)
				LocalAlphaCommitmentGroup = "00000" + LastCommitment
			else
			if (LastCommitment > 99999)
				LocalAlphaCommitmentGroup = "000000" + LastCommitment
			else
			if (LastCommitment > 9999)
				LocalAlphaCommitmentGroup = "0000000" + LastCommitment
			else
			if (LastCommitment > 999)
				LocalAlphaCommitmentGroup = "00000000" + LastCommitment
			else
			if (LastCommitment > 99)
				LocalAlphaCommitmentGroup = "000000000" + LastCommitment
			else
			if (LastCommitment > 9)
				LocalAlphaCommitmentGroup = "0000000000" + LastCommitment
			else
				LocalAlphaCommitmentGroup = "00000000000" + LastCommitment	
															
			return GeneralLedgerSystemCode + LocalAlphaCommitmentGroup


#ifdef module integration
		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value


		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or BODActionCode.Create)
				return "Add"
			else
			if(action type.Delete or BODActionCode.Delete)
				return "Delete"
			else
			if(action type.Update)
				return "Replace"
			else
				return ""
		
		DerivedBODDocumentID is a DerivedField
			type is Alpha size 45
			restricted
			return FinanceEnterpriseGroup + DerivedDelimiter + GeneralLedgerSystemCode + DerivedDelimiter + "S"
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
							
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID + ":" + ":"  + ":" + DerivedBODDocumentID+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
			
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor	
		
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
			
		DerivedRecordType is a DerivedField
			type is Alpha 10
			restricted
			if(RecordType = "0")
				return "User"
			else
			if(RecordType = "1")
				return "System"
			else
				return ""
	            		
		GeneralLedgerSystemCodeXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	        GeneralLedgerSystemCodeXMLBOD = template.IONSyncCodeDefination_GeneralLedgerSystemCode_ST document for this instance
#endif

									
	Local Fields
		LocalAlphaJournalizeGroup	is Alpha 12
		LocalAlphaCommitmentGroup	is Alpha 12
		BODActionCode				is Alpha size 1
			States
				Create  value is "C"
				Update	value is "U"
				Delete	value is "D"
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		LocalNativeLPLBODTrigger		is Boolean	
#ifdef module integration			
		NewBODTracker  				is a FSMInboundBODTracker view
#endif  
		LocalFSMInboundBODTracker	is Numeric 15
		Error            			is Boolean
	    ErrorMessage     			is Alpha 300
		LocalBODTrigger				is Boolean
		LocalConfigurationParameter	is Alpha size up to 200
		
	Context Fields
#ifdef module integration
		FSMInboundBODTracker
#endif  

	Conditions
        LawsonDefinedSystemCode
        	restricted
            when (RecordType.System)
           
        UserDefinedSystemCode
        	restricted
            when (RecordType.User)
           
		CommitmentsTracked      
			restricted    
        	when (!EncumbranceOption.NotUsed)
		           
		CommitmentSystemCode
			restricted
			when (GeneralLedgerSystemCode	= "PS"
			or    GeneralLedgerSystemCode 	= "AP"		    
			or    GeneralLedgerSystemCode	= "PO"		    
			or    GeneralLedgerSystemCode	= "RQ"		    
			or    GeneralLedgerSystemCode	= "PR"		    
			or    GeneralLedgerSystemCode	= "PA"		    
			or    GeneralLedgerSystemCode	= "CA"		    
			or    GeneralLedgerSystemCode	= "CB"		    
			or    GeneralLedgerSystemCode	= "EE"	
			or    GeneralLedgerSystemCode	= "GL"		    
			or    GeneralLedgerSystemCode	= "AR"		    
			or    GeneralLedgerSystemCode	= "BL"		    
			or    GeneralLedgerSystemCode	= "OE"		    
			or    GeneralLedgerSystemCode	= "RJ"		    																    															
			or    GeneralLedgerSystemCode	= "GM")
		BudgetSystemCode
			restricted
			when (GeneralLedgerSystemCode	= "PS"
			or    GeneralLedgerSystemCode 	= "AP"		    
			or    GeneralLedgerSystemCode	= "PO"		    
			or    GeneralLedgerSystemCode	= "RQ"		    
			or    GeneralLedgerSystemCode	= "CA"		    
			or    GeneralLedgerSystemCode	= "CB"		    
			or    GeneralLedgerSystemCode	= "EE"	
			or    GeneralLedgerSystemCode	= "GL"		    
			or    GeneralLedgerSystemCode	= "RJ")
		CommitmentOrBudget
			restricted
			when (CommitmentSystemCode
			or    BudgetSystemCode
			or	  UserDefinedSystemCode)	    																    															

		IsCBSystemCode
			restricted
			when (GeneralLedgerSystemCode	= "CB"
			or    GeneralLedgerSystemCode 	= "AP")
				
	Relations
		APSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode = "AP"
				
		POSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode = "PO"
			
		RQSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode = "RQ"
				
		GLCommitRel
			one-to-many relation to GLCommit
			Field Mapping uses BySystemAndStatus
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.System					= GeneralLedgerSystemCode
			Instance Selection
				where (related.Status			= 0
				or     related.Status			= 3)
				

#ifdef module integration
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
				
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
#endif


		BudgetTemplateInProcesingRel
			one-to-many relation to BudgetTemplate
			Field Mapping uses ByStatus
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.Status								= 1 		
	Field Rules
	
		GeneralLedgerSystemCode
			if	(GeneralLedgerSystemCode		= "RQ"
			and	 !EncumbranceOption.NotUsed)


				constraint (!POSystemCodeRel.EncumbranceOption.NotUsed)
					"P\O_SystemEncumbranceMustBeEnabled"
			
			if	(GeneralLedgerSystemCode		= "PO"
			and	 !EncumbranceOption.NotUsed)
				constraint (!APSystemCodeRel.EncumbranceOption.NotUsed)
					"A\P_SystemEncumbranceMustBeEnabled"

			if	(GeneralLedgerSystemCode		= "PO"
			and	 EncumbranceOption.NotUsed)
				constraint (RQSystemCodeRel.EncumbranceOption.NotUsed)
					"R\Q_SystemEncumbranceMustBeDisabled"

			if	(GeneralLedgerSystemCode		= "AP"
			and	 EncumbranceOption.NotUsed)
				constraint (RQSystemCodeRel.EncumbranceOption.NotUsed)
					"R\Q_SystemEncumbranceMustBeDisabled"
				constraint (POSystemCodeRel.EncumbranceOption.NotUsed)
					"P\O_SystemEncumbranceMustBeDisabled"

		Description
			required

		RecordType
			initial value is RecordType.User

		EncumbranceOption
			if (EncumbranceOption.TrackAndEdit)
				constraint (BudgetSystemCode
				or			UserDefinedSystemCode)
					"CannotTrackAndEditThisSystemCode"
				constraint (BudgetTemplateInProcesingRel not exists)
					"CannotSetToTrackAndEdit;BudgetTemplatesAreCurrentlyProcessing"

			if (EncumbranceOption.Track)
				constraint (CommitmentSystemCode
				or			UserDefinedSystemCode)
					"CannotTrackThisSystemCode"
					
			if  (EncumbranceOption changed
			and  GLCommitRel exists)
				constraint (!EncumbranceOption.NotUsed)
					"CannotChange_\Encumbrance_\Option;CommitmentsExist"	





	Rule Blocks			

		TriggerSystemCodeServiceRules
    		trigger "GeneralLedgerSystemCodeService" PA service
				resume on error
				title is "EG:<FinanceEnterpriseGroup>SystemCode:<GeneralLedgerSystemCode>"
				Criteria
					FinanceEnterpriseGroup
				Variables
					BODActionCode
					include persistent fields from GeneralLedgerSystemCode
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
								
    Actions
		Create is a Create Action
			Field Rules
				RecordType
					default to RecordType.User



		AutoCreate is a Create Action
			restricted
			Field Rules
				RecordType
					default to RecordType.System

		Update is an Update Action

		Delete is a Delete Action
			Entrance Rules
				if (FinanceEnterpriseGroup.BODTrigger)
					BODActionCode	= BODActionCode.Delete
					increment bod id.VariationID
					include TriggerSystemCodeServiceRules



			Action Rules
				constraint (UserDefinedSystemCode)
					"CannotDeleteSystemDefinedSystemCode"
						
		DeleteFromFinanceGroup is a Delete Action
			restricted
	
		IncrementLastJournalizeGroup is an Instance Action
			restricted
			refresh and lock this instance
	     	
	     	Action Rules
	     		LastJournalizeGroup += 1
	     		
	    UpdateBODIdFields is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord

#ifdef module integration
		SendGeneralLedgerSystemCodeBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is GeneralLedgerSystemCodeXMLBOD
					bod type is "Sync.CodeDefinition"
					document id is DerivedBODDocumentID
					variation id is	BODVariationID

		TriggerGeneralLedgerSystemCodeNativeLPLBOD is an Instance Action			
			restricted
			Entrance Rules
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "GeneralLedgerSystemCode"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_GeneralLedgerSystemCode_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)								
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= DerivedBODDocumentID
							invoked.BODVariationID			= BODVariationID
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.Reference1				= GeneralLedgerSystemCode
							invoked.Reference2              = "GeneralLedgerSystemCode"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= DerivedBODDocumentID
							invoked.BODVariationID			= BODVariationID
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1	
							invoked.Reference1				= GeneralLedgerSystemCode
							invoked.Reference2              = "GeneralLedgerSystemCode"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendGeneralLedgerSystemCodeBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= GeneralLedgerSystemCodeXMLBOD
							invoked.BODID					            = DerivedBODFormattedId
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODXML									= GeneralLedgerSystemCodeXMLBOD
							invoked.BODID					                = DerivedBODFormattedId
#endif  
		
					
	Action Exit Rules
		if (FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					BODActionCode	= BODActionCode.Update
					if (action type.Create)
						BODActionCode	= BODActionCode.Create				
					else
						if (action type.Delete)
							BODActionCode	= BODActionCode.Delete
					increment bod id.VariationID
					include TriggerSystemCodeServiceRules



