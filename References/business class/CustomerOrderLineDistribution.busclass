CustomerOrderLineDistribution is a BusinessClass
    owned by oe
    prefix is OEMMD
    
    Ontology
    	symbolic key is CustomerOrderLineDistribution


	Persistent Fields
		AssetInformation
        DistributionPercent				is Percent size 6.3
        DistributionQuantity			is a Quantity
        PostingType                 	is AlphaUpper size 2
            States
                Inventory           value is "I1"
                Offset              value is "O1"
                RniExpenseAccount   value is "E1"
                ReceivedNotInvoiced value is "RN"
                MatchedNotReceived  value is "MN"
                AddOnCost           value is "A"

	Transient Fields
		DistributionAccount		 		is a TransactionCodeBlock
			derive value from CustomerOrderLine.ExpenseAccount
	
	Local Fields
		LocalDistributionAccount		is like CurrencyAmount
		LocalExchangeDate				is an ExchangeDate

	Derived Fields
		DistributionAmount		 		is a DerivedField
			type is like CurrencyAmount
			if (DistributionQuantity entered)
				LocalDistributionAccount	= DistributionQuantity * CustomerOrderLine.EnteredPrice
			else
			if (DistributionPercent entered)
				LocalDistributionAccount	= ((DistributionPercent * CustomerOrderLine.OrderQuantity) * CustomerOrderLine.EnteredPrice)
			return LocalDistributionAccount
			
	Conditions
		DistributionByQuantity
			restricted
			when (first OtherCustomerOrderLineDistributionsRel.DistributionQuantity entered)
		
		DistributionByPercent
			restricted
			when (first OtherCustomerOrderLineDistributionsRel.DistributionPercent entered)

		DistributionByAmount
			restricted
			when (!DistributionByQuantity
			and   !DistributionByPercent)
					
	Relations
		OtherCustomerOrderLineDistributionsRel
			one-to-many relation to CustomerOrderLineDistribution
			Field Mapping uses symbolic key
				related.Company				= Company
				related.CustomerOrder		= CustomerOrder
				related.CustomerOrderLine	= CustomerOrderLine
			Instance Selection
				where (related.CustomerOrderLineDistribution != CustomerOrderLineDistribution)

	Field Rules
		DistributionAccount
			required

		DistributionPercent
			force default to 100%

		DistributionQuantity
			if (DistributionByAmount
			or  DistributionByPercent)
				cannot be entered
					"CannotEnterDistributionQuantity;DistributionPercentOrAmountEntered"
			else
			if (DistributionPercent entered
			or  DistributionAmount entered)
				cannot be entered
					"CannotEnterDistributionQuantity;DistributionPercentOrAmountEntered"
			else
			if (DistributionByQuantity)
				required

				constraint (DistributionQuantity <= CustomerOrderLine.OrderQuantity)
					"DistributionQuantityCannotExceedCustomerOrderLineOrderQuantity"

	Actions
		Create is a Create Action
			Entrance Rules
				LocalExchangeDate = CustomerOrder.OrderDate

		Update is an Update Action
			Entrance Rules
				LocalExchangeDate = CustomerOrder.OrderDate

		Delete is a Delete Action
