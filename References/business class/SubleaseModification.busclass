SubleaseModification is a BusinessClass
	owned by lm
	prefix is SLMOD

	Ontology
		symbolic key is SubleaseModification
	
	Patterns
		implements Archivable

	Persistent Fields
		Status											is Numeric 1
			States
				Unreleased					value is 1
				Released					value is 2
		ModificationDate								is Date
		Description
		TermModification								is Boolean
		TermModificationType							is Numeric 2
			States
				Extension					value is 1
				Reduction					value is 2
		TermModificationMonths							is Numeric 3
		OriginalTerm									is Numeric 3
		OriginalEndDate									is Date
		Term
		BeginDate										is an ExchangeDate
		EndDate											is Date
		CompanyAndCustomer

	Transient Fields
		Currency										is a FromCurrency
			derive value from Sublease.Currency
		BaseCurrency									is a ToCurrency
			derive value from Sublease.BaseCurrency

	Derived Fields
		DerivedTerm is a DerivedField
			type is Numeric 3
			if (TermModification)
				if (TermModificationType.Extension)
					return (Sublease.Term + TermModificationMonths)
				else
					return (Sublease.Term - TermModificationMonths)
			else
				return (Sublease.Term)

		DerivedEndDate is a DerivedField
			type is Date
			if (TermModification)
				if (TermModificationType.Extension)
					return (Sublease.EndDate + TermModificationMonths as months)
				else
					return (Sublease.EndDate - TermModificationMonths as months)
			else
				return (Sublease.EndDate)

	Relations
		UnreleasedBillingScheduleDetailsBeforeModificationDateRel is a SubleaseBillingScheduleDetail set
			Instance Selection
				where (related.InvoiceDate <= ModificationDate
				and !related.Released)

		ReleasedBillingScheduleDetailsAfterModificationDateRel is a SubleaseBillingScheduleDetail set
			Instance Selection
				where (related.InvoiceDate > ModificationDate
				and related.Released)

	Conditions
		HasDifferentCurrencies
			when (Sublease.Currency != Sublease.BaseCurrency)

	Sets

		Set1
			indexed
			Sort Order
				CompanyAndCustomer.Company
				Sublease
				CompanyAndCustomer.Customer
				SubleaseModification

	Field Rules
		ModificationDate
			required
				"MustEnterModificationDateWhenModifyingASublease"
			constraint (ModificationDate < Sublease.EndDate)
				"ModificationDateHasToBeBeforeItsScheduledEndDate;ConsiderClosingTheSublease"

		Description
			default to Sublease.Description

		TermModificationType
			if (TermModification)
				required
					"TermModificationTypeIsRequired"
			else
				initialize

		TermModificationMonths
			if (TermModification)
				required
					"MustEnterNumberOfMonthsYouWouldLikeToExtendOrReduceTheSublease"
			else
				initialize
			constraint (TermModification > 0)
				"NumberOfMonthsMustBeGreaterThanZero"

		OriginalTerm
			initial value is Sublease.Term
			default to Sublease.Term

		Term
			initial value is Sublease.Term
			if (TermModification)
				Term = DerivedTerm
			else
				Term = Sublease.Term

		BeginDate
			initial value is Sublease.BeginDate

		EndDate
			initial value is Sublease.EndDate
			if (TermModification)
				EndDate = DerivedEndDate
			else
				EndDate = Sublease.EndDate
				
	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted		
