ARToAPApplicationSelect is a BusinessClass
	owned by ar
	prefix is AAI
	classic name is ARAPSELECT

	Ontology
		symbolic key is ARToAPApplicationSelect
			classic set name is AAISET1
			classic name for ARToAPApplicationSelect.BatchNumber is BATCH-NBR
			classic name for ARToAPApplicationSelect.TransactionType is TRANS-TYPE

	Patterns
		implements StaticJava
		disable AuditIndex
		implements Archivable

	Persistent Fields

		Customer
		Status				is Numeric size 1
			States
				Unreleased	value is 0
				Released	value is 1
		ApInvoice			is AlphaUpper size 22
		BaseAmount			is an InternationalAmount
			classic name is BASE-AMT
			precision is Company.Currency.NumberOfDecimals
		TransactionAmount	is an InternationalAmount
			classic name is ORIG-AMT
		DisputeIndicator	is AlphaUpper size 1
			classic name is DISPUTE-FL
		CommentIndicator	is AlphaUpper size 1
			classic name is COMMENT-FL
		TransactionDate		is Date
			classic name is TRANS-DATE
		DueDate
		Currency
			classic name is CURRENCY-CODE
			default label is "TransactionCurrency"	
		Project				is a FinanceCodeBlockProjectOnly
			classic name is ACTIVITY

	Local Fields
		LocalProcessLevel		is like ReceivableProcessLevel


	
	Transient Fields
		TransSignedBaseAmount			is like InternationalAmount
			precision is Company.Currency.NumberOfDecimals
			derive value from SignedBaseAmount
		TransDerivedSignedBaseAmount	is like InternationalAmount
			precision is Company.Currency.NumberOfDecimals
			derive value from DerivedSignedBaseAmount
		TransientToCurrency				is a ToCurrency

	Derived Fields
		Affiliate is a DerivedField
			type is Boolean
			return first Customer.CompanyCustomer set.Affiliate

		SignedBaseAmount is a DerivedField
			type is like InternationalAmount
				precision is Company.Currency.NumberOfDecimals
			if (ARToAPApplicationSelect.TransactionType.CreditMemo
			or  ARToAPApplicationSelect.TransactionType.CustomerPayment)
				return BaseAmount * -1
			else
				return BaseAmount

		DerivedSignedBaseAmount is a DerivedField
			type is like InternationalAmount
				precision is Company.Currency.NumberOfDecimals
			restricted
			if (ARToAPApplicationSelect.TransactionType.CreditMemo)
				return BaseAmount * -1
			else
				return BaseAmount

	Conditions
		IsCashOperatorRequired
			restricted
			when (Company.CashOperatorRequired)

		IsReceivableTransaction
			classic name is OPEN-ITEMS
			restricted
			when (ARToAPApplicationSelect.TransactionType.CreditMemo
			or	ARToAPApplicationSelect.TransactionType.DebitMemo
			or	ARToAPApplicationSelect.TransactionType.Invoice)

		IsReceivablePayment
			classic name is PAYMENTS
			restricted
			when (ARToAPApplicationSelect.TransactionType.CustomerPayment)

	Sets

		Set2
			indexed
			Instance Selection
				where (IsReceivableTransaction)
			Sort Order
				Company
				ARToAPApplicationSelect.TransactionType
				ARToAPApplicationSelect.Invoice
				ARToAPApplicationSelect.PaymentSeq

		Set3
			indexed
			Instance Selection
				where (IsReceivablePayment)
			Sort Order
				Company
				Customer
				ARToAPApplicationSelect.BatchNumber
				ARToAPApplicationSelect.PaymentSeq

		Set4
			indexed
			Sort Order
				Company
				Customer
				Status
				ARToAPApplicationSelect

	Relations
		ReceivableInvoiceDetailRel
			one-to-one relation to ReceivableInvoiceDetail
			Field Mapping uses Set5
				related.Company			= Company
				related.Customer		= Customer
				related.ReceivableInvoiceDetail.ReceivableInvoiceDetailType = ARToAPApplicationSelect.TransactionType
				related.ReceivableInvoiceDetail.Invoice 					= ARToAPApplicationSelect.Invoice
				related.ReceivableInvoiceDetail.PaymentSeq 					= ARToAPApplicationSelect.PaymentSeq


		ReceivablePaymentRel
			one-to-one relation to ReceivablePayment
			Field Mapping uses symbolic key
				related.Company					= Company
				related.ReceivablePaymentHeader	= ARToAPApplicationSelect.BatchNumber
				related.ReceivablePayment		= ARToAPApplicationSelect.PaymentSeq










		ARToAPProcessLevelRel
			one-to-one relation to ReceivableProcessLevel
			Field Mapping uses symbolic key
				related.Company					= Company
				related.ReceivableProcessLevel	= LocalProcessLevel

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
			confirmation required
				"AllSelectedRecordsWillBeDeleted;PressEnterToDelete"		

 		DeleteWithoutConfirmation is a Delete Action
 			restricted

		Purge is a Purge Action
			restricted


