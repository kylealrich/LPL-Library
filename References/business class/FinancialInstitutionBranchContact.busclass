FinancialInstitutionBranchContact is a BusinessClass
	owned by cashmgmt
	prefix is CFIBC
	sql name is BranchContact
	 
	Ontology
		symbolic key is FinancialInstitutionBranchContact
	
	Patterns 
    	implements StaticJava
    	disable AuditIndex
	
	Persistent Fields
		ContactInformation					is a FullContactInfo
		Comments							is Text
		AddressDifferentFromBranchAddress	is Boolean
		Attachment							is an AlternateAttachment

	Local Fields
		LocalAttributeCtr   is Numeric 2

	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 50
			restricted
			"InforFinancialInstitutionBranchContact"

		ContextMessageText is a MessageField
			restricted
			"FinancialInstitutionBranchContact<FinancialInstitutionBranchContact>"
	
	Context Fields
		CashManagementAccount
		
	Field Rules
		FinancialInstitutionBranchContact
			constraint (ContactInformation.PersonName.GivenName entered)
				"FirstNameIsRequired"
			
			constraint (ContactInformation.PersonName.FamilyName entered)
				"LastNameIsRequired"

		ContactInformation
			if (AddressDifferentFromBranchAddress)
				constraint (ContactInformation.ContactInfo.PostalAddress entered)
					"AddressIsRequired"
		
		AddressDifferentFromBranchAddress
			if (!AddressDifferentFromBranchAddress)
				initialize ContactInformation.ContactInfo.PostalAddress

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
				
	Conditions
		HasAttachment
			restricted
			when (Attachment entered)
			
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
