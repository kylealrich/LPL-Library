CashCategoryDetail is a BusinessClass	
    owned by cb
    prefix is CCDtl

    Ontology
        symbolic key is CashCategoryDetail

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        RevenueAccount 				is a FinanceCodeBlock  
        TaxCode

	Local Fields
		CalculateTax
		TaxOut 						is Alpha 1
		TaxTableOutput
		I1							is Numeric 2		

	Relations
		CashCategoryDetailByEntityRel
			one-to-many relation to CashCategoryDetail		
			Field Mapping uses CategoryByEntity
				related.CashManagementGroup 				= CashManagementGroup
				related.CashCategory						= CashCategory
				related.CashProcessLevel					= CashProcessLevel
				related.RevenueAccount.ToAccountingEntity 	= RevenueAccount.ToAccountingEntity
			Instance Selection								
				where (related.UniqueID != UniqueID)		
				
	Sets
		CategoryByEntity
			indexed
			Sort Order
				CashManagementGroup
				CashCategory
				CashProcessLevel
				RevenueAccount.ToAccountingEntity
			
	Rule Blocks		
		CalculateTax
			initialize CalculateTax
			CalculateTax.Function 		= "I"
			CalculateTax.TaxCode 		= TaxCode
			CalculateTax.TaxEntity		= RevenueAccount.ToAccountingEntity
			TaxOut            			= CalculateTax.ExecuteCalculateTax
			TaxTableOutput    			= CalculateTax.TaxTable.TaxTableOutput[1]
			constraint (CalculateTax.OutputErrorNumber not entered)
                "<CalculateTax.OutputErrorMessage>"	
			I1 = 1
			while (I1 <= 8)
            	constraint (CalculateTax.TaxTable.TaxTableOutput[I1].TaxPoint not = "S")
            		"TaxPointCannotBe_\S"  
				I1 += 1                

	Field Rules
		RevenueAccount
			required 
				
		TaxCode
			include CalculateTax

	Actions
		Create is a Create Action
			Entrance Rules
				constraint (CashCategoryDetailByEntityRel not exists)
					"AccountStructureAlreadyExistsForToAccountingEntity<RevenueAccount.ToAccountingEntity>"
						
		Update is an Update Action
			Entrance Rules
				if (RevenueAccount changed)
					constraint (CashCategoryDetailByEntityRel not exists)
						"AccountStructureAlreadyExistsForToAccountingEntity<RevenueAccount.ToAccountingEntity>"
							
		Delete is a Delete Action
				

