IDMJob is a BusinessClass
	owned by idm
	
	prefix is IDMJ
	
	Ontology
		symbolic key is IDMJob
		
	Patterns
		implements StaticJava
	
	Persistent Fields
		Actor
			default label is "SubmittedBy"
		Description
		CreatedDate			is TimeStamp
		Status				is Numeric size 1 
			default label is "JobStatus"
			States
				Submitted	value is 0
				Failed		value is 1
				Finished	value is 2
				TimedOut	value is 3
		FileName			is an IDMFileName
		DocumentType		is an IDMDocumentType
		PID					is an IDMPID 
		MDSID				is an IDMPID 
			default label is "DocumentID"
		JobID				is an IDMPID
		PrintStatus			is Alpha size 1
			States
				NotApplicable	value is blank
				Submitted		value is "S"
				Failed			value is "F"
				TimedOut		value is "T"
				Finished		value is "P" 
		PrintErrorMessage	is a Description4 
		EmailStatus			is Alpha size 1
			States
				NotApplicable	value is blank
				Submitted		value is "S"
				Failed			value is "F"
				TimedOut		value is "T"
				Finished		value is "P" 
		EmailErrorMessage	is a Description4 
		LongDescription		is Text
			default label is "Details"
		GenerationStatus	is Alpha size 1
			States
				NotApplicable	value is blank
				Submitted	    value is "S"
				Failed			value is "F"
				TimedOut		value is "T"
				Finished		value is "P"
		ErrorMessage			is Text
        PrinterType                is Numeric 1 
            States
                GCP            value is 0
                Email          value is 1
                IEP            value is 2
        Printer 				is like IDMPrinter
        	default label is "IDMPrinter"
		PrinterSettings			is like IDMPrinterSettings
        PrinterName				is Text
        PrinterID				is an IDMPrinterID
        PrinterLocation			is Text
        PrinterEmailAddress		is an EmailAddress
        EmailFrom 				is an EmailAddress
        	default label is "From"
        EmailTo					is an EmailAddressMulti 
			protected
			holds pii
        	default label is "To"
        EmailCc					is an EmailAddressMulti 
			protected
			holds pii
        	default label is "Cc"
        EmailBc					is an EmailAddressMulti 
			protected
			holds pii
        	default label is "Bcc"
        EmailSubject			is Alpha 1000
        	default label is "Subject"
        AdditionalDetails		is Text
		BatchID					is an IDMPID
		EmailToMulti			is a MultipleEmailAddress 
			protected
			holds pii
			default label is "To"
		EmailCcMulti			is a MultipleEmailAddress 
			protected
			holds pii	
			default label is "Cc"			
		EmailBcMulti            is a MultipleEmailAddress 
			protected
			holds pii
			default label is "Bcc"
		OverrideUser			is Boolean

	Transient Fields
		TransientLink is Alpha size 2083
			derive value from DerivedIDMDocumentURL
		IDMGenerateDocument
		IDMSendEmail
		IDMItem
		BypassGetStatus			is Boolean
		ConsolidateIDMNotification	is Boolean
			
	Local Fields
		LocalExecute			is Boolean
		LocalGotIDMDocumentURL	is Boolean
		AsyncId					is a AsyncActionRequest
		IDMPrinter
		IDMPrinterSettings
		IDMUniqueIDOccurs 

	Relations
		PrinterSettingsRel
			one-to-one relation to IDMPrinterSettings
			Field Mapping uses symbolic key
				related.IDMPrinter	 		= Printer
				related.IDMPrinterSettings	= PrinterSettings
		
	Derived Fields
		DerivedInputDataURL		is a DerivedField
    		type is Alpha 2083
    		if (JobID entered)
    			IDMItem.DocumentType	= "MDS_DistributionJobs"
    			IDMItem.IDMUniqueId		= JobID
    			return IDMItem.GetLink
    		return blank
    		
    	DerivedJobDrillBackURL		is a DerivedField
    		type is Alpha 2083 
    		if (JobID entered)
    			IDMItem.DocumentType	= "MDS_DistributionJobs"
    			IDMItem.IDMUniqueId		= JobID
    			if (IDMItem.GetItemDetails)
    				IDMItem.IDMPID = IDMItem.IDMItemDetails.PID
					return IDMItem.DerivedDrillBackLink
    		return blank	
	
		DerivedIDMDocumentURL	is a DerivedField
    		type is Alpha 2083
    		if (not LocalGotIDMDocumentURL and (MDSID entered or PID entered))
	    		if (MDSID entered)
	    			IDMItem.DocumentType	= DocumentType
	    			IDMItem.IDMUniqueId		= MDSID
	    			LocalGotIDMDocumentURL 	= true
	    			return IDMItem.GetLink
	    		else 
	    		if (PID entered)
	    			IDMItem.IDMPID			= PID 
	    			LocalGotIDMDocumentURL 	= true
	    			return IDMItem.GetLink
    		return blank
 
 		DerivedJobDescription	is a DerivedField
 			type is Text
 			default label is "Description"
 			if (LongDescription entered)
 				return LongDescription
 			else
 				return Description

		DerivedEmailTo is a DerivedField  
			type is like MultipleEmailAddress
			default label is "EmailTo"
			holds pii
			if (EmailToMulti entered)
				return EmailToMulti
			else
			if (EmailTo entered)
				return EmailTo

		DerivedEmailCc is a DerivedField 
			type is like MultipleEmailAddress
			default label is "EmailCc"
			holds pii
			if (EmailCcMulti entered)
				return EmailCcMulti
			else
			if (EmailCc entered)
				return EmailCc

    Sets
    	ByCreateDate
    		Sort Order
    			CreatedDate
    			FileName
    			IDMJob
		ByBatchID
			Sort Order
				Actor
				IDMJob
				BatchID
    		
    Conditions
    	DisplayPrintStatus
    		restricted
    		when (not PrintStatus.NotApplicable)
    	
    	DisplayEmailStatus
    		restricted
    		when (not EmailStatus.NotApplicable)
    	
    	DisplayPrintError
    		restricted
    		when (PrintErrorMessage entered)
    		
    	DisplayEmailError
    		restricted
    		when (EmailErrorMessage entered)

		DisplayGenerationStatus
			restricted
			when (not GenerationStatus.NotApplicable) 
		
 		DisplayErrorMessage
 			restricted
 			when (ErrorMessage entered)

		DisplayFile
 			restricted
 			when (MDSID entered or PID entered)
 			
		RaiseAlertOnRecord
			restricted
			when (Status.Failed
			or	  Status.TimedOut)

		PrinterSettingsExist
			restricted
			when (PrinterSettingsRel exists)
 
	Field Rules
		Actor
			default to actor
			
	Actions
	
		CreateFromGenerateDocument is a Create Action
			restricted
			Action Rules
			
				LocalExecute 	= IDMGenerateDocument.Execute
				FileName		= IDMGenerateDocument.FileName
				DocumentType	= IDMGenerateDocument.DocumentType
				CreatedDate		= current timestamp
				Status			= Status.Submitted
				GenerationStatus = GenerationStatus.Submitted
				PrinterType		= IDMGenerateDocument.IDMPrinter.Type	
				BatchID			= IDMGenerateDocument.BatchID
				OverrideUser 	= IDMGenerateDocument.OverrideUser
				
				if (IDMGenerateDocument.IDMPrinter entered) 			
					PrintStatus	= PrintStatus.Submitted
					Printer		= IDMGenerateDocument.IDMPrinter
					PrinterName = IDMGenerateDocument.IDMPrinter.PrinterName
					PrinterID	= IDMGenerateDocument.IDMPrinter.PrinterID
        			PrinterLocation 	= IDMGenerateDocument.IDMPrinter.LocationName
        			PrinterEmailAddress = IDMGenerateDocument.IDMPrinter.Email
				else
					PrintStatus	= PrintStatus.NotApplicable
				
				if (IDMGenerateDocument.IDMEmail entered)
					EmailStatus		= EmailStatus.Submitted
					EmailFrom  		= IDMGenerateDocument.IDMEmail.From		
      				EmailToMulti	= IDMGenerateDocument.IDMEmail.To	
       				EmailCcMulti	= IDMGenerateDocument.IDMEmail.Cc 
       				EmailBcMulti	= IDMGenerateDocument.IDMEmail.Bc
        			EmailSubject	= IDMGenerateDocument.IDMEmail.Subject					
				else
					EmailStatus		= EmailStatus.NotApplicable
				
				constraint (IDMGenerateDocument.JobId entered)
					"IDMGenerateDocumentFailed.FailedToRetrieveJobID"
					
				JobID = IDMGenerateDocument.JobId
				
			Exit Rules
				if (not BypassGetStatus)
					invoke GetStatusBackground
						assign async action request id to AsyncId
						invoked.PrmFromGenerateDocument = true
				
		CreateFromAssemble is a Create Action
			restricted	
			Action Rules
					
				LocalExecute    = IDMGenerateDocument.Assemble	
				FileName		= IDMGenerateDocument.FileName
				DocumentType	= IDMGenerateDocument.DocumentType
				CreatedDate		= current timestamp
				Status			= Status.Submitted
				GenerationStatus = GenerationStatus.Submitted
				PrinterType		= IDMGenerateDocument.IDMPrinter.Type
				
				if (IDMGenerateDocument.IDMPrinter entered)
					PrintStatus	= PrintStatus.Submitted
					Printer		= IDMGenerateDocument.IDMPrinter
					PrinterName = IDMGenerateDocument.IDMPrinter.PrinterName
					PrinterID	= IDMGenerateDocument.IDMPrinter.PrinterID
        			PrinterLocation 	= IDMGenerateDocument.IDMPrinter.LocationName
        			PrinterEmailAddress = IDMGenerateDocument.IDMPrinter.Email
				else
					PrintStatus	= PrintStatus.NotApplicable
				
				if (IDMGenerateDocument.IDMEmail entered)
					EmailStatus		= EmailStatus.Submitted
					EmailFrom  		= IDMGenerateDocument.IDMEmail.From		
      				EmailToMulti	= IDMGenerateDocument.IDMEmail.To	
       				EmailCcMulti	= IDMGenerateDocument.IDMEmail.Cc 
       				EmailBcMulti	= IDMGenerateDocument.IDMEmail.Bc
        			EmailSubject	= IDMGenerateDocument.IDMEmail.Subject					
				else
					EmailStatus		= EmailStatus.NotApplicable
				
				constraint (IDMGenerateDocument.JobId entered)
					"IDMGenerateDocumentFailed.FailedToRetrieveJobID"
					
				JobID 		 = IDMGenerateDocument.JobId	
				
			Exit Rules
				if (not BypassGetStatus)
					invoke GetStatusBackground
						assign async action request id to AsyncId
						invoked.PrmFromGenerateDocument = true	
				
		CreateFromSendEmail is a Create Action
			restricted
			Action Rules
				CreatedDate		= current timestamp
				Status			= Status.Submitted
				EmailStatus		= EmailStatus.Submitted
				EmailFrom  		= IDMSendEmail.EmailFrom
      			EmailToMulti	= IDMSendEmail.EmailTo
       			EmailCcMulti	= IDMSendEmail.EmailCc 
       			EmailBcMulti	= IDMSendEmail.EmailBcc
        		EmailSubject	= IDMSendEmail.EmailSubject
        		
				LocalExecute	= IDMSendEmail.Create
				
				constraint (IDMSendEmail.JobId entered)
					"IDMSendEmailFailed.FailedToRetrieveJobID"
					
				JobID = IDMSendEmail.JobId
				
			Exit Rules
				invoke GetStatus
					invoked.PrmFromSendToEmail = true
				
		SendToEmail is a Create Action
			restricted
			Action Rules
				CreatedDate		= current timestamp
				Status			= Status.Submitted
				EmailStatus		= EmailStatus.Submitted
				EmailFrom  		= IDMItem.IDMEmail.From
      			EmailToMulti	= IDMItem.IDMEmail.To
       			EmailCcMulti	= IDMItem.IDMEmail.Cc 
       			EmailBcMulti	= IDMItem.IDMEmail.Bc
        		EmailSubject	= IDMItem.IDMEmail.Subject				
				LocalExecute	= IDMItem.Email
				DocumentType    = IDMItem.DocumentType
				MDSID			= IDMItem.IDMUniqueId
				
				constraint (IDMItem.JobId entered)
					"IDMSendToEmailFailed.FailedToRetrieveJobID"
				
				JobID = IDMItem.JobId
				
			Exit Rules
				invoke GetStatusBackground
					assign async action request id to AsyncId
					invoked.PrmFromSendToEmail = true
					invoked.PrmConsolidateIDMNotification = ConsolidateIDMNotification
									
		SendToPrinter is a Create Action
			restricted
			Action Rules
				Actor			= actor
				CreatedDate		= current timestamp
				Status			= Status.Submitted
				PrintStatus		= PrintStatus.Submitted
				PrinterType		= IDMItem.IDMPrinter.Type		
				Printer			= IDMItem.IDMPrinter
				PrinterName 	= IDMItem.IDMPrinter.PrinterName
				PrinterID		= IDMItem.IDMPrinter.PrinterID
        		PrinterLocation = IDMItem.IDMPrinter.LocationName
        		PrinterEmailAddress = IDMItem.IDMPrinter.Email	
				PrinterSettings = IDMItem.IDMPrinterSettings	
				IDMPrinterSettings = IDMItem.IDMPrinterSettings
				DocumentType    = IDMItem.DocumentType
				MDSID			= IDMItem.IDMUniqueId
				LocalExecute	= IDMItem.Print
				
				constraint (IDMItem.JobId entered)
					"IDMSendToPrinterFailed.FailedToRetrieveJobID"
					
				JobID = IDMItem.JobId
				
			Exit Rules
				if (not BypassGetStatus)
					invoke GetStatus
						invoked.PrmFromSendToPrinter = true
					

		BatchDocumentPrint is a Create Action
			restricted
			Parameters
				PrmIDMUniqueIDs		 is an IDMUniqueIDOccurs 
					default label is "IDMUniqueIDs"
				PrmPrinter			 is an IDMPrinter
					default label is "Printer"
				PrmPrinterSettings	 is an IDMPrinterSettings
					default label is "Printer"
				PrmDocumentType		 is an IDMDocumentType
					default label is "DocumentType"
				PrmFileName			 is an IDMFileName
					default label is "FileName"
				PrmDescription       is a Description
					default label is "Description"
				PrmBypassGetStatus   is Boolean
					default label is "BypassGetStatus"
				PrmAdditionalDetails is Text
					
			Parameter Rules
				PrmIDMUniqueIDs
					required
				PrmPrinter	
					required
				PrmDocumentType		
					required
				PrmDescription       
					required
				PrmFileName
					required
				
			Action Rules
				invoke CreateFromBatchDocumentPrint this instance			
					invoked.CreatedDate  = current timestamp
					invoked.DocumentType = PrmDocumentType
					invoked.Description  = PrmDescription
					invoked.FileName	 = PrmFileName + ".pdf"
					invoked.IDMUniqueIDOccurs = PrmIDMUniqueIDs
					invoked.IDMPrinter	 = PrmPrinter
					invoked.IDMPrinterSettings = PrmPrinterSettings
					invoked.PrinterSettings = PrmPrinterSettings
					invoked.Printer		 = PrmPrinter
					invoked.PrinterType	 = PrmPrinter.Type
					invoked.PrinterName  = PrmPrinter.PrinterName
					invoked.PrinterID	 = PrmPrinter.PrinterID
        			invoked.PrinterLocation 	= PrmPrinter.LocationName
        			invoked.PrinterEmailAddress = PrmPrinter.Email						
					invoked.BypassGetStatus = PrmBypassGetStatus
					invoked.PrintStatus		= "S"
					invoked.AdditionalDetails	= PrmAdditionalDetails
					


		CreateFromBatchDocumentPrint is a Create Action
			restricted
			
			Action Rules
				constraint (JobID entered)
					"IDMBatchDocumentPrintFailed.FailedToRetrieveJobID"
			
			Exit Rules
				if (not BypassGetStatus)
					invoke GetStatusBackground
						assign async action request id to AsyncId
						invoked.PrmFromSendToPrinter = true
				
				 
		GetStatusBackground is an Instance Action
			run in background
			restricted
			Parameters
				PrmFromGenerateDocument is Boolean
				PrmFromSendToEmail		is Boolean
				PrmFromSendToPrinter	is Boolean
				PrmConsolidateIDMNotification is Boolean
				
			Action Rules
				invoke GetStatus
					invoked.PrmFromGenerateDocument = PrmFromGenerateDocument
					invoked.PrmFromSendToEmail 		= PrmFromSendToEmail
					invoked.PrmFromSendToPrinter 	= PrmFromSendToPrinter
					invoked.PrmConsolidateIDMNotification = PrmConsolidateIDMNotification
							
		GetStatus is an Instance Action

			restricted
			Parameters
				PrmFromGenerateDocument is Boolean
				PrmFromSendToEmail		is Boolean
				PrmFromSendToPrinter	is Boolean
				PrmConsolidateIDMNotification		is Boolean
				
			Local Fields
				LocalActor	is an Actor
				
			Action Rules
				LocalActor = actor
				if (not PrmConsolidateIDMNotification)
					if (Status.Finished)
						if (PrmFromGenerateDocument)
							if (MDSID entered)
								send notification
									to LocalActor
									description is "<DerivedJobDescription>Completed.File<FileName>IsAvailableToViewInIDM"
									priority is high
									detail is "<DerivedJobDescription>Completed.File<FileName>IsAvailableToViewInIDM.RefreshTheJobListToViewTheLatestDocument."
							else
								send notification
									to LocalActor
									description is "ProblemOccurredWhileRetrievingTheGeneratedDocumentFrom<DerivedJobDescription>InIDM."
									priority is high
									detail is "ProblemOccurredWhileRetrievingTheGeneratedDocumentInIDM.PleaseRun<DerivedJobDescription>Again."
						else
						if (PrmFromSendToEmail)
								send notification
									to LocalActor
									description is "<DerivedJobDescription>"
									priority is high
									detail is "<DerivedJobDescription>"

						else
						if (PrmFromSendToPrinter)
							send notification
								to LocalActor
								description is "<DerivedJobDescription>"
								priority is high
								detail is "<DerivedJobDescription>"
						else
							send notification
								to LocalActor
								description is "RecheckOfIDMJobStatusDone."
								priority is high
								detail is "RecheckOfIDMJobStatusDone.StatusIs<Status>"							
					else
					if (Status.Failed)
						if (PrmFromGenerateDocument)
							send notification
								to LocalActor
								description is "<DerivedJobDescription>IDMDocumentGenerationFailed."
								priority is high
								detail is "<DerivedJobDescription>IDMDocumentGenerationFailed.PleaseRun<DerivedJobDescription>Again."    
						else	
						if (PrmFromSendToEmail)	
							send notification
								to LocalActor						
								description is "ProblemOccurredWhileSendingTheDocument."
								priority is high
								detail is "ProblemOccurredWhileSendingTheDocument."  
						else	
						if (PrmFromSendToPrinter)	
							send notification
								to LocalActor						
								description is "ProblemOccuredWhilePrintingTheDocument."
								priority is high
								detail is "ProblemOccuredWhilePrintingTheDocument."  	
						else
							send notification
								to LocalActor
								description is "RecheckOfIDMJobStatusFailed."
								priority is high
								detail is "RecheckOfIDMJobStatusFailed"														




		GetStatusByBatchID is a Set Action
			restricted
			Parameters
				PrmActor	  is an Actor
				PrmIDMBatchID is an IDMPID

			Instance Selection
				where (Actor = PrmActor
				and BatchID  = PrmIDMBatchID)
			
			Action Rules
				Instance Rules	
					invoke GetStatus
						invoked.PrmFromGenerateDocument = true


		RecheckStatus is an Instance Action
			restricted
			valid when (Status.TimedOut)
			Action Rules
				invoke GetStatus


		PurgeIDMJob is a Set Action
			restricted
			Parameters
				PrmDateRange is like TimeStampRange

			Parameter Rules
				PrmDateRange
					constraint (PrmDateRange.Begin entered and PrmDateRange.End entered)
						"BeginAndEndDateAreRequired"
					confirmation required
						"ThisWillPurgeIDM_\JobRecordsFrom<PrmDateRange.Begin>To<PrmDateRange.End>.Continue?"
	
			Local Fields
				LocalActor	is an Actor
				LocalCount  is Numeric size 16

			Instance Selection
				where (CreatedDate within PrmDateRange)

			Action Rules
				Empty Set Rules
					initialize LocalCount
					LocalActor = actor

					send notification
						to LocalActor
						description is "NoIDMJobRecordsFoundToPurge"
						priority is high
						detail is "IDMJobPurgeHasBeenCompletedfor<LocalCount>records"

				Set Rules
					Entrance Rules
						initialize LocalCount

					Exit Rules
						LocalActor = actor

						send notification
							to LocalActor
							description is "IDMJobPurgeHasBeenCompleted"
							priority is high
							detail is "IDMJobPurgeHasBeenCompletedFor<LocalCount>records"

				Instance Rules	
					invoke Purge
					increment LocalCount


		Purge is a Purge Action
			restricted	
			bypass relational integrity rules
