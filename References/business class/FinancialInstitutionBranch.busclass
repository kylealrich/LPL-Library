FinancialInstitutionBranch is a BusinessClass
    owned by cashmgmt
    prefix is CMFIB

    Ontology
    	symbolic key is FinancialInstitutionBranch

	Patterns 
        implements StaticJava
        disable AuditIndex
        
    Persistent Fields
		Description					is Alpha up to 250
		BranchType					is Numeric 2
			States
				Operating			value is 1
				Debt				value is 2
				Investment			value is 3
		RoutingNumber				is Alpha 35
			default label is "ElectronicRoutingNumber"
		CheckRoutingNumber			is Alpha 35
		WireRoutingNumber			is Alpha 35
		BranchIdentifier			is AlphaUpper 50
		BankClientIdentifier		is AlphaUpper 50
		BranchSwift					is Alpha 35
			default label is "BranchSWIFT"
		Address						is a PostalAddressV2	
			holds pii
		Address2					is a PostalAddressV2	
			holds pii
		SWIFTCharge            		is Numeric size 1
            States
                PrincipalsBaseAccount    value is 1
                    default label is "Principals base account"
                PrincipalsForeignAccount value is 2
                    default label is "Principals foreign account"
                Beneficiary              value is 3
		Logo						is a LogoAttachment
    	Active
    	AuthorizationID				is Numeric size 5
    	ClearingSystemMemberID		is Numeric size 4
    	WireTransferOutputFileType 	is Numeric 2
    		States
    			CSVFile			value is 0
    			BODOutput		value is 1
    			InforTreasury	value is 2			

	Local Fields
		LocalFinanceResource		is a FinanceResource
		MaxLogoSize

	Field Rules
		Description
			default to FinancialInstitutionBranch
			
		BranchType
			required
		
		Active
			initial value is true

		Logo
			if (Logo changed
			or	FinancialInstitutionBranch not exists)
				MaxLogoSize = CashManagementGroup.LogoImageMaximumSizeBytes

		WireTransferOutputFileType
			if (WireTransferOutputFileType.CSVFile
			and CashManagementGroup.InforTreasuryIntegrationType entered)
				required
					"IntegrationTypeMustBeBlankOn_\Cash_\Management_\GroupToUtilizeCSVOutput"

			if (WireTransferOutputFileType.BODOutput)
				constraint (!CashManagementGroup.InforTreasuryIntegrationType entered)
					"IntegrationTypeMustBeBlankOn_\Cash_\Management_\GroupToUtilizeBODOutput"

			if (WireTransferOutputFileType.InforTreasury)						
				constraint (CashManagementGroup.InforTreasuryIntegrationType entered)
					"IntegrationTypeMustBeEnteredOn_\Cash_\Management_\GroupToUtilize_\Infor_\TreasuryOutput"

	Derived Fields

		ContextMessageEntityType is a StringField
			type is Alpha 50
			restricted
			"InforFinancialInstitutionBranch"

		ContextMessageText is a MessageField
			restricted
			"FinancialInstitutionBranch<FinancialInstitutionBranch>"

		FinancialInstitutionBranchAndDescription is a MessageField
			default label is "FinancialInstitutionBranch"
			"<FinancialInstitutionBranch>_-_<Description>"
			
	Conditions
		HasBranchContacts
			when (FinancialInstitutionBranchContact set exists)
			
	Relations
		FinanceResourceHelperRel 
			one-to-many relation to FinanceResource
			Field Mapping uses symbolic key
				related.HROrganization = CashManagementGroup.HROrganization
		
		CashManagementAccountRel
			one-to-many relation to CashManagementAccount
			Field Mapping uses ByFinancialInstitution
				related.CashManagementGroup			= CashManagementGroup
				related.FinancialInstitution		= FinancialInstitution
				related.FinancialInstitutionBranch	= FinancialInstitutionBranch

	Attach Rules
		constraint (Active)
			"BranchIsInactive"
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
			Exit Rules
				if (BankClientIdentifier changed)
					for each CashManagementAccountRel
						if (each.BankClientIdentifier	= old BankClientIdentifier)
							invoke Update each
								invoked.BankClientIdentifier	= BankClientIdentifier
		
		CreateCounterparty is an Instance Action 
			Parameters
				CashManagementGroup
				DebtInstrument
				CounterpartyRole		 is Alpha 50
				Comment				 	 is Alpha up to 250
			
			Parameter Rules
				CashManagementGroup		 required
				DebtInstrument		 	 required
				CounterpartyRole	 	 required
					
			Action Rules
				invoke Create DebtInstrumentCounterparty
					invoked.CashManagementGroup		   = CashManagementGroup
					invoked.DebtInstrument		 	   = DebtInstrument
					invoked.FinancialInstitution 	   = FinancialInstitution
					invoked.FinancialInstitutionBranch = FinancialInstitutionBranch
					invoked.CounterpartyRole		   = CounterpartyRole
					invoked.Comment				 	   = Comment
					
		Delete is a Delete Action
