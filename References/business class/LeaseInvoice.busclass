LeaseInvoice is a BusinessClass
    owned by lm
    prefix is INV
    classic name is LMINVOICE

    Ontology
        symbolic key is LeaseInvoice
            classic set name is INVNEWPRIMARYINDEX
            classic name for LeaseInvoice.PaymentNumber is PMT-NBR
            classic name for LeaseInvoice.CancelSequence is CANCEL-SEQ
            classic name for Vendor is PMT-VENDOR

    Patterns
        implements StaticJava
        disable AuditIndex
		implements Archivable

    Persistent Fields

        Invoice
        Suffix
        Status                              is Numeric size 1
            States                    			 
                Unreleased            value is 0
                Released              value is 1
                Posted                value is 9










        AuthorityCode                       is a PayablesAuthorityCode
            classic name is AUTH-CODE
        ProcessLevel						is a PayablesProcessLevel				
            classic name is PROC-LEVEL
        VoucherNumber

		Reference
        AccrualCode                         is a PayablesAccrualCode
            classic name is ACCR-CODE
        InvoiceType                         is AlphaUpper size 1
            States
                RegularInvoice value is "R"
                CreditMemo     value is "C"
                Historical     value is "H"
        TransactionInvoiceAmount            is an InternationalAmount
            classic name is TRAN-INV-AMT
        TransactionCurrency                 is a Currency
            classic name is TRAN-CURR
        TransactionCurrencyNumberOfDecimals is a CurrencyNumberOfDecimals
            sql name is TCurrencyNumberOfDecimals
            classic name is TRAN-ND
        BaseInvoiceAmount                   is an InternationalAmount
            classic name is BASE-INV-AMT
        BaseCurrency                        is a Currency
            classic name is BASE-CURR
        BaseNumberOfDecimals
            classic name is BASE-ND
        OriginalConversionRate              is an AcctRate
            classic name is ORIG-CNV-RATE
        InvoiceDate                         is Date
        Description
        DueDate
        DistributionDate                    is Date
            classic name is DISTRIB-DATE
        FiscalYear                          is a Year
        Period
        Approved                            is Boolean 
            classic name is APPROVED-FLAG
        Cancelled                            is Boolean  
            classic name is CANCELLED-FL
        RemitToCode                         is a VendorLocation
        TransactionTaxAmount                is an InternationalAmount
            classic name is TRAN-TAX-AMT
        BaseTaxAmount                       is an InternationalAmount
            classic name is BASE-TAX-AMT
        TransactionTaxableAmount            is an InternationalAmount
            classic name is TRAN-TAXABLE
		ExternalPurchaseOrder				is a PurchaseOrder
			default label is "PurchaseOrder"
 		DoNotCreateAPInvoice				is Boolean
			default label is "DoNotCreatePayablesInvoice"

	Transient Fields


    Local Fields
    	LocalAccountingEntityYearPeriod     is an AccountingEntityYearPeriod
    	LocalInvoiceDateAccoutingEntityYear is a Year
		LocalInvoiceDateAccoutingEntityPeriod is Numeric 2
		LocalDueDateAccoutingEntityYear 	is a Year
		LocalDueDateAccoutingEntityPeriod	is Numeric 2
		LocalPayablesInvoice				is a PayablesInvoice
		NewPayablesInvoice					is a PayablesInvoice view		
		LocalPrincipalAmount                is an InternationalAmount
		LocalPrincipalPercent				is an InterestRate
		LocalInterestAmount                 is an InternationalAmount
		LocalPaymentAmount                  is an InternationalAmount




		IDMAttributes
    	LocalJournalizeGroup		   		is a JournalizeGroup
		LocalGLTransDetail             		is a GLTransactionDetail view

		LocalProject						is a Project
		LocalAccountingEntity				is like AccountingEntity
		LocalAccountingUnit					is like AccountingUnit
		LocalFinanceDimension1				is like FinanceDimension1
		LocalFinanceDimension2				is like FinanceDimension2
		LocalFinanceDimension3				is like FinanceDimension3
		LocalFinanceDimension4				is like FinanceDimension4
		LocalFinanceDimension5				is like FinanceDimension5
		LocalFinanceDimension6				is like FinanceDimension6
		LocalFinanceDimension7				is like FinanceDimension7
		LocalFinanceDimension8				is like FinanceDimension8
		LocalFinanceDimension9				is like FinanceDimension9
		LocalFinanceDimension10				is like FinanceDimension10
		LocalTotalFundTransactionAmount		is an InternationalAmount
		LocalAccumulatedFundAmount			is an InternationalAmount
		LocalBaseInvoiceAmount				is an InternationalAmount
		LocalBaseTotalFundTransactionAmount	is an InternationalAmount
		LocalBaseAccumulatedFundAmount		is an InternationalAmount		
		LocalPercentContribution			is Percent size 9.6
		LocalNumberOfFunds					is Numeric size 2
		FundCounter							is Numeric size 2
		LocalTransactionAmount              is an InternationalAmount
		LocalTransactionAccount				is a FinanceCodeBlock
	Derived Fields
		DerivedCommentCount is a DerivedField
			type is Alpha size up to 20
        	restricted
			if (LeaseInvoiceComment set exists)
				return "(" + instance count of LeaseInvoiceComment set + ")"
			else
				return ""

		DerivedTaxMethodInvOrAcc is a DerivedField
			type is like TaxMethod
			restricted
			if (LeasePaymentDetailRel.TaxCode entered)
				if ((LeasePaymentDetailRel.TaxEntityRel.UseTaxCodeAccounts
				and  LeasePaymentDetailRel.EntityTaxCodeRel.AccruedOrInvoiced.Accrued)
				or (!LeasePaymentDetailRel.TaxEntityRel.UseTaxCodeAccounts
				and  LeasePaymentDetailRel.TaxEntityRel.AccruedOrInvoiced.Accrued))
					return "A"
				else
					return "I"

		IsResidualPeriod is a DerivedField
			type is Boolean
			restricted

			if ((Lease.GuaranteedAmount.TransactionAmount entered 
			or (Lease.ExercisePurchase and Lease.BargainPurchaseAmount.TransactionAmount entered)) 
			and DueDate = Lease.EndDate)
				return true
			else
				return false
												
		DerivedResidualPrincipal is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalPrincipalAmount
			initialize LocalPaymentAmount
			initialize LocalPrincipalPercent




			if ((Lease.BeginningResidual
			and DueDate = Lease.EndDate)
			or  (Lease.HasBeginningBargainPurchaseOption
			and DueDate = Lease.EndDate))



				LocalPrincipalAmount = LeaseCurrencyPaymentPeriodBalanceRel.PrincipalAmount
			else 
				if (Lease.EndingResidual
				and DueDate = Lease.EndDate)
					LocalPaymentAmount = LeaseCurrencyPaymentPeriodBalanceRel.Amount
					LocalPrincipalPercent = TransactionInvoiceAmount / LocalPaymentAmount
					LocalPrincipalAmount = LocalPrincipalPercent * LeaseCurrencyPaymentPeriodBalanceRel.PrincipalAmount
					round LocalPrincipalAmount to nearest DerivedRoundTo
				else 
				if (Lease.HasEndingBargainPurchaseOption
				and DueDate = Lease.EndDate)
					LocalPaymentAmount = LeaseCurrencyPaymentPeriodBalanceRel.Amount
					LocalPrincipalPercent = TransactionInvoiceAmount / LocalPaymentAmount
					LocalPrincipalAmount = LocalPrincipalPercent * LeaseCurrencyPaymentPeriodBalanceRel.PrincipalAmount
					round LocalPrincipalAmount to nearest DerivedRoundTo				
			return LocalPrincipalAmount
				
		DerivedResidualInterest is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalInterestAmount
			initialize LocalPaymentAmount




			if ((Lease.BeginningResidual
			and DueDate = Lease.EndDate)
			or  (Lease.HasBeginningBargainPurchaseOption
			and DueDate = Lease.EndDate))



				LocalInterestAmount = LeaseCurrencyPaymentPeriodBalanceRel.InterestAmount
			else 
				if (Lease.EndingResidual
				and DueDate = Lease.EndDate)



					LocalInterestAmount = TransactionInvoiceAmount - DerivedResidualPrincipal
					round LocalInterestAmount to nearest DerivedRoundTo
				else
				if (Lease.HasEndingBargainPurchaseOption
				and DueDate = Lease.EndDate)
					LocalInterestAmount = TransactionInvoiceAmount - DerivedResidualPrincipal
					round LocalInterestAmount to nearest DerivedRoundTo				
			return LocalInterestAmount
		
		DerivedRoundTo is a DerivedField
    		type is Decimal 5.4
    		restricted
    		if (TransactionCurrencyNumberOfDecimals = 2)
    			return .01
    		else
    		if (TransactionCurrencyNumberOfDecimals = 0)
    			return 1
    		else
    		if (TransactionCurrencyNumberOfDecimals = 3)
    			return .001
    		else
    		if (TransactionCurrencyNumberOfDecimals = 4)
    			return .0001
    			
		CompanyRepresentativeText is a StringField
			type is Text
			default label is "Company"
			Company " - " Company.Name
			
		InvoiceRepresentativeText is a StringField
			type is Text
			default label is "Invoice"
			Invoice " - " Description
			
		VoucherNumberDisplay is a DerivedField
			type is Alpha 15
			default label is "Voucher"

			if (VoucherNumber.Prefix entered)
				return VoucherNumber
			else
				return VoucherNumber.Sequence
    Conditions

        IsNotCancelled
        	restricted
            when (LeaseInvoice.CancelSequence not entered
            and   Status.Unreleased)
            
        IsInvoiceAdjustment
        	restricted
        	when (!Approved)

		IsValidForActorContext
			restricted
			when (Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

		UpdateDeferredRentBalance
			restricted
			when (Lease.LeaseClassification.Operating
			and LeasePaymentRel.IsStraightLine
			and Lease.Status.Released
			and LeaseInvoicePaymentDetailRel.LeasePaymentDetail.ExecutoryCostCode not entered)


		
		HasPayablesInvoice
			restricted
			when (PayablesInvoiceRel exists)




        IsValidToApproveAndPostToPayables
        	restricted
        	when (!Approved
			and !DoNotCreateAPInvoice)

        IsValidToPostToPayables
        	restricted
        	when (Approved
			and !DoNotCreateAPInvoice)

        IsValidToUnapprove
        	restricted
        	when (Approved
			and (Status.Unreleased
			or  (Status.Released
			and DoNotCreateAPInvoice)))

		AllowCreateJournals
			restricted
			when (Status.Released
			and   DoNotCreateAPInvoice
			and   TransactionInvoiceAmount != 0)

    Relations

        LeaseInvoicePaymentDetailRel					
			one-to-many relation to LeasePaymentDetail
            Field Mapping uses Set2
            	related.Company                    	 		= Company
            	related.Lease                       		= Lease
        		related.Vendor								= Vendor
           	Instance Selection				
                where (related.LeasePaymentDetail.PaymentNumber	= LeaseInvoice.PaymentNumber)

        LeaseInvoiceDistributionRel					
			one-to-many relation to LeaseInvoiceDistribution
            Field Mapping uses symbolic key
            	related.Company                    	 	= Company
            	related.Lease                       	= Lease
        		related.Vendor							= Vendor
                related.LeaseInvoice.PaymentNumber		= LeaseInvoice.PaymentNumber
                related.LeaseInvoice.CancelSequence		= LeaseInvoice.CancelSequence
           	Instance Selection
       			where (related.Status = 0)

        LeaseInvoiceDistributionWithLTOLRel					
			one-to-many relation to LeaseInvoiceDistribution
            Field Mapping uses symbolic key
            	related.Company                    	 	= Company
            	related.Lease                       	= Lease
        		related.Vendor							= Vendor
                related.LeaseInvoice.PaymentNumber		= LeaseInvoice.PaymentNumber
                related.LeaseInvoice.CancelSequence		= LeaseInvoice.CancelSequence



			
        LeaseTransactionRel
            one-to-many relation to LeaseTransaction
            Field Mapping uses Set2
            	related.Company                    	 	= Company
            	related.Lease                       	= Lease
        		related.Type							= "ACR"
                related.Process							= "ACC"
                related.Status							= 0
           	Instance Selection
       			where (related.LeaseInvoice.PaymentNumber = LeaseInvoice.PaymentNumber 
            	and    related.Vendor                 	= Vendor)

        UnreleasedLeaseTransactionsRel
            one-to-many relation to LeaseTransaction
            Field Mapping uses Set4
            	related.Company                    	 	= Company
            	related.Status							= 0
            	related.Lease                       	= Lease
				related.LeaseInvoice.PaymentNumber 		= LeaseInvoice.PaymentNumber 
        	Instance Selection   
 				where (related.LeaseInvoice.CancelSequence = LeaseInvoice.CancelSequence)			

        ReleasedLeaseTransactionsRel
            one-to-many relation to LeaseTransaction
            Field Mapping uses Set4
            	related.Company                    	 	= Company
            	related.Status							= 9
            	related.Lease                       	= Lease
				related.LeaseInvoice.PaymentNumber 		= LeaseInvoice.PaymentNumber 
        	Instance Selection   
 				where (related.LeaseInvoice.CancelSequence = LeaseInvoice.CancelSequence)

		LeasePaymentDetailRel	
            one-to-many relation to LeasePaymentDetail
            Field Mapping uses symbolic key
            	related.Company                    	 		= Company
            	related.Lease								= Lease
            	related.Vendor                       		= Vendor
         	Instance Selection   
 				where (related.LeasePaymentDetail.PaymentNumber	= LeaseInvoice.PaymentNumber)	
 				









 				
        LeaseCurrencyPaymentPeriodBalanceRel
            one-to-many relation to LeaseCurrencyPaymentPeriodBalance
            Field Mapping uses ByCompanyLeaseVendorFiscalYearDueDate
                related.Company                        = Company
                related.Lease                          = Lease
                related.Vendor                         = Vendor

            Instance Selection
                where (related.LeasePaymentBalance.ExecutoryCostCode not entered
                and    related.PaymentDueDate = DueDate)

		LeasePaymentRel	
            one-to-one relation to LeasePayment
            Field Mapping uses symbolic key
            	related.Company                    	 	= Company
            	related.Lease							= Lease
            	related.Vendor                       	= Vendor

        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
            	related.Company                    	 	= Company        		

                            	
        PayablesInvoiceRel
            one-to-one relation to PayablesInvoice
            Field Mapping uses ByCompanyVendorInvoice
            	related.Company                    	 	= Company        		
                related.Vendor         					= Vendor
                related.Invoice        					= Invoice
                related.Suffix							= Suffix
                related.CancelSequence					= LeaseInvoice.CancelSequence
				related.InvoiceDate						= InvoiceDate			

        CancelSequenceLeaseInvoiceRel
            one-to-many relation to LeaseInvoice
            Field Mapping uses symbolic key
                related.Company						= Company
                related.Lease						= Lease
                related.Vendor						= Vendor
			Instance Selection
                where (related.LeaseInvoice.PaymentNumber	= LeaseInvoice.PaymentNumber
                and    related.LeaseInvoice.CancelSequence entered)

        PostedLeaseTransactionRel
            one-to-many relation to LeaseTransaction
            Field Mapping uses Set4
                related.Company						= Company
                related.Status						= 9
                related.Lease						= Lease
                related.LeaseInvoice.PaymentNumber	= LeaseInvoice.PaymentNumber

		LeaseFundsRel
			one-to-many relation to LeaseFund
			Field Mapping uses ByLeaseFund
				related.Company					= Company
				related.Lease	 				= Lease	

        LeasePaymentsRel
            one-to-one relation to LeasePayment
            Field Mapping uses symbolic key
            	related.Company		= Company
                related.Lease 		= Lease
                related.Vendor   	= Lease.Lessor

		PayablesLeaseInvoiceCommentRel is a LeaseInvoiceComment set
            Instance Selection
            	where (related.Type.Payables)
 
 		PurgeLeaseInvoiceInterestRel is an LeaseInvoiceInterest set
			Instance Selection
				include deleted records
 
		PurgeLeaseInvoiceDistributionRel is an LeaseInvoiceDistribution set
			Instance Selection
				include deleted records
				
		PurgeLeaseInvoiceCommentRel is an LeaseInvoiceComment set
			Instance Selection
				include deleted records
				            	
    Sets

        Set10
            indexed
            Sort Order
                Company
                Lease
                FiscalYear descending
                Period descending
                LeaseInvoice.PaymentNumber descending
                Vendor
                LeaseInvoice.CancelSequence

        Set11
            indexed
            Sort Order
                Company
                Lease
                DueDate descending
                LeaseInvoice.PaymentNumber descending
                Vendor
                LeaseInvoice.CancelSequence

        Set2
            indexed
            Instance Selection
                where (IsNotCancelled)
            Sort Order
                Company
                AuthorityCode
                Vendor
                Lease
                LeaseInvoice.PaymentNumber
                DueDate

        Set3
            indexed
            Instance Selection
                where (IsNotCancelled)
            Sort Order
                Company
                Lease
                Vendor
                LeaseInvoice.PaymentNumber

        Set4
            indexed
            Instance Selection
                where (IsNotCancelled)
            Sort Order
                Company
                Approved
                AuthorityCode
                Lease
                LeaseInvoice.PaymentNumber
                Vendor

        Set6
            indexed
            Sort Order
                Company
                Lease
                DueDate
                LeaseInvoice.PaymentNumber
                Vendor
                LeaseInvoice.CancelSequence

        Set7
            indexed
            Sort Order
                Company
                Vendor
                Invoice
                Suffix
                LeaseInvoice.CancelSequence

        Set8
            indexed
            Instance Selection
                where (IsNotCancelled)
            Sort Order
                Company
                Vendor
                Lease
                LeaseInvoice.PaymentNumber

        Set9
            indexed
            Sort Order
                Company
                Lease
                FiscalYear
                Period
                Vendor
                LeaseInvoice

        Set1
            Sort Order
                Company
                Lease
                LeaseInvoice.PaymentNumber
                Vendor
                LeaseInvoice.CancelSequence

	Rule Blocks
	
		CreatePayablesInvoiceAndUpdateLeaseAndLeaseTransactionDetails
		
			if (LeasePaymentDetailRel exists)	
				for each UnreleasedLeaseTransactionsRel
					invoke UpdateStatusFromLeaseInvoice each
						invoked.PrmStatus = 9
			
			if (UpdateDeferredRentBalance)
				invoke Released.UpdateDeferredRentBalance Lease
					invoked.PrmDeferredRentBalance =  LeasePaymentDetailRel.PaymentAmount - LeaseCurrencyPaymentPeriodBalanceRel.LeaseExpense

			invoke Unreleased.CreateInvoice PayablesInvoiceRel
				assign result to NewPayablesInvoice
				fill in fields from this instance
				if (InvoiceType.RegularInvoice)
					initialize invoked.InvoiceType 											
				invoked.InvoiceCurrency   										 = TransactionCurrency
				invoked.InvoiceAmount.CurrencyAmount							 = TransactionInvoiceAmount
				invoked.InvoiceAmount.FunctionalAmount.EnteredCurrencyRate   	 = OriginalConversionRate
				if (DerivedTaxMethodInvOrAcc.Invoiced)
					invoked.TaxAmount  											 = TransactionTaxAmount
				invoked.TaxableAmount   										 = TransactionTaxableAmount
				invoked.NumberOfDecimals										 = TransactionCurrencyNumberOfDecimals
				invoked.TaxCode													 = LeasePaymentDetailRel.TaxCode
				invoked.InvoiceSource											 = "L"
				invoked.TransientLineTax									 	 = true





				
			LocalPayablesInvoice = NewPayablesInvoice.PayablesInvoice

			invoke CreatePayablesInvoiceDistribution LeaseInvoiceDistributionRel
				invoked.PrmPayablesInvoice  = LocalPayablesInvoice
				invoked.PrmTaxIndicator		= LeaseInvoiceDistributionRel.TaxIndicator
			
			for each PayablesLeaseInvoiceCommentRel
				invoke Create PayablesInvoiceComment
					fill in fields from each
					invoked.Company				= Company
					invoked.PayablesInvoice		= LocalPayablesInvoice
					invoked.Title				= each.Name
			    	invoked.Comment				= each.Comment
			    	invoked.Type				= "N" 
    										
			invoke RefreshTotalTax LocalPayablesInvoice
			invoke BatchRelease LocalPayablesInvoice
			VoucherNumber	= LocalPayablesInvoice.VoucherNumber
			make transition to Released

		LeaseFunSplitDistributionAccounts
			invoked.InvoiceDistributionAccount.ToAccountingEntity			= LocalAccountingEntity
			if (LocalAccountingUnit entered)
				invoked.InvoiceDistributionAccount.AccountingUnit			= LocalAccountingUnit
			if (LocalProject entered)
				invoked.InvoiceDistributionAccount.Project					= LocalProject
			if (LocalFinanceDimension1 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension1		= LocalFinanceDimension1
			if (LocalFinanceDimension2 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension2		= LocalFinanceDimension2
			if (LocalFinanceDimension3 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension3		= LocalFinanceDimension3
			if (LocalFinanceDimension4 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension4		= LocalFinanceDimension4
			if (LocalFinanceDimension5 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension5		= LocalFinanceDimension5
			if (LocalFinanceDimension6 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension6		= LocalFinanceDimension6
			if (LocalFinanceDimension7 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension7		= LocalFinanceDimension7
			if (LocalFinanceDimension8 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension8		= LocalFinanceDimension8
			if (LocalFinanceDimension9 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension9		= LocalFinanceDimension9
			if (LocalFinanceDimension10 entered)
				invoked.InvoiceDistributionAccount.FinanceDimension10		= LocalFinanceDimension10

		LeaseFundSplits
			LocalNumberOfFunds	= instance count of LeaseFundsRel
			initialize FundCounter
			initialize LocalAccumulatedFundAmount
			initialize LocalBaseAccumulatedFundAmount
			LocalTotalFundTransactionAmount									= LocalTransactionAmount
			LocalBaseTotalFundTransactionAmount								= BaseInvoiceAmount
			for each LeaseFundsRel
				FundCounter													= FundCounter + 1
				LocalAccountingEntity										= each.AccountingEntity
				LocalAccountingUnit											= each.AccountingUnit
				LocalProject												= each.Project
				LocalFinanceDimension1										= each.FinanceDimension1
				LocalFinanceDimension2										= each.FinanceDimension2
				LocalFinanceDimension3										= each.FinanceDimension3
				LocalFinanceDimension4										= each.FinanceDimension4
				LocalFinanceDimension5										= each.FinanceDimension5
				LocalFinanceDimension6										= each.FinanceDimension6
				LocalFinanceDimension7										= each.FinanceDimension7
				LocalFinanceDimension8										= each.FinanceDimension8
				LocalFinanceDimension9										= each.FinanceDimension9
				LocalFinanceDimension10										= each.FinanceDimension10
				LocalPercentContribution									= each.PercentContribution

				if (FundCounter != LocalNumberOfFunds)
					LocalTransactionAmount	= LocalTotalFundTransactionAmount * LocalPercentContribution
					round LocalTransactionAmount to nearest DerivedRoundTo
					LocalAccumulatedFundAmount								= LocalAccumulatedFundAmount + LocalTransactionAmount

					LocalBaseInvoiceAmount	= LocalBaseTotalFundTransactionAmount * LocalPercentContribution
					round LocalBaseInvoiceAmount to nearest DerivedRoundTo
					LocalBaseAccumulatedFundAmount								= LocalBaseAccumulatedFundAmount + LocalBaseInvoiceAmount
				else
					LocalTransactionAmount									= LocalTotalFundTransactionAmount - LocalAccumulatedFundAmount
					LocalBaseInvoiceAmount									= LocalBaseTotalFundTransactionAmount - LocalBaseAccumulatedFundAmount

				invoke Create LeaseInvoiceDistribution
					invoked.Invoice							= Invoice
					invoked.Suffix							= Suffix
					invoked.Description						= "Lease Invoice Accrual"
					invoked.DistributionAmount				= LocalTransactionAmount
					invoked.BaseDistributionAmount			= LocalBaseInvoiceAmount
					invoked.ToBaseCurrency					= BaseCurrency
					invoked.ToBaseCurrencyNumberOfDecimals	= BaseNumberOfDecimals
					invoked.ToBaseRate						= OriginalConversionRate
					invoked.ToBaseAmount					= LocalBaseInvoiceAmount
					invoked.Status							= "7"
					invoked.Company							= Company
					invoked.Lease							= Lease
					invoked.Vendor							= Lease.Lessor
					invoked.LeaseInvoice.PaymentNumber		= LeaseInvoice.PaymentNumber
					invoked.InterestCalculation				= true
					invoked.ExecutoryCostCode				= PrmExecutoryCostCode
					invoked.InvoiceDistributionAccount		= LocalTransactionAccount
					include LeaseFunSplitDistributionAccounts

    Field Rules
    
    	TransactionCurrencyNumberOfDecimals
			default to TransactionCurrency.NumberOfDecimals 
	
        InvoiceDate
            required
            
			LocalAccountingEntityYearPeriod.InputDate   = InvoiceDate
			LocalInvoiceDateAccoutingEntityYear      	= LocalAccountingEntityYearPeriod.AccountingEntityYear
			LocalInvoiceDateAccoutingEntityPeriod    	= LocalAccountingEntityYearPeriod.AccountingEntityPeriod
			
		DueDate
			default to InvoiceDate
			constraint (DueDate >= InvoiceDate)
				"DueDateShouldBeGreaterThanOrEqualToInvoiceDate"
				
			LocalAccountingEntityYearPeriod.InputDate   = DueDate
			LocalDueDateAccoutingEntityYear      		= LocalAccountingEntityYearPeriod.AccountingEntityYear
			LocalDueDateAccoutingEntityPeriod    		= LocalAccountingEntityYearPeriod.AccountingEntityPeriod
			
			constraint (LocalDueDateAccoutingEntityYear = LocalInvoiceDateAccoutingEntityYear)
				"DueDateYearMustBeEqaulToInvoiceDateYear"
			
			constraint (LocalInvoiceDateAccoutingEntityPeriod = LocalDueDateAccoutingEntityPeriod)
				"DueDatePeriodMustBeEqualToInvoiceDatePeriod"
					 
		InvoiceType
			initial value is InvoiceType.RegularInvoice
			default to InvoiceType.RegularInvoice
			
		AuthorityCode
			default to LeasePaymentDetailRel.AuthorityCode
			if (Approved)				
				required
					"AuthorityCodeIsRequiredForApproval"

		TransactionCurrency
			default to Lease.Currency
			
		BaseCurrency
			default to Lease.BaseCurrency
				
	Actions

		UpdateIDMAttributes is an Instance Action
			run in background
			restricted
			Parameters
				AttrsSearch							is an IDMAttributeOccurs
   				Result								is Boolean

			Action Rules
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName  	= "PayablesInvoice"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue  	= PayablesInvoiceRel.PayablesInvoice
				if (AttrsSearch entered)
					IDMAttributes.IDMDocumentType = "FSM_LeaseInvoice"
					IDMAttributes.AttributeSearch = AttrsSearch
					Result = IDMAttributes.ExecuteUpdateAttribute
								
		UpdateTransactionAndBaseAmount		is an Instance Action
			restricted
			Parameters
				PrmTransactionInvoiceAmount         is an InternationalAmount
				PrmBaseInvoiceAmount            	is an InternationalAmount
				PrmTransactionTaxAmount				is an InternationalAmount
				PrmBaseTaxAmount					is an InternationalAmount
			
			Action Rules
				TransactionInvoiceAmount           += PrmTransactionInvoiceAmount            
				BaseInvoiceAmount                  += PrmBaseInvoiceAmount
				if (PrmTransactionTaxAmount entered
				or  PrmBaseTaxAmount entered)
					TransactionTaxAmount           +=PrmTransactionTaxAmount
					BaseTaxAmount				   +=PrmBaseTaxAmount			
					
		CancelLeaseInvoice is an Instance Action 
			restricted
			Parameters
				PrmCancelSequence is a CancelSequence 
			Local Fields
				Lmci80wsCancelSeq 				is a CancelSequence
				LocalLeaseInvoiceDistribution 	is a LeaseInvoiceDistribution
			Action Rules

				if (CancelSequenceLeaseInvoiceRel not exists)
					Lmci80wsCancelSeq = PrmCancelSequence
				else
					if (first CancelSequenceLeaseInvoiceRel.LeaseInvoice.CancelSequence entered)
						Lmci80wsCancelSeq = first CancelSequenceLeaseInvoiceRel.LeaseInvoice.CancelSequence - 1
					else
						Lmci80wsCancelSeq = PrmCancelSequence

				

				invoke Released.Create LeaseInvoice
					fill in fields from this instance
						except invoked.LeaseInvoice.CancelSequence
					invoked.LeaseInvoice.CancelSequence = Lmci80wsCancelSeq
					invoked.Cancelled					= false


				for each LeaseInvoiceDistribution set
					LocalLeaseInvoiceDistribution = each.LeaseInvoiceDistribution
					for each LocalLeaseInvoiceDistribution.LeaseInvoiceInterest set
						invoke Create LeaseInvoiceInterest
							fill in fields from each
								except invoked.LeaseInvoice.CancelSequence
							invoked.LeaseInvoice.CancelSequence = Lmci80wsCancelSeq
						invoke Delete each
						
					invoke Create LeaseInvoiceDistribution
						fill in fields from each
							except invoked.LeaseInvoice.CancelSequence
						invoked.LeaseInvoice.CancelSequence = Lmci80wsCancelSeq
					invoke Delete each
				

				for each PostedLeaseTransactionRel
					invoke Update each
						invoked.LeaseInvoice.CancelSequence = Lmci80wsCancelSeq


				for each LeasePaymentDetailRel
					if (!Lease.Status.Closed
					and !Lease.Status.Terminated)
						invoke Update each
							invoked.Released = false
					else
						invoke Update each
							invoked.Released = true
				
			Exit Rules

				invoke RestrictedDelete

		Unapprove is an Instance Action
			valid when (IsValidToUnapprove)
			Parameters
				PrmAuthorityCode    	is a PayablesAuthorityCode
				PrmApproved				is Boolean
			Parameter Rules
				PrmAuthorityCode
					default to AuthorityCode
				PrmApproved
					PrmApproved		= false
			
			Local Fields
				LocalDeferredRentBalance	is an InternationalAmount

			Action Rules
				AuthorityCode    = PrmAuthorityCode	
				Approved		 = PrmApproved

				if (DoNotCreateAPInvoice)
					if (LeasePaymentDetailRel exists)	
						for each ReleasedLeaseTransactionsRel
							invoke UpdateStatusFromLeaseInvoice each
								invoked.PrmStatus = 0
					
					if (UpdateDeferredRentBalance)
						invoke Released.UpdateDeferredRentBalance Lease
							LocalDeferredRentBalance		= LeasePaymentDetailRel.PaymentAmount - LeaseCurrencyPaymentPeriodBalanceRel.LeaseExpense
							LocalDeferredRentBalance		= LocalDeferredRentBalance * -1
							invoked.PrmDeferredRentBalance 	=  LocalDeferredRentBalance
					make transition to Unreleased

		RestrictedDelete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted
			
			Entrance Rules
				invoke Purge PurgeLeaseInvoiceInterestRel
				invoke Purge PurgeLeaseInvoiceDistributionRel
				invoke Purge PurgeLeaseInvoiceCommentRel			


		CreateDistributionForLeaseInvoiceAccrual is an Instance Action  
			restricted
			Parameters
				PrmJournalizeGroup      is a JournalizeGroup
				PrmExecutoryCostCode	is like ExecutoryCostCode
			Action Rules
				if (DoNotCreateAPInvoice)
					LocalTransactionAmount	= TransactionInvoiceAmount * -1

					LocalTransactionAccount	= LeasePaymentsRel.LeaseInvoiceAccrualAccount
					if (LeaseFundsRel exists)
						include LeaseFundSplits
					else
						invoke Create LeaseInvoiceDistribution
							invoked.Invoice							= Invoice
							invoked.Suffix							= Suffix
							invoked.Description						= "Lease Invoice Accrual"
							invoked.DistributionAmount				= LocalTransactionAmount
							invoked.BaseDistributionAmount			= BaseInvoiceAmount
							invoked.ToBaseCurrency					= BaseCurrency
							invoked.ToBaseCurrencyNumberOfDecimals	= BaseNumberOfDecimals
							invoked.ToBaseRate						= OriginalConversionRate
							invoked.ToBaseAmount					= BaseInvoiceAmount
							invoked.Status							= "7"
							invoked.InvoiceDistributionAccount		= LocalTransactionAccount
							invoked.Company							= Company
							invoked.Lease							= Lease
							invoked.Vendor							= Lease.Lessor
							invoked.LeaseInvoice.PaymentNumber		= LeaseInvoice.PaymentNumber
							invoked.InterestCalculation				= true
							invoked.ExecutoryCostCode				= PrmExecutoryCostCode





	StateCycles
	
		LeaseInvoiceProcessing is a StateCycle
			state field is Status
			
			Unreleased is a State
				
				Create 			is a Create Action
					restricted
											
				Update is an Update Action
					valid when (!Approved)
					Action Rules
						constraint (!Approved)
							"ValidOnlyForUnapprovedInvoices"
						



						
				
				Delete 			is a Delete Action
					Entrance Rules
						for each LeaseTransactionRel
							invoke Delete each 
							
						invoke UpdateStatusAfterDeleteLeaseInvoice LeasePaymentDetailRel
						
				InvoiceAdjustment		is an Update Action
					valid when (IsInvoiceAdjustment)
					
					Action Rules
						constraint (IsInvoiceAdjustment)
							"InvoiceAdjustmentActionIsNotAValidFor<Status>Invoice"
									
				Approve is an Instance Action
					valid when (!Approved)
					Parameters
						PrmAuthorityCode    is a PayablesAuthorityCode
						PrmApproved			is Boolean
					
					Parameter Rules
						PrmAuthorityCode
							default to AuthorityCode
							required
								"AuthorityCodeIsRequiredForApproval"
						PrmApproved
							initial value is true

					Action Rules
						Approved 		 = PrmApproved
						AuthorityCode    = PrmAuthorityCode

						if (DoNotCreateAPInvoice)
							if (LeasePaymentDetailRel exists)	
								for each UnreleasedLeaseTransactionsRel
									invoke UpdateStatusFromLeaseInvoice each
										invoked.PrmStatus = 9
							
							if (UpdateDeferredRentBalance)
								invoke Released.UpdateDeferredRentBalance Lease
									invoked.PrmDeferredRentBalance =  LeasePaymentDetailRel.PaymentAmount - LeaseCurrencyPaymentPeriodBalanceRel.LeaseExpense
							make transition to Released
				
				ApproveAndPostToAccountsPayable is an Instance Action
					default label is "ApproveAndInterfaceToPayables"
					valid when (IsValidToApproveAndPostToPayables)
			
					Parameters
						PrmAuthorityCode    is a PayablesAuthorityCode
					
					Parameter Rules
						PrmAuthorityCode
							default to AuthorityCode
							required
								"AuthorityCodeIsRequiredForApproval"

					Action Rules
						Approved 		 = true
						AuthorityCode    = PrmAuthorityCode
						
						include CreatePayablesInvoiceAndUpdateLeaseAndLeaseTransactionDetails

				PostToAccountsPayable is an Instance Action
					valid when (IsValidToPostToPayables)
			
					Action Rules
						include CreatePayablesInvoiceAndUpdateLeaseAndLeaseTransactionDetails
						
						
				UpdateZeroPaymentInvoiceFromBatch is an Instance Action 
					restricted
					Action Rules
						Approved = true
						Status = Status.Released
						if (UpdateDeferredRentBalance)
							invoke Released.UpdateDeferredRentBalance Lease
								invoked.PrmDeferredRentBalance =  LeasePaymentDetailRel.PaymentAmount - LeaseCurrencyPaymentPeriodBalanceRel.LeaseExpense
						
						

			Released is a State
				Entrance Rules
					invoke UpdateIDMAttributes
						invoked.AttrsSearch.IDMAttribute[1].IDMAttributeName	= "Company"
						invoked.AttrsSearch.IDMAttribute[1].IDMAttributeValue	= Company
						invoked.AttrsSearch.IDMAttribute[2].IDMAttributeName  	= "Lease"
						invoked.AttrsSearch.IDMAttribute[2].IDMAttributeValue  	= Lease
						invoked.AttrsSearch.IDMAttribute[3].IDMAttributeName  	= "PaymentNumber"
						invoked.AttrsSearch.IDMAttribute[3].IDMAttributeValue  	= LeaseInvoice.PaymentNumber
						invoked.AttrsSearch.IDMAttribute[4].IDMAttributeName  	= "CancelSequence"
						invoked.AttrsSearch.IDMAttribute[4].IDMAttributeValue  	= LeaseInvoice.CancelSequence

				Create 			is a Create Action
					restricted

			Posted	 is a State


	
