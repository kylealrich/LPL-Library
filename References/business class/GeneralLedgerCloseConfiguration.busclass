GeneralLedgerCloseConfiguration is a BusinessClass
	default label is "GlobalLedgerCloseConfiguration"
	sql name is "GLCloseConfig"
	owned by GeneralLedger
	representative text is "<GeneralLedgerCloseConfiguration>-<Description>"
	
	prefix is GLCCf
	
	Ontology
		symbolic key is GeneralLedgerCloseConfiguration
		
	Patterns
		implements BODId
	
	Persistent Fields
		Description
		PeriodsAreEndOfMonth is Boolean
		DefaultPeriodName	 is Alpha 50
    		Text Variables
				MonthName
				MonthAbbreviation
				MonthNumber
				CloseYear
				EndDateYear
				NthPeriodOfYear
		RebuildRequired		is Boolean
		FirstNewYear		is Year	

	Context Fields
#ifdef module integration
		FSMInboundBODTracker
#endif
	Relations
		NewYearsRel			is a GeneralLedgerCloseYear set
			Instance Selection
				where (related.GeneralLedgerCloseYear >= FirstNewYear)
				

		GeneralLedgerCalPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses symbolic key 	
				related.FinanceEnterpriseGroup		    	 = FinanceEnterpriseGroup
			Instance Selection
				where (related.GeneralLedgerCalendarPeriod   = LocalGLCPeriod)

		GeneralLedgerCloseYearRel is a GeneralLedgerCloseYear set
				
		GeneralLedgerClosePeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key 	
				related.FinanceEnterpriseGroup		    = FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
			Instance Selection
				where (related.GeneralLedgerCloseYear   = LocalGLCYear)
				
#ifdef module integration								
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "FinancialCalendar"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
#endif

							
	Field Rules
		Description
			required
		DefaultPeriodName
			initial value is "{MonthName} {CloseYear}"
			default to "{MonthName} {CloseYear}"

	Local Fields
		LocalActionCode					is an ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp

		LocalNativeLPLBODTrigger			is Boolean
		LocalDate							is Alpha 25
		LocalEndDate						is Alpha 25
		LocalStartDate						is Alpha 25
		LocalGLCYear						is Alpha 25
		LocalGLCPeriod						is Alpha 25
		LocalDerivedBeginGregorian 			is Alpha 25
		LocalPeriodName						is Alpha 25
		LocalPeriodNumber					is Alpha 25
		LocalStatusCode						is Alpha 10
		LocalCalendarPeriodXML				is XMLDocument
#ifdef module integration
		NewBODTracker  						is a FSMInboundBODTracker view
#endif
		LocalFSMInboundBODTracker			is Numeric 15
		Error            					is Boolean
	    ErrorMessage     					is Alpha 300
	    LocalBODTrigger						is Boolean
	    LocalBeginningBalancePeriodName		is Alpha 60
		LocalBeginningBalancePeriod			is Alpha 30
		LocalPeriodXML						is XMLDocument
	    LocalConfigurationParameter			is Alpha size up to 200


	Derived Fields

#ifdef module integration
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (LocalActionCode = "D" or LocalActionCode.Delete) 
				return "Deleted"
			else 
				return "Open"

		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (LocalActionCode = "C" or LocalActionCode.Create)
				return "Add"
			else
				return "Replace"
				
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
		
		DerivedBODVariationID is a DerivedField
			type is Alpha size 25
			restricted
			return  bod id.VariationID
			
		DerivedAccountingEntity is a DerivedField
			type is Alpha size 25
			restricted
			return  ""
					
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return current timestamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
			
		DerivedlastModUserID is a DerivedField
			type is Alpha size 25
			restricted
			return update stamp.actor
			
		DerivedyearMethod is a DerivedField
			type is Alpha size 25
			restricted
			if (PeriodsAreEndOfMonth = true)
				return "Calendar"
			else 
				return "Fiscal"
			
		FormattedDerivedstartDate is a DerivedField
			type is Alpha size 25
			restricted
			return LocalStartDate[1:4] + "-" + LocalStartDate[5:6] + "-" + LocalStartDate[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
		
		FormattedDerivedDate is a DerivedField
			type is Alpha size 25
			restricted
			return LocalDate[1:4] + "-" + LocalDate[5:6] + "-" + LocalDate[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
		
		FormattedDerivedEndDate is a DerivedField
			type is Alpha size 25
			restricted
			return LocalEndDate[1:4] + "-" + LocalEndDate[5:6] + "-" + LocalEndDate[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"	
	
		FormattedDerivedGLCPBeginGregorian is a DerivedField
			type is Alpha size 25
			restricted
			return LocalDerivedBeginGregorian[1:4] + "-" + LocalDerivedBeginGregorian[5:6] + "-" + LocalDerivedBeginGregorian[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
		
		DerivedSystemOfRecord is a DerivedField
			type is Alpha size 25
			restricted
			return "0"
			

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
			if (DerivedDelimiter="")
				return "-"
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
				
		DerivedIntegrationApplication is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "IntegrationApplication"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
				
		DerivedBeginningBalanceFlag is a DerivedField
			type is Alpha size 2
			restricted
			LocalConfigurationParameter = "FinancialCalendar_With_BeginningBalance"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedBODID is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedAccountingEntity + ":" + ":" +DerivedDocumentID + ":" +DerivedBODRevision+"?FinancialCalendar&verb=Sync&TrackerID="+ LocalFSMInboundBODTracker
					
		DerivedDocumentID is a DerivedField
			type is Alpha size 25
			restricted
			return FinanceEnterpriseGroup+DerivedDelimiter+GeneralLedgerCloseConfiguration
					


		SyncFinancialCalendarBODXML is a DerivedField
			type is XMLDocument
			restricted
			SyncFinancialCalendarBODXML = template.IONSyncFinancialCalendar_GeneralLedgerCloseConfiguration_Header_ST document for this instance


		CalendarPeriodXML is a DerivedField
			type is XMLDocument
			restricted
			if (GeneralLedgerCalPeriodRel exist)
				return LocalCalendarPeriodXML
			else
				return ""
		
		GeneralLedgerCloseYearXML is a DerivedField
			type is XMLDocument
			restricted
			if (GeneralLedgerCloseYearRel exist)
				LocalStatusCode = "Open"
				initialize GeneralLedgerCloseYearXML
				for each GeneralLedgerCloseYearRel
					initialize LocalCalendarPeriodXML
					LocalStartDate	= each.DerivedStartDate
					LocalGLCYear	= each.GeneralLedgerCloseYear
					if (GeneralLedgerClosePeriodRel exist)
						for each GeneralLedgerClosePeriodRel
							LocalGLCPeriod 				= each.GeneralLedgerClosePeriod
							LocalDerivedBeginGregorian	= each.DerivedBeginGregorian
							LocalPeriodName				= each.PeriodName
							LocalPeriodNumber			= each.PeriodNumber
							if (GeneralLedgerCalPeriodRel exist)
								LocalDate				= GeneralLedgerCalPeriodRel.Date
							LocalCalendarPeriodXML += template.IONSyncFinancialCalendar_GeneralLedgerCloseConfiguration_Header_CalendarPeriod_ST document for this instance
					LocalEndDate 				    = LocalDate
					if (DerivedIntegrationApplication = "Local.ly" and DerivedBeginningBalanceFlag = "Y")
						LocalBeginningBalancePeriod = LocalGLCYear +"000"
						LocalBeginningBalancePeriodName = "January" +" " + LocalGLCYear + " "+ "Beginning" + " "+" Period"
						LocalPeriodXML = template.IONSyncFinancialCalendar_GeneralLedgerCloseConfiguration_PeriodXML_ST document for this instance
					GeneralLedgerCloseYearXML += template.IONSyncFinancialCalendar_GeneralLedgerCloseConfiguration_Header_GLCloseYear_ST document for this instance
				return GeneralLedgerCloseYearXML
			else
				return ""
#endif

		
	Rule Blocks

 		FinancialCalendarRules
			trigger "CloseConfigurationService" PA service
				resume on error
				title is "EG:<FinanceEnterpriseGroup>CC:<GeneralLedgerCloseConfiguration>"
				Criteria
					FinanceEnterpriseGroup
					GeneralLedgerCloseConfiguration
				Variables
					LocalActionCode
						variable name is ActionCode
					include persistent fields from FinanceEnterpriseGroup
					include persistent fields from GeneralLedgerCloseConfiguration
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
				
	Actions
		Create is an Action
		Update is an Action
		Delete is an Action	
			Entrance Rules
				if (FinanceEnterpriseGroup.BODTrigger)
					LocalActionCode = LocalActionCode.Delete
					increment bod id.VariationID
					include FinancialCalendarRules



				
		CreateYear is an Instance Action
			Parameters
				Year		is Year
				
			Parameter Rules
				Year
					required
					
			Action Rules
				invoke Create GeneralLedgerCloseYear set
					invoked.GeneralLedgerCloseYear = Year
					
		ShiftPeriods is an Instance Action
		
			Parameters
				PrmShiftBy				   is Numeric 2
					default label is "ShiftBy"
				PrmAssignDefaultPeriodName is Boolean
					default label is "AssignDefaultPeriodName"
				PrmStartingInYear		   is a GeneralLedgerCloseYear
					default label is "StartingInYear"
					
			Parameter Rules
				PrmShiftBy
					required
					constraint (PrmShiftBy >= -11
					and         PrmShiftBy <= 11)
						"CannotShiftByMoreThan11Periods"
				PrmStartingInYear
					required
								
			Local Fields
				LocalPeriodArray	is a ClosePeriodShiftArray
				LocalCloseYear		is Year
				I1					is Numeric 3
				LocalPeriodCount	is Numeric 2
				LocalPeriodsInYear  is Numeric 2
				LocalClosePeriod	is a GeneralLedgerClosePeriod
				LocalShiftCheck		is Numeric 2
				LocalLookupYear		is a GeneralLedgerCloseYear
				
			Action Rules
				constraint (instance count of GeneralLedgerClosePeriod set.GeneralLedgerClosePeriod < 501)
					"ConfigurationContainsTooManyPeriods"
				
				I1 = 0
				for each GeneralLedgerClosePeriod set
					if (each.GeneralLedgerCloseYear >= PrmStartingInYear)
						I1 += 1
						if (each.GeneralLedgerCloseYear != LocalCloseYear)
							LocalCloseYear   = each.GeneralLedgerCloseYear
							LocalPeriodCount = 0
							LocalLookupYear  = LocalCloseYear
							LocalPeriodArray.ClosePeriodInfo[I1].PeriodsInYear = (instance count of LocalLookupYear.GeneralLedgerClosePeriod set.GeneralLedgerClosePeriod)						
						LocalPeriodCount += 1
						LocalPeriodArray.ClosePeriodInfo[I1].ClosePeriod = each.GeneralLedgerClosePeriod
						LocalPeriodArray.ClosePeriodInfo[I1].CloseYear   = each.GeneralLedgerCloseYear
					
				LocalCloseYear = blank
				I1			   = 1
				
				while (I1 < 501)
					if (LocalPeriodArray.ClosePeriodInfo[I1].ClosePeriod = blank)
						end while
	
					if (LocalPeriodArray.ClosePeriodInfo[I1].CloseYear != LocalCloseYear)
						LocalCloseYear     = LocalPeriodArray.ClosePeriodInfo[I1].CloseYear
						LocalPeriodsInYear = LocalPeriodArray.ClosePeriodInfo[I1].PeriodsInYear
						LocalPeriodCount   = 0
						if (PrmShiftBy > 0)
							LocalLookupYear = (LocalCloseYear + 1)
						else
							LocalLookupYear = (LocalCloseYear - 1)
					LocalPeriodCount += 1
					LocalClosePeriod = LocalPeriodArray.ClosePeriodInfo[I1].ClosePeriod 
					if (!LocalLookupYear exists)
						invoke Create GeneralLedgerCloseYear set
							invoked.GeneralLedgerCloseYear = LocalLookupYear
					invoke MoveToDifferentYear LocalClosePeriod
						if (PrmShiftBy < 0)
							LocalShiftCheck = LocalPeriodCount
							LocalShiftCheck += PrmShiftBy
							if (LocalShiftCheck < 1) 
								invoked.PrmCloseYear = (LocalPeriodArray.ClosePeriodInfo[I1].CloseYear - 1)
							else
								invoked.PrmCloseYear = LocalPeriodArray.ClosePeriodInfo[I1].CloseYear
						else
							LocalShiftCheck = (LocalPeriodCount - LocalPeriodsInYear)
							LocalShiftCheck += PrmShiftBy						
							if (LocalShiftCheck > 0)
								invoked.PrmCloseYear = (LocalPeriodArray.ClosePeriodInfo[I1].CloseYear + 1)
							else
								invoked.PrmCloseYear = LocalPeriodArray.ClosePeriodInfo[I1].CloseYear
						invoked.PrmAssignDefaultPeriodName = PrmAssignDefaultPeriodName
					I1 += 1

		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location"
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord

#ifdef module integration

		TriggerFinancialCalendarRulesBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules 
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "FinancialCalendar"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "GeneralLedgerCloseConfiguration"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup 
					invoked.PrmMainUserTemplate			= "IONSyncFinancialCalendar_GeneralLedgerCloseConfiguration_Header_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger	
				LocalBODTrigger = true
				if(FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "FinancialCalendar"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Direction				= 1
							invoked.Reference1				= GeneralLedgerCloseConfiguration
							invoked.Reference2				= "GeneralLedgerCloseConfiguration"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.BODID					= DerivedBODID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= GeneralLedgerCloseConfiguration
							invoked.Reference2				= "GeneralLedgerCloseConfiguration"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendGeneralLedgerCloseConfigurationNativeLPLBOD
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODID
							invoked.BODXML								= SyncFinancialCalendarBODXML
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODID
							invoked.BODXML									= SyncFinancialCalendarBODXML
				
									
		SendGeneralLedgerCloseConfigurationNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules 
				send ion bod
					bod is SyncFinancialCalendarBODXML
					bod type is "Sync.FinancialCalendar"
					document id is DerivedDocumentID
					variation id is DerivedBODVariationID

#endif

		UpdateRebuildInfo is an Instance Action
			restricted
			Parameters
				PrmYearCreated	is Year
					default label is "Year"
			Action Rules
				if (!RebuildRequired)
					RebuildRequired = true
					FirstNewYear	= PrmYearCreated
			
		CreatePeriodStatus is an Instance Action
			valid when (RebuildRequired)
			confirmation required
				"PeriodStatusDataWillBeCreated"					
			completion message is "PeriodStatusCreationSubmitted"
						
			Action Rules
				for each NewYearsRel
					invoke FixEntityPeriod CodeBlockRelationDetail in background
						invoked.PrmEnterpriseGroup		= FinanceEnterpriseGroup
						invoked.PrmCodeBlockRelation	= FinanceEnterpriseGroup.LedgerToEntityCodeBlockRelationRel.CodeBlockRelation
						invoked.PrmCloseConfiguration	= GeneralLedgerCloseConfiguration
						invoked.PrmYear					= each.GeneralLedgerCloseYear
				RebuildRequired = false
				initialize FirstNewYear			
	Action Exit Rules
		if (FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					LocalActionCode = LocalActionCode.Update
					if (action type.Create)
						LocalActionCode = LocalActionCode.Create
					else
						if (action type.Delete)
							LocalActionCode = LocalActionCode.Delete
		
					increment bod id.VariationID
					include FinancialCalendarRules



