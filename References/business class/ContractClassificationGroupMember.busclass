ContractClassificationGroupMember is a BusinessClass
	owned by po
	prefix is CCgm
	sql name is CCGroupMember
	
	Ontology
		symbolic key is ContractClassificationGroupMember
	
	Patterns
	
	Persistent Fields
	
	Conditions
	
		SubclassificationExists
			restricted
			when (SubclassificationRel exists)
			
		ClassificationOnlyExists
			restricted
			when (SameClassificationRel exists)

		SubclassificationNotEntered 
			restricted 
			when (ContractSubclassification !entered)
	
		SubclassificationEntered 
			restricted 
			when (ContractSubclassification entered)

	Relations
	
		SameClassificationRel
			one-to-many relation to ContractClassificationGroupMember
			Field Mapping uses symbolic key	
				related.ContractGroup         		= ContractGroup
				related.ContractClassificationGroup = ContractClassificationGroup
				related.ContractClassification      = ContractClassification
			Instance Selection
				where (related.ContractSubclassification !entered)
				
		SubclassificationRel
			one-to-many relation to ContractClassificationGroupMember
			Field Mapping uses symbolic key	
				related.ContractGroup         		= ContractGroup
				related.ContractClassificationGroup = ContractClassificationGroup
				related.ContractClassification      = ContractClassification
			Instance Selection
				where (related.ContractSubclassification entered)
		ContractMatchClassificationsRel
			one-to-many relation to Contract
			Field Mapping uses ByClassification
				related.ContractClassification 		= ContractClassification	
				related.ContractSubclassification 	= ContractSubclassification
				related.ContractGroup    			= ContractGroup
						
	Sets 

		ByClassificationOnly 
			Sort Order
				ContractGroup
				ContractClassificationGroup
				ContractClassification
			Instance Selection	
				where (SubclassificationNotEntered)

		BySubclassification 
			Sort Order
				ContractGroup
				ContractClassificationGroup
				ContractClassification
				ContractSubclassification
			Instance Selection	
				where (SubclassificationEntered)

		ByClassificationGroup
			Sort Order
				ContractClassificationGroup
				ContractGroup
				ContractClassification
			Instance Selection	
				where (SubclassificationNotEntered)

	Field Rules
	
	Actions
	
		Create is a Create Action
		
			Action Rules
			
				if (ContractSubclassification entered)
					constraint (!ClassificationOnlyExists)
						"CannotEnterAGroupMemberForASubclassificationIfAMemberForTheSameClassificationOnlyExists"	
				
				if (ContractSubclassification !entered)
					constraint (!SubclassificationExists)
						"CannotEnterAGroupMemberForAClassificationOnlyIfAMemberAlreadyAlreadyExistsForASubclassification"
						
		Delete is a Delete Action
	
