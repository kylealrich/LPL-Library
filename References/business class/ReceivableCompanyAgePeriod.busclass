ReceivableCompanyAgePeriod is a BusinessClass
    owned by ar
    prefix is RCAP

    Ontology
    	symbolic key is ReceivableCompanyAgePeriod
    
    Patterns
    	disable Auditing		
        disable EffectiveDated	
        
    Persistent Fields
    	FromDay			is Numeric 5
    	ToDay			is Numeric 5

	Context Fields
		Customer
		
	Field Rules
		FromDay
			constraint (FromDay > ToDay)
				"FromDayMustBeGreaterThanToDay"
	
	Local Fields

	Derived Fields
	
		FromDate is a DerivedField
			type is Date
			return (current corporate date - FromDay)

		ToDate is a DerivedField
			type is Date
			return (current corporate date - ToDay)

		DateRangeMsg is a MessageField
			restricted
			"<ToDay>to<FromDay>Days"

		PeriodTitle is a DerivedField
			type is Alpha size 50
			return DateRangeMsg

		NumPaymentsForAgePeriod	is a ComputeField
			type is Decimal size 18
			(instance count of PaymentsAgePeriodRel)
			
		SumPaymentsForAgePeriod	is a DerivedField		
			type is like InternationalAmount 	
			return (sum PaymentsAgePeriodRel.TransactionAmount)

		NumTransactionsForAgePeriod	is a ComputeField
			type is Decimal size 18
			(instance count of TransactionDetailAgePeriodRel)
			
		SumTransactionsForAgePeriod	is a DerivedField		
			type is like InternationalAmount 		
			return (sum TransactionDetailAgePeriodRel.InvoiceAmount.CurrencyAmount)
	

	Relations
		PaymentsAgePeriodRel
			one-to-many relation to ReceivablePayment
			Field Mapping uses Set10
				related.Company = Company
				related.Customer = Customer
			Instance Selection
				where (related.DepositDate >= FromDate
				and    related.DepositDate <= ToDate)

		TransactionDetailAgePeriodRel
			one-to-many relation to ReceivableInvoiceDetail
			Field Mapping uses Set5
				related.Company = Company
				related.Customer = Customer
			Instance Selection
				where (related.TransactionDate >= FromDate
				and    related.TransactionDate <= ToDate)

	Actions
		Create is an Action

		
		Update is an Action

		
		Delete is an Action 

		
		Purge is a Purge Action

		
		
