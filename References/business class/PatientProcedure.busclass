PatientProcedure is a BusinessClass
    owned by ic
    prefix is HPP
    classic name is HCPATPROC

    Ontology
        symbolic key is PatientProcedure
            classic set name is HPPSET1
            classic name is OBJ-ID

    Patterns
        implements StaticJava
        implements Encrypted
        disable AuditIndex
		implements Archivable

    Persistent Fields

        VisitNumber             is a PatientVisit
        	classic name is VISIT-NBR 
        PatientID 				is a Patient
        	classic name is PATIENT-ID
        CaseNumber
        	classic name is CASE-NBR
        Procedure
        	classic name is PROC
        ProcedureModifier		is Alpha size 2
        	classic name is PROC-MODIFIER
        ProcedureDate  			is Date
        	classic name is PROC-DATE
        PhysicianName			is a PhysName 
        	holds pii
        	classic name is PHYS-NAME 
        NurseCoordinator		is a NrsCoord
        	classic name is NRS-COORD
        PatientLastName			is a LastName
        	classic name is PAT-LAST-NAME
        PatientFirstName		is a FirstName	 
        	holds pii
        	classic name is PAT-FIRST-NAME
        PatientMiddleName		is a MiddleName
        	classic name is PAT-MIDDLE-NAM
        Birthdate				is Date  
        	holds pii
        Gender
        SocialSecurityNumber	is a TaxID 
        	holds pii
        	classic name is SSN
        SalesRepresentative 	is a SalesRep
        	classic name is SALES-REP
        OrderNumber				is an OrdNbr
        	classic name is ORD-NBR
        Requestor
        Lot       				is like ItemLot
        SerialNumber    		is like ItemSerialNumber
        	classic name is SER-NBR
        UserDefinedField1		is a Userdef1
            classic name is USERDEF1
        UserDefinedField2		is a Userdef2
            classic name is USERDEF2
        UniqueDeviceId			is Alpha 120
        	classic name is UDI
        GTIN					is like ItemGTIN
        	classic name is GTIN
        Manufacturer
        	classic name is MANUF-CODE
        	classic name is MANUF-DIVISION
        ManufacturerNumber
        	classic name is MANUF-NBR
        ManufacturingDate		is Date
        ExpirationDate			is Date
        RecordType              is Numeric 1
        	States
        		Line            value is 0 
        		Header          value is 1			     	
		Requisition
			restricted
		RequisitionLine
			restricted
		PurchaseOrder
			restricted
		PurchaseOrderLine
			restricted
	Field Groups
		GroupProcedureInfo
			PatientID
			VisitNumber
			CaseNumber
			Procedure
			ProcedureModifier
			ProcedureDate
			PhysicianName
			NurseCoordinator
			PatientLastName
			PatientFirstName
			PatientMiddleName
			Birthdate
        	Gender
        	SocialSecurityNumber
			SalesRepresentative
			OrderNumber
			Requestor
			Lot
			SerialNumber
			UserDefinedField1
			UserDefinedField2
			UniqueDeviceId
			GTIN
			Manufacturer
			ManufacturerNumber
			ManufacturingDate
			ExpirationDate

	Local Fields
		LocalRecallGroup	is AlphaUpper size 4
		LocalRecallNotice	is a RecallNotice
		LocalRecallProduct	is a RecallProduct
		LocalItem			is an Item
		LocalRequisition	is Numeric size 7
		LocalRequisitionLine	is Numeric size 6
		LocalFullName		is Alpha 200
	
	Derived Fields
	
		SerialNumberOfCharacters is a DerivedField
			type is Numeric size 3
			restricted
			return SerialNumber size 
			
		LotNumberOfCharacters is a DerivedField
			type is Numeric size 3
			restricted
			return Lot size 
	
		DerivedStrippedSerialNumber is a DerivedField
			type is like ItemSerialNumber
			return com.lawson.apps.procurement.base.StringUtils.stripNonAlphaNumeric(SerialNumber)
			
		DerivedStrippedLot is a DerivedField
			type is like ItemLot
			return com.lawson.apps.procurement.base.StringUtils.stripNonAlphaNumeric(Lot)

		DerivedFromLocation is a DerivedField
			type is like InventoryLocation
			restricted
			if (ReqLineRel exists)
   				return ReqLineRel.FromCompanyLocationBin.FromLocation

		DerivedPatientFullName is a MessageField
			default label is "Name"
			"<PatientLastName>,_<PatientFirstName>_<PatientMiddleName>"

		DerivedGTINFiller is a DerivedField
			type is Alpha size 6
			restricted
			if (ItemGTINStructure8)
				return "000000"
			else
			if (ItemGTINStructure12)
				return "00"
			else
			if (ItemGTINStructure13)
				return "0"
		
		DerivedGTINNumeric is a StringField
			type is Alpha size 14
			restricted
			DerivedGTINFiller
			GTIN

		DerivedManufacturingYear is a DerivedField
			type is AlphaUpper size 4
			restricted
			return ManufacturingDate year

		DerivedManufacturingMonth is a DerivedField
			type is Numeric size 2
			restricted
			return ManufacturingDate month
			
		DerivedManufacturingDay is a DerivedField
			type is Numeric size 2
			restricted
			return ManufacturingDate day
			
		DerivedManufacturingDate is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			DerivedManufacturingMonth
			DerivedManufacturingDay
			
		DerivedManufacturingDateWith0Month0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			0
			DerivedManufacturingMonth
			0
			DerivedManufacturingDay
			
		DerivedManufacturingDateWith0Month is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			0
			DerivedManufacturingMonth
			DerivedManufacturingDay
			
		DerivedManufacturingDateWith0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			DerivedManufacturingMonth
			0
			DerivedManufacturingDay
						
		DerivedManufacturingDate6 is a DerivedField
			type is AlphaUpper size 6
			restricted
			if (DerivedManufacturingMonth < 10
			and DerivedManufacturingDay < 10)
				return DerivedManufacturingDateWith0Month0Day
			else
			if (DerivedManufacturingMonth < 10)
				return DerivedManufacturingDateWith0Month
			else
			if (DerivedManufacturingDay < 10)
				return DerivedManufacturingDateWith0Day
			else
				return DerivedManufacturingDate

		DerivedExpirationYear is a DerivedField
			type is AlphaUpper size 4
			restricted
			return ExpirationDate year 

		DerivedExpirationMonth is a DerivedField
			type is Numeric size 2
			restricted
			return ExpirationDate month
			
		DerivedExpirationDay is a DerivedField
			type is Numeric size 2
			restricted
			return ExpirationDate day

		DerivedExpirationDate is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			DerivedExpirationMonth
			DerivedExpirationDay

		DerivedExpirationDateWith0Month0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			0
			DerivedExpirationMonth
			0
			DerivedExpirationDay
			
		DerivedExpirationDateWith0Month is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			0
			DerivedExpirationMonth
			DerivedExpirationDay
			
		DerivedExpirationDateWith0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			DerivedExpirationMonth
			0
			DerivedExpirationDay
						
		DerivedExpirationDate6 is a DerivedField
			type is AlphaUpper size 6
			restricted
			if (DerivedExpirationMonth < 10
			and DerivedExpirationDay < 10)
				return DerivedExpirationDateWith0Month0Day
			else
			if (DerivedExpirationMonth < 10)
				return DerivedExpirationDateWith0Month
			else
			if (DerivedExpirationDay < 10)
				return DerivedExpirationDateWith0Day
			else
				return DerivedExpirationDate
									
		DerivedUDIOption1 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			"(10)"
			Lot
			"(21)"
			SerialNumber
				
		DerivedUDIOption2 is a StringField
			type is Alpha size up to 120
			restricted
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6

		DerivedUDIOption3 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6

			
		DerivedUDIOption4 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(10)"
			Lot
			
		DerivedUDIOption5 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(21)"
			SerialNumber
			
		DerivedUDIOption6 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			
		DerivedUDIOption7 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(10)"
			Lot
			
		DerivedUDIOption8 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(21)"
			SerialNumber
			
		DerivedUDIOption9 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6
			"(10)"
			Lot
			
		DerivedUDIOption10 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6
			"(21)"
			SerialNumber
			
		DerivedUDIOption11 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(10)"
			Lot
			"(21)"
			SerialNumber
						
		DerivedUDIOption12 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6
			"(10)"
			Lot
			"(21)"
			SerialNumber
			
		DerivedUDIOption13 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedExpirationDate6
			"(10)"
			Lot
			"(21)"
			SerialNumber

		DerivedUDIOption14 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			"(21)"
			SerialNumber
			
		DerivedUDIOption15 is a StringField
			type is Alpha size up to 120
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			"(21)"
			SerialNumber
			"(10)"
			Lot
								
	Sets
        ByRequisitionLine
            indexed
            Sort Order
                Company
                Requisition
                RequisitionLine
                PatientProcedure
                
        ByPurchaseOrderLine
            indexed
            Sort Order
                Company
                PurchaseOrder
                PurchaseOrderLine   
                PatientProcedure 

	Field Rules

		GTIN
			if (GTIN entered)
				constraint (GTINRecordExists)
					"G\T\I\NDoesNotExist"
					
		UniqueDeviceId
			initialize UniqueDeviceId
			
			if (UDIOption1)
				UniqueDeviceId = DerivedUDIOption1
			else
			if (UDIOption2)
				UniqueDeviceId = DerivedUDIOption2
			else
			if (UDIOption3)
				UniqueDeviceId = DerivedUDIOption3
			else
			if (UDIOption4)
				UniqueDeviceId = DerivedUDIOption4
			else
			if (UDIOption5)
				UniqueDeviceId = DerivedUDIOption5
			else
			if (UDIOption6)
				UniqueDeviceId = DerivedUDIOption6
			else
			if (UDIOption7)
				UniqueDeviceId = DerivedUDIOption7
			else
			if (UDIOption8)
				UniqueDeviceId = DerivedUDIOption8
			else
			if (UDIOption9)
				UniqueDeviceId = DerivedUDIOption9
			else
			if (UDIOption10)
				UniqueDeviceId = DerivedUDIOption10
			else
			if (UDIOption11)
				UniqueDeviceId = DerivedUDIOption11
			else
			if (UDIOption12)
				UniqueDeviceId = DerivedUDIOption12
			else
			if (UDIOption13)
				UniqueDeviceId = DerivedUDIOption13
			else
			if (UDIOption14)
				UniqueDeviceId = DerivedUDIOption14
			else
			if (UDIOption15)
				UniqueDeviceId = DerivedUDIOption15

	Conditions
		HasPatientProcedureInfo
			restricted
			when (GroupProcedureInfo entered)

		HasPatientName
			restricted
			when (PatientLastName entered
			or    PatientFirstName entered
			or    PatientMiddleName entered)

		SerialNumberEntered
			restricted
			when (SerialNumber entered)
			
		LotNumberEntered
			restricted
			when (Lot entered)
			
		RequisitionLineEntered
			restricted
			when (RequisitionLine entered)
			
		PurchaseOrderLineEntered
			restricted
			when (PurchaseOrderLine entered)
			
		RejectedRequisitionLine
			restricted
			when (RequisitionLineEntered
			and   RequisitionLine.Status.Rejected)
			
		RequisitionSourceExists
			restricted
			when (RequisitionLineEntered
			and   RequisitionLine.SourceRecordExists)
			
		FullyCanceledPurchaseOrderLine
			restricted
			when (PurchaseOrderLineEntered
			and   PurchaseOrderLine.QuantityFullyCancelled)
			
		GTINRecordExists
			restricted
			when (ItemGTINRel exists)

		NeedsInventoryUDIDtlUpdate
			restricted
			when (NeedsUDIProcessing
			and  (Lot changed
			or    SerialNumber changed
			or    ManufacturingDate changed
			or    ExpirationDate changed))
			
		UDIOption1
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber entered)

		UDIOption2
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber not entered)
			
		UDIOption3
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber not entered)
			
		UDIOption4
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber not entered)
			
		UDIOption5
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber entered)

		UDIOption6
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber not entered)

		UDIOption7
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber not entered)
			
		UDIOption8
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber entered)
			
		UDIOption9
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber not entered)
			
		UDIOption10
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber entered)
											
		UDIOption11
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber entered)

		UDIOption12
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber entered)
											
		UDIOption13
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber entered)

		UDIOption14
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber entered)
			
		UDIOption15
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber not entered)
			
		ItemGTINStructure8
			restricted
			when (GTINRecordExists
			and   ItemGTINRel.ItemGTIN.GTINStructure.GTIN8)
			
		ItemGTINStructure12
			when (GTINRecordExists
			and   ItemGTINRel.ItemGTIN.GTINStructure.GTIN12)
			
		ItemGTINStructure13
			when (GTINRecordExists
			and   ItemGTINRel.ItemGTIN.GTINStructure.GTIN13)

		NeedsUDIProcessing
			when (ItemLocationRel.UDITracked.UDIRequiredAtReceipt or ItemLocationRel.UDITracked.UDIRequiredAtIssue
			and   ReqLineRel exists
			and   not ReqLineRel.CreatePurchaseOrder)
									
		NeedsLotProcessing
			when (ItemLocationRel.LotTracked.LotRequiredAtReceipt or ItemLocationRel.LotTracked.LotRequiredAtIssue
			and   ReqLineRel exists
			and   not ReqLineRel.CreatePurchaseOrder)

		NeedsSerialProcessing
			when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt or ItemLocationRel.SerialTracked.SerialRequiredAtIssue
			and   ReqLineRel exists
			and   not ReqLineRel.CreatePurchaseOrder)
									
	Relations
   		InventoryUDIDetailRel
   			one-to-one relation to InventoryUDIDetail
   			Field Mapping uses symbolic key
   				related.Company											= Company
   				related.InventoryLocation								= ReqLineRel.FromCompanyLocationBin.FromLocation
   				related.InventoryUDIDetail.InventoryTransactionType		= ReqLineRel.InventoryTransactionType
   				related.InventoryUDIDetail.TransactionSystemCode		= "RQ"
   				related.InventoryUDIDetail.DocumentNumberNumeric		= ReqLineRel.Requisition
				related.InventoryUDIDetail.WarehouseShipment			= blank
				related.InventoryUDIDetail.LineNumber					= ReqLineRel.RequisitionLine
				related.InventoryUDIDetail.TransactionSequence			= 1

		RecallProductSerialRel
			one-to-many relation to RecallProductSerial
   			Field Mapping uses ByStrippedSerialNumber
   				related.RecallGroup			= LocalRecallGroup
   				related.RecallNotice		= LocalRecallNotice
   				related.RecallProduct		= LocalRecallProduct
   				related.RecallProductSerialStripped	= DerivedStrippedSerialNumber
   				
		RecallProductSerialRangeRel
			one-to-many relation to RecallProductSerial
   			Field Mapping uses symbolic key
   				related.RecallGroup			= LocalRecallGroup
   				related.RecallNotice		= LocalRecallNotice
   				related.RecallProduct		= LocalRecallProduct
   			Instance Selection
   				where (related.ThruSerial		 	!= blank
   				and    related.NumberOfCharacters 	 = SerialNumberOfCharacters
   				and    related.RecallProductSerial 	<= SerialNumber
   				and    related.ThruSerial          	>= SerialNumber)
   				
   		RecallProductLotRel
			one-to-many relation to RecallProductLot
   			Field Mapping uses ByStrippedLot
   				related.RecallGroup			= LocalRecallGroup
   				related.RecallNotice		= LocalRecallNotice
   				related.RecallProduct		= LocalRecallProduct
   				related.RecallProductLotStripped	= DerivedStrippedLot
   				
		RecallProductLotRangeRel
			one-to-many relation to RecallProductLot
   			Field Mapping uses symbolic key
   				related.RecallGroup			= LocalRecallGroup
   				related.RecallNotice		= LocalRecallNotice
   				related.RecallProduct		= LocalRecallProduct
   			Instance Selection
   				where (related.ThruLot  		 	!= blank
   				and    related.NumberOfCharacters 	 = LotNumberOfCharacters
   				and    related.RecallProductLot 	<= Lot
   				and    related.ThruLot          	>= Lot)
   				
   		PurchaseOrderLineRel
   			one-to-many relation to PurchaseOrderLine
   			Field Mapping uses ByPatientProcedure
   				related.Company             = Company
   				related.PatientProcedure    = PatientProcedure
   				
   		RequisitionLineRel
   			one-to-many relation to RequisitionLine
   			Field Mapping uses ByPatientProcedure
   				related.Company             = Company
   				related.PatientProcedure    = PatientProcedure

   		ReqLineRel
   			one-to-one relation to RequisitionLine
   			Field Mapping uses symbolic key
   				related.Company             = Company
   				related.Requisition			= LocalRequisition
   				related.RequisitionLine		= LocalRequisitionLine

   		ItemRel
			one-to-one relation to Item
   			Field Mapping uses symbolic key
   				related.ItemGroup			= Company.ItemGroup
   				related.Item				= LocalItem

   		ItemGTINRel
			one-to-one relation to ItemGTIN
   			Field Mapping uses symbolic key
   				related.ItemGroup			= Company.ItemGroup
   				related.Item				= LocalItem
   				related.ItemGTIN			= GTIN

        ItemLocationRel
            one-to-one relation to ItemLocation
            required
            Field Mapping uses symbolic key
                related.Company				= Company
                related.InventoryLocation	= DerivedFromLocation
                related.Item				= LocalItem

        StockOnHandDetailRel
            one-to-many relation to StockOnHandDetail
            Field Mapping uses symbolic key
                related.Company							= Company
   				related.InventoryLocation				= ReqLineRel.FromCompanyLocationBin.FromLocation
                related.Item							= LocalItem
   			Instance Selection
   				where (related.StockOnHandDetail.Lot	= Lot)

        ItemSerialNumberRel
            one-to-one relation to ItemSerialNumber
            Field Mapping uses symbolic key
                related.Company							= Company
                related.Item							= LocalItem
   				related.ItemSerialNumber				= SerialNumber

        ItemLocationSerialNumberRel
            one-to-one relation to ItemSerialNumber
            Field Mapping uses Set2
                related.Company							= Company
   				related.InventoryLocation				= ReqLineRel.FromCompanyLocationBin.FromLocation
                related.Item							= LocalItem
   				related.ItemSerialNumber				= SerialNumber

   		ItemLotRel
			one-to-one relation to ItemLot
   			Field Mapping uses symbolic key
   				related.Company				= Company
   				related.InventoryLocation	= ReqLineRel.FromCompanyLocationBin.FromLocation
   				related.Item				= LocalItem
   				related.ItemLot				= Lot
   					
	Actions
		Create is a Create Action

			Field Rules
				Manufacturer
					if (Manufacturer !entered)
						default to ItemLocationRel.Manufacturer
						
				ManufacturerNumber
					if (ManufacturerNumber !entered)
						default to ItemLocationRel.ManufacturerNumber
			Exit Rules	
		
		Update is an Update Action
			Exit Rules
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
			
			Action Rules
				display "LOG:_InvokedPurgeAction"
			
		PurgeDeletedPatientProcedures is a Set Action
			restricted
			Parameters
				PrmCompany			is a PurchasingCompany
				
			Instance Selection
				include only deleted records
				where (Company = PrmCompany)
				
			Action Rules
				Instance Rules
					invoke Purge
					
		QueryPatientProcedureSerialOrLotNumber is a Set Action
			restricted
			Parameters
				PrmRecallGroup		is a RecallGroup
				PrmRecallNotice		is a RecallNotice
				PrmRecallProduct	is a RecallProduct
				PrmQueryPriority	is Numeric size 2
					States
		   				High			value is 1
		   				Medium			value is 2
		   				Low				value is 3
				PrmQueryDueDate		is TimeStamp
				PrmCreateTasks      is Boolean
				
			Instance Selection
				where (RecordType = 0
				and   (SerialNumberEntered
				or     LotNumberEntered)
    			and	   Company.ItemGroup = PrmRecallGroup
    			and    !RejectedRequisitionLine
    			and    !RequisitionSourceExists
    			and    !FullyCanceledPurchaseOrderLine)
				
			Action Rules
				Instance Rules
					LocalRecallGroup 	= PrmRecallGroup
					LocalRecallNotice 	= PrmRecallNotice
					LocalRecallProduct 	= PrmRecallProduct
					
					if  (RecallProductSerialRel exists
					or   RecallProductSerialRangeRel exists
					or   RecallProductLotRel exists
					or   RecallProductLotRangeRel exists)
						invoke Create RecallProductTransaction
							invoked.RecallGroup									= PrmRecallGroup
							invoked.RecallNotice								= PrmRecallNotice
							invoked.RecallProduct								= PrmRecallProduct
							invoked.ActivityType								= 17 
							if (RequisitionLineRel exists)
								invoked.RequestingLocationGroup.RequestingCompany      = Company
                            	invoked.RequestingLocationGroup.RequestingLocation 	   = first RequisitionLineRel.Requisition.RequestingLocation
	                        	invoked.RequisitionLineGroup.InventoryCompany          = Company 
	                        	invoked.RequisitionLineGroup.Requisition               = first RequisitionLineRel.Requisition
	                        	invoked.RequisitionLineGroup.RequisitionLine           = first RequisitionLineRel.RequisitionLine
							if (PurchaseOrderLineRel exists)
								invoked.InventoryLocationGroup.InventoryCompany			= Company
								invoked.InventoryLocationGroup.InventoryLocation		= first PurchaseOrderLineRel.ShipToLocation
								invoked.PurchaseOrderLineGroup.PurchasingCompany		= Company
								invoked.PurchaseOrderLineGroup.PurchaseOrder 			= first PurchaseOrderLineRel.PurchaseOrder
								invoked.PurchaseOrderLineGroup.PurchaseOrderLine		= first PurchaseOrderLineRel.PurchaseOrderLine
								if (PurchaseOrderLineRel.FromRequisitions)
									invoked.RequestingLocationGroup.RequestingCompany		= first PurchaseOrderLineRel.first LineSourcesFromRQRel.PurchaseOrderLineSource.OperatingCompany
									invoked.RequestingLocationGroup.RequestingLocation		= first PurchaseOrderLineRel.first LineSourcesFromRQRel.RequestingLocation
									invoked.RequisitionLineGroup.InventoryCompany			= first PurchaseOrderLineRel.first LineSourcesFromRQRel.PurchaseOrderLineSource.OperatingCompany
									invoked.RequisitionLineGroup.Requisition				= first PurchaseOrderLineRel.first LineSourcesFromRQRel.PurchaseOrderLineSource.SourceDocumentNumeric
									invoked.RequisitionLineGroup.RequisitionLine			= first PurchaseOrderLineRel.first LineSourcesFromRQRel.PurchaseOrderLineSource.SourceDocumentLineNumber	
							invoked.PatientProcedureGroup.InventoryCompany      = Company
							invoked.PatientProcedureGroup.PatientProcedure      = PatientProcedure
							invoked.PatientProcedureGroup.SerialNumber          = SerialNumber
							invoked.PatientProcedureGroup.Lot                   = Lot                      
							invoked.LocalPriority								= PrmQueryPriority
							invoked.LocalDueDate								= PrmQueryDueDate
							invoked.LocalCreateTasks                            = PrmCreateTasks
		
		InitializeFields is an Instance Action
			restricted
			Action Rules
				initialize VisitNumber
				initialize PatientID
				initialize CaseNumber
				initialize Procedure
				initialize ProcedureModifier
				initialize ProcedureDate
				initialize PhysicianName
				initialize NurseCoordinator
				initialize PatientLastName
				initialize PatientFirstName
				initialize PatientMiddleName
				initialize Birthdate
				initialize Gender
				initialize SocialSecurityNumber
				initialize SalesRepresentative
				initialize OrderNumber
				initialize Requestor
				initialize Lot
				initialize SerialNumber
				initialize UserDefinedField1
				initialize UserDefinedField2
				initialize UniqueDeviceId
				initialize GTIN
				initialize Manufacturer
				initialize ManufacturerNumber
				initialize ManufacturingDate
				initialize ExpirationDate
