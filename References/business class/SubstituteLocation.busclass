SubstituteLocation is a BusinessClass
    owned by ic
    prefix is ILG
    classic name is ICLOCGRP

    Ontology
        symbolic key is SubstituteLocation
            classic set name is ILGSET1
            classic name is SEQ
            classic name for InventoryLocation is LOCATION
            classic name for TransactionSystemCode is SYSTEM-CD

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        SubLocation is an InventoryLocation

    Sets

        Set2
            indexed
            Sort Order
                Company
                InventoryLocation
                TransactionSystemCode
                SubLocation
                SubstituteLocation

	Conditions
		SequenceExists
			restricted
			when (SubstituteLocation exists)
			
	Relations
		LastRecordRel
			one-to-many relation to SubstituteLocation
			Field Mapping uses symbolic key
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.TransactionSystemCode	= TransactionSystemCode

		DuplicateRecordRel
			one-to-many relation to SubstituteLocation
			Field Mapping uses Set2
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.TransactionSystemCode	= TransactionSystemCode
				related.SubLocation				= SubLocation
			Instance Selection
				where (related.SubstituteLocation != SubstituteLocation)

	Field Rules
		TransactionSystemCode
			force default to "WO"

		SubstituteLocation
			SubstituteLocation = last LastRecordRel.SubstituteLocation + 1

		SubLocation
			constraint (InventoryLocation.WorkOrderLocationType.ProductionLocation)
				"SubstituteLocationsCanBeDefinedForProductionLocationsOnly"
			constraint (SubLocation != InventoryLocation)
				"SubstituteLocationCannotBeSameAsInventoryLocation"

			constraint (DuplicateRecordRel not exists)
				"LocationIsAlreadyASubstituteFor<InventoryLocation>"       
			constraint (!SubLocation.WorkOrderLocationType.OffsiteLocation)
				"OffsiteLocationCannotBeUsedAsSubstitute"                  

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action
