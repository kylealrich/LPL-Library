MobileSupplyChainDeliveryEvent is a BusinessClass
    owned by mscm
    prefix is DLVET

    Ontology
        symbolic key is MobileSupplyChainDeliveryEvent

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        EventCompanyLocation        is a MSCMCompanyAndLocation
        PerformedAction
        Signature                    is an ImageAttachment
        EmployeeID
        FirstName                   is a Name
        LastName                    is a Name
        PackageCount
        TimeSubmitted               is TimeStamp

    Field Rules
        EventCompanyLocation
            if (not PerformedAction.Docklogged)
                required

        TimeSubmitted
            required
        
        PerformedAction
            required

        PackageCount
            if (IsDelivered)
                required
            
    Conditions
        IsDelivered
            when (PerformedAction.DeliveryConfirmed
            or    PerformedAction.DeliveryReleased)

        HasSignature
            when (Signature entered)
            
    Derived Fields
        DerivedRecipient is a DerivedField
            type is Alpha size 120
            if (FirstName entered and LastName entered)
                return (LastName + ", " + FirstName)
            else
            if (FirstName entered)
                return FirstName
            else
            if (LastName entered)
                return LastName
            return "-"

        DerivedLineCount is a DerivedField
            type is Numeric size 12
            return instance count of MobileSupplyChainDeliveryEventLineRel.MobileSupplyChainDeliveryEventLine

        DerivedBlankDisplay is a DerivedField
            type is Alpha size 1
            return "-"

    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted

        CreateEventAttachment is an Instance Action
            restricted
            Parameters
                PrmAttachmentXML                is XMLDocument
            Local Fields
                LocalAttachmentCount            is Numeric 4
                LocalAttachmentMaxCount         is Numeric 3
                LocalAttachmentCounter          is Numeric 4
                LocalAttachment                 is BinaryDocument
                LocalAttachmentTitle            is DocumentTitle size 100
                LocalAlphaTimeSubmitted         is Alpha size 16
            Action Rules

                LocalAttachmentCount            = PrmAttachmentXML select "count (/Images/Image)"
                LocalAlphaTimeSubmitted         = TimeSubmitted

                if (config.MSCMDeliveryAttachmentMaxCount != blank)
                    LocalAttachmentMaxCount       = config.MSCMDeliveryAttachmentMaxCount
                else
                    LocalAttachmentMaxCount       = 50

                if (LocalAttachmentCount >= 1)
                    LocalAttachmentCounter   = 1

                    if (LocalAttachmentCount > LocalAttachmentMaxCount)
                        LocalAttachmentCount = LocalAttachmentMaxCount

                    while (LocalAttachmentCounter <= LocalAttachmentCount)
                        LocalAttachment         = PrmAttachmentXML select "/Images/Image[<LocalAttachmentCounter>]"
                        LocalAttachmentTitle    = MobileSupplyChainDeliveryTicket.InforTrackingNumber + "-" + PerformedAction + "-" + LocalAlphaTimeSubmitted + "-" + LocalAttachmentCounter + ".jpg"
                        invoke Create MobileSupplyChainDeliveryEventAttachment
                            invoked.Company                                 = Company
                            invoked.MobileSupplyChainLocation               = MobileSupplyChainLocation
                            invoked.MobileSupplyChainDeliveryTicket         = MobileSupplyChainDeliveryTicket
                            invoked.MobileSupplyChainDeliveryEvent          = MobileSupplyChainDeliveryEvent
                            invoked.Attachment.Title                        = LocalAttachmentTitle
                            invoked.Attachment.File                         = LocalAttachment
                            invoked.Attachment.MimeType                     = "image/jpg"

                        LocalAttachmentCounter += 1

    Relations
        MobileSupplyChainDeliveryEventLineRel is a MobileSupplyChainDeliveryEventLine set

        MobileSupplyChainDeliveryEventRel
            one-to-one relation to MobileSupplyChainDeliveryEvent
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.MobileSupplyChainLocation       = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket = MobileSupplyChainDeliveryTicket
                related.MobileSupplyChainDeliveryEvent  = MobileSupplyChainDeliveryEvent
