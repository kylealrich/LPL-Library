CustodialAccountManagement is a BusinessClass
	owned by cam
	
	prefix is CamAm
	
	Ontology
		symbolic key is CustodialAccountManagement
	
	Patterns
	
	Persistent Fields
		CustodialSummary			is a FinanceDimension10
			default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>"
		CustodialAccountType 	is AlphaUpper size 1
			default label is "AccountType"
			States
				Other 			value is "O"
					default label is "Entity\/\Other"
				Individual 		value is "I"
		Title				is Alpha 100
		CamCategory
			default label is "Category"
		SocialSecurityNumber 	is Alpha 11
			holds pii
			protected
			restricted
		Address 				is a PostalAddressV2
			holds pii 
		EmailAddress			is an EmailAddressMulti
			holds pii
			default label is "Email"			
		CamCustodian
			default label is "Custodian"
		AccountingEntity
		AccUnitSummaryFund		is an AccountingUnit
			default label is "SummaryFund"
		AccUnitPostingFund		is an AccountingUnit
			default label is "PostingFund"
		DimOneSummaryFund		is a FinanceDimension1
			default label is "SummaryFund"
		DimOnePostingFund		is a FinanceDimension1
			default label is "PostingFund"
		DateOfRequest			is Date
		Approver				is Alpha 100
		DateOfApproval			is Date
		FirstName
			holds pii
		MiddleName
			holds pii
		LastName
			holds pii
		VendorGroup
		Vendor
		DefaultFinanceCodeBlock	is a FinanceCodeBlock 
			default label is "DefaultFinanceStructure"
		BankTransactionCode
			default label is "TransferCode"
		OtherReference			is Alpha 15
			default label is "Reference"
		CamSite
			default label is "Site"
		InCareOf				is Alpha size 50
		PhoneNumber				is a TelephoneNumber
		TaxID
			holds pii

	Field Rules
		CustodialSummary
			required
			constraint (!CustodialSummaryDuplicateRel exists)
				"<FinanceEnterpriseGroup.FinanceDimension10Label>_HasAlreadyBeenSetup"

		CustodialAccountType
			required
		
		CamCategory
			required
			if (CamCategory.SSNRequired.Yes)
				constraint (TaxID entered)
					"TaxIDMustBeEntered"
			if (CamCategory.SSNRequired.No)
				initialize TaxID

		TaxID
			if (TaxID entered
			and CamCategory.TaxIDType entered
			and TaxIdentificationTypeRel.HasExpression)
				constraint (TaxID matches TaxIdentificationTypeRel.RegularExpression)
					"TaxIdFormatDoesNotMatch;FormatIs<TaxIdentificationTypeRel.FormatExample>"

		AccountingEntity
			required

		VendorGroup
			default to Vendor.VendorGroup
		DefaultFinanceCodeBlock
			if(DefaultFinanceCodeBlock.ToAccountingEntity not entered)
				DefaultFinanceCodeBlock.ToAccountingEntity = AccountingEntity
			if(FinanceEnterpriseGroup.FundDimension.AccountingUnit
			and DefaultFinanceCodeBlock.AccountingUnit not entered)
				DefaultFinanceCodeBlock.AccountingUnit = AccUnitPostingFund
			if(FinanceEnterpriseGroup.FundDimension.Dimension1
			and DefaultFinanceCodeBlock.FinanceDimension1 not entered)
				DefaultFinanceCodeBlock.FinanceDimension1 = DimOnePostingFund
			constraint (DefaultFinanceCodeBlock.ToAccountingEntity = AccountingEntity)
				"AccountingEntityMustBeTheSameOnCustodialRecordAndDefaultFinanceStructure"
			if (FinanceEnterpriseGroup.AccountingUnitRequired)
				constraint (DefaultFinanceCodeBlock.AccountingUnit entered)
					"<FinanceEnterpriseGroup.AccountingUnitLabel>IsRequired"
			if (FinanceEnterpriseGroup.ProjectRequired)
				constraint (DefaultFinanceCodeBlock.Project entered)
					"<FinanceEnterpriseGroup.ProjectLabel>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension1Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension1 entered)
					"<FinanceEnterpriseGroup.FinanceDimension1Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension2Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension2 entered)
					"<FinanceEnterpriseGroup.FinanceDimension2Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension3Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension3 entered)
					"<FinanceEnterpriseGroup.FinanceDimension3Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension4Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension4 entered)
					"<FinanceEnterpriseGroup.FinanceDimension4Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension5Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension5 entered)
					"<FinanceEnterpriseGroup.FinanceDimension5Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension6Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension6 entered)
					"<FinanceEnterpriseGroup.FinanceDimension6Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension7Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension7 entered)
					"<FinanceEnterpriseGroup.FinanceDimension7Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension8Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension8 entered)
					"<FinanceEnterpriseGroup.FinanceDimension8Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension9Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension9 entered)
					"<FinanceEnterpriseGroup.FinanceDimension9Label>IsRequired"
			if (FinanceEnterpriseGroup.FinanceDimension10Required)
				constraint (DefaultFinanceCodeBlock.FinanceDimension10 entered)
					"<FinanceEnterpriseGroup.FinanceDimension10Label>IsRequired"
			
	Transient Fields
	
	Local Fields
		LocalFEG				is like FinanceEnterpriseGroup
		LocalFinDim10 			is like FinanceDimension10
		LocalDimStructure 		is like FinanceDimension10Structure	
	
		LocalAdbBasis					is an ReportingBasis
		LocalAdbPeriod					is a GeneralLedgerCalendarPeriod
		LocalAdbEntity					is an AccountingEntity
		LocalAdbAccount					is a GeneralLedgerChartAccount

		LocalFund						is like AccountingUnit
		LocalFundType					is a CodeBlockDimensionType		
		
		LocalFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
		LocalAllocationSourceSystem		is like AllocationSourceSystem
		LocalAllocationControl			is like AllocationControl
		LocalAllocationRun				is like AllocationRun
		LocalAllocation					is like Allocation
		LocalAllocationLine				is like AllocationLine
	
	Context Fields
		ContextAllocationControl	is an AllocationControl
	Derived Fields
		InactiveMF is a MessageField
			restricted
			"INACTIVE"

		ActiveMF is a MessageField
			restricted
			"ACTIVE"

		StatusMessage is a ConditionalField
			type is Alpha size 20
			if (CustodialSummary.Active)
				ActiveMF
			else
				InactiveMF

		IndividualName	is a StringField
			type is Alpha 200
			FirstName
			" "
			MiddleName
			" "
			LastName
 
        TitleField is a ConditionalField
            type is Alpha size 50
            if (CustodialAccountType.Other)
                Title
            else
                IndividualName

		ADBDynamicSelection is a DerivedField
			type is LPLText
			restricted
			is condition for "AverageDailyBalanceAggregate"

			ADBDynamicSelection = "(1 = 1 "
			if (FinanceEnterpriseGroup.ADBDimensions.Dimension1.Type.Dimension10)
				ADBDynamicSelection += " and ADBRel.ADBDimension1 = any ShadowDescendantsRel.FinanceDimension10 "
			if (FinanceEnterpriseGroup.ADBDimensions.Dimension2.Type.Dimension10)
				ADBDynamicSelection += " and ADBRel.ADBDimension2 = any ShadowDescendantsRel.FinanceDimension10 "
			if (FinanceEnterpriseGroup.ADBDimensions.Dimension3.Type.Dimension10)
				ADBDynamicSelection += " and ADBRel.ADBDimension3 = any ShadowDescendantsRel.FinanceDimension10 "
			if (LocalFund entered)
				if (FinanceEnterpriseGroup.ADBDimensions.Dimension1.Type = LocalFundType)
					ADBDynamicSelection += " and ADBRel.ADBDimension1 = LocalFund"
				if (FinanceEnterpriseGroup.ADBDimensions.Dimension2.Type = LocalFundType)
					ADBDynamicSelection += " and ADBRel.ADBDimension2 = LocalFund"
				if (FinanceEnterpriseGroup.ADBDimensions.Dimension3.Type = LocalFundType)
					ADBDynamicSelection += " and ADBRel.ADBDimension3 = LocalFund"
			ADBDynamicSelection +=")"


	Conditions
		AllocationLoggingEnabled
			restricted
			when (ContextAllocationControl.AllocationLoggingEnabled)

		DoesExist
			restricted
			when (CustodialAccountManagement exists)
			
		HasTaxID
			restricted
			when (TaxID entered)
	
	Relations
		AllocationRunJobRel
			one-to-one relation to AllocationRunJob
			Field Mapping uses ByAllocationLine
                related.FinanceEnterpriseGroup		= LocalFinanceEnterpriseGroup
                related.AllocationSourceSystem		= LocalAllocationSourceSystem
                related.AllocationControl			= LocalAllocationControl
                related.Allocation					= LocalAllocation
                related.AllocationLine				= LocalAllocationLine
                related.AllocationRun				= LocalAllocationRun
                related.JobType						= AllocationRunJobType.InterestAllocationOverrideGeneration


		CustodialSummaryDuplicateRel
			one-to-many relation to CustodialAccountManagement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.UniqueID != UniqueID
				and related.CustodialSummary = CustodialSummary)

		CustodialDetailRel
			one-to-many relation to CustodialDetailAmount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.CustodialAccountManagement = CustodialAccountManagement)
				
		CreatedFineDim10ByDisplayDimRel
			one-to-one relation to FinanceDimension10
			Field Mapping uses ByDisplayDimension
				related.FinanceEnterpriseGroup	= LocalFEG
				related.DisplayDimension		= LocalFinDim10
				related.DimensionStructure		= LocalDimStructure				

		TaxIdentificationTypeRel
			one-to-one relation to TaxIdentificationType
			Field Mapping uses symbolic key
				related.TaxIdentificationType   = CamCategory.TaxIDType

		ShadowDescendantsRel
			one-to-many relation to FinanceDimension10Shadow
			Field Mapping uses BySummaryDimension
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.SummaryFinanceDimension10	= CustodialSummary

		ADBRel
			one-to-many relation to AverageDailyBalanceAggregate
			Field Mapping uses ByGeneralLedgerCalendarPeriod
				related.FinanceEnterpriseGroup										= FinanceEnterpriseGroup
				related.ReportingBasis												= LocalAdbBasis
				related.AverageDailyBalanceAggregate.GeneralLedgerCalendarPeriod	= LocalAdbPeriod
				related.AccountingEntity											= LocalAdbEntity
				related.AverageDailyBalanceAggregate.GeneralLedgerChartAccount		= LocalAdbAccount
			Instance Selection
				where(ADBDynamicSelection)

		ReceiveInterestChildRel
			one-to-many relation to FinanceDimension10
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FinanceDimension10 			= DimensionHierarchyRel.FinanceDimension10
			Instance Selection
				where (related.ReceiveInterest)

		DimensionHierarchyRel
			one-to-many relation to FinanceDimension10Hierarchy
			Field Mapping uses ByParentDimension
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.ParentDimension				= CustodialSummary	


	Rule Blocks
		SetRunJobMappingFields
			LocalFinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup
			LocalAllocationSourceSystem				= PrmAllocationSourceSystem
			LocalAllocationControl					= PrmAllocationControl
			LocalAllocationRun						= PrmAllocationRun
			LocalAllocation							= PrmAllocation
			LocalAllocationLine						= PrmAllocationLine
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		RequestNewCustodialRecord is a Create RequestAction

			action comment required
			request action process is RequestNewCustodialRecord
			request action linkback webapp is FinancialBusinessAnalyst
			allow user fields as parameters
			Parameters
				RequesterActor				is an Actor
			 	RequestedBy					is Alpha 100
	 			RequesterPhone				is a Telephone
			 	RequesterEmail				is an EmailAddress
				RequestPriority				is Numeric 1
					States
						High				value is 1
						Medium				value is 2
						Low					value is 3
				NeededBy					is Date
				CustodialAccountDescription 		is a Description
					default label is "<PrmFinanceEnterpriseGroup.FinanceDimension10Label>Description"
				DocumentId				is Alpha 100
				TransientAttachmentDescription	is a Description
					default label is "Description"
				TransientAttachment				is a DocumentAttachment
					default label is "Attachment"
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				FinanceDimension10Structure
					default label is "<PrmFinanceEnterpriseGroup.FinanceDimension10Label>Structure"
				NewCustodialSummary				is like FinanceDimension10
					default label is "<PrmFinanceEnterpriseGroup.FinanceDimension10Label>"
				DateOfRequest			is Date
				CamCategory
				AccountingEntity
				Approver				is Alpha 100
				DateOfApproval			is Date
				CustodialAccountType    is AlphaUpper size 1
					default label is "AccountType"
					States
						Other 			value is "O"
							default label is "Entity\/\Other"
						Individual 		value is "I"
				DefaultFinanceCodeBlock	is a FinanceCodeBlock
				TaxID
				Title					is Alpha 100
				FirstName
				MiddleName
				LastName
				AccUnitSummaryFund		is an AccountingUnit
				AccUnitPostingFund		is an AccountingUnit
				DimOneSummaryFund		is a FinanceDimension1
				DimOnePostingFund		is a FinanceDimension1
				InterestBearing			is Boolean
				InterestBearingThreshold	is an InternationalAmount

			Parameter Rules
				RequestPriority
					required
				NeededBy
					required
				RequesterActor
					initial value is actor
					default to actor
				RequestedBy
					initial value is RequesterActor.PersonName.GivenName + " " + RequesterActor.PersonName.FamilyName
					default to RequesterActor.PersonName.GivenName + " " + RequesterActor.PersonName.FamilyName
				FinanceDimension10Structure
					initial value is PrmFinanceEnterpriseGroup.EnterpriseDim10Structure
					default to PrmFinanceEnterpriseGroup.EnterpriseDim10Structure
					if (actor.final approver)
						required
							"EnterpriseStructureFor:<PrmFinanceEnterpriseGroup.FinanceDimension10Label>MustBeSetup"
				NewCustodialSummary
					if (actor.final approver)
						required			
				AccountingEntity
					if (actor.final approver)
						required	
				CamCategory
					if (actor.final approver)
						required
						if (CamCategory.SSNRequired.Yes)
							constraint (TaxID entered)
								"TaxIDMustBeEntered"
						if (CamCategory.SSNRequired.No)
							initialize TaxID
				TaxID
					if (actor.final approver)
						if (TaxID entered
						and CamCategory.TaxIDType entered
						and TaxIdentificationTypeRel.HasExpression)
							constraint (TaxID matches TaxIdentificationTypeRel.RegularExpression)
								"TaxIdFormatDoesNotMatch;FormatIs<TaxIdentificationTypeRel.FormatExample>"
				InterestBearingThreshold
					constraint (InterestBearing)
						"InterestBearingMustBeEnabled"
				DateOfRequest
					if (actor.initiator)
						default to current corporate date
				Approver
					if (!actor.initiator)
						default to actor.PersonName.GivenName + " " + actor.PersonName.FamilyName
				DateOfApproval
					if (!actor.initiator)
						default to current corporate date
				TransientAttachment
					if (TransientAttachmentDescription entered)
						required
							"AttachmentIsRequiredWhen_DescriptionIsEntered"
				CustodialAccountType
					if (actor.final approver)
						required

				DefaultFinanceCodeBlock
					if (actor.final approver)
						required
							"FinanceStructureIsRequired"
						if(DefaultFinanceCodeBlock.ToAccountingEntity not entered)
							DefaultFinanceCodeBlock.ToAccountingEntity = AccountingEntity
						constraint (DefaultFinanceCodeBlock.ToAccountingEntity = AccountingEntity)
							"AccountingEntityMustBeTheSameOnCustodialRecordAndDefaultFinanceStructure"
						if (PrmFinanceEnterpriseGroup.AccountingUnitRequired)
							constraint (DefaultFinanceCodeBlock.AccountingUnit entered)
								"<PrmFinanceEnterpriseGroup.AccountingUnitLabel>IsRequired"
						if (PrmFinanceEnterpriseGroup.ProjectRequired)
							constraint (DefaultFinanceCodeBlock.Project entered)
								"<PrmFinanceEnterpriseGroup.ProjectLabel>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension1Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension1 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension1Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension2Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension2 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension2Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension3Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension3 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension3Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension4Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension4 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension4Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension5Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension5 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension5Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension6Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension6 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension6Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension7Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension7 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension7Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension8Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension8 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension8Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension9Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension9 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension9Label>IsRequired"
						if (PrmFinanceEnterpriseGroup.FinanceDimension10Required)
							constraint (DefaultFinanceCodeBlock.FinanceDimension10 entered)
								"<PrmFinanceEnterpriseGroup.FinanceDimension10Label>IsRequired"

			Local Fields
			
			Action Rules

				invoke CreateFinanceDimension10 FinanceDimension10Structure
					fill in user fields from parameters
					invoked.NewFinanceDimension10		= NewCustodialSummary
					invoked.Description					= CustodialAccountDescription
					invoked.DimensionType 				= DimensionType.Summary
					invoked.CustodialManagementRecord	= true
					invoked.InterestBearing				= InterestBearing
					invoked.InterestBearingThreshold	= InterestBearingThreshold
				LocalFEG 			= PrmFinanceEnterpriseGroup
				LocalFinDim10		= NewCustodialSummary
				LocalDimStructure	= FinanceDimension10Structure
				invoke Create this instance
					fill in user fields from parameters
					invoked.FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
					invoked.CustodialSummary				= CreatedFineDim10ByDisplayDimRel.FinanceDimension10
					invoked.CamCategory						= CamCategory
					invoked.TaxID							= TaxID
					invoked.AccountingEntity				= AccountingEntity
					invoked.DateOfRequest					= DateOfRequest
					invoked.Approver						= Approver
					invoked.DateOfApproval					= DateOfApproval
					invoked.CustodialAccountType			= CustodialAccountType
					invoked.DefaultFinanceCodeBlock			= DefaultFinanceCodeBlock
					invoked.Title							= Title
					invoked.FirstName						= FirstName
					invoked.MiddleName						= MiddleName
					invoked.LastName						= LastName
					invoked.AccUnitSummaryFund				= AccUnitSummaryFund
					invoked.AccUnitPostingFund				= AccUnitPostingFund
					invoked.DimOneSummaryFund				= DimOneSummaryFund
					invoked.DimOnePostingFund				= DimOnePostingFund
				if(TransientAttachment entered)
					invoke Create CustodialAttachment
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.CustodialAccountManagement	= this instance.CustodialAccountManagement
						invoked.DocumentId					= DocumentId
						invoked.Description					= TransientAttachmentDescription
						invoked.Attachment					= TransientAttachment


		CreateAllocationLineOverrides is a Set Action
			restricted
			default label is untranslatable
			disable checkpoint
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
				PrmAllocationControl      	is an AllocationControl
				PrmAllocationSourceSystem 	is an AllocationSourceSystem
				PrmAllocation				is an Allocation
				PrmAllocationLine			is an AllocationLine
				PrmAllocationRun			is an AllocationRun
				PrmAccountingEntity			is an AccountingEntity
				PrmReportingBasis			is a ReportingBasis
				PrmAccount					is an Account
				PrmPeriod					is a GeneralLedgerCalendarPeriod
				PrmFilterByDim1				is Boolean
				PrmFilterByAccountingUnit	is Boolean
				PrmFund						is like AccountingUnit
				PrmFundType					is a CodeBlockDimensionType
			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    DefaultFinanceCodeBlock.ToAccountingEntity 	= PrmAllocationLine.SourceAddress.AccountingEntity
				and   (not PrmFilterByDim1 			 or DefaultFinanceCodeBlock.FinanceDimension1	= PrmFund)
				and   (not PrmFilterByAccountingUnit or DefaultFinanceCodeBlock.AccountingUnit      = PrmFund))

			Local Fields
				LocalAdb				 		is an InternationalAmount
			Action Rules
				Empty Set Rules
					if (not PrmAllocationRun.ListingReportOnly
					and AllocationLoggingEnabled)
						include SetRunJobMappingFields
						invoke Start AllocationRunJobRel
						invoke End AllocationRunJobRel

				Set Rules
					Entrance Rules
						if (not PrmAllocationRun.ListingReportOnly
						and AllocationLoggingEnabled)
							include SetRunJobMappingFields
							invoke Start AllocationRunJobRel
					Exit Rules
						if (not PrmAllocationRun.ListingReportOnly
						and AllocationLoggingEnabled)
							include SetRunJobMappingFields
							invoke End AllocationRunJobRel
				Instance Rules
					if (CustodialSummary.InterestBearing)
						LocalFund			= PrmFund
						LocalFundType		= PrmFundType
						LocalAdbBasis 		= PrmReportingBasis
						LocalAdbPeriod 		= PrmPeriod
						LocalAdbEntity 		= PrmAccountingEntity
						LocalAdbAccount 	= PrmAccount

						LocalAdb = sum ADBRel.AverageOnPostDate

						if (config.AbsoluteValueADBCustodialAccountInterest = true)
							if (LocalAdb < 0)
								LocalAdb = LocalAdb * -1

						if (LocalAdb entered 
						and LocalAdb >= CustodialSummary.InterestBearingThreshold)

							for each ReceiveInterestChildRel
								invoke Create AllocationLineTempOverride
									invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
									invoked.AllocationSourceSystem  	= PrmAllocationSourceSystem
									invoked.AllocationControl			= PrmAllocationControl
									invoked.Allocation					= PrmAllocation
									invoked.AllocationLine				= PrmAllocationLine
									invoked.AllocationRun				= PrmAllocationRun
									invoked.Direction					= 1 
									invoked.WeightDecimal				= LocalAdb

									invoked.AccountingEntity			= DefaultFinanceCodeBlock.ToAccountingEntity
									invoked.AccountingUnit				= DefaultFinanceCodeBlock.AccountingUnit
									invoked.GeneralLedgerChartAccount 	= DefaultFinanceCodeBlock.GeneralLedgerChartAccount
									invoked.Project						= DefaultFinanceCodeBlock.Project
									invoked.FinanceDimension1			= DefaultFinanceCodeBlock.FinanceDimension1
									invoked.FinanceDimension2			= DefaultFinanceCodeBlock.FinanceDimension2
									invoked.FinanceDimension3			= DefaultFinanceCodeBlock.FinanceDimension3
									invoked.FinanceDimension4			= DefaultFinanceCodeBlock.FinanceDimension4
									invoked.FinanceDimension5			= DefaultFinanceCodeBlock.FinanceDimension5
									invoked.FinanceDimension6			= DefaultFinanceCodeBlock.FinanceDimension6
									invoked.FinanceDimension7			= DefaultFinanceCodeBlock.FinanceDimension7
									invoked.FinanceDimension8			= DefaultFinanceCodeBlock.FinanceDimension8
									invoked.FinanceDimension9			= DefaultFinanceCodeBlock.FinanceDimension9
									invoked.FinanceDimension10  		= each.FinanceDimension10
									invoked.SourceAddress				= PrmAllocationLine.SourceAddress

									invoked.GeneralLedgerCalendarPeriod = PrmPeriod
