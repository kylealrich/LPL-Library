CashForecastPeriodAmount is a BusinessClass
	owned by cashmgmt
	prefix is CMFPA

	Ontology
		symbolic key is CashForecastPeriodAmount
		
	Patterns
 		implements DynamicCreation
		implements AnalyticCube
 			disable continuous update
 			dynamically calculate totals
 			
	Persistent Fields								
		ForecastAmount	 		is an InternationalAmount 
			default label is "Forecast"
		OriginalForecastAmount 	is an InternationalAmount
			default label is "Forecast"
		WeeklyPublishedForecastAmount is an InternationalAmount
			default label is "Forecast"
		MonthlyPublishedForecastAmount is an InternationalAmount
			default label is "Forecast"
		AdjustmentAmount 		is an InternationalAmount
		ActualAmount	 		is an InternationalAmount
			default label is "Actual"
		Comment			 		is Text
		CreatedByStatement		is a BankStatement
			delete cascades
			context of CashForecastAccount.CashManagementAccount
		ReconciledToStatement	is Boolean
	
	Transient Fields
		TransientFromCurrency	is a FromCurrency
			derive value from CashForecastAccount.CashManagementAccount.Currency
		SystemCalculated		is Boolean
		
	Field Rules
		ForecastAmount
			if (CashForecast.Status.Active)
				constraint (CashForecastPeriod >= current corporate date)
					"CannotMaintainForecastForPeriodsPriorToToday"
			else
				constraint (CashForecast.Status.New)	
					"CannotMaintainForecastInAnAwaitingApprovalOrClosedStatus"
						
	Derived Fields
		CurrentDate is a DerivedField
			type is Date
			restricted
			if (!CashForecast.Status.Active) 
				return CashForecast.ForecastDateRange.Begin
			else
				return first ImportedBankStatementByDateRel.StatementDate
			
		CurrentPeriod is a DerivedField
			type is Numeric 3
			restricted
			return CurrentDate year day
		
		CurrentWeek is a DerivedField
			type is Numeric 2
			restricted
			return CurrentDate week
			
		CurrentMonth is a DerivedField
			type is Numeric 2
			restricted
			return CurrentDate month
		



				
		CurrentYear is a DerivedField
			type is Numeric 4
			restricted
			return CurrentDate year
				
		ForecastDatePeriod is a DerivedField
			type is Numeric 3
			restricted
			default label is "Day/Year"
			return CashForecastPeriod year day
		
		ForecastDateWeek is a DerivedField
			type is Numeric 2
			restricted
			return CashForecastPeriod week
			
		ForecastDateMonth is a DerivedField
			type is Numeric 2
			restricted
			return CashForecastPeriod month
		



					
		ForecastDateYear is a DerivedField
			type is Numeric 4
			restricted
			return CashForecastPeriod year
	
		CorporateBaseAmount is a DerivedField
			type is like InternationalAmount
			if (CashForecastAccount.CashManagementAccount.Currency = CashManagementGroup.Currency)
				return ForecastAmount
			else
				return (ForecastAmount * CashForecastAccount.CorporateBaseCurrencyRate)
		
		LocationAmount is a DerivedField
			type is like InternationalAmount
			if (CashForecastAccount.CashManagementAccount.Currency = CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
				return ForecastAmount
			else
				return (ForecastAmount * CashForecastAccount.LocationCurrencyRate)
		
		OriginalCorporateBaseAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (CashForecastAccount.CashManagementAccount.Currency = CashManagementGroup.Currency)
				return OriginalForecastAmount
			else
				return (OriginalForecastAmount * CashForecastAccount.CorporateBaseCurrencyRate)
		
		OriginalLocationAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (CashForecastAccount.CashManagementAccount.Currency = CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
				return OriginalForecastAmount
			else
				return (OriginalForecastAmount * CashForecastAccount.LocationCurrencyRate)
		
		WeeklyPublishedCorporateBaseAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (CashForecastAccount.CashManagementAccount.Currency = CashManagementGroup.Currency)
				return WeeklyPublishedForecastAmount
			else
				return (WeeklyPublishedForecastAmount * CashForecastAccount.CorporateBaseCurrencyRate)
		
		WeeklyPublishedLocationAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (CashForecastAccount.CashManagementAccount.Currency = CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
				return WeeklyPublishedForecastAmount
			else
				return (WeeklyPublishedForecastAmount * CashForecastAccount.LocationCurrencyRate)
		
		MonthlyPublishedCorporateBaseAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (CashForecastAccount.CashManagementAccount.Currency = CashManagementGroup.Currency)
				return MonthlyPublishedForecastAmount
			else
				return (MonthlyPublishedForecastAmount * CashForecastAccount.CorporateBaseCurrencyRate)
		
		MonthlyPublishedLocationAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (CashForecastAccount.CashManagementAccount.Currency = CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
				return MonthlyPublishedForecastAmount
			else
				return (MonthlyPublishedForecastAmount * CashForecastAccount.LocationCurrencyRate)
				
		DerivedActualAmount is a DerivedField
			type is like InternationalAmount
			default label is "Actual"
			return (sum ActualAmountRel.LineAmount)
		
		VarianceAmount is a DerivedField
			type is like InternationalAmount
			default label is "Variance"
			return (ActualAmount - ForecastAmount)
			
		WeeklyPublishedVarianceAmount is a DerivedField
			type is like InternationalAmount
			default label is "Variance"
			return (ActualAmount - WeeklyPublishedForecastAmount)
		
		MonthlyPublishedVarianceAmount is a DerivedField
			type is like InternationalAmount
			default label is "Variance"
			return (ActualAmount - MonthlyPublishedForecastAmount)
				
		OriginalVarianceAmount is a DerivedField
			type is like InternationalAmount
			default label is "Variance"
			return (ActualAmount - OriginalForecastAmount)
			
        YTDActualChange is a DerivedField
        	type is Percent 6.3
        	default label is "Yo\YChange"
        	return ((ActualAmount period ending ytd balance - ActualAmount prior year period ending ytd balance) / ActualAmount prior year period ending ytd balance)
        	
        DerivedCommentLinkName is a DerivedField
			type is Alpha 12
			restricted
			if (Comment entered)
				return ViewCommentMessage
			else
				return AddCommentMessage

		AddCommentMessage is a LabelField
			restricted
			"AddComment"		

		ViewCommentMessage is a LabelField
			restricted
			"ViewComment"		
		
		CashTransactionMessage is a MessageField
			restricted
			"CashForecastTransaction"
		
		CashAdjustmentMessage is a MessageField
			restricted
			"CashForecastAdjustment"
							
	Dimensions
    	ForecastDatePeriod 
	    	is a daily period dimension with year of ForecastDateYear
	    		current year is CurrentYear
	    		current period is CurrentPeriod
		CashForecast
			Instance Selection 
				where (IsNewOrActiveNonSimulated)
		CashForecastCategory
		CashForecastAccount
		CashForecastAccount.CashManagementAccount.FinancialInstitution
			dimension name is FinancialInstitution
		CashForecastAccount.CashManagementAccount.CashManagementLocation
			dimension name is CashManagementLocation
		CashForecastAccount.CashManagementAccount.LegalEntity
			dimension name is LegalEntity
	
	Measures
		ForecastAmount
		ActualAmount
		VarianceAmount
		YTDActualChange
		OriginalForecastAmount
		OriginalVarianceAmount
		WeeklyPublishedForecastAmount
		WeeklyPublishedVarianceAmount
		MonthlyPublishedForecastAmount
		MonthlyPublishedVarianceAmount
		
	Conditions
		ForecastDetailExists
			restricted
			when (ForecastAmountRel exists)
		
		ShowDetailLink
			restricted
			when (ForecastAmount entered
			and	  ForecastAmountRel exists)
		
		ForecastUpdatable
			restricted
			when (CashForecast.Status.New
			or	 (CashForecast.Status.Active
			and	  CashForecastPeriod >= current corporate date))
		
		HasAdjustment
			restricted
			when (AdjustmentAmount entered)
			
		AccountCurrencyNotCorporateBaseCurrency
			restricted
			when (CashForecastAccount.CashManagementAccount.Currency != CashManagementGroup.Currency)
			
		AccountCurrencyNotLocationCurrency
			restricted
			when (CashForecastAccount.CashManagementAccount.Currency != CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)

		CanResetActual
			restricted
			when (ActualAmount != DerivedActualAmount)
			
		CanPurge
			restricted
			when (!ForecastAmount entered
			and	  !ActualAmount entered
			and	  !OriginalForecastAmount entered
			and	  !WeeklyPublishedForecastAmount entered
			and	  !MonthlyPublishedForecastAmount entered)
			
	Relations
		ForecastAmountRel
			one-to-many relation to CashForecastDetail
		 	Field Mapping uses symbolic key
		 		related.CashManagementGroup	 = CashManagementGroup
		 		related.CashForecast		 = CashForecast		
		 		related.CashForecastAccount	 = CashForecastAccount
		 		related.CashForecastPeriod	 = CashForecastPeriod
		 		related.CashForecastCategory = CashForecastCategory
		
		ActualAmountRel
			one-to-many relation to BankStatementLine
		 	Field Mapping uses ByCategoryDate
		 		related.CashManagementGroup		= CashManagementGroup			
		 		related.CashManagementAccount	= CashForecastAccount.CashManagementAccount
		 		related.CashTransactionCategory = CashForecastCategory.CashTransactionCategory
				related.StatementDate 			= CashForecastPeriod
			Instance Selection
				where (!related.StatementIntraday)
				
		ForecastAdjustmentDetailRel
			one-to-many relation to CashForecastDetail
		 	Field Mapping uses symbolic key
		 		related.CashManagementGroup	 = CashManagementGroup
		 		related.CashForecast		 = CashForecast		
		 		related.CashForecastAccount	 = CashForecastAccount
		 		related.CashForecastPeriod	 = CashForecastPeriod
		 		related.CashForecastCategory = CashForecastCategory
			Instance Selection
				where (related.Adjustment)
		
		ImportedBankStatementByDateRel 
			one-to-many relation to BankStatement
			Field Mapping uses ByStatementDate
				related.CashManagementGroup = CashManagementGroup
		
		CashForecastPeriodAmountCubeRel
            one-to-one relation to AnalyticCube
            Field Mapping uses AnalyticCubeSet
                related.BusinessClass = "CashForecastPeriodAmount"
				
  	Sets
		ByCashTransactionCategory
  			Sort Order
  				CashManagementGroup
				CashForecast
				CashForecastCategory
				CashForecastAccount
				CashForecastPeriod

	Rule Blocks
  		CreateOnlyCashForecastDetail
			if (!SystemCalculated
			and (ForecastAmount changed
			or   !old ForecastAmount entered))
				invoke CreateOnly CashForecastDetail
					invoked.CashManagementGroup	 = CashManagementGroup
			 		invoked.CashForecast		 = CashForecast		
			 		invoked.CashForecastAccount	 = CashForecastAccount
			 		invoked.CashForecastPeriod	 = CashForecastPeriod
			 		invoked.CashForecastCategory = CashForecastCategory
					if (CashForecast.Status.New)
						invoked.Description		 = CashTransactionMessage
					else
						invoked.Description		 = CashAdjustmentMessage
						invoked.Adjustment		 = true
					if (CashForecastPeriodAmount exists
					and ForecastAmount changed) 
						invoked.ForecastAmount 	 = (ForecastAmount - old ForecastAmount)
					else
						invoked.ForecastAmount 	 = ForecastAmount
						
				if (CashForecast.Status.Active)
					AdjustmentAmount += (ForecastAmount - old ForecastAmount)
	
  	Actions
  		Create is a Create Action
			valid when (ForecastUpdatable)
			Action Rules
				constraint (CashForecastPeriod set exists)
					"CashForecastNotDefinedForPeriod<CashForecastPeriod>"
						
				constraint (!CashForecastCategory.HasChild)
  					"CannotUpdateASummaryLevelCategory"
  					
  				include CreateOnlyCashForecastDetail
				
		CreateOnly is a Create Action
			default label is untranslatable
			restricted
			Action Rules
				constraint (CashForecastPeriod set exists)
					"CashForecastNotDefinedForPeriod<CashForecastPeriod>"
					
				constraint (!CashForecastCategory.HasChild)
  					"CannotUpdateASummaryLevelCategory"
  								
  		Update is an Update Action
  			valid when (ForecastUpdatable)
  			Action Rules
				constraint (CashForecastPeriod set exists)
					"CashForecastNotDefinedForPeriod<CashForecastPeriod>"
					
				constraint (!CashForecastCategory.HasChild)
  					"CannotUpdateASummaryLevelCategory"
  					
				if (CashForecast.Status.Active)
					constraint (CashForecastPeriod >= current corporate date)
						"CannotMaintainForecastForPeriodsPriorToToday"
				else
					constraint (CashForecast.Status.New)	
						"CannotMaintainForecastInAnAwaitingApprovalOrClosedStatus"
						
				include CreateOnlyCashForecastDetail
				
			Exit Rules
				if (!ForecastAmount entered
				and !CreatedByStatement entered)
					invoke Delete
				
  		UpdateOnly is an Update Action
  			default label is untranslatable
  			restricted
  			Action Rules
  				constraint (CashForecastPeriod set exists)
					"CashForecastNotDefinedForPeriod<CashForecastPeriod>"
					
				constraint (!CashForecastCategory.HasChild)
  					"CannotUpdateASummaryLevelCategory"
  					
  			Exit Rules
				invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
  					
		UpdateComment is an Update Action
			valid when (ForecastUpdatable)
			Field Rules
				ForecastAmount
					cannot be changed
				OriginalForecastAmount
					cannot be changed
				WeeklyPublishedForecastAmount
					cannot be changed
				MonthlyPublishedForecastAmount
					cannot be changed
				AdjustmentAmount
					cannot be changed
				ActualAmount
					cannot be changed
				CreatedByStatement
					cannot be changed
		
		UpdateAmount is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCashManagementGroup	is a CashManagementGroup
				PrmCashForecast			is a CashForecast
				PrmAmountType			is Numeric 1
					States
						Original			value is 1
						PublishWeek			value is 2
						PublishMonth		value is 3
				PrmDateRange			is a DateRange
			
			Parameter Rules
				PrmCashManagementGroup	required
				PrmCashForecast			required
				PrmAmountType			required
				PrmDateRange 			required
				
			Instance Selection
				where (CashManagementGroup = PrmCashManagementGroup
				and	   CashForecast = PrmCashForecast
				and    CashForecastPeriod within PrmDateRange
				and    ForecastAmount entered)
			
			Action Rules
				Empty Set Rules
					invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
					
				Set Rules
					Exit Rules
						invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
						
				Instance Rules
					if (PrmAmountType.Original)
						OriginalForecastAmount = ForecastAmount
					else
					if (PrmAmountType.PublishWeek)
						WeeklyPublishedForecastAmount = ForecastAmount
					else
					if (PrmAmountType.PublishMonth)
						MonthlyPublishedForecastAmount = ForecastAmount
				
		UpdateReconciledStatus is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				if (all ForecastAmountRel.Reconciled)
					ReconciledToStatement	= true
				else
					ReconciledToStatement	= false

		LoadForecastPositionTransactions is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCashManagementGroup		 is a CashManagementGroup
  				PrmCashForecast				 is a CashForecast
  				PrmCashManagementAccount	 is a CashManagementAccount
  				PrmStatementDate			 is Date
  				PrmBankStatement			 is a BankStatement
  				
  			Parameter Rules
  				PrmCashManagementGroup		 required
  				PrmCashForecast				 required
  				PrmCashManagementAccount	 required
  				PrmStatementDate			 required	
  				PrmBankStatement			 required
  			
  			Local Fields
				LocalCashTransactionCategory is a CashTransactionCategory
  				BankStatementPosition
  				
  			Sort Order
				CashManagementGroup
				CashForecast
				CashForecastAccount
				CashForecastPeriod
				CashForecastCategory

			Instance Selection
  				where (CashManagementGroup						 = PrmCashManagementGroup
  				and    CashForecast								 = PrmCashForecast
  				and	   CashForecastAccount.CashManagementAccount = PrmCashManagementAccount
  				and    CashForecastPeriod						 = PrmStatementDate)	
  			
  			Action Rules
  				Set Rules







  						
  				Instance Rules
  					LocalCashTransactionCategory = CashForecastCategory.CashTransactionCategory
  					
  					invoke Update BankStatementPosition
						invoked.CashManagementGroup 	= PrmCashManagementGroup
  						invoked.CashManagementAccount	= PrmCashManagementAccount
  						invoked.BankStatement			= PrmBankStatement
  						invoked.CashTransactionCategory = CashForecastCategory.CashTransactionCategory
  						invoked.ForecastAmount			= ForecastAmount
  		
  		ResetActualAmount is an Instance Action
			valid when (CanResetActual)
			Action Rules
				ActualAmount = DerivedActualAmount
				
		UpdateActualAmountsOnForecast is a Set Action 
			restricted
			Parameters
				PrmCashManagementGroup	is a CashManagementGroup
				PrmCashForecast			is a CashForecast
				PrmCashForecastAccount	is a CashForecastAccount
				PrmCashForecastCategory	is a CashForecastCategory
  				
  			Parameter Rules
  				PrmCashManagementGroup	required
  			
  			Sort Order
  				CashManagementGroup
				CashForecast
				CashForecastAccount
				CashForecastPeriod
				CashForecastCategory
  			
			Instance Selection
  				where (CashManagementGroup = PrmCashManagementGroup
				and   (!PrmCashForecast entered
				or     CashForecast = PrmCashForecast)
				and	  (!PrmCashForecastAccount entered
				or 	   CashForecastAccount = PrmCashForecastAccount)
				and   (!PrmCashForecastCategory entered
				or	   CashForecastCategory = PrmCashForecastCategory))
  			
  			Action Rules
				Empty Set Rules
					invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background

				Set Rules
					Exit Rules
						invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background

  				Instance Rules
  					ActualAmount = DerivedActualAmount
  						
  		Delete is a Delete Action
  			valid when (ForecastUpdatable)
  			Action Rules
  				invoke DeleteOnly ForecastAmountRel
  			
  		Purge is a Purge Action
  			valid when (CanPurge)
  			Action Rules
  				invoke Purge ForecastAmountRel
  				
   			Exit Rules
  				if (CashForecastCategory.CreatedFromStatementLine
  				and !CashForecastCategory.CashForecastPeriodAmount set exists)
  					invoke Purge CashForecastCategory
