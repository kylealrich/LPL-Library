ASStoredConfiguration is a BusinessClass
	default label is "StoredConfigurationFile"
    owned by appsetup
    prefix is ASSCF
        
    Ontology
     	symbolic key is ASStoredConfiguration
     	
    Patterns
    	implements LightweightAuditing
    	disable EffectiveDated
     	disable AuditIndex
    
    Persistent Fields
    	Description 		    	is Alpha size up to 500
    	ConfigurationFile 	    	is BinaryDocument
    	ConfigurationFileName   	is FileName size 50
    		default label is "FileName"
    	MimeType
        ASConfigurationName			is like ASConfiguration
		VersionNumber				is Numeric 5
        StoredConfigType			is Numeric size 1
			default label is "StoredFileType"
			States
    			UpgradeBackup		value is 0
				RestoreBackup		value is 1
    			CustomerBackup		value is 2
				DifferenceSummary	value is 3
				CSPListing			value is 4
		BaseConfiguration			is like ASConfiguration
		BaseVersionNumber			is Numeric 5
		CreateTimestamp 			is TimeStamp

	Local Fields
		ConfigurationParameterName		is Alpha size up to 150	
	
	Derived Fields
		SuperuserOn is a DerivedField
			type is Boolean
			ConfigurationParameterName = "appsetup.superuser.mode"
			if (ConfigurationParameterRel exists)
				return ConfigurationParameterRel.Value
			return false

	Field Rules
        CreateTimestamp
            default to current timestamp
	
	Relations
		ConfigurationParameterRel
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= ConfigurationParameterName	
		
	Actions	
		Create is an Action
			restricted

		Delete is an Action

		Update is an Action

		Purge is a Purge Action
    		restricted
			bypass relational integrity rules
	
		PurgeStoredConfiguration is an Instance Action
			valid when (SuperuserOn)

			Action Rules
				invoke Purge	

		
		PurgeOldBackups is a Set Action 
			disable checkpoint

			Parameters
				PrmPurgeDate 				is Date
					default label is untranslatable
				PrmASConfigurationName		is like ASConfiguration
					default label is untranslatable
				
            Instance Selection
                where (ASConfigurationName = PrmASConfigurationName
				and CreateTimestamp < PrmPurgeDate)

			Action Rules
				Instance Rules
					invoke Purge




				
