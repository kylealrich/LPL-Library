PurchaseOrderInterfaceInput is a BusinessClass
    owned by po
    prefix is W2960

    Ontology
    	symbolic key is PurchaseOrderInterfaceInput
    
    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations
		disable StaticTranslations

    Persistent Fields
       	RunGroup                
        Company                         is a PurchasingCompany                   
        RecordType						is Numeric 2
			States
				AddPurchaseOrder			value is 1
				AddPurchaseOrderLine     	value is 2
				UpdatePurchaseOrder			value is 3
				UpdatePurchaseOrderLine  	value is 4
				AddLineDistribution         value is 5
				UpdateLineDistribution      value is 6
				CancelPurchaseOrder			value is 7
				CancelPurchaseOrderLine  	value is 8
				PurchaseOrderSpreadAOC		value is 9
				AddAddOnCharge				value is 10
				DeleteLineDistribution      value is 11
				UpdateAddOnCharge			value is 12
				CancelAddOnCharge			value is 13
				AddComment                  value is 14
				AddAssetDefault             value is 15
				UpdateAssetDefault          value is 16
        		DoNotUpdate                 value is 99
        PurchaseOrderImport     is like PurchaseOrderImport
        PurchaseOrder           is like PurchaseOrder
        PORelease               is like PORelease
        POCode                  is like POCode   
        LineNumber              is like PurchaseOrderLine
        DistributionSequence    is like PurchaseOrderLineDistribution
        AddOnCharge             is like AddOnCharge 
        Vendor                  is like Vendor
        InterfacedVendor        is a OldVendor
        CrossReferenceVendor    is like CrossReferenceVendor
        PurchaseFromLocation    is like VendorLocation  
        Buyer                   is like Buyer    
        PurchaseOrderDate       is Date	
        Currency                is like Currency
        EnteredCurrencyConversionRate is like CnvRate  
        CurrencyExchangeSetPoint  is like CurrencyExchangeSetPoint
        Revalue                 is Boolean
        DefaultDeliveryDate     is Date	
        ShipToLocation          is like InventoryLocation   
        DropshipName            is a Name	 
        	holds pii
        DropshipAddress  		is a PostalAddressV2	
        	holds pii
        DropshipPhoneNumber		is a TelephoneNumber 
        	holds pii
        DropshipContact         is an AddressContact
        Dropship                is Boolean
        PrintReceipt            is Boolean
        FaxNumber				is like FaxNumber
        ProcessLevel            is like PayablesProcessLevel
        FreightTerm             is like FreightTerm
        ShipTerm                is like ShipTerm
        ShippingMethod          is like ShipVia 
        TermCode                is like TermsCode
        TaxCode                 is like TaxCode        
        LetterOfCredit          is like LetterOfCredit
        DefaultTaxable          is Boolean
		DefaultAssetTemplate    is like AssetTemplate
        DefaultAsset            is like Asset    
		DefaultSourceInformation is like DefaultSourceInformation
        Closed                  is like Closed
        InvoiceMethodCode       is like InvoiceMethod
        IssueMethod             is like PurchaseOrderIssueMethod
        RevisionsInclude        is AlphaUpper size 1
        UnloadingPort           is like UnloadingPort
        NatureOfTransactionCode is like NatureOfTransactionCode
        StatisticalProcedure    is like IntrastatStatisticalProcedure
        ShipToArriveDate        is Date
        SubcontractPurchaseOrder is Boolean
        WarehouseNumber         is like WarehouseNumber
        MatchPrepayment         is Boolean
        ProcurementCardNumber   is like ProcurementCardNumber
        UseProcurementCard      is Boolean
        RetainagePercentCompleteTracking is Numeric size 1
        RetainageFirstPercent   is like Pct    
        RetainageSecondPercent  is like Pct    
        RetainageUpToPercentComplete is like Pct     
        RetainageOverridePercentComplete is like Pct     
        RetainageDueDate        is Date
        PurchaseOrderBillToName is like Name	 
        	holds pii
        PurchaseOrderBillToAddress is a PostalAddressV2	
        	holds pii
        PurchaseOrderBillToContact is like AddressContact
        PurchaseOrderBillToPhoneNumber is like TelephoneNumber 
        	holds pii
        PurchaseOrderBillToEmailAddress is like EmailAddressMulti 
        DefaultDistributionAccount is a FinanceCodeBlock
        GlobalDocumentType      is like GlobalDocumentType
        DefaultProcedureInformation is like DefaultProcedureInformation
        UserDate1                   is Date
        UserDate2                   is Date
		PurchaseOrderUserField1
        PurchaseOrderUserField3     is a UserField2
        PurchaseOrderUserField5     is a UserFld2
		PurchaseOrderUserField7     is a UserFld7

        Item                    is like Item
        ItemType                is like ItemType
        ServiceCode             is like ServiceCode
        Description             is like Description    
        Quantity                is like Quantity
        EnteredUnitCost         is like InternationalCost
        TaxableUnitCost         is like InternationalCost
        EnteredBuyUOM           is like UnitOfMeasure
        EarlyDeliveryDate       is Date
        LateDeliveryDate        is Date
        VendorItem              is like VendorItem    
        DistributionCode        is like PayablesDistributionCode
        StockWeight             is like StockWeight
        StockVolume             is like StockVolume
        CostOption              is like CostOption
        CertificationRequired    is Boolean
        InspectionRequired       is Boolean
        MSDSRequired             is Boolean
        IgnoreLeadTimeCalculation is Boolean
        OvershipmentTolerancePercentage is like OvershipmentTolerancePercentage    
        UndershipmentTolerancePercentage is like UndershipmentTolerancePercentage    
        MajorPurchasingClass    is like MajorPurchasingClass  
        MinorPurchasingClass    is like MinorPurchasingClass  
        Manufacturer            is like Manufacturer   
        ManufacturerNumber      is like ManufacturerNumber  
        ZeroOvershipment         is Boolean
        	restricted
        ZeroUndershipment        is Boolean
        	restricted
        ExcludeFromAddOnChargeSpread is Boolean
        TaxUsageCode            is like TaxUsageCode
        ICNCode                 is like ICNCode
        SupplementaryQuantity   is like Quantity
        CommodityCode           is like CommCodes
        Retainage               is Boolean
		ProductTaxCategory      is like ProductTaxCategory
		ItemGTIN                is like ItemGTIN
        GlobalLineType          is like GlobalLineType        
        DistributionBy          is like DistributionBy
        UserDate3               is Date
        UserDate4               is Date
		PurchaseOrderUserField2
        PurchaseOrderUserField4 is a UserFld2
        PurchaseOrderUserField6 is a UserFld3

        CommentTitle            is like CommentName
        CommentType             is AlphaUpper size 1
        CommentText             is like TextComment

        AddOnChargePercent      is like AddOnChargePercent
        TotalAddOnChargeAmount  is like InternationalAmount
        PrintOnPO               is Boolean
        Summarize               is Boolean
        ZeroCost	   		    is Boolean

        DistributionAccount     is like FinanceCodeBlock
        AssetTemplate           is like AssetTemplate
        Asset                   is like Asset
		DistributionAmount      is an InternationalAmount
        DistributionPercent     is like TotalPercent
        DistributionQuantity    is a Quantity

        AssetDescription        is like Description
        TagNumber               is like TagNumber
        AssetGroup              is like AssetGroup
        AssetAccountingUnitGroup is like AssetAccountingUnitGroup
        Combine                 is like Combine
        HoldAsset               is Boolean
        ItemNumber              is like AssetItemNumber
        Project                 is like Project
        ItemDescription         is like Description
        ModelNumber             is like ModelNumber
        PurchaseDate            is Date    
        InServiceDate           is Date    
        ItemTaxTran             is like InternationalAmount
        AssetLocation           is like AssetLocation   
        AssetDivision           is like AssetDivision
        LocationDetail          is like AssetLocationDetail
               


        DfltCnCode              is like ConsignCode
        PatientId               is like Patient
        VisitNbr                is like  VisitNumber
        CaseNbr                 is like CaseNumber
        Proc                    is like Procedure
        ProcDate                is Date
        PhysName                is like Name	 
        	holds pii
        NrsCoord                is like NrsCoord
        SalesRep                is like SalesRep
        OrdNbr                  is like OrdNbr
        Requestor               is like Requestor 
        Userdef1                is like Userdef1
        Userdef2                is like Userdef2
        PatientLastName    		is a LastName
        PatientFirstName        is a FirstName	 
        	holds pii
        PatientMiddleName       is a MiddleName
        ProcedureModifier       is Alpha size 2
        Birthdate               is Date
        Gender
        SSN                     is a TaxID 
        	holds pii
        Lot                     is Alpha size 20
        SerNbr                  is Alpha size 20
        CnCode                  is like ConsignCode
		OvershipmentToleranceOption
		UndershipmentToleranceOption

		EAMWorkOrder
		EAMWorkOrderActivity
		EAMProjectID
		EAMProjectType
		EAMProjectTaxCode
		EAMManufacturing
    	EAMTrade
    	EAMCommodityCode
    	EAMLineType
    	Chemical					is Boolean
    	RebuildRepair				is Boolean
    	TestRequired				is Boolean
    	EAMTool						is a EAMToolField  

    Local Fields
    	LocalRunGroup           		is a RunGroup
    	LocalCompany            		is like Company
    	LocalPurchaseOrderImport 		is like PurchaseOrderImport
		LocalPOCode                     is like POCode
		LocalErrorMessage       		is Alpha size 150
		InterfacedPurchaseOrderNumber   is a PurchaseOrder view
		LocalInterfaceResult            is like PurchaseOrderInterfaceResult
		LocalPurchaseOrder              is like PurchaseOrder
		LocalAddOnCharge                is like AddOnCharge
		LocalLineNumber                 is like PurchaseOrderLine
		LocalPurchaseOrder2             is like PurchaseOrder
		LocalPurchaseOrderLineExists    is like PurchaseOrder
		LocalFinanceEnterpriseGroup 	is like FinanceEnterpriseGroup 
		LocalRecordType					is Numeric 2
        LocalErrorLevel                 is Numeric 2
        	States
        		Header                   value is 1
        		Line                     value is 2
        		HeaderAOC                value is 3
        		SpreadAOC                value is 4
        		LineAOC                  value is 5
        		HeaderComment            value is 6
        		LineComment              value is 7
        		LineDistribution         value is 8
        		LineAssetDefault         value is 9

				
    Derived Fields
    	
    	NotEligibleToCancelMessage is a MessageField
    		restricted
    		"PurchaseOrderIsNotEligibleToCancel"
    		
    	AddOnChargeNotEligibleToCancelMessage is a MessageField
    		restricted
    		"AddOnChargeIsNotEligibleToCancel"
    		
    	NotFoundToCancelMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderCannotBeFoundToCancel"
    		
    	PurchaseOrderAlreadyExistsMessage is a MessageField
    		restricted
    		"CannotAddNewPurchaseOrder;InterfacedPurchaseOrderAlreadyCreatedPurchaseOrder"
    
    	CannotUpdateClosedMessage is a MessageField
    		restricted
    		"CannotUpdate;PurchaseOrderIsClosed"
    		
    	NotFoundToUpdateMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderCannotBeFoundToUpdate"
    		
    	AddOnChargeNotFoundToUpdateMessage is a MessageField
    		restricted
    		"AddOnChargeCannotBeFoundToUpdate"
    		
    	LineNotEligibleToCancelMessage is a MessageField
    		restricted
    		"PurchaseOrderLineIsNotEligibleToCancel"
    		
    	LineNotFoundToCancelMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderLineCannotBeFoundToCancel"
		
		PurchaseOrderLineAlreadyExistsMessage is a MessageField
    		restricted
    		"CannotAddNewPurchaseOrderLine;PurchaseOrderLineNumberAlreadyExists"
    
    	LineNotFoundToUpdateMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderLineCannotBeFoundToUpdate"
    		
    	DistributionNotFoundMessage is a MessageField
    		restricted
    		"ReferencedDistributionCannotBeFoundToUpdateOrDelete"
    		
    	SpreadAlreadyExistsMessage is a MessageField
    		restricted
    		"CannotAddNewPurchaseOrderSpread;PurchaseOrderSpreadAlreadyExists"

		AOCAlreadyExistsMessage is a MessageField
    		restricted
    		"CannotAddNewPurchaseOrderAddOnCharge;PurchaseOrderAddOnChargeAlreadyExists"
    		
    	CannotSetToClosedMessage is a MessageField
    		restricted
    		"CannotSetHeaderOrLineToClosed;MustProcessToGetToClosedStatus"
    		
    	CannotBeHistoricalMessage is a MessageField
    		restricted
    		"LineOrAddOnChargeCannotBeSetToHistoricalIfHeaderNotSetToHistorical"

    	DuplicateAddOnChargeMessage is a MessageField
    		restricted
    		"CannotProcessAddOnCharge;DuplicateAddOnChargesExistForThisPurchaseOrderAndAddOnCharge"
    	
    	AddOnChargeNotFoundToCancelMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderAddOnChargeCannotBeFoundToCancel"

    	CannotCancelBothHeaderAndLine is a MessageField
    		restricted
    		"CannotCancelBothTheHeaderAndTheLines"	
    
		LineCountForAPO is a DerivedField
			type is Numeric 10 
			return instance count of PurchaseOrderLinesForCleanupRel 
		
		DistributionCountForAPO is a DerivedField
			type is Numeric 10
			return instance count of PurchaseOrderDistributionsForErrorRel
		
		CommentCountForAPO is a DerivedField
			type is Numeric 10
			return instance count of PurchaseOrderCommentsForErrorRel		
		
		AOCCountForAPO is a DerivedField
			type is Numeric 10
			return instance count of PurchaseOrderAddOnChargesForErrorRel

    Conditions
    
    	VendorMappingTableExists
			restricted
			when (InterfaceMappingVendorRel exists)
		
		LegacyVendorExists
			restricted
			when (VendorForLegacyVendorRel exists)
    	
    	PurchaseOrderLineExists
			restricted
			when (PurchaseOrderLineRel exists)
			
		LineDistributionsExist
			restricted
			when (LineDistributionsRel exists)
    
    	LineAddOnChargesExist
    		restricted
    		when (LineAddOnChargesRel exists)
    	
    	SpreadExists
    		restricted
    		when (PurchaseOrderSpreadAddOnChargeRel exists)
    		
    	RunGroupExistsInImport
    		restricted
    		when (ImportRunGroupRel exists)
    	
    	AddOnChargeExists
    		restricted
    		when (PurchaseOrderAndLineAddOnChargeRel exists)
    		
    	ClosedPO
    		restricted
    		when (Closed = "Y")
    		
    	HistoricalPO
    		restricted
    		when (Closed = "H")
    		
    	HeaderIsHistorical
    		restricted
    		when (InterfaceHeaderIsHistoricalRel exists)
    	
    	HeaderExists
    		restricted
    		when (PurchaseOrderForCleanupRel exists)

    	HeaderAddOnCharge
    		restricted
    		when ((RecordType.AddAddOnCharge
        	or     RecordType.UpdateAddOnCharge
        	or     RecordType.CancelAddOnCharge)
    		and    LineNumber not entered)

    	LineAddOnCharge
    		restricted
    		when ((RecordType.AddAddOnCharge
        	or     RecordType.UpdateAddOnCharge
        	or     RecordType.CancelAddOnCharge)
    		and    LineNumber entered)

        SpreadAddOnCharge
        	restricted
    		when (RecordType.PurchaseOrderSpreadAOC)

     	DuplicateInterfaceAddOnChargeExists
    		restricted
    		when (DuplicateInterfaceAddOnChargesRel exists)

		BillToDetailsNotEntered
			restricted
			when (PurchaseOrderBillToName not entered
			and PurchaseOrderBillToAddress not entered
			and PurchaseOrderBillToContact not entered
			and PurchaseOrderBillToPhoneNumber not entered
			and PurchaseOrderBillToEmailAddress not entered)
    			
    Relations
    	
    	PurchaseOrderInterfaceInputByRunGroupRel
    		one-to-many relation to PurchaseOrderInterfaceInput
    		Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = LocalFinanceEnterpriseGroup)
				
		InterfacedPurchaseOrderRel
			one-to-many relation to InterfacedPurchaseOrder
			Field Mapping uses ByImport
				related.Company              = Company
				related.PurchaseOrderImport  = PurchaseOrderImport
				related.POCode               = POCode
		
    	PurchaseOrder2Rel
			one-to-one relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company              = Company
				related.PurchaseOrder        = LocalPurchaseOrder2
    	
    	PurchaseOrderImportHeaderAOCsAddRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode                   
			Instance Selection
				where (related.LineNumber not entered
				and    related.RecordType.AddAddOnCharge)
				
		LineAddOnChargesRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber = LocalLineNumber
				and    related.LineNumber > 0
				and   (related.RecordType.AddAddOnCharge
				or     related.RecordType.UpdateAddOnCharge
				or     related.RecordType.CancelAddOnCharge))
		
		LineDistributionsRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company						= LocalCompany
				related.PurchaseOrderImport			= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection	
				where (related.LineNumber = LocalLineNumber
				and    related.LineNumber > 0
				and   (related.RecordType.AddLineDistribution
				or     related.RecordType.UpdateLineDistribution))
		
		PurchaseOrderImportLineAOCsAddRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber = LocalLineNumber
				and    related.LineNumber > 0
				and    related.RecordType.AddAddOnCharge)
		
		PurchaseOrderImportLineAOCsUpdateRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber = LocalLineNumber
				and    related.LineNumber > 0
				and    related.RecordType.UpdateAddOnCharge)
		
		PurchaseOrderImportLineAOCsCancelRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber = LocalLineNumber
				and    related.LineNumber > 0
				and    related.RecordType.CancelAddOnCharge)
		
		PurchaseOrderImportHeaderAOCsUpdateRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber not entered
				and    related.RecordType.UpdateAddOnCharge)
		
		PurchaseOrderImportHeaderAOCsCancelRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber not entered
				and    related.RecordType.CancelAddOnCharge)
		
		LocalInterfaceResultsRel
			one-to-one relation to PurchaseOrderInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.PurchaseOrderInterfaceResult		= LocalInterfaceResult
				
		PurchaseOrderImportHeaderCommentsRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber not entered
				and    related.RecordType.AddComment)
				
		PurchaseOrderImportLineCommentsRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.LineNumber = LocalLineNumber
				and    related.RecordType.AddComment)
		
		PurchaseOrderAndLineAddOnChargeRel
			one-to-many relation to PurchaseOrderAndLineAddOnCharge
			Field Mapping uses symbolic key
				related.Company                             = LocalCompany
				related.PurchaseOrder                       = LocalPurchaseOrder
				related.PurchaseOrderLine                   = LocalLineNumber
				related.AddOnCharge                         = LocalAddOnCharge
				
		PurchaseOrderImportAssetDefaultsRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport         = LocalPurchaseOrderImport 
				related.POCode                      = LocalPOCode                 
			Instance Selection
				where (related.LineNumber           = LocalLineNumber
				and    related.RecordType.AddAssetDefault)
		
		PurchaseOrderLineAssetDefaultsRel
			one-to-one relation to PurchaseOrderLineAssetDefaults
			Field Mapping uses symbolic key
				related.Company                     = Company
				related.PurchaseOrder			    = LocalPurchaseOrderLineExists                  
				related.PurchaseOrderLine           = LineNumber
		
		PurchaseOrderRel
			one-to-one relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company                     = LocalCompany
				related.PurchaseOrder               = LocalPurchaseOrder
				
		PurchaseOrderForDeletionRel	
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
		
		PurchaseOrderForCleanupRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= RunGroup
				related.Company           			= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
			Instance Selection
				where (related.RecordType.AddPurchaseOrder
				or     related.RecordType.UpdatePurchaseOrder
				or     related.RecordType.CancelPurchaseOrder)
		
		PurchaseOrderLinesForCleanupRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.RecordType.AddPurchaseOrderLine
				or     related.RecordType.UpdatePurchaseOrderLine
				or     related.RecordType.CancelPurchaseOrderLine)
			
		PurchaseOrderAddOnChargesForErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.RecordType.AddAddOnCharge
				or     related.RecordType.UpdateAddOnCharge
				or     related.RecordType.CancelAddOnCharge)
		
		PurchaseOrderAddOnChargesForLineErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
				related.LineNumber                  = LocalLineNumber
			Instance Selection
				where (related.RecordType.AddAddOnCharge
				or     related.RecordType.UpdateAddOnCharge
				or     related.RecordType.CancelAddOnCharge)
		
		PurchaseOrderCommentsForErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.RecordType.AddComment)
		
		PurchaseOrderCommentsForLineErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
				related.LineNumber                  = LocalLineNumber
			Instance Selection
				where (related.RecordType.AddComment)
		
		PurchaseOrderDistributionsForErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.RecordType.AddLineDistribution
				or     related.RecordType.UpdateLineDistribution
				or     related.RecordType.DeleteLineDistribution)
		
		PurchaseOrderDistributionsForLineErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
				related.LineNumber                  = LocalLineNumber
			Instance Selection
				where (related.RecordType.AddLineDistribution
				or     related.RecordType.UpdateLineDistribution
				or     related.RecordType.DeleteLineDistribution)
		
		PurchaseOrderAssetDefaultsForErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
			Instance Selection
				where (related.RecordType.AddAssetDefault)
		
		PurchaseOrderAssetDefaultsForLineErrorRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.PurchaseOrderImport     	= LocalPurchaseOrderImport
				related.POCode                      = LocalPOCode
				related.LineNumber                  = LocalLineNumber
			Instance Selection
				where (related.RecordType.AddAssetDefault)
		
		InterfaceMappingVendorRel
            one-to-many relation to VendorMappingTableInterface
            Field Mapping uses symbolic key
				related.VendorGroup					= PayablesCompanyRel.VendorGroup
			Instance Selection
				where (related.VendorMappingTableInterface.OldVendor	= InterfacedVendor)
				
		VendorForLegacyVendorRel
            one-to-many relation to Vendor
            Field Mapping uses ByLegacyVendor
				related.VendorGroup						= Company.VendorGroup
				related.LegacyVendor					= InterfacedVendor
		
		CrossReferenceVendorRel
            one-to-many relation to CrossReferenceVendor
            Field Mapping uses Set2
				related.VendorGroup					= PayablesCompanyRel.VendorGroup
				related.CrossReferenceVendor        = CrossReferenceVendor	
				
		PayablesCompanyRel
			one-to-many relation to PayablesCompany
			Field Mapping uses symbolic key
				related.Company                     = Company
		
		CompanyRel
			one-to-one relation to PurchasingCompany
			Field Mapping uses symbolic key
				related.Company                    = Company
				
		PurchaseOrderLineRel
			one-to-one relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company              = Company
				related.PurchaseOrder        = LocalPurchaseOrderLineExists
				related.PurchaseOrderLine    = LineNumber
				
		PurchaseOrderSpreadAddOnChargeRel
			one-to-one relation to PurchaseOrderSpreadAddOnCharge
			Field Mapping uses symbolic key
				related.Company              = Company
				related.PurchaseOrder        = LocalPurchaseOrder
				related.AddOnCharge          = AddOnCharge
		
		PurchaseOrderLineDistributionRel
			one-to-one relation to PurchaseOrderLineDistribution
			Field Mapping uses symbolic key
				related.Company              		  = Company
				related.PurchaseOrder        		  = LocalPurchaseOrder
				related.PurchaseOrderLine    	      = LineNumber
				related.AddOnCharge                   = blank
				related.PurchaseOrderLineDistribution = DistributionSequence
				
		ImportRunGroupRel
			one-to-many relation to PurchaseOrderImport
			Field Mapping uses ByRunGroup
				related.RunGroup                      = RunGroup
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = LocalFinanceEnterpriseGroup)

		PurchaseOrderImportRel
			one-to-one relation to PurchaseOrderImport
			Field Mapping uses ByRunGroup2
				related.RunGroup						= LocalRunGroup
				related.Company							= LocalCompany
				related.PurchaseOrderImport				= LocalPurchaseOrderImport
				related.POCode							= LocalPOCode
				related.PurchaseOrderImportRecordType	= LocalRecordType
				
		PurchaseOrderImportLineRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses ByRunGroup
				related.RunGroup				= LocalRunGroup
				related.Company					= LocalCompany
				related.PurchaseOrderImport		= LocalPurchaseOrderImport
				related.POCode					= LocalPOCode
				related.LineNumber				= LocalLineNumber
				related.RecordType 				= LocalRecordType

		PurchaseOrderImportAOCRel
			one-to-many relation to PurchaseOrderImportAddOnCharge
			Field Mapping uses ByRunGroup
				related.RunGroup				= LocalRunGroup
				related.Company					= LocalCompany
				related.PurchaseOrderImport		= LocalPurchaseOrderImport
				related.POCode					= LocalPOCode
				related.LineNumber				= LocalLineNumber
				related.AddOnCharge				= LocalAddOnCharge
				related.RecordType				= LocalRecordType 

		PurchaseOrderImportAssetDefaultRel
			one-to-many relation to PurchaseOrderImportAssetDefaults
			Field Mapping uses Set1
                related.Company					= LocalCompany
                related.PurchaseOrderImport		= LocalPurchaseOrderImport
                related.POCode					= LocalPOCode
                related.LineNumber				= LocalLineNumber
                related.RecordType				= LocalRecordType

		InterfaceHeaderIsHistoricalRel
			one-to-many relation to PurchaseOrderInterfaceInput
			Field Mapping uses ByImport
				related.Company              = Company
				related.PurchaseOrderImport  = PurchaseOrderImport
				related.POCode               = POCode
			Instance Selection
				where (related.Closed = "H")

		DuplicateInterfaceAddOnChargesRel
        	one-to-many relation to PurchaseOrderInterfaceInput
        	Field Mapping uses ByRunGroup
        		related.RunGroup			= RunGroup
        		related.Company          	= Company
        		related.PurchaseOrderImport = PurchaseOrderImport
        		related.POCode              = POCode
				related.LineNumber          = LineNumber
			Instance Selection
				where  (related.AddOnCharge					= AddOnCharge
				and		related.RecordType          		= RecordType
				and		related.PurchaseOrderInterfaceInput != PurchaseOrderInterfaceInput)    

		InventoryLocationRel
			one-to-one relation to InventoryLocation
			Field Mapping uses symbolic key
				related.Company 			= Company
				related.InventoryLocation 	= ShipToLocation

    Sets

        ByRunGroup
			Sort Order
				RunGroup
				Company
				PurchaseOrderImport
				POCode
				LineNumber
				PurchaseOrderInterfaceInput
				
		ByImport
			Sort Order
				Company
				PurchaseOrderImport
				POCode
				PurchaseOrderInterfaceInput
				
		ByLine
			Sort Order
				Company
				RecordType
				PurchaseOrderImport
				POCode
				LineNumber	
				AddOnCharge	

    Field Rules
        		
        RunGroup
        	required
        	
        Company 
        	required
        	
        PurchaseOrderImport
        	required
        
        RecordType
        	required
        
        PurchaseOrder
    		if (InterfacedPurchaseOrderRel exists
        	and !RecordType.AddPurchaseOrder)
        		PurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
        
        LineNumber
        	if (RecordType.AddPurchaseOrderLine
        	or  RecordType.UpdatePurchaseOrderLine
        	or  RecordType.CancelPurchaseOrderLine
        	or  RecordType.AddLineDistribution
        	or  RecordType.UpdateLineDistribution
        	or  RecordType.DeleteLineDistribution)
        		required
        		
        AddOnCharge
        	if (RecordType.AddAddOnCharge
        	or  RecordType.UpdateAddOnCharge
        	or  RecordType.CancelAddOnCharge
        	or  RecordType.PurchaseOrderSpreadAOC)
        		required
        		
        DistributionAccount
        	if (RecordType.AddLineDistribution
        	or  RecordType.UpdateLineDistribution)
        		required
        
    Actions

        Create is a Create Action
   				
        Update is an Update Action
        
        Delete is a Delete Action
        
        FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
        
        LoadInterfacedPurchaseOrders is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup 
					default label is "FinanceEnterpriseGroup"
				PrmHROrganization           is an HROrganization
					default label is "HROrganization"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany                  is a Company
				DefaultBuyer                is a Buyer
					context of PrmHROrganization
				ReleasePurchaseOrders       is Boolean
				PrmMoveErrorsToNewRunGroup  is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
				EnableGTINProcessing        is Boolean	
				
			Parameter Rules
				
				PrmFinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup

			
				PrmHROrganization
					default to actor.context.HROrganization
				
				PrmRunGroup
					LocalRunGroup     = PrmRunGroup
					required
						"RunGroupIsRequired"
					constraint (!RunGroupExistsInImport)
						"RunGroupAlreadyExistsInInterfaceRecords;MustFinishProcessingRunGroupBeforeUsingAgain"
				
			Local Fields
				LocalInterfaceResultView		is a PurchaseOrderInterfaceResult view
				LocalErrorOccurred              is Boolean	
				LocalPOLineCount                is Numeric 12
				LocalPOCount                    is Numeric 12   
				LocalPODistributionCount        is Numeric 12
				LocalPOCommentCount             is Numeric 12
				LocalPOAOCCount                 is Numeric 12
			
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and   ((PrmCompany  not entered
				and    Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or     Company	= PrmCompany)
				and   (RecordType.AddPurchaseOrder
				or     RecordType.CancelPurchaseOrder
				or     RecordType.UpdatePurchaseOrder))

			Sort Order
				RunGroup
				Company
				PurchaseOrderImport
				RecordType
				
			Action Rules

				Empty Set Rules
					
					invoke Create PurchaseOrderInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.RunTime						= current timestamp
						invoked.RunGroup					= PrmRunGroup
						invoked.Company						= PrmCompany
						invoked.DefaultBuyer                = DefaultBuyer
						invoked.ReleasePurchaseOrders       = ReleasePurchaseOrders
						invoked.EnableGTINProcessing        = EnableGTINProcessing
						invoked.SpecificAction              = PrmSpecificAction
						invoked.MoveErrorsToNewRunGroup     = PrmMoveErrorsToNewRunGroup	
						invoked.ErrorRunGroupPrefix         = PrmErrorRunGroupPrefix
					
					invoke LoadInterfacedPurchaseOrderLines PurchaseOrderInterfaceInput
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup 
						invoked.PrmRunGroup                 = PrmRunGroup
						invoked.PrmCompany                  = PrmCompany
						invoked.PrmResult                   = LocalInterfaceResultView.PurchaseOrderInterfaceResult
						invoked.PrmSpecificAction           = PrmSpecificAction
						
				RunGroup Set Rules
					Entrance Rules
			
						LocalErrorOccurred							= false
						
						invoke Create PurchaseOrderInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.Company						= PrmCompany
							invoked.DefaultBuyer                = DefaultBuyer
							invoked.ReleasePurchaseOrders       = ReleasePurchaseOrders
							invoked.SpecificAction              = PrmSpecificAction
							invoked.EnableGTINProcessing        = EnableGTINProcessing
							invoked.MoveErrorsToNewRunGroup     = PrmMoveErrorsToNewRunGroup	
							invoked.ErrorRunGroupPrefix         = PrmErrorRunGroupPrefix
							
					Exit Rules
					
						LocalInterfaceResult                    = LocalInterfaceResultView.PurchaseOrderInterfaceResult
						LocalFinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup 
						invoke Update LocalInterfaceResultsRel
							invoked.InterfaceCounts.PassedHeaderCount          	= LocalPOCount         
							invoked.InterfaceCounts.DetailCount          		= LocalPOLineCount
							invoked.InterfaceCounts.DistributionCount    		= LocalPODistributionCount
							invoked.InterfaceCounts.AddOnChargeCount     		= LocalPOAOCCount
							invoked.InterfaceCounts.Additional1Count	   		= LocalPOCommentCount						
						
						invoke LoadInterfacedPurchaseOrderLines PurchaseOrderInterfaceInput
							invoked.PrmFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup 
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmCompany                  = PrmCompany
							invoked.PrmResult                   = LocalInterfaceResultView.PurchaseOrderInterfaceResult
							invoked.PrmSpecificAction           = PrmSpecificAction
							invoked.PrmPOHeaderRecordType		= RecordType
							
				Instance Rules
						
					LocalErrorOccurred   				= false
					initialize LocalErrorMessage
					LocalPurchaseOrder                  = PurchaseOrder 
					LocalCompany                        = Company
					LocalPurchaseOrderImport            = PurchaseOrderImport 
					LocalPOCode                         = POCode
					LocalRunGroup                       = PrmRunGroup
					LocalFinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
					
					if (RecordType.AddPurchaseOrder
					and (PurchaseOrder = 0
                    or   InterfacedPurchaseOrderRel not exists)
					and (PrmSpecificAction = 0
					or  PrmSpecificAction = 1))
						LocalPOCount    				    += 1	
						LocalPOLineCount                	+= LineCountForAPO 
						LocalPODistributionCount        	+= DistributionCountForAPO
						LocalPOCommentCount             	+= CommentCountForAPO
						LocalPOAOCCount                 	+= AOCCountForAPO						
						if (ClosedPO)
							LocalErrorOccurred  = true
							LocalErrorMessage 	= CannotSetToClosedMessage
						else
						if (!InterfacedPurchaseOrderRel exists)
							invoke Unreleased.CreateFromInterface PurchaseOrder
								assign result to InterfacedPurchaseOrderNumber
								resume on error
									LocalErrorOccurred	= true
									LocalErrorMessage	= error message
								fill in fields from this instance
									except invoked.Vendor
								if (Buyer !entered)
									invoked.Buyer                                           = DefaultBuyer
								if (Vendor entered)
									invoked.Vendor                                          = Vendor
								else
								if (InterfacedVendor entered
								and VendorMappingTableExists)
									invoked.Vendor                                          = InterfaceMappingVendorRel.Vendor
								else
								if (InterfacedVendor entered
								and LegacyVendorExists)
									invoked.Vendor                                          = VendorForLegacyVendorRel.Vendor
								else
								if (CrossReferenceVendor entered)
									invoked.Vendor                                          = first CrossReferenceVendorRel.Vendor
								invoked.LocalFromInterface                                  = true
								invoked.Reference1                                          = PurchaseOrderImport
								invoked.InterfaceInProcess		  							= true
								invoked.OriginatingInterfaceRun   							= LocalInterfaceResultView.PurchaseOrderInterfaceResult
				                invoked.TransientVisitNumber									= VisitNbr 
				                invoked.TransientPatientID										= PatientId
				                invoked.TransientCaseNumber										= CaseNbr 
				                invoked.DefaultProcedureInformation.DefaultProcedure			= Proc 
				                invoked.DefaultProcedureInformation.DefaultProcedureModifier	= ProcedureModifier 
				                invoked.DefaultProcedureInformation.DefaultProcedureDate		= ProcDate 
				                invoked.DefaultProcedureInformation.DefaultPractitioner			= PhysName 
				                invoked.DefaultProcedureInformation.DefaultNurseCoordinator		= NrsCoord 
				                invoked.TransientPatientLastName								= PatientLastName 
				                invoked.TransientPatientFirstName								= PatientFirstName 
				                invoked.TransientPatientMiddleName								= PatientMiddleName 
				                invoked.TransientBirthdate										= Birthdate 
				                invoked.TransientGender											= Gender 
				                invoked.TransientSocialSecurityNumber							= SSN 
				                invoked.DefaultProcedureInformation.DefaultSalesRepresentative	= SalesRep 
				                invoked.DefaultProcedureInformation.DefaultSupplierOrderNumber	= OrdNbr 
				                invoked.DefaultProcedureInformation.DefaultRequester			= Requestor 
				                invoked.DefaultProcedureInformation.DefaultUserDefined1			= Userdef1 
				                invoked.DefaultProcedureInformation.DefaultUserDefined2			= Userdef2 
								invoked.DefaultBillCode											= DfltCnCode
								invoked.ShipToLocation											= ShipToLocation
								if (BillToDetailsNotEntered)
									if (InventoryLocationRel.PurchaseOrderBillToName entered
									and InventoryLocationRel.PurchaseOrderBillToAddress.DeliveryAddress.AddressLine1 entered)
										invoked.PurchaseOrderBillToName 							= InventoryLocationRel.PurchaseOrderBillToName
										invoked.PurchaseOrderBillToAddress 							= InventoryLocationRel.PurchaseOrderBillToAddress
										invoked.PurchaseOrderBillToContact 							= InventoryLocationRel.PurchaseOrderBillToContact
										invoked.PurchaseOrderBillToPhoneNumber 						= InventoryLocationRel.PurchaseOrderBillToPhoneNumber
										invoked.PurchaseOrderBillToEmailAddress 					= InventoryLocationRel.PurchaseOrderBillToEmailAddress
									else
										invoked.PurchaseOrderBillToName 							= Company.PurchaseOrderBillToName
										invoked.PurchaseOrderBillToAddress 							= Company.PurchaseOrderBillToAddress
										invoked.PurchaseOrderBillToContact 							= Company.PurchaseOrderBillToContact
										invoked.PurchaseOrderBillToPhoneNumber 						= Company.PurchaseOrderBillToPhoneNumber
										invoked.PurchaseOrderBillToEmailAddress 					= Company.PurchaseOrderBillToEmailAddress
								if (HistoricalPO)
									invoked.Released   = true
									invoked.Issued     = true 
									invoked.CloseDate  = current corporate date
									invoked.PurchaseOrderLifeCycleState = 4 
							if (LocalErrorOccurred = false)
								invoke Create InterfacedPurchaseOrder
									fill in fields from this instance
									invoked.Company                                             = Company
									invoked.PurchaseOrderImport                                 = PurchaseOrderImport
									invoked.POCode                                              = POCode
									invoked.PurchaseOrder                                       = InterfacedPurchaseOrderNumber.PurchaseOrder
								PurchaseOrder  		= InterfacedPurchaseOrderNumber.PurchaseOrder
								LocalPurchaseOrder 	= InterfacedPurchaseOrderNumber.PurchaseOrder	
						else
						if (InterfacedPurchaseOrderRel exists)
							LocalErrorOccurred  = true
							LocalErrorMessage 	= PurchaseOrderAlreadyExistsMessage
						
						if (LocalErrorOccurred)
							invoke CreateErrorRecord
								invoked.PrmErrorMessage 		= LocalErrorMessage
								invoked.PrmResult       		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
								invoked.PrmErrorLevel   		= 1
						
						if (!LocalErrorOccurred)
							if (PurchaseOrderImportHeaderCommentsRel exists)
								for each PurchaseOrderImportHeaderCommentsRel
									if (!LocalErrorOccurred)
										invoke SystemCreate PurchaseOrderComment
											resume on error
												LocalErrorOccurred     = true
												LocalErrorMessage 	   = error message
											fill in fields from each
											invoked.PurchaseOrder        = LocalPurchaseOrder
										if (LocalErrorOccurred)
											invoke CreateErrorRecord
												invoked.PrmErrorMessage = LocalErrorMessage
												invoked.PrmResult       = LocalInterfaceResultView.PurchaseOrderInterfaceResult
												invoked.PrmErrorLevel   = 6	
										invoke FastDelete each
					else
					if ((RecordType.UpdatePurchaseOrder
					or  RecordType.DoNotUpdate)
					and (PrmSpecificAction = 0
					or  PrmSpecificAction = 2))
						if ((RecordType.UpdatePurchaseOrder
						or  RecordType.DoNotUpdate)
						and PurchaseOrder !entered)   
							PurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder 
						LocalPurchaseOrder = PurchaseOrder
						LocalPOCode        = POCode
						if (PurchaseOrder entered)
							if (PurchaseOrderRel.Released = false
							and RecordType.UpdatePurchaseOrder)
								invoke Unreleased.Update PurchaseOrderRel
									resume on error
										LocalErrorOccurred  = true
										LocalErrorMessage	= error message
									fill in fields from this instance
									if (Vendor entered)
										invoked.Vendor                                          = Vendor
									else
									if (InterfacedVendor entered
									and VendorMappingTableExists)
										invoked.Vendor                                          = InterfaceMappingVendorRel.Vendor
									else
									if (InterfacedVendor entered
									and LegacyVendorExists)
										invoked.Vendor                                          = VendorForLegacyVendorRel.Vendor
									else
									if (CrossReferenceVendor entered)
										invoked.Vendor                                          = first CrossReferenceVendorRel.Vendor
									else
									if (invoked.Vendor !entered
									and InterfacedPurchaseOrderRel exists)
										invoked.Vendor                                          = InterfacedPurchaseOrderRel.PurchaseOrder.Vendor
									invoked.OriginatingInterfaceRun = LocalInterfaceResultView.PurchaseOrderInterfaceResult
									invoked.InterfaceInProcess		= 2
									invoked.FromInterfaceUpdate     = true
							else 
							if  (PurchaseOrderRel.Released = true
							and  RecordType.UpdatePurchaseOrder
							and (PurchaseOrderRel.IsClosed = false
							or   CompanyRel.ReopenPurchaseOrder))
								invoke Released.Update PurchaseOrderRel
									resume on error
										LocalErrorOccurred  = true
										LocalErrorMessage	= error message
									fill in fields from this instance
									if (Vendor entered)
										invoked.Vendor                                          = Vendor
									else
									if (InterfacedVendor entered
									and VendorMappingTableExists)
										invoked.Vendor                                          = InterfaceMappingVendorRel.Vendor
									else
									if (InterfacedVendor entered
									and LegacyVendorExists)
										invoked.Vendor                                          = VendorForLegacyVendorRel.Vendor
									else
									if (CrossReferenceVendor entered)
										invoked.Vendor                                          = first CrossReferenceVendorRel.Vendor
									else
									if (invoked.Vendor !entered
									and InterfacedPurchaseOrderRel exists)
										invoked.Vendor                                          = InterfacedPurchaseOrderRel.PurchaseOrder.Vendor
									invoked.OriginatingInterfaceRun = LocalInterfaceResultView.PurchaseOrderInterfaceResult
									invoked.InterfaceInProcess		= 2
									invoked.FromInterfaceUpdate     = true
							else
							if (PurchaseOrderRel.IsClosed
							and !CompanyRel.ReopenPurchaseOrder)
								LocalErrorOccurred	= true
								LocalErrorMessage 	= CannotUpdateClosedMessage
							else
								if (RecordType.DoNotUpdate)
									invoke UpdateFromInterface PurchaseOrderRel
										invoked.SetUpdateInProcessTrue  	= true
										invoked.PrmOriginatingInterfaceRun  = LocalInterfaceResultView.PurchaseOrderInterfaceResult
						else
						if (PurchaseOrder !entered)
							LocalErrorOccurred	= true
							LocalErrorMessage 	= NotFoundToUpdateMessage
						
						if (LocalErrorOccurred)
							invoke CreateErrorRecord
								invoked.PrmErrorMessage 		= LocalErrorMessage
								invoked.PrmResult       		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
								invoked.PrmErrorLevel   		= 1
						
					
						if (!LocalErrorOccurred)
							if (PurchaseOrderImportHeaderCommentsRel exists)
								for each PurchaseOrderImportHeaderCommentsRel
									if (!LocalErrorOccurred)
										invoke SystemCreate PurchaseOrderComment
											resume on error
												LocalErrorOccurred = true
												LocalErrorMessage  = error message
											fill in fields from each
											invoked.PurchaseOrder        = PurchaseOrder
										if (!LocalErrorOccurred)
											invoke FastDelete each
										if (LocalErrorOccurred)
											invoke CreateErrorRecord
												invoked.PrmErrorMessage = LocalErrorMessage
												invoked.PrmResult       = LocalInterfaceResultView.PurchaseOrderInterfaceResult
												invoked.PrmErrorLevel   = 6
					else
					if (RecordType.CancelPurchaseOrder
					and (PrmSpecificAction = 0
					or  PrmSpecificAction = 3))
						
						if (PurchaseOrder entered
						and PurchaseOrderRel.PurchaseOrderEligibleToBeCancelled)
							invoke Released.InvokeCancelPurchaseOrder PurchaseOrderRel
								invoked.PrmOriginatingInterfaceRun 	= LocalInterfaceResultView.PurchaseOrderInterfaceResult	
						else
						if (PurchaseOrder !entered)
							LocalErrorOccurred	= true
							LocalErrorMessage 	= NotFoundToCancelMessage
						else
						if (PurchaseOrderRel.PurchaseOrderEligibleToBeCancelled = false)
							LocalErrorOccurred	= true
							LocalErrorMessage 	= NotEligibleToCancelMessage
						if (LocalErrorOccurred)
							invoke CreateErrorRecord
								invoked.PrmErrorMessage 		= LocalErrorMessage
								invoked.PrmResult       		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
								invoked.PrmErrorLevel   		= 1
									
		LoadInterfacedPurchaseOrderLines is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup 	
				PrmRunGroup					is a RunGroup
				PrmCompany                  is a Company
				PrmResult                   is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
				PrmPOHeaderRecordType 		is Numeric 2
		
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
			
				PrmRunGroup
				
					LocalRunGroup     = PrmRunGroup
			
			Local Fields
				LocalErrorOccurred              is Boolean
				LocalExtendedAmount				is an InternationalAmount
    			LocalExtendedTaxableAmount		is an InternationalAmount
    			LocalStockWeight				is a StockWeight
    			LocalStockVolume				is a StockVolume
    			LocalAccruedTaxAmount			is an InternationalAmount
    			LocalInvoicedTaxAmount			is an InternationalAmount
    			LocalOpenPurchaseAmount         is an InternationalAmount
    			LocalNumberOfLines				is a NumberOfLines
    			LocalNumberOfClosedLines		is a NumberOfClosedLines
    			LocalLinePurchaseOrder          is like PurchaseOrder	
							
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and  ((PrmCompany  not entered
				and    Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or     Company	= PrmCompany)
				and   (RecordType.AddPurchaseOrderLine
				or     RecordType.UpdatePurchaseOrderLine
				or     RecordType.CancelPurchaseOrderLine))

			Sort Order
				RunGroup
				Company	
				PurchaseOrderImport
				POCode
				LineNumber
				
			Action Rules
				Empty Set Rules
				
					invoke LoadInterfacedPurchaseOrderLineDistributions PurchaseOrderInterfaceInput
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup 
						invoked.PrmRunGroup                 = PrmRunGroup
						invoked.PrmCompany                  = PrmCompany
						invoked.PrmResult                   = PrmResult
						invoked.PrmSpecificAction           = PrmSpecificAction
				
				RunGroup Set Rules
					
					Exit Rules
						
						invoke LoadInterfacedPurchaseOrderLineDistributions PurchaseOrderInterfaceInput
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmCompany                  = PrmCompany
							invoked.PrmResult                   = PrmResult
							invoked.PrmSpecificAction           = PrmSpecificAction
			
				POCode Set Rules
					
					Entrance Rules
						LocalLinePurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
						LocalPurchaseOrder2 = InterfacedPurchaseOrderRel.PurchaseOrder
						
						initialize LocalNumberOfLines
						initialize LocalNumberOfClosedLines
						initialize LocalStockWeight
						initialize LocalStockVolume
						initialize LocalExtendedAmount
    					initialize LocalExtendedTaxableAmount
    					initialize LocalAccruedTaxAmount
    					initialize LocalInvoicedTaxAmount
    					initialize LocalOpenPurchaseAmount
 
					Exit Rules
						
						LocalPurchaseOrder2 = InterfacedPurchaseOrderRel.PurchaseOrder
						invoke UpdateHeaderAmountsFromPurchaseOrderLine PurchaseOrder2Rel
							invoked.PrmFromBatch				= true
							invoked.PrmNumberOfLines  			= LocalNumberOfLines
							invoked.PrmNumberOfClosedLines 		= LocalNumberOfClosedLines
							invoked.PrmStockWeight          	= LocalStockWeight
							invoked.PrmStockVolume	        	= LocalStockVolume
							invoked.PrmExtendedAmount			= LocalExtendedAmount
    						invoked.PrmExtendedTaxableAmount	= LocalExtendedTaxableAmount
    						invoked.PrmAccruedTaxAmount			= LocalAccruedTaxAmount
    						invoked.PrmInvoicedTaxAmount	    = LocalInvoicedTaxAmount
							invoked.PrmOpenPurchaseAmount       = LocalOpenPurchaseAmount
							
				Instance Rules
					
					if (this instance exists)
						LocalErrorOccurred              = false					
						LocalInterfaceResult 			= PrmResult
						if (LocalLinePurchaseOrder entered)
							LocalPurchaseOrderLineExists 	= LocalLinePurchaseOrder
						LocalCompany                    = Company
						LocalPurchaseOrderImport        = PurchaseOrderImport 
						LocalPOCode                     = POCode
						LocalRunGroup                   = PrmRunGroup
						if (InterfacedPurchaseOrderRel exists)
	        				PurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
						if (PurchaseOrder entered)
							LocalPurchaseOrderLineExists = PurchaseOrder

						if  (RecordType.AddPurchaseOrderLine
						and (PrmSpecificAction = 0
						or   PrmSpecificAction = 1))
							initialize LocalErrorMessage
							LocalLineNumber = LineNumber
							if (ClosedPO)
								LocalErrorOccurred  = true
								LocalErrorMessage 	= CannotSetToClosedMessage
							else 
							if (HistoricalPO
							and !HeaderIsHistorical)
								LocalErrorOccurred = true
								LocalErrorMessage  = CannotBeHistoricalMessage
							else
							if (!PurchaseOrderLineExists)

								invoke CreateFromImport PurchaseOrderLine
									resume on error
										LocalErrorOccurred	= true
										LocalErrorMessage	= error message
									fill in fields from this instance
										except invoked.PurchaseOrder
										except invoked.ItemGTIN
										except invoked.EnteredUnitCost
									invoked.PurchaseOrder 		= LocalLinePurchaseOrder
									invoked.PurchaseOrderLine 	= LineNumber
									invoked.VendorBuyUnitCost	= EnteredUnitCost
									invoked.TransientLot 			= Lot
									invoked.TransientSerialNumber 	= SerNbr
									if (LocalInterfaceResultsRel.EnableGTINProcessing)
										invoked.ItemGTIN    = ItemGTIN
									if (Retainage = true)
										invoked.Retainage  = 1
									else
									if (Retainage = false)
										invoked.Retainage  = 0
									if (LineDistributionsExist
									or  DistributionCode entered)
										invoked.BypassDistributionCreation = true
									if (HistoricalPO
									or  HeaderIsHistorical)
										invoked.Released   = true
										invoked.Issued     = true 
										invoked.PurchaseOrderLineLifeCycleState = 4 
										invoked.Closed = "H"
										
								if (!LocalErrorOccurred)
									increment LocalNumberOfLines 
									if (Closed = true)
										increment LocalNumberOfClosedLines
									LocalExtendedAmount				+= PurchaseOrderLineRel.ExtendedAmount
									LocalExtendedTaxableAmount		+= PurchaseOrderLineRel.ExtendedTaxableAmount
									LocalStockWeight				+= PurchaseOrderLineRel.StockWeight
									LocalStockVolume				+= PurchaseOrderLineRel.StockVolume
									LocalAccruedTaxAmount			+= PurchaseOrderLineRel.AccruedTaxAmount
									LocalInvoicedTaxAmount			+= PurchaseOrderLineRel.InvoicedTaxAmount
									LocalOpenPurchaseAmount         += PurchaseOrderLineRel.OpenPurchaseAmount													
													
								if (!LocalErrorOccurred)
									for each PurchaseOrderImportLineCommentsRel
										if (!LocalErrorOccurred)
											invoke SystemCreateLine PurchaseOrderLineComment
												resume on error
													LocalErrorOccurred	= true
													LocalErrorMessage 	= error message
												fill in fields from each
												invoked.PurchaseOrder        = LocalLinePurchaseOrder
												invoked.PurchaseOrderLine    = LineNumber
												
											if (!LocalErrorOccurred)
												invoke FastDelete each
											else 
											if (LocalErrorOccurred)
												if (HeaderExists)
													invoke CreateErrorRecord PurchaseOrderForCleanupRel 
														invoked.PrmErrorMessage = LocalErrorMessage
														invoked.PrmResult       = PrmResult
														invoked.PrmErrorLevel   = 7
												else
													invoke CreateLineErrorRecord
														invoked.PrmErrorMessage = LocalErrorMessage
														invoked.PrmResult       = PrmResult
														invoked.PrmErrorLevel   = 7
										
								if (!LocalErrorOccurred)
									for each PurchaseOrderImportAssetDefaultsRel
										if (each.RecordType.AddAssetDefault)
											invoke Create PurchaseOrderLineAssetDefaults
												resume on error
													LocalErrorOccurred	= true
													LocalErrorMessage 	= error message
									    		invoked.Company						= Company
							                	invoked.PurchaseOrder               = LocalLinePurchaseOrder
							                	invoked.PurchaseOrderLine           = LineNumber
									            invoked.AssetDescription            = each.AssetDescription
									            invoked.TagNumber                   = each.TagNumber
									            invoked.AssetGroup                  = each.AssetGroup
									            invoked.AssetAccountingUnitGroup    = each.AssetAccountingUnitGroup
									            invoked.Project                     = each.Project
									            invoked.Combine               	    = each.Combine
									            invoked.HoldAsset                   = each.HoldAsset
									            invoked.ItemNumber                  = each.ItemNumber
									            invoked.ItemDescription             = each.ItemDescription
									            invoked.ModelNumber                 = each.ModelNumber
									            invoked.PurchaseDate                = each.PurchaseDate
									            invoked.InServiceDate               = each.InServiceDate
									            invoked.AssetLocation               = each.AssetLocation
									            invoked.AssetDivision               = each.AssetDivision
									            invoked.LocationDetail              = each.LocationDetail
										else
										if (each.RecordType.UpdateAssetDefault)
											invoke Update PurchaseOrderLineAssetDefaultsRel
												resume on error
													LocalErrorOccurred	= true
													LocalErrorMessage 	= error message
												fill in fields from each
									if (!LocalErrorOccurred)
										invoke FastDelete PurchaseOrderImportAssetDefaultsRel
									else 
									if (LocalErrorOccurred)
										if (HeaderExists)
											invoke CreateErrorRecord PurchaseOrderForCleanupRel 
												invoked.PrmErrorMessage = LocalErrorMessage
												invoked.PrmResult       = PrmResult
												invoked.PrmErrorLevel   = 9
										else
											invoke CreateLineErrorRecord
												invoked.PrmErrorMessage = LocalErrorMessage
												invoked.PrmResult       = PrmResult
												invoked.PrmErrorLevel   = 9
							else
							if (PurchaseOrderLineExists)
								LocalErrorOccurred   = true
								LocalErrorMessage 	 = PurchaseOrderLineAlreadyExistsMessage
						else
						if  ((RecordType.UpdatePurchaseOrderLine
						or    RecordType.DoNotUpdate)
						and (PrmSpecificAction = 0
						or   PrmSpecificAction = 2))
							initialize LocalErrorMessage
							LocalLineNumber = LineNumber
							if (PurchaseOrderLineExists
							and RecordType.UpdatePurchaseOrderLine)
								if (PurchaseOrderLineRel.Released = false)
									invoke Unreleased.Update PurchaseOrderLineRel
										resume on error
											LocalErrorOccurred	= true
											LocalErrorMessage	= error message
										fill in fields from this instance
											except invoked.EnteredUnitCost
										invoked.FromInterfaceUpdate = true
										invoked.PurchaseOrder 		= LocalLinePurchaseOrder
										invoked.PurchaseOrderLine 	= LineNumber
										invoked.VendorBuyUnitCost	= EnteredUnitCost
										if (Retainage = true)
											invoked.Retainage  = 1
										else
										if (Retainage = false)
											invoked.Retainage  = 0
								else
								if (PurchaseOrderLineRel.Released
								and (PurchaseOrderRel.IsClosed = false
								or  CompanyRel.ReopenPurchaseOrder))
									invoke Released.Update PurchaseOrderLineRel
										resume on error
											LocalErrorOccurred	= true
											LocalErrorMessage	= error message
										fill in fields from this instance
											except invoked.EnteredUnitCost
										invoked.FromInterfaceUpdate = true
										invoked.PurchaseOrder 		= LocalLinePurchaseOrder
										invoked.PurchaseOrderLine 	= LineNumber
										invoked.VendorBuyUnitCost	= EnteredUnitCost
										if (Retainage = true)
											invoked.Retainage  = 1
										else
										if (Retainage = false)
											invoked.Retainage  = 0
								else
								if (PurchaseOrderRel.IsClosed
								and CompanyRel.ReopenPurchaseOrder = false)
									LocalErrorOccurred	= true
									LocalErrorMessage 	= CannotUpdateClosedMessage 

								if (!LocalErrorOccurred)
									for each PurchaseOrderImportLineCommentsRel
										if (!LocalErrorOccurred)
											invoke SystemCreateLine PurchaseOrderLineComment
												resume on error
													LocalErrorOccurred	      = true
													LocalErrorMessage = error message
												fill in fields from each
												invoked.PurchaseOrder        = LocalLinePurchaseOrder
												invoked.PurchaseOrderLine    = LineNumber
												
											if (!LocalErrorOccurred)
												invoke FastDelete each
											else 
											if (LocalErrorOccurred)
												if (HeaderExists)
													invoke CreateErrorRecord PurchaseOrderForCleanupRel 
														invoked.PrmErrorMessage = LocalErrorMessage
														invoked.PrmResult       = PrmResult
														invoked.PrmErrorLevel   = 7
												else
													invoke CreateLineErrorRecord
														invoked.PrmErrorMessage = LocalErrorMessage
														invoked.PrmResult       = PrmResult
														invoked.PrmErrorLevel   = 7
										
								if (!LocalErrorOccurred)
									for each PurchaseOrderImportAssetDefaultsRel
										if (each.RecordType = 15)
											invoke Create PurchaseOrderLineAssetDefaults
												resume on error
													LocalErrorOccurred	      = true
													LocalErrorMessage = error message
									    		invoked.Company						= Company
							                	invoked.PurchaseOrder               = LocalLinePurchaseOrder
							                	invoked.PurchaseOrderLine           = LineNumber
								                invoked.AssetDescription            = each.AssetDescription
								                invoked.TagNumber                   = each.TagNumber
								                invoked.AssetGroup                  = each.AssetGroup
								                invoked.AssetAccountingUnitGroup    = each.AssetAccountingUnitGroup
								                invoked.Project                     = each.Project
								                invoked.Combine               	    = each.Combine
								                invoked.HoldAsset                   = each.HoldAsset
								                invoked.ItemNumber                  = each.ItemNumber
								                invoked.ItemDescription             = each.ItemDescription
								                invoked.ModelNumber                 = each.ModelNumber
								                invoked.PurchaseDate                = each.PurchaseDate
								                invoked.InServiceDate               = each.InServiceDate
								                invoked.AssetLocation               = each.AssetLocation
								                invoked.AssetDivision               = each.AssetDivision
								            	invoked.LocationDetail              = each.LocationDetail
										else
										if (each.RecordType = 16)
											invoke Update PurchaseOrderLineAssetDefaultsRel
												resume on error
													LocalErrorOccurred	      = true
													LocalErrorMessage = error message
												fill in fields from each
										if (!LocalErrorOccurred)
											invoke FastDelete PurchaseOrderImportAssetDefaultsRel
										else 
										if (LocalErrorOccurred)
											if (HeaderExists)
												invoke CreateErrorRecord PurchaseOrderForCleanupRel 
													invoked.PrmErrorMessage = LocalErrorMessage
													invoked.PrmResult       = PrmResult
													invoked.PrmErrorLevel   = 9
											else
												invoke CreateLineErrorRecord
													invoked.PrmErrorMessage = LocalErrorMessage
													invoked.PrmResult       = PrmResult
													invoked.PrmErrorLevel   = 9
							else
							if (!PurchaseOrderLineExists)
								LocalErrorOccurred  = true
								LocalErrorMessage   = LineNotFoundToUpdateMessage
						else
						if  (RecordType.CancelPurchaseOrderLine
						and (PrmSpecificAction = 0
						or   PrmSpecificAction = 3))

							if (PrmPOHeaderRecordType = 7)	
								LocalErrorOccurred  = true
								LocalErrorMessage   = CannotCancelBothHeaderAndLine	
							else 
								initialize LocalErrorMessage
								LocalLineNumber = LineNumber
								if (PurchaseOrderLineExists
								and PurchaseOrderLineRel.PurchaseOrderLineEligibleToBeCancelled)
									invoke CancelLine PurchaseOrderLineRel
										resume on error
											LocalErrorOccurred	= true
											LocalErrorMessage	= error message
								if (!PurchaseOrderLineExists)
									LocalErrorOccurred	= true
									LocalErrorMessage 	= LineNotFoundToCancelMessage
								else
								if (PurchaseOrderLineRel.PurchaseOrderLineEligibleToBeCancelled = false)
									LocalErrorOccurred	= true
									LocalErrorMessage 	= LineNotEligibleToCancelMessage
						

						if (LocalErrorOccurred)
							display "TryingToGetInForAnotherLine;LineNumber<LineNumber>"
							if (HeaderExists)

								invoke CreateErrorRecord PurchaseOrderForCleanupRel 
									invoked.PrmErrorMessage = LocalErrorMessage
									invoked.PrmResult       = PrmResult
									invoked.PrmErrorLevel   = 2
							else

								invoke CreateLineErrorRecord
									invoked.PrmErrorMessage = LocalErrorMessage
									invoked.PrmResult       = PrmResult
									invoked.PrmErrorLevel   = 2
						else
						if (!LocalErrorOccurred)
							if ((PrmSpecificAction = 0)
							or (RecordType.UpdatePurchaseOrderLine
							or  PrmSpecificAction = 2)
							or (RecordType.AddPurchaseOrderLine
							and PrmSpecificAction = 1)
							or (RecordType.CancelPurchaseOrderLine
							and PrmSpecificAction = 1))
								if (!LineDistributionsExist)
												
									invoke FastDelete
		
		LoadInterfacedPurchaseOrderLineDistributions is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
				PrmCompany                  is a PurchasingCompany
				PrmResult                   is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
			
			
			Local Fields
				LocalDistPurchaseOrder          is like PurchaseOrder
				LocalErrorOccurred              is Boolean	
			
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and   ((PrmCompany  not entered
				and     Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or      Company	= PrmCompany)
				and     RecordType.AddLineDistribution
				or      RecordType.UpdateLineDistribution
				or      RecordType.DeleteLineDistribution)

			Sort Order
				RunGroup
				Company	
				PurchaseOrderImport
				POCode
				
			Action Rules
				Empty Set Rules
					invoke LoadInterfacedAddOnCharges PurchaseOrderInterfaceInput
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup                 = PrmRunGroup
						invoked.PrmCompany                  = PrmCompany
						invoked.PrmResult                   = PrmResult	
						invoked.PrmSpecificAction           = PrmSpecificAction	
				
				RunGroup Set Rules
					Exit Rules
						
						invoke LoadInterfacedAddOnCharges PurchaseOrderInterfaceInput
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmCompany                  = PrmCompany
							invoked.PrmResult                   = PrmResult	
							invoked.PrmSpecificAction           = PrmSpecificAction	
						
				POCode Set Rules
					
					Entrance Rules
						LocalDistPurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
						
				Instance Rules
					LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup 
					LocalInterfaceResult 		= PrmResult
					LocalErrorOccurred = false
					if  ((PrmSpecificAction = 1
					or   PrmSpecificAction = 0)
					and  RecordType.AddLineDistribution) 
						invoke Create PurchaseOrderLineDistribution
							resume on error
								LocalErrorOccurred  = true
								LocalErrorMessage	= error message
							fill in fields from this instance
							invoked.PurchaseOrder 					= LocalDistPurchaseOrder
							invoked.PurchaseOrderLine               = LineNumber
							invoked.PurchaseOrderLineDistribution   = DistributionSequence
							invoked.AssetInformation.Asset 			= Asset
							invoked.AssetInformation.AssetTemplate 	= AssetTemplate
							if (LocalInterfaceResultsRel.ReleasePurchaseOrders = true)
								invoked.TransientFromMassPOCreateAndRelease = true
					else
					if ((PrmSpecificAction = 2
					or  PrmSpecificAction = 3
					or  PrmSpecificAction = 0)
					and (RecordType.UpdateLineDistribution
					or   RecordType.DeleteLineDistribution))
						if (PurchaseOrderLineDistributionRel exists)
							if (RecordType.UpdateLineDistribution)
								invoke Update PurchaseOrderLineDistributionRel
									resume on error
										LocalErrorOccurred  = true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.AssetInformation.Asset 			= Asset
									invoked.AssetInformation.AssetTemplate 	= AssetTemplate
							else
							if (RecordType.DeleteLineDistribution)
								invoke Delete PurchaseOrderLineDistributionRel
									resume on error
										LocalErrorOccurred  = true
										LocalErrorMessage	= error message
								
						if (PurchaseOrderLineDistributionRel !exists)																		
							LocalErrorOccurred	= true
							LocalErrorMessage 	= DistributionNotFoundMessage
							
					if (LocalErrorOccurred)
						if (HeaderExists)
							invoke CreateErrorRecord PurchaseOrderForCleanupRel 
								invoked.PrmErrorMessage = LocalErrorMessage
								invoked.PrmResult       = PrmResult
								invoked.PrmErrorLevel   = 2
						else
							invoke CreateLineErrorRecord
								invoked.PrmErrorMessage = LocalErrorMessage
								invoked.PrmResult       = PrmResult
								invoked.PrmErrorLevel   = 2
					else
					if (!LocalErrorOccurred)
						invoke FastDelete
		
		LoadInterfacedAddOnCharges is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
				PrmCompany                  is a PurchasingCompany
				PrmResult                   is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
			
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
			
				
			Local Fields
				LocalErrorOccurred			is Boolean	
				LocalAddOnPurchaseOrder		is like PurchaseOrder
			
			Instance Selection			
				where (RunGroup				= PrmRunGroup
				and  ((PrmCompany  not entered
				and    Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or     Company				= PrmCompany)
				and   (HeaderAddOnCharge
				or     LineAddOnCharge
				or	   SpreadAddOnCharge))

			Sort Order
				RunGroup
				Company	
				PurchaseOrderImport
				POCode
				
			Action Rules
				Empty Set Rules
				
					invoke FinishLoadInterface PurchaseOrderInterfaceInput
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup 
						invoked.PrmRunGroup  				= PrmRunGroup
						invoked.PrmCompany   				= PrmCompany
						invoked.PrmResult    				= PrmResult
						invoked.PrmSpecificAction 			= PrmSpecificAction
					
				RunGroup Set Rules
					Exit Rules
						
						invoke FinishLoadInterface PurchaseOrderInterfaceInput
								invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup 
								invoked.PrmRunGroup  				= PrmRunGroup
								invoked.PrmCompany   				= PrmCompany
								invoked.PrmResult    				= PrmResult
								invoked.PrmSpecificAction 			= PrmSpecificAction
					
				PurchaseOrderImport Set Rules
					Entrance Rules
						LocalAddOnPurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder

				POCode Set Rules
					
					Entrance Rules
						LocalAddOnPurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
						
				Instance Rules
					LocalPurchaseOrder			= LocalAddOnPurchaseOrder
					LocalCompany                = Company
					LocalPurchaseOrderImport    = PurchaseOrderImport 
					LocalPOCode                 = POCode
					LocalLineNumber        		= LineNumber
					LocalAddOnCharge 			= AddOnCharge
					LocalInterfaceResult       	= PrmResult
					LocalErrorOccurred 			= false
					

					if  (RecordType.AddAddOnCharge
					and (PrmSpecificAction not entered
					or   PrmSpecificAction.Create))
						if (ClosedPO)
							LocalErrorOccurred = true
							LocalErrorMessage  = CannotSetToClosedMessage
						else 
						if (HistoricalPO
						and !HeaderIsHistorical)
							LocalErrorOccurred = true
							LocalErrorMessage  = CannotBeHistoricalMessage
						else
						if (DuplicateInterfaceAddOnChargeExists)
							LocalErrorOccurred = true
							LocalErrorMessage  = DuplicateAddOnChargeMessage
						else
						if (AddOnChargeExists)
							LocalErrorOccurred = true
							LocalErrorMessage  = AOCAlreadyExistsMessage
						else

							invoke Create PurchaseOrderAndLineAddOnCharge
								resume on error
									LocalErrorOccurred   = true
									LocalErrorMessage 	 = error message
								fill in fields from this instance
								invoked.PurchaseOrder               = LocalPurchaseOrder
		                		invoked.PurchaseOrderLine           = LineNumber
								if (HistoricalPO
								or  HeaderIsHistorical)
									invoked.Closed = "H"


					else
					if  (RecordType.UpdateAddOnCharge
					and (PrmSpecificAction not entered
					or   PrmSpecificAction.Update))

						if (not AddOnChargeExists)
							LocalErrorOccurred  = true
						    LocalErrorMessage   = AddOnChargeNotFoundToUpdateMessage
						else
						if (PurchaseOrderRel.IsClosed
						and not Company.ReopenPurchaseOrder)
							LocalErrorOccurred	= true
							LocalErrorMessage   = CannotUpdateClosedMessage
						else

							invoke Update PurchaseOrderAndLineAddOnChargeRel
								resume on error
									LocalErrorOccurred	= true
									LocalErrorMessage	= error message
								fill in fields from this instance


					else
					if  (RecordType.CancelAddOnCharge
					and (PrmSpecificAction not entered
					or   PrmSpecificAction.Cancel))
						if (not AddOnChargeExists)
							LocalErrorOccurred  = true
						    LocalErrorMessage   = AddOnChargeNotFoundToCancelMessage
						else
						if (not PurchaseOrderAndLineAddOnChargeRel.IsCancelAllowed)
							LocalErrorOccurred  = true
							LocalErrorMessage   = AddOnChargeNotEligibleToCancelMessage
						else

							invoke CancelPurchaseOrderAddOnCharge PurchaseOrderAndLineAddOnChargeRel
								resume on error
									LocalErrorOccurred	= true
									LocalErrorMessage	= error message


					else
					if (SpreadAddOnCharge)												
						if (SpreadExists)
							LocalErrorOccurred	= true
							LocalErrorMessage 	= SpreadAlreadyExistsMessage
						else

							invoke Create PurchaseOrderSpreadAddOnCharge
								resume on error
									LocalErrorOccurred	= true
									LocalErrorMessage	= error message
								fill in fields from this instance
								invoked.PurchaseOrder 				= LocalPurchaseOrder 
								invoked.EnteredAddOnChargeAmount 	= TotalAddOnChargeAmount


					if (LocalErrorOccurred)
						invoke CreateErrorRecord PurchaseOrderForCleanupRel
							invoked.PrmErrorMessage = LocalErrorMessage
							invoked.PrmResult       = PrmResult
							if (SpreadAddOnCharge)
								invoked.PrmErrorLevel   = LocalErrorLevel.SpreadAOC
							else
							if (LineNumber entered)
								invoked.PrmErrorLevel   = LocalErrorLevel.LineAOC
							else
								invoked.PrmErrorLevel   = LocalErrorLevel.HeaderAOC
					else						
						if (RecordType.PurchaseOrderSpreadAOC
						or  PrmSpecificAction not entered
						or (RecordType.AddAddOnCharge
						and PrmSpecificAction.Create)
						or (RecordType.UpdateAddOnCharge
						and PrmSpecificAction.Update)
						or (RecordType.CancelAddOnCharge
						and PrmSpecificAction.Cancel))	
							invoke FastDelete


		LoadInterfacedSpreadAddOnCharges is a Set Action 
			restricted
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
				PrmCompany                  is a PurchasingCompany
				PrmResult                   is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
			
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
			
				
			Local Fields
				LocalErrorOccurred              is Boolean	
				LocalAddOnPurchaseOrder         is like PurchaseOrder
			
			Instance Selection			
				where  (RunGroup		= PrmRunGroup
				and   ((PrmCompany  not entered
				and     Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or      Company		= PrmCompany)
				and     RecordType.PurchaseOrderSpreadAOC)
				
			Sort Order
				RunGroup
				Company	
				PurchaseOrderImport
				POCode
				
			Action Rules
				Instance Rules
								
		CreateErrorRecord is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like PurchaseOrderInterfaceResult
 				PrmErrorLevel           is Numeric 2
 					
			Action Rules
				
				LocalInterfaceResult   		= PrmResult
				LocalRunGroup          		= RunGroup
				LocalCompany           		= Company
				LocalPurchaseOrderImport 	= PurchaseOrderImport
				LocalPOCode            		= POCode
				LocalRecordType				= RecordType
				if (PurchaseOrderImportRel exists)
					invoke FastUpdate PurchaseOrderImportRel
						fill in fields from this instance
						invoked.RecordInError					= true
						invoked.ErrorMessage					= PrmErrorMessage
						invoked.ErrorLevel                      = PrmErrorLevel
						invoked.ResultProcessedBy               = PrmResult
				else
					invoke CreateNoRules PurchaseOrderImport
						fill in fields from this instance
						invoked.PurchaseOrderImportRecordType   = RecordType
						invoked.RecordInError					= true
						invoked.ErrorMessage					= PrmErrorMessage
						invoked.ErrorLevel                      = PrmErrorLevel
						invoked.ResultProcessedBy               = PrmResult
							
				if (PurchaseOrderLinesForCleanupRel exists)
					display "CheckingLinesInError"
					for each PurchaseOrderLinesForCleanupRel
						LocalLineNumber	= each.LineNumber
						LocalRecordType = each.RecordType
						if (PurchaseOrderImportLineRel exists)
							invoke FastUpdate PurchaseOrderImportLineRel
								fill in fields from each
						else
							invoke CreateNoRules PurchaseOrderImportLine
								fill in fields from each

				if (PurchaseOrderAddOnChargesForErrorRel exists)
					for each PurchaseOrderAddOnChargesForErrorRel
						LocalRecordType 	= each.RecordType
						LocalAddOnCharge 	= each.AddOnCharge
						if (PurchaseOrderImportAOCRel exists)
							invoke FastUpdate PurchaseOrderImportAOCRel
								fill in fields from each
						else
							invoke CreateNoRules PurchaseOrderImportAddOnCharge
								fill in fields from each

				if (PurchaseOrderCommentsForErrorRel exists)
					for each PurchaseOrderCommentsForErrorRel
						invoke Create PurchaseOrderImportComment
							fill in fields from each

				if (PurchaseOrderDistributionsForErrorRel exists)
					for each PurchaseOrderDistributionsForErrorRel
						invoke Create PurchaseOrderImportLineDistribution
							fill in fields from each

				if (PurchaseOrderAssetDefaultsForErrorRel exists)
					for each PurchaseOrderAssetDefaultsForErrorRel
						LocalRecordType = each.RecordType
						if (PurchaseOrderImportAssetDefaultRel exists)
							invoke FastUpdate PurchaseOrderImportAssetDefaultRel
								fill in fields from each
						else
							invoke Create PurchaseOrderImportAssetDefaults
								fill in fields from each

			
				display "JustBeforeDeletionOfPORecords"
				invoke FastDelete PurchaseOrderForDeletionRel
				invoke UpdateErrorInformation LocalInterfaceResultsRel
				
		CreateLineErrorRecord is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like PurchaseOrderInterfaceResult
 				PrmErrorLevel           is Numeric 2
 					
			Action Rules
			
				LocalLineNumber	= LineNumber
				LocalRecordType = RecordType
				if (PurchaseOrderImportLineRel exists)
					invoke FastUpdate PurchaseOrderImportLineRel
						fill in fields from this instance
				else
					invoke CreateNoRules PurchaseOrderImportLine
						fill in fields from this instance
				
				LocalInterfaceResult   = PrmResult
				LocalRunGroup          = RunGroup
				LocalCompany           = Company
				LocalPurchaseOrderImport = PurchaseOrderImport
				LocalPOCode            = POCode
				if (PurchaseOrderAddOnChargesForLineErrorRel exists)
					for each PurchaseOrderAddOnChargesForErrorRel
						LocalRecordType 	= each.RecordType
						LocalAddOnCharge	= each.AddOnCharge
						if (PurchaseOrderImportAOCRel exists)
							invoke FastUpdate PurchaseOrderImportAOCRel
								fill in fields from each
						else
							invoke CreateNoRules PurchaseOrderImportAddOnCharge
								fill in fields from each
						invoke FastDelete each
				if (PurchaseOrderCommentsForLineErrorRel exists)
					for each PurchaseOrderCommentsForErrorRel
						invoke Create PurchaseOrderImportComment
							fill in fields from each
						invoke FastDelete each
				if (PurchaseOrderDistributionsForLineErrorRel exists)
					for each PurchaseOrderDistributionsForErrorRel
						invoke Create PurchaseOrderImportLineDistribution
							fill in fields from each
						invoke FastDelete each 
				if (PurchaseOrderAssetDefaultsForLineErrorRel exists)
					for each PurchaseOrderAssetDefaultsForErrorRel
						LocalRecordType = each.RecordType
						if (PurchaseOrderImportAssetDefaultRel exists)
							invoke FastUpdate PurchaseOrderImportAssetDefaultRel
								fill in fields from each
						else
							invoke Create PurchaseOrderImportAssetDefaults
								fill in fields from each
						invoke FastDelete each 
						
				invoke FastDelete 
				invoke UpdateErrorInformation LocalInterfaceResultsRel
		
		FinishLoadInterface is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup 		is like FinanceEnterpriseGroup 
				PrmRunGroup             		is a RunGroup
				PrmCompany              		is a Company
				PrmResult               		is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
						       
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
									       
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and  ((PrmCompany  not entered
				and    Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or     Company	= PrmCompany))
				
			Sort Order
				RunGroup
				Company
				
			Action Rules
				Empty Set Rules
					LocalFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup 	
					LocalInterfaceResult       		= PrmResult
					invoke Finish LocalInterfaceResultsRel
				
				RunGroup Set Rules
								
					Exit Rules
						LocalFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup
						LocalInterfaceResult       		= PrmResult
						invoke Finish LocalInterfaceResultsRel
							
				Instance Rules
					LocalCompany         			= Company
					LocalPurchaseOrder   			= PurchaseOrder
					LocalPOCode          			= POCode
					LocalFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup
					LocalInterfaceResult 			= PrmResult
					if ((PrmSpecificAction = 0)
					or (RecordType.UpdatePurchaseOrder
					and  PrmSpecificAction = 2)
					or (RecordType.AddPurchaseOrder
					and PrmSpecificAction = 1)
					or (RecordType.CancelPurchaseOrder
					and PrmSpecificAction = 1))
						invoke UpdateFromInterface PurchaseOrderRel
							invoked.SetInterfaceInProcessFalse   = true
							if (LocalInterfaceResultsRel.ReleasePurchaseOrders
							and !ClosedPO
							and !HistoricalPO)
								invoked.ReleasePurchaseOrder = true
							if (RecordType.AddPurchaseOrder)
								invoked.PrmTransientFromMassPOCreateAndRelease = true
						invoke FastDelete PurchaseOrderLinesForCleanupRel
						invoke FastDelete
								

        
