CashRequirementsReport is a BusinessClass
	owned by ap
	prefix is CRRP
	Ontology
		symbolic key is CashRequirementsReport
		
    Patterns
        disable AuditIndex
        disable Auditing
        disable EffectiveDated

	Persistent Fields
        VendorSearchName		is a snapshot of CashRequirementsReport.PayVendor.VendorSearchName
        VendorName 								
		TotalPaymentAmount		is an InternationalAmount
			precision is CashRequirementsReport.InvoiceCurrency.NumberOfDecimals
		TotalDiscountAmount		is an InternationalAmount
			precision is CashRequirementsReport.InvoiceCurrency.NumberOfDecimals
		TotalNetPaymentAmount	is an InternationalAmount
			precision is CashRequirementsReport.InvoiceCurrency.NumberOfDecimals
		TotalBankCheckAmount	is an InternationalAmount
		
	Field Rules
		VendorName
			if (CashRequirementsReport.RemitToCode entered)
				force default to CashRequirementsReport.RemitToCode.VendorName
			else
				force default to CashRequirementsReport.PayVendor.VendorName
		
	Transient Fields	 
		CheckCurrency			is a Currency			
			derive value from CashRequirementsReport.CashCode.Currency

	Relations
		PayablesInvoicePaymentRel
			one-to-many relation to PayablesInvoicePayment
			Field Mapping uses ByCashRequirementsReport
				related.VendorGroup							= VendorGroup
				related.PayGroup							= PayGroup
				related.CashRequirementsResult				= CashRequirementsResult
				related.CashCode							= CashRequirementsReport.CashCode
				related.BankTransactionCode					= CashRequirementsReport.BankTransactionCode
				related.InvoiceCurrency						= CashRequirementsReport.InvoiceCurrency
				related.PayVendor							= CashRequirementsReport.PayVendor
				related.RemitToCode							= CashRequirementsReport.RemitToCode

		SelectedPayablesInvoicePaymentRel
			one-to-many relation to PayablesInvoicePayment
			Field Mapping uses ByCashRequirementsReport
				related.VendorGroup							= VendorGroup
				related.PayGroup							= PayGroup
				related.CashRequirementsResult				= CashRequirementsResult
				related.CashCode							= CashRequirementsReport.CashCode
				related.BankTransactionCode					= CashRequirementsReport.BankTransactionCode
				related.InvoiceCurrency						= CashRequirementsReport.InvoiceCurrency
				related.PayVendor							= CashRequirementsReport.PayVendor
				related.RemitToCode							= CashRequirementsReport.RemitToCode
			Instance Selection
				where (!related.IsOnHold)

	Conditions
		CanRemoveFromCashRequirementsResult
			when (CashRequirementsResult.Status.Selected
			or    CashRequirementsResult.Status.Processing)
		
		CanRemoveFromCashRequirementsResultForeground
			restricted
			when (CashRequirementsResult.Status.Selected
			and   NumSelectedPayablesInvoicePaymentRel < 1000)

		TotalNetPaymentAmountIsNegative		
			restricted
			when (TotalNetPaymentAmount < 0)

	Derived Fields
		Address is a DerivedField
			type is Alpha size 100
			if (CashRequirementsReport.RemitToCode entered)
				return CashRequirementsReport.RemitToCode.CommaSeparatedAddress
			else
				return CashRequirementsReport.PayVendor.CommaSeparatedAddress

		DerivedVendorName is a DerivedField	 
			type is Alpha size up to 120	
			if (CashRequirementsReport.RemitToCode entered)
				return CashRequirementsReport.RemitToCode.VendorName
			else
				return CashRequirementsReport.PayVendor.VendorName

		NumSelectedPayablesInvoicePaymentRel is a DerivedField
			type is Numeric size 8
			return instance count of SelectedPayablesInvoicePaymentRel

		ConcatenatedKey is a StringField
			type is Alpha 86
			VendorGroup
			"-"
			PayGroup
			"-"
			CashRequirementsResult
			"-"
			CashRequirementsReport.CashCode
			"-"
			CashRequirementsReport.BankTransactionCode
			"-"
			CashRequirementsReport.InvoiceCurrency
			"-"
			CashRequirementsReport.PayVendor
			"-"
			CashRequirementsReport.RemitToCode
				
		BackgroundGroupKey is a StringField
			type is Alpha 150
			default label is untranslatable
			"CashRequirementsReport"
			"-"
			ConcatenatedKey
			"-"

		NegativeNetPaymentAmountMessage is a MessageField		
			"PaymentsWillBePutOnHoldDuringSchedulePayments"

	Sets
		ByCashRequirementsResult
			Sort Order
				VendorGroup
				PayGroup
				CashRequirementsResult
				CashRequirementsReport.PayVendor
				CashRequirementsReport.RemitToCode
				CashRequirementsReport.CashCode
				CashRequirementsReport.BankTransactionCode
				CashRequirementsReport.InvoiceCurrency

		ByVendorSearchName
			Sort Order
				VendorGroup
				PayGroup
				CashRequirementsResult
				VendorSearchName
				CashRequirementsReport.CashCode
				CashRequirementsReport.BankTransactionCode
				CashRequirementsReport.InvoiceCurrency
				CashRequirementsReport.PayVendor
				CashRequirementsReport.RemitToCode

		ByVendorName
			Sort Order
				VendorGroup
				PayGroup
				CashRequirementsResult
				VendorName
				CashRequirementsReport.CashCode
				CashRequirementsReport.BankTransactionCode
				CashRequirementsReport.InvoiceCurrency
				CashRequirementsReport.PayVendor
				CashRequirementsReport.RemitToCode
				
	Local Fields
		CRBackgroundGroup			is AlphaUpper up to 200
		LastBackgroundGroup			is AlphaUpper up to 200
		
	Rule Blocks

	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted

		DeleteInBackground is an Instance Action
			restricted
			Action Rules
				invoke Delete

		Purge is a Purge Action
			restricted
											
		UpdateAmounts is an Instance Action
			restricted
			Parameters
				PrmPaymentAmount		is an InternationalAmount
				PrmDiscountAmount		is an InternationalAmount
				PrmNetPaymentAmount		is an InternationalAmount
				PrmNetCheckAmount		is an InternationalAmount
				AddToAmounts			is Boolean
			Action Rules
				if (AddToAmounts)			
					TotalPaymentAmount		+= PrmPaymentAmount
					TotalDiscountAmount		+= PrmDiscountAmount
					TotalNetPaymentAmount	+= PrmNetPaymentAmount
					TotalBankCheckAmount	+= PrmNetCheckAmount
				else
					TotalPaymentAmount		-= PrmPaymentAmount
					TotalDiscountAmount		-= PrmDiscountAmount
					TotalNetPaymentAmount	-= PrmNetPaymentAmount
					TotalBankCheckAmount	-= PrmNetCheckAmount
		
		RemoveFromCashRequirementsResult is an Instance Action
			valid when (CanRemoveFromCashRequirementsResult)
			Local Fields
				InvokeDelete is Boolean
			Action Rules
				if (CanRemoveFromCashRequirementsResultForeground)
					invoke RemoveFromCashRequirementsResultBatch SelectedPayablesInvoicePaymentRel
					invoke SetManuallyChanged CashRequirementsResult
					InvokeDelete = true
				else
					invoke IncrementNumberOfVendorDeletes CashRequirementsResult
	
					CRBackgroundGroup		 				= BackgroundGroupKey + "CRRP-P1"
					invoke RemoveFromCashRequirementsResultBackground PayablesInvoicePayment in background group(CRBackgroundGroup)
						invoked.PrmVendorGroup 				= VendorGroup
						invoked.PrmPayGroup					= PayGroup
						invoked.PrmCashRequirementsResult 	= CashRequirementsResult
						invoked.PrmCashRequirementsReport 	= CashRequirementsReport
					LastBackgroundGroup						= CRBackgroundGroup
	
					CRBackgroundGroup		 				= BackgroundGroupKey + "CRRP-P2"
					invoke DeleteInBackground in background group(CRBackgroundGroup)
						run after background group (LastBackgroundGroup)
					invoke DecrementNumberOfVendorDeletes CashRequirementsResult in background group(CRBackgroundGroup)
						run after background group (LastBackgroundGroup)
			Exit Rules
				if (InvokeDelete)
					invoke Delete
		
		DeleteForCashRequirementsResult is a Set Action
			restricted
			completion message is "DeleteForCashRequirementsResultSubmitted"
			Parameters
				PrmVendorGroup				is a VendorGroup
				PrmPayGroup					is a PayGroup
				PrmCashRequirementsResult	is like CashRequirementsResult
			Instance Selection
				where (VendorGroup 				= PrmVendorGroup
				and    PayGroup					= PrmPayGroup
				and    CashRequirementsResult	= PrmCashRequirementsResult)
			Action Rules
				Instance Rules
					invoke Purge
					
		PopulateVendorNameField is a Set Action 
			default label is untranslatable
			restricted
			Instance Selection
				where (VendorName not entered)
			Action Rules
				Instance Rules
					VendorName = DerivedVendorName
			

