KitComponent is a BusinessClass
    owned by ic
    prefix is COM
    classic name is COMPONENT

    Ontology
        symbolic key is KitComponent
            classic set name is COMSET1
            classic name for KitComponent.ComponentSequence is SEQ
            classic name for KitComponent.OptionalComponentSequence is OPT-SEQ
            classic name for KitItem.InventoryLocation is LOCATION
            classic name for KitItem.Item is KIT-ITEM

    Patterns
        implements StaticJava
        disable AuditIndex
        implements ContextualParent
        enable explicit context override
        implements BODId
   	
    Persistent Fields
        ComponentItem                 is an Item
            classic name is ITEM
        Quantity					  
        StartDate
        StopDate                      is Date
        RequiredField                 is Boolean
            classic name is REQUIRED-FL
        InstructionLine
            classic name is INST-LINE
        LastOptionalComponentSequence is Numeric size 3
            classic name is LAST-OPT-SEQ
        QuantityTolerancePercentage
            classic name is QTY-TOL-PCT
        ZeroQuantity                  is Boolean
            classic name is ZERO-QTY-FL
        ComponentType
        InvLocation                   is an InventoryLocation
			default label is "ComponentLocation"
			context of KitItem.Company
        WasteAccount                  is a FinanceCodeBlock
            classic name for WasteAccount.AccountingUnit is WST-ACCT-UNIT
            classic name for WasteAccount.GeneralLedgerChartAccount is WST-ACCOUNT
        MaterialExpenseAccount        is a FinanceCodeBlock
            classic name for MaterialExpenseAccount.AccountingUnit is ME-ACCT-UNIT
            classic name for MaterialExpenseAccount.GeneralLedgerChartAccount is ME-ACCOUNT

	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 50
			restricted
			"InforBillOfMaterials"
			
		ContextMessageText is a MessageField
			restricted
			"KitComponent<KitComponent>OfKitItem<KitItem>"
			
		RequiredQuantity				is a DerivedField
			type is like Quantity
				precision is ComponentItem.NumberOfDecimalsQuantity
			if (KitItem.OrderSize entered)
				return (KitItem.OrderSize * Quantity)
			else
				return Quantity
				
		KitOrderQuantity 				is a DerivedField
			type is like Quantity
			return CustomerOrderLineRel.OrderQuantity * Quantity
				




		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedTenantID is a DerivedField 
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value


		
		DerivedComponentSequence is a DerivedField
			type is Numeric size 3
			restricted
			return KitComponent.ComponentSequence
		
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODCurrentTimeStamp = current timestamp
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedAccountingEntity is a DerivedField
			type is Alpha size 12
			restricted
			return ItemGroup.InventoryCompaniesRel.Company.GeneralLedgerCompany.AccountingEntity
			
		DerivedBODAccountingEntity is a DerivedField
			type is Alpha size 100
			restricted
			return DerivedFinanceEnterpriseGroup + DerivedDelimiter + DerivedAccountingEntity
		
		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha size 4
			restricted
			return ItemGroup.BusinessGroup.FinanceEnterpriseGroup
		
		DerivedLineItemID is a DerivedField
			type is Alpha size 200
			restricted
			return DerivedFinanceEnterpriseGroup + DerivedDelimiter + ItemGroup + DerivedDelimiter + ComponentItem
		
		DerivedInventoryLocation is a DerivedField
			type is Alpha size 20
			restricted
			return KitItem.InventoryLocation
		
		DerivedLocation is a DerivedField
			type is Alpha size 100
			restricted
			return DerivedCompany + DerivedDelimiter + DerivedInventoryLocation
		
		DerivedCompany is a DerivedField
			type is Alpha size 4
			restricted
			return KitItem.Company using "%d"
		
		DerivedDocumentID is a DerivedField
			type is Alpha 200
			restricted
			return DerivedCompany + DerivedDelimiter + DerivedInventoryLocation + DerivedDelimiter + DerivedItem
		
		DerivedBodID is a DerivedField
			type is Alpha size 200
			restricted
			return "infor-nid:" + DerivedTenantID + ":" + DerivedBODAccountingEntity + ":" + DerivedLocation + ":" + DerivedDocumentID + ":" + "?BillOfMaterials&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
		
		DerivedActionCode is a DerivedField
			type is Alpha size 10
			restricted
			if(ActionCode.Create or action type.Create)
				return "Add"
			if(ActionCode.Update or action type.Update)
				return "Replace"
			if(ActionCode.Delete or action type.Delete)
				return "Replace"
		
		DerivedBODVariationID is a DerivedField
			type is Alpha size 25
			restricted
			return bod id.VariationID
		
		DerivedHeaderItemID is a DerivedField
			type is Alpha size 100
			restricted
			return DerivedFinanceEnterpriseGroup + DerivedDelimiter + ItemGroup + DerivedDelimiter + DerivedItem
		
		DerivedItem is a DerivedField
			type is Alpha size 32
			restricted
			return KitItem.Item
		
		DerivedItemCodeUsed is a DerivedField
			type is Alpha size 1
			restricted
			return KitItem.ItemCodeUsed
		
		DerivedMakeCode is a DerivedField
			type is Alpha size 1
			restricted
			return KitItem.MakeCode
		
		DerivedOrderSize is a DerivedField
			type is Alpha size 8
			restricted
			return KitItem.OrderSize
		
		DerivedStatusCode is a DerivedField
			type is Alpha size 7 
			restricted
			if (ActionCode.Delete or action type.Delete)
				return "Deleted"
			if (ActionCode.Create or action type.Create)
				return "Open"
			if (ActionCode.Update or action type.Update)
				return "Open"
						
		DerivedFEG is a DerivedField
			type is Alpha size 4
			restricted 
			return ItemGroup.BusinessGroup.FinanceEnterpriseGroup
		
		DerivedNotes is a DerivedField
			type is Alpha size 20
			restricted
			return KitItem.Notes
					
		KitComponentLineXML is a DerivedField
			type is XMLDocument
			restricted
			if (KitComponentsItemRel exists)
				for each KitComponentsItemRel
					KitComponentLineXML += template.IONSyncBillOfMaterialsLine_KitComponent_ST document for each
				return KitComponentLineXML
			else
				return ""
				
		SyncBillOfMaterialsXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			SyncBillOfMaterialsXMLBOD = template.IONSyncBillOfMaterials_KitComponent_ST document for this instance
			return SyncBillOfMaterialsXMLBOD	
					


   	Local Fields
   		LocalItem						is an Item
   		LocalCompany					is a Company
		LocalInventoryLocation			is like InventoryLocation 
		LocalComponent 					is an Item 
		LocalTitle 				            is Alpha size 255
		ActionCode							is Alpha size 1
			States
				Create  value is "C"
				Update	value is "U"
				Delete	value is "D"

		LocalNativeLPLBODTrigger		is Boolean
		NewBODTracker  					is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker		is Numeric 15
		Error            				is Boolean
	    ErrorMessage     				is Alpha 300
	    LocalBODTrigger					is Boolean
	    LocalConfigurationParameter		is Alpha size up to 200

																								
	Transient Fields
	
		TotalOrderQuantity 				is like Quantity
	
	Context Fields
		ContextRequisition				is a Requisition
		ContextRequisitionLine			is a RequisitionLine  
		ContextCompany					is a BillingCompany
		ContextCustomerOrder			is a CustomerOrder 
		ContextCustomerOrderLine		is a CustomerOrderLine
		FSMInboundBODTracker 

	Conditions
		AvailableQuantityLessThanRequired
			restricted
			when (ItemLocationRel.AvailableQuantity < RequiredQuantity)

        IsMakeToOrderKit
            classic name is MK-TO-ORD-KIT
        	restricted
            when (KitItem.MakeCode.MakeToOrder)
            
		IsMakeToOrderKitAndInstructionsExists
			restricted
            when (KitItem.MakeCode.MakeToOrder
            and   KitComponentInstructionsRel exists)
            
        IsMakeToStockKit
            classic name is MK-TO-STK-KIT
        	restricted
            when (KitItem.MakeCode.MakeToStock)
            
        IsMakeToStockKitAndUseIncomeStatementAccounts
        	restricted
        	when (KitItem.MakeCode.MakeToStock
        	and InventoryLocationRel.UseIncomeStatementAccounts)

		HasMakeToOrderKitItem
			restricted
        	when (KitItem.MakeCode.MakeToOrder)
        	
        HasActiveKitItem
        	restricted
        	when (KitItem.Active)
        	
       	HasKitInstructions
       		restricted
       		when (KitComponentInstructionsRel exists)

		HasOptionalComponents
			restricted
			when (KitComponentWithOptionalComponentRel exist)

		IsComponentAKitItem
			restricted
			when (ComponentKitItemRel exists
			and   IsMakeToStockKit)
		
		IsFromRequisitionLineAndUnreleased
			restricted
			when (ContextRequisition.IsUnreleased
			and RequisitionLineRel exists)
			
		HasStockOnHand
			restricted
			when (ItemLocationRel.AvailableQuantity > 0)

    Relations
        ComponentKitItemRel
            classic name is COMP-IS-KIT
            one-to-one relation to KitItem
            Field Mapping uses symbolic key
                related.ItemGroup         		  = ItemGroup
                related.KitItem.Company           = KitItem.Company
                related.KitItem.InventoryLocation = KitItem.InventoryLocation
                related.KitItem.Item      		  = ComponentItem

        ItemLocationRel
            classic name is ITEMLOC
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = KitItem.Company
                related.InventoryLocation = KitItem.InventoryLocation
                related.Item              = ComponentItem
		
		ItemLocationForOptionalItemRel
			classic name is ITEMLOC
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = KitItem.Company
                related.InventoryLocation = KitItem.InventoryLocation
                related.Item              = LocalComponent
                
		ItemLocationForInventoryLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = KitItem.Company
                related.InventoryLocation = InvLocation
                related.Item              = ComponentItem
                
		ItemLocationForReplacementComponentRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = LocalCompany
                related.InventoryLocation = LocalInventoryLocation
                related.Item              = LocalItem
                
        KitInstructionsRel
            classic name is KTINSTRUCT
            one-to-many relation to KitInstruction
            delete cascades
            Field Mapping uses symbolic key
                related.ItemGroup         		= ItemGroup
                related.KitItem           		= KitItem
			Instance Selection
                where (related.KitInstruction.Sequence	= KitComponent.ComponentSequence)
		
		KitComponentInstructionsRel
            one-to-many relation to KitInstruction
            Field Mapping uses symbolic key
                related.ItemGroup         		= ItemGroup
                related.KitItem           		= KitItem
			Instance Selection
                where (related.KitInstruction.Sequence	= KitComponent.ComponentSequence)
		
		KitComponentRel
            one-to-many relation to KitComponent
            Field Mapping uses symbolic key
                related.ItemGroup         		  = ItemGroup
                related.KitItem.Company           = KitItem.Company
                related.KitItem.InventoryLocation = KitItem.InventoryLocation
                related.KitItem.Item		 	  = KitItem.Item
           	Instance Selection
           		where (related.KitComponent.ComponentSequence  = KitComponent.ComponentSequence
           		and	   related.KitComponent.OptionalComponentSequence not entered)
       	
       	KitComponentWithOptionalComponentRel
            one-to-many relation to KitComponent
            Field Mapping uses symbolic key
                related.ItemGroup         		  = ItemGroup
                related.KitItem.Company           = KitItem.Company
                related.KitItem.InventoryLocation = KitItem.InventoryLocation
                related.KitItem.Item		 	  = KitItem.Item
           	Instance Selection
           		where (related.KitComponent.ComponentSequence 			 = KitComponent.ComponentSequence
           		and	   related.KitComponent.OptionalComponentSequence entered)
           		
       	InventoryLocationRel
       		one-to-one relation to InventoryLocation
       		Field Mapping uses symbolic key
       			related.Company					= KitItem.Company
       			related.InventoryLocation		= KitItem.InventoryLocation
       
       	KitComponentsItemRel
        	one-to-many relation to KitComponent
            Field Mapping uses Set3
                related.ItemGroup         		  	= ItemGroup
                related.KitItem.Company           	= KitItem.Company
                related.KitItem.InventoryLocation 	= KitItem.InventoryLocation
                related.KitItem.Item		 	  	= KitItem.Item 
       	           
       	KitComponentsForReplacementRel
        	one-to-many relation to KitComponent
            Field Mapping uses Set3
                related.ItemGroup         		  	= ItemGroup
                related.KitItem.Company           	= LocalCompany
                related.KitItem.InventoryLocation 	= LocalInventoryLocation
                related.KitItem.Item		 	  	= KitItem.Item
                	
       	KitComponentStockedToTwoDifferentInventoryLocationRel
        	one-to-many relation to KitComponent
            Field Mapping uses Set3
                related.ItemGroup         		  	= ItemGroup
                related.KitItem.Company           	= KitItem.Company
                related.KitItem.InventoryLocation 	= KitItem.InventoryLocation
                related.KitItem.Item		 	  	= KitItem.Item 
                related.ComponentItem				= ComponentItem
         	Instance Selection
         		where (related.KitComponent.ComponentSequence = KitComponent.ComponentSequence)
                
       	ItemLocationForComponentItemRel
   			one-to-one relation to ItemLocation
   			Field Mapping uses symbolic key
   				related.Company 		  			= KitItem.Company
   				related.InventoryLocation 			= KitItem.InventoryLocation
   				related.Item			  			= LocalItem
   				
   		KitItemRel
   			one-to-one relation to KitItem
   			Field Mapping uses symbolic key 
   				related.ItemGroup					= ItemGroup
   				related.KitItem.Company 		  	= KitItem.Company
   				related.KitItem.InventoryLocation 	= InvLocation
   				related.KitItem.Item				= ComponentItem
   	
   		RequisitionLineRel
            one-to-one relation to RequisitionLine
            Field Mapping uses symbolic key
                related.Company         			= KitItem.Company
                related.Requisition     			= ContextRequisition
                related.RequisitionLine				= ContextRequisitionLine
        
        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company 					= KitItem.Company 
                
       	CustomerOrderLineRel
       		one-to-one relation to CustomerOrderLine
       		Field Mapping uses symbolic key
                related.Company						= ContextCompany
            	related.CustomerOrder				= ContextCustomerOrder
            	related.CustomerOrderLine			= ContextCustomerOrderLine  
        
       	LastComponentSeqRel
        	one-to-many relation to KitComponent
            Field Mapping uses symbolic key
                related.ItemGroup         		  	= ItemGroup
                related.KitItem.Company           	= KitItem.Company
                related.KitItem.InventoryLocation 	= KitItem.InventoryLocation
                related.KitItem.Item		 	  	= KitItem.Item
        	Instance Selection
         		where (related.KitComponent.ComponentSequence != KitComponent.ComponentSequence)  				
            	

        
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "BillOfMaterials"
            	related.FSMBODConfiguration.Direction 	= 1
				
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
				

    Sets
        Set2
            indexed
            Sort Order
                ItemGroup
                ComponentItem
                KitItem.Item
                KitItem.Company
                KitItem.InventoryLocation
                KitComponent.ComponentSequence
                KitComponent.OptionalComponentSequence

        Set3
            indexed
            Sort Order
                ItemGroup
                KitItem.Company
                KitItem.InventoryLocation
                KitItem.Item
                ComponentItem
                KitComponent.ComponentSequence
                KitComponent.OptionalComponentSequence
	
	Rule Blocks 
		TriggerBillOfMaterialsServiceRules
			LocalTitle = "EG:"+ItemGroup.BusinessGroup.FinanceEnterpriseGroup+" ITG:"+ItemGroup+" CO:"+KitItem.Company+" KIL:"+KitItem.InventoryLocation+" KITM:"+KitItem.Item
			trigger "BillOfMaterialsService" PA service
				resume on error
				title is "<LocalTitle>"
				Criteria
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
				Variables
					ActionCode
					include persistent fields from KitItem
					include persistent fields from KitComponent
					bod id.VariationID
						variable name is VariationId
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup	
					ItemGroup.InventoryCompaniesRel.Company.GeneralLedgerCompany.AccountingEntity
						variable name is AccountingEntity
					KitItem.ItemCodeUsed
						variable name is ItemCodeUsed
			
		
		MakeToStockEdits
			if (IsMakeToStockKit)
				if (ComponentType.Offsite)
					constraint (ItemLocationForInventoryLocationRel	not exists)
						"MustBeAnOffsiteLocation"									
				else
					for each KitComponentStockedToTwoDifferentInventoryLocationRel
						if (InvLocation != each.InvLocation
						and KitComponent.ComponentSequence != each.KitComponent.ComponentSequence
						and !each.ComponentType.Offsite)
							if ((each.StartDate not entered 
							and  each.StopDate  not entered) 
							or	(StartDate 		not entered
							and  StopDate       not entered)
							or  (StartDate 		not entered
							and  each.StartDate <= StopDate)
							or  (StopDate       not entered
							and (each.StartDate >= StartDate
							or   each.StopDate  >= StartDate))
							or  (each.StartDate not entered
							and  StartDate <= each.StopDate)
							or  (each.StopDate  not entered
							and (StartDate >= each.StartDate
							or   StopDate  >= each.StartDate))
							or  (StartDate >= each.StartDate
							and  StopDate  <= each.StopDate 
							and  StopDate 		entered)
							or  (each.StopDate 	entered
							and  each.StartDate >= StartDate
							and  each.StopDate  <= StopDate)
							or  (StopDate 		entered
							and  StopDate >= each.StartDate
							and  StopDate <= each.StopDate)
							or  (each.StopDate 	entered
							and  each.StopDate >= StartDate
							and  each.StopDate <= StopDate))
								constraint (false)
									"ComponentIsAlreadyStockedFromLocation<KitItem.InventoryLocation>"		
				if (!ComponentType.Offsite)
					constraint (ItemLocationForInventoryLocationRel exists)
						"ItemLocationDoesNotExistForComponentItem"
					constraint (ItemLocationForInventoryLocationRel.InventoryTracked)
						"ItemIsNotInventoryTracked"															
				constraint (!KitItemRel.MakeCode.MakeToOrder)
					"ComponentCannotBeAMakeToOrderKit"		
	
		IncrementComponentSeq
			if (LastComponentSeqRel exists) 
				KitComponent.ComponentSequence = last LastComponentSeqRel.KitComponent.ComponentSequence + 1 
			else
				KitComponent.ComponentSequence += 1
		

    Field Rules
    	InvLocation
    		if (IsMakeToStockKit)
    			default to KitItem.InventoryLocation

		ComponentItem
			required

            if (IsMakeToOrderKit)
            	if (KitItem.Company entered)
					constraint (ItemLocationRel exists)
						"ItemLocationDoesNotExistForComponentItem"											
					constraint (ItemLocationRel.InventoryTracked)
						"ComponentItemMustBeAnInventoryTrackedItem"								
		           	if (StopDate not entered
					or	StopDate >= current corporate date)
						constraint (ItemLocationRel.Active)
							"ItemLocationForComponentItemIsInactive"							
				constraint (ComponentItem != KitItem.Item)
					"ComponentItem/OptionalItemCannotBeSameAsKitItem"					
				constraint (!ComponentItem.IsCatchWeightItem)
					"KitItemOrComponentCannotBeCatchWeightItem" 						
			
			if (IsMakeToStockKit)
				constraint (ItemLocationRel exists)
					"ItemLocationDoesNotExistForComponentItem"											
				constraint (ItemLocationRel.InventoryTracked)
					"ComponentItemMustBeAnInventoryTrackedItem"								
				if (ComponentType.Rework)
					constraint (ComponentItem = KitItem.Item)
						"ComponentItemMustBeSameAsFinishedGoodItemForReworkComponent"	
				else   
					constraint (ComponentItem != KitItem.Item)
						"ComponentItemMustNotSameAsFinishedGoodItem"					

		StartDate
			if (StopDate entered)
				constraint (StartDate <= StopDate)
					"StartDateCannotBeGreaterThanStopDate"									
					
		RequiredField
			if (IsMakeToOrderKit)
				if (KitComponentWithOptionalComponentRel exists)     
					cannot be changed
						"OptionExistsComponentMustStayRequired"							

		Quantity
			if (IsMakeToOrderKit)
            	required

			if (IsMakeToStockKit)
				if (ComponentType.Component
				or  ComponentType.Offsite)
					required
						"QuantityIsRequired"											
				if (ComponentType.Rework)
					constraint (Quantity = 1)
						"PlanComponentQuantityMustBeOneForReworkTypeComponent"			

		ComponentType
			if (IsMakeToStockKit)
				if (InventoryLocationRel.IsOffsiteLocation)
					default to ComponentType.Offsite
				else 
					default to ComponentType.Component

				if (InventoryLocationRel.IsOffsiteLocation)
					constraint (ComponentType.Offsite)
						"LocationDefinedAsOffsiteWhileComponentTypeIsNotOffsite"			

		QuantityTolerancePercentage
			if (IsMakeToStockKit)
				constraint (!ZeroQuantity)
					"ZeroQuantityCannotBeSelectedIfToleranceIsNotZer"						
				constraint (!ComponentType.Offsite) 
					"ToleranceNotAllowedForOffsiteComponents"								

		WasteAccount
			if (IsMakeToStockKit)
				if (ComponentType.Offsite)
					cannot be entered
						"WasteAccountNotAllowedForOffsiteComponents"						
				else	
					default to InventoryLocationRel.WasteAccount

		MaterialExpenseAccount
			if (IsMakeToStockKit)
				if (ComponentType.Offsite)
					cannot be entered
						"MaterialExpenseAccountNotAllowedForOffsiteComponents"				
				if (InventoryLocationRel.UseIncomeStatementAccounts)
					required
						"MaterialExpenseMustBeSpecified"									
				else 
					cannot be entered
						"InventoryLocationDoesNotSpecifyTheUseOfAssetAccounts"				
		
		

    Actions
    	Create is a Create Action
    		restricted

		CreateRequiredComponent is a Create Action
			Field Rules
				RequiredField
					force default to true
					
			Action Rules
				include IncrementComponentSeq
				
				if (InvLocation not entered)
					InvLocation = KitItem.InventoryLocation
				include MakeToStockEdits

        CreateOptionalComponent is an Instance Action
        	valid when (IsMakeToOrderKit) 
        	Parameters
				PrmOptionalSequence	  is Numeric size 3		
				PrmComponent		  is an Item		  
				PrmQuantity			  is like Quantity 
				PrmStartDate		  is Date	  
				PrmStopDate           is Date
			
			Action Rules
				include IncrementComponentSeq
				
				if (PrmOptionalSequence entered)
					LocalComponent = PrmComponent
					constraint (ItemLocationForOptionalItemRel exists)
						"ItemLocationDoesNotExistForOptionalComponent"
					constraint (ItemLocationForOptionalItemRel.IsNotLotTracked)
						"OptionalItemCannotbeLotTrackedForMakeToOrder"				
				
				for each KitComponentRel
					constraint (each.ComponentItem != PrmComponent)	
						"OptionalItemCannotBeSameAsComponent"							
							








				
				invoke Create KitComponent
                	invoked.KitComponent.OptionalComponentSequence	= PrmOptionalSequence	
					invoked.ComponentItem							= PrmComponent
					invoked.Quantity								= PrmQuantity
					invoked.StartDate 								= PrmStartDate
					invoked.StopDate								= PrmStopDate
					invoked.RequiredField 							= false
		
		Update is an Update Action
			Action Rules
				if (InvLocation not entered)
					InvLocation = KitItem.InventoryLocation
				include MakeToStockEdits 
					
        Delete is a Delete Action
        	Exit Rules
				if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger)
					ActionCode = ActionCode.Delete
					increment bod id.VariationID
					include TriggerBillOfMaterialsServiceRules



       			
		ReplaceComponent is a Set Action
			run in foreground
			Parameters
				PrmItemGroup			is an ItemGroup
				PrmFromCompanyLocation	is a FromCompanyLocation
				PrmComponentItem		is an Item
				PrmReplacementComponent	is an Item
				PrmQuantityFactor		is Numeric size 8
				PrmActive				is Boolean
						
			Parameter Rules
				PrmItemGroup
					required
				PrmFromCompanyLocation
					required
					constraint (PrmFromCompanyLocation.FromCompany entered
					and			PrmFromCompanyLocation.FromLocation entered)
						"MustEnterBothCompanyAndLocation"
				PrmComponentItem
					required     
				PrmReplacementComponent
					required
			
			Sort Order 
				ItemGroup
				ComponentItem
				KitItem.Company 
				KitItem.InventoryLocation
							
			Instance Selection
				where (ItemGroup 	 			 = PrmItemGroup
				and    ComponentItem 			 = PrmComponentItem)
			
			Action Rules
				Instance Rules
					constraint (KitItem.Company	 			= PrmFromCompanyLocation.FromCompany
					and 		KitItem.InventoryLocation 	= PrmFromCompanyLocation.FromLocation)
						"CompanyOrLocationIsDifferent"
					if (KitItem.Item  != PrmReplacementComponent
					and KitItem.Active = PrmActive)
						if (KitItem.MakeCode.MakeToOrder)
							LocalItem		 		= PrmReplacementComponent
							LocalCompany			= PrmFromCompanyLocation.FromCompany
							LocalInventoryLocation	= PrmFromCompanyLocation.FromLocation
							constraint (ItemLocationForReplacementComponentRel exists)
								"ItemLocationDoesNotExistForReplacementComponent"											
							constraint (ItemLocationForReplacementComponentRel.InventoryTracked)
								"ComponentMustBeAnInventoryTrackedItem"								
							if (StopDate not entered
							or	StopDate >= current corporate date)
								constraint (ItemLocationForReplacementComponentRel.Active)
									"ItemLocationForComponentIsInactive"							
									
							constraint (!PrmReplacementComponent.IsCatchWeightItem)
								"KitItemOrComponentCannotBeCatchWeightItem" 						
							if (KitComponent.OptionalComponentSequence entered)
								constraint (ItemLocationForReplacementComponentRel.IsNotLotTracked)
									"OptionalItemCannotbeLotTrackedForMakeToOrder"				
							constraint (PrmReplacementComponent != KitItem.Item)
								"OptionalItemCannotBeSameAsKitItem"	
							for each KitComponentRel
								constraint (each.ComponentItem != PrmReplacementComponent)	
									"OptionalItemCannotBeSameAsComponent"									
							for each KitComponentsForReplacementRel
								LocalItem = each.ComponentItem
								if (each.RequiredField)
									constraint (ItemLocationForReplacementComponentRel.IsNotLotTracked)		
										"RequiredComponentForMakeToOrderKitCannotBeLotTracked"
									constraint (ItemLocationForReplacementComponentRel.IsNotSerialTracked)
										"MakeToOrderComponentSequenceMustNotBeSerialTracked"				
										
						if (KitItem.MakeCode.MakeToStock)
							if (InvLocation not entered)
								InvLocation = KitItem.InventoryLocation
							include MakeToStockEdits
							
						ComponentItem	= PrmReplacementComponent
						if (PrmQuantityFactor entered)
							Quantity		= Quantity * PrmQuantityFactor						

		CreateRequisitionLine is an Instance Action
			valid when (IsFromRequisitionLineAndUnreleased)
			default label is "AddToRequisition"
			completion message is "Item<ComponentItem>AddedTo_\Requisition<ContextRequisition>"
			Action Rules
				invoke Create RequisitionLine
					fill in fields from this instance
					invoked.Company				= KitItem.Company
					invoked.Requisition			= ContextRequisition
					invoked.Item				= ComponentItem
					invoked.ItemEntryMethod		= ItemEntryMethod.Item
					invoked.Description			= ComponentItem.Description
					invoked.EnteredUOM			= ComponentItem.DefaultSellingUOM
					invoked.Quantity			= RequisitionLineRel.Quantity * Quantity
					
		UpdateOrderQuantityForKitComponent is an Instance Action
		
			Action Rules
				invoke Update CustomerOrderLineRel
					invoked.OrderQuantity	= KitOrderQuantity 
		
		SendBillOfMaterialsNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is SyncBillOfMaterialsXMLBOD
					bod type is "Sync.BillOfMaterials"
					document id is  DerivedDocumentID
					variation id is DerivedBODVariationID
					accounting entity is DerivedBODAccountingEntity
					location is DerivedLocation
		
		TriggerBillOfMaterialsNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "BillOfMaterials"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "KitComponent"
					invoked.PrmFinanceEnterpriseGroup	= ItemGroup.BusinessGroup.FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate 		= "IONSyncBillOfMaterials_KitComponent_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "BillOfMaterials"
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.BODAccountingEntity		= DerivedBODAccountingEntity
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= ItemGroup
							invoked.Reference2				= KitItem.Company
							invoked.Reference3				= KitItem.InventoryLocation
							invoked.Reference4				= KitItem.Item
							invoked.Reference5				= KitComponent.ComponentSequence
							initialize invoked.Error			
							initialize invoked.ErrorMessage
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.BODAccountingEntity		= DerivedBODAccountingEntity
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= ItemGroup
							invoked.Reference2				= KitItem.Company
							invoked.Reference3				= KitItem.InventoryLocation
							invoked.Reference4				= KitItem.Item
							invoked.Reference5				= KitComponent.ComponentSequence
							initialize invoked.Error			
							initialize invoked.ErrorMessage
							
					invoke SendBillOfMaterialsNativeLPL
						resume on error
		                    Error            							= true
		                    ErrorMessage     							= error message
		                        
		          	if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBodID
							invoked.BODXML								= SyncBillOfMaterialsXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBodID
							invoked.BODXML									= SyncBillOfMaterialsXMLBOD			
									
		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location" 
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"	
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"


			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord


							
	Action Exit Rules
    	if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
    		if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode = ActionCode.Update
					if (action type.Create)
						ActionCode = ActionCode.Create
					increment bod id.VariationID
					include TriggerBillOfMaterialsServiceRules



