LandingPageWidget is a BusinessClass
	owned by lpdesigner
	
	prefix is LPW
	
	Ontology
		symbolic key is LandingPageWidget
		
	Patterns
		implements DynamicCreation
		disable EffectiveDated
		disable AsOfDateProcessing
		
	Persistent Fields
		ShortDescription		is Alpha size 70
			translatable
			default label is "Description"
		LandingPageWidgetType
			default label is "WidgetType"
		DataSizeWidth			is Numeric size 2
			default label is "WidgetWidth"
			States
				OneColumn		value is 1
				TwoColumn		value is 2
				ThreeColumn		value is 3
				FourColumn		value is 4
		WidgetHeight		is Numeric size 2
			States
				1X			value is 1
				2X			value is 2
				3X			value is 3
				4X			value is 4
		WidgetLongText			is Alpha size up to 250
			translatable
			default label is "LongDescription"
		WidgetText				is Alpha size 30
			translatable		
			default label is "Title"
		WidgetImage				is a UserImage
			default label is "Image"
		WidgetURL				is Alpha size up to 512
			default label is "URL"
		WidgetTargetTab		is Alpha size 30
			default label is "NamedTab"
		OpenInNewWindow			is Boolean
		ExternalSite			is Boolean
		BusinessView
		BusinessList
		BusinessChart
		BusinessClass								
		BusinessClassChart							
		BusinessForm
		LandingPageCarouselWidget
		CustomCode				is RichText
			default label is "RichText"
		URLParameter			is Alpha size up to 512	
			default label is "AdditionalURLParameters"

		OpenLinkIn				is Numeric size 2
			States
				SameTab			value is 0
				NewTab			value is 1
				NamedTab		value is 2
		KeyFieldParameters		is Alpha size up to 512	
		IsTemplate				is Boolean
			default label is "Template"
		CopiedWidget			is a LandingPageWidget 
		LandingPageGroupWidget
		NavigationWidget		is a LandingPageWidget
		OpenNavigationLinkIn	is Numeric size 2
			default label is "OpenLinkIn"
			States
				SameTab			value is 0
				NewTab			value is 1
				NamedTab		value is 2
		NavigationTargetTab		is Alpha size 30		
			default label is "NamedTab"
		ImageAlignment			is Numeric size 1
			States
				Fill		value is 0
				Center		value is 1
				Fit			value is 2

	Conditions
		LandingPageDetailsExists
			when (LandingPageDetailRel exists)

		ProtocolExists
			when (URLProtocol = "http:"
			or    URLProtocol = "https"
			or    URLProtocol = "HTTP:"
			or    URLProtocol = "HTTPS")

		ProtocolMismatch
			when (ProtocolExists
			and   BaseProtocol != URLProtocol)

		WidgetExists
			when (LandingPageWidget exists)

	Derived Fields

		LpdBaseURL is a DerivedField
			type is Alpha 300
			return base url(webapp is LandingPageWebapp)
		
		HomePageWidgetURL is a DerivedField
			type is Text
			return "/widget/" + LandingPageWidget + "?mw=true&csk.LogicalId=<your logicalId here>"						

		BaseProtocol is a DerivedField
			type is Alpha 5
			return LpdBaseURL 

		URLProtocol is a ConditionalField
			type is Alpha 5
			if (LandingPageWidgetType.ExternalFrame)
				WidgetURL
			else
				blank 

		ProtocolMessage is a MessageField
			"WidgetsURLProtocol_(<URLProtocol>)DoesNotMatchApplicationURLProtocol_(<BaseProtocol>)"	

		ProtocolMessageText is a MessageField
			"WidgetMayNotDisplayCorrectly"

		NavigationWidgetUrl is a DerivedField
			type is Alpha size 512
			return NavigationWidget.WidgetURL

        IsGovCloud is a ConditionalField
            type is Boolean
            default label is untranslatable
            if (com.lawson.rdtech.type.FipsCryptoConstants.isFedRAMP()
            or  com.lawson.rdtech.type.FipsCryptoConstants.isFipsEnabled())
                true
            else
                false

	Field Rules
		ShortDescription
			default to LandingPageWidget
			
		LandingPageWidgetType
			required
			cannot be changed

			if (LandingPageWidgetType.ExternalFrame
			and (com.lawson.rdtech.type.FipsCryptoConstants.isFedRAMP()
			or   com.lawson.rdtech.type.FipsCryptoConstants.isFipsEnabled()))
				constraint (IsTemplate)
					"ExternalFrameWidgetsAreNoLongerSupportedOrRendered"

		DataSizeWidth
			required

		WidgetLongText
			if (LandingPageWidgetType.ProcessLink)
				required
		
		WidgetText
			if (LandingPageWidgetType.ProcessLink
			or  LandingPageWidgetType.ExternalFrame
			or  LandingPageWidgetType.RichText)
				required
		
		WidgetURL
			if (LandingPageWidgetType.ProcessLink
			or  LandingPageWidgetType.ExternalFrame)
				required
		
		WidgetImage
			if (LandingPageWidgetType.ProcessLink)
				required 
			
		BusinessView
			if (LandingPageWidgetType.LMList
			or  LandingPageWidgetType.LMChart
			or  LandingPageWidgetType.LMForm
			or  LandingPageWidgetType.ImageList
			or  LandingPageWidgetType.StatusCounter)
				required

		BusinessList
			if (LandingPageWidgetType.LMList
			or  LandingPageWidgetType.LMChart
			or  LandingPageWidgetType.ImageList)
				required
		
		BusinessChart
			if (LandingPageWidgetType.LMChart)
				required
				
		BusinessForm
			if (LandingPageWidgetType.LMForm
			or  LandingPageWidgetType.StatusCounter)
				required

		OpenNavigationLinkIn
			default to OpenNavigationLinkIn.SameTab
				
		NavigationTargetTab
			if (OpenNavigationLinkIn.NamedTab)
				required
				
		OpenLinkIn
			default to OpenLinkIn.SameTab
				
		WidgetTargetTab
			if (OpenLinkIn.NamedTab)
				required
				
		CustomCode
			if (LandingPageWidgetType.RichText)
				required

	Relations
		LandingPageDetailRel is a LandingPageDetail set
			Instance Selection
				where (related.LandingPageWidget = LandingPageWidget)
				
	Rule Blocks
		GroupWidgetTemplate
			if (IsTemplate
			and LandingPageGroupWidget entered
			and not LandingPageGroupWidget.IsTemplate)
				invoke UpdateToTemplate LandingPageGroupWidget				
			
	Create Rules
		include GroupWidgetTemplate	
				
	Actions	
		CreateTemplate is a Create Action
			restricted
			
		UpdateTemplate is an Update Action

			
			Action Rules
				include GroupWidgetTemplate
		
		DeleteTemplate is a Delete Action
			valid when (IsTemplate)

		Create is an Action

		Update is an Action
			valid when (!IsTemplate)
			
			Action Rules
				include GroupWidgetTemplate

		Delete is an Action
			valid when (!IsTemplate)

        Copy is an Instance Action
            Parameters
                PrmLandingPageWidget       	is Alpha size 60
					default label is "LandingPageWidget"

            Parameter Rules
                PrmLandingPageWidget
					required
                              
			Action Rules
				invoke Create 
					fill in fields from this instance
					invoked.LandingPageWidget			= PrmLandingPageWidget
					invoked.IsTemplate					= 0
					invoked.CopiedWidget				= LandingPageWidget
					
		AssignToLandingPage is an Instance Action
   			Parameters
				LandingPage
				PrmLandingPageDetail					is AlphaUpper size 60
					default label is "LandingPageDetail"

			Parameter Rules
				LandingPage
					required
				PrmLandingPageDetail
					required
					initial value is LandingPageWidget

			Action Rules
				constraint(!LandingPage.IsTemplate)
					"CannotAssignToATemplateLandingPage"

				invoke Create LandingPageDetail
					invoked.LandingPage					= LandingPage
					invoked.LandingPageDetail			= PrmLandingPageDetail
					invoked.LandingPageWidget			= LandingPageWidget
					
		UpdateWidget is an Update Action
			restricted
			confirmation required
				"ThisChangeWillAffectAllLandingPagesWhereThisWidgetIsUsed;Continue?"
