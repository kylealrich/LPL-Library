CashManagementLocation is a BusinessClass
    owned by cashmgmt
    prefix is CMCML

    Ontology
    	symbolic key is CashManagementLocation

    Persistent Fields
		Description			is Alpha 250
		Currency
		Active
		
	Local Fields
		NewParentLocation	is a CashManagementLocation

	Derived Fields
		NewParentLocationIsAChildLocation is a DerivedField
			type is Boolean
			restricted
   			for each NewParentLocation and ancestors
				if (each.ParentCashLocation = CashManagementLocation)
					return true
					end for each
					
	Field Rules
		Description
			default to CashManagementLocation
		
		Currency
			default to CashManagementGroup.Currency
			required
			constraint (CashManagementGroup.Currency	entered)
				"CorporateCurrencyRequiredOnCashManagementGroup"
			if (Currency	!= CashManagementGroup.Currency)
				constraint (CashManagementGroup.FinanceEnterpriseGroup.CurrencyTable entered)
					"CorporateCurrencyTableRequiredWhenLocationCurrencyIsDifferentFromCorporateCurrency"

		Active
			initial value is true
				
	Conditions
		HasParentLocation
			restricted
    		when (ParentCashLocation entered)
    		
    	HasChild
    		restricted
    		when (CashManagementLocation children exist)
    		
    Attach Rules
    	if (parentcontext.name != "BankStatementTotal")
			constraint (Active)
				"LocationIsInactive"
	
	Relations
		CashManagementAccountByLocationRel
			one-to-many relation to CashManagementAccount
			Field Mapping uses ByLocation
				related.CashManagementGroup    = CashManagementGroup
				related.CashManagementLocation = CashManagementLocation
					
	Actions
		Create is a Create Action
					
		Update is an Update Action
		
		ReparentLocation is an Instance Action
			Parameters
				PrmParentLocation is a CashManagementLocation
				
			Parameter Rules
				PrmParentLocation
					required
						"NewParentLocationIsRequired"
						
					constraint (PrmParentLocation != CashManagementLocation)
						"CannotSelectLocationToBeItsOwnParent"
						
					constraint (PrmParentLocation != ParentCashLocation)
						"NewParentIsTheSameAsCurrentParent"
						
			Action Rules
				NewParentLocation = PrmParentLocation
				
				constraint (!NewParentLocationIsAChildLocation)
					"CannotChooseAChildLocationToBeTheNewParentLocation"
					
				ParentCashLocation = NewParentLocation
				
		RemoveParentLocation is an Instance Action
			valid when (HasParentLocation)
			Action Rules
				initialize ParentCashLocation
							
		Delete is a Delete Action
