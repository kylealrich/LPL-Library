StatementParsingRule is a BusinessClass
    owned by cashmgmt
    prefix is SPR
  
    Ontology
    	symbolic key is StatementParsingRule

    Persistent Fields
        Description				        is Alpha up to 250
        ParsingRule				        is a BankStatementLine group
			default label is "CustomGroup"
        RemoveCharacterString			is Boolean
        RemoveString				    is AlphaUpper up to 20
        RemoveCharactersBefore		    is Boolean
			default label is "RemoveCharacterStringAndPrecedingCharacters"
        RemoveBefore				    is AlphaUpper up to 20
        RemoveCharactersAfter			is Boolean
			default label is "RemoveCharacterStringAndSubsequentCharacters"
        RemoveAfter				        is AlphaUpper up to 20
		CharacterStringValueRange		is Boolean
			default label is "IncludeCharacterStringInRange"
		CharacterStringRange
		LiteralValueField				is Boolean
			default label is "LiteralValue"
		LiteralValue				    is Alpha 50
            Text Variables
				RelatedReference
				BankReference
				TextReference
        Active
    
    Local Fields

    Derived Fields

        DerivedRemoveString is a StringField
			type is AlphaUpper up to 30
			"\Q"
			RemoveString
			"\E"

        DerivedRemoveBefore is a StringField
			type is AlphaUpper up to 30
			"^.*"
			"\Q"
			RemoveBefore
			"\E"

        DerivedRemoveAfter is a StringField
			type is AlphaUpper up to 30
			"\Q"
			RemoveAfter
			"\E"
			".*$"


    Field Rules

        Description
			default to StatementParsingRule

        ParsingRule
			required
				"CustomGroupIsRequired"

		LiteralValue
			if(LiteralValueField)
				required
					"LiteralValueIsRequired"
			else
				initialize

        RemoveCharacterString
			constraint (!RemoveCharactersBefore)
				"CannotRemoveCharacterStringAndRemoveCharacterStringAndPrecedingCharacters"
            constraint (!RemoveCharactersAfter)
				"CannotRemoveCharacterStringAndRemoveCharacterStringAndSubsequentCharacters"
			constraint (LiteralValue !entered)
				"CannotRemoveCharacterStringAndEnterALiteralValue"

        RemoveString
			if (RemoveCharacterString)
				required
					"MustEnterACharacterStringToBeRemoved"
			else
				initialize

        RemoveCharactersBefore
			constraint (LiteralValue !entered)
				"CannotRemoveCharactersBeforeAndEnterALiteralValue"
        
        RemoveBefore
			if (RemoveCharactersBefore
			and RemoveCharactersAfter)
				constraint (RemoveBefore != RemoveAfter)
					"RemoveCharacterStringAndPrecedingCharactersMustBeDifferentThanRemoveCharacterStringAndSubsequentCharacters"
            if (RemoveCharactersBefore)
				required
					"MustEnterACharacterStringToBeRemoved"
			else
				initialize

        RemoveCharactersAfter
			constraint (LiteralValue !entered)
				"CannotRemoveCharactersAfterAndEnterALiteralValue"


        RemoveAfter
			if (RemoveCharactersAfter
			and RemoveCharactersBefore)
				constraint (RemoveAfter != RemoveBefore)
					"RemoveCharacterStringAndSubsequentCharactersMustBeDifferentThanRemoveCharacterStringAndPrecedingCharacters"
				
			if (RemoveCharactersAfter)
				required
					"MustEnterACharacterStringToBeRemoved"
			else
				initialize
		
		CharacterStringValueRange
			constraint (LiteralValue !entered)
				"CannotEnterACharacterStringRangeAndEnterALiteralValue"

        CharacterStringRange
			constraint (CharacterStringRange.CharacterToValue entered)
				"CharacterToValueRequired"
        
			constraint (CharacterStringRange.CharacterFromValue entered)
				"CharacterFromValueRequired"

            constraint (CharacterStringRange.CharacterFromValue < CharacterStringRange.CharacterToValue)
				"CharacterToValueMustBeGreaterThanCharacterFromValue"
			
			if (CharacterStringValueRange)
				required
					"MustEnterACharacterStringValueRange"
			else
				initialize

        Active
			initial value is true

			if (Active changed and not Active)	
				constraint (NotInParsingRuleGroup)
					"CannotInactivate;_\Statement_\Parsing_\RuleIsAssignedTo_\Statement_\Parsing_\Rule_\Group:<first StatementParsingRuleGroupDetailRel.StatementParsingRuleGroup>"
				

	Conditions
	
		NotInParsingRuleGroup
			restricted
			when (!StatementParsingRuleGroupDetailRel exists)

	Relations

		StatementParsingRuleGroupDetailRel
			one-to-many relation to StatementParsingRuleGroupDetail
			Field Mapping uses ByStatementParsingRule
				related.CashManagementGroup			= CashManagementGroup
				related.StatementParsingRule		= StatementParsingRule



	Attach Rules
		constraint (Active)
			"ParsingRuleIsInactive"

	
	Actions
		Create is a Create Action
					
		Update is an Update Action
		
		AddParsingRule is an Instance Action
			valid when (Active)
			default label is "AddToParsingRuleGroup"
			Parameters
				PrmCashManagementGroup 		  		is a CashManagementGroup
				PrmStatementParsingRuleGroup	is a StatementParsingRuleGroup
			Action Rules
				invoke Create StatementParsingRuleGroupDetail
					invoked.CashManagementGroup	 				= PrmCashManagementGroup
					invoked.StatementParsingRuleGroup	= PrmStatementParsingRuleGroup
					invoked.StatementParsingRule			= StatementParsingRule
					
		Delete is a Delete Action
			Action Rules
				constraint (NotInParsingRuleGroup)
					"CannotDelete;<StatementParsingRule>ExistsIn_\Statement_\Parsing_\Rule_\Group:<first StatementParsingRuleGroupDetailRel.StatementParsingRuleGroup>"



