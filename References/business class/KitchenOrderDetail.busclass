KitchenOrderDetail is a BusinessClass
	owned by recipe
	prefix is KDTL
	
	Ontology
		symbolic key is KitchenOrderDetail
		
	Patterns
		disable AuditIndex
		
	Persistent Fields
		Item
		UnitOfMeasure
		Bin
		UOMMultiplier
		LotOnHold				is Boolean
		LotExpirationDate		is Date
		TransactionBarcode		is AlphaUpper size 25
		Lot						is an ItemLot
		Sublot
		Serial					is an ItemSerialNumber
			delete ignored
		Quantity				is an UnsignedQuantity
		SecondaryQuantity		is an UnsignedQuantity
	
	Relations
		ItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company					= KitchenOrder.Company
				related.InventoryLocation		= KitchenOrder.InventoryLocation
				related.Item					= KitchenOrder.Item
	
		OtherKitchenOrderDetailWithSameSerialRel
			one-to-many relation to KitchenOrderDetail
			Field Mapping uses symbolic key
				related.Company						 = KitchenOrder.Company
				related.InventoryLocation			 = KitchenOrder.InventoryLocation
				related.KitchenOrder				 = KitchenOrder
			Instance Selection
				where (related.KitchenOrderDetail 	!= KitchenOrderDetail
				and    related.Serial entered
				and    related.Serial 				 = Serial)
	
	Derived Fields
		DerivedDetailTrackingMethod is a DerivedField
			type is MessageField
			if (KitchenOrder.ItemLocationRel.BinTracked)
				return "Bin"
			else
			if (KitchenOrder.ItemLocationRel.LotTracked.LotRequiredAtReceipt)
				return "Lot"
			else
			if (KitchenOrder.ItemLocationRel.SerialTracked.SerialRequiredAtReceipt)
				return "Serial"
				
		DerivedRequiredMessage is a DerivedField
			type is MessageField
			return "Item " + KitchenOrder.Item + " is " + DerivedDetailTrackingMethod + " Tracked at Location " + KitchenOrder.InventoryLocation
		
		DerivedCannotBeEnteredMessage is a DerivedField
			type is MessageField
			return "Item " + KitchenOrder.Item + " is not " + DerivedDetailTrackingMethod + " Tracked at Location " + KitchenOrder.InventoryLocation
		
	Field Rules
		Item
			force default to KitchenOrder.Item
			
		Bin
			if (KitchenOrder.ItemLocationRel.BinTracked)
				required
					"<DerivedRequiredMessage>"
			else
				cannot be entered
					"<DerivedCannotBeEnteredMessage>"
	
		Lot
			if (KitchenOrder.ItemLocationRel.LotTracked.LotRequiredAtReceipt)
				required
					"<DerivedRequiredMessage>"
			else
				cannot be entered
					"<DerivedCannotBeEnteredMessage>"
					
		Serial
			if (KitchenOrder.ItemLocationRel.SerialTracked.SerialRequiredAtReceipt)
				required
					"<DerivedRequiredMessage>"
				constraint (Serial not exists)
					"SerialNumber<Serial>AlreadyExists"
				constraint (OtherKitchenOrderDetailWithSameSerialRel not exists)
					"SerialNumber<Serial>AlreadyInUseForItem"
			else
				cannot be entered
					"<DerivedCannotBeEnteredMessage>"
		
		UnitOfMeasure
			force default to KitchenOrder.BatchYieldUOM
			
		UOMMultiplier
			force default to KitchenOrder.BatchYieldUOMMultiplier
	
		Quantity
			if (Serial entered)
				force default to 1
		
	Actions
		Create is a Create Action
			Action Rules

		Update is an Update Action
		
		Delete is a Delete Action		
			
