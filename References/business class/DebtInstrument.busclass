DebtInstrument is a BusinessClass
	owned by treasury
	prefix is DMDI	

	Ontology
		symbolic key is DebtInstrument
		
	Persistent Fields								
		DebtInstrumentType					is Numeric size 2
			default label is "InstrumentType"
			States
    			DebtInstrument		value is 1
				Derivative			value is 2
				CreditSupport		value is 3
				LineOfCredit		value is 4
		Description							is Alpha up to 250	
		DebtStatus							is Numeric size 2
			default label is "Status"
    		States
    			New					value is 1
    			AwaitingApproval	value is 2
				Outstanding			value is 3
				Paid				value is 4
				Redeemed			value is 5
				Defeased			value is 6
		InterestMethod						is Numeric 1
			States
				Fixed				value is 1
				Variable			value is 2
		DebtGroup
		DebtType
        CashTransactionCategory
		CashForecastAccount					is a CashManagementAccount
		Issuer								is a FinancialInstitution
		IssuerBranch						is a FinancialInstitutionBranch
		IssueDate							is an ExchangeDate
        TaxExempt							is AlphaUpper 1
            States
                No  				value is "N"
                Yes 				value is "Y"
		DebtCurrency						is a FromCurrency
			default label is "Currency"
		DebtCalendar						is a SystemCalendar
		DebtCurrencyExchange				is a CurrencyAmount
		CorporateBaseCurrencyExchange		is a CurrencyExchange
		LocationCurrencyExchange	    	is a CurrencyExchange
		MultiplyBaseCurrencyRate			is Boolean
		MultiplyLocationCurrencyRate		is Boolean
        DebtReportGroup
        Obligor								is Alpha up to 100
		CashManagementLocation
		LegalEntityGroup
		CallProvision						is Boolean
		CallProvisionDescription			is Alpha up to 250
		LineOfCreditCapacityAmount			is an InternationalAmount
			default label is "CapacityAmount"
		OutstandingTimestamp				is TimeStamp
		AwaitingApprovalTimestamp			is TimeStamp
		PaidTimestamp 						is TimeStamp
		RedeemedTimestamp 					is TimeStamp
		DefeasedTimestamp 					is TimeStamp
		Active

	Local Fields
		FirstParentDebtGroupIteration		is Boolean
		LocalParentDebtGroupStructure 		is Alpha up to 500
		FirstParentDebtTypeIteration		is Boolean
		LocalParentDebtTypeStructure 		is Alpha up to 500
		FirstParentLocationIteration		is Boolean
		LocalParentLocationStructure 		is Alpha up to 500
		FirstParentDebtReportGroupIteration	is Boolean
		LocalParentDebtReportGroupStructure is Alpha up to 500
		CurrencyTable
	
	Context Fields
		MaturityDateRange				is a DateRange
				
	Derived Fields
		ParentDebtGroupStructure is a DerivedField
			type is Alpha up to 500
			restricted
			FirstParentDebtGroupIteration = true
			
			for each DebtGroup ancestors
				if (FirstParentDebtGroupIteration = true)
					LocalParentDebtGroupStructure = each.DebtGroup
					FirstParentDebtGroupIteration = false
				else
					LocalParentDebtGroupStructure = each.DebtGroup + "-" + LocalParentDebtGroupStructure
					
			return LocalParentDebtGroupStructure
			
		ParentDebtTypeStructure is a DerivedField
			type is Alpha up to 500
			restricted
			FirstParentDebtTypeIteration = true
			
			for each DebtType ancestors
				if (FirstParentDebtTypeIteration = true)
					LocalParentDebtTypeStructure = each.DebtType
					FirstParentDebtTypeIteration = false
				else
					LocalParentDebtTypeStructure = each.DebtType + "-" + LocalParentDebtTypeStructure
					
			return LocalParentDebtTypeStructure
			
		ParentLocationStructure is a DerivedField
			type is Alpha up to 500
			restricted
			FirstParentLocationIteration = true
			
			for each CashManagementLocation ancestors
				if (FirstParentLocationIteration = true)
					LocalParentLocationStructure = each.CashManagementLocation
					FirstParentLocationIteration = false
				else
					LocalParentLocationStructure = each.CashManagementLocation + "-" + LocalParentLocationStructure
					
			return LocalParentLocationStructure
		
		ParentDebtReportGroupStructure is a DerivedField
			type is Alpha up to 500
			restricted
			FirstParentDebtReportGroupIteration = true
			
			for each DebtReportGroup ancestors
				if (FirstParentDebtReportGroupIteration = true)
					LocalParentDebtReportGroupStructure = each.DebtReportGroup
					FirstParentDebtReportGroupIteration = false
				else
					LocalParentDebtReportGroupStructure = each.DebtReportGroup + "-" + LocalParentDebtReportGroupStructure
					
			return LocalParentDebtReportGroupStructure
		
		MaturityDate is a DerivedField
			type is Date
			return last DebtInstrumentIdentifierMaturityRel.MaturityDate
		
		LineOfCreditCommittedAmount is a DerivedField
			type is like InternationalAmount
			default label is "CommittedAmount"
			return (sum LineOfCreditCommittedRel.OriginalPrincipalAmount)
		
		AdjustedInterestMethod is a DerivedField
			type is Alpha 20
			restricted
			if (InterestMethod.Fixed)
				if (HasSwapAttached)
					return "SyntheticVariable"
				else
					return "Fixed"
			else
			if (InterestMethod.Variable)
				if (HasSwapAttached)
					return "SyntheticFixed"
				else
					return "Variable"	
						
	Field Rules
		DebtInstrumentType
			required
		
		Description
			default to DebtInstrument
		
		InterestMethod
			required
			
			if (InterestMethod changed
			and	DebtInstrumentIdentifier set exists)
				if (InterestMethod.Fixed) 
					confirmation required
						"InterestDetailsWillBeUpdatedForAllIdentifiers;WouldYouLikeToContinue"
					
					for each DebtInstrumentIdentifier set
						invoke Update each 
							if (!each.FixedInterestRate entered)
								invoked.FixedInterestRate = .01
							initialize invoked.DebtRateTable
							initialize invoked.RateFactor
							initialize invoked.SpreadRate
							initialize invoked.RateFloor
							initialize invoked.RateCap
							initialize invoked.RateDependentOnRating
					
		DebtGroup
			required
			
		DebtType
			required
		
		TaxExempt
			initial value is "N"
			default to "N"
			
        Issuer
        	required
        	
        IssueDate
			required
			
        MaturityDate
        	required
        	constraint (MaturityDate >= IssueDate)
        		"MaturityDateMustAfterTheIssueDate"
        
        DebtCurrency
			initial value is CashManagementGroup.Currency
			default to CashManagementGroup.Currency
			if (DebtCurrency changed)
				initialize CorporateBaseCurrencyExchange
				initialize LocationCurrencyExchange
		
		DebtCalendar
  			initial value is CashManagementGroup.CorporateCalendar
  			default to CashManagementGroup.CorporateCalendar
  			required
  			
		DebtCurrencyExchange
			CurrencyTable								= CashManagementGroup.FinanceEnterpriseGroup.CurrencyTable
			DebtCurrencyExchange						= 1
			CorporateBaseCurrencyExchange.ToCurrency	= CashManagementGroup.Currency
			LocationCurrencyExchange.ToCurrency			= CashManagementLocation.Currency
		
		MultiplyBaseCurrencyRate
			if (CorporateBaseCurrencyExchange.EnteredCurrencyAmount	= CorporateBaseCurrencyExchange.EnteredCurrencyRate)
				MultiplyBaseCurrencyRate = true
			else
				MultiplyBaseCurrencyRate = false

		MultiplyLocationCurrencyRate
			if (LocationCurrencyExchange.EnteredCurrencyAmount = LocationCurrencyExchange.EnteredCurrencyRate)
				MultiplyLocationCurrencyRate = true
			else
				MultiplyLocationCurrencyRate = false
					
		CashManagementLocation
			required
		
		DebtReportGroup
			required
				
		LegalEntityGroup
			required
				"LegalEntityIsRequired"
		
		CallProvision
			if (!CallProvision)
				initialize CallProvisionDescription
		
		Active
			initial value is true
	
  	Conditions
  		DebtCurrencyEqualsCorporateBaseCurrency
  			restricted
			when (DebtCurrency = CashManagementGroup.Currency)
			
		DebtCurrencyNotCorporateBaseCurrency
			restricted
			when (DebtCurrency != CashManagementGroup.Currency)
			
		DebtCurrencyEqualsLocationCurrency
			restricted
			when (DebtCurrency = CashManagementLocation.Currency)
			
		DebtCurrencyNotLocationCurrency
			restricted
			when (DebtCurrency != CashManagementLocation.Currency)
		
		InstrumentSearchCondition
			restricted
            when (!MaturityDateRange entered
            or   (MaturityDateRange entered
            and   MaturityDate within MaturityDateRange))
		
		HasReportGroupRating
			restricted
			when (DebtReportGroupRatingsRel exists)
		
		HasSwapAttached
			restricted
			when (!DebtInstrumentType.Derivative
			and	  DebtInstrumentDerivative(DebtInstrument) set exists)
			
		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)
					
	Relations
  		DebtReportGroupRatingsRel
  			one-to-many relation to DebtReportGroupRating
  			Field Mapping uses part of key
  				related.CashManagementGroup	= CashManagementGroup
  				related.DebtReportGroup 	= DebtReportGroup
  			Instance Selection
  				where (related.IsLatestVersion)
  		
  		CreditSupportInstrumentRel 
  			one-to-many relation to DebtInstrument
  			Field Mapping uses ByDebtInstrumentType
  				related.CashManagementGroup = CashManagementGroup
  				related.DebtInstrumentType  = 3 
  		
  		DerivativeInstrumentRel 
  			one-to-many relation to DebtInstrument
  			Field Mapping uses ByDebtInstrumentType
  				related.CashManagementGroup = CashManagementGroup
  				related.DebtInstrumentType  = 2 
  		
  		FinancialInstitutionBranchRel 
			one-to-many relation to FinancialInstitutionBranch
			Field Mapping uses symbolic key
				related.CashManagementGroup = CashManagementGroup
			Instance Selection
				where (related.BranchType.Debt)
		
  		DebtFeeCodeRel 
  			one-to-many relation to DebtFeeCode
  			Field Mapping uses symbolic key
  				related.CashManagementGroup = CashManagementGroup
  		
  		DebtInstrumentIdentifierMaturityRel 
  			one-to-many relation to DebtInstrumentIdentifier
  			Field Mapping uses ByMaturityDate
  				related.CashManagementGroup	= CashManagementGroup
  				related.DebtInstrument		= DebtInstrument
  		
  		LineOfCreditCommittedRel
  			one-to-many relation to DebtInstrumentIdentifier
  			Field Mapping uses ByLineOfCredit
  				related.CashManagementGroup	= CashManagementGroup
  				related.LineOfCredit		= DebtInstrument
  				
  		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = LegalEntityGroup.LegalEntity
  										
  	Sets
  		ByDebtInstrumentType
  			Sort Order
  				CashManagementGroup
  				DebtInstrumentType
  				DebtInstrument
  				
  		ByDebtType
  			Sort Order
  				CashManagementGroup
  				DebtType
  				DebtInstrument
  	
  		ByCashForecastAccount
  			Sort Order
  				CashManagementGroup
  				CashForecastAccount
  				DebtInstrument
  						
  		ByCashTransactionCategory
  			Sort Order
  				CashManagementGroup
  				CashTransactionCategory
  				DebtInstrument
  							
  	Attach Rules
		constraint (Active)
			"DebtInstrumentIsInactive"
	
	Actions
		CopyDebtInstrument is a Create Action
			Parameters
				CashManagementGroup
				FromDebtInstrument	 	 	is a DebtInstrument
				NewDebtInstrument			is a DebtInstrument
				NewDescription			 	is Alpha up to 250
				
			Parameter Rules
    			FromDebtInstrument 	
    				required
    				constraint (FromDebtInstrument exists)
    					"DebtInstrument<FromDebtInstrument>DoesNotExist"
    					
				NewDebtInstrument
					required
					constraint (!NewDebtInstrument exists)
    					"DebtInstrument<NewDebtInstrument>AlreadyExists"
    					
				NewDescription
					initial value is FromDebtInstrument.Description
    				default to FromDebtInstrument.Description
    				
			Local Fields
				LocalDebtInstrumentIdentifier is a DebtInstrumentIdentifier view
							
    		Action Rules
				invoke New.Create this instance
					fill in fields from FromDebtInstrument
					invoked.CashManagementGroup	  = FromDebtInstrument.CashManagementGroup
					invoked.DebtInstrument	  	  = NewDebtInstrument
					invoked.Description		  	  = NewDescription
					initialize invoked.OutstandingTimestamp
					initialize invoked.AwaitingApprovalTimestamp
					initialize invoked.PaidTimestamp
					initialize invoked.RedeemedTimestamp
					initialize invoked.DefeasedTimestamp
				
				for each FromDebtInstrument.DebtInstrumentIdentifier(DebtInstrument) set
					invoke Create DebtInstrumentIdentifier set
						assign result to LocalDebtInstrumentIdentifier
						fill in fields from each
						invoked.CashManagementGroup		 = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument			 = NewDebtInstrument
						invoked.DebtInstrumentIdentifier = each.DebtInstrumentIdentifier
						
					for each each.DebtInstrumentIdentifierPrincipal set
						invoke Create LocalDebtInstrumentIdentifier.DebtInstrumentIdentifier.DebtInstrumentIdentifierPrincipal set
							fill in fields from each
							invoked.CashManagementGroup		  	 	  = FromDebtInstrument.CashManagementGroup
							invoked.DebtInstrument			 		  = NewDebtInstrument
							invoked.DebtInstrumentIdentifier 		  = each.DebtInstrumentIdentifier
							invoked.DebtInstrumentIdentifierPrincipal = each.DebtInstrumentIdentifierPrincipal
				
					for each each.DebtInstrumentIdentifierDistribution set
						invoke Create LocalDebtInstrumentIdentifier.DebtInstrumentIdentifier.DebtInstrumentIdentifierDistribution set
							fill in fields from each
							invoked.CashManagementGroup		 = FromDebtInstrument.CashManagementGroup
							invoked.DebtInstrument			 = NewDebtInstrument
							invoked.DebtInstrumentIdentifier = each.DebtInstrumentIdentifier
							
				for each FromDebtInstrument.DebtInstrumentDerivative(DebtInstrument) set
					invoke Create DebtInstrumentDerivative set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
				
				for each FromDebtInstrument.DebtInstrumentCreditSupport(DebtInstrument) set
					invoke Create DebtInstrumentCreditSupport set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
						
				for each FromDebtInstrument.DebtInstrumentFee set
					invoke Create DebtInstrumentFee set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
						invoked.DebtFeeCode    		= each.DebtFeeCode
						
				for each FromDebtInstrument.DebtInstrumentCounterparty set
					invoke Create DebtInstrumentCounterparty set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
						
				for each FromDebtInstrument.DebtInstrumentRating set
					invoke Create DebtInstrumentRating set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
						
				for each FromDebtInstrument.DebtInstrumentDocument set
					invoke Create DebtInstrumentDocument set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
				
				for each FromDebtInstrument.DebtInstrumentComment set
					invoke Create DebtInstrumentComment set
						fill in fields from each
						invoked.CashManagementGroup = FromDebtInstrument.CashManagementGroup
						invoked.DebtInstrument 		= NewDebtInstrument
						
		Purge is a Purge Action
			restricted
					
	StateCycles
		DebtInstrumentLifeCycle is a StateCycle
			state field is DebtStatus
		
			New is a State
				Create is a Create Action
					Action Rules
						DebtInstrumentType = DebtInstrumentType.DebtInstrument
						
				CreateDerivative is a Create Action
					Action Rules
						DebtInstrumentType = DebtInstrumentType.Derivative
		
				CreateCreditSupport is a Create Action
					Action Rules
						DebtInstrumentType = DebtInstrumentType.CreditSupport
						
				CreateLineOfCredit is a Create Action
					Action Rules
						DebtInstrumentType = DebtInstrumentType.LineOfCredit
						
				Update is an Update Action
				
				Release is an Instance Action
					Action Rules
						make transition to Outstanding
							
				AddCreditSupport is an Instance Action 
					Parameters
						PrmCashManagementGroup		is a CashManagementGroup
						PrmDebtInstrument			is a DebtInstrument
						
					Parameter Rules
						PrmCashManagementGroup		required
						PrmDebtInstrument			required
							
					Action Rules
						invoke Create DebtInstrumentCreditSupport
							invoked.CashManagementGroup		= PrmCashManagementGroup
							invoked.DebtInstrument			= PrmDebtInstrument
							invoked.CreditSupportInstrument	= DebtInstrument
				
				AddDerivative is an Instance Action 
					Parameters
						PrmCashManagementGroup		is a CashManagementGroup
						PrmDebtInstrument			is a DebtInstrument
						
					Parameter Rules
						PrmCashManagementGroup		required
						PrmDebtInstrument			required
							
					Action Rules
						invoke Create DebtInstrumentDerivative
							invoked.CashManagementGroup	 = PrmCashManagementGroup
							invoked.DebtInstrument		 = PrmDebtInstrument
							invoked.DerivativeInstrument = DebtInstrument
											
				Delete is a Delete Action
				
			AwaitingApproval is a State
				on entrance to AwaitingApproval
					Action Rules
						if (!CashManagementGroup.DebtInstrumentApprovalRequired)
							invoke Approve
						else
						    AwaitingApprovalTimestamp = current timestamp
						    
						    initiate DebtInstrumentApproval process
								title is "ReviewDebtInstrument<DebtInstrument>"
        						Variables
									CashManagementGroup
									DebtInstrument
								URLs
									"<linkback(webapp is TreasuryManager navigation is DebtInstrumentNavigation text is \"ViewDebtInstrument\")>"
							
							send email
								to		actor.agent(Employee).EmployeeWorkEmailAddress 
								from 	config.DefaultFromEmailAddress
								subject "DebtInstrument<DebtInstrument>HasBeenSubmittedForYourApproval"
								Contents
										"DebtInstrument<DebtInstrument>HasBeenSubmittedForYourApproval"
											
				Approve is an Instance Action
					restricted
					Action Rules
						make transition to Outstanding
											
				Reject is an Instance Action
					restricted
					Action Rules
						make transition to New
				
				ManualApprove is an Instance Action
					confirmation required
						"ThisWillBypassTheProcessFlowApprovalProcess;DoYouWantToContinue?"
						
					Action Rules
						make transition to Outstanding
			
						cancel DebtInstrumentApproval process
								
				ManualReject is an Instance Action
					subject is RejectDebtInstrument
					
					Action Rules
						make transition to New
						
						initialize AwaitingApprovalTimestamp
						
						cancel DebtInstrumentApproval process
								
			Outstanding is a State
				on entrance to Outstanding
					Action Rules
						OutstandingTimestamp = current timestamp
				
				Paid is an Instance Action
					Action Rules
						make transition to Paid
				
				Redeem is an Instance Action
					Action Rules
						make transition to Redeemed
								
			Paid is a State
				on entrance to Paid
					Action Rules
						PaidTimestamp = current timestamp
				
				Outstanding is an Instance Action
					Action Rules
						make transition to Outstanding
						
				Redeem is an Instance Action
					Action Rules
						make transition to Redeemed
								
			Redeemed is a State
				on entrance to Redeemed
					Action Rules
						RedeemedTimestamp = current timestamp
				
				Outstanding is an Instance Action
					Action Rules
						make transition to Outstanding
				
				Paid is an Instance Action
					Action Rules
						make transition to Paid
								
				Defease is an Instance Action
					Action Rules
						make transition to Defeased
						
			Defeased is a State
				on entrance to Defeased
					Action Rules
						DefeasedTimestamp = current timestamp
