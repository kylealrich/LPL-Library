PlatformComponent is a BusinessClass
    owned by la
    prefix is VERS
    store using com.lawson.rdtech.framework.PlatformComponentStorage
    
    Ontology
        symbolic key is PlatformComponent

    Patterns
        implements CRUD   

    Persistent Fields
    	Version					is Alpha 200














	Derived Fields
		DerivedSubject is a MessageField
			restricted
  			"VersionInformation_for_\"<PlatformComponent>\""
  			
		DerivedBody is a MessageField
			restricted
  			"<PlatformComponent>_version_as_of_<system current timestamp>_was_<Version>"

        DerivedFromAddress is a DerivedField
            type is EmailAddressField
            if (actor.ContactInfo.EmailAddress entered)
                return actor.ContactInfo.EmailAddress
            else
                return actor + "emailunkown@" + parentcontext.dataarea + ".org"

   	Field Rules
   		Version
   			required





			
	Conditions
	    ActorIsAnonymous
	        when (actor = "Anonymous")











	Actions	








		EmailVersion is an Instance Action
			completion message is "EmailSent"
			Parameters
				To			is a MultipleEmailAddress
				CC			is a MultipleEmailAddress

				Subject		is Alpha 255
				Body		is Text
			Parameter Rules
				To
					required



				Subject
					if (!Body entered)
						required
					initial value is DerivedSubject
				Body
					if (!Subject entered)
						required
					initial value is DerivedBody
			Action Rules
				send email
					to		To
					from	DerivedFromAddress
					cc		CC
					subject "<Subject>"
					Contents
						"<Body>"

		EmailVersions is a Set Action
			completion message is "EmailSent"
			run in foreground 
			
			Parameters
				To			is a MultipleEmailAddress
				CC			is a MultipleEmailAddress


			Local Fields
				BodyText is Text
								
			Parameter Rules
				To
					required


				


				
			Action Rules
				Instance Rules
					BodyText += DerivedBody + "<P>"
					
				Set Rules
				    Exit Rules
		  			    send email
							to		To
							from	DerivedFromAddress
							cc		CC
							subject "VersionInformation"
							Contents
								"<BodyText>"
