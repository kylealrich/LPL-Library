ReceivableElectronicFundsTransferNotify is a BusinessClass
    owned by ar
    prefix is FTN
    sql name is RElectronicFundsTransferNotify
    classic name is FTCUSTNOTE

    Ontology
        symbolic key is ReceivableElectronicFundsTransferNotify
            classic set name is FTNSET1


    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        EFTPrenotification              is AlphaUpper size 1
            classic name is PRE-NOTE
            States
            	Exempt			value is "E"
                No          	value is "N"
                Yes          	value is "Y"
        EFTNotificationMethod           is AlphaUpper size 1
            classic name is NOTE-METH
            States
                AlternateEdi value is "A"
                    default label is "Alternate EDI"
                Edi          value is "E"
                    default label is "EDI"
                Fax          value is "F"
        EFTAdjustmentNotificationMethod is an AdjNoteMeth
            sql name is EFTANotificationMethod
            classic name is ADJ-NOTE-METH
        User1Field
            classic name is USER1-FIELD
        User2Field
            classic name is USER2-FIELD
        PaymentFormat
		HoldAllProcessing 				is Boolean

	Field Rules
		EFTNotificationMethod	
			required
		EFTAdjustmentNotificationMethod			
			required			
		PaymentFormat
			required			


	Conditions
		CustomerOpenEFTTransactionsExist
        	restricted
			when (CustomerRel.OpenElectronicFundsTransferTransactionsRel exists)

		CompanyCustomerOpenEFTTransactionsExist
        	restricted
			when (CompanyCustomerRel.OpenElectronicFundsTransferTransactionsRel exists)	

	Relations
		CustomerRel
            one-to-one relation to Customer
            Field Mapping uses symbolic key
                related.CustomerGroup  = CustomerGroup
                related.Customer = Customer
		
		CompanyCustomerRel
            one-to-one relation to CompanyCustomer
            Field Mapping uses symbolic key
                related.Company  	= Company
                related.Customer	= Customer

        EFTCustomerGroupsRel
        	one-to-many relation to ElectronicFundsTransferCustomerGroup
        	Field Mapping uses Set2
        		related.ElectronicFundsTransferCustomerGroup.Customer	= Customer
        		related.ElectronicFundsTransferCustomerGroup.Company	= Company
				related.CustomerGroup 									= CustomerGroup		

	Actions
        Create is a Create Action
            Entrance Rules
        		constraint (!EFTCustomerGroupsRel exists)       
        			"CannotEnterData;CustomerPartOfEFTGroup"

        Update is an Update Action
		Delete is a Delete Action
			Action Rules
				constraint (!CustomerOpenEFTTransactionsExist)
					"CannotDelete;TransactionsForElectronicFundsProcessingExist"

				if (!CustomerOpenEFTTransactionsExist)
   					confirmation required
				
				constraint (!CompanyCustomerOpenEFTTransactionsExist)
   					"CannotDelete;TransactionsForElectronicFundsProcessingExist"

				if (!CompanyCustomerOpenEFTTransactionsExist)
					confirmation required

		
