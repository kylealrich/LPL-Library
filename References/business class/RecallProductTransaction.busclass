RecallProductTransaction is a BusinessClass
    sql name is RecallProductTrans
    owned by recall
    prefix is RMRPA

    Ontology
    	symbolic key is RecallProductTransaction
    
    Patterns
    	disable Auditing		
        disable EffectiveDated	
        implements ContextualParent
    	implements CompoundDocument	
        	Document Components
				RecallProductTransactionTask set
					
    Persistent Fields
    	TransactionDate					is Date
   		ActivityType					is Numeric size 2
   			States
   				RequisitionLines			value is 1
   				ProcurementTemplateLines	value is 2
   				PurchaseOrderLines			value is 4
   				Inventory					value is 5
   				ParLocations				value is 6
   				IntransitTransfers			value is 7
   				Issues						value is 8
   				Equipment					value is 9
   				Itserial					value is 10
   				StockOnHandDetail			value is 11
   				SourcingEventLines			value is 12
   				ContractLines				value is 13
   				Polinesrc					value is 14
   				IssueDetail					value is 15
   				IntransitTransferDetail		value is 16
   				PatientProcedure            value is 17
   		RequestingLocationGroup
    	InventoryLocationGroup
   		RequisitionLineGroup
		ProcurementTemplateLineGroup		
		ContractLineGroup
		SourcingEventLineGroup
		PurchaseOrderLineGroup
		InventoryParGroup
		InventoryTransactionGroup
		AssetItemGroup
		ItemSerialGroup
		StockOnHandDetailGroup
		PurchaseOrderLineSourceGroup
		InventoryTransactionLineDetailGroup
		PatientProcedureGroup 
		UndefinedLocationInOrganization	is Boolean		
   		
   	Local Fields
   		LocalRecallUser 				is a RecallUser
   		LocalRecallNoticeTask			is a RecallNoticeTask 
   		LocalRequestingCompany			is like Company 
   		LocalRequestingLocation			is like RequestingLocation
   		LocalInventoryLocation			is like InventoryLocation
   		LocalOrganization               is a RecallOrganization
   		LocalPriority					is Numeric size 2
   			States
				High						value is 1
				Medium						value is 2
				Low							value is 3
		LocalDueDate					is TimeStamp
		TempPO							is AlphaUpper size 14
		LocalInstructions               is a RecallTaskInstruction
		LocalCreateTasks                is Boolean
				
   	Derived Fields
		FormattedTransDate is a StringField
			type is Alpha 10
			restricted
			TransactionDate month
			"/"
			TransactionDate day
			"/"
			TransactionDate year
   			
   		RemoveHoldMessage is a MessageField
   			restricted
   			"RemoveHold"
   			
   		PlaceOnHoldMessage is a MessageField
   			restricted
   			"PlaceOnHold"
   		
   		ContractHoldButton is a ConditionalField
   			type is Text
   			restricted
   			if (ContractLineGroup.ContractLine.OnHold)
   				RemoveHoldMessage
   			else
   				PlaceOnHoldMessage
   		
		DerivedPOLinePurchOrd is a DerivedField
			type is Alpha size up to 24
			return com.lawson.apps.procurement.base.StringUtils.trimLeadingSpaces(PurchaseOrderLineGroup.PurchaseOrder.DisplayFullPurchaseOrder)

		DerivedNotifyEmailAddress is a DerivedField
			type is Alpha size 60
			restricted
			if (ActivityType.RequisitionLines
			or  ActivityType.Polinesrc
			or (ActivityType.PatientProcedure
   			and RequisitionLineGroup entered))
				return RequisitionLineGroup.RequisitionLine.Requisition.Requester.EmailAddress
			else
			if (ActivityType.PurchaseOrderLines
			or (ActivityType.PatientProcedure
   			and RequisitionLineGroup !entered))
				return PurchaseOrderLineGroup.PurchaseOrder.Buyer.EmailAddress
			else
			if (ActivityType.SourcingEventLines)
				return SourcingEventLineGroup.SourcingEvent.Buyer.EmailAddress
			else
			if (ActivityType.ContractLines)
				return ContractLineGroup.Contract.PrimaryContactRel.DerivedEmail

		DerivedEmailNotifyLine is a ConditionalField
			type is Text
			restricted
			if (ActivityType.RequisitionLines
			or  ActivityType.Polinesrc
			or (ActivityType.PatientProcedure
   			and RequisitionLineGroup entered))
				RequisitionDetail
			else
			if (ActivityType.PurchaseOrderLines
			or (ActivityType.PatientProcedure
   			and RequisitionLineGroup !entered))
				PODetail
			else
			if (ActivityType.SourcingEventLines)
				SourcingEventDetail
			else
			if (ActivityType.ContractLines)
				ContractDetail
			else
			if (ActivityType.ProcurementTemplateLines)
				TemplateDetail
			else
				"Detail Not Found For" + ActivityType
			
		RequisitionMessage is a MessageField
			restricted
			"__Requisition:_"
			
		LineMessage is a MessageField
			restricted
			"__Line:_"
			
		ItemMessage is a MessageField
			restricted
			"__Item:_"
			
		CompanyMessage is a MessageField
			restricted
			"__Company:_"
		
		RequisitionDetail is a StringField
			type is Text
			RequisitionMessage
			RequisitionLineGroup.Requisition
			LineMessage
			RequisitionLineGroup.RequisitionLine
			ItemMessage 
			RequisitionLineGroup.RequisitionLine.Item
			CompanyMessage
			RequisitionLineGroup.InventoryCompany
						
		PurchaseOrderMessage is a MessageField
			restricted
			"__PurchaseOrder:_"
		
		PODetail is a StringField
			type is Text
			restricted
			PurchaseOrderMessage
			DerivedPOLinePurchOrd
			LineMessage
    		PurchaseOrderLineRel.PurchaseOrderLine
			ItemMessage 
    		PurchaseOrderLineRel.Item
			CompanyMessage
			PurchaseOrderLineGroup.PurchasingCompany

		TemplateMessage is a MessageField
			restricted
			"Template:"
		
		TemplateDetail is a StringField
			type is Text
			restricted
			TemplateMessage
			ProcurementTemplateLineGroup.ProcurementTemplate
			LineMessage
			ProcurementTemplateLineGroup.ProcurementTemplateLine
			ItemMessage
			ProcurementTemplateLineGroup.ProcurementTemplateLine.Item
				
		ContractMessage is a MessageField
			restricted
			"Contract:"
		
		ContractDetail is a StringField
			type is Text
			restricted
			ContractMessage
			ContractLineGroup.Contract
			LineMessage
			ContractLineGroup.ContractLine
			ItemMessage
			ContractLineGroup.ContractLine.ItemNumber
							
		SourcingEventMessage is a MessageField
			restricted
			"SourcingEvent"
	
		NameMessage is a MessageField
			restricted
			"__Name:"
		
		SourcingEventDetail is a StringField
			type is Text
			restricted
			SourcingEventMessage  
			SourcingEventLineGroup.SourcingEvent
			LineMessage
			SourcingEventLineGroup.SourcingEventLine
			NameMessage
			SourcingEventLineGroup.SourcingEventLine.Name
			ItemMessage
			SourcingEventLineGroup.SourcingEventLine.ItemNumber
			
		DerivedTransferToLocation is a DerivedField
			type is AlphaUpper size 5
			if (InventoryTransactionGroup.InventoryTransaction.InventoryDocumentType = "IT")
				return (InventoryTransactionGroup.InventoryTransaction.FromToCompanyLocation.FromToLocation)
			else
				return (InventoryTransactionGroup.Location)
				
		DerivedTransferFromLocation is a DerivedField
			type is AlphaUpper size 5
			if (InventoryTransactionGroup.InventoryTransaction.InventoryDocumentType = "IR")
				return (InventoryTransactionGroup.InventoryTransaction.FromToCompanyLocation.FromToLocation)
			else
				return (InventoryTransactionGroup.Location)
				


		MobileItemDescriptionLabel is a LabelField
			"<PurchaseOrderLineRel.Item>_-_<PurchaseOrderLineRel.Description>"

		MobileRemainingQuantityUOMLabel is a LabelField
			"<PurchaseOrderLineRel.RemainingQuantity>_<PurchaseOrderLineRel.EnteredBuyUOM>"
		
		MobileReceivedQuantityUOMLabel is a LabelField
			"<PurchaseOrderLineRel.ReceivedQuantity>_<PurchaseOrderLineRel.EnteredBuyUOM>"
		

		MobileItemLabel is a LabelField
			"Item:<InventoryParGroup.ItemLocation.Item>"
			
		MobileItemLocationLabel is a LabelField
			"<InventoryParGroup.Location>_-_<InventoryParGroup.Location.Name>"
		

		MobileStockOnHandItemLabel is a LabelField
			"Item:<StockOnHandDetailGroup.Item>"
			
		MobileStockOnHandCompanyLabel is a LabelField
			"<StockOnHandDetailGroup.InventoryCompany>_-_<StockOnHandDetailGroup.InventoryCompany.Name>"
			
		MobileStockOnHandLocationLabel is a LabelField
			"<StockOnHandDetailGroup.Location>_-_<StockOnHandDetailGroup.Location.Name>"
			
		MobileStockOnHandLabel is a LabelField
			"<StockOnHandDetailGroup.StockOnHandDetail.StockOnHandQuantity>_<StockOnHandDetailGroup.StockOnHandDetail.UnitOfMeasure>"
			

		MobileItemSerialLabel is a LabelField
			"Item:<ItemSerialGroup.Item>"

		MobileItemSerialCompanyLabel is a LabelField
			"<ItemSerialGroup.InventoryCompany>_-_<ItemSerialGroup.InventoryCompany.Name>"
			
		MobileItemSerialLocationLabel is a LabelField
			"<ItemSerialGroup.Location>_-_<ItemSerialGroup.Location.Name>"
			


		MobileQuantityUOMLabel is a LabelField
			"<InventoryTransactionGroup.InventoryTransactionLine.Quantity>_<InventoryTransactionGroup.InventoryTransactionLine.TransactionUOM>"

		MobileTransactionItemLabel is a LabelField
			"Item:<InventoryTransactionLineDetailGroup.InventoryTransactionLine.Item>"
			
		MobileTransactionFromLocationLabel is a LabelField
			"<InventoryTransactionLineDetailGroup.DtlCompany>_-_<InventoryTransactionLineDetailGroup.DtlLocation>"
			
		MobileTransactionToLocation is a LabelField
			"<InventoryTransactionLineDetailGroup.InventoryTransaction.FromToCompanyLocation.FromToCompany>_-_<InventoryTransactionLineDetailGroup.InventoryTransaction.FromToCompanyLocation.FromToLocation>"
			
		MobileIssueQuantityLabel is a LabelField
			"<InventoryTransactionLineDetailGroup.InventoryTransactionLineDetail.IssueQuantity>_<InventoryTransactionLineDetailGroup.InventoryTransactionLineDetail.UnitOfMeasure>"
			
		MobileIntransitTransferDetailQuantity is a LabelField
			"<InventoryTransactionLineDetailGroup.InventoryTransactionLineDetail.OriginalQuantity>_<InventoryTransactionLineDetailGroup.InventoryTransactionLineDetail.UnitOfMeasure>"
			

		MobileIntransitTransferItemLabel is a LabelField
			"Item:<InventoryTransactionGroup.InventoryTransactionLine.Item>"
		
		MobileIntransitTransferFromLocationlabel is a LabelField
			"<InventoryTransactionGroup.InventoryCompany>_-_FromLocation:<DerivedTransferFromLocation>"
				
		MobileIntransitTransferToLocationlabel is a LabelField
			"<InventoryTransactionGroup.InventoryCompany>_-_ToLocation:<DerivedTransferToLocation>"
				
		MobileIntransitTransferQuantityLabel is a LabelField
			"<InventoryTransactionGroup.InventoryTransactionLine.DerivedIntransitQty>_<InventoryTransactionGroup.InventoryTransactionLine.TransactionUOM>"
		

		MobileContractLineItemLabel is a LabelField
			"Item:<ContractLineGroup.ContractLine.ItemNumber>"
		
		MobileBaseCostUOMLabel is a LabelField
			"<ContractLineGroup.ContractLine.BaseCost>_<ContractLineGroup.ContractLine.UOM.UnitOfMeasure>"
			

		MobileSourcingEventLitemLabel is a LabelField
			"Item:<SourcingEventLineGroup.SourcingEventLine.ItemNumber>"
			

		MobileProcurementTemplateItemLabel is a LabelField
			"Item:<ProcurementTemplateLineGroup.ProcurementTemplateLine.Item>"
		
		MobileProcurementTemplateQuantityLabel is a LabelField
			"<ProcurementTemplateLineGroup.ProcurementTemplateLine.Quantity>_<ProcurementTemplateLineGroup.ProcurementTemplateLine.UnitOfMeasure>"
			

		MobilePatientNameLabel is a LabelField
			"<PatientProcedureGroup.PatientProcedure.PatientLastName>,_<PatientProcedureGroup.PatientProcedure.PatientFirstName>"
			

		MobilePurchaseOrderLineItemLabel is a LabelField
			"Item:<PurchaseOrderLineRel.Item>"
		
		MobilePurchaseOrderLineCompanyLabel is a LabelField
			"<PurchaseOrderLineGroup.PurchasingCompany>_-_<PurchaseOrderLineGroup.PurchasingCompany.Name>"
		
		MobilePurchaseOrderLineLocationLabel is a LabelField
			"<PurchaseOrderLineRel.ShipToLocation>_-_<PurchaseOrderLineRel.ShipToLocation.Name>"
		
		MobilePurchaseOrderLineRemainingLabel is a LabelField
			"<PurchaseOrderLineRel.RemainingQuantity>_<PurchaseOrderLineRel.EnteredBuyUOM>"
			
		MobilePurchaseOrderLineReceivedLabel is a LabelField
			"<PurchaseOrderLineRel.ReceivedQuantity>_<PurchaseOrderLineRel.EnteredBuyUOM>"
			

		MobileRequisitionLineItemLabel is a LabelField
			"Item:<RequisitionLineGroup.RequisitionLine.Item>"
			

				
   	Conditions
   		IsARequestingLocationActivity
   			restricted
   			when (ActivityType.RequisitionLines
   			or	  ActivityType.Issues
   			or	  ActivityType.IssueDetail
   			or    ActivityType.Polinesrc
   			or   (ActivityType.PatientProcedure
   			and   RequisitionLineGroup entered))
   			
   		IsAnInventoryLocationActivity
   			restricted
   			when (ActivityType.Inventory
   			or	  ActivityType.ParLocations
   			or	  ActivityType.IntransitTransfers
   			or	  ActivityType.IntransitTransferDetail
   			or	  ActivityType.PurchaseOrderLines
   			or	  ActivityType.Itserial
   			or	  ActivityType.StockOnHandDetail
   			or   (ActivityType.PatientProcedure
   			and   RequisitionLineGroup !entered))	
   		
   		NonLocationActivity
   			restricted
   			when (ActivityType.ProcurementTemplateLines
   			or	  ActivityType.ContractLines
   			or 	  ActivityType.SourcingEventLines
   			or	  ActivityType.Equipment)
   			
   		PurchaseOrderLinesFromRq
   			restricted
   			when (ActivityType.PurchaseOrderLines
   			and	  PolinesrcRqlocRel exists)
   		
   		PurchaseOrderLinesNotFromRq
   			restricted
   			when (ActivityType.PurchaseOrderLines
   			and	  !PolinesrcRqlocRel exists)
   			
		OpenPOReceiving
			restricted
			when (OpenPurchaseOrderLineRel exists)
		
		CanDelete 
			restricted 
			when (RecallNotice.Status.Pending)
		
		NotifyOnRelease
			restricted
			when ((ActivityType.RequisitionLines
			and    RecallGroup.SendRequesterNoticeEmail)
			or    (ActivityType.Polinesrc
			and    RecallGroup.SendRequesterNoticeEmail)
			or    ((ActivityType.PurchaseOrderLines
			or      ActivityType.Polinesrc)
			and    OpenPOReceiving
			and    RecallGroup.SendPOBuyerNoticeEmail)
			or    (ActivityType.SourcingEventLines
			and    RecallGroup.SendEventBuyerNoticeEmail)
			or    (ActivityType.ContractLines
			and    RecallGroup.SendContractNoticeEmail))
			
		InventoryOrganizationExists
			restricted
			when (OrganizationInventoryLocationRel exists)

		ShipmentRecordsExist
			restricted
			when (WarehouseShipmentLinesRel exists)				
			
   	Relations
   		PurchaseOrderLineRel
   			one-to-one relation to PurchaseOrderLine
   			Field Mapping uses symbolic key
   				related.Company						= PurchaseOrderLineGroup.PurchasingCompany
    			related.PurchaseOrder				= PurchaseOrderLineGroup.PurchaseOrder
    			related.PurchaseOrderLine			= PurchaseOrderLineGroup.PurchaseOrderLine
    	
   		OpenPurchaseOrderLineRel
   			one-to-many relation to PurchaseOrderLine
   			Field Mapping uses symbolic key
   				related.Company						= PurchaseOrderLineGroup.PurchasingCompany
    			related.PurchaseOrder				= PurchaseOrderLineGroup.PurchaseOrder
    			related.PurchaseOrderLine			= PurchaseOrderLineGroup.PurchaseOrderLine
    		Instance Selection
    			where (!related.IsOpenForReceiving)
   		
   		ReqLineRel
   			one-to-one relation to RequisitionLine
   			Field Mapping uses symbolic key
				related.Company				= RequisitionLineGroup.InventoryCompany 
				related.Requisition			= RequisitionLineGroup.Requisition
				related.RequisitionLine		= RequisitionLineGroup.RequisitionLine

   		PolinesrcReqlineRel
   			one-to-one relation to PurchaseOrderLineSource
   			Field Mapping uses Set2
				related.PurchaseOrderLineSource.OperatingCompany		= RequisitionLineGroup.InventoryCompany
				related.PurchaseOrderLineSource.SourceDocumentOrigin	= "RQ" 
				related.PurchaseOrderLineSource.SourceDocumentNumeric	= RequisitionLineGroup.Requisition
				related.PurchaseOrderLineSource.SourceDocumentLineNumber	= RequisitionLineGroup.RequisitionLine
    			related.PurchaseOrder									= PurchaseOrderLineGroup.PurchaseOrder
    			related.PurchaseOrderLine								= PurchaseOrderLineGroup.PurchaseOrderLine

		PolinesrcRel
   			one-to-one relation to PurchaseOrderLineSource
   			Field Mapping uses symbolic key
				related.Company			        	  							= PurchaseOrderLineSourceGroup.PurchasingCompany
		     	related.PurchaseOrder											= PurchaseOrderLineSourceGroup.PoNumber
		     	related.PurchaseOrderLine										= PurchaseOrderLineSourceGroup.LineNbr
	        	related.PurchaseOrderLineSource.SourceDocumentOrigin			= PurchaseOrderLineSourceGroup.OriginCd
	        	related.PurchaseOrderLineSource.OperatingCompany				= PurchaseOrderLineSourceGroup.OperCompany
	        	related.PurchaseOrderLineSource.SourceDocumentNumeric			= PurchaseOrderLineSourceGroup.SourceDocN
	        	related.PurchaseOrderLineSource.SourceDocumentAlpha				= PurchaseOrderLineSourceGroup.SourceDocA
	        	related.PurchaseOrderLineSource.SourceDocumentLineNumber		= PurchaseOrderLineSourceGroup.SrcLineNbr

		IssuesRel    	
   			one-to-one relation to InventoryTransactionLine
   			Field Mapping uses Set1
				related.Company												= InventoryTransactionGroup.InventoryCompany
				related.InventoryLocation									= InventoryTransactionGroup.Location 
				related.TransactionSystemCode								= InventoryTransactionGroup.TransactionSystemCode
				related.InventoryTransaction								= InventoryTransactionGroup.InventoryTransaction
				related.InventoryTransactionLine.WarehouseShipment			= InventoryTransactionGroup.InventoryTransactionLine.WarehouseShipment
				related.InventoryTransactionLine.LineNumber					= InventoryTransactionGroup.InventoryTransactionLine.LineNumber
				related.InventoryTransactionLine.ComponentSequence			= InventoryTransactionGroup.InventoryTransactionLine.ComponentSequence
    	
    	BinGroupDetailRel
    		one-to-many relation to BinGroupDetail
    		Field Mapping uses Set2
    			related.Company				= InventoryParGroup.InventoryCompany
    			related.InventoryLocation	= InventoryParGroup.Location
    			related.BinGroup			= InventoryParGroup.Location
		     	related.Bin					= InventoryParGroup.ItemLocation.PreferredBin
    		Instance Selection
    			where (ActivityType.ParLocations)
    	
    	RecallNoticeTaskRel
    		one-to-one relation to RecallNoticeTask
    		Field Mapping uses ByOwner
				related.RecallGroup		= RecallGroup
				related.RecallNotice	= RecallNotice
				related.Owner			= LocalRecallUser
    	
    	PolinesrcRqlocRel
			one-to-many relation to PurchaseOrderLineSource
			Field Mapping uses symbolic key
				related.Company					= PurchaseOrderLineRel.Company
				related.PurchaseOrder			= PurchaseOrderLineRel.PurchaseOrder
				related.PurchaseOrderLine		= PurchaseOrderLineRel.PurchaseOrderLine
			Instance Selection
				where (related.PurchaseOrderLineSource.SourceDocumentOrigin = "PO"
				or	   related.PurchaseOrderLineSource.SourceDocumentOrigin = "RQ")
				
		OrganizationRequestingLocationRel
			one-to-many relation to RecallOrganization
			Field Mapping uses ByRequestingLocation
				related.InventoryCompany	= LocalRequestingCompany
				related.RequestingLocation  = LocalRequestingLocation
			Instance Selection
				where (related.Active)
		
		OrganizationInventoryLocationRel
			one-to-many relation to RecallOrganization
			Field Mapping uses ByInventoryLocation
				related.InventoryCompany	= InventoryLocationGroup.InventoryCompany
				related.InventoryLocation   = InventoryLocationGroup.InventoryLocation
			Instance Selection
				where (related.Active)
		
		RecallNoticeOrganizationRel
			one-to-one relation to RecallNoticeOrganization
			Field Mapping uses symbolic key
				related.RecallGroup              = RecallGroup
				related.RecallNotice             = RecallNotice
				related.RecallOrganization       = LocalOrganization
		
		RequestingLocationRel
       		one-to-one relation to RequestingLocation
       		Field Mapping uses symbolic key
       			related.Company			     = LocalRequestingCompany
       			related.RequestingLocation   = LocalRequestingLocation	

		LocalOrganizationInventoryLocationRel
			one-to-many relation to RecallOrganization
			Field Mapping uses ByInventoryLocation
				related.InventoryCompany	= LocalRequestingCompany
				related.InventoryLocation   = LocalInventoryLocation
			Instance Selection
				where (related.Active)
		
		LocalInventoryLocationRel
			one-to-one relation to InventoryLocation
			Field Mapping uses symbolic key
				related.Company    = LocalRequestingCompany
				related.InventoryLocation = LocalInventoryLocation

		WarehouseShipmentLinesRel
			one-to-many relation to WarehouseShipmentLine
			Field Mapping uses Set2
				related.Company                             = RequisitionLineGroup.RequisitionLine.FromCompanyLocationBin.FromCompany
				related.RequestingDocument.DemandSystemCode = "RQ"
				related.RequestingDocument.DocumentNumber   = RequisitionLineGroup.Requisition
				related.WarehouseShipmentLine               = RequisitionLineGroup.RequisitionLine
			Instance Selection
				where (related.RequestingDocument.RequestingCompany = RequisitionLineGroup.RequisitionLine.Company) 								

   	Sets
   		ByActivityType
   			indexed
   			Sort Order
   				ActivityType
   				RecallGroup
   				RecallNotice
   				RecallProduct
   				RecallProductTransaction
   	
   		ByTransactionDate
   			indexed
   			Sort Order
   				RecallGroup
   				RecallNotice
   				TransactionDate descending
   				RecallProduct
   				RecallProductTransaction
   	
   	Rule Blocks
   		CreateRecallNoticeTaskWithOwner
   			invoke Update New RecallNoticeTaskRel
				assign result to LocalRecallNoticeTask
				invoked.Owner 						  = LocalRecallUser
				invoked.LocalRecallProduct 			  = RecallProduct
				invoked.LocalRecallProductTransaction = RecallProductTransaction
				invoked.Priority					  = LocalPriority
				invoked.DueDate						  = LocalDueDate
				invoked.LocalInstructions             = LocalInstructions
				invoked.FromProductTransactionQuery	  = true
										
   		CreateRecallNoticeTaskWithoutOwner
   			invoke Update New RecallNoticeTaskRel
				assign result to LocalRecallNoticeTask
				invoked.LocalRecallProduct 			  = RecallProduct
				invoked.LocalRecallProductTransaction = RecallProductTransaction
				invoked.Priority					  = LocalPriority
				invoked.DueDate						  = LocalDueDate
				invoked.LocalInstructions             = LocalInstructions
				invoked.FromProductTransactionQuery	  = true
				invoked.TaskForUndefinedLocations 	  = true
				
		CreateRecallNoticeOrganization
			invoke Update RecallNoticeOrganizationRel
				invoked.AssignedUser				= LocalRecallUser
				invoked.FromProductTransactionQuery = true
				
		AttachRequestingOrganization
			if (!OrganizationRequestingLocationRel exists)
				LocalInventoryLocation = LocalRequestingLocation
				
				invoke Create RecallOrganization
					resume on error
					invoked.RecallGroup				= RecallGroup
					invoked.InventoryCompany		= LocalRequestingCompany
					invoked.RequestingLocation  	= LocalRequestingLocation
					invoked.IssueAccountingUnit		= RequestingLocationRel.IssueAccount.AccountingUnit
					invoked.Active			    	= true
					if  (!LocalOrganizationInventoryLocationRel exists
					and   LocalInventoryLocationRel exists)
						invoked.InventoryLocation	= LocalInventoryLocation
				include NotifyCoordinatorOrganizationAdded

			if (OrganizationRequestingLocationRel exists)
				for each OrganizationRequestingLocationRel
					LocalOrganization = each.RecallOrganization
					include CreateRecallNoticeOrganization

				if (RecallNotice.SavedTransactionQueryParameters.IncludeWatchers)
					for each OrganizationRequestingLocationRel
		   				invoke AttachWatchersToNotice each
							invoked.LocalRecallNotice	= RecallNotice

		AttachInventoryOrganization
			LocalRequestingCompany	= InventoryLocationGroup.InventoryCompany
			LocalRequestingLocation = InventoryLocationGroup.InventoryLocation
			
			if (!OrganizationInventoryLocationRel exists)
				invoke Create RecallOrganization
					resume on error
					invoked.RecallGroup				= RecallGroup
					invoked.InventoryCompany		= InventoryLocationGroup.InventoryCompany
					invoked.InventoryLocation   	= InventoryLocationGroup.InventoryLocation
					invoked.Active			    	= true
					if  (!OrganizationRequestingLocationRel exists
					and   RequestingLocationRel exists)
						invoked.RequestingLocation	= LocalRequestingLocation
						invoked.IssueAccountingUnit	= RequestingLocationRel.IssueAccount.AccountingUnit
				include NotifyCoordinatorOrganizationAdded
				
			if (OrganizationInventoryLocationRel exists)
				for each OrganizationInventoryLocationRel
					LocalOrganization = each.RecallOrganization
					include CreateRecallNoticeOrganization

				if (RecallNotice.SavedTransactionQueryParameters.IncludeWatchers)
					for each OrganizationInventoryLocationRel
			   			invoke AttachWatchersToNotice each
							invoked.LocalRecallNotice		= RecallNotice

				
		NotifyCoordinatorOrganizationAdded
			send email
				to RecallNotice.RecallCoordinator.RecallUser.EmployeeWorkEmailAddress
				from config.DefaultFromEmailAddress
				subject "RecallLocationCreatedInOrganizationStructure"
				Contents
					"DuringAProductTransactionQueryATransactionWasIdentifiedForALocationThatDidNotExistInTheRecallOrganizationStructure:"
					"Company:<LocalRequestingCompany>_Location:<LocalRequestingLocation>"
					"ItHasBeenAddedToTheOrganizationStructureWithoutAParent."
					"ActionMayBeRequiredToPlaceItCorrectlyWithinTheHierarchy"
						
   	Actions
    	Create is a Create Action
    		restricted
    		
    		Action Rules
    		
			Exit Rules
				if (LocalCreateTasks)
					if (IsARequestingLocationActivity)
						if (ActivityType.PurchaseOrderLines)
							initialize LocalRecallUser
							include AttachInventoryOrganization 
							for each PolinesrcRqlocRel
								LocalRequestingCompany	= each.PurchaseOrderLineSource.OperatingCompany
								LocalRequestingLocation = each.RequestingLocation
								if (OrganizationRequestingLocationRel exists)
									for each OrganizationRequestingLocationRel
										LocalRecallUser = each.ResponderForOrganization
										include CreateRecallNoticeTaskWithOwner
									if (!LocalRecallUser entered)
										UndefinedLocationInOrganization = true
										include CreateRecallNoticeTaskWithoutOwner
								else
									initialize LocalRecallUser
									UndefinedLocationInOrganization = true
									include CreateRecallNoticeTaskWithoutOwner
								include AttachRequestingOrganization
						else
							LocalRequestingCompany	= RequestingLocationGroup.RequestingCompany
							LocalRequestingLocation = RequestingLocationGroup.RequestingLocation		 
							if (OrganizationRequestingLocationRel exists)
								for each OrganizationRequestingLocationRel
									LocalRecallUser = each.ResponderForOrganization
									include CreateRecallNoticeTaskWithOwner
								if (!LocalRecallUser entered)
									UndefinedLocationInOrganization = true
									include CreateRecallNoticeTaskWithoutOwner
							else
								initialize LocalRecallUser
								UndefinedLocationInOrganization = true
								include CreateRecallNoticeTaskWithoutOwner
							include AttachRequestingOrganization
					
					if (IsAnInventoryLocationActivity)
						
						if (OrganizationInventoryLocationRel exists)
							for each OrganizationInventoryLocationRel
	
								LocalRecallUser = each.ResponderForOrganization
								include CreateRecallNoticeTaskWithOwner
								
							if (!LocalRecallUser entered)
								UndefinedLocationInOrganization = true
								include CreateRecallNoticeTaskWithoutOwner
						else
							initialize LocalRecallUser
							UndefinedLocationInOrganization = true
							include CreateRecallNoticeTaskWithoutOwner
	
						include AttachInventoryOrganization
							
					if (NonLocationActivity)
						include CreateRecallNoticeTaskWithOwner
							
		Update is an Update Action
			restricted
										
		Delete is a Delete Action
			valid when (CanDelete)
		
		Purge is a Purge Action
			restricted
			
		TransferTask is a Create Action
			restricted
			Action Rules
				if (RecallNoticeTaskRel.Status.New)
		   			invoke Update New RecallNoticeTaskRel
						assign result to LocalRecallNoticeTask
						invoked.Owner 						  = LocalRecallUser
						invoked.LocalRecallProduct 			  = RecallProduct
						invoked.LocalRecallProductTransaction = RecallProductTransaction
						invoked.FromProductTransactionQuery	  = true
						invoked.Priority					  = LocalPriority
						invoked.DueDate						  = LocalDueDate
				else
				if (RecallNoticeTaskRel.Status.Accepted)
		   			invoke Merge Accepted RecallNoticeTaskRel
						assign result to LocalRecallNoticeTask
						invoked.Owner 						  = LocalRecallUser
						invoked.LocalRecallProduct 			  = RecallProduct
						invoked.LocalRecallProductTransaction = RecallProductTransaction
						invoked.FromProductTransactionQuery	  = true
						invoked.Priority					  = LocalPriority
						invoked.DueDate						  = LocalDueDate
				else
		   			invoke Merge Completed RecallNoticeTaskRel
						assign result to LocalRecallNoticeTask
						invoked.Owner 						  = LocalRecallUser
						invoked.LocalRecallProduct 			  = RecallProduct
						invoked.LocalRecallProductTransaction = RecallProductTransaction
						invoked.FromProductTransactionQuery	  = true
						invoked.Priority					  = LocalPriority
						invoked.DueDate						  = LocalDueDate
