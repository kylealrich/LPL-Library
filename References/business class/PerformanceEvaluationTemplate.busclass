PerformanceEvaluationTemplate is a BusinessClass

    owned by procurement
    prefix is PFEVT

    Ontology
		symbolic key is PerformanceEvaluationTemplate

	Patterns

    Persistent Fields
		Name      									is a Description
		CurrentTemplateForEvaluations				is Boolean
		CurrentTemplateForScores					is Boolean
		CreationDate								is TimeStamp
		CurrentEvaluationTemplateDate				is TimeStamp
		CurrentScoringTemplateDate					is TimeStamp
		Active 										is Boolean
		LastPerformanceCriteriaGroupDisplayOrder	is Numeric size 8
		
	Transient Fields

	Local Fields

	Derived Fields
		TotalCategoryWeighting is a ComputeField
			type is Percent size 6.3
			restricted
			(sum ActivePerformanceCriteriaGroupRel.CategoryWeighting)			

	Conditions
		PerformanceCategoriesExist
			restricted
			when (PerformanceCriteriaGroup set exists)

		ActiveCriteriaExist
			restricted
			when (ActiveCategoryWithActiveCriteriaRel exists)

		NotCurrentlyBeingUsed
			restricted
			when (!IsCurrentTemplateForEvaluations
			and	  !IsCurrentTemplateForScores)

		IsCurrentTemplateForEvaluations
			restricted
			when (CurrentTemplateForEvaluations = true)

		NewCurrentTemplateForEvaluations
			restricted
			when (CurrentTemplateForEvaluations changed
			and   IsCurrentTemplateForEvaluations)

		IsCurrentTemplateForScores
			restricted
			when (CurrentTemplateForScores = true)

		NewCurrentTemplateForScores
			restricted
			when (CurrentTemplateForScores changed
			and   IsCurrentTemplateForScores)

	Relations
		CategoriesByDisplayRel
			one-to-many relation to PerformanceCriteriaGroup
			Field Mapping uses ByDisplayOrder
				related.SupplierGroup					= SupplierGroup
				related.PerformanceEvaluationTemplate	= PerformanceEvaluationTemplate		

		ActivePerformanceCriteriaGroupRel
			one-to-many relation to PerformanceCriteriaGroup
			Field Mapping uses ByDisplayOrder
				related.SupplierGroup					= SupplierGroup
				related.PerformanceEvaluationTemplate	= PerformanceEvaluationTemplate
			Instance Selection
				where (related.Active)		

		ActiveCategoryWithActiveCriteriaRel
			one-to-many relation to PerformanceCriteriaGroup
			Field Mapping uses ByDisplayOrder
				related.SupplierGroup					= SupplierGroup
				related.PerformanceEvaluationTemplate	= PerformanceEvaluationTemplate
			Instance Selection
				where (related.Active
				and	   related.HasActiveCriteria)
				
		SupplierPerformanceEvaluationRel is a SupplierPerformanceEvaluation set
		
		CurrentTemplateForEvaluationsRel
			one-to-many relation to PerformanceEvaluationTemplate
			Field Mapping uses symbolic key
            	related.SupplierGroup					= SupplierGroup
            Instance Selection
            	where (related.PerformanceEvaluationTemplate 	!= PerformanceEvaluationTemplate
            	and	   related.CurrentTemplateForEvaluations 	= true)

		CurrentTemplateForScoresRel
			one-to-many relation to PerformanceEvaluationTemplate
			Field Mapping uses symbolic key
				related.SupplierGroup					= SupplierGroup
            Instance Selection	
            	where (related.PerformanceEvaluationTemplate 	!= PerformanceEvaluationTemplate
            	and	   related.CurrentTemplateForScores 		= true)

	Sets

 	Field Rules
 		Name 
 			required

 		Active 
 			default to true
 			if (Active changed and Active = false)
	 			constraint (!CurrentTemplateForEvaluations)
	 				"CannotMarkTemplateAsInactive;TemplateIsCurrentTemplateForEvaluations;"
	 			constraint (!CurrentTemplateForScores)
	 				"CannotMarkTemplateAsInactive;TemplateIsCurrentTemplateForScores;"

 		CurrentTemplateForEvaluations
 			if (CurrentTemplateForEvaluations changed and CurrentTemplateForEvaluations = true)
	 			constraint (TotalCategoryWeighting = 100%)
	 				"CannotMarkAsCurrentTemplateForEvaluations;TotalCategoryWeightingIsNot100%"
	 			constraint (ActiveCriteriaExist)
	 				"CannotMarkAsCurrentTemplateForEvaluations;TemplateDoesNotContainAnyActiveCriteria"
	 			constraint (Active)
	 				"CannotMarkAsCurrentTemplateForEvaluations;TemplateIsInactive"

 		CurrentTemplateForScores
 			if (CurrentTemplateForScores changed and CurrentTemplateForScores = true)
	 			constraint (Active)
	 				"CannotMarkAsCurrentTemplateForScores;TemplateIsInactive"
	 			constraint (ActiveCriteriaExist)
	 				"CannotMarkAsCurrentTemplateForScores;TemplateDoesNotContainAnyActiveCriteria"

    Attach Rules
		constraint (Active)
			"PerformanceEvaluationTemplate<Name>IsInactive"

	Rule Blocks

	Actions
		Create is an Action
			Action Rules
    			if (IsCurrentTemplateForEvaluations) 
	    			if (CurrentTemplateForEvaluationsRel exists)
	    				invoke UpdatePerformanceEvaluationTemplate CurrentTemplateForEvaluationsRel
	    					invoked.CurrentTemplateForEvaluations = false
	    			CurrentEvaluationTemplateDate = current timestamp
	    		else
	    			CurrentEvaluationTemplateDate = blank
    			if (IsCurrentTemplateForScores) 
	    			if (CurrentTemplateForScoresRel exists)
	    				invoke UpdatePerformanceEvaluationTemplate CurrentTemplateForScoresRel
	    					invoked.CurrentTemplateForScores = false
	    			CurrentScoringTemplateDate = current timestamp
	    		else
	    			CurrentScoringTemplateDate = blank
	    					
			Exit Rules
				CreationDate	= current timestamp

		Update is an Action
			Action Rules
    			if (NewCurrentTemplateForEvaluations) 
	    			if (CurrentTemplateForEvaluationsRel exists)
	    				invoke UpdatePerformanceEvaluationTemplate CurrentTemplateForEvaluationsRel
	    					invoked.CurrentTemplateForEvaluations = false
	    			CurrentEvaluationTemplateDate = current timestamp
	    		else
	    			if (CurrentTemplateForEvaluations = false)
	    				CurrentEvaluationTemplateDate = blank
    			if (NewCurrentTemplateForScores) 
	    			if (CurrentTemplateForScoresRel exists)
	    				invoke UpdatePerformanceEvaluationTemplate CurrentTemplateForScoresRel
	    					invoked.CurrentTemplateForScores = false
	    			CurrentScoringTemplateDate = current timestamp
	    		else
	    			if (CurrentTemplateForScores = false)
	    				CurrentScoringTemplateDate = blank

		UpdatePerformanceEvaluationTemplate is an Update Action
			restricted

		Copy is an Instance Action
			Parameters
				NewSupplierGroup						is a SupplierGroup
				NewPerformanceEvaluationTemplateName 	is a Description

			Parameter Rules
				NewSupplierGroup
					initial value is SupplierGroup
					required
				NewPerformanceEvaluationTemplateName
					required

			Local Fields
				LocalPerformanceEvaluationTemplate		is a PerformanceEvaluationTemplate view
				LocalPerformanceCriteriaGroup 			is a PerformanceCriteriaGroup view
			
			Action Rules
				invoke Create PerformanceEvaluationTemplate
					assign result to LocalPerformanceEvaluationTemplate
					invoked.SupplierGroup						= NewSupplierGroup
					invoked.Name								= NewPerformanceEvaluationTemplateName
					invoked.Active								= true

				for each PerformanceCriteriaGroup set
					invoke Create PerformanceCriteriaGroup
						assign result to LocalPerformanceCriteriaGroup
						invoked.SupplierGroup						= LocalPerformanceEvaluationTemplate.SupplierGroup
						invoked.PerformanceEvaluationTemplate		= LocalPerformanceEvaluationTemplate.PerformanceEvaluationTemplate
						fill in fields from each

					for each each.PerformanceCriteria set
						invoke Create PerformanceCriteria
							invoked.SupplierGroup					= LocalPerformanceCriteriaGroup.SupplierGroup
							invoked.PerformanceEvaluationTemplate	= LocalPerformanceEvaluationTemplate.PerformanceEvaluationTemplate
							invoked.PerformanceCriteriaGroup		= LocalPerformanceCriteriaGroup.PerformanceCriteriaGroup
							fill in fields from each

		Delete is an Action
			valid when (NotCurrentlyBeingUsed)
			
			Action Rules
				constraint (!SupplierPerformanceEvaluationRel exists)
					"CannotDelete;PerformanceEvaluationsExistForThisPerformanceEvaluationTemplate"
