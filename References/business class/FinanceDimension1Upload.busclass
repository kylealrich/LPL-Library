FinanceDimension1Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD1UP   
    
    Ontology
    	symbolic key is FinanceDimension1Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension1			is like FinanceDimension1
    	FinanceDimension1				is like FinanceDimension1
		ParentSequenceDimension1		is like FinanceDimension1
		SequenceDimension1				is like FinanceDimension1
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description	
		Active
    	SystemDimension					is Boolean
    	IsAZoneDimension				is Boolean
    	ZoneDimension					is like FinanceDimension1
    	EnterpriseZoneDimension			is Boolean
    	DefaultZoneDimension			is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean      	
		ErrorMessage					is Alpha 150
		AddressCode
		FundType

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension1Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension1 is a DerivedField
			type is like FinanceDimension1
			restricted
			if (ParentFinanceDimension1 entered)
				return DerivedParentFinanceDimension1String
			else
				return TopNodeString

		DerivedSequenceParentDimension1 is a DerivedField
			type is like FinanceDimension1
			restricted
			if (ParentSequenceDimension1 entered)
				return ParentSequenceDimension1
			else
				return DerivedParentFinanceDimension1
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension1Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension1String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension1Structure.StructureSequence
			"_"
			ParentFinanceDimension1
		
		DerivedSummaryFinanceDimension1 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension1Structure.StructureSequence
			"_"
			FinanceDimension1

		DerivedSummarySequenceDimension1 is a DerivedField
			type is like FinanceDimension1
			restricted
			if (SequenceDimension1 entered)
				return SequenceDimension1
			else 
				return DerivedSummaryFinanceDimension1

	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		CanDefineZones
 			restricted
 			when (FinanceEnterpriseGroup.Dimension1ZoneBalancing)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
		UsedForFundAccounting 
			when (FinanceEnterpriseGroup.FundAccounting = true
			and   FinanceEnterpriseGroup.FundDimension.Dimension1)

 			
 	Sets
 		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension1Structure
				FinanceDimension1
				FinanceDimension1Upload.RunGroup
				FinanceDimension1Upload.Sequence
					
	Relations
		FinanceDimension1HierarchyRel
			one-to-one relation to FinanceDimension1Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension1Structure = FinanceDimension1Structure
				related.FinanceDimension1		   = FinanceDimension1
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension1Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension1Structure = FinanceDimension1Structure
			Instance Selection
				where (related.FinanceDimension1Upload.RunGroup = FinanceDimension1Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 6
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 6
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension1
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"
    	IsAZoneDimension
    		if (!CanDefineZones)
    			IsAZoneDimension = false   	
    	ZoneDimension
    		if (!CanDefineZones)
    			ZoneDimension = blank    	
    	EnterpriseZoneDimension
    		if (!CanDefineZones)
    			EnterpriseZoneDimension = false    	
    	DefaultZoneDimension			
    		if (!CanDefineZones)
    			DefaultZoneDimension = false						

    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension1Upload.Sequence entered)
					FinanceDimension1Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension1Upload.Sequence + 1)

		Update is an Update Action
			Exit Rules

		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted

		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension1Structure		is an FinanceDimension1Structure
					default label is "FinanceDimension1Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension1Structure
					required
				PrmRunGroup
					required

			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension1	is a FinanceDimension1
				DynamicRulesExist		is Boolean		
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and	   FinanceDimension1Structure	= PrmFinanceDimension1Structure
				and    FinanceDimension1Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension1Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension1Structure
				FinanceDimension1Upload.RunGroup
				FinanceDimension1Upload.Sequence				

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
											
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension1 = FinanceDimension1
					else
						LocalFinanceDimension1 = DerivedSummarySequenceDimension1
					if (LocalFinanceDimension1 exists)
						if  (RecordType.Posting
						and !FinanceDimension1HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension1
								resume on error
									LocalErrorMessage		= error message	
								invoked.PrmStructure		= FinanceDimension1Structure						
								invoked.PrmParentDimension  = DerivedSequenceParentDimension1
						else					
							if (DerivedSequenceParentDimension1 != LocalFinanceDimension1.DerivedParentDimension)
								invoke Move LocalFinanceDimension1
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension1
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension1
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension1 
										except invoked.ParentFinanceDimension1
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension1
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension1
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension1
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension1 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension1
					if (!LocalErrorMessage entered)
						invoke Purge
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension1Structure		is a FinanceDimension1Structure
					default label is "FinanceDimension1Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension1Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension1Structure	    = PrmFinanceDimension1Structure
				and    FinanceDimension1Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension1Structure   is a FinanceDimension1Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension1Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension1Structure	    = PrmFinanceDimension1Structure
				and    FinanceDimension1Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge							
