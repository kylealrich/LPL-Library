WarehousePickListPrintDetail is a BusinessClass
	owned by wh
	
	prefix is WPLPD
	
	Ontology
		symbolic key is WarehousePickListPrintDetail
	
	Patterns
		implements ContextualParent
		implements StaticJava
		disable AuditIndex
		
	Persistent Fields

		HeadRecord		  is Boolean
			restricted
			
	Local Fields
		LocalCombine					is Boolean
		LocalCombinedQuantity			is like Quantity
	
	Rule Blocks
			
		CombinePrintedQuantities

			if(not LocalCombine)
				for each WarehousePickListPrintMergeRel
					LocalCombinedQuantity	+= each.WarehouseShipmentLineDetail.Quantity
				LocalCombine = true
				
	Conditions
		LastShipmentLineDetail
			when (WarehouseShipmentLineDetail.Bin 			= DerivedLastShipmentLineDetailBin
			and   WarehouseShipmentLineDetail.Lot 			= DerivedLastShipmentLineDetailLot
			and   WarehouseShipmentLineDetail.Sublot 		= DerivedLastShipmentLineDetailSublot
			and   WarehouseShipmentLineDetail.Serial 		= DerivedLastShipmentLineDetailSerial
			and   WarehouseShipmentLineDetail.UnitOfMeasure = DerivedLastShipmentLineDetailUOM)
			
		FromComponent
			restricted
			when (WarehouseShipmentLineComponent entered)
	
		WithAlternateUOM
			restricted
			when (WarehouseShipmentLine.TransactionUOM != WarehouseShipmentLineDetail.UnitOfMeasure)

		DisplayPrefCardTransaction
			when (LastShipmentLineDetail
			and   RequisitionWithPrefCardTranRel exists)
	
	Derived Fields
	
		DerivedExtendedCost is a DerivedField
			type is like InternationalCost
				precision is WarehouseShipmentLine.Item.NumberOfDecimalsCost
			if (WarehousePickListPrintIDMRel.DerivedIncludeCost)
				return WarehouseShipmentLine.DerivedPrintedQuantityInTransUOM * DerivedUnitCost
		
		DerivedCombinedQuantity is a DerivedField
			type is like Quantity
				precision is WarehouseShipmentLine.Item.NumberOfDecimalsQuantity
			include CombinePrintedQuantities
			return LocalCombinedQuantity
			
		DerivedLastShipmentLineDetailBin is a DerivedField
			type is like Bin
			return last OtherShipmentLineDetailRel.WarehouseShipmentLineDetail.Bin
			
		DerivedLastShipmentLineDetailLot is a DerivedField
			type is like ItemLot
			return last OtherShipmentLineDetailRel.WarehouseShipmentLineDetail.Lot
			
		DerivedLastShipmentLineDetailSublot is a DerivedField
			type is like Bin
			return last OtherShipmentLineDetailRel.WarehouseShipmentLineDetail.Sublot
			
		DerivedLastShipmentLineDetailSerial is a DerivedField
			type is like ItemSerialNumber
			return last OtherShipmentLineDetailRel.WarehouseShipmentLineDetail.Serial
			
		DerivedLastShipmentLineDetailUOM is a DerivedField
			type is like UnitOfMeasure
			return last OtherShipmentLineDetailRel.WarehouseShipmentLineDetail.UnitOfMeasure
			
		DerivedUnitCost is a DerivedField
			type is like InternationalCost
				precision is WarehouseShipmentLine.Item.NumberOfDecimalsCost
			if (WarehousePickListPrintIDMRel.DerivedIncludeCost)
				return WarehouseShipmentLine.UnitCost * WarehouseShipmentLine.TransactionUOMMultiplier
				
		DerivedItem is a DerivedField
			type is like Item
			if (FromComponent)
				return WarehouseShipmentLineComponent.Item
			else
				return WarehouseShipmentLine.Item
				
		DerivedItemDescription is a DerivedField
			type is like Description
			if (FromComponent)
				return WarehouseShipmentLineComponent.Item.Description
			else
				return WarehouseShipmentLine.Item.Description
				
		DerivedItemDescription2 is a DerivedField
			type is like Description
			if (FromComponent)
				return WarehouseShipmentLineComponent.Item.Description2
			else
				return WarehouseShipmentLine.Item.Description2
				
		DerivedItemCommentCheck is a DerivedField
			type is Alpha 3
			if (LineDetailItemCommentsRel exists)
				return "Yes"
			return "No"
			
		DerivedQuantity is a DerivedField
			type is like Quantity
				precision is WarehouseShipmentLine.Item.NumberOfDecimalsQuantity
			if (WarehouseShipmentLine.TransactionUOM != WarehouseShipmentLineDetail.UnitOfMeasure
			and not WarehouseShipmentLine.IsCatchWeightItem)
				return ((WarehouseShipmentLineDetail.Quantity * WarehouseShipmentLineDetail.UOMMultiplier) / WarehouseShipmentLine.TransactionUOMMultiplier)
			else
				if (FromComponent
				or WarehouseShipmentLineDetail.IsLineOrComponentSerialTracked
				or WarehouseShipmentLineDetail.IsLineOrComponentBinTracked
				or WarehouseShipmentLineDetail.IsLineOrComponentLotTracked)
					return WarehouseShipmentLineDetail.Quantity
				else
					return WarehouseShipmentLine.DerivedPrintedQuantityInTransUOM

		DetailedQuantity is a DerivedField
			type is like Quantity			
				precision is WarehouseShipmentLine.Item.NumberOfDecimalsQuantity
			if (WithAlternateUOM)
				return WarehouseShipmentLineDetail.Quantity
			return 0

		DetailedUOM	is a DerivedField
			type is like UnitOfMeasure
			if (WithAlternateUOM)
				return WarehouseShipmentLineDetail.UnitOfMeasure
			return blank

		DerivedPrintDetailHasSpecialHandling is a DerivedField
			type is Alpha 3
			if (FromComponent)
				if (WarehouseShipmentLineComponent.HasSpecialHandling)
					return "Yes"
			else
				if (WarehouseShipmentLine.HasSpecialHandling)
					return "Yes"
			return "No"
				
	Relations
	

		WarehousePickListPrintIDMRel
			one-to-one relation to WarehousePickListPrintIDM
			Field Mapping uses symbolic key
				related.Company												= Company
				related.InventoryLocation 									= InventoryLocation
				related.WarehouseShipment									= WarehouseShipment
		
		HeadDetailRecordTaggedRel
			one-to-many relation to WarehousePickListPrintDetail
			Field Mapping uses symbolic key
				related.Company												= Company
				related.InventoryLocation									= InventoryLocation
			Instance Selection	
				where (related.WarehouseShipment.WarehouseBatch				= WarehouseShipment.WarehouseBatch
				and	   related.WarehouseShipmentLineDetail.Bin				= WarehouseShipmentLineDetail.Bin
				and	   related.WarehouseShipmentLineDetail.UnitOfMeasure	= WarehouseShipmentLineDetail.UnitOfMeasure
				and	   related.WarehouseShipmentLineDetail.Lot				= WarehouseShipmentLineDetail.Lot
				and	   related.WarehouseShipmentLineDetail.Sublot			= WarehouseShipmentLineDetail.Sublot
				and	   related.WarehouseShipmentLineDetail.Serial			= WarehouseShipmentLineDetail.Serial
				and    related.WarehouseShipmentLine.Item					= WarehouseShipmentLine.Item
				and    related.HeadRecord 									= true)
		
		WarehousePickListPrintMergeRel
			one-to-many relation to WarehousePickListPrintDetail
			Field Mapping uses symbolic key
				related.Company												= Company
				related.InventoryLocation									= InventoryLocation
				related.WarehouseShipment									= WarehouseShipment
			Instance Selection
				where (related.WarehouseShipment.WarehouseBatch				= WarehouseShipment.WarehouseBatch
				and	   related.WarehouseShipmentLineDetail.Bin				= WarehouseShipmentLineDetail.Bin
				and	   related.WarehouseShipmentLineDetail.UnitOfMeasure	= WarehouseShipmentLineDetail.UnitOfMeasure
				and	   related.WarehouseShipmentLineDetail.Lot				= WarehouseShipmentLineDetail.Lot
				and	   related.WarehouseShipmentLineDetail.Sublot			= WarehouseShipmentLineDetail.Sublot
				and	   related.WarehouseShipmentLineDetail.Serial			= WarehouseShipmentLineDetail.Serial
				and    related.WarehouseShipmentLine.Item					= WarehouseShipmentLine.Item)
				
		OtherShipmentLineDetailRel
			one-to-many relation to WarehousePickListPrintDetail
			Field Mapping uses ByBinLineLotSublotSerial
				related.Company												= Company
				related.InventoryLocation									= InventoryLocation
			Instance Selection
				where (related.WarehouseShipment						= WarehouseShipment
				and    related.WarehouseShipmentLine					= WarehouseShipmentLine)
				
		RequisitionLineCommentRel													
			one-to-many relation to RequisitionLineComment
			Field Mapping uses part of key
				related.Company         			= WarehouseShipment.CompanyAndLocation.RequestingCompany
				related.Requisition					= WarehouseShipmentLine.WarehouseShipment.WarehouseDemand.DemandDocument
				related.RequisitionLine 			= WarehouseShipmentLine
			Instance Selection
				where (LastShipmentLineDetail
				and    related.CommentType.PrintOnPickList)
				
		CustomerOrderLineCommentRel
			one-to-many relation to OrderEntryOrderComment
			Field Mapping uses symbolic key
				related.Company						= Company
				related.Customer					= WarehouseShipmentLine.WarehouseShipment.CustomerOrderRel.Customer
				related.OrderEntryStandardOrder		= blank
				related.CustomerOrder				= WarehouseShipmentLine.WarehouseShipment.WarehouseDemand.DemandDocument
				related.CustomerOrderLine			= WarehouseShipmentLine
				
		LineCommentCheckRel is a WarehousePickListPrintDetail set
			Instance Selection
				where (RequisitionLineCommentRel exists
				or CustomerOrderLineCommentRel exists)
				
		LineDetailItemCommentsRel
        	one-to-many relation to ItemComment
        	Field Mapping uses symbolic key
        		related.ItemGroup 	= WarehouseShipmentLine.Item.ItemGroup
        		related.Item		= DerivedItem
			Instance Selection
				where (related.IsCommentNotExpired)

		RequisitionLineWithPrefCardTranRel													
			one-to-many relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company         			= WarehouseShipment.CompanyAndLocation.RequestingCompany
				related.Requisition					= WarehouseShipment.WarehouseDemand.DemandDocument
				related.RequisitionLine 			= WarehouseShipmentLine
			Instance Selection
				where (related.PreferenceCardTransaction)

		RequisitionWithPrefCardTranRel													
			one-to-many relation to Requisition
			Field Mapping uses symbolic key
				related.Company         			= WarehouseShipment.CompanyAndLocation.RequestingCompany
				related.Requisition					= WarehouseShipment.WarehouseDemand.DemandDocument
			Instance Selection
				where (related.HasPreferenceCardTransaction)
	
	Field Rules
		

	Sets
		IDMSet
			duplicates
			Sort Order
				Company
				InventoryLocation
				WarehouseShipmentLineDetail.UnitOfMeasure
				WarehouseShipmentLineDetail.Bin
				WarehouseShipmentLineDetail.Lot
				WarehouseShipmentLineDetail.Sublot
				WarehouseShipmentLine
				
		ByBinLotSublotSerial
			Sort Order
				Company
				InventoryLocation
				WarehouseShipmentLineDetail.Bin
				WarehouseShipmentLineDetail.Lot
				WarehouseShipmentLineDetail.Sublot
				WarehouseShipmentLineDetail.Serial
				WarehouseShipment
				WarehouseShipmentLine
				WarehouseShipmentLineDetail.UnitOfMeasure
				WarehouseShipmentLineComponent
				
		ByBinLineLotSublotSerial
			Sort Order
				Company
				InventoryLocation
				WarehouseShipmentLineDetail.Bin
				WarehouseShipmentLine
				WarehouseShipmentLineDetail.Lot
				WarehouseShipmentLineDetail.Sublot
				WarehouseShipmentLineDetail.Serial
				WarehouseShipment
				WarehouseShipmentLineDetail.UnitOfMeasure
				WarehouseShipmentLineComponent
				
		ByBinItemUOMLotSerial
			Sort Order
				Company
				InventoryLocation
				WarehouseShipmentLineDetail.Bin
				WarehouseShipmentLine.Item
				WarehouseShipmentLineComponent
				WarehouseShipmentLineDetail.UnitOfMeasure
				WarehouseShipmentLineDetail.Lot
				WarehouseShipmentLineDetail.Sublot
				WarehouseShipmentLineDetail.Serial
				WarehouseShipment
				WarehouseShipmentLine
	
	Actions
		Create is a Create Action
			restricted
			Action Rules
				if(HeadDetailRecordTaggedRel not exists)
					HeadRecord = true
					
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted
