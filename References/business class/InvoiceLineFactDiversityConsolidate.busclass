InvoiceLineFactDiversityConsolidate is a BusinessClass
	owned by procurement
	prefix is ILFDC
	sql name is InvLFDivConsol
	
	Ontology
		symbolic key is InvoiceLineFactDiversityConsolidate

	Patterns
        disable Auditing
        disable EffectiveDated

		implements DynamicCreation

	Persistent Fields
		TotalExtendedAmount		is an InternationalAmount
		TotalSpend				is an InternationalAmount

	Local Fields
        Dimension				is Numeric size 2
        	States
        		ProcurementGroup		value is 1
        		Year					value is 2
        		Month					value is 3
        		Company					value is 4
        		ShipToLocation			value is 5
        		RequestingLocation		value is 6
        		Buyer					value is 7
        		CommodityCode			value is 8
        		DiversityCode			value is 9
        		Supplier				value is 10
        		UNSPSC					value is 11
        		Item					value is 12

	Derived Fields
		DerivedEventName is a ConditionalField
			type is Text
			restricted
			if (InvoiceLineFactDiversityConsolidate.DiversityFlag = 1)
				DiversityString
			else
				NonDiversityString
		
		DiversityString is a MessageField
			restricted
			"DiverseSpend_=_<ComputePercentage>%"
		
		NonDiversityString is a MessageField
			restricted
			"NonDiverseSpend_=_<ComputePercentage>%"
		
		ComputePercentage is a ComputeField
			type is Decimal size 5.2
			restricted
			(TotalExtendedAmount / TotalSpend * 100)
	
	Conditions
		InvoicesExistForDiverseSpend
			restricted
			when (InvoiceLineFactDiversityConsolidate.DiversityFlag.Diversity)
		InvoicesExistForNonDiverseSpend
			restricted
			when (InvoiceLineFactDiversityConsolidate.DiversityFlag.NonDiversity)
			
	Relations
		InvoiceLineFactConsolidateDiversityCodeRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel 							= 1
				related.ProcurementGroup 						   	= ProcurementGroup
				related.InvoiceLineFactConsolidateControl			= InvoiceLineFactConsolidateControl
				related.InvoiceLineFactConsolidate.Level1.Type		= Dimension.DiversityCode
			Instance Selection
				where (related.InvoiceLineFactConsolidate.SpendCategory.OffContract
				and	   related.InvoiceLineFactConsolidate.Level1.Value entered
				and	   InvoiceLineFactDiversityConsolidate.DiversityFlag.Diversity)
		
		InvoiceLineFactsForDiverseInvoicesRel
			one-to-many relation to InvoiceLineFact
			Field Mapping uses ByInvoice
				related.ProcurementGroup = ProcurementGroup
			Instance Selection
				where (related.PayablesDiversityCode entered
				and    related.InvoiceDate >= ProcurementGroup.DiversitySpendAnalysisFromDate
				and    related.InvoiceDate <= ProcurementGroup.DiversitySpendAnalysisThruDate)
				
		InvoiceLineFactsForNonDiverseInvoicesRel
			one-to-many relation to InvoiceLineFact
			Field Mapping uses ByInvoice
				related.ProcurementGroup = ProcurementGroup
			Instance Selection
				where (!related.PayablesDiversityCode entered
				and    related.InvoiceDate >= ProcurementGroup.DiversitySpendAnalysisFromDate
				and    related.InvoiceDate <= ProcurementGroup.DiversitySpendAnalysisThruDate)
								
		ContextProcurementGroupRel
			one-to-one relation to ProcurementGroup
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
	
	Actions
		Create is a Create Action

			
		Update is an Update Action

			
		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted
