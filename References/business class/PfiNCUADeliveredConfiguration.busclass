PfiNCUADeliveredConfiguration is a BusinessClass
    owned by pfi
    prefix is NCUAD
	default label is "NCUADeliveredConfiguration"

    Ontology
        symbolic key is PfiNCUADeliveredConfiguration

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements DynamicCreation

    Persistent Fields
        PfiNCUAPreviewDeliveredConfiguration
        PfiNCUATagConfiguration
        Priority is Numeric size 1
        	States
        		Normal value is 0
        		High value is 1
        		Medium value is 2
        		Low value is 3
        PriorityVariable is Alpha size up to 500
    	Enabled is Boolean

    Field Rules
    	Enabled
    		default to true
    		initial value is true

    Derived Fields

        IsPrimarySecondaryActionNotSame is a DerivedField
            type is Boolean
            default label is untranslatable
            if(PfiNCUAPreviewDeliveredConfiguration.PrimaryAction entered and PfiNCUAPreviewDeliveredConfiguration.PrimaryAction = PfiNCUAPreviewDeliveredConfiguration.SecondaryAction)
                return false
            return true

        CustomOverrideLink is a NativeField
            type is Alpha size up to 1000

        IsCustomOverrideLinkEmpty is a DerivedField
            type is Boolean
            if(CustomOverrideLink = "")
                return true
            return false

        DeveloperMode is a NativeField
            type is Boolean
            restricted

    Actions
    	Create is a Create Action

    		Action Rules
    			constraint (IsPrimarySecondaryActionNotSame)
    				"PrimaryAndSceondaryActionsCannotBeSame"

        Update is an Update Action

            Entrance Rules
                constraint (DeveloperMode)
                    "UpdateAllowedBy_InforOnly"

            Action Rules
                constraint (IsPrimarySecondaryActionNotSame)
                    "PrimaryAndSceondaryActionsCannotBeSame"

        Delete is a Delete Action

            Entrance Rules
                constraint (DeveloperMode)
                    "DeleteAllowedBy_InforOnly"

    	CreateOverride is an Instance Action
    		restricted
    		completion message is "OverrideCreated"
    		Action Rules
    			constraint (IsCustomOverrideLinkEmpty)
    				"OverrideFailed"

    	Enable is an Instance Action
    		valid when (not Enabled)
    		Action Rules
    			Enabled = true

    	Disable is an Instance Action
    		valid when (Enabled)
    		Action Rules
    			Enabled = false
