ActorContextTemplate is a BusinessClass
	owned by person
	
	prefix is ACT1
	
	Ontology
		symbolic key is ActorContextTemplate
		
	
	Patterns
	
	Persistent Fields
		Description is Alpha size 100
		
	Local Fields
		LocalTemplateName  is AlphaUpper size 30
		LocalTemplate      is an ActorContextTemplate
		LocalActor         is an Actor
		LocalActorGroup    is an ActorGroup
		LocalRole          is an Role
		LocalRoleGroup     is an RoleGroup
	    
	Relations
	
		ActorContextTemplateDetailRel 	
			one-to-many relation to ActorContextTemplateDetail
			Field Mapping uses symbolic key
				related.ActorContextTemplate     = LocalTemplate
	
		ActorContextDetailRel is an ActorContextTemplateDetail set
		
		ActiveActorRel 
			one-to-many relation to Actor
			Field Mapping uses symbolic key
			Instance Selection
				where (!related.IsActorDisabled
				and    !related.HasCreatedFor
				and    !related.Actor="Anonymous")

		ActorGroupMemberRel 
			one-to-many relation to ActorGroupMember
			Field Mapping uses part of key
				related.ActorGroup = LocalActorGroup
				
		ActorGroupMemberShadowRel 
			one-to-many relation to ActorGroupMemberShadow
			Field Mapping uses symbolic key
				related.ActorGroup = LocalActorGroup

		ActorRoleRel 
			one-to-many relation to ActorRole
			Field Mapping uses ByRoleAndActor
				related.ActorRole.Role = LocalRole
				
		RoleGroupRel 
			one-to-many relation to RoleGroupMember
			Field Mapping uses part of key
				related.RoleGroup = LocalRoleGroup

		DuplicateNameRel
			one-to-one relation to ActorContextTemplate
			Field Mapping uses symbolic key
				related.ActorContextTemplate    = LocalTemplateName
						
	Conditions
	
		DetailsExist
			restricted
			when (ActorContextTemplateDetailRel exists)

		IsValidForRole
			restricted
			when (ActorRoleRel exists)
			
		IsValidForRoleGroup
			restricted
			when (RoleGroupRel exists)
	
	Actions
	
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action

		CreateActorContextFromTemplate is an Instance Action
			run in background
		
			Parameters
			
				For				is Alpha size 1
					States
						Actor 			value is 1
						ActorGroup		value is 2
						Role 			value is 3
						RoleGroup		value is 4
				
				ParamActor             	is an Actor                 
					default label is "Actor"
				ParamActorGroup			is an ActorGroup
					default label is "ActorGroup"
				ParamRole             	is a Role                 
					default label is "Role"
				ParamRoleGroup			is a RoleGroup
					default label is "RoleGroup"
				ParamSpecifyActor 			is Boolean
				    default label is "SpecifyActor"
				ParamIncludeDescendants	is Boolean
					default label is "IncludeDescendants"
					States
						Yes		value is true
						No		value is false					
				ParamDuplicateHandling       is Boolean
					default label is "OverrideIfExists"
					States
						Skip		value is false
						Replace		value is true					
				
			Parameter Rules
				For
					required
				ParamActor
					if (For = 1 and ParamSpecifyActor = true)
                        required
                    initial value is blank    
				ParamActorGroup
					if (For = 2)
                        required
				ParamRole
					if (For = 3)
                        required
				ParamRoleGroup
					if (For = 4)
                        required
				ParamDuplicateHandling
					initial value is false
				ParamIncludeDescendants
					initial value is false	
			
			Action Rules
			
				LocalTemplate 	= ActorContextTemplate
				LocalActor	= ParamActor
                if (For = 1 and ParamActor != blank) 
                    for each ActorContextTemplateDetailRel
                        invoke CreateActorContext each
                            invoked.ParamActor				= ParamActor
                            invoked.DuplicateHandling       = ParamDuplicateHandling

				if (For = 1 and ParamActor = blank) 
					invoke CreateActorContextForAllActors Actor
                		invoked.ActorContextTemplate = LocalTemplate
                		invoked.ParamDuplicateHandling = ParamDuplicateHandling
