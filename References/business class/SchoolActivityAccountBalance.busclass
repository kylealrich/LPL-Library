SchoolActivityAccountBalance is a BusinessClass
    owned by studentactivities
    prefix is SCAB

    Ontology
    	symbolic key is SchoolActivityAccountBalance

 	Patterns
		implements ContextualParent
 		implements DynamicCreation

    Persistent Fields
		ParentStudentActivityAccount	is a StudentActivityAccount
		BeginningBalance				is an InternationalAmount
		PeriodReceipts					is an InternationalAmount
		PeriodDisbursements				is an InternationalAmount
		PeriodTransfers					is an InternationalAmount
		BalanceAdjustment				is an InternationalAmount
		BeginningPostedBalance			is an InternationalAmount
		PostedReceipts					is an InternationalAmount
		PostedDisbursements				is an InternationalAmount
		PostedTransfers					is an InternationalAmount
		PostedBalanceAdjustment			is an InternationalAmount

	Transient Fields
		TransactionCurrencyCode is a Currency
			derive value from StudentActivitySchool.DefaultAccountingEntity.FunctionalCurrency
	
	Derived Fields
		PeriodEndingBalance is a DerivedField
			type is like InternationalAmount
			return (BeginningBalance + PeriodReceipts - PeriodDisbursements + PeriodTransfers)

		PostedEndingBalance is a DerivedField
			type is like InternationalAmount
			return (BeginningPostedBalance + PostedReceipts + PostedDisbursements + PostedTransfers)

	Sets
		ByDescendingDate
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				SchoolActivityAccount
				StudentActivityClosedPeriod	descending	

		ByAscendingDate
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				SchoolActivityAccount
				StudentActivityClosedPeriod

		ByDistrictDescendingDate
			Sort Order
				StudentActivityDistrict
				SchoolActivityAccount
				StudentActivityClosedPeriod	descending
				StudentActivitySchool

		ByStudentActivityAccount
			duplicates
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				StudentActivityClosedPeriod
				SchoolActivityAccount.StudentActivityAccount					

		ByDistrictStudentActivityAccount
			duplicates
			Sort Order
				StudentActivityDistrict
				SchoolActivityAccount.StudentActivityAccount
				StudentActivityClosedPeriod	descending
				StudentActivitySchool

		ByParentActivity
			duplicates
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				StudentActivityClosedPeriod
				ParentStudentActivityAccount

	Relations
		StudentActivityBalanceDetailRel
			one-to-one relation to StudentActivityBalanceDetail
			Field Mapping uses symbolic key
				related.StudentActivityDistrict				= StudentActivityDistrict
				related.StudentActivityClosedPeriod			= StudentActivityClosedPeriod
				related.StudentActivitySchool				= StudentActivitySchool
				related.StudentActivityAccount				= SchoolActivityAccount.StudentActivityAccount
	
		SchoolClosedPeriodRel
			one-to-many relation to SchoolClosedPeriod
			Field Mapping uses BySchool
				related.StudentActivityDistrict			= StudentActivityDistrict
				related.StudentActivitySchool			= StudentActivitySchool

		PreviousActivityAccountBalanceRel
			one-to-many relation to SchoolActivityAccountBalance
			Field Mapping uses ByDescendingDate
				related.StudentActivityDistrict			= StudentActivityDistrict
				related.StudentActivitySchool			= StudentActivitySchool
				related.SchoolActivityAccount			= SchoolActivityAccount
			Instance Selection
				where (related.StudentActivityClosedPeriod	< StudentActivityClosedPeriod)
			
		NextActivityAccountBalanceRel
			one-to-many relation to SchoolActivityAccountBalance
			Field Mapping uses ByDescendingDate
				related.StudentActivityDistrict			= StudentActivityDistrict
				related.StudentActivitySchool			= StudentActivitySchool
				related.SchoolActivityAccount			= SchoolActivityAccount
			Instance Selection
				where (related.StudentActivityClosedPeriod	> StudentActivityClosedPeriod)
		
		StudentActivityReceiptDetailRel
			one-to-many relation to StudentActivityReceiptDetail
			Field Mapping uses ByClosedPeriod		
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.SchoolActivityAccount.StudentActivityAccount	= SchoolActivityAccount.StudentActivityAccount
				related.SchoolActivityAccount.SubActivity				= SchoolActivityAccount.SubActivity
				related.StudentActivitySchool							= StudentActivitySchool
			
		StudentActivityBankReceiptDetailRel
			one-to-many relation to StudentActivityBankTransDetail
			Field Mapping uses ByClosedPeriod		
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.SchoolActivityAccount.StudentActivityAccount	= SchoolActivityAccount.StudentActivityAccount
				related.SchoolActivityAccount.SubActivity				= SchoolActivityAccount.SubActivity
				related.StudentActivitySchool							= StudentActivitySchool
			Instance Selection
				where (!related.BankTransactionType.Transfer
				and     related.StudentActivityBankTransaction.AmountType.BankCredit)

		StudentActivityDisbursementDetailRel
			one-to-many relation to StudentActivityBankTransDetail
			Field Mapping uses ByClosedPeriod		
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.SchoolActivityAccount.StudentActivityAccount	= SchoolActivityAccount.StudentActivityAccount
				related.SchoolActivityAccount.SubActivity				= SchoolActivityAccount.SubActivity
				related.StudentActivitySchool							= StudentActivitySchool
			Instance Selection
				where (!related.BankTransactionType.Transfer
				and     related.StudentActivityBankTransaction.AmountType.BankDebit)
			
		StudentActivityTransferDetailRel
			one-to-many relation to StudentActivityTransferDetail
			Field Mapping uses ByClosedPeriod		
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.SchoolActivityAccount.StudentActivityAccount	= SchoolActivityAccount.StudentActivityAccount
				related.SchoolActivityAccount.SubActivity				= SchoolActivityAccount.SubActivity
				related.StudentActivitySchool							= StudentActivitySchool
			
		StudentActivityBankTransferDetailRel
			one-to-many relation to StudentActivityBankTransDetail
			Field Mapping uses ByClosedPeriod		
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.SchoolActivityAccount.StudentActivityAccount	= SchoolActivityAccount.StudentActivityAccount
				related.SchoolActivityAccount.SubActivity				= SchoolActivityAccount.SubActivity
				related.StudentActivitySchool							= StudentActivitySchool
			Instance Selection
				where (related.BankTransactionType.Transfer)









	Conditions
		HasPostedBalances
			when (BeginningPostedBalance	entered
			or    PostedReceipts			entered
			or    PostedDisbursements		entered
			or    PostedTransfers			entered)

		PostingActivity
			when (SchoolActivityAccount.GLAccount entered)

		HasBalanceAdjustment
			when (BalanceAdjustment		entered
			and   PreviousActivityAccountBalanceRel exists)
			
		HasPostedBalanceAdjustment
			when (PostedBalanceAdjustment	entered
			and   PreviousActivityAccountBalanceRel exists)
	
		HasReceiptDetails
			when (StudentActivityReceiptDetailRel exists)
			
		HasBankReceiptDetails	
			when (StudentActivityBankReceiptDetailRel exists)

		HasDisbursementDetails
			when (StudentActivityDisbursementDetailRel exists)
			
		HasTransferDetails
			when (StudentActivityTransferDetailRel exists)
		
		HasBankTransferDetails
			when (StudentActivityBankTransferDetailRel exists)

		HasTransactionDetails
			when (HasReceiptDetails
			or    HasBankReceiptDetails
			or    HasDisbursementDetails
			or    HasTransferDetails
			or    HasBankTransferDetails)



			

	Field Rules
		BeginningBalance
			BeginningBalance	= (first PreviousActivityAccountBalanceRel.PeriodEndingBalance + BalanceAdjustment)

		BeginningPostedBalance
			BeginningPostedBalance	= (first PreviousActivityAccountBalanceRel.PostedEndingBalance + PostedBalanceAdjustment)

		ParentStudentActivityAccount
			default to SchoolActivityAccount.StudentActivityAccount.ParentStudentActivityAccount

	Actions

		UpdateBeginningBalances is an Instance Action
			restricted
			Action Rules
				invoke Update
				
			Exit Rules
				if (NextActivityAccountBalanceRel exists)
					invoke UpdateBeginningBalances last NextActivityAccountBalanceRel
									
		
		AdjustBeginningBalance is an Instance Action
			Parameters
				BalanceAdjustmentAmount			is an InternationalAmount
				PostedBalanceAdjustmentAmount	is an InternationalAmount
				
			Parameter Rules
				BalanceAdjustmentAmount
					if (!PostingActivity)
						required
							"AdjustmentAmountRequired"
							
				PostedBalanceAdjustmentAmount
					if (PostingActivity
					and !BalanceAdjustmentAmount entered)
						required
							"AdjustmentAmountOrPostedAdjustmentAmountRequired"
			
			Action Rules
				invoke Update
					invoked.BalanceAdjustment		+= BalanceAdjustmentAmount
					invoked.PostedBalanceAdjustment	+= PostedBalanceAdjustmentAmount
				
			Exit Rules
				invoke UpdateBalanceAndBalanceDetail
					invoked.AdjustmentAmount		= BalanceAdjustmentAmount
					invoked.PostedAdjustmentAmount	= PostedBalanceAdjustmentAmount
					
				if (NextActivityAccountBalanceRel exists)
					invoke UpdateBeginningBalances last NextActivityAccountBalanceRel
				
		UpdateBalanceAndBalanceDetail is an Instance Action
			restricted
			Parameters
				AdjustmentAmount		is an InternationalAmount
				PostedAdjustmentAmount	is an InternationalAmount
				
			Local Fields
				StudentActivityCloseFields
				
			Action Rules

				StudentActivityCloseFields.SchoolCalendarGroup			= StudentActivityDistrict.SchoolCalendarPeriodGroup
				StudentActivityCloseFields.StudentActivityClosedPeriod	= StudentActivityClosedPeriod
				StudentActivityCloseFields.StudentActivityAccount		= SchoolActivityAccount.StudentActivityAccount
				StudentActivityCloseFields.StudentActivitySchool		= StudentActivitySchool

				invoke Update StudentActivityCloseFields.StudentActivityBalance
					invoked.BalanceAdjustment		+= AdjustmentAmount
					invoked.PostedBalanceAdjustment	+= PostedAdjustmentAmount

				invoke Update StudentActivityCloseFields.StudentActivityBalanceDetail
					invoked.BalanceAdjustment		+= AdjustmentAmount
					invoked.PostedBalanceAdjustment	+= PostedAdjustmentAmount
					

		Create is a Create Action
			restricted
					
		Update is an Update Action
			restricted
	
		Delete is a Delete Action 
			restricted

  		DeleteIfNoAdjustmentsExist is an Instance Action
  			restricted
  			Action Rules
  				if (BalanceAdjustment 			!entered
				and PostedBalanceAdjustment		!entered)
  					invoke Delete
				else
					initialize	PeriodReceipts
					initialize	PeriodDisbursements
					initialize	PeriodTransfers
					initialize	PostedReceipts
					initialize	PostedDisbursements
					initialize	PostedTransfers
					
