FinanceResourceDialogue is a BusinessClass
	owned by sharedfinance



	prefix is FRDia
	
	Ontology
		symbolic key is FinanceResourceDialogue
		
	Patterns
		implements CreateStamp
	
	Persistent Fields	
		NavigateToThis						is BusinessObjectReference
		DialogueTitle						is Alpha 100
		Dialogue 							is Text
		DialogueAttachment					is an AlternateAttachment
		DialogueLink						is an URL
		Author	 							is a SharedFinanceResourceField 	
		SendToTeam							is a FinanceTeam
		SendToContact						is a SharedFinanceResourceField 	
		GroupingForTeam						is Numeric 9
		ResponseRequired					is Boolean
		Replied								is Boolean
		HideFromNewView						is Boolean //hide from list of "new" messages in message center
		DialogueType						is Numeric 02
			States
				GeneralQuestion				value is 0
				ResponseToQuestion			value is 1
				NotebookEntry				value is 2
				SupportingDocumentation		value is 3
				AuditDocumentation			value is 4
				ProcessDocumentation		value is 6
				SystemMessage				value is 7
				Escalation 					value is 8
				StatusReport				value is 9
		ExternalAudit						is Boolean
		PassedAudit							is Boolean
		FinancialForm						is Alpha 6		
		MessageType							is Numeric 1
			States
				MessageReceived				value is 0		
				MessageSent					value is 1
	Local Fields
		LocalGroupingForTeam		is Numeric 9
		LocalTeam					is a FinanceTeam						
		LocalAttributeCtr   		is Numeric 2

	Field Rules
		DialogueTitle
			required
		Dialogue
			default to DialogueTitle
		Author
			required
			initial value is actor.agent(Employee).Employee	
			force default to actor.agent(Employee).Employee	
			cannot be changed
		SendToTeam
			if (SendToContact entered)
				force default to blank
			else
				required
			cannot be changed	
		SendToContact
			if (SendToTeam entered)
				force default to blank
			if (DialogueType < 2) 
				required	
				constraint (SendToContact.DialogueResource.Active)
					"TheResourceForThisMessageIsNotActive"
				constraint (SendToContact not = Author)
					"YouCannotSendAMessageToYourself"
			if (DialogueType = 2) 
				force default to actor.agent(Employee).Employee
			if (DialogueType > 2) 
				force default to blank
			cannot be changed	
		HideFromNewView
			force default to false
		ResponseRequired
			initial value is false
			default to false
			cannot be changed
		Replied
			initial value is false
			default to false
		DialogueType	
			default to 0 
			cannot be changed

	Derived Fields
		StringReplySubject is a StringField
			type is Alpha 60
			restricted
			"RE: "
			DialogueTitle
			
		DerivedReplySubject is a DerivedField
			type is Alpha 60
			restricted
			if (DialogueType.GeneralQuestion)
				return StringReplySubject
			else
				return DialogueTitle
			
	Conditions
		MyReadyToView
			restricted
			when ((MessageSentToMe)
				and (!HideFromNewView))
		IAmTheAuthor
			restricted
			when (Author = actor.agent(Employee).Employee)
		MessageSentToMe	
			restricted
			when (SendToContact = actor.agent(Employee).Employee)
		PendingMyResponse
			restricted
			when ((ResponseRequired)
				and (MessageSentToMe)
				and (!Replied))
		RedAlert
			restricted
			when (ResponseRequired)
		HasAttachment
			restricted
			when (DialogueAttachment entered)
		HasLink
			restricted
			when (DialogueLink entered)
		AllowHide
			restricted
			when ((MessageSentToMe)
				and (!HideFromNewView)
				and (!PendingMyResponse))
		AllowUnhide
			restricted
			when ((MessageSentToMe)
				and (HideFromNewView))
		AllowForward
			restricted
			when (MessageSentToMe)		
		ShowAuthorPicture
			restricted
			when (Author.DialogueResource.ResourcePicture entered)
		AllowDelete
			restricted
			when (((ResponseRequired
			and    Replied)
			or    !ResponseRequired)
			or 	   MessageType.MessageSent)



		ClosePeriodTaskDialogue			
			restricted
			when (NavigateToThis.BusinessClassName = "ClosePeriodTask")
					
	Relations
		OtherMessagesInGroupRel 
			one-to-many relation to FinanceResourceDialogue
			Field Mapping uses ByGroupingForTeam
				related.HROrganization = HROrganization
				related.GroupingForTeam = GroupingForTeam
			Instance Selection
				where (related.FinanceResource not = this instance.FinanceResource)				

		SendToTeamMembersRel
			one-to-many relation to FinanceTeamMember 
			Field Mapping uses ByFinanceTeam
				related.FinanceEnterpriseGroup 	= HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
				related.FinanceTeam 			= LocalTeam				

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with DialogueAttachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with DialogueAttachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with DialogueAttachment

	Sets
		ByGroupingForTeam
			Sort Order
				HROrganization
				GroupingForTeam
				FinanceResource
				FinanceResourceDialogue
				
		ByFrom
			Sort Order
				HROrganization
				Author
				DialogueType
				SendToContact
				FinanceResource
				FinanceResourceDialogue
				
		ByTo
			Sort Order
				HROrganization
				SendToContact
				DialogueType
				Author
				FinanceResource
				FinanceResourceDialogue

		ByHideFromViewStatus
			Sort Order
				HROrganization
				SendToContact
				HideFromNewView
				create stamp
				Author
				FinanceResource
				FinanceResourceDialogue
		
		ByHideToViewStatus
			Sort Order
				HROrganization
				Author
				HideFromNewView
				create stamp
				SendToContact
				FinanceResource
				FinanceResourceDialogue

		ByNavigateToThis
			Sort Order
				HROrganization
				NavigateToThis	
				DialogueType	
				FinanceResource
				FinanceResourceDialogue

		ByTeam
			Sort Order
				HROrganization
				SendToTeam
				SendToContact
				DialogueType	
				FinanceResource
				FinanceResourceDialogue
						
	Actions
		Create is a Create Action
			restricted

		Purge is a Purge Action
			default label is "Delete"
			valid when (AllowDelete)

		UploadToIDM is an Instance Action  
			valid when (DialogueAttachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with DialogueAttachment	
														
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (DialogueAttachment.IsLocal)
			
			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with DialogueAttachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop







						














		Reply is an Instance Action
			default label is "Reply"
			completion message is "ResponsePosted"
			valid when (MessageSentToMe)
			Parameters
				PrmDialogueTitle				is Alpha 60
				PrmDialogue 					is Text
				PrmDialogueAttachment			is an Attachment
				PrmDialogueLink					is an URL
				PrmResponseRequired				is Boolean
				PrmAttachment					is a Attachment
				PrmLinkToDocumentation			is an URL	
			Parameter Rules
				PrmDialogueTitle
					default to DerivedReplySubject
					initial value is DerivedReplySubject
				PrmDialogue
					required
						"PleaseEnterYourResponse"
			Action Rules
				Replied = true
			Exit Rules	

				invoke Create
					fill in fields from this instance
					invoked.FinanceResource = this instance.Author
					invoked.SendToContact = this instance.Author	
					invoked.Author = actor.agent(Employee).Employee				 	
					invoked.DialogueTitle = PrmDialogueTitle
					invoked.Dialogue = PrmDialogue 
					invoked.DialogueAttachment = PrmDialogueAttachment
					invoked.DialogueLink = PrmDialogueLink		
					invoked.ResponseRequired = PrmResponseRequired
					invoked.HideFromNewView = false
					invoked.Replied = false
					invoked.DialogueType = 1 
					invoked.MessageType = 0 



				invoke Create
					fill in fields from this instance
					invoked.FinanceResource = actor.agent(Employee).Employee
					invoked.SendToContact = this instance.Author	
					invoked.Author = actor.agent(Employee).Employee				 	
					invoked.DialogueTitle = PrmDialogueTitle
					invoked.Dialogue = PrmDialogue 
					invoked.DialogueAttachment = PrmDialogueAttachment
					invoked.DialogueLink = PrmDialogueLink		
					invoked.ResponseRequired = PrmResponseRequired
					invoked.HideFromNewView = false
					invoked.Replied = false
					invoked.DialogueType = 1 
					invoked.MessageType = 1 

		SendMessage is an Instance Action
			Parameters
				MessageFor						is Alpha 08
					States
						Resource				value is "Resource"
						Team					value is "Team"
				MessageTeam						is a FinanceTeam
				MessagePerson					is a FinanceResource
				DialogueTitle					is Alpha 60
				Dialogue 						is Text 
				DialogueAttachment				is an Attachment
				DialogueLink					is an URL
				ResponseRequired				is Boolean
				PrmExternalToEmailAddress		is EmailAddressField with multiple addresses 
					holds pii
				PrmCcEmailAddress				is EmailAddressField with multiple addresses 
					holds pii
				PrmBccEmailAddress				is EmailAddressField with multiple addresses 
					holds pii
				PrmAddCcEmail					is Boolean
				PrmAddBccEmail					is Boolean
				PrmSendingLandmarkNotifications is Boolean
				PrmLocalActor					is an Actor
			Parameter Rules
				PrmExternalToEmailAddress
					if (PrmExternalToEmailAddress not entered)
						constraint(MessagePerson entered or MessageTeam entered)
							"MustEnter_a_Resource,_Team_and/or_Email_Address."
				PrmCcEmailAddress
					if (PrmCcEmailAddress entered)
						constraint(PrmExternalToEmailAddress entered)
							"MustEnter_To_E-mail_Address,_when_Cc_email_is_entered."
				PrmBccEmailAddress
					if (PrmBccEmailAddress entered)
						constraint(PrmExternalToEmailAddress entered)
							"MustEnter_To_E-mail_Address,_when_Bcc_email_is_entered."
				PrmSendingLandmarkNotifications
					if (MessagePerson not entered and MessageTeam not entered)
						constraint (not PrmSendingLandmarkNotifications entered)
							"MustEnterEither_Resource_and/or_TeamToSend_Notifications"
				DialogueTitle
					required
						"PleaseEnterASubject"
				Dialogue
					required
						"PleaseEnterYourMessage"
			Action Rules	
				if (MessagePerson entered)
					invoke Create 
						invoked.NavigateToThis = reference to FinanceResourceDialogue				
						invoked.HROrganization = HROrganization
						invoked.FinanceResource = MessagePerson
						invoked.DialogueTitle =	DialogueTitle
						invoked.Dialogue = Dialogue
						invoked.DialogueAttachment = DialogueAttachment
						invoked.SendToContact = MessagePerson
						invoked.Author = actor.agent(Employee).Employee
						invoked.DialogueLink = DialogueLink
						invoked.ResponseRequired = ResponseRequired
						invoked.MessageType = 0  

					invoke Create 
						invoked.NavigateToThis = reference to FinanceResourceDialogue				
						invoked.HROrganization = HROrganization
						invoked.FinanceResource = actor.agent(Employee).Employee
						invoked.DialogueTitle =	DialogueTitle
						invoked.Dialogue = Dialogue
						invoked.DialogueAttachment = DialogueAttachment
						invoked.SendToContact = MessagePerson
						invoked.Author = actor.agent(Employee).Employee
						invoked.DialogueLink = DialogueLink
						invoked.ResponseRequired = ResponseRequired
						invoked.MessageType = 1 

					if (PrmSendingLandmarkNotifications)
						PrmLocalActor = MessagePerson.FinanceResourceActor
						send notification
							to PrmLocalActor
							description is "<DialogueTitle>"
							priority is high
							detail is "<Dialogue>"

				if (MessageTeam entered) 
					LocalGroupingForTeam = FinanceResource.FinanceGroupRel.LastGroupingForTeam + 1


					LocalTeam = MessageTeam	
					for each SendToTeamMembersRel  
						if ((each.FinanceTeamMember.TeamMember != actor.agent(Employee).Employee)
						and (each.FinanceTeamMember.TeamMember != MessagePerson))
							invoke Create 
								invoked.NavigateToThis = reference to FinanceResourceDialogue				
								invoked.HROrganization = HROrganization
								invoked.FinanceResource = each.FinanceTeamMember.TeamMember
								invoked.DialogueTitle =	DialogueTitle
								invoked.Dialogue = Dialogue
								invoked.DialogueAttachment = DialogueAttachment
								invoked.SendToTeam = MessageTeam
								invoked.SendToContact = each.FinanceTeamMember.TeamMember
								invoked.Author = actor.agent(Employee).Employee
								invoked.DialogueLink = DialogueLink
								invoked.GroupingForTeam = LocalGroupingForTeam	
								invoked.ResponseRequired = ResponseRequired
								invoked.MessageType = 0	

							invoke Create 
								invoked.NavigateToThis = reference to FinanceResourceDialogue				
								invoked.HROrganization = HROrganization
								invoked.FinanceResource = actor.agent(Employee).Employee
								invoked.DialogueTitle =	DialogueTitle
								invoked.Dialogue = Dialogue
								invoked.DialogueAttachment = DialogueAttachment
								invoked.SendToTeam = MessageTeam
								invoked.SendToContact = each.FinanceTeamMember.TeamMember
								invoked.Author = actor.agent(Employee).Employee
								invoked.DialogueLink = DialogueLink
								invoked.GroupingForTeam = LocalGroupingForTeam	
								invoked.ResponseRequired = ResponseRequired
								invoked.MessageType = 1	

							if (PrmSendingLandmarkNotifications)
								PrmLocalActor = each.FinanceTeamMember.TeamMember.FinanceResourceActor															
								send notification
									to PrmLocalActor
									description is "<DialogueTitle>"
									priority is high
									detail is "<Dialogue>"

				if (PrmExternalToEmailAddress entered)
					send email
						to PrmExternalToEmailAddress
						cc PrmCcEmailAddress
						bcc PrmBccEmailAddress
						from actor.agent(Employee).EmployeeWorkEmailAddress
						subject "<DialogueTitle>"
						Attachments
							attachment DialogueAttachment.File
						Contents
								"<Dialogue>"
								blank line
								"<DialogueLink>"		







































					
