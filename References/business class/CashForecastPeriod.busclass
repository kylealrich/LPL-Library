CashForecastPeriod is a BusinessClass
	owned by cashmgmt
	prefix is CMCFP	

	Ontology
		symbolic key is CashForecastPeriod

	Persistent Fields
		BankDayNumber		is Numeric 8
		CreatedByStatement 	is a BankStatement
			delete cascades
			context of CashForecastAccount.CashManagementAccount
	
	Local Fields
		LocalNumberOfDays		is Numeric 4
		LocalNumberOfWeeks		is Numeric 4
		LocalNumberOfMonths		is Numeric 4

	Derived Fields
		CurrentDate is a DerivedField
			type is Date
			restricted
			return current corporate date
		
		CashForecastPeriodMonth is a DerivedField
			type is Numeric 2
			restricted
			return CashForecastPeriod month
			
		CashForecastPeriodAlphaMonth is a DerivedField
			type is Alpha 2
			restricted
			return CashForecastPeriod month
		
		CashForecastPeriodWeek is a DerivedField
			type is Numeric 2
			restricted
			return CashForecastPeriod week
			
		CashForecastPeriodDay is a DerivedField
			type is Numeric 3
			restricted
			return CashForecastPeriod day
		



				
		CashForecastPeriodYear is a DerivedField
			type is Numeric 4
			restricted
			return CashForecastPeriod year
			
		FormattedCashForecastPeriod is a DerivedField
			type is Alpha up to 25
			return CashForecastPeriodAlphaMonth + "/" + CashForecastPeriodDay + "/" + CashForecastPeriodYear
		
		Sunday is a MessageField
			restricted
			"Sunday"
			
		Monday is a MessageField
			restricted
			"Monday"
			
		Tuesday is a MessageField
			restricted
			"Tuesday"
			
		Wednesday is a MessageField
			restricted
			"Wednesday"
			
		Thursday is a MessageField
			restricted
			"Thursday"
			
		Friday is a MessageField
			restricted
			"Friday"	
		
		Saturday is a MessageField
			restricted
			"Saturday"
			
		DayOfWeek is a DerivedField
			type is Alpha 15
			if (CashForecastPeriod week day = 1)
				return Sunday
			else
			if (CashForecastPeriod week day = 2)
				return Monday
			else
			if (CashForecastPeriod week day = 3)
				return Tuesday
			else
			if (CashForecastPeriod week day = 4)
				return Wednesday
			else
			if (CashForecastPeriod week day = 5)
				return Thursday
			else
			if (CashForecastPeriod week day = 6)
				return Friday
			else
				return Saturday
		
		NumericDayOfWeek is a DerivedField
			type is Numeric 1
			restricted
			return (CashForecastPeriod week day) 
		
		DaysInCurrentWeek is a DerivedField
			type is Numeric 2
			restricted
			return (instance count of DaysInCurrentWeekRel)
			
		DaysInCurrentMonth is a DerivedField
			type is Numeric 2
			restricted
			return (instance count of DaysInCurrentMonthRel)
		
		SummarizedTransactionMessage is a MessageField
			restricted
			"SummarizedTransaction"
					
  			
  	Sets
  		ByBankDayNumber
  			Sort Order
  				CashManagementGroup
  				CashForecast
  				CashForecastAccount
  				BankDayNumber
  				CashForecastPeriod
  		
  	Relations
  		DaysInCurrentWeekRel
  			one-to-many relation to CashForecastPeriod
  			Field Mapping uses symbolic key
  				related.CashManagementGroup	= CashManagementGroup
				related.CashForecast 		= CashForecast
				related.CashForecastAccount = CashForecastAccount
			Instance Selection
				where (related.CashForecastPeriodWeek = CashForecastPeriodWeek)
  		
  		DaysInCurrentMonthRel
  			one-to-many relation to CashForecastPeriod
  			Field Mapping uses symbolic key
  				related.CashManagementGroup	= CashManagementGroup
				related.CashForecast 		= CashForecast
				related.CashForecastAccount = CashForecastAccount
			Instance Selection
				where (related.CashForecastPeriodMonth = CashForecastPeriodMonth)
  		
  	Actions
  		Create is a Create Action
			restricted
			
  		Update is an Update Action
  			restricted
  			
		CalculateForecastAverages is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCashManagementGroup	 	is a CashManagementGroup
				PrmCashForecast			 	is a CashForecast
				PrmCashManagementAccount 	is a CashManagementAccount
				PrmCashTransactionCategory	is a CashTransactionCategory
				PrmCategoryAmount		 	is an InternationalAmount
				PrmRollingDateRange			is a DateRange
				PrmDateRange				is a DateRange
				PrmRollingDays				is Numeric 4
				PrmRollingWeeks				is Numeric 4
				PrmRollingMonths			is Numeric 4
			
			Parameter Rules
				PrmCashManagementGroup	 	required
				PrmCashForecast			 	required
				PrmCashManagementAccount 	required
				PrmCashTransactionCategory	required
				PrmCategoryAmount			required
			
			Local Fields
				LocalCashForecastCategory	is a CashForecastCategory
				
			Instance Selection
				where (CashManagementGroup			 	  		 = PrmCashManagementGroup
				and	   CashForecast			 	  				 = PrmCashForecast
				and	   CashForecastAccount.CashManagementAccount = PrmCashManagementAccount
				and	   PrmCashTransactionCategory 				 = PrmCashTransactionCategory 
				and    CashForecastPeriod within PrmDateRange)
										
			Action Rules
				Instance Rules
					if (!PrmRollingDateRange entered)
						LocalNumberOfDays	= CashForecastAccount.NumberOfCalendarDays
						LocalNumberOfWeeks	= CashForecastAccount.NumberOfCalendarWeeks
						LocalNumberOfMonths	= CashForecastAccount.NumberOfCalendarMonths
					else
						LocalNumberOfDays	= PrmRollingDays
						LocalNumberOfWeeks	= PrmRollingWeeks
						LocalNumberOfMonths	= PrmRollingMonths

					LocalCashForecastCategory.CashTransactionCategory = PrmCashTransactionCategory
					invoke Create CashForecastDetail
						invoked.CashManagementGroup							 = CashManagementGroup
						invoked.CashForecast								 = CashForecast
						invoked.CashForecastAccount							 = CashForecastAccount
						invoked.CashForecastPeriod							 = CashForecastPeriod
						invoked.CashForecastCategory.CashTransactionCategory = PrmCashTransactionCategory
						invoked.Description									 = SummarizedTransactionMessage
						if (LocalCashForecastCategory.ForecastCreationParameter.HistoricalCalculationOption.DailyAverage)
							if (LocalCashForecastCategory.ForecastCreationParameter.CalculationAdjustment entered)
								invoked.ForecastAmount = ((PrmCategoryAmount / LocalNumberOfDays) * (1 + LocalCashForecastCategory.ForecastCreationParameter.CalculationAdjustment))
							else
								invoked.ForecastAmount = (PrmCategoryAmount / LocalNumberOfDays)
						if (LocalCashForecastCategory.ForecastCreationParameter.HistoricalCalculationOption.WeeklyAverage)
							if (LocalCashForecastCategory.ForecastCreationParameter.CalculationAdjustment entered)
								invoked.ForecastAmount = ((PrmCategoryAmount / LocalNumberOfWeeks / DaysInCurrentWeek) * (1 + LocalCashForecastCategory.ForecastCreationParameter.CalculationAdjustment))
							else
								invoked.ForecastAmount = (PrmCategoryAmount / LocalNumberOfWeeks / DaysInCurrentWeek)
						if (LocalCashForecastCategory.ForecastCreationParameter.HistoricalCalculationOption.MonthlyAverage)
							if (LocalCashForecastCategory.ForecastCreationParameter.CalculationAdjustment entered)
								invoked.ForecastAmount = ((PrmCategoryAmount / LocalNumberOfMonths / DaysInCurrentMonth) * (1 + LocalCashForecastCategory.ForecastCreationParameter.CalculationAdjustment))
							else
								invoked.ForecastAmount = (PrmCategoryAmount / LocalNumberOfMonths / DaysInCurrentMonth)

				Set Rules
					Exit Rules
						invoke UpdateActualAmountsOnForecast CashForecastPeriodAmount
							invoked.PrmCashManagementGroup							= PrmCashManagementGroup
							invoked.PrmCashForecast     							= PrmCashForecast
							invoked.PrmCashForecastAccount.CashManagementAccount	= PrmCashManagementAccount
							invoked.PrmCashForecastCategory.CashTransactionCategory	= PrmCashTransactionCategory	
														
  		Delete is a Delete Action
  			restricted
  			
  		Purge is a Purge Action
  			restricted
