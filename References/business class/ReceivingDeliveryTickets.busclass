ReceivingDeliveryTickets is a BusinessClass
	owned by po
	prefix is RDTI
	sql name is RecDelTickHdr
	classic name is PORDTI
	
	Ontology
		symbolic key is ReceivingDeliveryTickets
		
	Patterns
		implements StaticJava
		disable Auditing
 		disable EffectiveDated
 			
	Persistent Fields
		SourceDocumentOrigin
		OperatingCompany 			is like InventoryCompany
		SourceDocumentNumeric
		SourceDocumentAlpha
		SourceDocumentLineNumber 	is a LineNumber
		PurchaseOrder
		PurchaseOrderLine
		FromLocation 				is like InventoryLocation
        CurrentUser					is like Actor
			default label is "User"
        RequestingLocation			is like RequestingLocation
		RunBatchID 					is an IDMPID
	
	Derived Fields
		DerivedStatus is a DerivedField
			type is Alpha 20
			if (PurchaseOrderReceipt.Status = 0)
				return "Unreleased"
			else
			if (PurchaseOrderReceipt.Status = 1)
				return "Released"
			else
			if (PurchaseOrderReceipt.Status = 2)
				return "Create In Progress"
			else
			if (PurchaseOrderReceipt.Status = 5)
				return "Ready to Archive"
			else
			if (PurchaseOrderReceipt.Status = 7)
				return "DropShip"
			else
			if (PurchaseOrderReceipt.Status = 8)
				return "Release In Progress"
			else
			if (PurchaseOrderReceipt.Status = 9)
				return "Archived / Closed"
				
		DerivedRequester is a DerivedField
			type is Alpha size 13
			if (PurchaseOrderLineSourceRel.Requester entered)
				return PurchaseOrderLineSourceRel.Requester
			else
				return blank
				
		DerivedRequesterName is a DerivedField
			type is Alpha 230
			if (PurchaseOrderLineSourceRel.Requester entered)
				return PurchaseOrderLineSourceRel.Requester.Employee.PresentationNameSnapshot 
			else
				return blank
				
		DerivedFromCompany is a DerivedField
			type is like Company
			if (ReceivingDeliveryTicketLinesRel exists)
				return first ReceivingDeliveryTicketLinesRel.PurchaseOrderReceiptLine.Company
			else
				return blank
					
		DerivedFromCompanyName is a DerivedField
			type is like Name
			if (ReceivingDeliveryTicketLinesRel exists)
				return first ReceivingDeliveryTicketLinesRel.PurchaseOrderReceiptLine.Company.Name
			else
				return blank
								
		DerivedFromLocation is a DerivedField
			type is like InventoryLocation
			if (ReceivingDeliveryTicketLinesRel exists)
				return first ReceivingDeliveryTicketLinesRel.PurchaseOrderReceiptLine.ShipToLocation
			else
				return blank
								
		DerivedFromLocationName is a DerivedField
			type is like Name
			if (DerivedFromLocation entered)
				return first ReceivingDeliveryTicketLinesRel.PurchaseOrderReceiptLine.ShipToLocation.Name
			else
				return blank
								
		DerivedPurchaseFromLocationName is a DerivedField
			type is like VendorName
			if (PurchaseOrder.PurchaseFromLocation entered)
				return PurchaseOrder.PurchaseFromLocation.VendorName
			else
				return blank
		
		DerivedRequestingLocationName is a DerivedField
			type is like Name
			if (RequestingLocation entered)
				return RequestingLocationRel.Name
			else
				return blank
		
		DerivedRequestingCompany is a DerivedField
			type is like Name
			if (OperatingCompany entered)
				return OperatingCompanyRel.Name
			else
				return blank
		
		DerivedDeliveryDate is a DerivedField
			type is Date
			if (ReceivingDeliveryTicketLinesRel exists)
				return first ReceivingDeliveryTicketLinesRel.PurchaseOrderReceiptLine.PurchaseOrderLine.EarlyDeliveryDate
			else
				return blank
				
	Sets
		Set1
			Sort Order
				Company
				RequestingLocation
				PurchaseOrderReceipt
				ReceivingDeliveryTickets
		
		ByRunBatchID
			Sort Order
				Company
				CurrentUser
				RunBatchID
				RequestingLocation
				PurchaseOrderReceipt
				ReceivingDeliveryTickets

		BySourceDocument
			Sort Order
				Company
				CurrentUser
				RunBatchID
				SourceDocumentOrigin
				SourceDocumentNumeric
				RequestingLocation
				PurchaseOrderReceipt
				ReceivingDeliveryTickets
	
	Conditions
		DisplayRequisitionDetails
			when (SourceDocumentOrigin.Requisitions)

		HasLinesWithSpecialHandling
			when (SpecialHandlingReceivingDeliveryTicketLinesRel exists)
	
	Relations
	
		PurchaseOrderLineSourceRel
			one-to-one relation to PurchaseOrderLineSource
			Field Mapping uses symbolic key
				related.Company												= Company
				related.PurchaseOrder										= PurchaseOrder
				related.PurchaseOrderLine									= PurchaseOrderLine
				related.PurchaseOrderLineSource.SourceDocumentOrigin		= SourceDocumentOrigin
				related.PurchaseOrderLineSource.OperatingCompany			= OperatingCompany
				related.PurchaseOrderLineSource.SourceDocumentNumeric		= SourceDocumentNumeric
				related.PurchaseOrderLineSource.SourceDocumentAlpha			= SourceDocumentAlpha
				related.PurchaseOrderLineSource.SourceDocumentLineNumber	= SourceDocumentLineNumber
				
		RequisitionRel
			one-to-one relation to Requisition
			valid when (DisplayRequisitionDetails)
			Field Mapping uses symbolic key
				related.Company												= Company
				related.Requisition											= SourceDocumentNumeric
				
		OperatingCompanyRel
			one-to-one relation to InventoryCompany
			Field Mapping uses symbolic key
				related.Company												= OperatingCompany
				
		RequestingLocationRel	
			one-to-one relation to RequestingLocation
			Field Mapping uses symbolic key
				related.Company												= OperatingCompany
				related.RequestingLocation									= RequestingLocation
				
		FromLocationRel
			one-to-one relation to VendorLocation
			Field Mapping uses symbolic key
				related.VendorGroup											= PurchaseOrder.Vendor.VendorGroup
				related.Vendor												= PurchaseOrder.Vendor
				related.VendorLocation										= FromLocation
				
		FromLocInventoryLocationRel
			one-to-one relation to InventoryLocation
			Field Mapping uses symbolic key
				related.Company												= Company
				related.InventoryLocation									= FromLocation
				
		ReceivingDeliveryTicketLinesRel is a ReceivingDeliveryTicketLines set

		SpecialHandlingReceivingDeliveryTicketLinesRel is a ReceivingDeliveryTicketLines set
			Instance Selection
				where (related.HasLinesWithSpecialHandling
				and related.PurchaseOrderReceiptLine.DerivedSpecialHandlingCode entered
				and related.PurchaseOrderReceiptLine.IsNonInventory)

       	PurchaseOrderReceiptCommentsForIDMRel
    		one-to-many relation to PurchaseOrderReceiptComment
    		Field Mapping uses ByCommentType
    			related.Company				 = Company
    			related.PurchaseOrderReceipt = PurchaseOrderReceipt
    		Instance Selection
    			where (related.CommentType.PrintOnInternalDocuments
    			or     related.CommentType.PrintOnDeliveryTicket)  
		

	Field Rules
	
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
		
		Delete is a Delete Action
			restricted
					
		DeleteReceivingDeliveryTickets is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCompany	  	is like PurchasingCompany
				PrmActor 		is like Actor
				PrmRunBatchID	is like IDMPID

			Instance Selection
				where (Company 		= PrmCompany 
				and	   CurrentUser 	= PrmActor
				and	   RunBatchID 	= PrmRunBatchID)
				
			Action Rules
				Instance Rules
					invoke Delete
					
