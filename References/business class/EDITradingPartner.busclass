EDITradingPartner is a BusinessClass
    owned by edi
    prefix is TPT

    Ontology
        symbolic key is EDITradingPartner
            classic set name is TPTSET1
            classic name is EDI-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        GroupId                             is Alpha size 15
            default label is "OrganizationID"
        Direction
        Tpid                                is AlphaUpper size 10
            default label is "TradingPartnerID"
        Process
        Map                      
        EDITransactionType
            default label is "TransactionSet"
            classic name is EDITRANS-TYPE
        StdVersion
            default label is "StandardsVersion" 
        AckRequest                          is AlphaUpper size 1
            default label is "AcknowledgementRequest"
            States
                NoneRequested               value is "0"
                AcknowledgementRequested    value is "1"
        IsaSenderIdQualifier                is an InterchangeIdQualifier
            default label is "SenderQualifier"
            classic name is ISA-ID-QUAL
        IsaSenderId                         is an InterchangeID
            default label is "Sender_(ISA06)"
            classic name is ISA-ID
        IsaReceiverIdQualifier              is an InterchangeIdQualifier
            default label is "ReceiverQualifier"
        IsaReceiverId                       is an InterchangeID
            default label is "Receiver_(ISA08)"
        GsFuncId                            is Alpha size 2
            default label is "FunctionalID"
        GsSenderId                          is an InterchangeID
            default label is "Sender_(GS02)"
        GsReceiverId                        is an InterchangeID
            default label is "Receiver_(GS03)"
        Type                                is AlphaUpper size 2
            default label is "Production\/\Test"
            States
                Production  value is "P"
                Test        value is "T"
        ElementSeparator                    is Alpha size 1
        CompElementSeparator                is Alpha size 1
            default label is "ComponentElementSeparator"
        SegmentTerminator                   is AlphaUpper size 2
        EDICarrier                          is a PfiEDICarrier
            classic name is EDICARRIER
        Input                               is a SourceBusinessClass
            default label is "DataFrom"
        Output                              is a DestinationBusinessClass  
            default label is "OutputTo"
        RunGroup                            
        Active                              is Boolean
        OneSetPerInterchange				is Boolean
        VendorID                            is like Vendor
            default label is "VendorID"
    Derived Fields

        InterchangeControlNumber is a DerivedField
            type is like ControlNumber
            return IsaRel.ControlNumber

        GroupControlNumber is a DerivedField
            type is like ControlNumber
            return GsRel.ControlNumber

        TradingPartnerStatusDisplay is a DerivedField
			type is MessageField
            default label is "Status"
            if (Active)
                return ActiveMessage
            else
				return InactiveMessage
        
        ActiveMessage is a MessageField
            restricted
            "Active"
        
        InactiveMessage is a MessageField
            restricted
            "Inactive"


    Relations

        EDITransactionHistoryRel
            one-to-many relation to EDITransactionHistory
            Field Mapping uses Set1
                related.EDITradingPartner = EDITradingPartner

        IsaRel
            one-to-one relation to IsaControlNumber
            Field Mapping uses symbolic key
                related.IsaControlNumber.SenderId = IsaSenderId
                related.IsaControlNumber.ReceiverId = IsaReceiverId

        GsRel
            one-to-one relation to GsControlNumber
            Field Mapping uses symbolic key
                related.GsControlNumber.SenderId = GsSenderId
                related.GsControlNumber.ReceiverId = GsReceiverId

        DuplicateSet3Rel
            one-to-many relation to EDITradingPartner
            Field Mapping uses Set3
                related.Direction = Direction
                related.IsaSenderId = IsaSenderId
                related.IsaReceiverId = IsaReceiverId
                related.GsSenderId = GsSenderId
                related.GsReceiverId = GsReceiverId
                related.StdVersion = StdVersion
                related.EDITransactionType = EDITransactionType
            Instance Selection
                where (related.EDITradingPartner != EDITradingPartner)

        EDITradingPartnerCommentRel
            one-to-many relation to EDITradingPartnerComment
            Field Mapping uses part of key
                related.EDITradingPartner = EDITradingPartner

        RelatedByTpidRel
            one-to-many relation to EDITradingPartner
            Field Mapping uses symbolic key
            
            Instance Selection
                where   ( related.EDITradingPartner != EDITradingPartner
                and related.Tpid = Tpid)

    Sets
        Set2
            indexed
            Sort Order
                EDITradingPartner
                Input

        Set3
            indexed
            Sort Order
                Direction
                IsaSenderId
                IsaReceiverId
                GsSenderId
                GsReceiverId
                StdVersion
                EDITransactionType

        FromIsaRel
            indexed
            Sort Order
                IsaSenderId
                IsaReceiverId
                EDITradingPartner

        FromGsRel
            indexed
            Sort Order
                GsSenderId
                GsReceiverId
                EDITradingPartner


    Conditions
        IsaExists
            restricted
            when (IsaRel exists)

        GsExists
            restricted
            when (GsRel exists)

        InstanceIsAnOutbound850
            restricted
            when (this instance.Direction.Out
                and this instance.EDITransactionType.850)
        IsOutboundData
            when ( Direction = 1)

        RelatedByTpidExists
            restricted
            when (RelatedByTpidRel exists)

        TransactionHistoryExists
            restricted
            when (EDITransactionHistoryRel exists)

        VendorIDExists
            when (VendorID != blank)
    Field Rules
        IsaSenderIdQualifier
            default to "01"
        IsaReceiverIdQualifier
            default to "01"
        IsaReceiverId
            required
        IsaSenderId
            required
        GsReceiverId
            required
        GsSenderId
            required
        EDITransactionType
            required
            if (Direction.Out)
                constraint (EDITransactionType.850
                or          EDITransactionType.820
                or			EDITransactionType.828
                or			EDITransactionType.867
                or			EDITransactionType.997)
                    "EDITransactionTypeIsNotSupportedByDirection"
                if(Process.PurchaseOrderTo850)
                    default to EDITransactionType.850
                if(Process.EFTPaymentTo820)
                	default to EDITransactionType.820
                if(Process.PositivePayTo828)
                	default to EDITransactionType.828
                if(Process.ProductTransferResaleTo867)
                	default to EDITransactionType.867
                if(Process.FunctionalAcknowledgement997)
                	default to EDITransactionType.997
            else
                constraint(EDITransactionType.855
                or        EDITransactionType.856
                or        EDITransactionType.810
                or        EDITransactionType.832
                or        EDITransactionType.820
                or        EDITransactionType.821
                or        EDITransactionType.823)
                    "EDITransactionTypeIsNotSupportedByDirection"
                if(Process.POAcknowledgementFrom855)
                    default to EDITransactionType.855
                else
                if(Process.ReceiptFrom856)
                    default to EDITransactionType.856
                else
                if(Process.MatchInvoiceFrom810)
                    default to EDITransactionType.810
                else
                if(Process.ContractInterfaceCatalogFrom832)
                	default to EDITransactionType.832

        Tpid
            required
        StdVersion
            required
            initial value is "004010"
            default to "004010"
        Input
            if (Process.PurchaseOrderTo850)
            	default to SourceBusinessClass.PurchaseOrder
            else
            if (Process.PaymentOutputTo820)
            	default to SourceBusinessClass.PaymentOutputFile
            else
            if (Process.EFTPaymentTo820)
            	default to SourceBusinessClass.EFTPaymentOutput
            else
            if (Process.PositivePayTo828)
            	default to SourceBusinessClass.PositivePayOutput
            else
            if(Process.ProductTransferResaleTo867)
                	default to SourceBusinessClass.ProductTransferResale

            if(Direction.Out)
                required
        GsFuncId
            initial value is "PO"
            default to "PO"
            if(Direction.Out)
                required
                if (EDITransactionType = EDITransactionType.850)
                    default to "PO"
                if (EDITransactionType = EDITransactionType.867)
                    default to "PT"
            else
                initialize
        Type
            initial value is Type.Production
            default to Type.Production
            if(Direction.Out)
                required
        CompElementSeparator
            initial value is "|"
            default to "|"
            if(Direction.Out)
                required
        ElementSeparator
            initial value is "^"
            default to "^"
            if(Direction.Out)
                required
        SegmentTerminator
            initial value is "~"
            default to "~"
            if(Direction.Out)
                required
        Output
            if(Direction.In)
                required
            if(Direction.In and Output not entered)
                if (EDITransactionType = EDITransactionType.855)
                    default to DestinationBusinessClass.POAcknowledgement
                if (EDITransactionType = EDITransactionType.856)
                    default to DestinationBusinessClass.Receiving
                if (EDITransactionType = EDITransactionType.810)
                    default to DestinationBusinessClass.MatchInvoice
                if (EDITransactionType = EDITransactionType.832)
                	default to DestinationBusinessClass.ContractCatalog
                if (EDITransactionType = EDITransactionType.821)				
                	default to DestinationBusinessClass.CashLedgerBankUpdate
                if (EDITransactionType = EDITransactionType.823)				
                	default to DestinationBusinessClass.ReceivableLockBoxImport
                if (EDITransactionType = EDITransactionType.820)				
                	default to DestinationBusinessClass.ReceivablePaymentImport
                
        RunGroup
            if(Direction.In and RunGroup not entered)
                default to EDITradingPartner

        Direction
            required
            if (Direction.Out)
                initialize Output
            else
                initialize IsaSenderIdQualifier
                initialize IsaReceiverIdQualifier
                initialize GsFuncId
                initialize Type
                initialize ElementSeparator
                initialize CompElementSeparator
                initialize SegmentTerminator
            if(EDITransactionType.850
            or EDITransactionType.867
            or EDITransactionType.828)			
                default to Direction.Out
            else
            if(EDITransactionType.855
            or EDITransactionType.810
            or EDITransactionType.856
            or EDITransactionType.832
            or EDITransactionType.821
            or EDITransactionType.823)		
                default to Direction.In

        EDICarrier
            required

        AckRequest
            default to "0"

        Process
            required
            if(Direction.Out)
                if(EDITransactionType.850)
                    default to Process.PurchaseOrderTo850
                    constraint(Process.PurchaseOrderTo850)
                        "ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.867)
                    default to Process.ProductTransferResaleTo867
                    constraint(Process.ProductTransferResaleTo867)
                        "ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.828)
                	default to Process.PositivePayTo828
                	constraint(Process.PositivePayTo828)
                		"ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.820)
                	default to Process.PaymentOutputTo820
                	constraint(Process.PaymentOutputTo820
                	or 		   Process.EFTPaymentTo820)
                		"ProcessDoesNotSupportEDITransactionType"            
            else
                if(EDITransactionType.855)
                    default to Process.POAcknowledgementFrom855
                    constraint(Process.POAcknowledgementFrom855)
                        "ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.810)
                    default to Process.MatchInvoiceFrom810
                    constraint(Process.MatchInvoiceFrom810)
                        "ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.856)
                    default to Process.ReceiptFrom856
                    constraint(Process.ReceiptFrom856)
                        "ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.832)
                    default to Process.ContractInterfaceCatalogFrom832
                    constraint(Process.ContractInterfaceCatalogFrom832)
                        "ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.820)
                	default to Process.ReceivablePaymentImportFrom820
                	constraint(Process.ReceivablePaymentImportFrom820)
                		"ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.821)
                	default to Process.CashLedgerUpdateFrom821
                	constraint(Process.CashLedgerUpdateFrom821)
                		"ProcessDoesNotSupportEDITransactionType"
                else
                if(EDITransactionType.823)
                	default to Process.ReceivableLockBoxImportFrom823
                	constraint(Process.ReceivableLockBoxImportFrom823)
                		"ProcessDoesNotSupportEDITransactionType"

        Map
        	required      
            if (EDITransactionType.820
            or 	EDITransactionType.821
            or	EDITransactionType.823
            or	EDITransactionType.828)
           		default to Map.JPM
            	constraint(Map.JPM or Map.WF)
            		"Only_\J\P\MOr\W\FMapSupportedForTransactionType"
            else
           		default to Map.LAW

        Active
            initial value is true
            default to true
            if (Direction.Out)
                constraint(Active = true)
                    "OutboundTradingPartnerCannotBeInactive"
            
       	OneSetPerInterchange
            initial value is false
            default to false
            
    Rule Blocks
        CreateUpdateEdits
            constraint(DuplicateSet3Rel not exists)
                "TradingPartnerAlreadyExists"
            if (Direction.Out)
                constraint(CompElementSeparator != ElementSeparator
                and        CompElementSeparator != SegmentTerminator
                and        ElementSeparator != SegmentTerminator)
                    "SeparatorsAndTerminatorsCannotHaveRepeatingValues"

        CreateIsa
            if (!IsaExists)
                invoke Create IsaControlNumber
                    invoked.IsaControlNumber.SenderId            = IsaSenderId
                    invoked.IsaControlNumber.ReceiverId            = IsaReceiverId

        CreateGs
            if (!GsExists)
                invoke Create GsControlNumber
                    invoked.GsControlNumber.SenderId            = GsSenderId
                    invoked.GsControlNumber.ReceiverId            = GsReceiverId
			
    Actions
        Create is a Create Action
            Entrance Rules
                include CreateUpdateEdits
                include CreateIsa
                include CreateGs

        Update is an Update Action
            Entrance Rules
                include CreateUpdateEdits
                include CreateIsa
                include CreateGs

        Delete is a Delete Action

        CreateControlRecords is a Set Action
            restricted

            Action Rules
                Instance Rules
                    invoke CreateControlNumber

        CreateControlNumber is an Instance Action
            restricted
            Action Rules
                if (!IsaExists)
                    invoke Create IsaControlNumber
                        invoked.IsaControlNumber.SenderId            = IsaSenderId
                        invoked.IsaControlNumber.ReceiverId            = IsaReceiverId
                if (!GsExists)
                    invoke Create GsControlNumber
                        invoked.GsControlNumber.SenderId            = GsSenderId
                        invoked.GsControlNumber.ReceiverId            = GsReceiverId

        CreateInboundPartner is an Instance Action
            valid when (InstanceIsAnOutbound850)
                
            Parameters
                PrmEDINumber      is AlphaUpper size 15
                    default label is "NewEDINumber"
                PrmTransaction    is a EDITransactionType
                    default label is "TransactionSet"
                PrmRunGroup       is a RunGroup
                    default label is "RunGroup"
                
            Parameter Rules
                PrmTransaction
                    required
                PrmEDINumber
                    required
                    initial value is EDITradingPartner
                
            Action Rules
                
                invoke Create EDITradingPartner
                    fill in fields from this instance
                        except invoked.Process
                    invoked.EDITradingPartner =   PrmEDINumber
                    invoked.RunGroup =            PrmRunGroup 
                    invoked.Direction = 2
                    invoked.EDITransactionType = PrmTransaction
                    invoked.IsaSenderId = IsaReceiverId
                    invoked.GsSenderId = GsReceiverId
                    invoked.IsaReceiverId = IsaSenderId
                    invoked.GsReceiverId = GsSenderId
                    
        NewFromSelected is an Instance Action
                
            Parameters
                PrmEDINumber                          is like EDITradingPartner 
                PrmGroupId                            is Alpha size 15
                PrmDirection                          is a Direction
                PrmTpid                               is AlphaUpper size 10
                PrmProcess                            is a Process
                PrmMap                                is a Map
                PrmEDITransactionType                 is a EDITransactionType
                PrmStdVersion                         is a StdVersion
                PrmAckRequest                         is AlphaUpper size 1
                    States
                        NoneRequested               value is "0"
                        AcknowledgementRequested    value is "1"
                PrmIsaSenderIdQualifier               is an InterchangeIdQualifier
                PrmIsaSenderId                        is an InterchangeID
                PrmIsaReceiverIdQualifier             is an InterchangeIdQualifier
                PrmIsaReceiverId                      is an InterchangeID
                PrmGsFuncId                           is Alpha size 2
                PrmGsSenderId                         is an InterchangeID
                PrmGsReceiverId                       is an InterchangeID
                PrmType                               is AlphaUpper size 2
                PrmElementSeparator                   is Alpha size 1
                PrmCompElementSeparator               is Alpha size 1
                PrmSegmentTerminator                  is AlphaUpper size 2
                PrmEDICarrier                         is a PfiEDICarrier
                PrmInput                              is a SourceBusinessClass
                PrmOutput                             is a DestinationBusinessClass
                PrmRunGroup                           is a RunGroup
                PrmActive                             is Boolean
                PrmOneSetPerInterchange				  is Boolean
                PrmVendorID                           is like Vendor

            Parameter Rules
                PrmEDINumber
                    initial value is EDITradingPartner
                PrmGroupId
               	    initial value is GroupId
                PrmDirection
               	    initial value is Direction
                PrmTpid
               	    initial value is Tpid
                PrmProcess
               	    initial value is Process
                PrmMap
               	    initial value is Map
                PrmEDITransactionType
               	    initial value is EDITransactionType
                PrmStdVersion
               	    initial value is StdVersion
                PrmAckRequest
               	    initial value is AckRequest
                PrmIsaSenderIdQualifier
               	    initial value is IsaSenderIdQualifier
                PrmIsaSenderId
               	    initial value is IsaSenderId
                PrmIsaReceiverIdQualifier
               	    initial value is IsaReceiverIdQualifier
                PrmIsaReceiverId
               	    initial value is IsaReceiverId
                PrmGsFuncId
               	    initial value is GsFuncId
                PrmGsSenderId
               	    initial value is GsSenderId
                PrmGsReceiverId
               	    initial value is GsReceiverId
                PrmType
                PrmElementSeparator
               	    initial value is ElementSeparator
                PrmCompElementSeparator
               	    initial value is CompElementSeparator
                PrmSegmentTerminator
               	    initial value is SegmentTerminator
                PrmEDICarrier
               	    initial value is EDICarrier
                PrmInput
               	    initial value is Input
                PrmOutput
               	    initial value is Output
                PrmRunGroup
               	    initial value is RunGroup
                PrmActive                          
                    initial value is Active
                PrmOneSetPerInterchange
                	initial value is OneSetPerInterchange
                PrmVendorID
                    initial value is VendorID

            Action Rules
                
                invoke Create EDITradingPartner

























                    
                    invoked.EDITradingPartner        =  PrmEDINumber                           
                    invoked.GroupId                  =  PrmGroupId                               
                    invoked.Direction                =  PrmDirection                             
                    invoked.Tpid                     =  PrmTpid                                  
                    invoked.Process                  =  PrmProcess                               
                    invoked.Map                      =  PrmMap                                   
                    invoked.EDITransactionType       =  PrmEDITransactionType                    
                    invoked.StdVersion               =  PrmStdVersion                            
                    invoked.AckRequest               =  PrmAckRequest                            
                    invoked.IsaSenderIdQualifier     =  PrmIsaSenderIdQualifier                  
                    invoked.IsaSenderId              =  PrmIsaSenderId                           
                    invoked.IsaReceiverIdQualifier   =  PrmIsaReceiverIdQualifier                
                    invoked.IsaReceiverId            =  PrmIsaReceiverId                         
                    invoked.GsFuncId                 =  PrmGsFuncId                              
                    invoked.GsSenderId               =  PrmGsSenderId                            
                    invoked.GsReceiverId             =  PrmGsReceiverId                          
                    invoked.Type                     =  PrmType                                  
                    invoked.ElementSeparator         =  PrmElementSeparator                      
                    invoked.CompElementSeparator     =  PrmCompElementSeparator                  
                    invoked.SegmentTerminator        =  PrmSegmentTerminator                     
                    invoked.EDICarrier               =  PrmEDICarrier                            
                    invoked.Input                    =  PrmInput                                 
                    invoked.Output                   =  PrmOutput                                
                    invoked.RunGroup                 =  PrmRunGroup                              
                    invoked.Active                   =  PrmActive 
                    invoked.OneSetPerInterchange	 =  PrmOneSetPerInterchange
                    invoked.VendorID                 =  PrmVendorID
                                    
                
