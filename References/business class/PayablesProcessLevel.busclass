PayablesProcessLevel is a BusinessClass
    owned by ap
    prefix is APR
    classic name is APPROCLEV

    Ontology
        symbolic key is PayablesProcessLevel
            classic set name is APRSET1
            classic name is PROC-LEVEL

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		Image					is an ImageAttachment
        Name
        PostalAddress			is a PostalAddressV2	
        	holds pii
            classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
            classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
            classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
            classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
            classic name for PostalAddress.Municipality is CITY-ADDR5
            classic name for PostalAddress.StateProvince is STATE-PROV
            classic name for PostalAddress.Country is COUNTRY-CODE
        AccrualCode               is a PayablesAccrualCode
            classic name is ACCR-CODE
        CashCode
        IncomeWithholdingCode
            classic name is INC-ACCR-CODE
        DiscountCode              is a PayablesDiscountCode
        PayGroup
        DefaultProcessLevel       is Boolean
            classic name is DEF-PROC-LEVEL
        MatchProcessLevel         is Boolean
            classic name is MATCH-PROC-LEV
        BillToOnly				  is Boolean		
        DistributionCode          is a PayablesDistributionCode
            classic name is DIST-CODE
        RetainageAccrualCode      is a PayablesAccrualCode
            classic name is RET-ACCR-CODE
        SecondaryWithholdingCode1 is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE1
        SecondaryWithholdingCode2 is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE2
        SecondaryWithholdingCode3 is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE3
        Active                    is Boolean
            classic name is ACTIVE-STATUS
        InvoiceRoutingCategory

        ReceiptAccrualAccount           is a FinanceCodeBlock
            classic name for ReceiptAccrualAccount.AccountingUnit is ACC-ACCT-UNIT
            classic name for ReceiptAccrualAccount.GeneralLedgerChartAccount is ACC-ACCOUNT
        CostVarianceAccount             is a FinanceCodeBlock
            classic name for CostVarianceAccount.AccountingUnit is VAR-ACCT-UNIT
            classic name for CostVarianceAccount.GeneralLedgerChartAccount is VAR-ACCOUNT
        ToleranceOffsetAccount          is a FinanceCodeBlock
            classic name for ToleranceOffsetAccount.AccountingUnit is TOL-ACCT-UNIT
            classic name for ToleranceOffsetAccount.GeneralLedgerChartAccount is TOL-ACCOUNT
        ReceiptWriteOffAccount          is a FinanceCodeBlock
            classic name for ReceiptWriteOffAccount.AccountingUnit is REC-ACCT-UNIT
            classic name for ReceiptWriteOffAccount.GeneralLedgerChartAccount is REC-ACCOUNT
        MatchedNotReceivedAccount       is a FinanceCodeBlock
            classic name for MatchedNotReceivedAccount.AccountingUnit is MNR-ACCT-UNIT
            classic name for MatchedNotReceivedAccount.GeneralLedgerChartAccount is MNR-ACCOUNT
        MatchWriteOffAccount            is a FinanceCodeBlock
            classic name for MatchWriteOffAccount.AccountingUnit is MWO-ACCT-UNIT
            classic name for MatchWriteOffAccount.GeneralLedgerChartAccount is MWO-ACCOUNT
        PrepayAccount                        is a FinanceCodeBlock
        AddOnChargeDifferenceAccount    is a FinanceCodeBlock
            classic name for AddOnChargeDifferenceAccount.AccountingUnit is AOCD-ACCT-UNIT
            classic name for AddOnChargeDifferenceAccount.GeneralLedgerChartAccount is AOCD-ACCOUNT
        UnderbillAccount                is a FinanceCodeBlock
            classic name for UnderbillAccount.AccountingUnit is UBILL-ACCT-UNT
            classic name for UnderbillAccount.GeneralLedgerChartAccount is UBILL-ACCOUNT
        InventoryReceiptArchivalAccount is a FinanceCodeBlock
            classic name for InventoryReceiptArchivalAccount.AccountingUnit is IRA-ACCT-UNIT
            classic name for InventoryReceiptArchivalAccount.GeneralLedgerChartAccount is IRA-ACCOUNT
        InvoiceItemDetailAccount	  	is a FinanceCodeBlock
        TaxCode


		ICBillingDefaultExpenseAccount	is a FinanceCodeBlock
		MatchPrepayAccrualCode          is a PayablesAccrualCode		
			classic name is MAT-ACCR-CODE			
		
	Transient Fields
		TransientAccrualCodeBlock				is a FinanceCodeBlock
			derive value from first RNILineRel.TransientAccrualBlock
		TransientFEG 						is like FinanceEnterpriseGroup	
			derive value from Company.FinanceEnterpriseGroup
		ObjectReference 					is BusinessObjectReference	
			derive value from reference to this instance




















	Context Fields
		PurchaseOrderResult
	
	Local Fields

		LocalRecStatus 	is Numeric size 1
            States
                Unreleased              value is 0
                Released                value is 1
                SelectedForPayment      value is 2
                SystemGeneratedPayment  value is 3
                ManualPaid              value is 4
                DraftPayable            value is 5
                UnreleasedManualPayment value is 7
                Cancel                  value is 8
                Historical              value is 9
        CalculateTax										
		VertexErrorNumber			is Numeric 3 			
		WebServiceError     		is Boolean				
		MultipleTaxCodes    		is Boolean				
		WebServiceErrorMessage 		is Text					
		LocalLookupKey 				is Alpha 100			
		LocalSequence 				is like Sequence		
        OldTaxCode					is like TaxCode			
 		NewTaxCode					is like TaxCode			
 		CalculateTaxOutputMessage	is Alpha 200			

    Derived Fields
    	RNIRunPostDate is a DerivedField
    		type is Date
    		return first RNILinesInventoryItemTypeRel.PostDate
    	DerivedRNILineInventoryItemTotalExtendedAmount is a DerivedField
    		type is like InternationalCost
    		return sum RNILinesInventoryItemTypeRel.BaseCurAmt

    	DerivedRNILineNonStockItemTotalExtendedAmount is a DerivedField
    		type is like InternationalCost
    		return sum RNILinesNonStockItemTypeRel.BaseCurAmt

    	DerivedRNILineSpecialItemTotalExtendedAmount is a DerivedField
    		type is like InternationalCost
    		return sum RNILinesSpecialItemTypeRel.BaseCurAmt
    	PLRniQty is a DerivedField
    		type is like Quantity
    		return sum RNILineListForProcessLevelRel.RniQty
    	PLBaseExtendedTotal	is a DerivedField
    		type is like InternationalCost
    		return sum RNILineListForProcessLevelRel.BaseCurAmt
    	DerivedRNIProcessLevelCreditTotal is a DerivedField
    		type is like InternationalCost
    		return sum RNILineRel.DerivedLineCreditAmount		

		CompletionMessage is a DerivedField 			
   			type is MessageField
   			restricted
   			if (VertexErrorNumber entered)
   				return TaxErrorMessage
   			else
   			if (WebServiceError)
				return VertexWebserviceErrorMessage
   			else	
   			if (MultipleTaxCodes)
   				return VertexMultTaxCodesMessage
   			else
   			if (TaxCode entered)
   				return VertexCompletionMessage
   			else
   				return blank

		TaxErrorMessage is a MessageField				
			restricted
			"TaxErrorOccurred;TaxCodeNotUpdated"

		VertexWebserviceErrorMessage is a MessageField	
			restricted
			"NoTaxCodesReturnedDueToA\VertexError"

		VertexMultTaxCodesMessage is a MessageField		
			restricted
			"TaxCodeNotUpdated.MultipleTaxCodesExistsForVendorsAddress"

		VertexCompletionMessage	is a MessageField		
			restricted
			"UpdateTaxCodeComplete"

		VertexNoChangeMessage is a LabelField			
			"NoChangeNecessary,Correct\Tax\Area\IdExists"

		OldTaxCodeMsg is a MessageField					
			restricted
			"OldTaxCode:<OldTaxCode>"

		DerivedLookupMessage is a DerivedField			
			type is MessageField
			restricted
			if (NewTaxCode = OldTaxCode)
				return VertexNoChangeMessage
			else
				return CalculateTaxOutputMessage

		TaxCodeMessage is a DerivedField				
			type is MessageField
			restricted
			if (NewTaxCode = OldTaxCode)
				return blank
			else
				return OldTaxCodeMsg

		ProcessLevelAndName is a MessageField
			"<Name>_(<PayablesProcessLevel>)"

		CreateMessage is a LabelField
			restricted
			"CreatePayablesProcessLevel"
		
		FormLabel is a LabelField
			restricted
			"PayablesProcessLevel:_"

		RepresentativeText is a StringField
			type is Text
			default label is "PayablesProcessLevel"
			PayablesProcessLevel " - " Name
		
		DerivedPayablesProcessLevelTitle	is a DerivedField
			type is Alpha size up to 132
			default label is "PayablesProcessLevel"
			return RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return FormLabel + DerivedPayablesProcessLevelTitle
			else
				return CreateMessage

    Conditions
    	MNRReconciliationSubSystemBalanceRelExists
    		when (MNRReconciliationSubSystemBalanceRel exists)
    	RNIReconciliationSubSystemBalanceRelExists
    		when (RNIReconciliationSubSystemBalanceRel exists)
        RecordExists
			restricted
			when (PayablesProcessLevel exists)
		
		IsMatchProcessLevel
            classic name is MATCH-PL
            restricted
            when (MatchProcessLevel)
		RNILineListForProcessLevelRelExists
			when (RNILineListForProcessLevelRel exists)
		
		RNILinesInventoryItemTypeRelExists
			when (RNILinesInventoryItemTypeRel exists)
			
		RNILinesNonStockItemTypeRelExists
			when (RNILinesNonStockItemTypeRel exists)
			
		RNILinesSpecialItemTypeRelExists
			when (RNILinesSpecialItemTypeRel exists)

 		VertexResultsExist	
 			restricted
			when (VertexTaxAreaIdResultsRel exist
			and  !VertexTaxAreaIdResultsRel.WebServiceError)

		VertexErrorExist	
			restricted
			when (VertexWebserviceErrorRel exist)

		BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "CashPaymentRegister"		
			or    parentcontext.name = "PurchaseOrderEDIOutput"
			or    parentcontext.name = "PayablesReportableIncomeEntity")

    Relations
    	RNILineRel
    		one-to-many relation to RNILine
    		Field Mapping uses symbolic key
				related.Company = Company
    			related.PurchaseOrderResult 			= PurchaseOrderResult
    		Instance Selection
    			where (related.ProcessLevel = PayablesProcessLevel)

    	RNINonInventoryLineRel
    		one-to-many relation to RNILine
    		Field Mapping uses symbolic key
    			related.Company = Company
    			related.PurchaseOrderResult 			= PurchaseOrderResult
    		Instance Selection
    			where (related.IsNonInventoryType
    			and	   related.ProcessLevel = PayablesProcessLevel)


    	RNILinesInventoryItemTypeRel
    		one-to-many relation to RNILine
    		Field Mapping uses symbolic key
    			related.Company = Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    		Instance Selection
    			where (related.IsInventoryType
    			and	   related.ProcessLevel = PayablesProcessLevel)
    	
    	RNILinesNonStockItemTypeRel
    		one-to-many relation to RNILine
    		Field Mapping uses symbolic key
    			related.Company = Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    		Instance Selection
    			where (related.IsNonStockType
    			and	   related.ProcessLevel = PayablesProcessLevel)    	

    	RNILinesSpecialItemTypeRel
    		one-to-many relation to RNILine
    		Field Mapping uses symbolic key
    			related.Company = Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    		Instance Selection
    			where (related.IsSpecialType
    			and	   related.ProcessLevel = PayablesProcessLevel)

    	RNILineListForProcessLevelRel
    		one-to-many relation to RNILine
    		Field Mapping uses symbolic key
    			related.Company = Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    		Instance Selection
    			where (related.ProcessLevel = PayablesProcessLevel)
    		
		DefaultPayablesProcessLevelRel
			one-to-many relation to PayablesProcessLevel
			Field Mapping uses symbolic key
				related.Company 					= Company
			Instance Selection
				where (related.DefaultProcessLevel 	= true
				and    related.PayablesProcessLevel	!= PayablesProcessLevel)
				
		PayablesAccrualCodeDetailRel
			one-to-one relation to PayablesAccrualCodeDetail
			Field Mapping uses Set1
				related.VendorGroup			= Company.VendorGroup
				related.Company				= Company
				related.PayablesAccrualCode	= AccrualCode

		PayablesMatchPrepayAccrualCodeDetailRel		
			one-to-one relation to PayablesAccrualCodeDetail
			Field Mapping uses Set1
				related.VendorGroup			= Company.VendorGroup
				related.Company				= Company
				related.PayablesAccrualCode	= MatchPrepayAccrualCode	

		RetainageAccrualCodeDetailRel
			one-to-one relation to PayablesAccrualCodeDetail
			Field Mapping uses Set1
				related.VendorGroup			= Company.VendorGroup
				related.Company				= Company
				related.PayablesAccrualCode	= RetainageAccrualCode
				
		DiscountCodeRel
			one-to-one relation to PayablesDiscountCode
			Field Mapping uses symbolic key
				related.VendorGroup					= Company.VendorGroup
				related.PayablesDiscountCode		= DiscountCode
				
		PayablesDiscountCodeDetailRel
			one-to-one relation to PayablesDiscountCodeDetail
			Field Mapping uses symbolic key
				related.VendorGroup				= Company.VendorGroup
				related.PayablesDiscountCode	= DiscountCode
				related.Company					= Company

		IncomeWithholdingCodeRel
			one-to-one relation to IncomeWithholdingCode   
			Field Mapping uses symbolic key
				related.VendorGroup					= Company.VendorGroup
				related.IncomeWithholdingCode		= IncomeWithholdingCode

		IncomeWithholdingCodeDetailRel
			one-to-one relation to IncomeWithholdingCodeDetail
			Field Mapping uses symbolic key
				related.VendorGroup				= Company.VendorGroup
				related.IncomeWithholdingCode	= IncomeWithholdingCode
				related.Company					= Company

		PayablesInvoicePaymentRel
			one-to-many relation to PayablesInvoicePayment
			Field Mapping uses Set10
				related.Status						= LocalRecStatus
				related.Company 					= Company
				related.ProcessLevel				= PayablesProcessLevel
















		DeletePayablesInvoicePaymentHistoryRel
			one-to-many relation to PayablesInvoicePaymentHistory
			Field Mapping uses Set10
				related.Status				= LocalRecStatus
				related.Company 			= Company
				related.ProcessLevel		= PayablesProcessLevel
		
		TaxEntityRel
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.TaxEntity				= Company.AccountingEntity
				
		PayGroupCompanyRelationshipRel
			one-to-many relation to PayGroupCompanyRelationship
			Field Mapping uses symbolic key
                related.VendorGroup 		= Company.VendorGroup
				related.PayGroup			= PayGroup
				related.Company 			= Company
				related.ProcessLevel		= PayablesProcessLevel
		
		OldPayGroupCompanyRelationshipRel
			one-to-many relation to PayGroupCompanyRelationship
			Field Mapping uses symbolic key
                related.VendorGroup 		= Company.VendorGroup
				related.PayGroup			= old PayGroup
				related.Company 			= Company
				related.ProcessLevel		= PayablesProcessLevel

		VertexLookupResultsRel	
			one-to-many relation to VertexLookupResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.FinanceEnterpriseGroup
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance)

 		VertexConfigRel	
        	one-to-one relation to VertexConfig
        	Field Mapping uses symbolic key
				related.VertexConfig 									= Company.FinanceEnterpriseGroup

    	VertexTaxAreaIdResultsRel	
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance)

  		VertexWebserviceErrorRel	
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance
				and    related.WebServiceError							= true)
		
		MNRReconciliationSubSystemBalanceRel
			one-to-many relation to ReconciliationAccount
			Field Mapping uses ByGeneralLedgerChartAccount
				related.ReconciliationManagementGroup						= Company.FinanceEnterpriseGroup
				related.ReconciliationCodeBlock.GeneralLedgerChartAccount	= MatchedNotReceivedAccount.GeneralLedgerChartAccount
				related.ReconciliationCodeBlock.AccountingEntity			= MatchedNotReceivedAccount.ToAccountingEntity
			Instance Selection
				where(related.ReconciliationAccountType.MatchedNotReceived)

		RNIReconciliationSubSystemBalanceRel
			one-to-many relation to ReconciliationAccount
			Field Mapping uses ByGeneralLedgerChartAccount
				related.ReconciliationManagementGroup						= Company.FinanceEnterpriseGroup
				related.ReconciliationCodeBlock.GeneralLedgerChartAccount	= ReceiptAccrualAccount.GeneralLedgerChartAccount
				related.ReconciliationCodeBlock.AccountingEntity			= ReceiptAccrualAccount.ToAccountingEntity
			Instance Selection
				where(related.ReconciliationAccountType.ReceivedNotInvoiced)
		
    Sets
        Set2
            indexed
            Sort Order
                PayablesProcessLevel
                Company

        Set3
            indexed
            Sort Order
                PayGroup
                PayablesProcessLevel
                Company








	Attach Rules
		if (not BypassRuleForTheseFiles)
			constraint (Active)
				"ProcessLevelIsInactive"

	Rule Blocks

    Field Rules
    	Name
    		required








		PayGroup
			default to Company.PayGroup
			required


			constraint (PayGroup.VendorGroup = Company.VendorGroup)
				"CompanyVendorGroupMust_=PayGroupVendorGroup"	  

		DefaultProcessLevel



			constraint (DefaultPayablesProcessLevelRel not exists)
				"DefaultProcessLevelAlreadyExists;see<first DefaultPayablesProcessLevelRel.PayablesProcessLevel>"		//DefaultProcessLevelForCompanyExists"  
			constraint (!BillToOnly)		
				"CannotSetA_\BillTo_\OnlyLocationAsTheDefaultProcessLevel"
				
		AccrualCode
			constraint (PayablesAccrualCodeDetailRel exists)
				"AccrualCodeDetailIsNotDefinedForCompany<Company>"
			constraint (PayablesAccrualCodeDetailRel.DistributionAccount.ToAccountingEntity = Company.AccountingEntity)	
				"AccountingEntity<PayablesAccrualCodeDetailRel.DistributionAccount.ToAccountingEntity>OnAccrualCodeDetail;MustEqualAccountingEntity<Company.AccountingEntity>OnGlobalLedgerCompany"



		PostalAddress
			if (BillToOnly)		
				required
					"AddressRequiredIf_\BillTo_\Only"






		IncomeWithholdingCode
			constraint (IncomeWithholdingCodeDetailRel exists)
				"IncomeWithholdingCodeDetailIsNotDefinedForCompany<Company>"
			constraint (IncomeWithholdingCodeDetailRel.WithholdingAccount.ToAccountingEntity = Company.AccountingEntity)		
				"AccountingEntity<IncomeWithholdingCodeDetailRel.WithholdingAccount.ToAccountingEntity>OnIncomeWithholdingCodeDetail;MustEqualAccountingEntity<Company.AccountingEntity>OnGlobalLedgerCompany"			



					
		DiscountCode
			constraint (PayablesDiscountCodeDetailRel exists)
				"DiscountCodeDetailIsNotDefinedForCompany<Company>"
			constraint (PayablesDiscountCodeDetailRel.DiscountAccount.ToAccountingEntity = Company.AccountingEntity)		
				"AccountingEntity<PayablesDiscountCodeDetailRel.DiscountAccount.ToAccountingEntity>OnDiscountCodeDetail;MustEqualAccountingEntity<Company.AccountingEntity>OnGlobalLedgerCompany"
			



				




		



				
		SecondaryWithholdingCode2
			constraint (SecondaryWithholdingCode1 != SecondaryWithholdingCode2)
				"SecondaryWithholdingCodeCannotBeDuplicated"               






					
		SecondaryWithholdingCode3
			constraint (SecondaryWithholdingCode3 != SecondaryWithholdingCode1
			and			SecondaryWithholdingCode3 != SecondaryWithholdingCode2)
				"SecondaryWithholdingCodeCannotBeDuplicated"               






					
		RetainageAccrualCode


			constraint (RetainageAccrualCodeDetailRel exists)
				"RetainageAccrualCodeCodeDetailIsNotDefinedForCompany<Company>"
			constraint (RetainageAccrualCodeDetailRel.DistributionAccount.ToAccountingEntity = Company.AccountingEntity)		
				"AccountingEntity<RetainageAccrualCodeDetailRel.DistributionAccount.ToAccountingEntity>OnRetainageAccrualCodeDetail;MustEqualAccountingEntity<Company.AccountingEntity>OnGlobalLedgerCompany"
			
				
		DistributionCode
			constraint (Company.CreateTransitDistribution)
				"CompanyNotSetUpFor_\In_\Transit"            
			constraint (DistributionCode.AccountEdit = "C")
				"MustUseAFullyEditedDistCode,EditAccts_=_\C"               
			constraint (first DistributionCode.PayablesDistributionCodeDetail set.DistributionPercent = 100%)
				"Dist_\CodeMustBeLinkedToOnlyOneAcctAndReflect_100%"       
				
			constraint (first DistributionCode.PayablesDistributionCodeDetail set.AssetTemplate = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.ItemQuantity = 0
			and			first DistributionCode.PayablesDistributionCodeDetail set.Combine = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.AssetDescription = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.TagNumber = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.AssetGroup = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.ItemNumber = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.ItemDescription = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.ModelNumber = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.SerialNumber = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.Asset = 0
			and			first DistributionCode.PayablesDistributionCodeDetail set.AssetLocation = ""
			and			first DistributionCode.PayablesDistributionCodeDetail set.AssetDivision = "")
				"CannotUseADistCodeWhichContainsAssetInformation"          

		MatchProcessLevel
			if (ReceiptAccrualAccount not entered
			and CostVarianceAccount not entered
			and ToleranceOffsetAccount not entered
			and ReceiptWriteOffAccount not entered
			and MatchedNotReceivedAccount not entered
			and MatchWriteOffAccount not entered
			and AddOnChargeDifferenceAccount not entered
			and UnderbillAccount not entered
			and InventoryReceiptArchivalAccount not entered)
				MatchProcessLevel = false
			else
				MatchProcessLevel = true

		Active
			initial value is true

		ICBillingDefaultExpenseAccount
			if (ICBillingDefaultExpenseAccount.ToAccountingEntity entered)
				constraint (ICBillingDefaultExpenseAccount.ToAccountingEntity = Company.AccountingEntity)
					"IntercompanyBillingDefaultExpenseAccountAccountingEntityMustBeTheSameAsTheCompanyAccountingEntityOf<Company.AccountingEntity>"

	Actions
		Create is a Create Action
			Exit Rules
				invoke Create PayGroupCompanyRelationshipRel

		Update is an Update Action
			Action Rules				















				if (PayGroup changed)
					invoke Delete OldPayGroupCompanyRelationshipRel
					invoke Create PayGroupCompanyRelationshipRel

		Delete is a Delete Action
			Entrance Rules
				invoke Delete PayGroupCompanyRelationshipRel
			Action Rules










		UpdateVertexTaxCode is an Instance Action		 
			valid when (TaxEntityRel.ThirdParty.VertexOSeries)	
			run in background 
			completion message is "<CompletionMessage>" 

			Parameters

			Parameter Rules

			Local Fields
				LocalTaxCode			 is a TaxCode 
          
			Action Rules								
	  			MultipleTaxCodes = false    				 
	    		WebServiceError  = false   	
	    		initialize WebServiceErrorMessage 				 
	        	initialize LocalTaxCode 						 	
				initialize CalculateTax
				CalculateTax.RelatedObjectReference         	= reference to this instance
				CalculateTax.System								= "AP"
				CalculateTax.CallingModule						= "PayablesProcessLevel"			
				CalculateTax.VertexQuantumOption				= "G"
				CalculateTax.TaxEntity							= blank
				CalculateTax.InFinanceEnterpriseGroup       	= Company.FinanceEnterpriseGroup 
				CalculateTax.VendorGrp							= Company.VendorGroup
				CalculateTax.PostalAddress						= PostalAddress
				LocalTaxCode									= CalculateTax.ReturnedGeoCode
				MultipleTaxCodes								= CalculateTax.MultipleTaxAreaIds
				WebServiceError									= CalculateTax.WebServiceError 
				WebServiceErrorMessage							= CalculateTax.WebServiceErrorMessage
				
				if (LocalTaxCode entered)
					TaxCode = LocalTaxCode
				if (LocalTaxCode entered
				and !MultipleTaxCodes				
				and CalculateTax.OutputErrorNumber not entered)					
					if (PostalAddress.PostalCode not entered)
						PostalAddress.PostalCode 	= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
						PostalAddress.County 		= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County


					if (VertexLookupResultsRel exist)
						for each VertexLookupResultsRel
							invoke Update each
								invoked.Status = 1		

		BatchUpdateTaxCodeUsingVertex is a Set Action	
			valid when (TaxEntityRel.ThirdParty.VertexOSeries)	
			completion message is "<CompletionMessage>"

			Parameters
				PrmEnterpriseGroup	is a VertexConfig				 
					default label is "FinanceEnterpriseGroup"
				PrmProcessGroup		is Alpha 50
					default label is "ProcessGroup"
				PrmPayablesCompany	is a PayablesCompany	 
					default label is "PayablesCompany"
				PrmReadOnly			is Boolean
					default label is "ReadOnly"	
				EntityTaxCodeDefault is like TaxCode
				PrmTaxEntity 		 is like TaxEntity
					default label is "TaxEntity"						

			Parameter Rules

			Local Fields
				LocalTaxCode			is like TaxCode 
				CompletionMessage		is Alpha 150
				RecordCount				is Numeric 10
				
			Instance Selection
				where (PrmEnterpriseGroup   = Company.FinanceEnterpriseGroup
				and    (Company   			= PrmPayablesCompany
				or	    PrmPayablesCompany not entered))

			Sort Order
				PayablesProcessLevel

			Accumulators

				        								
			Action Rules								
				Empty Set Rules
					CompletionMessage = "<CompletionMessage>"
				
				Set Rules
					Entrance Rules				 
						constraint (VertexConfigRel exist)
							"VertexConfigurationNotSetUp;CheckIfTaxEntitySetUpToUseVertex"
						initialize RecordCount
						
					Exit Rules
						
				PayablesProcessLevel Set Rules
					Entrance Rules
			        
					Exit Rules				
						
				Instance Rules
					increment RecordCount
					initialize CalculateTax
	  				MultipleTaxCodes = false    				 
	    			WebServiceError  = false   	
			      	initialize VertexErrorNumber					 
	    			initialize WebServiceErrorMessage 				 
					initialize LocalLookupKey 						 
	        		initialize LocalSequence
					initialize CalculateTax
					CalculateTax.RelatedObjectReference         	= reference to this instance
					CalculateTax.System								= "AP"
					CalculateTax.CallingModule						= "PayablesProcessLevel"			
					CalculateTax.VertexQuantumOption				= "G"
					CalculateTax.InFinanceEnterpriseGroup       	= Company.FinanceEnterpriseGroup	 
					CalculateTax.VendorGrp							= Company.VendorGroup
					CalculateTax.TaxEntity							= PrmTaxEntity
					CalculateTax.EntityTaxCodeDefault				= EntityTaxCodeDefault										
					CalculateTax.PostalAddress						= PostalAddress
					LocalTaxCode									= CalculateTax.ReturnedGeoCode
					VertexErrorNumber								= CalculateTax.OutputErrorNumber 
					MultipleTaxCodes								= CalculateTax.MultipleTaxAreaIds
					WebServiceError									= CalculateTax.WebServiceError 
					WebServiceErrorMessage							= CalculateTax.WebServiceErrorMessage
					CalculateTaxOutputMessage						= CalculateTax.OutputErrorMessage

					NewTaxCode = LocalTaxCode	
					OldTaxCode = TaxCode
					if (LocalTaxCode entered
					and !PrmReadOnly)
						TaxCode = LocalTaxCode
					if (LocalTaxCode entered
					and !MultipleTaxCodes
					and !WebServiceError				
					and CalculateTax.OutputErrorNumber not entered
					and !PrmReadOnly)					
						if (PostalAddress.PostalCode not entered)
							PostalAddress.PostalCode 	= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
							PostalAddress.County 		= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County
	
					LocalLookupKey 	= CalculateTax.ResultsLookupKey
					LocalSequence	= CalculateTax.ResultsLookupKeySequence	
					if (VertexLookupResultsRel exist)
						invoke Delete VertexLookupResultsRel
						
					invoke Create VertexLookupResults
						invoked.VertexConfig			   		= Company.FinanceEnterpriseGroup
						invoked.VertexLookupResults.LookupKey 	= LocalLookupKey
						invoked.VertexLookupResults.Sequence	= LocalSequence 
						invoked.RelatedObjectReference			= reference to this instance
						invoked.LookupPayablesCompany			= Company 
						invoked.LookupPayablesProcessLevel		= PayablesProcessLevel
						invoked.LookupProcessGroup				= PrmProcessGroup
						invoked.ReturnedHighesTaxAreaId			= LocalTaxCode
						invoked.NumberOfTaxAreaIdsReturned		= CalculateTax.NumberOfTaxCodes	 
						invoked.LookupPostalAddress 			= PostalAddress
						invoked.LookupTaxEntity					= PrmTaxEntity
						invoked.ErrorNumber						= CalculateTax.OutputErrorNumber  
						invoked.ErrorMessage					= DerivedLookupMessage
						invoked.LookupDescription				= TaxCodeMessage
						invoked.WebServiceError 				= CalculateTax.WebServiceError 
						invoked.WebServiceErrorMessage 			= CalculateTax.WebServiceErrorMessage 
						if (MultipleTaxCodes
						or  WebServiceError
						or  CalculateTax.OutputErrorNumber entered)								
							invoked.Status = 0						
						else
						if (LocalTaxCode not = OldTaxCode)
							invoked.Status = 1						
						else
							invoked.Status = 2						


		UpdateTaxCodeAndAddress is an Instance Action
			restricted
			Parameters
				PrmTaxCode		is like TaxCode
				PrmCountry		is a Country
				PrmState		is a StateProvince
				PrmZip			is a PostalCode
				PrmCounty		is a County
			Parameter Rules
			Action Rules
				if (PrmTaxCode entered)
					TaxCode = PrmTaxCode
				if (PostalAddress.PostalCode not entered)
					PostalAddress.PostalCode	= PrmZip
				if (PostalAddress.County not entered)
					PostalAddress.County 		= PrmCounty
