PayablesInvoiceSupplierAddOnCharge is a BusinessClass

    owned by ma
	sql name is PayInvSupAddOnCharge
    prefix is MAB

    Ontology
        symbolic key is PayablesInvoiceSupplierAddOnCharge

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields
        AddOnChargeAmount           		is an InternationalAmount
			default label is "Amount"
		AddOnChargeDescription              is a Description
			default label is "Description"
		InvoiceAddOnChargeCreated           is Boolean

	Conditions 
		InvoiceHasNotBeenSubmitted
			restricted 
			when (PayablesInvoice.SupplierCreatedInvoiceStatus = 1)

		InvoiceHasBeenSubmitted 
			restricted 
			when (PayablesInvoice.SupplierCreatedInvoiceStatus = 2)

		InvoiceAddOnChargeNotCreated
			restricted 
			when (InvoiceHasBeenSubmitted
			and  !InvoiceAddOnChargeCreated)

	Relations 
		PayablesInvoiceErrorRel 
			one-to-many relation to PayablesInvoiceError 
			Field Mapping uses symbolic key
				related.Company					= Company
				related.PayablesInvoice			= PayablesInvoice
			Instance Selection
				where (related.ErrorType = 71)			

    Field Rules 
		AddOnChargeAmount 
			required 

		AddOnChargeDescription	
			required 
	
	Actions
        Create is a Create Action
			valid when (InvoiceHasNotBeenSubmitted)

		Update is an Update Action 
			valid when (InvoiceHasNotBeenSubmitted)

		SupplierDelete is a Delete Action 
			valid when (InvoiceHasNotBeenSubmitted)
			default label is "Delete"
		
		Delete is a Delete Action 
			valid when (InvoiceAddOnChargeNotCreated)
			Entrance Rules 

				if (!PayablesInvoice.HasOpenSupplierAddOnCharges)
					invoke Delete PayablesInvoiceErrorRel			
		
		Purge is a Purge Action
			restricted

		CreateMiscellaneousAddOnCost is an Instance Action		
			valid when (!InvoiceAddOnChargeCreated)
			Parameters 
				ParmAddOnCharge is an AddOnCharge
					default label is "AddOnChargeCode"

			Parameter Rules
				ParmAddOnCharge
					required
				
			Action Rules 

				constraint (ParmAddOnCharge.SpreadMethod.NoSpread)
					"CannotUseASpreadAddOnChargeCode"						

				if (ParmAddOnCharge.AddOnChargeType.Cost)
					constraint (!ParmAddOnCharge.LandedAddOnCharge)
						"CannotAddAMiscellaneousAddOnChargeThatIsLanded"	
				else
				if (ParmAddOnCharge.AddOnChargeType.Allowance)
					constraint (!ParmAddOnCharge.LandedAddOnCharge)	
						"CannotAddAMiscellaneousAllowanceThatIsLanded"		

				invoke CreateMiscellaneousAddOnCosts PayablesInvoiceAddOnCharge 
					invoked.Company					= Company 
					invoked.PayablesInvoice			= PayablesInvoice 
					invoked.AddOnCharge     		= ParmAddOnCharge 
					invoked.AddOnChargeAmount		= AddOnChargeAmount 
					invoked.TotalDistributionAmount	= AddOnChargeAmount

				InvoiceAddOnChargeCreated = true	

			Exit Rules 

				if (!PayablesInvoice.HasOpenSupplierAddOnCharges)
					invoke Delete PayablesInvoiceErrorRel

        CreateSpreadAddOnCost is an Instance Action
        	valid when (!InvoiceAddOnChargeCreated)
			Parameters 
				ParmAddOnCharge is an AddOnCharge
					default label is "AddOnChargeCode"

			Parameter Rules
				ParmAddOnCharge
					required
        	
        	Action Rules 

				invoke Create PayablesInvoiceSpreadAddOnCharge 
					invoked.Company				= Company 
					invoked.PayablesInvoice		= PayablesInvoice 
					invoked.AddOnCharge     	= ParmAddOnCharge 
					invoked.SpreadAmount    	= AddOnChargeAmount 				

				InvoiceAddOnChargeCreated = true

			Exit Rules 

				if (!PayablesInvoice.HasOpenSupplierAddOnCharges)
					invoke Delete first PayablesInvoiceErrorRel
			

