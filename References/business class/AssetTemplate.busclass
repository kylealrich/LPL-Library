AssetTemplate is a BusinessClass
    owned by am
    prefix is ATM
    classic name is AMTEMPLATE

    Ontology
        symbolic key is AssetTemplate
            classic set name is ATMSET1

    Patterns
        implements StaticJava
        disable AuditIndex
        
	Local Fields
		LocalFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
		LocalBook						is like Book
		LocalAssetTemplate				is like AssetTemplate
		LocalToAssetCompany				is like AssetCompany
		LocalAssetAccountGroup			is like AssetAccountGroup
		LocalDimensionGroup				is like AssetAccountingUnitGroup
		LocalAssetType					is like AssetType

    Persistent Fields

        Description
        AssetCategory
        Simulated                  is Boolean
        WorkInProcess              is Boolean
            classic name is WORK-IN-PROC
        TaxExempt                  is Boolean
        Used                       is Boolean
        Company                    is an AssetCompany
        AssetLocation
            classic name is LOCATION-NAME
        AssetDivision
            classic name is DIVISION
        AssetType
            classic name for AssetType.Type is ASSET-TYPE
        AssetAccountGroup
            classic name is ACCT-GRP

        AccountingUnit						 	is a FinanceCodeBlockNoAccountFull
        	default label is "DefaultTransactionDimensions"
        AssetAccountingUnitGroup
        	default label is "AssetDimensionGroup"
            classic name is AU-GROUP
        Project						
           	classic name is ACTIVITY
        DepreciationExpenseProject	is a FinanceCodeBlockProjectOnly
      		default label is "DepreciationExpenseProject"
            classic name for DepreciationExpenseProject.Project is DEP-EXP-ACTVTY
        CreationDate               is TimeStamp
        CreatedBy                  is an Operator 
        	holds pii
            classic name is CREATOR-ID
        TransactionDimensionDefault	is Numeric 1
        	States
        		FromTemplate		value is 0  	
        		FromDistribution	value is 1		
        		LeaveBlank			value is 2		
        AssetGuidelineClass
            
	Transient Fields
		ComputeOption              					is a Compute

    Derived Fields
		DimensionCode		is a StringField
			type is Alpha 70
			restricted
			AccountingUnit.DimensionCode
			
        CompanyName                    is a StringField 
            type is like Name
        	holds pii
            restricted
            classic name is GL-NAME
            Company.Name

        CompanyCurrency                is a StringField
            type is Alpha size 5
            restricted
            classic name is GL-CURR-CODE
            Company.Currency

        AccountGroupDescription        is a StringField
            type is Alpha size 30
            restricted
            classic name is ACCT-GRP-DESC
            AssetAccountGroup.Description

        AssetLocationDescription       is a StringField
            type is Alpha size 30
            restricted
            classic name is LOC-DESC
            AssetLocation.Description

        AssetDivisionDescription       is a StringField
            type is Alpha size 30
            restricted
            classic name is DIV-DESC
            AssetDivision.Description

        AssetTypeDescription           is a StringField
            type is Alpha size 30
            restricted
            classic name is TYPE-DESC
            AssetType.Description

        AccountingUnitGroupDescription is a StringField
            type is Alpha size 30
            restricted
            classic name is AU-GROUP-DESC
            AssetAccountingUnitGroup.Description

    Conditions

        CompanyEntered
        	restricted
            classic name is COMPANY-EXIST
            when (Company entered)
            
		DefaultAccountingUnitAvailable
			restricted
			when (AccountingUnit entered)
		
		TemplateEntered
			restricted
			when (AssetTemplate entered)
		
        HasAssetAU
        	restricted   
        	when (AssetAccountingUnitGroup.AssetAccountingUnit entered)
        
        HasAccumulatedDepreciationAU
        	restricted 
        	when (AssetAccountingUnitGroup.AccumulatedDepreciationAccountingUnit entered)
        	
        HasDepreciationExpenseAU
        	restricted
        	when (AssetAccountingUnitGroup.DepreciationExpenseAccountingUnit entered)
        	
        HasClearingAU
        	restricted 
        	when (AssetAccountingUnitGroup.ClearingAccountingUnit entered)
        	             
        HasProceedsAU
        	restricted     
        	when (AssetAccountingUnitGroup.ProceedsAccountingUnit entered)
        	
       	HasDisposalExpenseAU
       		restricted     
       	   	when (AssetAccountingUnitGroup.DisposalExpenseAccountingUnit entered)
       	   	       	   	
        HasGainAU
        	restricted     
          	when (AssetAccountingUnitGroup.GainAccountingUnit entered)
          	   	       
        HasLossAU
        	restricted   
        	when (AssetAccountingUnitGroup.LossAccountingUnit entered)
        	
        HasDeferredGainAU
        	restricted     
        	when (AssetAccountingUnitGroup.DeferredGainAccountingUnit entered)
             
        HasDeferredLossAU
        	restricted  
        	when (AssetAccountingUnitGroup.DeferredLossAccountingUnit entered)
               
        HasRevalueSurplusAU
        	restricted  
			when ((Company.Revalue 
			and    AssetAccountingUnitGroup.RevalueSurplusAccountingUnit entered)
			or 	  !Company.Revalue)
        
        HasRevalueLossAU
        	restricted          
			when ((Company.Revalue 
			and    AssetAccountingUnitGroup.RevalueLossAccountingUnit entered)
			or    !Company.Revalue)
	
		HasAllRequiredAccountingUnits
			restricted
			when (DefaultAccountingUnitAvailable
			or 	 (HasAccumulatedDepreciationAU 
        	and   HasDepreciationExpenseAU
        	and   HasClearingAU 
        	and   HasProceedsAU     
        	and	  HasDisposalExpenseAU     
       	   	and   HasGainAU       
          	and   HasLossAU   
        	and   HasDeferredGainAU     
            and   HasDeferredLossAU  
            and   HasRevalueSurplusAU  
        	and   HasRevalueLossAU)) 
		
		AseetGuidelineClassesExists
			restricted
			when (AssetGuidelineClassesRel exists)

		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

	Relations
		BookCalendarRel
			one-to-one relation to BookCalendar	
			Field Mapping uses Set5
				related.Book				= LocalBook
				related.Company				= Company
				
		AssetCompanyRel
			one-to-one relation to AssetCompany
			Field Mapping uses symbolic key
				related.Company				= Company				

		TemplateBooksRel 
			one-to-many relation to AssetTemplateBook
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= LocalFinanceEnterpriseGroup
				related.AssetTemplate			= AssetTemplate

		TemplateBooksWithLifeOrMethodRel
			one-to-many relation to AssetTemplateBook
			Field Mapping uses ByTemplate
				related.AssetTemplate		= AssetTemplate            
			Instance Selection
				where (related.Method entered
				or related.Life entered)

		TemplateBooksWithoutLifeOrMethodRel
			one-to-many relation to AssetTemplateBook
			Field Mapping uses ByTemplate
				related.AssetTemplate		= AssetTemplate            
			Instance Selection
				where ((related.Method not entered
				or related.Life not entered)
				and !related.ClassTypeBook)				
		
		InterfacingAssetsRel is a AssetInterface set
		
		AllAssetTemplatesRel 
			one-to-many relation to AssetTemplate
			Field Mapping uses Set3

		AssetTypeRel
			one-to-one relation to AssetType
			Field Mapping uses symbolic key
				related.Company							= Company
				related.AssetType.Type					= AssetType.Type
				related.AssetType.SubType				= AssetType.SubType

		ToCompanyTemplateRel
			one-to-one relation to AssetTemplate
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.Company					= LocalToAssetCompany			
				related.AssetTemplate			= LocalAssetTemplate
				
		ToAssetTypeRel
			one-to-one relation to AssetType
			Field Mapping uses symbolic key
				related.Company					= LocalToAssetCompany
				related.AssetType				= LocalAssetType

		ToBookCalendarRel
			one-to-many relation to BookCalendar	
			Field Mapping uses Set5
				related.Book					= LocalBook
				related.Company					= LocalToAssetCompany
			
		ToAssetAccountGroupRel
			one-to-one relation to AssetAccountGroup
			Field Mapping uses symbolic key
				related.Company					= LocalToAssetCompany			
				related.AssetAccountGroup		= LocalAssetAccountGroup
						
		ToAssetDimensionGroupRel
			one-to-one relation to AssetAccountingUnitGroup
			Field Mapping uses Set2
				related.AssetAccountingUnitGroup = LocalDimensionGroup
				related.Company					 = LocalToAssetCompany	

		GuidelineClassRel
			one-to-many relation to AssetGuidelineClass
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.AssetGuidelineClass = AssetGuidelineClass)						

		AssetGuidelineClassesRel
			one-to-many relation to AssetGuidelineClass
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup			
		
		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
            Field Mapping uses symbolic key
            	related.Company = Company
    Sets

        Set2
            indexed
            Sort Order
            	FinanceEnterpriseGroup
                Company
                AssetTemplate

        Set3
            indexed
            Sort Order
                Company
                AssetType
                AssetTemplate

    Field Rules

        Description
            required
		
		Company
			required

		AssetType
			required
				"AssetTypeIsRequired"
			constraint (AssetTypeRel exists)
				"AssetTypeIsNotDefininedForCompany<Company>"
			
			if (AssetCategory.Depreciable)
				constraint (!AssetType.Property.Stock and !AssetType.Property.Land)
					"PropertyTypeNotValidWithDepreciableAsset"				
		
		AssetCategory
			if (AssetCategory changed) 
				if (old AssetCategory.Depreciable)	
					if (AssetCategory.NonDepreciable)
						constraint (!TemplateBooksWithLifeOrMethodRel exist)
							"BeforeChangingFromDepreciableToNonDepreciable;ChangeTheAssetCategoryToNonCategorizedAndRemoveTheMethodAndLife"
			
				if (old AssetCategory.NonDepreciable)
					if (AssetCategory.Depreciable)	
						constraint (!TemplateBooksWithoutLifeOrMethodRel exist)
							"BeforeChangingFromNonDepreciableToDepreciable;ChangeTheAssetCategoryToNonCategorizedAndAssignRequiredMethodAndLifeValues"
	

	Actions
		Create is a Create Action
























































































































































































								
		CopyAssetTemplate is an Instance Action
			default label is "CopyAssetTemplate"
			completion message is "Template<ToAssetTemplate>HasBeenCreated"
			Parameters
				CopyTemplate				is an AssetTemplate  
				ToAssetTemplate				is AlphaUpper 25
				ToCompany   				is an AssetCompany
				ToDescription				is like Description
		        	default label is "Description"
		        ToAssetCategory				is an AssetCategory
		        	default label is "AssetCategory"
		        ToSimulated               	is Boolean
		        	default label is "Simulated"
		        ToWorkInProcess           	is Boolean
		        	default label is "WorkInProcess"
		        ToTaxExempt             	is Boolean
		        	default label is "TaxExempt"
		        ToUsed                  	is Boolean
		        	default label is "Used"
		        ToAssetLocation				is an AssetLocation
		        	default label is "AssetLocation"
		        ToAssetDivision				is an AssetDivision
		        	default label is "Division"
		        ToAssetType					is an AssetType
		        	default label is "AssetType"
				ToAssetGuidelineClass		is an AssetGuidelineClass
					default label is "GuidelineClass"	
		        ToAssetAccountGroup			is an AssetAccountGroup
		        	default label is "AssetAccountGroup"
		        ToAccountingUnit			is a FinanceCodeBlockNoAccountFull
        			default label is "DefaultTransactionDimensions"
        		ToProject					is a Project
        			default label is "Project"
		        ToAssetDimensionGroup		is a AssetAccountingUnitGroup
        			default label is "AssetDimensionGroup"
		        ToDepreciationExpenseProject is a FinanceCodeBlockProjectOnly
		      		default label is "DepreciationExpenseProject"
				CopyBooks					is Boolean
 	

			Parameter Rules
				CopyTemplate
					initial value is AssetTemplate
				ToAssetTemplate
					required
				ToCompany   	
					required
					constraint (!ToCompanyTemplateRel exists)
						"Template<ToAssetTemplate>AlreadyExistsInCompany<ToCompany>"
				ToDescription
					initial value is Description
		        ToAssetCategory		
		        	initial value is AssetCategory
		        ToSimulated            
		        	initial value is Simulated
		        ToWorkInProcess          
		        	initial value is WorkInProcess
		        ToTaxExempt             
		        	initial value is TaxExempt
		        ToUsed                  
		        	initial value is Used
		        ToAssetLocation			
		        	initial value is AssetLocation
		        ToAssetDivision				
		        	initial value is AssetDivision
		        ToAssetType				
		        	initial value is AssetType
				ToAssetGuidelineClass
					initial value is AssetGuidelineClass
		        ToAssetAccountGroup		
		        	initial value is AssetAccountGroup
		        ToAccountingUnit		
		        	initial value is AccountingUnit
		        ToAssetDimensionGroup
		        	initial value is AssetAccountingUnitGroup
		        ToProject
		        	initial value is Project
		        ToDepreciationExpenseProject
		        	initial value is DepreciationExpenseProject

			Local Fields
				LocalAssetType			is like AssetType
				LocalTemplate			is like AssetTemplate
				LocalToAssetCompany		is like AssetCompany
				LocalAssetTemplate		is like AssetTemplate
				LocalAssetAccountGroup	is like AssetAccountGroup
				LocalDimensionGroup		is like AssetAccountingUnitGroup
				LocalBook				is like Book

			Entrance Rules
				LocalAssetType			= ToAssetType
				LocalToAssetCompany 	= ToCompany
				LocalAssetTemplate		= ToAssetTemplate	
				LocalAssetAccountGroup	= ToAssetAccountGroup
				LocalDimensionGroup 	= ToAssetDimensionGroup
				LocalFinanceEnterpriseGroup	= actor.context.FinanceEnterpriseGroup	
				
										
			Action Rules
				invoke Create AssetTemplate
					fill in fields from this instance
					invoked.FinanceEnterpriseGroup		= LocalFinanceEnterpriseGroup
					invoked.AssetTemplate 		 		= ToAssetTemplate
					invoked.Company						= ToCompany
					invoked.Description				 	= ToDescription				
					invoked.AssetCategory			 	= ToAssetCategory			
					invoked.Simulated 		 			= ToSimulated		
					invoked.WorkInProcess 				= ToWorkInProcess										
					invoked.TaxExempt 				 	= ToTaxExempt					
					invoked.Used		 			 	= ToUsed				
					invoked.AssetLocation 			 	= ToAssetLocation
					invoked.AssetDivision				= ToAssetDivision
					invoked.AssetType 		 			= ToAssetType	
					invoked.AssetGuidelineClass			= ToAssetGuidelineClass		
					invoked.AssetAccountGroup        	= ToAssetAccountGroup
					invoked.AccountingUnit		 		= ToAccountingUnit		
					invoked.Project						= ToProject	
					invoked.AssetAccountingUnitGroup    = ToAssetDimensionGroup
					invoked.DepreciationExpenseProject 	= ToDepreciationExpenseProject

			Exit Rules
				if (CopyBooks)
					if (ToUsed
					and CopyBooks)
						confirmation required	
							"TheUsedParameterWillCauseTheUsedFlagToBeSetOnAllBooksCreatedInCopyToTemplate"					
					for each TemplateBooksRel
						LocalBook 			= each.Book
						LocalToAssetCompany = ToCompany	
						invoke Create AssetTemplateBook
							fill in fields from each
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.AssetTemplate 		 	= LocalAssetTemplate	
							invoked.Book					= LocalBook
							invoked.LocalCreateFromCopy		= true
							if (ToUsed)
								invoked.Used 				= ToUsed	
							else 
								invoked.Used				= each.Used					

		Update is an Update Action
			Action Rules
				if ((AssetGuidelineClass changed
				and  AssetGuidelineClass entered)
				or  (Used changed))
					if (Used changed)
						confirmation required
							"ThisChangeWillUpdateTheUsedFlagAllRelatedTemplateBooks,YouMayUpdateTheUsedFieldOnTheTemplateBookIfItMustBeDifferentThanTheUsedValueAssignedToTheAssetTemplate"
					invoke Update AssetTemplateBook set
						invoked.TransientUpdateFromTemplate = true
						if (Used changed)
							invoked.TransientUpdateUsedFromTemplate = true


		Delete is a Delete Action
			Entrance Rules
				invoke Delete AssetTemplateBook set
