AssetRecapture is a BusinessClass
    owned by am
    prefix is RCP
    classic name is AMASTRECAP

    Ontology
        symbolic key is AssetRecapture
            classic set name is RCPSET0
            classic name for AssetRecapture.RecaptureType is RECAP-TYPE
            classic name for AssetRecapture.RecaptureDate is RECAP-DATE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        TransactionNumberOfDecimals is a CurrencyNumberOfDecimals
            classic name is TRAN-ND
        RecaptureAmount             is an InternationalAmount
            classic name is RECAP-AMOUNT
                precision is Asset.TransactionNumberOfDecimals

    Conditions

        IsRcpset2
        	restricted
            classic name is RCPSET2
            when (AssetRecapture.RecaptureType.TaxCredit)
            
	Local Fields		
		LocalBook						is like Book
		LocalAsset						is like Asset
		LocalFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
		
	Relations
		AssetBookPurgeRel
			one-to-many relation to AssetBook
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup		= LocalFinanceEnterpriseGroup
		      	related.Book						= LocalBook
		    Instance Selection
		    	include deleted records
    Sets

        Set1
            not indexed
            Sort Order
                AssetRecapture.RecaptureType
                Asset
                Book
                AssetRecapture.RecaptureDate

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
		
		Purge is a Purge Action
			bypass relational integrity rules
			restricted
			
		AssetRecapturePurge is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmAsset						is an Asset
				PrmBook							is a Book
				PrmPurgeOption					is Numeric 1
					default label is "ActionOption"
					States
						PurgeRecords 	value is 1
						ReportOnly		value is 2
				PrmCompany						is like Company
			Parameter Rules
				PrmFinanceEnterpriseGroup
					LocalFinanceEnterpriseGroup 		= PrmFinanceEnterpriseGroup
				PrmBook
					LocalBook 							= PrmBook
				PrmAsset
					LocalAsset = PrmAsset
			Local Fields
				LocalCounter					is Numeric 10
			Instance Selection
				include deleted records
				where(FinanceEnterpriseGroup					= PrmFinanceEnterpriseGroup
				and	  Book										= PrmBook
				and   Asset.Company								= PrmCompany
				and   (PrmAsset not entered 	or Asset		= PrmAsset))
				
			Action Rules
				Set Rules
					Exit Rules

				Instance Rules
					if(PrmPurgeOption.PurgeRecords)
						invoke Purge
						
		PurgeDisposedAssetRecapture is a Set Action
			restricted
			Parameters
				PrmCompany					is an AssetCompany
				PrmAsset					is an Asset
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmLeaseCompany				is like GeneralLedgerCompany
				PrmLocationName				is like AssetLocation
				PrmDivision					is like AssetDivision
				PrmAssetGroup				is like AssetGroup
				PrmFromDate					is Date
				PrmToDate					is Date
				
			Instance Selection
				include deleted records
				where (Asset.Company = PrmCompany
				and   (PrmAsset not entered or (Asset = PrmAsset and Asset.Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup))
				and   (PrmLeaseCompany not entered or Asset.AssetLease.LeaseCompany = PrmLeaseCompany)
				and   (PrmLocationName not entered or Asset.AssetLocation = PrmLocationName)
				and   (PrmDivision not entered or Asset.AssetDivision = PrmDivision)
				and   (PrmAssetGroup not entered or Asset.AssetGroup = PrmAssetGroup)
				and   (PrmFromDate not entered or Asset.AssetDisposalDate >= PrmFromDate)
				and   (PrmToDate not entered or Asset.AssetDisposalDate <= PrmToDate)
				and   Asset.Status.Disposed)
				
			Action Rules
				Instance Rules
					invoke Purge
