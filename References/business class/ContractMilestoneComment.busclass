ContractMilestoneComment is a BusinessClass
    owned by po
    prefix is CMSC

    Ontology
    	part of ContractMilestone
    		relative key is SequenceNumber

    Persistent Fields
    	CommentTitle					is a CommentName
    	ContractMilestoneCommentType 	is Numeric size 1
    		States
				NotYetStarted							 			value is 1
    			InProgress						  					value is 2
    			Complete						  					value is 3
    	Description 			 		is Text
    	Attachment 						is an AlternateAttachment
    	IncludeAttachmentOnEmails		is Boolean

	Local Fields
		LocalAttributeCtr	is Numeric 2	
    
	Field Rules
		SequenceNumber
			autosequence 

		IncludeAttachmentOnEmails
			constraint (IncludeAttachmentOnEmails = false
			or         (IncludeAttachmentOnEmails = true
			and         Attachment entered))
				"'Include_Attachment_On_Milestone_Notification_Emails'_canOnlyBeCheckedWhenThereIsAnAttachmentForTheComment."

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Conditions
		HasAttachment
			restricted
			when (Attachment entered)

		HasAttachmentToEmail
			restricted
			when (Attachment entered
			and   IncludeAttachmentOnEmails)

	Actions	
		Create is a Create Action
			Action Rules
				constraint (Attachment entered
				or      	CommentTitle entered)
					"MustEnterEitherACommentTitleOrAnAttachment"

		Update is an Update Action
			Action Rules
				constraint (Attachment entered
				or      	CommentTitle entered)
					"MustEnterEitherACommentTitleOrAnAttachment"

		Delete is a Delete Action

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
														
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
