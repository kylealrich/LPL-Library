SourcingEventWorksheet is a BusinessClass
    owned by ss
    prefix is SEW

	Ontology
		symbolic key is SourcingEventWorksheet
		
    Persistent Fields
 		ItemNumber 				is an Item
 		ItemDescription			is a Description
 		ItemType				
 		ServiceCode				is Alpha size 1
 		Location				is like InventoryLocation
 		EnteredUom				is a UnitOfMeasure
    	Manufacturer
    	ManufacturerNumber			
 		CommodityCode           is a CommCodes
 		RequestedDeliveryDate	is Date
 		DistributionType		is a LineDistributionType
 		ReqlineDescription      is RichText 
  	
 	Transient Fields
 		TransientDescriptionOrComment is Boolean
 	
 	Conditions
 	
 		RequesterEmailEntered
 			restricted
 			when (PurchaseOrderInterface.Requester.EmployeeWorkEmailAddress entered)
 			
 		BuyerEmailEntered
 			restricted
 			when (PurchaseOrderInterface.Buyer.EmployeeWorkEmailAddress entered)
 			
 		WorksheetForCompanyExists
 			restricted
 			when (SourcingEventWorksheetCompanyRel exists)
 			
 		RequisitionPOCommentsExist
 			restricted
 			when (POCommentRel exists)
 			
 		CompanyUsesPOComments
 			restricted
 			when (SourcingCompanyRel.UseCommentsForEventLineDesc)
 			
 		HeaderAddOnChargeExists
 			restricted
 			when (HeaderAddOnChargeRel exists)
 			
 		LineAddOnChargeNotSpreadExists
 			restricted
 			when (LineAddOnChargeNotSpreadRel exists)
 		
 		RequisitionSpreadAddOnChargeExists
 			restricted
 			when (RequisitionSpreadAddOnChargeRel exists)
 		
 		LocalHeaderAOCExists
 			restricted
 			when (LocalHeaderAOCRel exists)
 		
 		LocalLineAOCExists
 			restricted
 			when (LocalLineAOCRel exists)
 		
 		NonService	
 			restricted
 			when (!ItemType = "S")
 		
 		AllServiceItems
 			restricted
 			when (!SourcingEventWorksheetCompanyNonServiceRel exists)
 	
 	Local Fields
 		LocalCompany            is a Company
		LocalSourcingEvent      is a SourcingEvent
		LocalSourcingEventLine  is a SourcingEventLine
 		LocalAOC                is an AddOnCharge
 		LocalCommentTitle       is a CommentName
 		 	
 	Relations
 		ReqLineRel
			one-to-one relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company				= PurchaseOrderInterface.OperatingCompany
				related.Requisition			= PurchaseOrderInterface.SourceDocumentNumeric
				related.RequisitionLine		= PurchaseOrderInterface.LineNumber
 	
 		RequisitionRel
 			one-to-one relation to Requisition
 			Field Mapping uses symbolic key
 				related.Company				= PurchaseOrderInterface.OperatingCompany
				related.Requisition			= PurchaseOrderInterface.SourceDocumentNumeric
 		
 		HeaderAddOnChargeRel
 			one-to-many relation to RequisitionLineAddOnCharge
 			Field Mapping uses symbolic key
 				related.Company			= PurchaseOrderInterface.OperatingCompany
				related.Requisition     = PurchaseOrderInterface.SourceDocumentNumeric
				related.RequisitionLine	= 0
 		
 		LineAddOnChargeNotSpreadRel
 			one-to-many relation to RequisitionLineAddOnCharge
 			Field Mapping uses symbolic key
 				related.Company					= PurchaseOrderInterface.OperatingCompany
				related.Requisition				= PurchaseOrderInterface.SourceDocumentNumeric
				related.RequisitionLine 		= PurchaseOrderInterface.LineNumber
			Instance Selection
				where (!related.FromSpread)	
 		
 		POCommentRel
 			one-to-many relation to RequisitionComment
 			Field Mapping uses part of key
 				related.Company				= PurchaseOrderInterface.OperatingCompany
				related.Requisition			= PurchaseOrderInterface.SourceDocumentNumeric
			Instance Selection
				where (related.CommentType = "P"
				or     related.CommentType = "T")
 		
 		LinePOCommentRel
 			one-to-many relation to RequisitionLineComment
 			Field Mapping uses part of key
 				related.Company				= PurchaseOrderInterface.OperatingCompany
				related.Requisition			= PurchaseOrderInterface.SourceDocumentNumeric
				related.RequisitionLine		= PurchaseOrderInterface.LineNumber
			Instance Selection
				where (related.CommentType = "P")
 		
 		LocalHeaderAOCRel
 			one-to-many relation to SourcingEventLineAOC
 			Field Mapping uses symbolic key
 				related.Company             = Company
 				related.SourcingEvent       = LocalSourcingEvent
 				related.SourcingEventLine   = 0
 			Instance Selection
 				where (related.AOC          = LocalAOC)
 		
 		LocalLineAOCRel
 			one-to-many relation to SourcingEventLineAOC
 			Field Mapping uses symbolic key
 				related.Company             = Company
 				related.SourcingEvent       = LocalSourcingEvent
 				related.SourcingEventLine   = LocalSourcingEventLine
 			Instance Selection
 				where (related.AOC          = LocalAOC)
 		
 		SourcingEventWorksheetCompanyRel
			one-to-many relation to SourcingEventWorksheet
			Field Mapping uses symbolic key
				related.Actor               = actor
			Instance Selection
				where (related.PurchaseOrderInterface.Company  = LocalCompany)
				
		SourcingEventWorksheetCompanyNonServiceRel
			one-to-many relation to SourcingEventWorksheet
			Field Mapping uses symbolic key
				related.Actor               = actor
			Instance Selection
				where (related.PurchaseOrderInterface.Company  = LocalCompany
				and    related.NonService)
		
		SourcingCompanyRel
			one-to-one relation to SourcingCompany
			Field Mapping uses symbolic key
				related.Company             = Company
				
		RequisitionSpreadAddOnChargeRel
			one-to-many relation to RequisitionSpreadAddOnCharge
			Field Mapping uses symbolic key
				related.Company				= PurchaseOrderInterface.OperatingCompany
				related.Requisition			= PurchaseOrderInterface.SourceDocumentNumeric
 	
 		ExistingHeaderCommentRel
 			one-to-one relation to SourcingEventComment
 			Field Mapping uses part of key
 				related.Company             = Company
 				related.SourcingEvent       = LocalSourcingEvent
 				related.Title		        = LocalCommentTitle
 	
 	Sets
 		ByPurchaseOrderInterface
 			indexed
 		 	Sort Order
 		 		Company
	 		 	PurchaseOrderInterface
	 		 	
	Rule Blocks
		CreateSourcingEvent
		
			invoke Create Draft SourcingEvent
				assign result to SavedSourcingEvent
				
				invoked.CreateByCopy 	  			= true
				invoked.CreateFromLibrary 			= true
				invoked.Company 					= Company
				invoked.Name						= NewName
				invoked.Description					= NewDescription
				invoked.EventReference				= NewEventReference
				invoked.SourcingEventType 			= NewSourcingEventType
				invoked.CurrencyCode                = NewCurrencyCode
				invoked.AllowResponseInOtherCurrency = NewAllowResponseInOtherCurrency
				if (NewBuyer entered)
					invoked.Buyer 					= NewBuyer
				else
					invoked.Buyer					= actor.context.Buyer
				invoked.Category 					= NewCategory
				invoked.SubCategory 				= NewSubCategory
				invoked.PostingOptions 				= NewPostingOptions
				invoked.OutputType					= NewOutputType
				invoked.DefaultContractType			= NewDefaultContractType
				invoked.DefaultContractSubType		= NewDefaultContractSubType
				invoked.DefaultContractClass		= NewDefaultContractClass
				invoked.DefaultContractSubClass		= NewDefaultContractSubClass
				invoked.DefaultEffectiveDate		= NewDefaultEffectiveDate
				invoked.DefaultExpirationDate		= NewDefaultExpirationDate
				invoked.DefaultScheduleFrequency	= NewDefaultScheduleFrequency
				invoked.DefaultMaxReleaseNumber		= NewDefaultMaxReleaseNumber
				invoked.DefaultParticipant			= NewDefaultParticipant
				invoked.CreateAmendments			= NewCreateAmendments
				invoked.AllowSupplierQandA			= NewAllowSupplierQandA
				invoked.InformalQuote				= NewInformalQuote
				invoked.AllowAddOnCharge			= NewAllowAddOnCharge
				invoked.PreviewDate					= NewPreviewDate
				invoked.OpenDate					= NewOpenDate
				invoked.CloseDate					= NewCloseDate
				invoked.QandAOpenDate				= NewQandAOpenDate
				invoked.QandACloseDate				= NewQandACloseDate
				invoked.DisputeCloseDate			= NewDisputeCloseDate
				invoked.RequireBidBond				= NewRequireBidBond
				invoked.BidBondPercent				= NewBidBondPercent
				invoked.TwoStepBidding				= NewTwoStepBidding
				invoked.BestAndFinalOffer			= NewBestAndFinalOffer

				if (ApplyTemplate entered)
					fill in fields from ApplyTemplate
				else
				if (!ApplyTemplate entered)
					invoked.VendorItemProcessing = Company.VendorItemProcessing 

			if (ApplyTemplate entered)
				for each ApplyTemplate.SourcingEventQuestion set 
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventQuestion set
						assign result to LocalSourcingEventQuestion
						fill in fields from each

					for each each.SourcingEventQuestionValue set 
						invoke CreateDisplay LocalSourcingEventQuestion.SourcingEventQuestion.SourcingEventQuestionValue set
							fill in fields from each

				for each ApplyTemplate.ActiveSourcingEventArticlesRel
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventArticle set
						assign result to LocalSourcingEventArticle
						fill in fields from each

					for each each.ActiveSourcingEventTermAndConditionRel
						invoke Create LocalSourcingEventArticle.SourcingEventArticle.SourcingEventTermAndCondition set
							assign result to LocalSourcingEventTerm
							fill in fields from each
						
						for each each.SourcingEventTermAndConditionAttachment set		
							invoke Create LocalSourcingEventTerm.SourcingEventTermAndCondition.SourcingEventTermAndConditionAttachment set 
								fill in fields from each
						
				for each ApplyTemplate.SourcingEventMeeting set 
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventMeeting set
						fill in fields from each
				
				for each ApplyTemplate.SourcingEventAttachment set 
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventAttachment set
						fill in fields from each
				
				for each ApplyTemplate.SourcingEventContact set 
					if (each.Contact entered)
						invoke CreateContactFromEmployee SavedSourcingEvent.SourcingEvent.SourcingEventContact set
							fill in fields from each
					else
						invoke CreateAdHocContact SavedSourcingEvent.SourcingEvent.SourcingEventContact set
							fill in fields from each
	
				for each ApplyTemplate.SourcingEventComment set 
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventComment set
						fill in fields from each
						
				for each ApplyTemplate.SourcingEventCommodities set 
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventCommodities set
						fill in fields from each
						
				for each ApplyTemplate.SourcingEventBuyerWeighting set
					invoke Create SavedSourcingEvent.SourcingEvent.SourcingEventBuyerWeighting set
						fill in fields from each
			
			if (HeaderAddOnChargeExists)
				for each HeaderAddOnChargeRel
					LocalSourcingEvent 	= SavedSourcingEvent.SourcingEvent
					LocalAOC 			= each.AddOnCharge
					if (!LocalHeaderAOCExists)
						invoke CreateFromRQ SavedSourcingEvent.SourcingEvent.SourcingEventLineAOC set
							invoked.AOC         	= each.AddOnCharge
							invoked.PerUnitEstimate = each.EnteredUnitCost
							invoked.Account         = each.Account
							
			if (RequisitionSpreadAddOnChargeExists)
				for each RequisitionSpreadAddOnChargeRel
					LocalSourcingEvent 		= SavedSourcingEvent.SourcingEvent
					LocalAOC 				= each.AddOnCharge
					if (!LocalHeaderAOCExists)
						invoke CreateFromRQ SavedSourcingEventLine.SourcingEventLine.SourcingEventLineAOC set
							invoked.Company         = Company
							invoked.SourcingEvent   = SavedSourcingEvent.SourcingEvent
							invoked.AOC         	= each.AddOnCharge
							invoked.PerUnitEstimate = each.EnteredAddOnChargeAmount
							invoked.SpreadMethod    = each.SpreadMethod
						
		CreateSourcingEventLineForNewEvent
			invoke Create Draft SavedSourcingEvent.SourcingEvent.SourcingEventLine set 
				assign result to SavedSourcingEventLine
				invoked.Name												= ItemDescription
				if (!NewOutputType.Service)
					invoked.ItemNumber                                      = ItemNumber
				else
					initialize invoked.ItemNumber
				invoked.ItemDescription										= ItemDescription
				invoked.Description	                                        = ReqlineDescription
				invoked.ItemType											= ItemType
				invoked.ServiceCode											= ServiceCode
				if (NewOutputType.PO)
					invoked.ShipTo											= Location
					invoked.RequestedDeliveryDate							= first RequestedDeliveryDate  
					invoked.DeliverTo										= PurchaseOrderInterface.DeliverTo
				else
					initialize invoked.ShipTo
					initialize invoked.RequestedDeliveryDate
					initialize invoked.DeliverTo
				if (NewOutputType.Contract
				or  NewOutputType.Standing
				or  NewOutputType.Service
				or  NewOutputType.Blanket
				or  NewOutputType.CatalogQuote)
					invoked.DefaultContractType								= NewDefaultContractType
					invoked.DefaultContractSubType							= NewDefaultContractSubType
					invoked.DefaultContractClass							= NewDefaultContractClass
					invoked.DefaultContractSubClass							= NewDefaultContractSubClass
					invoked.DefaultEffectiveDate							= NewDefaultEffectiveDate
					invoked.DefaultExpirationDate							= NewDefaultExpirationDate
				invoked.Quantity											= PurchaseOrderInterface.Quantity
				invoked.UOM.UnitOfMeasure									= EnteredUom
				invoked.ManufacturerInformation.Manufacturer	        	= Manufacturer
				invoked.ManufacturerInformation.ManufacturerNumber			= ManufacturerNumber
				invoked.DistributionType									= DistributionType
				invoked.OutputType											= NewOutputType
				invoked.CommodityCode										= CommodityCode
				if (ApplyTemplate entered)
					invoked.TemplateApplied									= true
				invoked.FromRQ												= true
				invoked.GlobalTradeItemNumber 								= PurchaseOrderInterface.ItemGTIN
		
		CreateSourcingEventLineForAppendedEvent
			invoke Create Draft SourcingEventLine
				assign result to SavedSourcingEventLine
				invoked.Company												= Company
				invoked.SourcingEvent										= Event
				invoked.Name												= ItemDescription
				invoked.ItemNumber	                                        = ItemNumber
				invoked.ItemDescription										= ItemDescription
				invoked.Description  	                                    = ReqlineDescription
				invoked.ItemType											= ItemType
				invoked.ServiceCode											= ServiceCode
				invoked.ShipTo							     				= Location
				invoked.RequestedDeliveryDate				    			= first RequestedDeliveryDate  
				invoked.DeliverTo								    		= PurchaseOrderInterface.DeliverTo
				invoked.Quantity											= PurchaseOrderInterface.Quantity
				invoked.UOM.UnitOfMeasure									= EnteredUom
				invoked.ManufacturerInformation.Manufacturer		    	= Manufacturer
				invoked.ManufacturerInformation.ManufacturerNumber			= ManufacturerNumber
				invoked.DistributionType                                    = DistributionType
				invoked.OutputType											= 1
				invoked.CommodityCode										= CommodityCode
				invoked.FromRQ												= true
				invoked.GlobalTradeItemNumber 								= PurchaseOrderInterface.ItemGTIN
		
		CreateSourcingEventLineSource
		
			invoke Create SavedSourcingEventLine.SourcingEventLine.SourcingEventLineSource set
				invoked.OriginCode				= PurchaseOrderInterface.PurchaseOrderInterface.SourceDocumentOrigin
				invoked.SourceDocumentNumeric	= PurchaseOrderInterface.PurchaseOrderInterface.SourceDocumentNumeric
				invoked.OperatingCompany		= PurchaseOrderInterface.PurchaseOrderInterface.OperatingCompany
				invoked.RequestingLocation		= PurchaseOrderInterface.PurchaseOrderInterface.RequestingLocation
				invoked.LineNumber   			= PurchaseOrderInterface.PurchaseOrderInterface.LineNumber
				invoked.Quantity				= PurchaseOrderInterface.Quantity
				invoked.ShipTo					= PurchaseOrderInterface.ShipToLocation
				invoked.ProcurementGroup        = Company.ProcurementGroup
				invoked.POCode					= PurchaseOrderInterface.POCode
				
		CreateSourcingEventLineAOC
			if (LineAddOnChargeNotSpreadExists)
				for each LineAddOnChargeNotSpreadRel
					LocalAOC 				= each.AddOnCharge
					if (!LocalLineAOCExists)
						invoke CreateFromRQ SavedSourcingEventLine.SourcingEventLine.SourcingEventLineAOC set
							invoked.Company         	= Company
							invoked.SourcingEvent   	= LocalSourcingEvent 
							invoked.SourcingEventLine 	= LocalSourcingEventLine
							invoked.AOC         		= each.AddOnCharge
							invoked.PerUnitEstimate 	= each.EnteredUnitCost
							invoked.Account             = each.Account
	
	Actions
 		Create is a Create Action
 			restricted
 			 		
 		Delete is a Delete Action
			restricted
			 		
 		RemoveSelectedLines is a Delete Action
			default label is "RemoveSelectedLine"
 			
		CreateEventByItem is a Set Action
			run in foreground
			completion message is "Event#<ResultSourcingEvent.SourcingEvent>HasBeenCreated"
			Parameters
				Company						is a SourcingCompany
				ApplyTemplate				is a SourcingEvent
    			NewName						is a Description
				NewDescription				is Text
				NewEventReference			is a Description
				NewSourcingEventType		is a SourcingEventType
				NewBuyer					is a Buyer
				NewCurrencyCode				is a Currency
				NewAllowResponseInOtherCurrency is Boolean
				NewCategory					is a Category
				NewSubCategory				is a SubCategory
				NewPostingOptions			is a PostingOptions
				NewOutputType				is Numeric size 2
					States
						PO				value is 1
						Contract		value is 2
						Blanket			value is 3
						CatalogQuote	value is 4
						Standing		value is 5
						Service			value is 6
						NoOutput		value is 7
				NewDefaultContractType		is a ContractType
				NewDefaultContractSubType	is a ContractSubtype
				NewDefaultContractClass		is a ContractClassification
				NewDefaultContractSubClass	is a ContractSubclassification
				NewDefaultEffectiveDate		is Date
				NewDefaultExpirationDate	is Date
				NewDefaultScheduleFrequency	is a ScheduleFrequency
				NewDefaultMaxReleaseNumber	is Numeric 4
				NewDefaultParticipant		is a Participant
				NewCreateAmendments			is Boolean
				NewAllowSupplierQandA		is Boolean
				NewRequireBidBond			is Boolean
				NewBidBondPercent			is Numeric size 2
				NewTwoStepBidding			is Numeric size 1
					States
						Separate	value is 1
							default label is "TechnicalProposalSubmittedSeparatelyFromBidAmounts"
						Combined	value is 2
							default label is "TechnicalProposalAndBidAmountsSubmittedAtTheSameTime"
				NewBestAndFinalOffer		is Boolean
					default label is "New Best and Final Offer"
				NewInformalQuote			is Boolean
				NewAllowAddOnCharge			is Boolean
				NewPreviewDate				is TimeStamp
				NewOpenDate					is TimeStamp
				NewCloseDate				is TimeStamp
				NewQandAOpenDate			is TimeStamp
				NewQandACloseDate			is TimeStamp
				NewDisputeCloseDate			is TimeStamp

			Parameter Rules
				Company 
					LocalCompany = Company
					required
					constraint (WorksheetForCompanyExists)
						"CannotAppendToEventForCompanyNotEqualToRecordSelected"
				ApplyTemplate
					constraint (ApplyTemplate.IsTemplate)
						"MustSelectAnEventTemplate"
					constraint (!ApplyTemplate.SourcingEventLine set exists)
						"CanOnlyApplyAnEventTemplateWithNoLines"
				NewName
					required
    				initial value is ApplyTemplate.Name
    				if (ApplyTemplate entered) 
    					default to ApplyTemplate.Name
				NewDescription
					initial value is ApplyTemplate.Description
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.Description
				NewEventReference
					initial value is ApplyTemplate.EventReference
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.EventReference
				NewSourcingEventType
					initial value is ApplyTemplate.SourcingEventType
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.SourcingEventType
				NewCurrencyCode
					initial value is ApplyTemplate.CurrencyCode
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.CurrencyCode
				NewAllowResponseInOtherCurrency
					initial value is ApplyTemplate.AllowResponseInOtherCurrency
				NewBuyer
					initial value is ApplyTemplate.Buyer
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.Buyer
				NewCategory
					initial value is ApplyTemplate.Category
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.Category
				NewSubCategory
					initial value is ApplyTemplate.SubCategory
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.SubCategory
				NewPostingOptions
					initial value is ApplyTemplate.PostingOptions
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.PostingOptions

    			NewOutputType
    				initial value is NewOutputType.PO
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.OutputType

					if (NewOutputType.PO)
						initialize NewDefaultContractType
						initialize NewDefaultContractSubType
						initialize NewDefaultContractClass
						initialize NewDefaultContractSubClass
						initialize NewDefaultEffectiveDate
						initialize NewDefaultExpirationDate
						initialize NewDefaultScheduleFrequency
						initialize NewDefaultMaxReleaseNumber
						initialize NewDefaultParticipant

					if (NewOutputType.Service)
						constraint (AllServiceItems)
							"CannotChangeOutputTypeToService;NonServiceItemsExist"
							
					if (NewOutputType.PO
					or  NewOutputType.NoOutput)
						initialize NewDefaultContractType
						initialize NewDefaultContractSubType
						initialize NewDefaultContractClass
						initialize NewDefaultContractSubClass

				NewCreateAmendments
					initial value is ApplyTemplate.CreateAmendments
				NewAllowSupplierQandA
					initial value is ApplyTemplate.AllowSupplierQandA
				NewInformalQuote
					initial value is ApplyTemplate.InformalQuote
				NewAllowAddOnCharge
					initial value is ApplyTemplate.AllowAddOnCharge
				NewRequireBidBond           
					initial value is ApplyTemplate.RequireBidBond
		        NewBidBondPercent           
		        	initial value is ApplyTemplate.BidBondPercent
		        NewTwoStepBidding           
   					initial value is ApplyTemplate.TwoStepBidding
   				NewBestAndFinalOffer        
   					initial value is ApplyTemplate.BestAndFinalOffer
						
			Results
            	ResultSourcingEvent			is a SourcingEvent view
							
			Local Fields
            	SavedSourcingEvent			is a SourcingEvent view
            	SavedSourcingEventLine		is a SourcingEventLine view
				CombinedLineCount       	is Numeric size 10
				TotalPercent            	is a Percent 
				DistributionCount       	is Numeric size 10
				NumberOfDistributions   	is Numeric size 10
				LocalSourcingEventArticle 	is a SourcingEventArticle view
				LinePercentage				is Percent size 6.3
				RoundedTotalPercent			is a Percent
				LocalSourcingEventQuestion	is a SourcingEventQuestion view
				LocalSourcingEventTerm		is a SourcingEventTermAndCondition view
				
			Instance Selection
				where (Actor               = actor
				and    PurchaseOrderInterface.Company = Company)

			Sort Order
				ItemNumber
				ItemDescription 				
		 		ItemType		
		 		ServiceCode				
		 		Location				
		 		EnteredUom				
		 		Manufacturer			
		    	ManufacturerNumber					
		 		CommodityCode                   
		 		DistributionType
		 		PurchaseOrderInterface.SourceDocumentNumeric
			
			Accumulators
				TotalQuantity
				CombinedLines
				CombinedDistributionLines
				
			Action Rules
				Set Rules
					Exit Rules
						ResultSourcingEvent = SavedSourcingEvent
				
					Entrance Rules
						include CreateSourcingEvent
				
				DistributionType Set Rules
					Entrance Rules
						include CreateSourcingEventLineForNewEvent
															
					Exit Rules
						if (CombinedLines > 1)
							invoke UpdateLineFromWorksheet Draft SavedSourcingEventLine.SourcingEventLine
								if (invoked.ServiceCode.Amount)
									invoked.Quantity = 1
								else 
									invoked.Quantity = TotalQuantity 

				Instance Rules
					
					if (!PurchaseOrderInterface.Company entered)
						invoke Delete
					else
						LocalSourcingEvent 		= SavedSourcingEvent.SourcingEvent
						LocalSourcingEventLine 	= SavedSourcingEventLine.SourcingEventLine 
						include CreateSourcingEventLineAOC
						include CreateSourcingEventLineSource
						TotalQuantity             += PurchaseOrderInterface.Quantity
						CombinedLines             += 1
						
					if (CompanyUsesPOComments)
						for each POCommentRel
							LocalCommentTitle 	= each.CommentTitle
							LocalSourcingEvent 	= SavedSourcingEvent.SourcingEvent
							if (ExistingHeaderCommentRel !exists) 
							
								invoke Create SourcingEventComment
									invoked.Company			 	= Company
									invoked.SourcingEvent   	= SavedSourcingEvent.SourcingEvent
									invoked.Title               = each.CommentTitle
									invoked.Type                = each.CommentType
									invoked.Comment             = each.CommentText
									invoked.Attachment          = each.Attachment
									invoked.SupplierCanView     = true
								
						for each LinePOCommentRel
							invoke Create SourcingEventLineComment
								invoked.Company			 	= Company
								invoked.SourcingEvent   	= SavedSourcingEvent.SourcingEvent
								invoked.SourcingEventLine   = SavedSourcingEventLine.SourcingEventLine
								invoked.Title               = each.CommentTitle
								invoked.Type                = each.CommentType
								invoked.Comment             = each.CommentText
								invoked.Attachment          = each.Attachment
								invoked.SupplierCanView     = true
					
					invoke Delete

		CreateEventByLineNumber is a Set Action
			run in foreground
			completion message is "Event#<ResultSourcingEvent.SourcingEvent>HasBeenCreated"
			Parameters
				Company						is a SourcingCompany
				ApplyTemplate				is a SourcingEvent
    			NewName						is a Description
				NewDescription				is Text
				NewEventReference			is a Description
				NewSourcingEventType		is a SourcingEventType
				NewCurrencyCode             is a Currency
				NewAllowResponseInOtherCurrency is Boolean
				NewBuyer					is a Buyer					
				NewCategory					is a Category
				NewSubCategory				is a SubCategory
				NewPostingOptions			is a PostingOptions
				NewOutputType		 		is Numeric size 2
					States
						PO 						value is 1
						Contract 				value is 2
						Blanket 				value is 3
						CatalogQuote 			value is 4
						Standing 				value is 5
						Service 				value is 6
						NoOutput				value is 7
				NewDefaultContractType		is a ContractType
				NewDefaultContractSubType	is a ContractSubtype
				NewDefaultContractClass		is a ContractClassification
				NewDefaultContractSubClass	is a ContractSubclassification
				NewDefaultEffectiveDate		is Date
				NewDefaultExpirationDate	is Date
				NewDefaultScheduleFrequency	is a ScheduleFrequency
				NewDefaultMaxReleaseNumber	is Numeric 4
				NewDefaultParticipant		is a Participant
				NewCreateAmendments			is Boolean
				NewAllowSupplierQandA		is Boolean
				NewRequireBidBond			is Boolean
				NewBidBondPercent			is Numeric size 2
				NewTwoStepBidding			is Numeric size 1
					States
						Separate value is 1
							default label is "TechnicalProposalSubmittedSeparatelyFromBidAmounts"
						Combined value is 2
							default label is "TechnicalProposalAndBidAmountsSubmittedAtTheSameTime"
				NewBestAndFinalOffer		is Boolean
					default label is "New Best and Final Offer"
				NewInformalQuote			is Boolean
				NewAllowAddOnCharge			is Boolean
				NewPreviewDate				is TimeStamp
				NewOpenDate					is TimeStamp
				NewCloseDate				is TimeStamp
				NewQandAOpenDate			is TimeStamp
				NewQandACloseDate			is TimeStamp
				NewDisputeCloseDate			is TimeStamp

			Parameter Rules
				Company
					LocalCompany = Company
					required
					constraint (WorksheetForCompanyExists)
						"CannotAppendToEventForCompanyNotEqualToRecordSelected"
				ApplyTemplate
					constraint (ApplyTemplate.IsTemplate)
						"MustSelectAnEventTemplate"
					constraint (!ApplyTemplate.SourcingEventLine set exists)
						"CanOnlyApplyAnEventTemplateWithNoLines"
				NewName
    				required
    				initial value is ApplyTemplate.Name
    				if (ApplyTemplate entered) 
    					default to ApplyTemplate.Name
				NewDescription
					initial value is ApplyTemplate.Description
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.Description
				NewEventReference
					initial value is ApplyTemplate.EventReference
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.EventReference
				NewSourcingEventType
					initial value is ApplyTemplate.SourcingEventType
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.SourcingEventType
				NewBuyer
					initial value is ApplyTemplate.Buyer
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.Buyer
				NewCurrencyCode
					initial value is ApplyTemplate.CurrencyCode
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.CurrencyCode
				NewAllowResponseInOtherCurrency
					initial value is ApplyTemplate.AllowResponseInOtherCurrency
				NewCategory
					initial value is ApplyTemplate.Category
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.Category
				NewSubCategory
					initial value is ApplyTemplate.SubCategory
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.SubCategory
				NewPostingOptions
					initial value is ApplyTemplate.PostingOptions
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.PostingOptions

    			NewOutputType
    				initial value is NewOutputType.PO
    				if (ApplyTemplate entered)
    					default to ApplyTemplate.OutputType

					if (NewOutputType.PO)
						initialize NewDefaultContractType
						initialize NewDefaultContractSubType
						initialize NewDefaultContractClass
						initialize NewDefaultContractSubClass
						initialize NewDefaultEffectiveDate
						initialize NewDefaultExpirationDate
						initialize NewDefaultScheduleFrequency
						initialize NewDefaultMaxReleaseNumber
						initialize NewDefaultParticipant
						
					if (NewOutputType.Service)
						constraint (AllServiceItems)
							"CannotChangeOutputTypeToService;NonServiceItemsExist"
					
					if (NewOutputType.PO
					or  NewOutputType.NoOutput)
						initialize NewDefaultContractType
						initialize NewDefaultContractSubType
						initialize NewDefaultContractClass
						initialize NewDefaultContractSubClass

				NewCreateAmendments
					initial value is ApplyTemplate.CreateAmendments
				NewAllowSupplierQandA
					initial value is ApplyTemplate.AllowSupplierQandA
				NewInformalQuote
					initial value is ApplyTemplate.InformalQuote
				NewAllowAddOnCharge
					initial value is ApplyTemplate.AllowAddOnCharge
				NewRequireBidBond           
					initial value is ApplyTemplate.RequireBidBond
		        NewBidBondPercent           
		        	initial value is ApplyTemplate.BidBondPercent
		        NewTwoStepBidding           
   					initial value is ApplyTemplate.TwoStepBidding
   				NewBestAndFinalOffer        
   					initial value is ApplyTemplate.BestAndFinalOffer
						
			Local Fields
            	SavedSourcingEvent			is a SourcingEvent view
            	SavedSourcingEventLine		is a SourcingEventLine view
            	LocalSourcingEventArticle 	is a SourcingEventArticle view
				LocalSourcingEventQuestion	is a SourcingEventQuestion view
				LocalSourcingEventTerm		is a SourcingEventTermAndCondition view
			
			Results
            	ResultSourcingEvent			is a SourcingEvent view
							
			Instance Selection
				where (Actor               = actor
				and    PurchaseOrderInterface.Company = Company)

			Sort Order
		 		PurchaseOrderInterface.SourceDocumentNumeric
		 		PurchaseOrderInterface.LineNumber
			
			Action Rules
				Set Rules
					Exit Rules
						ResultSourcingEvent = SavedSourcingEvent
				
					Entrance Rules
						include CreateSourcingEvent
				
				Instance Rules
	
					include CreateSourcingEventLineForNewEvent
																
					LocalSourcingEvent = SavedSourcingEvent.SourcingEvent
					LocalSourcingEventLine = SavedSourcingEventLine.SourcingEventLine
					include CreateSourcingEventLineAOC
					include CreateSourcingEventLineSource

					if (CompanyUsesPOComments)
						for each POCommentRel
							LocalCommentTitle 	= each.CommentTitle
							LocalSourcingEvent 	= SavedSourcingEvent.SourcingEvent
							if (ExistingHeaderCommentRel !exists) 
								invoke Create SourcingEventComment
									invoked.Company			 	= Company
									invoked.SourcingEvent   	= SavedSourcingEvent.SourcingEvent
									invoked.Title               = each.CommentTitle
									invoked.Type                = each.CommentType
									invoked.Comment             = each.CommentText
									invoked.Attachment          = each.Attachment
									invoked.SupplierCanView     = true
								
						for each LinePOCommentRel
							invoke Create SourcingEventLineComment
								invoked.Company			 	= Company
								invoked.SourcingEvent   	= SavedSourcingEvent.SourcingEvent
								invoked.SourcingEventLine   = SavedSourcingEventLine.SourcingEventLine
								invoked.Title               = each.CommentTitle
								invoked.Type                = each.CommentType
								invoked.Comment             = each.CommentText
								invoked.Attachment          = each.Attachment
								invoked.SupplierCanView     = true
					
					invoke Delete
		
		AppendToEventByItem is a Set Action
			run in foreground
			Parameters
				Company				is a SourcingCompany
				Event				is a SourcingEvent
    			
			Parameter Rules
				Company 
					LocalCompany = Company
					required
					constraint (WorksheetForCompanyExists)
						"NoRecordsFoundToProcessForCompanyEntered"
				Event
					required
					constraint (Event.Status.Draft)
		    			"EventMustBeInADraftStatus"
    				constraint (Event exists
    				and 		Event.IsEvent)
    					"EventDoesNotExist"
							
			Local Fields
            	SavedSourcingEventLine	is a SourcingEventLine view
				CombinedLineCount       is Numeric size 10
				TotalPercent            is a Percent 
				LinePercentage			is Percent size 6.3
				RoundedTotalPercent		is a Percent
				
			Instance Selection
				where (Actor               = actor
				and    PurchaseOrderInterface.Company = Company)
			
			Sort Order
				ItemNumber
				ItemDescription 				
		 		ItemType		
		 		ServiceCode				
		 		Location				
		 		EnteredUom				
		 		Manufacturer			
		    	ManufacturerNumber					
		 		CommodityCode                     
		 		DistributionType
		 		PurchaseOrderInterface.SourceDocumentNumeric
			
			Accumulators
				TotalQuantity
				CombinedLines
				
			Action Rules
				Set Rules
				
				DistributionType Set Rules
				
					Entrance Rules
						include CreateSourcingEventLineForAppendedEvent
																						
				Instance Rules
					
					if (!PurchaseOrderInterface.Company entered)
						invoke Delete
					else
						LocalSourcingEvent     = Event
						LocalSourcingEventLine = SavedSourcingEventLine.SourcingEventLine
						include CreateSourcingEventLineAOC
						include CreateSourcingEventLineSource										
		
						TotalQuantity             += PurchaseOrderInterface.Quantity
						CombinedLines             += 1
						
					if (CompanyUsesPOComments)
						for each POCommentRel
							LocalCommentTitle 	= each.CommentTitle
							LocalSourcingEvent 	= Event
							if (ExistingHeaderCommentRel !exists) 
								invoke Create SourcingEventComment
									invoked.Company			 	= Company
									invoked.SourcingEvent   	= Event
									invoked.Title               = each.CommentTitle
									invoked.Type                = each.CommentType
									invoked.Comment             = each.CommentText
									invoked.Attachment          = each.Attachment
									invoked.SupplierCanView     = true
								
						for each LinePOCommentRel
							invoke Create SourcingEventLineComment
								invoked.Company			 	= Company
								invoked.SourcingEvent   	= Event
								invoked.SourcingEventLine   = SavedSourcingEventLine.SourcingEventLine
								invoked.Title               = each.CommentTitle
								invoked.Type                = each.CommentType
								invoked.Comment             = each.CommentText
								invoked.Attachment          = each.Attachment
								invoked.SupplierCanView     = true
					
					invoke Delete
					
		AppendToEventByLineNumber is a Set Action
			run in foreground
			Parameters
				Company				is a SourcingCompany
				Event				is a SourcingEvent
    			
			Parameter Rules
				Company 
					LocalCompany = Company
					required
					constraint (WorksheetForCompanyExists)
						"NoRecordsFoundToProcessForCompanyEntered"
				Event
					required
					constraint (Event.Status.Draft)
		    			"EventMustBeInADraftStatus"
    				constraint (Event exists
    				and 		Event.IsEvent)
    					"EventDoesNotExist"
							
			Local Fields
            	SavedSourcingEventLine		is a SourcingEventLine view
			
			Instance Selection
				where (Actor               = actor
				and    PurchaseOrderInterface.Company = Company)
			
			Sort Order
				PurchaseOrderInterface.SourceDocumentNumeric
		 		PurchaseOrderInterface.LineNumber
			
			Action Rules
																						
				Instance Rules
					
					include CreateSourcingEventLineForAppendedEvent
					LocalSourcingEvent     = Event
					LocalSourcingEventLine = SavedSourcingEventLine.SourcingEventLine
					include CreateSourcingEventLineAOC
					include CreateSourcingEventLineSource
					
					if (CompanyUsesPOComments)
						for each POCommentRel
							LocalCommentTitle 	= each.CommentTitle
							LocalSourcingEvent 	= Event
							if (ExistingHeaderCommentRel !exists) 
								invoke Create SourcingEventComment
									invoked.Company			 	= Company
									invoked.SourcingEvent   	= Event
									invoked.Title               = each.CommentTitle
									invoked.Type                = each.CommentType
									invoked.Comment             = each.CommentText
									invoked.Attachment          = each.Attachment
									invoked.SupplierCanView     = true
								
						for each LinePOCommentRel
							invoke Create SourcingEventLineComment
								invoked.Company			 	= Company
								invoked.SourcingEvent   	= Event
								invoked.SourcingEventLine   = SavedSourcingEventLine.SourcingEventLine
								invoked.Title               = each.CommentTitle
								invoked.Type                = each.CommentType
								invoked.Comment             = each.CommentText
								invoked.Attachment          = each.Attachment
								invoked.SupplierCanView     = true
					
					invoke Delete

