PayablesSecondaryWithholdingCode is a BusinessClass
    owned by ap
    prefix is SWHC
    sql name is PSecondaryWithholdingCode

    Ontology
        symbolic key is PayablesSecondaryWithholdingCode
            sql name is PSecondaryWithholdingCode

    Patterns
        implements StaticJava
        disable AuditIndex
    
    Attach Rules
		if (parentcontext.name != "PayablesSecondaryWithholdingRate")
			constraint (Active)
				"PayablesSecondaryWithholdingCodeIsNotActive"
			constraint (PayablesSecondaryWithholdingRate set exists)
				"SecondaryWithholdingRateMustBeAddedFirst"

    Persistent Fields
		Description
        WithholdingType			is Numeric size 1
            classic name is WITHHOLD-TYPE
            States
                Federal value is 1
                State   value is 2
                Other   value is 3
        MinimumPaymentAmount   is an InternationalAmount
        Active

    Field Rules
    	Description	
    		required
    		
        WithholdingType
            required

		MinimumPaymentAmount

			constraint (MinimumPaymentAmount >= 0)		
				"MinimumPaymentAmountMustBeGreaterThanOrEqualTo_0"                  

    Derived Fields
        CreateMessage is a LabelField
			restricted
			"CreateSecondaryWithholdingCode"
		
		FormLabel is a LabelField
			restricted
			"SecondaryWithholdingCode:_"

		RepresentativeText is a StringField
			type is Text
			default label is "SecondaryWithholdingCode"
			PayablesSecondaryWithholdingCode " - " Description
		
		DerivedSecondaryWithholdingCodeTitle	is a DerivedField
			type is Alpha size up to 132
			default label is "SecondaryWithholdingCode"
			return RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return FormLabel + DerivedSecondaryWithholdingCodeTitle
			else
				return CreateMessage

	Conditions
		RecordExists
			restricted
			when (PayablesSecondaryWithholdingCode exists)

	Actions
		Create is a Create Action
				
		Update is an Update Action

		Delete is a Delete Action

