ProcurementCardRequest is a BusinessClass
    owned by po
    prefix is PDC
    classic name is PDCARD

    Ontology
        symbolic key is ProcurementCardRequest
            classic set name is PDCSET1
            classic name is REQUEST-NBR
            classic name for ProcurementCardProgram is PCARD-PROGRAM

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        ProcurementCardUser
            classic name is CARD-USER-ID
        ProcurementCardProxyUser             is a ProcurementCardUser
            classic name is PROXY-USER-ID
        ProcurementCardNumber
            classic name is PCARD-NBR
        Status                               is Numeric size 1
            States
                New        value is 0
                Unapproved value is 1
                Approved   value is 2
                Activated  value is 3
                Rejected   value is 8
                Cancelled  value is 9
                All        value is blank
                    default label is "ALL"
        ProcurementCardRequestEffectiveDate  is Date
            sql name is PCardRequestEffectiveDate
            classic name is EFFECT-DATE
        ProcurementCardRequestExpirationDate is Date
            sql name is PCardRequestExpirationDate
            classic name is EXP-DATE
        CardUserName
        UserPhoneNumber                      is a TelephoneNumber 
        	holds pii
            classic name for UserPhoneNumber.InternationalPrefix is USER-PREFIX
            classic name for UserPhoneNumber.SubscriberNumber is USER-PHONE
            classic name for UserPhoneNumber.Extension is USER-EXT
        EmailAddress
        ProxyName                            is a CardUserName	 
        	holds pii
        ProxyPhoneNumber                     is a TelephoneNumber 
        	holds pii
            classic name for ProxyPhoneNumber.InternationalPrefix is PROXY-PREFIX
            classic name for ProxyPhoneNumber.SubscriberNumber is PROXY-PHONE
            classic name for ProxyPhoneNumber.Extension is PROXY-EXT
        ProxyEmailAddress                    is an EmailAddress 
        	holds pii
            classic name is PROXY-EMAIL
        CardType
        ComCodeFl                            is a Flag
        TaxOption
        SingleTransactionLimit               is an UnsignedInternationalAmount
            classic name is TRAN-LIMIT
        MerchantGroup
            classic name is MERCHANT-GRP
        IncludeExcludeMerchantGroup          is AlphaUpper size 1
            classic name is MERCHANT-FL
            States
                Exclude value is "E"
                Include value is "I"
        IncludeExcludeItems                  is AlphaUpper size 1
            classic name is ITEM-FL
            States
                Exclude value is "E"
                Include value is "I"
        UserField01
            classic name is USR-FLD-01
        UserField02
            classic name is USR-FLD-02
        UserField03
            classic name is USR-FLD-03
        UserField04
            classic name is USR-FLD-04
        UserField05
            classic name is USR-FLD-05
        UserId                               is AlphaUpper size 80
        Employee                             
        Note                                 is a Text
	
	Local Fields

		LocalPulseAlertFrom 			is Alpha size 100
		LocalConfigurationParameter		is Alpha size up to 200

	Derived Fields
		DisplayCardUserName		is a ConditionalField
			type is Alpha up to 101
			
			if (ProcurementCardUser entered)
				ProcurementCardUser.Employee.FirstLastName
			else
				CardUserName
		
		FormTitleMessage		is a MessageField
			restricted
			"<ProcurementCardRequest>inProgram<ProcurementCardProgram>_-_<ProcurementCardProgram.Description>Requested_by<UserId>"


		DerivedTenantID is a DerivedField
			type is Alpha size 60
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		ProcurementCardRejectAlertDescription is a DerivedField
			type is Alpha size 100
			restricted
			return "P-Card request " + ProcurementCardProgram + " has been rejected"
		
		ProcurementCardRejectAlertXML is a DerivedField
			type is XMLDocument
			restricted
			ProcurementCardRejectAlertXML = template.IONProcurementCardRejectAlert_ProcurementCardRequest_ST document for this instance
			return ProcurementCardRejectAlertXML
		
		DerivedProcurementCardRejectPulseAlertBODID is a DerivedField
			type is Alpha size 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup +":"+ ProcurementCardProgram +":"+ "?ProcurementCardRejectAlert&verb=Process"	
			
		



		ProcurementCardRequestAlertDescription is a DerivedField
			type is Alpha size 100
			restricted
			return "P-Card request " + ProcurementCardProgram + " has been created"
		
		ProcurementCardRequestAlertXML is a DerivedField
			type is XMLDocument
			restricted
			ProcurementCardRequestAlertXML = template.IONProcurementCardRequestAlert_ProcurementCardRequest_ST document for this instance
			return ProcurementCardRequestAlertXML
		
		DerivedProcurementCardRequestPulseAlertBODID is a DerivedField
			type is Alpha size 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup +":"+ ProcurementCardProgram +":"+ "?ProcurementCardRequestAlert&verb=Process"	


			
    Conditions
        IsActivated
        	restricted
            when (Status.Activated)

        IsApproved
        	restricted
            when (Status.Approved)

        IsCancelled
        	restricted
            when (Status.Cancelled)

        HasProxyName
            classic name is PROXY
            restricted
            when (ProxyName entered)

        IsRejected
        	restricted
            when (Status.Rejected)

        IsRequested
        	restricted
            when (Status.Unapproved)

        HasCardUserName
            classic name is USER
            restricted
            when (CardUserName entered)
            
        IsNew
        	restricted
        	when (Status.New)   

		ProcurementGroupExists
			restricted
			when (ProcurementGroup entered)

		ProcurementCardExists
			restricted
			when (ProcurementCardProgram entered) 
			 	
    Relations				
		DefaultProcurementCardCompaniesRel
			one-to-many relation to ProcurementCardCompany
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
				related.ProcurementCardProgram	= ProcurementCardProgram
				related.ProcurementCardRequest 	= ProcurementCardRequest
			Instance Selection
				where (related.IsDefault)		
				
		ProcurementCardProgramCompaniesRel
			one-to-many relation to ProcurementCardProgramCompany
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
				related.ProcurementCardProgram	= ProcurementCardProgram   
				
		PurchaseOrdersRel
			one-to-many relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company												= ProcurementCardProgramCompaniesRel.ProcurementCardProgramCompany.Company
			Instance Selection
				where (related.ProcurementCardNumber						= ProcurementCardNumber)	

		PurchaseOrdersWithProcurementCardNumberRel
			one-to-many relation to PurchaseOrder
			Field Mapping uses ByProcurementCardNumber
				related.Company					= ProcurementCardProgramCompaniesRel.ProcurementCardProgramCompany.Company
				related.ProcurementCardNumber	= ProcurementCardNumber

		ProcurementCardStatementTransactionsCannotCancelRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
				related.ProcurementCardProgram	= ProcurementCardProgram
			Instance Selection
				where (related.ProcurementCardStatementTransaction.Status	!= related.ProcurementCardStatementTransaction.Status.Closed
				and	   related.ProcurementCardNumber						= ProcurementCardNumber)		

		ProcurementCardRequestsByCardNumberRel
			one-to-many relation to ProcurementCardRequest
			Field Mapping uses Set2
				related.ProcurementCardNumber	= ProcurementCardNumber	
			Instance Selection
				where (related.ProcurementCardRequest	!= ProcurementCardRequest)


		
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationPulseAlertRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "PulseAlert"
            	related.FSMBODConfiguration.Direction 	= 1
        
        FSMBODConfigurationDetailPulseAlertRel
        	one-to-many relation to FSMBODConfigurationDetail
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 			= 2	
            	related.FSMBODConfiguration.Noun 			= "PulseAlert"
            	related.FSMBODConfiguration.Direction 		= 1
            Instance Selection
				where (related.Alert						= LocalPulseAlertFrom
				and	   related.Enable)


														
    Sets
        Set2
            indexed
            Sort Order
                ProcurementCardNumber
                ProcurementCardProgram
                ProcurementCardRequest

        Set3
            indexed
            Sort Order
                UserId
                ProcurementCardProgram
                ProcurementCardNumber
                ProcurementCardRequest
                
        Set6
            indexed
            Sort Order
                ProcurementCardProxyUser
                ProcurementCardProgram
                ProcurementCardNumber
                ProcurementCardRequest

    Rule Blocks
    	CancelActionRules
			confirmation required
				"Warning:CancellationActionsCannotBeReversed.DoYouWantToContinue?"

			constraint (ProcurementCardStatementTransactionsCannotCancelRel not exists)
				"CannotCancel,OpenChargesExist"
				
			if (ProcurementCardNumber entered)
				constraint (PurchaseOrdersWithProcurementCardNumberRel not exists)
					"CannotCancel,OpenPO'sExist"

			make transition to Cancelled			
                
	Field Rules
		ProcurementCardNumber
			if (old ProcurementCardNumber entered)
				cannot be changed
				
			constraint (ProcurementCardRequestsByCardNumberRel not exists)
				"CardNumberMustBeUnique"
			
		ProcurementCardRequestEffectiveDate
			default to ProcurementCardNumber.ProcurementCardNumberEffectiveDate
			constraint (ProcurementCardProgram.ProcurementCardProgramEffectiveDate <= ProcurementCardRequestEffectiveDate)
				"ProgramEffectiveDate<ProcurementCardProgram.ProcurementCardProgramEffectiveDate>GreaterThanCardEffectiveDate"					
			
			constraint (ProcurementCardNumber.ProcurementCardNumberEffectiveDate <= ProcurementCardRequestEffectiveDate)
				"ProcurementCardRequestEffectiveDateIsOutOfRangeOfProcurementCardNumberEffectiveDate"
							
		ProcurementCardRequestExpirationDate
			default to ProcurementCardNumber.ProcurementCardNumberExpirationDate
			constraint (ProcurementCardRequestExpirationDate >= current corporate date
			and  ProcurementCardRequestExpirationDate > ProcurementCardRequestEffectiveDate)
				"ExpirationDateMustBeGreaterThanCurrentDateAndEffectiveDate"					
				
			constraint (ProcurementCardNumber.ProcurementCardNumberExpirationDate >= ProcurementCardRequestExpirationDate)
				"ProcurementCardRequestExpirationDateIsOutOfRangeOfProcurementCardNumberExpirationDate"

		CardType
			default to ProcurementCardNumber.CardType

		TaxOption
			default to ProcurementCardNumber.TaxOption

		CardUserName
			initial value is ProcurementCardUser.Employee.FirstLastName

			default to ProcurementCardUser.Employee.FirstLastName
			default to Employee.FirstLastName

			if (ProcurementCardUser not entered)
				required
					"EitherCardUserID_orCardUserNameMustBeEntered"
					          
		UserPhoneNumber			          
			initial value is ProcurementCardUser.Employee.UseForWorkPhone.ContactDetail.Telephone
			
			default to ProcurementCardUser.Employee.UseForWorkPhone.ContactDetail.Telephone
			default to Employee.UseForWorkPhone.ContactDetail.Telephone

			if (old ProcurementCardUser entered
			and ProcurementCardUser not entered
			and Employee not entered)
				initialize
					          
		EmailAddress
			initial value is ProcurementCardUser.EmployeeWorkEmailAddress

			default to ProcurementCardUser.EmployeeWorkEmailAddress
			default to Employee.EmployeeWorkEmailAddress

			if (old ProcurementCardUser entered
			and ProcurementCardUser not entered
			and Employee not entered)
				initialize
			
		Note
			initial value is ProcurementCardUser.Note
			default to ProcurementCardUser.Note

			if (old ProcurementCardUser entered
			and ProcurementCardUser not entered)
				initialize

		Employee
			if (old ProcurementCardUser entered
			and ProcurementCardUser not entered)
				initialize

		ProcurementCardProxyUser
			constraint (ProcurementCardProxyUser.ProxyUser)
				"ProxyUserID_isNotSetUpAsProxy"
			
		ProxyName
			initial value is ProcurementCardProxyUser.Employee.FirstLastName
			force default to ProcurementCardProxyUser.Employee.FirstLastName

			if (ProcurementCardProxyUser not entered)
				required
					"EitherProxyUserID_orProxyUserNameMustBeEntered"          
					          
		ProxyPhoneNumber			          
			initial value is ProcurementCardProxyUser.Employee.UseForWorkPhone.ContactDetail.Telephone			
			force default to ProcurementCardProxyUser.Employee.UseForWorkPhone.ContactDetail.Telephone

			if (old ProcurementCardProxyUser entered
			and ProcurementCardProxyUser not entered)
				initialize
					          
		ProxyEmailAddress
			initial value is ProcurementCardProxyUser.EmployeeWorkEmailAddress
			force default to ProcurementCardProxyUser.EmployeeWorkEmailAddress

			if (old ProcurementCardProxyUser entered
			and ProcurementCardProxyUser not entered)
				initialize
					
 		UserId
 			initial value is actor
 			force default to actor

	StateCycles
		ProcurementCardRequestLifeCycle is a StateCycle
			state field is Status
			
			New is a State
			
				Create is a Create Action
					Field Rules			
						ProcurementCardRequest
							autosequence using ProcurementCardProgram.LastProcurementCardRequest 
							cannot be changed	
					
				Release is an Instance Action
					Action Rules
						invoke TriggerProcurementCardRequestService

						invoke TriggerProcurementCardRequestAlert	
							invoked.PrmPulseAlert = "FSM_ION_ProcurementCardRequestAlert"

						send email
							to ProcurementCardProgram.ProcurementCardProgramAdministrator.Employee.EmployeeWorkEmailAddress
							from ProcurementCardUser.Employee.EmployeeWorkEmailAddress
							subject "NewCardRequestReleased"
							Contents
								"DearProcurementCardAdmin,"
								"ThisIsAMessageToLetYouKnowThatANewCardRequestHasBeenReleased."
								"Details:"
								"-_Program:<ProcurementCardProgram>"
								"-_RequestID:<ProcurementCardRequest.ProcurementCardRequest>"
								"-_UserID:<ProcurementCardUser>"
								"-_CardholderName:<CardUserName>"
							
						make transition to Unapproved
						
				Update is an Update Action
				
				Delete is a Delete Action			
			
			Unapproved is a State
				Approve is an Instance Action			
					Action Rules
						constraint (DefaultProcurementCardCompaniesRel exists)			
							"OneProcurementCardCompanyMustBeMarkedAsTheDefault"
			
						make transition to Approved
						
				Reject is an Instance Action
					Action Rules
						invoke TriggerProcurementCardRejectService

						invoke TriggerProcurementCardRejectAlert
							invoked.PrmPulseAlert = "FSM_ION_ProcurementCardRejectAlert"

						send email
							to ProcurementCardUser.Employee.EmployeeWorkEmailAddress
							from ProcurementCardProgram.ProcurementCardProgramAdministrator.Employee.EmployeeWorkEmailAddress
							subject "CardHasBeenRejected"
							Contents
								"DearProcurementCardHolder,"
								"ThisIsAMessageToLetYouKnowThatYourCardHasBeenRejected."
								"Details:"
								"-_Program:<ProcurementCardProgram>"
								"-_RequestID:<ProcurementCardRequest.ProcurementCardRequest>"
								"-_UserID:<ProcurementCardUser>"
								"-_CardholderName:<CardUserName>"
						
						make transition to Rejected											
						
				Update is an Update Action

			Rejected is a State
					
			Approved is a State
				Activate is an Instance Action			
					Action Rules
						constraint (DefaultProcurementCardCompaniesRel exists)			
							"OneProcurementCardCompanyMustBeMarkedAsTheDefault"
							
						constraint (ProcurementCardNumber entered)
							"CardNumberRequiredToActivate"	
							
						constraint (ProcurementCardRequestEffectiveDate entered)
							"CannotActivateEffectiveDateIsRequired"	
																		
						constraint (ProcurementCardRequestExpirationDate entered)
							"CannotActivateExpirationDateIsRequired"							

						constraint (ProcurementCardUser entered)
							"CardUserIDRequiredToActivate"	
							
						constraint (ProcurementCardProxyUser entered)
							"ProxyUserIDRequiredToActivate"	
							
						invoke UpdateCardNumber ProcurementCardCompany
							invoked.PrmProcurementGroup 		= ProcurementGroup
							invoked.PrmProcurementCardProgram 	= ProcurementCardProgram
							invoked.PrmProcurementCardRequest 	= ProcurementCardRequest
							invoked.PrmProcurementCardNumber	= ProcurementCardNumber
						
						send email
							to ProcurementCardUser.EmployeeWorkEmailAddress
							from ProcurementCardProgram.ProcurementCardProgramAdministrator.Employee.EmployeeWorkEmailAddress
							subject "CardHasBeenApproved"
							Contents
								"DearProcurementCardHolder,"
								"ThisIsAMessageToLetYouKnowThatYourCardHasBeenApproved."
								"Details:"
								"-_Program:<ProcurementCardProgram>"
								"-_RequestID:<ProcurementCardRequest.ProcurementCardRequest>"
								"-_UserID:<ProcurementCardUser>"
								"-_CardholderName:<CardUserName>"
						
						make transition to Activated

				Cancel is an Instance Action
					Action Rules							
						include CancelActionRules
						
				Update is an Update Action
					
			Activated is a State
				Update is an Update Action

				Cancel is an Instance Action	
					Action Rules
						include CancelActionRules
					
			Cancelled is a State
    
    Actions
    



    	TriggerProcurementCardRequestService is an Instance Action  
	  		restricted
			Action Rules
				if (ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger)	
					trigger "ProcurementCardRequestService" PA service
						resume on error
						title is "EG:<ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup>ProCardProgram:<ProcurementCardProgram>"
						Criteria
							ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup
							ProcurementCardProgramCompaniesRel.ProcurementCardProgramCompany.Company
						Variables
							ProcurementCardProgram	
							ProcurementCardNumber
							ProcurementCardUser
							CardUserName
							EmailAddress
							ProxyEmailAddress









    	TriggerProcurementCardRejectService is an Instance Action  
	  		restricted
			Action Rules
				if (ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger)		
					trigger "ProcurementCardRejectService" PA service
						resume on error
						title is "EG:<ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup>ProCardProgram:<ProcurementCardProgram>"
						Criteria
							ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup
							ProcurementCardProgramCompaniesRel.ProcurementCardProgramCompany.Company
						Variables
							ProcurementCardProgram	
							ProcurementCardNumber
							ProcurementCardUser
							CardUserName
							EmailAddress
							ProxyEmailAddress




		TriggerProcurementCardRejectAlert is an Instance Action
			restricted
			Parameters
				PrmPulseAlert is Alpha size 100
			Action Rules
				LocalPulseAlertFrom = PrmPulseAlert
				if (ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and FSMBODConfigurationPulseAlertRel.Enable)
			        if(FSMBODConfigurationDetailPulseAlertRel.Enable)
						invoke TriggerPulseAlert FSMBODConfigurationPulseAlertRel
							invoked.PrmActorGroup 	= "PROCUREMENTGROUP" 
							invoked.PrmMainXML 		= ProcurementCardRejectAlertXML	
							invoked.PrmDescription	= ProcurementCardRejectAlertDescription
							invoked.PrmBODID		= DerivedProcurementCardRejectPulseAlertBODID	



		TriggerProcurementCardRequestAlert is an Instance Action
			restricted
			Parameters
				PrmPulseAlert is Alpha size 100
			Action Rules
				LocalPulseAlertFrom = PrmPulseAlert
				if (ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and FSMBODConfigurationPulseAlertRel.Enable)
			        if(FSMBODConfigurationDetailPulseAlertRel.Enable)
						invoke TriggerPulseAlert FSMBODConfigurationPulseAlertRel
							invoked.PrmActorGroup 	= "PROCUREMENTGROUP" 
							invoked.PrmMainXML 		= ProcurementCardRequestAlertXML	
							invoked.PrmDescription	= ProcurementCardRequestAlertDescription
							invoked.PrmBODID		= DerivedProcurementCardRequestPulseAlertBODID

