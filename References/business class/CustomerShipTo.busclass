CustomerShipTo is a BusinessClass
    owned by bl
    prefix is SHP
    classic name is SHIPTO

    Ontology
        symbolic key is CustomerShipTo
            classic set name is SHPSET1
            classic name is SHIP-TO

    Patterns
        implements StaticJava

        disable AuditIndex
        implements BODId

    Persistent Fields

        Name					   is a VendorName 
        	holds pii
        Contact                    is a Name	 
        	holds pii
        ShipToAddress              is a PostalAddressV2	
        	holds pii
            classic name for ShipToAddress.DeliveryAddress.AddressLine1 is ADDR1
            classic name for ShipToAddress.DeliveryAddress.AddressLine2 is ADDR2
            classic name for ShipToAddress.DeliveryAddress.AddressLine3 is ADDR3
            classic name for ShipToAddress.DeliveryAddress.AddressLine4 is ADDR4
            classic name for ShipToAddress.Municipality is CITY
            classic name for ShipToAddress.StateProvince is STATE
            classic name for ShipToAddress.PostalCode is ZIP
            classic name for ShipToAddress.Country is COUNTRY
            classic name for ShipToAddress.Region is REGION
        TaxExemptCode
            classic name is TAX-EXEMPT-CD
        TaxCode
        FreightCode                is a BillingFreightCode
        Carrier                    is a Vendor
		BrokenCaseCharge 		   is Boolean
            classic name is BRK-CHRG-FL
		AllowBackorder		   	   is Boolean							
            classic name is BACK-ORD-FL
        ShipComplete		   	   is Boolean	
            classic name is SHIP-COMP-FL
        PickingPriority
            classic name is PK-PRIORITY
        ProofOfDeliveryRequired	   is Boolean	
            classic name is POD-REQ-FL
        ProofOfDeliveryGracePeriod is an AgePeriods
            classic name is POD-GRACE-PER
        DefaultLocation            is an InventoryLocation
            classic name is DEF-LOCATION
		DefaultMethod 			   is a ShippingMethod
            classic name is DEF-METHOD
		InUse		   	   		   is Boolean
            classic name is USED-FL
        Active
            classic name is ACTIVE-FLAG
        Territory
        SalesRepresentative
            classic name is SALESMAN
        SalesRepresentative2                  is a SalesRepresentative

		CommissionSplitPercentage
            classic name is COMM-SPLIT
        TermCode		 is a TermsCode
            classic name is TERMS-CD
        ListName		 is a PriceListContractPromotion
        	default label is "PriceList"
        LastCommentSequence        is a Sequence3
            classic name is LAST-CMNT-SEQ
        ShipToEDI        is AlphaUpper size 16
            classic name is SHIP-TO-EDI
        EmailAddress
            classic name is E-MAIL-ADDRESS
        Vendor
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
            context of Vendor
        EuropeanCommunityCountry   is an EcCtryNbr
            classic name is EC-CTRY-NBR
        ISOCountry                 is an IsoCtryNbr
            classic name is ISO-CTRY-NBR
        ShipTerm
            classic name is FOB-CODE
        CountryCode					is a Country
        RegionCode					is a Region
            classic name is REGION
        PortCode		 is a IntrastatPortCode
            classic name is PORT-CD
        MemoTerm         is a TermsCode
		PhoneNumber            is a TelephoneNumber 
			holds pii
            classic name for PhoneNumber.InternationalPrefix is INT-PREFIX
            classic name for PhoneNumber.SubscriberNumber is PHONE-NMBR
            classic name for PhoneNumber.Extension is PHONE-EXT
        FaxNumber
            classic name for FaxNumber.SubscriberNumber is FAX-NUMBER
            classic name for FaxNumber.Extension is FAX-EXT
        AddDate                    is Date
		Logo						is a LogoAttachment
		CustomerLocationType	is a LocationRegistrationType
			restricted                                            
			protected
		VATRegistrationNumber
		TaxRegistrationType					is Numeric size 1
            States
                Registered				value is 1
                Unregistered			value is 2
                SpecialEconomicZone		value is 3
		        
	Transient Fields
	
		DefaultShipTo 				is Boolean  
			derive value from DeriveValueForDefaultShipTo
		ObjectReference 			is BusinessObjectReference
			derive value from reference to this instance
 		TransientFEG 				is like FinanceEnterpriseGroup
 			derive value from Company.FinanceEnterpriseGroup      		
		TransientAccountingEntity	is like AccountingEntity
 			derive value from Company.AccountingEntity		
 					
	Local Fields
	
		CalculateTax
		LocalState					is a StateProvince
		LocalZip					is a PostalCode
		LocalCounty					is a County
		LocalGeoCode 				is a VertexTaxAreaID
		LocalLookupKey 				is Alpha 100
        LocalResponseBusclassKey 	is like LastSequence	 
        LocalSequence 				is like Sequence
		MultipleTaxCodes     		is Boolean
     	WebServiceError     		is Boolean
    	WebServiceErrorMessage 		is Text	
    	OldTaxCode					is like TaxCode

		MaxLogoSize
		ActionCode							is Alpha size 1
            States
                Create  value is "C"
                Update	value is "U"
                Delete	value is "D"
        LocalTitle 				is Alpha size 255
		NewTaxCode					is like TaxCode
		CalculateTaxOutputMessage 	is Alpha 200
		LocalMaxCustomerShipTo		is Numeric size 4
		LocalShipTo					is Numeric size 4
		LocalCustomerShipTo			is Numeric size 4

		LocalNativeLPLBODTrigger	is Boolean
		LocalMainUserTemplate		is Alpha size 250
		LocalFSMInboundBODTracker	is Numeric 15
		Error            			is Boolean
	    ErrorMessage     			is Alpha 300
	    LocalBODTrigger				is Boolean
	    NewBODTracker  				is a FSMInboundBODTracker view

		LocalConfigurationParameter	is Alpha size up to 200
		LocalAEAlreadyExecuted		is Boolean
		LocalAccountingEntity  		is Alpha 100
		
	Context Fields
		FSMInboundBODTracker

	Derived Fields

		ContextMessageEntityType is a StringField
	    	type is Alpha 30
	    	restricted
	        "InforShipToPartyMaster"
                  
		ContextMessageText is a MessageField
	        restricted
	        "Customer<Customer>CustomerShipTo<CustomerShipTo>"
		
		MessageForAdd		is a MessageField
			restricted
			"AddCompleteStateMayBeRequiredForVertexTaxCodeLookUp"
		MessageForUpdate	is a MessageField
			restricted
			"UpdateCompleteInvalidDataToPerformVertexLookUp"
		
		RepresentativeText is a StringField
			type is Text
			default label is "CustomerShipTo"
			CustomerShipTo " - " Name

		VertexCompletionMessage	is a DerivedField
   			type is MessageField
   			restricted
   			if (WebServiceError)
   				return WebServiceErrorMessage
   			else	
   			if (MultipleTaxCodes)
   				return VertexMultTaxCodesMessage
   			else
   				return VertexMessage	 

		OldTaxCodeMsg		is a MessageField
			restricted
			"OldTaxCode:<OldTaxCode>"
			   				
		VertexMultTaxCodesMessage is a MessageField
			restricted
			"TaxCodeNotUpdated.MultipleTaxCodesExistsForVendorsAddress"
			
		VertexMessage	is a MessageField
			restricted
			"VertexLookupComplete.TaxCodeUpdated"
		
		NoRecordsMessage	is a MessageField
			restricted
			"NoRecordsFoundToProcess"
						
		CompletionMessage	is a DerivedField
   			type is MessageField
   			
   			if (action type.Create)
   				return MessageForAdd
   			else
   			if (action type.Update)
   				return MessageForUpdate	 
   				
   		LocalStateZip   is a StringField
            type is Alpha size 30
            restricted
			LocalState
            LocalZip
            
       	DeriveValueForDefaultShipTo	is a ConditionalField
			type is Boolean
			restricted
       		if (DefaultShipToEqualToCustomerShipTo and CustomerShipTo entered)
				true
			else
				false      		

		VertexNoChangeMessage				is a LabelField
			"NoChangeNecessary,Correct\Tax\Area\IdExists"

		DerivedLookupMessage				is a DerivedField
			type is MessageField
			restricted
			if (NewTaxCode = OldTaxCode)
				return VertexNoChangeMessage
			else
				return CalculateTaxOutputMessage 

		TaxCodeMessage				is a DerivedField
			type is MessageField
			restricted
			if (NewTaxCode = OldTaxCode)
				return blank
			else
				return OldTaxCodeMsg


		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
			if (DerivedDelimiter = "")
				return "@@"
		
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
				
		DerivedCustomerStatus is a DerivedField
			type is Alpha 10
			restricted
			if (ActionCode="D")
				return "Deleted"
			else 
			if (Active !="true") 
				return "Closed"
			else
				return "Open"

		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (ActionCode="C")
				return "Add"
			else
			if (ActionCode="U")
				return "Replace"
			else
			if (ActionCode="D")
				return "Delete"
			else
				return ""		
		
		DerivedFEG is a DerivedField
			type is Alpha size 4
			restricted
			return Company.FinanceEnterpriseGroup
		
		DerivedAE is a DerivedField
			type is Alpha size 12
			restricted
			return Company.AccountingEntity 
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
		
		DerivedBODVariationID is a DerivedField
			type is Alpha size 25
			restricted
			return  bod id.VariationID
		
		DerivedBODID is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedAccountingEntity + ":" +DerivedLocation +":"+DerivedDocumentID + ":"+"?ShipToPartyMaster&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return current timestamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedConfigFEG is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "IsMultipleFEG"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedConfigDocID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "RequiredCleanDocumentID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedCrossGLC is a DerivedField
			type is Alpha size 50
			restricted
			if (DerivedConfigFEG = "Y")
				return DerivedFEG+DerivedDelimiter+DerivedCompany
			else
				return DerivedCompany
				
		DerivedAECrossAccountingEntity is a DerivedField
			type is Alpha size 50
			restricted
			if(LocalAEAlreadyExecuted != true)
				LocalAEAlreadyExecuted = true
				if (FSMBODAECrossReferenceDetailRel.DestinationValue entered)
					LocalAccountingEntity = FSMBODAECrossReferenceDetailRel.DestinationValue
				else
					LocalAccountingEntity = DerivedCrossGLC
				return LocalAccountingEntity
			else
				return LocalAccountingEntity
		
		DerivedAccountingEntity is a DerivedField
			type is Alpha size 20
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedAECrossAccountingEntity
			else
				return DerivedFEG+DerivedDelimiter+DerivedAE
		
		DerivedLocation is a DerivedField
			type is Alpha size 20
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedCustomer + DerivedDelimiter + CustomerShipTo
			else
				return DerivedCompany +DerivedDelimiter +DerivedCustomer+ DerivedDelimiter + CustomerShipTo
		
		DerivedCustomer is a DerivedField
			type is Alpha size 9
			restricted
			return CompanyCustomer.Customer using "%d"
			
		DerivedCustomerPartyID is a DerivedField
			type is Alpha size 25
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedCustomer
			else
				return	DerivedCompany + DerivedDelimiter + DerivedCustomer
			
		DerivedCompany is a DerivedField
			type is Alpha size 10
			restricted
			return Company using "%d"
						
		DerivedInternationalPrefix is a DerivedField
			type is Alpha 6
			restricted
			return CompanyCustomer.PhoneNumber.InternationalPrefix
			
		DerivedSubscriberNumber is a DerivedField
			type is Alpha 15
			restricted
			return CompanyCustomer.PhoneNumber.SubscriberNumber
			
		DerivedExtension is a DerivedField
			type is Alpha 5
			restricted
			return CompanyCustomer.PhoneNumber.Extension
			
		DerivedSearchName is a DerivedField
			type is Alpha size 120
			restricted
			return CompanyCustomer.Customer.SearchName
			
		DerivedName is a DerivedField
			type is Alpha size 80
			restricted
			return CompanyCustomer.Customer.Name
		
		DerivedContact is a DerivedField
			type is Alpha size 20
			restricted
			return CompanyCustomer.Customer.Contact
			
		DerivedContactTitle is a DerivedField
			type is Alpha size 30
			restricted
			return CompanyCustomer.Customer.ContactTitle
			
		DerivedAddressLine1 is a DerivedField
			type is Alpha 40
			restricted
			return CompanyCustomer.Customer.PostalAddress.DeliveryAddress.AddressLine1
			
		DerivedAddressLine2 is a DerivedField
			type is Alpha 40
			restricted
			return CompanyCustomer.Customer.PostalAddress.DeliveryAddress.AddressLine2
			
		DerivedAddressLine3 is a DerivedField
			type is Alpha 40
			restricted
			return CompanyCustomer.Customer.PostalAddress.DeliveryAddress.AddressLine3
			
		DerivedAddressLine4 is a DerivedField
			type is Alpha 40
			restricted
			return CompanyCustomer.Customer.PostalAddress.DeliveryAddress.AddressLine4
			
		DerivedMunicipality is a DerivedField
			type is Alpha 58
			restricted
			return CompanyCustomer.Customer.PostalAddress.Municipality
			
		DerivedStateProvince is a DerivedField
			type is Alpha 3
			restricted
			return CompanyCustomer.Customer.PostalAddress.StateProvince
		
		DerivedCountry is a DerivedField
			type is Alpha 3
			restricted
			return CompanyCustomer.Customer.PostalAddress.Country
		
		DerivedPostalCode is a DerivedField
			type is Alpha 12
			restricted
			return CompanyCustomer.Customer.PostalAddress.PostalCode
		
		DerivedCompCustInternationalPrefix is a DerivedField
			type is Alpha 6
			restricted
			return CompanyCustomer.Customer.PhoneNumber.InternationalPrefix
		
		DerivedCompCustSubscriberNumber is a DerivedField
			type is Alpha 15
			restricted
			return CompanyCustomer.Customer.PhoneNumber.SubscriberNumber
		
		DerivedCompCustExtension is a DerivedField
			type is Alpha 5
			restricted
			return CompanyCustomer.Customer.PhoneNumber.Extension
		
		DerivedTaxId is a DerivedField
			type is Alpha 25
			restricted
			return CompanyCustomer.Customer.TaxID
		
		DerivedDocumentID is a DerivedField
			type is Alpha size 60
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedCustomer + DerivedDelimiter + CustomerShipTo
			else
				return DerivedCompany +DerivedDelimiter +DerivedCustomer+ DerivedDelimiter + CustomerShipTo
			
		DerivedIntegrationApplication is a DerivedField
			type is Alpha size 15
			restricted
			LocalConfigurationParameter	= "IntegrationApplication"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedIntegrationApplicationLocally is a DerivedField
			type is Boolean
			restricted
			if (DerivedIntegrationApplication = "Local.ly")
				return true
			else
				return false
		
		DerivedTaxRegType is a DerivedField
			type is Alpha size 25
			restricted
			if(DerivedIntegrationApplicationLocally and ShipToAddress.Country = "IN")
				if(TaxRegistrationType = 1)
					return "Registered"
				else 
				if(TaxRegistrationType = 2)
					return "UnRegistered"
				else
				if(TaxRegistrationType = 3)
					return "SpecialEconomicZone"
				else 
				if(TaxRegistrationType = 4)
					return "CompositeDealer"
				else
					return ""
			else
				return ""
	
		ShipToPartyMasterBODXML is a DerivedField
			type is XMLDocument
			restricted
			if (DerivedConfigDocID = "Y")
				ShipToPartyMasterBODXML = template.IONSyncShipToPartyMaster_CustomerShipTo_CleanDoc_ST document for this instance
			else
				ShipToPartyMasterBODXML = template.IONSyncShipToPartyMaster_CustomerShipTo_ST document for this instance
			return ShipToPartyMasterBODXML


		ProcessDerivedBODID is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedAccountingEntity + ":" +DerivedLocation +":"+DerivedDocumentID + ":"+"?ShipToPartyMaster&verb=Process&TrackerID=" + LocalFSMInboundBODTracker
		
		ProcessShipToPartyMasterBODXML is a DerivedField
			type is XMLDocument
			restricted
			ProcessShipToPartyMasterBODXML = template.IONProcessShipToPartyMaster_CustomerShipTo_ST document for this instance
			return ProcessShipToPartyMasterBODXML

	
	Rule Blocks
		TriggerCustomerShipToRules
			LocalTitle = "EG:"+Company.FinanceEnterpriseGroup+" CG:"+Company.CustomerBusinessGroup+" CO:"+Company+" CU:"+CompanyCustomer.Customer+" CST:"+CustomerShipTo
			trigger PayablesService.CustomerShipToService PA service
				resume on error
				title is "<LocalTitle>"
				Criteria
					Company.CustomerBusinessGroup.FinanceEnterpriseGroup
					Company
				Variables
					ActionCode
					include persistent fields from CustomerShipTo
					include persistent fields from CompanyCustomer.Customer
					Company.CustomerBusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					Company.GeneralLedgerCompany.AccountingEntity
						variable name is AccountingEntity
		
		TriggerProcessCustomerShipToRules
			LocalTitle = "EG:"+Company.FinanceEnterpriseGroup+" CG:"+Company.CustomerBusinessGroup+" CO:"+Company+" CU:"+CompanyCustomer.Customer+" CST:"+CustomerShipTo
			trigger PayablesService.ProcessCustomerShipToService PA service
				resume on error
				title is "<LocalTitle>"
				Criteria
					Company.CustomerBusinessGroup.FinanceEnterpriseGroup
					Company
				Variables
					ActionCode
					include persistent fields from CustomerShipTo
					include persistent fields from CompanyCustomer.Customer
					Company.CustomerBusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					Company.GeneralLedgerCompany.AccountingEntity
						variable name is AccountingEntity
						
		CustomerShipToAutosequence
			initialize LocalMaxCustomerShipTo
			LocalMaxCustomerShipTo = last CustomerShipToRel.CustomerShipTo
			if (LocalMaxCustomerShipTo = 9999)
				initialize LocalCustomerShipTo
				for each CustomerShipToRel
					LocalShipTo = each.CustomerShipTo
					LocalCustomerShipTo = LocalCustomerShipTo + 1
					if (LocalShipTo!=LocalCustomerShipTo)
						LocalShipTo = LocalCustomerShipTo
						end for each
					constraint (!LocalCustomerShipTo = 9999)
						"CannotCreate:MaximumCustomerShipToNumberReached"
			else
				LocalShipTo = LocalMaxCustomerShipTo+1
				 
	Attach Rules

		constraint (Active)
			"CustomerShipToMustBeActive"
			
    Conditions

        IsCarrierDisallowed
        	restricted
            when (FreightCode entered
            and   FreightCode.BillingFreightCarrier.NotAllowed)
            
        IsCarrierRequired
        	restricted
            when (FreightCode entered
            and   FreightCode.BillingFreightCarrier.Required)
            
        IsSalesRepresentativeSame
        	restricted
            when (SalesRepresentative = SalesRepresentative2)

        IsShipToEDI
        	restricted
            when (ShipToEDI entered)


        QuantumGeoCodeLookupRequired
        	restricted
        	when (TaxEntityRel.ThirdParty.VertexOSeries		
        	and   TaxCode not entered
        	and  (TaxExemptCode not entered
        	or    TaxExemptCode.Taxable)
        	and   ShipToAddress.StateProvince not entered) 
        	
        ReturnQuantumGeoCode
        	restricted
        	when (TaxEntityRel.ThirdParty.VertexOSeries		
        	and   TaxExemptCode.Taxable)

		IsNotVertexStqGeocode
			restricted
			when (!TaxEntityRel.ThirdParty.VertexOSeries)	
		
		IsTaxableAndNotVertexStqGeocode
			restricted
			when (TaxExemptCode.Taxable
			and   IsNotVertexStqGeocode)
			
		DefaultShipToEqualToCustomerShipTo
			restricted
			when (CustomerShipTo = OrderEntryCustomerRel.DefaultCustomerShipTo)

		IsTaxable
			restricted
			when (TaxCode entered
			and   TaxExemptCode.Taxable)

		VertexResultsExist
			restricted	 
			when (VertexTaxAreaIdResultsRel exist
			and  !VertexTaxAreaIdResultsRel.WebServiceError)

		VertexErrorExist
			restricted	 
			when (VertexWebserviceErrorRel exist)

    	RecordExists	
    		restricted
    		when (CustomerShipTo exists)

    Relations
    
    	CustomerShipToRel
			one-to-many relation to CustomerShipTo
			Field Mapping uses ByCustomer
				related.Customer			= Customer
				related.Company				= Company

        BillingCompanyRel
            one-to-one relation to BillingCompany
            Field Mapping uses symbolic key
                related.Company 			= Company

        BillingFreightCodeRel
            one-to-one relation to BillingFreightCode
            Field Mapping uses symbolic key
                related.Company            	= Company
                related.BillingFreightCode 	= FreightCode






       	TaxEntityRel				 
            one-to-one relation to TaxEntity
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
                related.TaxEntity 				= Company.AccountingEntity
	           	
        OrderEntryCustomerRel
        	one-to-one relation to OrderEntryCustomer
            Field Mapping uses symbolic key
            	related.Company        		= Company
 	           	related.Customer	   		= Customer 
 	           	
 	    PurchasingCompanyRel
           	one-to-one relation to PurchasingCompany
           	Field Mapping uses symbolic key
	           	related.Company		  		= Company   

    	VertexTaxAreaIdResultsRel
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance)

  		VertexWebserviceErrorRel
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance
				and    related.WebServiceError							= true)
				









   		SelectedPostalAddressRecordsRel
			one-to-many relation to VertexTaxAreaIdPostalAddress
			Field Mapping uses ByTaxAreaId	
				related.VertexConfig										= Company.CustomerBusinessGroup.FinanceEnterpriseGroup 
				related.VertexTaxAreaIdPostalAddress.LookupKey  			= LocalLookupKey
				related.VertexTaxAreaIdPostalAddress.ResponseBusclassKey 	= LocalResponseBusclassKey

			Instance Selection	
				where (related.Selected 									= true)

  		UpdatePostalAddressRecordsRel
			one-to-many relation to VertexTaxAreaIdPostalAddress
			Field Mapping uses ByTaxAreaId	
				related.VertexConfig										= Company.CustomerBusinessGroup.FinanceEnterpriseGroup	
				related.VertexTaxAreaIdPostalAddress.LookupKey  			= LocalLookupKey


            Instance Selection

				where  (related.VertexTaxAreaIdPostalAddress.ResponseBusclassKey 	= LocalResponseBusclassKey)

		VertexLookupResultsRel
			one-to-many relation to VertexLookupResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance)
				

		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "ShipToPartyMaster"
            	related.FSMBODConfiguration.Direction 	= 1
		
		FSMProcessBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "ShipToPartyMaster"
            	related.FSMBODConfiguration.Direction 	= 1
		
		
		FSMBODAECrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
            	related.FSMBODCrossReference.SourceField 		= "CSFGLC"
            	related.FSMBODCrossReference.DestinationField 	= "DESTAE"
            Instance Selection
				where (related.SourceValue	= DerivedCrossGLC )
		
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker


    Sets

        Set2
            indexed
            Instance Selection
                where (IsShipToEDI)
            Sort Order
                Company
                Customer
                ShipToEDI

		ByCustomer
            indexed
            Sort Order
                Customer
                Company
				CustomerShipTo

    Field Rules

		VATRegistrationNumber
			if (TaxRegistrationType.Registered)
				required
					"MustEnterVATRegistrationNumberWhenTaxRegistrationTypeIsRegistered"



			
        Name
            required
            
       	Company
       		constraint (BillingCompanyRel exists)
       			"ShipToCompanyIsNotAValidBillingCompany"
       		constraint (TaxEntityRel exists)
       			"ShipToCompanyIsNotAValidTaxEntity"

        EuropeanCommunityCountry
        	EuropeanCommunityCountry = CountryCode.EcCountryNumber

		ISOCountry
			ISOCountry = CountryCode.EcCountryNumber
		
		ShipToAddress
			default to Customer.PostalAddress
			if (ReturnQuantumGeoCode)	
				initialize CalculateTax
				CalculateTax.RelatedObjectReference         = reference to this instance
				CalculateTax.System							= "AR"
				CalculateTax.CallingModule					= "CustomerShipTo"			
				CalculateTax.VertexQuantumOption			= "G"
				CalculateTax.TaxEntity						= Company.AccountingEntity	 
				CalculateTax.PostalAddress					= ShipToAddress
				LocalGeoCode								= CalculateTax.ReturnedGeoCode

				if (LocalGeoCode not entered)
					LocalGeoCode 							= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].GeoCode 
									
				constraint (LocalGeoCode entered)
					"VertexTaxCodeNotFound.CheckAddress."
					

				LocalState									= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].StateProvince 
				LocalZip									= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
				LocalCounty 								= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County
				MultipleTaxCodes							= CalculateTax.MultipleTaxAreaIds
				WebServiceError								= CalculateTax.WebServiceError 
				WebServiceErrorMessage						= CalculateTax.WebServiceErrorMessage
				
				if (ShipToAddress.County not entered)
					ShipToAddress.County = 	LocalCounty
				if (ShipToAddress.PostalCode not entered)
					ShipToAddress.PostalCode = LocalZip
				TaxCode	= LocalGeoCode														 
				if (TaxCode not exists
				and LocalGeoCode entered)							
					invoke Create TaxCode
						invoked.TaxCode						= LocalGeoCode
						invoked.TaxType						= "S"
						invoked.Description					= LocalStateZip		

		TaxExemptCode
			if (not CompanyCustomer.TaxExempt)
				default to TaxExemptCode.Taxable
			default to TaxExemptCode.Exempt
			if (TaxExemptCode changed
			and old TaxExemptCode.Taxable
			and TaxExemptCode.Exempt
			and VertexResultsExist)
    			invoke Delete VertexTaxAreaIdResultsRel				
			
				
		TaxCode
			if (TaxExemptCode.Exempt)
				cannot be entered
			else
			if (ReturnQuantumGeoCode)
				default to LocalGeoCode 
			else
			if (TaxEntityRel.ThirdParty not entered
			and TaxExemptCode.Taxable)
				required
					"TaxCodeShouldBeEntered"								
			if (IsTaxableAndNotVertexStqGeocode)
				initialize CalculateTax 
				CalculateTax.TaxEntity								= Company.AccountingEntity	
				CalculateTax.TaxCode								= 	TaxCode
				CalculateTax.Function								= 	"I"   
				constraint (CalculateTax.OutputErrorNumber not entered) 
					"<CalculateTax.OutputErrorMessage>" 
							
		SalesRepresentative2
			if (SalesRepresentative not entered)
				cannot be entered
					"FirstSalesRepresentativeMustBeEnteredToUseTheSecondSalesRepresentative"	
			else	
				constraint (!IsSalesRepresentativeSame)
					"BothSalesRepresentativesCannotBeSame"										
		
		CommissionSplitPercentage
			if (SalesRepresentative2 entered)	
				required
					"MustEnterCommisionSplitPercentageWhenSecondSalesRepresentativeIsUsed"      
			else
				cannot be entered
					"DoNotEnterCommisionSplitPercentageWhenSecondSalesRepresentativeIsNotUsed"      
		
		Carrier
			if (IsCarrierRequired)
				required
					"MustEnterCarrierForThisFreightCode<FreightCode>"	
			else
			if (IsCarrierDisallowed)
				cannot be entered
					"DoNotEnterCarrierForThisFreightCode<FreightCode>"	
			
		Vendor
			if (DefaultLocation entered)
				cannot be entered
					"ForDefaultShippingLocation<DefaultLocation>VendorShouldNotBeEntered"
					
		PurchaseFromLocation
			if (Vendor not entered)	
				cannot be entered	
					"VendorNotEnteredDoNotEnterPurchaseFromLocation"
										
		ProofOfDeliveryGracePeriod
			if (ProofOfDeliveryRequired)
				required									
			else
				cannot be entered
					"ProofOfDeliveryGracePeriodCannotBeEnteredIfProofOfDeliveryIsNotRequired"		

		MemoTerm
			default to TermCode	
			
		ShipTerm
			if (CountryCode.MemberOfEC)
				constraint (TaxEntityRel.IntrastatUsed)
					"IntraStatRequiredForThisTaxEntity<Company.AccountingEntity>"		
				constraint (RegionCode entered)
					"ShipTermIsEnteredRegionShouldBeEntered"
			else
				cannot be entered
					"DeliveryTermsInvalidForNonIntrastatTransactions"
				
		PortCode
			if (CountryCode.MemberOfEC)		
				constraint (TaxEntityRel.IntrastatUsed)
					"IntraStatRequiredForThisTaxEntity<Company.AccountingEntity>"
				constraint (RegionCode entered)
					"PortCodeIsEnteredRegionShouldBeEntered"		
			else
				cannot be entered	
					"TransportationPortInvalidForNonIntrastatTransactions"				

		Logo
			if (Logo changed
			or	CustomerShipTo not exists)
				MaxLogoSize = Customer.CustomerGroup.LogoImageMaximumSizeBytes
				
	Action Exit Rules
		if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode = ActionCode.Update
					if (action type.Create)
						ActionCode = ActionCode.Create
					else 
						if(action type.Delete)			
							ActionCode = ActionCode.Delete
					increment bod id.VariationID
					include TriggerCustomerShipToRules
					include TriggerProcessCustomerShipToRules
					



	Actions

		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location" 
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"	
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"


			Action Rules			
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord


				
		Create is a Create Action
			Action Rules
			Exit Rules
				if (DefaultShipTo)			
					invoke Update OrderEntryCustomerRel
						invoked.DefaultCustomerShipTo	=	CustomerShipTo

		T2VCreate is a Create Action				
			restricted 
			default label is untranslatable 
			bypass field rules 
			Entrance Rules 
				LocalBODTrigger = true 
				
		CreateCustomerShipTo is a Create Action
			restricted
			Action Rules
				if (CustomerShipTo not entered)
					include CustomerShipToAutosequence
					CustomerShipTo = LocalShipTo
			Exit Rules
				if (DefaultShipTo)			
					invoke Update OrderEntryCustomerRel
						invoked.DefaultCustomerShipTo	=	CustomerShipTo
								
		Update is an Update Action
			Action Rules
				if (DefaultShipTo)
					constraint (Active)
        				"CannotHaveInactiveShipToIfDefaultShipTo=<DefaultShipTo>"

				if (DefaultShipTo changed)
					if (DefaultShipToEqualToCustomerShipTo)	
						invoke Update OrderEntryCustomerRel
							initialize invoked.DefaultCustomerShipTo
					else				
						invoke Update OrderEntryCustomerRel
							invoked.DefaultCustomerShipTo	=	CustomerShipTo

					
		Delete is a Delete Action	
			Entrance Rules
    			invoke Delete VertexTaxAreaIdResultsRel
    			if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger)
					if (action type.Delete)
						ActionCode = ActionCode.Delete
					increment bod id.VariationID
					include TriggerCustomerShipToRules
					include TriggerProcessCustomerShipToRules


							
		SendCustomerShipToBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ShipToPartyMasterBODXML
					bod type is "Sync.ShipToPartyMaster"
					accounting entity is DerivedAccountingEntity
					document id is DerivedDocumentID
					variation id is	DerivedBODVariationID
					
					
		TriggerCustomerShipToNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				if (DerivedConfigDocID = "Y")
					LocalMainUserTemplate	= "IONSyncShipToPartyMaster_CustomerShipTo_CleanDoc_ST"
				else
					LocalMainUserTemplate	= "IONSyncShipToPartyMaster_CustomerShipTo_ST"
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "ShipToPartyMaster"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "CustomerShipTo"
					invoked.PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup 
					invoked.PrmCompany					= Company
					invoked.PrmMainUserTemplate			= LocalMainUserTemplate
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "ShipToPartyMaster"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFEG
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= Company
							invoked.Reference2				= CustomerShipTo
							invoked.Customer				= Customer
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFEG
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= Company
							invoked.Reference2				= CustomerShipTo
							invoked.Customer				= Customer
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendCustomerShipToBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODID
							invoked.BODXML								= ShipToPartyMasterBODXML
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODID
							invoked.BODXML									= ShipToPartyMasterBODXML

		SendProcessCustomerShipToBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ProcessShipToPartyMasterBODXML
					bod type is "Process.ShipToPartyMaster"
					accounting entity is DerivedAccountingEntity
					document id is DerivedDocumentID
					variation id is	DerivedBODVariationID
					
		TriggerProcessCustomerShipToNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMProcessBODConfigurationRel
					invoked.PrmVerb 					= 2
					invoked.PrmNoun						= "ShipToPartyMaster"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "CustomerShipTo"
					invoked.PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup 
					invoked.PrmCompany					= Company
					invoked.PrmMainUserTemplate			= "IONProcessShipToPartyMaster_CustomerShipTo_ST"
				LocalNativeLPLBODTrigger = FSMProcessBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 2
							invoked.Noun 					= "ShipToPartyMaster"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFEG
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Direction				= 1
							invoked.Reference1				= Company
							invoked.Reference2				= CustomerShipTo
							invoked.Customer				= Customer			
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFEG
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Direction				= 1
							invoked.Reference1				= Company
							invoked.Reference2				= CustomerShipTo
							invoked.Customer				= Customer
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendProcessCustomerShipToBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= ProcessDerivedBODID
							invoked.BODXML								= ProcessShipToPartyMasterBODXML
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= ProcessDerivedBODID
							invoked.BODXML									= ProcessShipToPartyMasterBODXML



		BatchUpdateTaxUsingVertex is a Set Action		 



			completion message is "<VertexCompletionMessage>" 

			Parameters
				PrmEnterpriseGroup	is a VertexConfig				 
					default label is "FinanceEnterpriseGroup"			
				PrmProcessGroup				is Alpha 50	
					default label is "ProcessGroup"
				PrmCustomerGroup			is a CustomerGroup				 
					default label is "CustomerGroup"					
				PrmARCompany				is a ReceivableCompany
					default label is "ReceivableCompany"
				PrmCompanyCustomer			is like CompanyCustomer
				PrmFromCustomer				is like Customer
					default label is "FromCustomer"
				PrmToCustomer 				is like Customer
					default label is "ToCustomer"
				BeginCustomerShipTo			is like CustomerShipTo
					default label is "BeginCustomerShipTo"
				EndCustomerShipTo			is like CustomerShipTo
					default label is "EndCustomerShipTo"
				PrmReadOnly					is Boolean
					default label is "ReadOnly"
				EntityTaxCodeDefault 		is like TaxCode	
				PrmTaxEntity 		 		is like TaxEntity	
									
			Parameter Rules
				PrmEnterpriseGroup
					initial value is PrmARCompany.CustomerBusinessGroup.FinanceEnterpriseGroup
					required
				PrmARCompany 	 
					required
				PrmFromCustomer
					if (PrmToCustomer not entered)
						required
				PrmToCustomer
					if (PrmFromCustomer not entered)
						required
				EndCustomerShipTo
					default to 9999		
			Local Fields
				VertexCompletionMsg		is Alpha 150
				RecordCount				is Numeric 10
			
			Instance Selection
				where (PrmEnterpriseGroup 		= PrmARCompany.CustomerBusinessGroup.FinanceEnterpriseGroup
				and  ((PrmCustomerGroup entered							
				and	   PrmCustomerGroup = Customer.CustomerGroup)		
				or	   PrmCustomerGroup not entered)					
				and    CompanyCustomer.Company	= PrmARCompany
				and    CompanyCustomer.Customer >= PrmFromCustomer
				and    CompanyCustomer.Customer <= PrmToCustomer
				and    CustomerShipTo			>= BeginCustomerShipTo
				and    CustomerShipTo			<= EndCustomerShipTo
				and    Active)		
							
			Sort Order
				CompanyCustomer

			Accumulators

				
				        								
			Action Rules								
				Empty Set Rules
					VertexCompletionMsg = "<NoRecordsMessage>"
				
				Set Rules
					Entrance Rules				 
						initialize RecordCount
					Exit Rules
						
				CompanyCustomer Set Rules
					Entrance Rules
			        
					Exit Rules				
						
				Instance Rules
					increment RecordCount


					if (TaxExemptCode.Taxable
					and Active)	
						initialize CalculateTax
						CalculateTax.RelatedObjectReference         = reference to this instance
						CalculateTax.System							= "AR"
						CalculateTax.CallingModule					= "CustomerShipTo"			
						CalculateTax.VertexQuantumOption			= "G"
						CalculateTax.InFinanceEnterpriseGroup		= PrmEnterpriseGroup		 

						CalculateTax.PostalAddress					= ShipToAddress
						CalculateTax.TaxEntity						= PrmTaxEntity			
						CalculateTax.EntityTaxCodeDefault			= EntityTaxCodeDefault	
						LocalGeoCode								= CalculateTax.ReturnedGeoCode	
						LocalState									= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].StateProvince 
						LocalZip									= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
						LocalCounty 								= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County
						MultipleTaxCodes							= CalculateTax.MultipleTaxAreaIds
						WebServiceError								= CalculateTax.WebServiceError 
						WebServiceErrorMessage						= CalculateTax.WebServiceErrorMessage
						CalculateTaxOutputMessage					= CalculateTax.OutputErrorMessage
						NewTaxCode 				= LocalGeoCode
						OldTaxCode				= TaxCode
						if (!MultipleTaxCodes
						and !WebServiceError
						and CalculateTax.OutputErrorNumber not entered
						and LocalGeoCode entered
						and LocalGeoCode not = TaxCode)
							if (!PrmReadOnly)		
								if (ShipToAddress.County not entered)
									ShipToAddress.County = 	LocalCounty
								if (ShipToAddress.PostalCode not entered)
									ShipToAddress.PostalCode = LocalZip
								if (!WebServiceError)	
									TaxCode	= LocalGeoCode														 
								if (TaxCode not exists)							
									invoke Create TaxCode
										invoked.TaxCode						= LocalGeoCode
										invoked.TaxType						= "S"
										invoked.Description					= LocalStateZip	




						LocalLookupKey 	= CalculateTax.ResultsLookupKey
						LocalSequence	= CalculateTax.ResultsLookupKeySequence	
						if (VertexLookupResultsRel exist)
							invoke Delete VertexLookupResultsRel
							
						invoke Create VertexLookupResults
							invoked.VertexConfig		   			= Company.FinanceEnterpriseGroup
							invoked.LookupProcessGroup				= PrmProcessGroup
							invoked.VertexLookupResults.LookupKey 	= LocalLookupKey
							invoked.VertexLookupResults.Sequence	= LocalSequence 
							invoked.RelatedObjectReference			= reference to this instance
							invoked.LookupBusinessGroup				= Company.BusinessGroup
							invoked.LookupCustomerGroup				= Customer.CustomerGroup	
							invoked.DisplayOption					= "C"
							invoked.LookupARCompany					= Company
							invoked.LookupCustomer					= Customer
							invoked.LogSearchName					= Name
							invoked.LookupCompanyCustomer 			= CompanyCustomer
							invoked.LookupCustomerShipToLoc			= CustomerShipTo
							invoked.ReturnedHighesTaxAreaId			= LocalGeoCode
							invoked.NumberOfTaxAreaIdsReturned		= CalculateTax.NumberOfTaxCodes	
							invoked.LookupPostalAddress 			= ShipToAddress
							invoked.LookupDescription				= TaxCodeMessage	
							invoked.LookupTaxEntity					= PrmTaxEntity			
							invoked.ErrorNumber						= CalculateTax.OutputErrorNumber
							invoked.ErrorMessage					= DerivedLookupMessage	
							invoked.WebServiceError 				= CalculateTax.WebServiceError 
							invoked.WebServiceErrorMessage 			= CalculateTax.WebServiceErrorMessage 									 
							if (MultipleTaxCodes
							or  WebServiceError
							or  CalculateTax.OutputErrorNumber entered)								
								invoked.Status = 0
							else
							if (LocalGeoCode not = OldTaxCode)
								invoked.Status = 1
							else
								invoked.Status = 2																												


		UpdateVertexTaxCode is an Instance Action		


			completion message is "<VertexCompletionMessage>" 
			run in background 
			
			Parameters

			Parameter Rules

			Local Fields
							
			Action Rules

				if (TaxExemptCode.Taxable)	




					initialize CalculateTax
					CalculateTax.RelatedObjectReference         = reference to this instance
					CalculateTax.System							= "AR"
					CalculateTax.CallingModule					= "CustomerShipTo"			
					CalculateTax.VertexQuantumOption			= "G"
					CalculateTax.InFinanceEnterpriseGroup		= Company.FinanceEnterpriseGroup

					CalculateTax.PostalAddress					= ShipToAddress
					LocalGeoCode								= CalculateTax.ReturnedGeoCode	
					LocalState									= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].StateProvince 
					LocalZip									= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
					LocalCounty 								= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County
					MultipleTaxCodes							= CalculateTax.MultipleTaxAreaIds
					WebServiceError								= CalculateTax.WebServiceError 
					WebServiceErrorMessage						= CalculateTax.WebServiceErrorMessage
					
					if (!MultipleTaxCodes
					and !WebServiceError
					and CalculateTax.OutputErrorNumber not entered)	 
						if (ShipToAddress.County not entered)
							ShipToAddress.County = 	LocalCounty
						if (ShipToAddress.PostalCode not entered)
							ShipToAddress.PostalCode = LocalZip
					
					if (LocalGeoCode entered)	
						TaxCode	= LocalGeoCode														 
						if (TaxCode not exists)							
							invoke Create TaxCode
								invoked.TaxCode						= LocalGeoCode
								invoked.TaxType						= "S"
								invoked.Description					= LocalStateZip	

								


		UpdateTaxCodeAndAddress is an Instance Action

			restricted
			Parameters
				PrmTaxCode		is like TaxCode
				PrmCountry		is a Country
				PrmState		is a StateProvince
				PrmZip			is a PostalCode
				PrmCounty		is a County
			Parameter Rules
			Action Rules
				TaxCode 						= PrmTaxCode
				if (PrmZip entered)
					ShipToAddress.County 		= PrmCounty
					ShipToAddress.PostalCode	= PrmZip																							


														



		DeleteVertexResultsRecords is an Instance Action



			Parameters
				DeleteFilesAfterUpdate				is Boolean				 
					default label is "DeleteResultsFilesAfterUpdate"
			Action Rules
				invoke Delete VertexTaxAreaIdResultsRel


									
		BODDataInitialLoadCustomerShipToCriteria is a Set Action
			restricted
			Parameters
				PrmFromCompany					is a ReceivableCompany
				PrmToCompany					is a ReceivableCompany
				PrmCustomerGroup  				is a CustomerGroup	
				PrmFromCustomer					is a Customer
					context of PrmCustomerGroup					
				PrmToCustomer					is a Customer
					context of PrmCustomerGroup					
				PrmCompanyCustomer				is a CompanyCustomer
					context of PrmFromCompany
					context of PrmFromCustomer 
				PrmFromCustomerShipTo  			is a CustomerShipTo
					context of PrmFromCompany
					context of PrmFromCustomer 
				PrmToCustomerShipTo   			is a CustomerShipTo
					context of PrmFromCompany
					context of PrmFromCustomer 
				
			Parameter Rules

			Instance Selection			
				where  (((PrmCustomerGroup entered
				and		  PrmCustomerGroup = Customer.CustomerGroup)
				or 		  PrmCustomerGroup not entered )
				and    ((PrmFromCompany entered
				and      PrmFromCompany <= CompanyCustomer.Company)
				or       PrmFromCompany not entered)
				and	   ((PrmToCompany entered
				and      PrmToCompany >= CompanyCustomer.Company)
				or       PrmToCompany not entered)
				and	   ((PrmFromCustomer entered
				and      PrmFromCustomer <= Customer)
				or       PrmFromCustomer not entered)
				and	   ((PrmToCustomer entered
				and      PrmToCustomer >= Customer)
				or       PrmToCustomer not entered)
				and	   ((PrmFromCustomerShipTo entered
				and      PrmFromCustomerShipTo <= CustomerShipTo)
				or       PrmFromCustomerShipTo not entered)
				and	   ((PrmToCustomerShipTo entered
				and      PrmToCustomerShipTo >= CustomerShipTo)
				or       PrmToCustomerShipTo not entered)
				and     (Active = true))
				
			Action Rules								
				Instance Rules	
					if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger)
						ActionCode = ActionCode.Create
						increment bod id.VariationID
						include TriggerCustomerShipToRules
						include TriggerProcessCustomerShipToRules
						


		
		Anonymize is an Instance Action	
			restricted
			Action Rules
				Name				= Customer
				Contact				= Customer
			
				initialize ShipToAddress
				initialize EmailAddress
				initialize ShipToEDI
				initialize PhoneNumber
				initialize FaxNumber

			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
					invoked.Status						= 1
					invoked.AffectedBusinessClass		= "AR7"
					invoked.ReceivableCompany			= Company
					invoked.Customer					= Customer
					invoked.CustomerShipTo				= CustomerShipTo
			
	    PurgeAuditLog is an Instance Action	
	    	restricted
	        Action Rules
				invoke purge audit log entries
				 
          	Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
					invoked.Status						= 2
					invoked.AffectedBusinessClass		= "AR7"
					invoked.ReceivableCompany			= Company
					invoked.Customer					= Customer
					invoked.CustomerShipTo				= CustomerShipTo
					
		Purge is a Purge Action
			restricted
