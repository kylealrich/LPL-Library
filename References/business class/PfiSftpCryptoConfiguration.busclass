PfiSftpCryptoConfiguration is a BusinessClass
    owned by pfi
    prefix is PSFCC
	stored in environment
	disable data area copy
		preserve target data
    default label is "SFTPCryptoConfiguration"

    Ontology
        symbolic key is PfiSftpCryptoConfiguration

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements DynamicCreation

    Persistent Fields
        KeyExchange is Alpha size up to 2000
        	default label is "KeyExchange_(Kex)"
		Mac is Alpha size up to 2000
			default label is "MessageAuthenticationCode_(MAC)"
		Cipher is Alpha size up to 2000
			default label is "Cipher_(Encryption)"
		Signature is Alpha size up to 2000
			default label is "HostKeySignature"
        NotificationUsers is Alpha size up to 500
        	default label is "SendToUsers_(CommaSeparated)"
        ReminderCount is Numeric size 2
        NotificationFromEmail is Alpha size up to 100
        	default label is "FromEmail"
        ExpiryDate is TimeStamp
        	
    Derived Fields
    	ApprovedByInfor is a DerivedField
    		type is Boolean
    		if(ExpiresOn not empty and ExpiresOn > current date)
    			return true
    		return false
    			
    	ExpiresOn is a NativeField
    		type is Date

		DefaultKeyExchange is a NativeField
			type is Alpha size up to 2000
			
		DefaultMac is a NativeField
			type is Alpha size up to 2000

		DefaultCipher is a NativeField
			type is Alpha size up to 2000

		DefaultSignature is a NativeField
			type is Alpha size up to 2000
											
		AvailableKeyExchange is a NativeField
			type is Alpha size up to 2000
		
		AvailableMac is a NativeField
			type is Alpha size up to 2000
			default label is "Available MAC"
			
		AvailableCipher is a NativeField
			type is Alpha size up to 2000
		
		AvailableSignature is a NativeField
			type is Alpha size up to 2000
			default label is "AvailableHostKeySignature"

							
		Validate is a NativeField
			type is Boolean
			
		UpdateCache is a NativeField
			type is Boolean
						
    Field Rules
    	KeyExchange
    		default to DefaultKeyExchange
	    	initial value is DefaultKeyExchange	
    		
		Mac
			default to DefaultMac
			initial value is DefaultMac
			
		Cipher
			default to	DefaultCipher
			initial value is DefaultCipher
		
		Signature
			default to DefaultSignature
			initial value is DefaultSignature
	
		NotificationUsers
			default to actor
			initial value is actor
			
		NotificationFromEmail
			default to "sftpcryptoconfig_noreply@infor.com"
			initial value is "sftpcryptoconfig_noreply@infor.com"
			
	Actions
		Create is a Create Action
			Action Rules
				constraint (ApprovedByInfor)
					"ApprovalRequiredFromInfor.PleaseContactInforSupportForMoreInformation."

				constraint (Validate)
					"ValidationFailed"
			Exit Rules
				constraint (UpdateCache) 
                    "CacheNotUpdated"
                    			
		Update is an Update Action
			Action Rules
				constraint (ApprovedByInfor)
					"ApprovalRequiredFromInfor.PleaseContactInforSupportForMoreInformation."
				constraint (Validate)
					"ValidationFailed"
					
			Exit Rules
				constraint (UpdateCache) 
                    "CacheNotUpdated"
                    
        UpdateReminderCount is an Update Action
                    		
		Delete is a Delete Action
			Exit Rules
				constraint (UpdateCache) 
                    "CacheNotUpdated"
		
		ResetToDefault is an Instance Action
			confirmation required
				"DoYouWantToResetCryptoConfiguration?"
			Action Rules
				KeyExchange = DefaultKeyExchange
				Mac = DefaultMac
				Cipher = DefaultCipher
				Signature = DefaultSignature
				
			Exit Rules
				constraint (UpdateCache) 
                    "CacheNotUpdated"  
