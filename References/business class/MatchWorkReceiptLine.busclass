MatchWorkReceiptLine is a BusinessClass
    owned by ma
    prefix is MWPRL

    Ontology
        symbolic key is MatchWorkReceiptLine

    Patterns

        disable AuditIndex
        disable Auditing
        disable EffectiveDated
      	disable DataTranslations

    Persistent Fields
		Vendor
        Item
		ItemType
        MatchDetailKey
        MatchAmount					is an InternationalCost
        ExtendedAmount				is an InternationalCost
        MatchedQuantity				is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
        UnmatchedQuantity			is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
        MatchUnitCost				is like UnitCost
            precision is DerivedNumberOfDecimalsCost
        OpenToMatchQuantity			is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
        ReceivedUOM					is like UnitOfMeasure

		Selected					is Boolean
		Updated						is Boolean
        PayablesInvoice 			is like PayablesInvoice
		MatchSub					is Numeric 3
		MatchObjectID				is an ObjId
		MatchSequence

		LastAdjustedMatchUnitCost	is like UnitCost
		EnteredReceivedQuantity		is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
		BuyUOMQuantity				is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
		VendorPriceMatchedQuantity	is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
		Status						is Numeric 1
		VendorPriceUOMQuantity		is like Quantity
            precision is DerivedNumberOfDecimalsQuantity
		VendorPriceUOM				is like UnitOfMeasure
		LastRecord					is Boolean
		
		TotalMatchAmount			is an InternationalAmount
		Po30UnitCost				is like UnitCost
		
		ArchivedQuantity			is like Quantity
		ArchiveWriteOffQuantity		is like Quantity
		VendorPriceArchiveQuantity	is like Quantity			

        ShipToLocation              is like InventoryLocation
		IntrastatNumber

		SortNumber					is Numeric 1	
		SortSelect					is Numeric 1	
		MatchReconQueueSet			is like MatchReconQueueSet
		
	Context Fields




		MatchInvoiceMessage
		MatchPurchaseOrderInvoice
		MatchReconQueue
		ContextMatchWorkInvoiceDetail is a MatchWorkInvoiceDetail

	Local Fields
	    LocalExtendedAmount			is like InternationalCost
	    
	Derived Fields

		DerivedOpenToMatchQuantity is a DerivedField  
			type is like Quantity





			return OpenToMatchQuantity
					
		DerivedUnmatchedQuantity is a DerivedField  
			type is like Quantity





			return UnmatchedQuantity
					
		DerivedMatchUnitCost is a DerivedField
			type is like InternationalCost





			return MatchUnitCost

		DerivedMatchAmount is a DerivedField
			type is like InternationalCost





			return MatchAmount

		DerivedReceiptLineQuantity is a DerivedField  
			type is like Quantity
	            precision is DerivedNumberOfDecimalsQuantity
			if (PurchaseOrderLine.IsCatchWeightItem)
			    return VendorPriceUOMQuantity
			else
			if (OpenToMatchQuantity entered)
				return OpenToMatchQuantity
			else
				return EnteredReceivedQuantity 
								    
		DerivedUnitOfMeasure is a ConditionalField
			type is AlphaUpper size 4
			restricted
			if (PurchaseOrderReceiptLine not entered)
				PurchaseOrderLine.VendorPriceUOM
			else
			if (!PurchaseOrderReceiptLine.VendorPriceUOM = PurchaseOrderReceiptLine.VendorBuyUOM)
				PurchaseOrderReceiptLine.VendorPriceUOM
			else
				PurchaseOrderReceiptLine.ReceivedUOM 
				
		DerivedReceiptUnitCost is a DerivedField
			type is like InternationalCost
	            precision is DerivedNumberOfDecimalsCost
			if (PurchaseOrderLine.IsCatchWeightItem)
				return PurchaseOrderLine.EnteredUnitCost
			else
				if (PurchaseOrderReceiptLine not entered)
					for each PurchaseOrderReceiptLine.MatchInvoiceMessage.POCostMessagesForMatchReconciliationInvoiceMessageRel
						if (each.MatchMessageOrigin.PoCost)
							if (each.ApplicationAction.AdjustCost)
								return each.AdjustedUnitCost
								end for each
				return MatchUnitCost

		DerivedSelectedInvoiceLineQuantity is a DerivedField
		    type is like Quantity
	            precision is DerivedNumberOfDecimalsQuantity
		    return sum PayablesInvoiceDetailRel.MatchedQuantity
		    							
		DerivedReceiptExtendedCost is a DerivedField
			type is like InternationalCost

			if (!PurchaseOrderLine.IsCatchWeightItem
			and  DerivedSelectedInvoiceLineQuantity entered
			and  DerivedReceiptLineQuantity > DerivedSelectedInvoiceLineQuantity)
				LocalExtendedAmount = (DerivedSelectedInvoiceLineQuantity * DerivedReceiptUnitCost)
			else			 
				LocalExtendedAmount = (DerivedReceiptLineQuantity * DerivedReceiptUnitCost)
			
			round LocalExtendedAmount to nearest DerivedRoundTo
							
			return LocalExtendedAmount

		DerivedSelectedDisplayQuantity is a DerivedField
		    type is like Quantity
	            precision is DerivedNumberOfDecimalsQuantity
			if (!PurchaseOrderLine.IsCatchWeightItem
			and  DerivedSelectedInvoiceLineQuantity entered
			and  DerivedReceiptLineQuantity > DerivedSelectedInvoiceLineQuantity)
				return DerivedSelectedInvoiceLineQuantity
			else			 
				return DerivedReceiptLineQuantity
		    							

		DerivedItemType is a DerivedField
			type is Alpha 1
			if (ItemType.Inventoried)
				return "I"
			else
			if (ItemType.NonStock)
				return "N"
			else
			if (ItemType.Special)
				return "X"
			else
			if (ItemType.Service)
				return "S"

		ServiceMsg is a MessageField
			restricted
			"Service"
			
		DerivedReceipt is a DerivedField
			type is Alpha 15
			if (ItemType.Service)
				return ServiceMsg
			else
			if (PurchaseOrderReceiptLine entered)
				return PurchaseOrderReceipt
		
		DerivedReceiptLine is a DerivedField
			type is Alpha 9
			if (PurchaseOrderReceiptLine not entered)
				return PurchaseOrderLine
			else
				return PurchaseOrderReceiptLine

		DerivedNumberOfDecimalsQuantity is a ConditionalField
			type is Numeric size 1
			restricted
			if (ItemType.NonStock
			or  ItemType.Inventoried)
				Item.NumberOfDecimalsQuantity
			else
			if (ItemType.Special
			or  ItemType.Service)
				InventoryCompanyRel.NumberOfDecimalsQuantity
			else
				4
				
		DerivedNumberOfDecimalsCost is a ConditionalField
			type is Numeric size 1
			restricted
			if (ItemType.NonStock
			or  ItemType.Inventoried)
				Item.NumberOfDecimalsCost
			else
			if (ItemType.Special
			or  ItemType.Service)
				InventoryCompanyRel.NumberOfDecimalsCost
			else
				8
				
		OutputNumberOfDecimals is a DerivedField	
			type is Numeric 1
			if (MatchWork.PayablesInvoice.InvoiceCurrency entered)
				return MatchWork.PayablesInvoice.InvoiceCurrency.NumberOfDecimals
			else
				return 2


		DerivedRoundTo is a DerivedField		
    		type is Decimal 5.4
    		restricted
    		if (OutputNumberOfDecimals = 2)
    			return .01
    		else
    		if (OutputNumberOfDecimals = 0)
    			return 1
    		else
    		if (OutputNumberOfDecimals = 3)
    			return .001
    		else
    		if (OutputNumberOfDecimals = 4)
    			return .0001

		DerivedRoundToCost is a DerivedField
			type is like RoundTo
			restricted
			if (DerivedNumberOfDecimalsCost = 8)
				return .00000001
			else
			if (DerivedNumberOfDecimalsCost = 7)
				return .0000001
			else
			if (DerivedNumberOfDecimalsCost = 6)
				return .000001
			else
			if (DerivedNumberOfDecimalsCost = 5)
				return 0.00001
			else
			if (DerivedNumberOfDecimalsCost = 4)
				return 0.0001
			else
			if (DerivedNumberOfDecimalsCost = 3)
				return 0.001
			else
			if (DerivedNumberOfDecimalsCost = 2)
				return 0.01
			else
			if (DerivedNumberOfDecimalsCost = 1)
				return 0.1
			else
			if (DerivedNumberOfDecimalsCost = 0)
				return 1

				
	Conditions
		DetailMismatch
			restricted
			when (Selected
            and   PayablesInvoice not entered
            and   !ItemType.Service)

		HasSelectedDetail
			when (SelectedInvoiceDetailRel exists)
				
	Relations
		MatchWorkReceiptRel
			one-to-one relation to MatchWorkReceipt
			Field Mapping uses symbolic key
                related.Company                 	= Company
				related.MatchWork					= MatchWork
				related.PurchaseOrderReceipt		= PurchaseOrderReceipt
					
		AnotherSelectedMatchWorkReceiptLineRel
			one-to-many relation to MatchWorkReceiptLine
			Field Mapping uses ByVendorMatchDetailKey
				related.MatchWork					= MatchWork
				related.Company						= Company
				related.Vendor						= Vendor
				related.ItemType					= ItemType
				related.MatchDetailKey				= MatchDetailKey
				related.PurchaseOrder				= PurchaseOrder
				related.PurchaseOrderLine			= PurchaseOrderLine
			Instance Selection
				where (related.UniqueID				!= UniqueID
				and    related.Selected
				and    related.MatchSub				= MatchSub
				and    related.MatchObjectID not entered)

		WorkReceiptLineAOCsRel
			one-to-many relation to MatchWorkReceiptLineAOC
			Field Mapping uses symbolic key
                related.Company                 	= Company
				related.MatchWork					= MatchWork
				related.PurchaseOrder				= PurchaseOrder
				related.PurchaseOrderLine			= PurchaseOrderLine
				related.PurchaseOrderReceipt		= PurchaseOrderReceipt
				related.PurchaseOrderReceiptLine	= PurchaseOrderReceiptLine

		SelectedInvoiceDetailRel
			one-to-many relation to MatchWorkInvoiceDetail
			Field Mapping uses ByVendorMatchDetailKey
				related.MatchWork					= MatchWork
				related.Company						= Company
				related.Vendor						= Vendor
				related.ItemType					= ItemType
				related.MatchDetailKey				= MatchDetailKey
				related.PurchaseOrder				= PurchaseOrder
				related.PurchaseOrderLine			= PurchaseOrderLine
			Instance Selection
				where (related.Selected)

		WorkInvoiceDetailRel
			one-to-many relation to MatchWorkInvoiceDetail
			Field Mapping uses ByVendorMatchDetailKey
				related.MatchWork					= MatchWork
				related.Company						= Company
				related.Vendor						= Vendor
				related.ItemType					= ItemType
				related.MatchDetailKey				= MatchDetailKey
				related.PurchaseOrder				= PurchaseOrder
				related.PurchaseOrderLine			= PurchaseOrderLine

		InventoryCompanyRel
			one-to-one relation to InventoryCompany
			Field Mapping uses symbolic key
				related.Company = Company

		MatchReconQueueSetRel
			one-to-many relation to MatchReconQueueSet
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PayablesInvoice		= PayablesInvoice
				related.MatchReconQueueSet	= MatchReconQueueSet

		PayablesInvoiceDetailRel 
			one-to-many relation to PayablesInvoiceDetail
			Field Mapping uses symbolic key 
				related.Company						= Company
				related.PayablesInvoice				= PayablesInvoice
				related.PurchaseOrder				= PurchaseOrder
				related.PurchaseOrderLine			= PurchaseOrderLine

	Sets
		ByVendorMatchDetailKey  
            indexed
            Sort Order
            	MatchWork
				Company
				Vendor
				ItemType
				MatchDetailKey
				PurchaseOrder
				PurchaseOrderLine
				PurchaseOrderReceipt
				PurchaseOrderReceiptLine

		BySortNumberMatchDetailKey  
            indexed
            Sort Order
            	MatchWork
				Company
				Vendor
                SortNumber descending
				ItemType
				MatchDetailKey
				PurchaseOrder
				PurchaseOrderLine
				PurchaseOrderReceipt
				PurchaseOrderReceiptLine

		ByPurchaseOrderReceipt  
            indexed
            Sort Order
            	MatchWork
				Company
				PurchaseOrder
				PurchaseOrderReceipt
				PurchaseOrderLine
				PurchaseOrderReceiptLine

        ByQuantity
            indexed
            Sort Order
            	MatchWork
                Company
                Vendor
                SortNumber descending
                EnteredReceivedQuantity
                PurchaseOrderReceipt
                PurchaseOrderReceiptLine
                PurchaseOrder
                PurchaseOrderLine

        ByCost
            indexed
            Sort Order
            	MatchWork
                Company
                Vendor
                SortNumber descending
                MatchUnitCost
                PurchaseOrderReceipt
                PurchaseOrderReceiptLine
                PurchaseOrder
                PurchaseOrderLine

        BySelected 
            indexed
            Sort Order
            	MatchWork
                Company
                Vendor
				SortSelect descending
                SortNumber descending
				PurchaseOrder
				PurchaseOrderLine
				PurchaseOrderReceipt
				PurchaseOrderReceiptLine

	Field Rules
		ReceivedUOM
			if (PurchaseOrderReceiptLine not entered)
				default to PurchaseOrderLine.VendorPriceUOM

		VendorPriceUOMQuantity
			if (PurchaseOrderReceiptLine entered)
				default to PurchaseOrderReceiptLine.VendorPriceUOMQuantity
			else
				default to PurchaseOrderLine.VendorPriceUOMQuantity

		VendorPriceUOM
			if (PurchaseOrderReceiptLine entered)
				default to PurchaseOrderReceiptLine.VendorPriceUOM
			else
				default to PurchaseOrderLine.VendorPriceUOM



	Actions
		Create is a Create Action
			restricted




			Exit Rules








		Update is an Update Action
			restricted
			Exit Rules




		FastUpdate is an Update Action
			restricted
			bypass field rules
			Entrance Rules
				if (Selected)
					if (PayablesInvoice not entered)
						PayablesInvoice = MatchWork.PayablesInvoice
	
					if (MatchReconQueueSet not entered)
						if  (MatchWork.MatchReconQueueRel.Previous1 entered)
							MatchReconQueueSet = MatchWork.MatchReconQueueRel.Previous1
							invoke FastUpdate MatchWork.MatchReconQueueRel
								initialize invoked.PendingType							
							invoke Update MatchWork.MatchReconQueueRel.MatchReconQueuePrevious1Rel
				    			initialize invoked.Type
						else
							MatchReconQueueSet = 1
							invoke FastUpdate MatchWork.MatchReconQueueRel
								invoked.Previous1 = 1
	
							if  (MatchReconQueueSetRel not exists)
								invoke Create MatchReconQueueSet
									invoked.Company				= Company
									invoked.PayablesInvoice		= PayablesInvoice
									invoked.MatchReconQueueSet	= MatchReconQueueSet
			
			Action Rules


			Exit Rules
				if (Selected
				and !MatchWorkReceiptRel.Selected)
					invoke FastUpdate MatchWorkReceiptRel
						invoked.Selected = true





		Delete is a Delete Action
			restricted
			bypass relational integrity rules

		Purge is a Purge Action
			restricted
			bypass relational integrity rules

		SelectRecordForRecon is an Instance Action
			valid when (!Selected)
			default label is "Select"
			Action Rules
				Selected		= true
				SortSelect		= 1
				PayablesInvoice	= MatchWork.PayablesInvoice

				invoke SelectRecordForRecon WorkReceiptLineAOCsRel

		UnselectRecordForRecon is an Instance Action
			valid when (Selected)
			default label is "Unselect"
			Action Rules
				Selected	= false
				SortSelect	= 0
				initialize PayablesInvoice
				initialize MatchReconQueueSet
				initialize SortNumber

				invoke UnselectRecordForRecon WorkReceiptLineAOCsRel



		SetSortNumber is an Instance Action
			restricted
			Action Rules
				SortNumber = 1

		InitializeSortNumber is an Instance Action
			restricted
			Action Rules
				initialize SortNumber
		
		SelectAll is a Set Action
			restricted 
			default label is "All"
			run in foreground
			Instance Selection
				where (MatchWork = MatchReconQueue.MatchWork
					and !Selected)

			Action Rules
				Instance Rules
					invoke SelectRecordForRecon

		DeselectAll is a Set Action
			restricted 
			default label is "None"
			run in foreground
			Instance Selection
				where (MatchWork = MatchReconQueue.MatchWork
				and    Selected)

			Action Rules
				Instance Rules
					invoke UnselectRecordForRecon
