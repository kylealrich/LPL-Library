LCalcImportQueueEntry is a BusinessClass
    owned by la
    prefix is CAIE
    
    Ontology
    	part of LCalcImportQueue
    		relative key is Sequence is Numeric size 6
    		
    Patterns
        implements CRUD
        implements CreateStamp
        disable Auditing
        disable EffectiveDated

    Persistent Fields   
        EnterpriseGroup
        BusinessClassName
        
        DataChangeStatus        is Numeric size 1
            States
                Create              value is 0
                Delete              value is 1
                Update              value is 2
        
        ProcessStatus           is Numeric size 2
            States
                New                 value is 0
                Pending             value is 10
                Processing          value is 20
                Complete            value is 30
                RolledBack          value is 40
                Failed              value is 90
           
        DataSize                is Numeric 6
        ErrorData               is Text
        



        FieldData1              is Alpha size up to 800 
        FieldData2              is Alpha size up to 800 
        FieldData3              is Alpha size up to 800 
        FieldData4              is Alpha size up to 800
        ActionData				is Text
        
    Conditions
        IsNew
            default label is "New"
            when (ProcessStatus.New) 
        IsPending
            default label is "Pending"
            when (ProcessStatus.Pending)
        IsComplete
            default label is "Complete"
            when (ProcessStatus.Complete)
        IsFailed
            default label is "Failed"
            when (ProcessStatus.Failed)
        IsCreated
            default label is "Created"
            when (DataChangeStatus.Create) 
        IsDeleted
            default label is "Deleted"
            when (DataChangeStatus.Delete)
        IsUpdated
            default label is "Updated"
            when (DataChangeStatus.Update)
    









	Field Rules
		Sequence
			autosequence using LCalcImportQueue.LastSequence
			
	StateCycles
	
		EntryLifeCycle is a StateCycle
			state field is ProcessStatus
			initial state is New
			
			New is a State

				Create is a Create Action
					restricted
				
				MakePending is an Instance Action

					Action Rules
						make transition to Pending	

			Pending is a State
				
				MakeComplete is an Instance Action

					Action Rules
						make transition to Complete
						
				MakeFailed is an Instance Action

					Parameters
						Message is Text						
					Action Rules
						ErrorData = Message
						make transition to Failed
						
			Complete is a State
				Delete is a Delete Action
				Purge is a Purge Action
			
			Failed is a State
				
				ResetToPending is an Instance Action

					Action Rules
						make transition to Pending
				
				ForceComplete is an Instance Action

					Action Rules
						make transition to Complete
						

