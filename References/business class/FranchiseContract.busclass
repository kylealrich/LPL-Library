FranchiseContract is a BusinessClass
    owned by fr
    prefix is FRM
    classic name is FRCONTMAST

    Ontology
        symbolic key is FranchiseContract
            classic set name is FRMSET1
            classic name is CONTR-NBR
            classic name for CustomerShipTo is SHIP-TO

    Patterns
        implements Proxy for FranchiseContractLicenseeRel 
    	implements InlineUserFields
    		size is 1000
        implements StaticJava

        disable AuditIndex

    Persistent Fields

        Active
            classic name is ACTIVE-FLAG
        Currency
            classic name is CURRENCY-CODE
        ContractDate       		is Date
        TerminateDate      		is Date
        FranchiseGroupCode
            classic name is GROUP-CODE
		TaxableFlag 			is AlphaUpper size 1        
			States
			    Exempt  value is "E"
            	Taxable value is "T"		
        TaxCode
        LastActivityDate   		is Date
            classic name is LAST-ACT-DATE
        TotalEstimates     		is an InternationalAmount
            classic name is TTL-ESTIMATES
        VarianceLimit      		is an InternationalAmount
        ProcessLevel			is a BillingProcessLevel
        FranchiseCalendar
            classic name is CALENDAR-ID
        SalesFlag
            classic name is SALES-FL
        SalesByDate
        FranchiseBudget
            classic name is BUDGET-ID
        EstimatesFlag      		is AlphaUpper size 1
            classic name is ESTIMATES-FL
            States
                EstimatesFromActuals value is "A"
                    default label is "Estimates from actuals"
                EstimatesFromBudget  value is "B"
                    default label is "Estimates from budget"
		LastFranchiseSales		is Numeric 18
			disable Auditing
		CalculateInvoiceAmountOnGrossSales  is Boolean
		CalculateTaxOnNetSales 				is Boolean
		BillingInvoiceType
		BillingCreditMemoInvoiceType   is a BillingInvoiceType
		AdvanceCycleDateForNoActivity  is Boolean

	Context Fields
		ChargeTypeFilter		is a ChargeType
		DateRangeFilter			is a DateRange

		



			
	Local Fields
		LocalCustomer 					is like Customer
    	LocalFranchiseCompany			is like FranchiseCompany
	    LocalCustomerShipTo				is like CustomerShipTo
	    LocalFranchiseContract			is like FranchiseContract
	    LocalFranchiseStandardChargeGroup		is like FranchiseStandardChargeGroup
		LocalCurrencyRelationship 			    is a CurrencyRelationship
		
	Derived Fields
		
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforCustomerContract"

		ContextMessageText is a MessageField
			restricted
			"CustomerContractForCompany<Company>,Customer<Customer>,ShipTo<CustomerShipTo>,AndContractNumber<FranchiseContract>"
		
		ConstantValue1 is a ComputeField
			type is Numeric 1
			(1+0)
			
		StandardChargesCount is a DerivedField
			type is Numeric 3
			return (instance count of CopyStandardContractChargesRel)			






		FranchiseContractFormTitle is a DerivedField
			type is Alpha size up to 80
			if (RecordExists)
				return FranchiseContractTitle
			else
				return FranchiseContractTitleNoRecord
		
		FranchiseContractTitleNoRecord is a LabelField
			restricted
			"FranchiseContract"
		
		FranchiseContractTitle is a LabelField
			restricted
			"FranchiseContract<FranchiseContract>_for_<Customer.Name>"

		ActiveStatus is a DerivedField
			type is Alpha size 10
			if(Active)
				return "Active"
			if(not Active)
				return ("InActive")

		DerivedCompany is a StringField
			type is Text
			default label is "Company"
			Company " - " Company.BillingCompany.Name

		DerivedCustomer is a StringField
			type is Text
			default label is "Customer"
			Customer " - " Customer.Name

		DerivedShipTo is a StringField
			type is Text
			default label is "ShipTo"
			CustomerShipTo " - " CustomerShipTo.Name

        DerivedDaysRemaining is a ComputeField
			type is Numeric size 3
            restricted
            ((TerminateDate - current corporate date))

    Conditions
        IsActiveContrct
        	restricted
            when (Active)
        
        IsSalesOnly
        	restricted
            when (SalesFlag.Yes)
		
		FranchiseContractChargeExists
			restricted
			when (FranchiseContractChargesRel exists)
		
		FranchiseContractLicenseeExists
			restricted
			when (FranchiseContractLicenseeRel exists)	
		
		OrderEntryCustomerIsComplete
			restricted
			when (ReceivableCompanyCustomerRel.IsOrderEntryCustomerComplete)
		
		InvoiceType
			restricted
			when (BillingInvoiceType.Credit)
		
		CreditMemoInvoiceType
			restricted
			when (BillingCreditMemoInvoiceType.Credit)
			
		IsAdvanceCycleDate
			restricted
            when (AdvanceCycleDateForNoActivity and Active)
            
		RecordExists
			restricted
			when (FranchiseContract exists)

		IsValidForActorContext			
			restricted
			when ((actor.context.FinanceEnterpriseGroup != ""
			and   Company.GeneralLedgerCompany.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
			or   (actor.context.FinanceEnterpriseGroup = ""))

        IsContractExpiringIn180Days
            restricted
			when (DerivedDaysRemaining >= 0
			and	DerivedDaysRemaining <  180)

    Relations
        ActiveFranchiseContractRel
            one-to-many relation to FranchiseContract
            Field Mapping uses symbolic key
                related.Company           = Company
                related.Customer          = Customer
                related.CustomerShipTo    = CustomerShipTo
			Instance Selection
				where (related.FranchiseContract != FranchiseContract
				and related.Active)

		CopyContractChargesRel
			one-to-many relation to FranchiseContractCharge
			Field Mapping uses symbolic key
			   	related.Company						= Company
            	related.Customer					= LocalCustomer		   	
            	related.CustomerShipTo				= LocalCustomerShipTo
            	related.FranchiseContract			= LocalFranchiseContract
			
		CopyStandardContractChargesRel
			one-to-many relation to FranchiseStandardChargeGroupDetail
			Field Mapping uses symbolic key
			   	related.Company								= Company
			   	related.FranchiseStandardChargeGroup 		= LocalFranchiseStandardChargeGroup


		ContractSalesChargesRel is a FranchiseSales set
			
		SalesTypeChargesRel
			one-to-many relation to FranchiseStandardCharge
			Field Mapping uses Set2
				related.Company			= Company
			Instance Selection
				where (related.Active)

        ReceivableCompanyCustomerRel
            one-to-one relation to CompanyCustomer
            required
            Field Mapping uses symbolic key
                related.Company  = Company
                related.Customer = Customer

        FranchiseContractChargesRel
            one-to-many relation to FranchiseContractCharge
            Field Mapping uses symbolic key
                related.Company           = Company
                related.Customer          = Customer
                related.CustomerShipTo    = CustomerShipTo
                related.FranchiseContract = FranchiseContract

        FranchisePrepaymentsRel
            one-to-many relation to FranchisePrepayment
            Field Mapping uses Set1
                related.Company           = Company
                related.Customer          = Customer
                related.CustomerShipTo    = CustomerShipTo
                related.FranchiseContract = FranchiseContract

        FranchiseCompanyRel
            one-to-one relation to FranchiseCompany
            required
            Field Mapping uses symbolic key
                related.Company        = Company
                
        FranchiseSalesRel
            one-to-many relation to FranchiseSales
            Field Mapping uses Set2
                related.Company           = Company
                related.Customer          = Customer
                related.CustomerShipTo    = CustomerShipTo
                related.FranchiseContract = FranchiseContract

        ShiptoRel
            one-to-one relation to CustomerShipTo
            required
            Field Mapping uses symbolic key
                related.Company        = Company
                related.Customer       = Customer
                related.CustomerShipTo = CustomerShipTo

        ReceivableCustomerRel
            one-to-one relation to Customer
            required
            Field Mapping uses symbolic key
                related.CustomerGroup = Company.CustomerGroupField.CustomerGroup	
                related.Customer      = Customer
                
     	FranchiseContractLicenseeRel
     		one-to-one relation to FranchiseContractLicensee
     		Field Mapping uses symbolic key
     			related.Company				= Company		
				related.Customer			= Customer
				related.CustomerShipTo		= CustomerShipTo
     			related.FranchiseContract	= FranchiseContract	           

        OrderEntryCustomerRel
            one-to-many relation to OrderEntryCustomer
            Field Mapping uses symbolic key
                related.Company  = Company
                related.Customer = Customer
                
        EntityTaxCodeRel
			one-to-one relation to EntityTaxCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.TaxEntity				= Company.AccountingEntity
				related.TaxCode					= TaxCode
				
		TaxRateRel
            one-to-many relation to TaxRate
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup 
                related.TaxCode 						= TaxCode
    
    Sets
        Set2
            indexed
            Instance Selection
                where (IsActiveContrct)
            Sort Order
                Company
                FranchiseGroupCode
                Customer
                CustomerShipTo
                FranchiseContract
                
                
        Set3
            indexed
            Instance Selection
                where (IsAdvanceCycleDate)
            Sort Order
	            Company
	            FranchiseGroupCode
	            Customer
	            CustomerShipTo
	            FranchiseContract

         


  	Attach Rules
		constraint (Active)
			"ContractIsInactive"
			
    Field Rules
        FranchiseCalendar
        	default to (FranchiseCompanyRel.FranchiseCalendar)
            required

        EstimatesFlag
        	initial value is "A"
            default to "A"

		Active
			initial value is true
			if (Active)
				constraint (ActiveFranchiseContractRel not exists)
					"AnActiveContractAlreadyExistsForCustomer<Customer>ShipTo<CustomerShipTo>"
			if (action type.Create
			and !ProcessLevel entered)
				ProcessLevel	 = FranchiseCompanyRel.ProcessLevel

		TaxableFlag
			initial value is TaxableFlag.Exempt
			default to TaxableFlag.Exempt
		
		TaxCode
			if (TaxableFlag.Exempt)
				cannot be entered
					"TaxCodeCannotBeEnteredAsTaxableFlagIsExempt"//"TaxCodeIsNotValidWithTaxableFlagOfExempt"  
			else
				required
					"TaxCodeIsRequired"	
				constraint(EntityTaxCodeRel exists)
					"TaxCodeNotSetupForEntityTaxCode"
				constraint(TaxRateRel exists)
					"TaxRateNotFoundForTaxCode:<TaxCode>"	
		SalesFlag
			initial value is SalesFlag.No
			default to SalesFlag.No
			if (FranchiseCompanyRel.SalesFlag.No)
				constraint (SalesFlag.No)
					"Company_\OptionsDoesNotAllowAggregateSales"//"CompanyOptionsDoesNotAllowAggregateSales"        
		SalesByDate
            initial value is SalesByDate.No
			default to SalesByDate.No
			if (FranchiseCompanyRel.SalesByDate.No)
				constraint (SalesByDate.No)
					"Company_\OptionsDoesNotAllowSalesByDate"//"CompanyOptionsDoesNotAllowSalesByDate"      

		TerminateDate
			required 
			constraint (TerminateDate > ContractDate)
				"TerminationDateMustBeGreaterThanContractDate"//"TerminationDateMustBeGreaterThanContractDate"     
			if (FranchiseContractChargeExists)
				cannot be changed
		ContractDate
			required
			if (FranchiseContractChargeExists)
				cannot be changed

        Currency    
            required		
			if (FranchiseContractChargeExists)
				cannot be changed
			
			if (Currency != ReceivableCompanyCustomerRel.Currency)		
				LocalCurrencyRelationship.FromCurrency 	= Currency
				LocalCurrencyRelationship.ToCurrency	= ReceivableCompanyCustomerRel.Currency
				constraint (LocalCurrencyRelationship exists)
					"CurrencyRelationshipBetween<LocalCurrencyRelationship.FromCurrency>To<LocalCurrencyRelationship.ToCurrency>DoesNotExist"
					
		FranchiseBudget
			constraint (FranchiseBudget.BudgetType.AmountsAndDates)
				"Budget_\TypeMustBe_\A"//"BudgetTypeMustBeAmountsAndDates" 
			constraint (FranchiseBudget.Currency = Currency)
				"Budget_\CurrencyCodeMustBeTheSameAsThe_\Contract"//"BudgetCurrencyCodeMustBeTheSameAsTheContract" 

		CalculateTaxOnNetSales
			if (TaxableFlag.Taxable)
				if (action type.Create)
					default to Company.CalculateTaxOnNetSales
			else	
				cannot be entered
					"CannotEnterCalculateTaxOnNetSales;ContractIsExempt"
					
		BillingInvoiceType
			default to FranchiseCompanyRel.BillingInvoiceType
			constraint (!InvoiceType)
				"InvoiceTypeShouldNotBeCredit"
			
		BillingCreditMemoInvoiceType
			default to FranchiseCompanyRel.BillingCreditMemoInvoiceType
			constraint (CreditMemoInvoiceType)
				"CreditMemoTypeShouldBeCredit"
				
		AdvanceCycleDateForNoActivity
			constraint (FranchiseCompanyRel.AdvanceCycleDateForNoActivity)
				"Advance\Cycle\Date\For\No\ActivityFlagIsNotSetToTrueIn_\Company"
				
	Actions
		Create is a Create Action
			Action Rules
				constraint (FranchiseContractLicensee not exists)
					"CannotAdd;_\Licensee/\LocationFieldsExist"//"CannotAdd;Licensee/LocationFieldsExist"     
				constraint (CustomerShipTo.Active)
					"CustomerShipToIsInactive"//"CustomerShipToIsInactive"    

						
    	CopyChargesFromContract is an Instance Action
    		default label is "CopyCharges"
    		valid when (!FranchiseContractChargeExists)
    		completion message is "AddCompleted"
    		Parameters
    			PrmFranchiseCompany				is a FranchiseCompany
    			PrmFranchiseContract			is a FranchiseContract
    			PrmFranchiseCustomer			is a Customer
    			PrmFranchiseCompanyCustomer		is a CompanyCustomer
    			PrmCustomerShipTo				is a CustomerShipTo
    			PrmFranchiseChargeGroup			is a FranchiseStandardChargeGroup
	
			Local Fields
				LocalContractCharge is Numeric 1
			Parameter Rules
				PrmFranchiseCompany
					initial value is Company
					required
					if (PrmFranchiseCustomer entered)
						constraint (PrmCustomerShipTo entered)
							"FranchiseContractTypeCopy;ShipToIsRequired"
						constraint (PrmFranchiseContract entered)
							"FranchiseContractTypeCopy;ContractIsRequired"
						constraint (PrmFranchiseChargeGroup not entered)
							"ChargeGroupCannotBeEnteredWhenPerformingAFranchiseContractTypeCopy"
					else	
						constraint (PrmFranchiseChargeGroup entered)	
							"EitherCustomerOrChargeGroupIsRequired"
						constraint (PrmCustomerShipTo not entered)
							"ShipToNotAllowedWhenCopyingFromChargeGroup"
						constraint (PrmFranchiseContract not entered)
							"ContractNotAllowedWhenCopyingFromChargeGroup"
								
					if (PrmCustomerShipTo entered)
						constraint (PrmFranchiseCustomer entered)
							"FranchiseContractTypeCopy;CustomerIsRequired"
						constraint (PrmFranchiseContract entered)
							"FranchiseContractTypeCopy;ContractIsRequired"							
					if (PrmFranchiseContract entered)		
						constraint (PrmFranchiseCustomer entered)
							"FranchiseContractTypeCopy;CustomerIsRequired"					
						constraint (PrmCustomerShipTo entered)
							"FranchiseContractTypeCopy;ShipToIsRequired"						
			
    			
			Action Rules
   				LocalFranchiseCompany					= PrmFranchiseCompany	
    			LocalCustomer							= PrmFranchiseCustomer
	    		LocalCustomerShipTo						= PrmCustomerShipTo
	    		LocalFranchiseContract					= PrmFranchiseContract
	    		LocalFranchiseStandardChargeGroup 		= PrmFranchiseChargeGroup		

			
			
				if (PrmFranchiseContract entered)	
					for each CopyContractChargesRel						
						invoke Create FranchiseContractCharge

							fill in fields from each
								except invoked.FranchiseContract
								except invoked.Customer
								except invoked.CustomerShipTo
								except invoked.Active
								except invoked.BeginDate								
							invoked.FranchiseContract = FranchiseContract
							invoked.Customer          = FranchiseContract.Customer	
							invoked.CustomerShipTo    = FranchiseContract.CustomerShipTo	
							invoked.Active            = FranchiseContract.Active
							invoked.BeginDate		  = FranchiseContract.ContractDate
				else
					LocalContractCharge = 1
					for each CopyStandardContractChargesRel	
						while (LocalContractCharge <= StandardChargesCount)					
							invoke CopyFromStandardChargeGroup FranchiseContractCharge

								fill in fields from each
									except invoked.FranchiseContract
									except invoked.Customer
									except invoked.CustomerShipTo
									except invoked.Active
									except invoked.BeginDate								
								invoked.FranchiseContract 			= FranchiseContract
								invoked.Customer          			= FranchiseContract.Customer
								invoked.FranchiseContractCharge 	= LocalContractCharge	
								invoked.CustomerShipTo    			= FranchiseContract.CustomerShipTo	
								invoked.Active            			= FranchiseContract.Active
								invoked.FranchiseStandardCharge 	= each.FranchiseStandardCharge
								invoked.BeginDate		  			= FranchiseContract.ContractDate
								invoked.EndDate			  			= FranchiseContract.TerminateDate
								invoked.ChargeType		  			= each.FranchiseStandardCharge.ChargeType
								invoked.Frequency         			= each.FranchiseStandardCharge.Frequency								
								invoked.ProcessLevel         		= each.FranchiseStandardCharge.ProcessLevel
								invoked.TaxableFlag		  			= each.FranchiseStandardCharge.TaxableFlag
								invoked.TaxCode		  	  			= each.FranchiseStandardCharge.TaxCode
								invoked.MinimumAmount	  			= each.FranchiseStandardCharge.MinimumAmount
								invoked.MaximumAmount	  			= each.FranchiseStandardCharge.MaximumAmount
								invoked.BaseAmount	  				= each.FranchiseStandardCharge.BaseAmount
								invoked.PercentOfSales	  			= each.FranchiseStandardCharge.PercentOfSales
								invoked.InterestRate	  			= each.FranchiseStandardCharge.InterestRate
								invoked.BalanceFlag	  				= each.FranchiseStandardCharge.BalanceFlag
								invoked.Description					= each.FranchiseStandardCharge.Description
								
							LocalContractCharge += 1		


        Update is an Update Action
			Action Rules
				if (Active changed
				and !old Active)
					constraint (FranchiseContractChargesRel not exists)
						"ContractCannotBeReactivated"//"ContractCannotBeReactivated"     
						
        Delete is a Delete Action
        	
			Entrance Rules
				invoke Delete FranchiseContractLicenseeRel
