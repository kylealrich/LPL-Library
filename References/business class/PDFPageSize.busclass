PDFPageSize is a BusinessClass
    owned by la
    prefix is PDPS
    representative text is "<PDFPageSize>-<PageSizeInchesDesc>"
	store using com.lawson.rdtech.framework.PDFPageSizeStorage
	
    Ontology
    	symbolic key is PDFPageSize

    Patterns
        disable Auditing
        disable EffectiveDated
        disable UniqueID
    	implements ReadOnly
    	
    Persistent Fields
    	Ordinal is Numeric size 2
    	PDFPageDimensionsInPoints is a PDFPageDimensions
    	PDFPageDimensionsInInches is a PDFPageDimensions
    	PDFPageDimensionsInCm 	  is a PDFPageDimensions
    	
    Conditions
    	IsCustomPageSize
    		when (PDFPageSize = "Custom")
    		
    Derived Fields
    	InchesMessageField is a MessageField
    		restricted
    		"inches"
    		
    	CentimeterMessageField is a MessageField
    		restricted
    		"centimeters"
    		
    	PointsMessageField is a MessageField
    		restricted
    		"points"
    	
    	CustomMessageField is a MessageField
    		restricted
    		"Custom"
    		
    	PageSizePointsDescMessage is a MessageField
    		restricted
    		"<PDFPageDimensionsInPoints.PageWidth>x<PDFPageDimensionsInPoints.PageHeight>_<PointsMessageField>"
    		
    	PageSizeInchesDescMessage is a MessageField
    		restricted
    		"<PDFPageDimensionsInInches.PageWidth>x<PDFPageDimensionsInInches.PageHeight>_<InchesMessageField>"
    		
    	PageSizeCentimeterDescMessage is a MessageField
    		restricted
    		"<PDFPageDimensionsInCm.PageWidth>x<PDFPageDimensionsInCm.PageHeight>_<CentimeterMessageField>"
    	
    	PageSizePointsDesc is a DerivedField
    		type is MessageField
    		if (IsCustomPageSize)
    			return CustomMessageField
			return PageSizePointsDescMessage
    		
    	PageSizeInchesDesc is a DerivedField
    		type is MessageField
    		if (IsCustomPageSize)
    			return CustomMessageField
			return PageSizeInchesDescMessage
    			
    	PageSizeCentimeterDesc is a DerivedField
    		type is MessageField
    		if (IsCustomPageSize)
    			return CustomMessageField
			return PageSizeCentimeterDescMessage
    	
    Sets
		ByOrdinal
			indexed
			Sort Order
				Ordinal
				PDFPageSize
    	
