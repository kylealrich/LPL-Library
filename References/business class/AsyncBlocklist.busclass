AsyncBlocklist is a BusinessClass
    owned by async
    prefix is ASBL
    stored in environment
    disable data area copy
		preserve target data

    Ontology
		symbolic key is AsyncBlocklist

    Patterns
        disable Auditing
        disable EffectiveDated
        implements CreateStamp

	Persistent Fields
		NumberOfInterrupts is Numeric size 4
		InterruptedWithin  is Numeric size 19 
			default label is "NumberOfSecondsInterruptsOccurredWithin"
		ManuallyCreated    is Boolean
	
	Field Rules
		ManuallyCreated
			cannot be changed
			
	Local Fields
		LocAsyncBlocklist is an AsyncBlocklist
		
	Derived Fields
		InterruptedWithinDays is a DerivedField    
            type is Numeric 6
            InterruptedWithinDays = ((InterruptedWithin / 86400) - .5)
            if (InterruptedWithinDays < 0)
                InterruptedWithinDays = 0
                
        InterruptedWithinHours is a DerivedField    
            type is Numeric 2
            InterruptedWithinHours = (((InterruptedWithin - (InterruptedWithinDays * 86400)) / 3600) - .5)
            if (InterruptedWithinHours < 0)
                InterruptedWithinHours = 0
                
		InterruptedWithinMinutes is a DerivedField 
            type is Numeric 2
            restricted
            InterruptedWithinMinutes = ((((InterruptedWithin - (InterruptedWithinDays * 86400)) - (InterruptedWithinHours * 3600)) / 60) - .5)
            if (InterruptedWithinMinutes < 0)
                InterruptedWithinMinutes = 0

		InterruptedWithinSeconds is a DerivedField  
            type is Numeric 2
            InterruptedWithinSeconds = ((((InterruptedWithin - (InterruptedWithinDays * 86400)) - (InterruptedWithinHours * 3600)) - (InterruptedWithinMinutes * 60)) - .5)
            if (InterruptedWithinSeconds < 0)
                InterruptedWithinSeconds = 0
                
        SecondMessage is a MessageField
        	"<InterruptedWithinSeconds>_second"
        
        SecondsMessage is a MessageField
        	"<InterruptedWithinSeconds>_seconds"
        	
        InterruptedSecondsMessage is a DerivedField
        	type is MessageField	
        	if (InterruptedWithinSeconds = 1)
        		return SecondMessage
        	return SecondsMessage
        	
		MinuteMessage is a MessageField
			"<InterruptedWithinMinutes>_minute"
        	
        MinutesMessage is a MessageField
        	"<InterruptedWithinMinutes>_minutes" 
        	
        MinuteOrMinutesMessage is a DerivedField
        	type is MessageField	
        	if (InterruptedWithinMinutes = 1)
        		return MinuteMessage
        	return MinutesMessage
        	
        HourMessage is a MessageField
        	"<InterruptedWithinHours>_hour"
        	
        HoursMessage is a MessageField
        	"<InterruptedWithinHours>_hours"
        
        HourOrHoursMessage is a DerivedField
        	type is MessageField	
        	if (InterruptedWithinHours = 1)
        		return HourMessage
        	return HoursMessage
        
        DayMessage is a MessageField
        	"<InterruptedWithinDays>_day"
        	
        DaysMessage is a MessageField
        	"<InterruptedWithinDays>_days"
        		
        DayOrDaysMessage is a DerivedField
        	type is MessageField	
        	if (InterruptedWithinDays = 1)
        		return DayMessage
        	return DaysMessage
        	
		InterruptedDaysMessage is a MessageField
        	"<DayOrDaysMessage>_<InterruptedHoursMessage>"
        	
        InterruptedHoursMessage is a MessageField
        	"<HourOrHoursMessage>_<InterruptedMinutesMessage>"
        	
        InterruptedMinutesMessage is a MessageField
        	"<MinuteOrMinutesMessage>_<InterruptedSecondsMessage>"
        	
        InterruptedMessage is a DerivedField
        	type is MessageField
        	
        	if (InterruptedWithinDays > 0)
        		return InterruptedDaysMessage

			if (InterruptedWithinHours > 0)
        		return InterruptedHoursMessage
        	
        	if (InterruptedWithinMinutes > 0)
        		return InterruptedMinutesMessage
        	
        	return InterruptedSecondsMessage
        
        AsyncAuditingEnabled is a NativeField
			type is Boolean
			default label is untranslatable
			restricted

    Conditions
    	HasAuditRecords
			default label is "AuditRecordsExist"
			when (AuditingRel exists)	
			
	Create Rules

		LocAsyncBlocklist = AsyncBlocklist
		constraint (not LocAsyncBlocklist exists)
			"<AsyncBlocklist label>AlreadyExists"
		
	Actions
		CreateActionBlock is a Create Action
			confirmation required
				"ThisWillBlockTheSpecifiedActionFromRunning.AreYouSure?"
			Parameters
				ParamDataArea is a DataArea
					default label is untranslatable:"<AsyncBlocklist.DataArea label>"
				ParamBusinessView is a BusinessView
					default label is untranslatable:"<AsyncBlocklist.BusinessView label>"
        		ParamBusinessAction is a BusinessAction
        			default label is untranslatable:"<AsyncBlocklist.BusinessAction label>"
        		ParamMappingField1 is a AsyncMappingField
        			default label is untranslatable:"<AsyncBlocklist.MappingField1 label>"
				ParamMappingField2 is a AsyncMappingField
        			default label is untranslatable:"<AsyncBlocklist.MappingField2 label>"
        		        			
        	Parameter Rules
        		ParamDataArea 
        			initial value is parentcontext.dataarea
        			force default to parentcontext.dataarea
        		ParamBusinessView
        			required
        		ParamBusinessAction
    				required
        		
			Action Rules
				invoke CreateInternal this instance
					invoked.AsyncBlocklist.DataArea = ParamDataArea
					invoked.AsyncBlocklist.BusinessView   = ParamBusinessView
					invoked.AsyncBlocklist.BusinessAction = ParamBusinessAction
					invoked.AsyncBlocklist.MappingField1  = ParamMappingField1
					invoked.AsyncBlocklist.MappingField2  = ParamMappingField2
					invoked.ManuallyCreated = true
					
		CreateListBlock is a Create Action
			confirmation required
				"ThisWillBlockTheSpecifiedListFromGeneratingDocuments.AreYouSure?"
			Parameters
				ParamDataArea is a DataArea
					default label is untranslatable:"<AsyncBlocklist.DataArea label>"
				ParamBusinessView is a BusinessView
					default label is untranslatable:"<AsyncBlocklist.BusinessView label>"
        		ParamBusinessList is a BusinessList
        			default label is "List"
        			
        	Parameter Rules
        		ParamDataArea 
        			initial value is parentcontext.dataarea
        			force default to parentcontext.dataarea
        		ParamBusinessView
    				required
        		ParamBusinessList
    				required
			Action Rules
				invoke CreateInternal this instance
					invoked.AsyncBlocklist.DataArea = ParamDataArea
					invoked.AsyncBlocklist.BusinessView   = "ReportDocument"
					invoked.AsyncBlocklist.BusinessAction = "Create"
					invoked.AsyncBlocklist.MappingField1  = ParamBusinessView
					invoked.AsyncBlocklist.MappingField2  = ParamBusinessList
					invoked.ManuallyCreated = true
			
		CreateFormBlock is a Create Action
			confirmation required
				"ThisWillBlockTheSpecifiedFormFromGeneratingDocuments.AreYouSure?"
			Parameters
				ParamDataArea is a DataArea
					default label is untranslatable:"<AsyncBlocklist.DataArea label>"
				ParamBusinessView is a BusinessView
					default label is untranslatable:"<AsyncBlocklist.BusinessView label>"
        		ParamBusinessForm is a BusinessForm
        			default label is "Form"
        			
        	Parameter Rules
        		ParamDataArea 
        			initial value is parentcontext.dataarea
        			force default to parentcontext.dataarea
        		ParamBusinessView
    				required
        		ParamBusinessForm
    				required
			Action Rules
				invoke CreateInternal this instance
					invoked.AsyncBlocklist.DataArea = ParamDataArea
					invoked.AsyncBlocklist.BusinessView   = "ReportDocument"
					invoked.AsyncBlocklist.BusinessAction = "Create"
					invoked.AsyncBlocklist.MappingField1  = ParamBusinessView
					invoked.AsyncBlocklist.MappingField2  = ParamBusinessForm
					invoked.ManuallyCreated = true
					
		CreatePageBlock is a Create Action
			confirmation required
				"ThisWillBlockTheSpecifiedPageFromGeneratingDocuments.AreYouSure?"
			Parameters
				ParamDataArea is a DataArea
					default label is untranslatable:"<AsyncBlocklist.DataArea label>"
				ParamBusinessPage is a BusinessPage
        			default label is "Page"
        		ParamMappingField2 is a AsyncMappingField
        			default label is "Panel"
        			
        	Parameter Rules
        		ParamDataArea 
        			initial value is parentcontext.dataarea
        			force default to parentcontext.dataarea
        		ParamBusinessPage
    				required
			Action Rules
				invoke CreateInternal this instance
					invoked.AsyncBlocklist.DataArea = ParamDataArea
					invoked.AsyncBlocklist.BusinessView   = "ReportDocument"
					invoked.AsyncBlocklist.BusinessAction = "Create"
					invoked.AsyncBlocklist.MappingField1  = ParamBusinessPage
					invoked.AsyncBlocklist.MappingField2  = ParamMappingField2
					invoked.ManuallyCreated = true
					
		CreateInternal is a Create Action
			default label is "Create"
			restricted
			
			Exit Rules
				if (ManuallyCreated and AsyncAuditingEnabled) 
					invoke Create AsyncAuditEntry  
				
		Delete is a Delete Action
			Entrance Rules
				if (not ManuallyCreated)
					confirmation required
						"UnlessTheUnderlyingIssueThatCausedThisActionToBeBlockedHasBeenResolvedItLikelyWillEndUpBlockedAgain.AreYouSure?"
				
				if (AsyncAuditingEnabled)
					invoke Create AsyncAuditEntry
				 		
	Relations
		AuditingRel
			one-to-many relation to AsyncAuditEntry
			Field Mapping uses ByBusClassID
				related.Reference.BusinessClassName = "AsyncBlocklist"
				related.Reference.BusinessObjectKey = UniqueID
		
	Sets



		ByBlocklistMembersFields
			not indexed
			Sort Order
				AsyncBlocklist.DataArea  
        		AsyncBlocklist.BusinessView
        		AsyncBlocklist.BusinessAction
        		AsyncBlocklist.MappingField1
				AsyncBlocklist.MappingField2
