RNILine is a BusinessClass
	owned by po
	
	prefix is RNIL
	
	Ontology
		symbolic key is RNILine
		
	
	Patterns
		disable Auditing
		disable AuditIndex
		disable EffectiveDated
	
	Persistent Fields
		PostDate					is Date
		Item						is like Item
		ItemType
		Description						
		Vendor						is like Vendor						
		VendorName					is like Name	 
			holds pii
		Buyer						is like Buyer
		BuyerName					is like Name	 
			holds pii
		PurchaseFromLoc				is like VendorLocation
		ProcessLevel				is like PayablesProcessLevel
		PORelease
		ShipToLocation				is like InventoryLocation
		RniQty						is like Quantity
			precision is DerivedNumberOfDecimalsQuantity
		PliUnitCost					is like InternationalCost
			precision is 2
		PoCurAmt					is like InternationalCost
			precision is 2
		BaseCurAmt					is like InternationalCost
			precision is 2
		RecActDate					is Date
		PrlEntRecQty				is like Quantity
			precision is DerivedNumberOfDecimalsQuantity
		PrlEntRecUom				is like UnitOfMeasure
		DistributionCodeBlock		is a FinanceCodeBlock
		LineAOC						is like AddOnCharge
		InvNbr						is a RNILineInvoice
	
	Transient Fields
		TransientAccrualBlock				is a FinanceCodeBlock
			derive value from first LineDistributionRel.AccrualCodeBlock
		

	Derived Fields
		
		DerivedNumberOfDecimalsQuantity is a ConditionalField
			type is Numeric size 1
			restricted
			if (ItemType.NonStock
			or  ItemType.Inventoried)
				ItemRel.NumberOfDecimalsQuantity
			else
			if (ItemType.Special
			or  ItemType.Service)
				InventoryCompanyRel.NumberOfDecimalsQuantity
			else
				4
		
		DerivedLineCreditAmount is a DerivedField
			type is like InternationalCost
			return sum LineDistributionRel.CreditAmount
					
	Conditions
		
		InvoiceMessageExists
			when (InvNbr.InvoiceNumber[1] entered)
		
		IsNonInventoryType
			when (ItemType.NonStock 
			or    ItemType.Special)
		
		IsInventoryType
			when (ItemType.Inventoried)
		
		IsNonStockType
			when (ItemType.NonStock)
		
		IsSpecialType
			when (ItemType.Special)		
    
    Relations

		ItemRel
            one-to-one relation to Item
            Field Mapping uses symbolic key
                related.ItemGroup 	= Company.InventoryCompany.ItemGroup
                related.Item		= Item
    	
    	InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = Company
    	
    	LineDistributionRel
			one-to-many relation to RNILineDistribution
			delete cascades
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PurchaseOrderResult = PurchaseOrderResult
				related.PurchaseOrder 		= PurchaseOrder
				related.PurchaseOrderLine	= PurchaseOrderLine
				related.RNILine 			= RNILine
    	
    	
    	RNILineDistributionsRel
    		one-to-many relation to RNILineDistribution
    		delete cascades
    		Field Mapping uses symbolic key
    			related.Company				= Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    			related.PurchaseOrder 		= PurchaseOrder
    			related.PurchaseOrderLine	= PurchaseOrderLine
    			related.RNILine 			= RNILine
            Instance Selection
            	where (related.AddOnCharge not entered)
		
		RNILineAOCDistributionsRel
    		one-to-many relation to RNILineDistribution
    		delete cascades
    		Field Mapping uses symbolic key
    			related.Company				= Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    			related.PurchaseOrder 		= PurchaseOrder
    			related.PurchaseOrderLine	= PurchaseOrderLine
    			related.RNILine 			= RNILine
            Instance Selection
            	where (related.AddOnCharge entered)

		RNILineAOCRel
			one-to-many relation to RNILineAOC
			delete cascades
			Field Mapping uses symbolic key
    			related.Company				= Company
    			related.PurchaseOrderResult = PurchaseOrderResult
    			related.PurchaseOrder 		= PurchaseOrder
    			related.PurchaseOrderLine	= PurchaseOrderLine
    			related.RNILine 			= RNILine
				

	Sets
		Set1
			Sort Order
				PurchaseOrderResult
				Company
				ItemType
				ProcessLevel
				PurchaseFromLoc
				PurchaseOrder
				PurchaseOrderLine
	
	Actions
		Create is a Create Action
			restricted
			bypass field rules
		Delete is a Delete Action
			Entrance Rules
				invoke Delete LineDistributionRel
				invoke Delete RNILineAOCRel
	
