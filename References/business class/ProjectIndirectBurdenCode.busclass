ProjectIndirectBurdenCode is a BusinessClass
    owned by Projects
    prefix is PJIBD

	Ontology
		symbolic key is ProjectIndirectBurdenCode
        		
	Persistent Fields
		Description
		Rate					is Percent size 7.3
		Active
		FinanceCodeBlock		is a FinanceCodeBlockNoProject
			default label is "PostToFinanceStructure"
		OffsetCodeBlockOption	is a RevenueCodeBlockOption
			default label is "OffsetFinanceStructureOption"
		OffsetCodeBlock			is a FinanceCodeBlockFullBurdenOffset
			default label is "FullOffsetAccountFinanceStructure"
		OffsetPartialCodeBlock	is a FinanceCodeBlockBurdenOffset
			default label is "PartialOffsetAccountFinanceStructure"

	Derived Fields

			
	Conditions

	Relations

	Sets
		ByDateDescendingActive
			Sort Order
				FinanceEnterpriseGroup
				ProjectIndirectBurdenCode
				EffectiveDate descending
			Instance Selection
				where (Active)

	Attach Rules
		constraint (Active)
			"ProjectIndirectBurdenCode<ProjectIndirectBurdenCode>IsInactive"

	Field Rules
		Description 
			required
			default to ProjectIndirectBurdenCode

		Rate
			required

		OffsetCodeBlockOption
			required
			
		OffsetCodeBlock
			if (OffsetCodeBlockOption.FullStructure)
				required
					"FullOffsetAccountFinanceStructureRequired"
			else
				cannot be entered
					"CannotEnterFullOffsetFinanceStructureUnlessOffsetFinanceStructureOptionIs_Full"

		OffsetPartialCodeBlock
			if (OffsetCodeBlockOption.PartialStructure)
				required
					"PartialOffsetAccountFinanceStructureRequired"
			else
				cannot be entered
					"CannotEnterPartialOffsetFinanceStructureUnlessOffsetFinanceStructureOptionIs_Partial"

	Actions 
		Create is an Action
										
		Update is an Action 
								
		Delete is an Action	
		
        CreateNewVersion is an Instance Action
        	Parameters
				NewEffectiveDate	is Date
					default label is "EffectiveDate"
				NewRate			is Percent size 7.3
			Parameter Rules
				NewEffectiveDate
					required
				NewRate
					initial value is Rate
					required
			Action Rules
				invoke Create
					fill in fields from this instance
					invoked.EffectiveDate	= NewEffectiveDate
					invoked.Rate			= NewRate
