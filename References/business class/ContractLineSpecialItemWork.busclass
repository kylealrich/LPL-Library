ContractLineSpecialItemWork is a BusinessClass   
    owned by po
    prefix is CLSIW

    Ontology
    	symbolic key is ContractLineSpecialItemWork

    Persistent Fields
		Vendor
    	ContractLine
    	LineNumber				is Numeric 6
    	ItemNumber				is like Item
		ItemDescription			is Alpha 30
		ItemDescription2		is Alpha 30
		ItemDescription3        is Alpha 350
		VendorItem				is Alpha 32
    	VendorItemDescription	is Alpha 30
		UOM 					is a UnitOfMeasure
		Manufacturer
		ManufacturerNumber
    	NewItemUpdated			is Boolean
		NewItemMaster			is an Item
    	UseUomForVendorBuy      is Boolean
    	UseUomForVendorPrice    is Boolean
    	GenericName 				
		MajorPurchasingClass	
		MinorPurchasingClass
			context of MajorPurchasingClass			
		MajorInventoryClass
		MinorInventoryClass
			context of MajorInventoryClass
		UNSPSCCode				
		CommodityCode	
		GlobalTradeItemNumber	is an ItemGTIN
		Upc 					is an ItemUPC
		Sku 					is an ItemSKU
		HibccUpn 				is a HIBCCItemUPN
		UccEanUpn 				is a UCCEANItemUPN
		NationalDrugCode		
		DefaultedFromGtinOrVendorItem is Boolean		

	Local Fields
		LocalExistingItemCheck	is like Item
		LocalBuyUom             is a UnitOfMeasure
		LocalItem               is like Item
		LocalContract           is like Contract
		LocalContractLine       is like ContractLine
		LocalItemMaster         is like Item
		LocalContractGroup      is like ContractGroup
	
	Derived Fields
		ItemCreationInProgressMessage	is a MessageField
			restricted
			"ItemCreationInProgress"
		ItemCreationCompletedMessage	is a MessageField
			restricted
			"ItemCreationCompleted"
		ReadyToCreateItemMessage		is a MessageField
			restricted
			"ReadyToCreateItem"
		NotReadyMessage                 is a MessageField
			restricted
			"NotReadyToRun"
		ReadyToUpdateLineMessage        is a MessageField
			restricted
			"ReadyToUpdateContractLine"
		ItemEnteredMessage				is a MessageField
			restricted
			"ItemEntered"
		ReadyToEnterItemMessage			is a MessageField
			restricted
			"ItemReadyToBeEnteredIfExistingItem"
		SequentialMessage               is a MessageField
			restricted
			"ItemNumberSequentiallyAssigned"	
		DefaultedMessage                is a MessageField
			restricted
			"ItemNumberDefaultedFromVendorItemOrGtin"
		ProcessStatusMessage            is a MessageField
			"*ProcessStatus*"
		CreateVendorItemOnlyMessage     is a MessageField
			restricted
			"RunCreateItemMasterToCreateVendorItem"

		Status							is a ConditionalField
			type is Alpha 75
			if (NewItemUpdated)
				ItemCreationCompletedMessage
			else
			if (CreateVendorItemOnly)
				CreateVendorItemOnlyMessage
			else
			if (CanCreateItemMaster)
				ReadyToCreateItemMessage
			else
				NotReadyMessage
				
		EnterItemNumberStatus is a DerivedField
			type is like Description
			if (DefaultedFromGtinOrVendorItem)
				return DefaultedMessage
			else
			if (NewItemMasterEntered)
				return ItemEnteredMessage
			else
				return ReadyToEnterItemMessage
				
		UpdateContractLineStatus is a DerivedField
			type is like Description
			if  (NewItemUpdated
			or  (NoProcurementProcessing 
			and  NewItemMasterEntered))
				return ReadyToUpdateLineMessage
			else
				return NotReadyMessage
					
		ContractTemplateText is a MessageField
			"ContractIsATemplate"
			
		SpecialItemWorkCount is a ComputeField
			type is Numeric 3
			restricted
			(instance count of DisplayItemContractLineSpecialItemWorkRel)
				
	Sets
		ByMfgInfo
			Sort Order
				ContractGroup
				Manufacturer.ManufacturerCode
				Manufacturer.ManufacturerDivision
				ManufacturerNumber
				Vendor
				VendorItem
				Contract
				ContractLine
				ContractLineSpecialItemWork

		ByContractLine
			Sort Order
				ContractGroup
				Contract
				ContractLine
				Actor
				ContractLineSpecialItemWork
		
		ByVenItemMfgInfoUOM
			Sort Order
				ContractGroup
				Contract
				VendorItem
				Manufacturer.ManufacturerCode
				Manufacturer.ManufacturerDivision
				ManufacturerNumber
				UOM
				ContractLine
				ContractLineSpecialItemWork
				
		
	Relations
		
        ItemGroupRel
        	one-to-one relation to ItemGroup
        	Field Mapping uses symbolic key
        		related.ItemGroup                                   = ContractGroup
        
        ItemMasterRel
        	one-to-one relation to Item
        	Field Mapping uses symbolic key
        		related.ItemGroup                                   = LocalContractGroup
        		related.Item                                        = LocalItemMaster
        
        ItemNumberRel
        	one-to-one relation to Item
        	Field Mapping uses symbolic key
        		related.ItemGroup                                   = ContractGroup
        		related.Item                                        = ItemNumber
        
        LocalItemRel
        	one-to-one relation to Item
        	Field Mapping uses symbolic key
        		related.ItemGroup                                   = ContractGroup
        		related.Item                                        = LocalItem
        
        GetVendorItemSetRel
        	one-to-many relation to VendorItem
        	Field Mapping uses Set3
        		related.ProcurementGroup  	= ContractGroup
        		related.Vendor 				= Contract.Vendor
        		related.VendorItem			= VendorItem

        ExistingVendorItemRel
        	one-to-one relation to VendorItem
        	Field Mapping uses Set3
        		related.ProcurementGroup  	= ContractGroup
        		related.Vendor 				= Contract.Vendor
        		related.VendorItem			= VendorItem
        		related.Item    			= LocalExistingItemCheck

		POVendorItemForSameMfgInfoRel
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup				= ContractGroup
				related.Vendor							= Contract.Vendor
				related.Manufacturer					= Manufacturer
				related.ManufacturerNumber				= ManufacturerNumber
			Instance Selection
				where (related.VendorItem != VendorItem
				and    related.VendorItem.Active)
		
		UnselectContractLineSpecialItemWorkRel
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses symbolic key
				related.Actor			= actor
		
		DisplayItemContractLineSpecialItemWorkRel
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses symbolic key
				related.Actor			= actor

		DisplayItemContractLineSpecialItemWorkItemRel
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses symbolic key
				related.Actor			= actor
			Instance Selection
				where (related.NewItemMasterEntered)	
		
		SameVenItemMfgInfoUOMRel
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses ByVenItemMfgInfoUOM
				related.ContractGroup 										= ContractGroup
				related.Contract											= Contract
				related.VendorItem											= VendorItem
				related.Manufacturer.ManufacturerCode						= Manufacturer.ManufacturerCode
				related.Manufacturer.ManufacturerDivision					= Manufacturer.ManufacturerDivision
				related.ManufacturerNumber									= ManufacturerNumber
				related.UOM													= UOM
			Instance Selection
				where (related.UniqueID	!= UniqueID)

		SameVenItemMfgInfoDiffUOMRel
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses ByVenItemMfgInfoUOM
				related.ContractGroup 										= ContractGroup
				related.Contract											= Contract
				related.VendorItem											= VendorItem
			Instance Selection
				where (related.UOM	!= UOM)
		
		ValidBuyUOMRel
			one-to-many relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup                    = ContractGroup
				related.Item                         = LocalExistingItemCheck
				related.UnitOfMeasure                = UOM
			Instance Selection
				where (related.OKForBuying)
		
		ValidUOMRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup                    = ContractGroup
				related.Item                         = LocalExistingItemCheck
				related.UnitOfMeasure                = UOM
		
		ItemUOMRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup                    = ContractGroup
				related.Item                         = LocalExistingItemCheck
				related.UnitOfMeasure                = LocalBuyUom
		
		IcitemgtinRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup	= ContractGroup
				related.ItemGTIN	= GlobalTradeItemNumber
			Instance Selection
				where (GlobalTradeItemNumber entered)

        SameGTINRel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (GlobalTradeItemNumber entered
        		and    related.GlobalTradeItemNumber  = GlobalTradeItemNumber
        		and    related.UniqueID              != UniqueID)
        		
        SameGTINDiffUOMRel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (GlobalTradeItemNumber entered
        		and    related.GlobalTradeItemNumber  = GlobalTradeItemNumber
        		and    related.UniqueID              != UniqueID
        		and    related.ContractLine.UOM      != ContractLine.UOM)
        
        SameNdcRel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (NationalDrugCode entered
        		and    related.NationalDrugCode  = NationalDrugCode
        		and    related.UniqueID              != UniqueID)
        		
        SameUpcRel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (Upc entered
        		and    related.Upc  = Upc
        		and    related.UniqueID              != UniqueID)
        		
        SameUpn1Rel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (HibccUpn entered
        		and    related.HibccUpn  = HibccUpn
        		and    related.UniqueID              != UniqueID)
        		
        SameUpn2Rel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (UccEanUpn entered
        		and    related.UccEanUpn  = UccEanUpn
        		and    related.UniqueID              != UniqueID)
        		
        SameSkuRel
        	one-to-many relation to ContractLineSpecialItemWork
        	Field Mapping uses symbolic key
        		related.Actor      = Actor
        	Instance Selection
        		where (Sku entered
        		and    related.Sku  = Sku
        		and    related.UniqueID              != UniqueID)
        		
        SkuUomRel
        	one-to-many relation to ItemSKU
        	Field Mapping uses Set6
        		related.ItemGroup     = ContractGroup
        		related.Item          = NewItemMaster
        		related.UnitOfMeasure.UnitOfMeasure = UOM
        		
        UpcUomRel
        	one-to-many relation to ItemUPC
        	Field Mapping uses Set6
        		related.ItemGroup     = ContractGroup
        		related.Item          = NewItemMaster
        		related.UnitOfMeasure.UnitOfMeasure = UOM
        		
        Upn1UomRel
        	one-to-many relation to HIBCCItemUPN
        	Field Mapping uses Set5
        		related.ItemGroup  			= ContractGroup
        		related.Item	   			= NewItemMaster
        		related.UnitOfMeasure.UnitOfMeasure       = UOM
        		
        Upn2UomRel
        	one-to-many relation to UCCEANItemUPN
        	Field Mapping uses Set5
        		related.ItemGroup  			= ContractGroup
        		related.Item	   			= NewItemMaster
        		related.UnitOfMeasure.UnitOfMeasure       = UOM
        		
        ContractItemRel
			one-to-many relation to ContractLine
			Field Mapping uses ByItemContractLine
				related.ContractGroup    = ContractGroup
				related.ItemNumber   	 = LocalItem
			Instance Selection
				where (!related.ContractLineState.Closed
				and     related.ItemType.Special
				and     related.CanUseForCostDefaulting)
				
		ContractItemToWorkRel
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses ByContractLine
				related.ContractGroup     = ContractGroup   
				related.Contract          = LocalContract
				related.ContractLine      = LocalContractLine

	Conditions
		AutomaticItemNumbering
			restricted
			when (ItemGroupRel.AutomaticItemNumbering)
		
		ManualItemNumbering
			restricted
			when (!ItemGroupRel.AutomaticItemNumbering)
			
		NumericItemNumbering
			restricted
			when (ItemGroupRel.NumericItemNumber)
		
		CreateVendorItemOnly
			restricted
			when (!PoitemvenExistsForVendorItem
			and    ItemNumber entered)
		PoitemvenExistsForVendorItem
			restricted
			when (GetVendorItemSetRel exists)
		SameVenItemMfgInfoUOMExists
			restricted
			when (SameVenItemMfgInfoUOMRel exists)
		GTINEntered
			restricted
			when (GlobalTradeItemNumber entered)
		GTINAlreadyExists
			restricted
			when (ContractLine.GlobalTradeItemNumberExists) 
		DuplicateGTIN
			restricted
			when  ((SameGTINRel exists)
			and   ((first SameGTINRel.Contract     < Contract)
			or     (first SameGTINRel.Contract     = Contract
			and     first SameGTINRel.ContractLine < ContractLine)))
		NdcEntered
			restricted
			when (NationalDrugCode entered)
		NdcAlreadyExists
			restricted
			when (ContractLine.NdcNbrExistsOnItemRel exists) 
		DuplicateNdc
			restricted
			when  ((SameNdcRel exists)
			and   ((first SameNdcRel.Contract     < Contract)
			or     (first SameNdcRel.Contract     = Contract
			and     first SameNdcRel.ContractLine < ContractLine)))
		UpcEntered
			restricted
			when (Upc entered)
		UpcAlreadyExists
			restricted
			when (ContractLine.UPCExistsOnItemRel exists) 
		DuplicateUpc
			restricted
			when  ((SameUpcRel exists)
			and   ((first SameUpcRel.Contract     < Contract)
			or     (first SameUpcRel.Contract     = Contract
			and     first SameUpcRel.ContractLine < ContractLine)))
		Upn1Entered
			restricted
			when (HibccUpn entered)
		Upn1AlreadyExists
			restricted
			when (ContractLine.HibccUpnExistsOnItemRel exists) 
		DuplicateUpn1
			restricted
			when  ((SameUpn1Rel exists)
			and   ((first SameUpn1Rel.Contract     < Contract)
			or     (first SameUpn1Rel.Contract     = Contract
			and     first SameUpn1Rel.ContractLine < ContractLine)))
		Upn2Entered
			restricted
			when (UccEanUpn entered)
		Upn2AlreadyExists
			restricted
			when (ContractLine.UccEanUpnExistsOnItemRel exists) 
		DuplicateUpn2
			restricted
			when  ((SameUpn2Rel exists)
			and   ((first SameUpn2Rel.Contract     < Contract)
			or     (first SameUpn2Rel.Contract     = Contract
			and     first SameUpn2Rel.ContractLine < ContractLine)))
		SkuEntered
			restricted
			when (Sku entered)
		SkuAlreadyExists
			restricted
			when (ContractLine.SkuExistsOnItemRel exists) 
		DuplicateSku
			restricted
			when  ((SameSkuRel exists)
			and   ((first SameSkuRel.Contract     < Contract)
			or     (first SameSkuRel.Contract     = Contract
			and     first SameSkuRel.ContractLine < ContractLine)))
		ExistingVendorItemFound
			restricted
			when (ExistingVendorItemRel exists)
		CommodityCodeEntered
			restricted
			when (CommodityCode entered)
		NoProcurementProcessing
			restricted
			when  ( ContractLine.HasBeenActivated = false
			and     NewItemMasterExists
			and     PoitemvenExistsForVendorItem
			and     GTINExistsIfEntered
			and    !NewItemUpdated)
		NewItemMasterEntered
			restricted
			when (NewItemMaster entered)
		NewItemMasterExists
			restricted
			when (ItemNumberRel exists)
		CanEnterExistingItem
			restricted
			when (!NewItemUpdated
			and   !DefaultedFromGtinOrVendorItem)
		CanUpdateContractLine
			restricted
			when (NoProcurementProcessing
			or    NewItemUpdated)
		CanCreateItemMaster
			restricted
			when  (!NewItemUpdated)
		GTINExistsIfEntered
			restricted
			when ((GlobalTradeItemNumber entered
			and  ContractLine.GlobalTradeItemNumber exists)
			or  (GlobalTradeItemNumberNotEntered))
		GlobalTradeItemNumberNotEntered
			restricted
			when (!GlobalTradeItemNumber entered)
		
	Field Rules
		Actor
			default to actor

		ItemDescription
			required
				"ItemDescriptionIsRequired"
				
		UOM
					
			constraint (!SameVenItemMfgInfoUOMExists)
				"VendorItem,ManufacturerInformation,UOMCombinationCannotBeDuplicatedOnAContract"

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Purge is a Purge Action
			restricted

		UnselectSpecialItemsForItemCreation is an Instance Action
			valid when (!NewItemUpdated)
			
			Action Rules
			
				invoke UnselectSpecialItemForItemCreation
					invoked.Actor          = actor
		
		UnselectSpecialItemForItemCreation is a Set Action
			run in foreground
			restricted
			Parameters
				Actor
			
			Instance Selection
				where ( Actor			= Actor)
						
			Action Rules
				Instance Rules
				
					for each UnselectContractLineSpecialItemWorkRel
						invoke UnselectSpecialItemForItemCreation each.ContractLine
						invoke Purge each

		EnterExistingItemNumber is an Instance Action
			valid when (CanEnterExistingItem)
			Parameters
				PrmContractGroup                is a ContractGroup
				PrmExistingItem                 is an Item
				PrmItemDescription		 		is Alpha 30
				
			Parameter Rules
			
				PrmContractGroup
				
					initial value is ContractGroup
					
				PrmExistingItem
					
					initial value is ItemNumber 
					
					constraint (PrmExistingItem exists)
						"MustEnterAnExistingItem"
				
				PrmItemDescription
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ItemDescription
					
			Action Rules
			
				if (Sku entered)
					constraint (!SkuUomRel exists)
						"CannotCreateSKUOf<Sku>;SKU<first SkuUomRel.ItemSKU>AlreadyExistsForItemWithUomOf<UOM>"
						
				if (Upc entered)
					constraint (!UpcUomRel exists)
						"CannotCreateUPCOf<Upc>;UPC<first UpcUomRel.ItemUPC>AlreadyExistsForItemWithUomOf<UOM>"
						
				if (HibccUpn entered)
					constraint (!Upn1UomRel exists)
						"CannotCreateHibccUpnOf<HibccUpn>;HibccUpn<first Upn1UomRel.HIBCCItemUPN>AlreadyExistsForItemWithUomOf<UOM>"
						
				if (UccEanUpn entered)
					constraint (!Upn2UomRel exists)
						"CannotCreateUccEanUpnOf<UccEanUpn>;UccEanUpn<first Upn2UomRel.UCCEANItemUPN>AlreadyExistsForItemWithUomOf<UOM>"
				
				invoke EnterItemNumberSet ContractLineSpecialItemWork
					invoked.PrmActor            = actor
					invoked.PrmContractGroup    = PrmContractGroup
					invoked.PrmExistingItem     = PrmExistingItem
		
		EnterItemNumberSet is a Set Action
			run in foreground
			restricted
			
			Parameters
				PrmContractGroup                is a ContractGroup
				PrmActor                        is an Actor
				PrmExistingItem                 is an Item
				
			Parameter Rules
					
			Instance Selection
				where (ContractGroup    = PrmContractGroup
				and    Actor			= PrmActor)
			
			Action Rules
				Instance Rules
					ItemNumber                       = PrmExistingItem
					ItemDescription 				 = PrmExistingItem.Description
					ItemDescription2 				 = PrmExistingItem.Description2
					ItemDescription3                 = PrmExistingItem.Description3
					GenericName		                 = PrmExistingItem.GenericName		
					MajorPurchasingClass             = PrmExistingItem.MajorPurchasingClass
					MinorPurchasingClass             = PrmExistingItem.MinorPurchasingClass		
					MajorInventoryClass              = PrmExistingItem.MajorInventoryClass
					MajorInventoryClass              = PrmExistingItem.MinorInventoryClass
					UNSPSCCode						 = PrmExistingItem.UNSPSCCode
					CommodityCode    			     = PrmExistingItem.CommodityCode
						
		CreateItemMaster is an Instance Action
			valid when (CanCreateItemMaster)

			Parameters
				PrmExistingItem                 is an Item
				PrmExistingItemDescription      is Alpha 30
				PrmExistingItemDescription2     is Alpha 30
				PrmExistingItemDescription3     is Alpha 350
				PrmReference1                   is a Reference
				PrmReference2                   is a Reference
				PrmContractLineItem             is like Item
				PrmNewItem                      is like Item
				PrmItemOnContract               is like Item
				PrmStockUom						is a UnitOfMeasure
				PrmContractUom                  is a UnitOfMeasure
				PrmUseUomForVendorBuy           is Boolean
				PrmUseUomForVendorPrice         is Boolean
				PrmBuyUom					    is a UnitOfMeasure  
				PrmBuyUomConversion				is a UOMConversion
				PrmBuyFlag                      is Numeric size 1
					States
						Default              	value is 1
						Valid	            	value is 2
				PrmCommodityCode                is a CommodityCode
				PrmGeneric 				        is a GenericName
				PrmMajorPurchasingClass	        is a MajorPurchasingClass
				PrmMinorPurchasingClass			is like MinorPurchasingClass
				PrmMajorInventoryClass          is a MajorInventoryClass
				PrmMinorInventoryClass          is like MinorInventoryClass
				PrmUnspscCode					is an UNSPSCCode
				PrmUserField1 					is AlphaUpper size 30
				PrmUserField2 					is AlphaUpper size 10
				PrmUserField3 					is AlphaUpper size 10
				PrmUserField4 					is AlphaUpper size 10
				PrmUserField5 					is AlphaUpper size 10
				PrmUserFieldN1 					is an UnitCost			
				PrmUserFieldN2 					is an UnitCost			
				PrmUserFieldN3 					is an UnitCost
				PrmExistingItemMasterExists          is Boolean
				
			Parameter Rules
					
				PrmExistingItem
					initial value is ItemNumber
				
				PrmExistingItemDescription
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ItemDescription
					
					required
						"ItemDescriptionIsRequired"
					
				PrmExistingItemDescription2
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ItemDescription2
				
				PrmExistingItemDescription3
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ItemDescription3
				
				PrmNewItem
					if (ManualItemNumbering
					and ItemNumber !entered)
						required
						
					if (NumericItemNumbering
					and ManualItemNumbering)
						constraint (PrmNewItem is numeric)
							"ManualItemMustBeNumeric"
				
					LocalItem = PrmNewItem
					if (PrmNewItem entered)
						constraint (LocalItemRel !exists)
							"ManualItemNumberAlreadyExistsAsAnItem;MustEnterADifferentNewItemNumberOrEnterItemNumberInEnterExistingItemNumberAction"
				
						if (ContractItemRel exists)
							for each ContractItemRel
								LocalContract 		= each.Contract
								LocalContractLine 	= each.ContractLine
								
								constraint (ContractItemToWorkRel exists)
									"ManualItemNumberAlreadyExistsAsASpecialItemOnAContractThatWillNotBeUpdatedByThisProcess;MustChangeManualItemOrItemOnContract"	
								
				PrmItemOnContract
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ItemNumber
				
				PrmStockUom
					
										
					if (!PrmExistingItem exists)
						required
							"StockUnitOfMeasureIsRequired"
				
				PrmContractUom
					initial value is first DisplayItemContractLineSpecialItemWorkRel.UOM
					
				PrmBuyUom
					constraint (PrmBuyUom != PrmStockUom)
						"BuyAndStockUomAreTheSameValue;IfStockUomWillBeTheDefaultBuyUomItIsNotNecessaryToEnterItHere"
						
					if  (PrmBuyUomConversion entered
					or   PrmBuyFlag entered)
						required
							"MustEnterUnitOfMeasureIfConversionFactorOrFlagAreEntered"
						
				PrmBuyUomConversion
				
					if  (PrmBuyUom entered)
						required
							"MustEnterUnitOfMeasureConversionFactorIfUnitOfMeasureIsEntered"
							
						if (!PrmExistingItem exists)
							constraint (PrmBuyUomConversion != 1)
								"OnlyStockUnitOfMeasureCanHaveAConversionFactorOf1ForNewItemMasterItems"
				
				PrmBuyFlag
					
					if  (PrmBuyUom entered
					and !PrmExistingItem exists)
						default to "2"

				PrmCommodityCode                
					initial value is first DisplayItemContractLineSpecialItemWorkRel.CommodityCode
				
				PrmGeneric
					initial value is first DisplayItemContractLineSpecialItemWorkRel.GenericName 				        
				PrmMajorPurchasingClass	
					initial value is first DisplayItemContractLineSpecialItemWorkRel.MajorPurchasingClass     
				PrmMinorPurchasingClass
					initial value is first DisplayItemContractLineSpecialItemWorkRel.MinorPurchasingClass		
				PrmMajorInventoryClass
					initial value is first DisplayItemContractLineSpecialItemWorkRel.MajorInventoryClass
				PrmMinorInventoryClass
					initial value is first DisplayItemContractLineSpecialItemWorkRel.MinorInventoryClass
				PrmUnspscCode	
					initial value is first DisplayItemContractLineSpecialItemWorkRel.UNSPSCCode
				PrmUserField1 
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserField1	
				PrmUserField2 
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserField2	
				PrmUserField3 
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserField3					
				PrmUserField4 	
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserField4 				
				PrmUserField5 
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserField5	
				PrmUserFieldN1 
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserFieldN1	
				PrmUserFieldN2 	
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserFieldN2 					
				PrmUserFieldN3 	
					initial value is first DisplayItemContractLineSpecialItemWorkRel.ContractLine.UserFieldN3 	
					
				PrmExistingItemMasterExists
					initial value is first DisplayItemContractLineSpecialItemWorkRel.NewItemMasterExists

			Action Rules

				if (SpecialItemWorkCount > 1)
					for each DisplayItemContractLineSpecialItemWorkRel
						if (each.GlobalTradeItemNumber entered)
							constraint (!each.SameGTINDiffUOMRel exists)
								"LinesExistWithTheSameNewGTINButDifferentUnitsOfMeasure;GTINCanOnlyHaveOneUnitOfMeasure"
								
						if (PrmUseUomForVendorBuy = true)
							constraint (!each.SameVenItemMfgInfoDiffUOMRel exists)
								"CannotSelectUseContractLineUnitOfMeasureForVendorItemBuy;MultipleUnitsOfMeasureExistForTheSameVendorItem"
				
						constraint (!each.POVendorItemForSameMfgInfoRel exists)
							"VendorItem<each.VendorItem>DoesNotMatchVendorItem<first POVendorItemForSameMfgInfoRel.VendorItem>AlreadyAssociatedWithManufacturerInformation"
				
				for each DisplayItemContractLineSpecialItemWorkRel 
					LocalExistingItemCheck = PrmExistingItem
				
					if  (PrmExistingItem entered
					and  each.ValidUOMRel exists)
						constraint (each.ValidBuyUOMRel exists)	
							"UnitOfMeasure<each.UOM>IsAValidUnitOfMeasureForTheItemButNotAValidBuyUnitOfMeasure"
							
					if (Sku entered)
						constraint (!each.ContractLine.SkuExistsOnItemRel exists)
							"SKUNumber<each.ContractLine.Sku>IsAlreadyAssignedToItem<each.ContractLine.SkuExistsOnItemRel.Item>;Contract<each.Contract>,Line<each.ContractLine>"
				
					if (NationalDrugCode entered)
						constraint (!each.ContractLine.NdcNbrExistsOnItemRel exists)
							"NDCNumber<each.ContractLine.NdcNbr>IsAlreadyAssignedToItem<each.ContractLine.NdcNbrExistsOnItemRel.Item>;Contract<each.Contract>,Line<each.ContractLine>"
					
					if (HibccUpn entered)
						constraint (!each.ContractLine.HibccUpnExistsOnItemRel exists)
							"HibccUPNNumber<each.ContractLine.HibccUpn>IsAlreadyAssignedToItem<each.ContractLine.HibccUpnExistsOnItemRel.Item>;Contract<each.Contract>,Line<each.ContractLine>"
					
					if (UccEanUpn entered)
						constraint (!each.ContractLine.UccEanUpnExistsOnItemRel exists)
							"UccEanUpnNumber<each.ContractLine.DerivedUccEanUpn>IsAlreadyAssignedToItem<each.ContractLine.UccEanUpnExistsOnItemRel.Item>;Contract<each.Contract>,Line<each.ContractLine>"
					
					if (Upc entered)
						constraint (!each.ContractLine.UPCExistsOnItemRel exists)
							"UPCNumber<each.ContractLine.Upc>IsAlreadyAssignedToItem<each.ContractLine.UPCExistsOnItemRel.Item>;Contract<each.Contract>,Line<each.ContractLine>"
						
				if (!PrmExistingItem entered)
					for each DisplayItemContractLineSpecialItemWorkRel 
						if  (each.UOM != PrmStockUom
						and  each.UOM != PrmBuyUom)
							confirmation required
								"UOM<each.UOM>ForContract:<Contract>;Line:<ContractLine>IsNotDefinedAsEitherAStockOrBuyUOM;YouMustUseTheMaintainItemInformationLinkAfterCreatingTheItemToAddTheUOM;DoYouWishToContinue?"
				
				if  (PrmExistingItem entered)
					for each DisplayItemContractLineSpecialItemWorkRel
						if  (each.ValidBuyUOMRel !exists
						and  each.UOM != PrmBuyUom)
							confirmation required
								"UOM<each.UOM>ForContract:<Contract>;Line:<ContractLine>IsNotDefinedAsEitherAStockOrBuyUOM;YouMustUseTheMaintainItemInformationLinkAfterCreatingTheItemToAddTheUOM;DoYouWishToContinue?"
						
				if (PrmMinorPurchasingClass entered)
					constraint (PrmMajorPurchasingClass entered)
						"MajorPurchasingClassRequiredWhenMinorPurchasingClassEntered"
				if (PrmMinorInventoryClass entered)
					constraint (PrmMinorInventoryClass entered)
						"MajorInventoryClassRequiredWhenMinorInventoryClassEntered"
						
			Exit Rules
				invoke CreateItemForSpecialItemsUpdate ContractLineSpecialItemWork
					invoked.PrmContractGroup       = ContractGroup
					invoked.PrmExistingItem		   = PrmExistingItem
					invoked.PrmNewItem             = PrmNewItem
					invoked.PrmExistingItemDescription  = PrmExistingItemDescription
					invoked.PrmExistingItemDescription2 = PrmExistingItemDescription2
					invoked.PrmExistingItemDescription3 = PrmExistingItemDescription3
					invoked.PrmReference1          = PrmReference1
					invoked.PrmReference2          = PrmReference2
					invoked.PrmStockUom            = PrmStockUom
					invoked.PrmUseUomForVendorBuy  = PrmUseUomForVendorBuy
		            invoked.PrmUseUomForVendorPrice =PrmUseUomForVendorPrice
					invoked.PrmBuyUom				= PrmBuyUom  
					invoked.PrmBuyUomConversion		= PrmBuyUomConversion		
					invoked.PrmBuyFlag              = PrmBuyFlag        
					invoked.PrmCommodityCode       = PrmCommodityCode
					invoked.PrmGeneric 			   = PrmGeneric
					invoked.PrmMajorPurchasingClass = PrmMajorPurchasingClass	        
					invoked.PrmMinorPurchasingClass = PrmMinorPurchasingClass			
					invoked.PrmMajorInventoryClass  = PrmMajorInventoryClass
					invoked.PrmMinorInventoryClass  = PrmMinorInventoryClass
					invoked.PrmUnspscCode          = PrmUnspscCode
					invoked.PrmUserField1          = PrmUserField1 			
					invoked.PrmUserField2          = PrmUserField2 				
					invoked.PrmUserField3          = PrmUserField3 	
					invoked.PrmUserField4          = PrmUserField4 	
					invoked.PrmUserField5          = PrmUserField5 	
					invoked.PrmUserFieldN1 		   = PrmUserFieldN1				
					invoked.PrmUserFieldN2 		   = PrmUserFieldN2	
					invoked.PrmUserFieldN3 		   = PrmUserFieldN3	 
					
		CreateItemForSpecialItemsUpdate is a Set Action
			restricted
			run in foreground

			Parameters
				PrmContractGroup        is a ContractGroup
				PrmExistingItem			is an Item
				PrmNewItem              is like Item
				PrmExistingItemDescription   is Alpha 30
				PrmExistingItemDescription2  is Alpha 30
				PrmExistingItemDescription3  is Alpha 350
				PrmReference1           is a Reference
				PrmReference2           is a Reference
				PrmStockUom				is an UnitOfMeasure
				PrmUseUomForVendorBuy   is Boolean
				PrmUseUomForVendorPrice is Boolean
				PrmBuyUom				is a UnitOfMeasure  
				PrmBuyUomConversion		is a UOMConversion
				PrmBuyFlag              is Numeric size 1
					States
						Default              	value is 1
						Valid	            	value is 2
				PrmCommodityCode        is a CommodityCode
				PrmGeneric 				is a GenericName
				PrmMajorPurchasingClass	is a MajorPurchasingClass
				PrmMinorPurchasingClass	is like MinorPurchasingClass
				PrmMajorInventoryClass  is a MajorInventoryClass
				PrmMinorInventoryClass  is like MinorInventoryClass
				PrmUnspscCode			is a UNSPSCCode
				PrmUserField1 			is AlphaUpper size 30
				PrmUserField2 			is AlphaUpper size 10
				PrmUserField3 			is AlphaUpper size 10
				PrmUserField4 			is AlphaUpper size 10
				PrmUserField5 			is AlphaUpper size 10
				PrmUserFieldN1 			is an UnitCost			
				PrmUserFieldN2 			is an UnitCost			
				PrmUserFieldN3 			is an UnitCost	

			Instance Selection
				where (ContractGroup	= PrmContractGroup
				and    Actor			= actor
				and    !NewItemUpdated)

			Sort Order
				ContractGroup
				Manufacturer
				ManufacturerNumber
				Vendor
				VendorItem
				Contract
				ContractLine

			Action Rules
				Set Rules

					Exit Rules
						LocalItemMaster 	= NewItemMaster
						LocalContractGroup 	= ContractGroup
				
				ManufacturerNumber Set Rules
					Entrance Rules


						if (!NewItemMasterExists)
							invoke Create Item
								invoked.FromExternal            = true
								invoked.ItemGroup				= PrmContractGroup
								invoked.Item                    = PrmNewItem
								invoked.Description				= PrmExistingItemDescription
								invoked.Description2			= PrmExistingItemDescription2
								invoked.Description3            = PrmExistingItemDescription3
								invoked.Reference1              = PrmReference1
								invoked.Reference2              = PrmReference2
								invoked.StockUOM				= PrmStockUom
								invoked.Active                  = true
								invoked.GenericName				= PrmGeneric
								invoked.MajorPurchasingClass	= PrmMajorPurchasingClass
								invoked.MinorPurchasingClass	= PrmMinorPurchasingClass
								invoked.CommodityCode			= PrmCommodityCode
								invoked.MajorInventoryClass		= PrmMajorInventoryClass
								invoked.MinorInventoryClass		= PrmMinorInventoryClass
								invoked.Manufacturer			= Manufacturer
								invoked.ManufacturerNumber		= ManufacturerNumber
								invoked.UNSPSCCode				= PrmUnspscCode
								invoked.UserField1				= PrmUserField1
								invoked.UserField2				= PrmUserField2
								invoked.UserField3				= PrmUserField3
								invoked.UserField4				= PrmUserField4
								invoked.UserField5				= PrmUserField5
								invoked.UserFieldN1				= PrmUserFieldN1
								invoked.UserFieldN2				= PrmUserFieldN2
								invoked.UserFieldN3				= PrmUserFieldN3

											
				Vendor Set Rules
					Entrance Rules

						LocalExistingItemCheck = PrmExistingItem
						if (!ExistingVendorItemFound
						and  ContractLine.CanUseForCostDefaulting)
							invoke Create VendorItem
								invoked.ProcurementGroup				= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.Vendor							= Contract.Vendor
								invoked.VendorItem						= VendorItem
								invoked.VendorItemDescription			= VendorItemDescription
								if (PrmUseUomForVendorBuy = true)
									invoked.VendorBuyUOM.UnitOfMeasure	= UOM
								if (PrmUseUomForVendorPrice = true)
									invoked.VendorPriceUOM.UnitOfMeasure	= UOM
								invoked.Active                          = true
								invoked.Manufacturer.ManufacturerCode	= Manufacturer.ManufacturerCode
								invoked.Manufacturer.ManufacturerDivision	= Manufacturer.ManufacturerDivision
								invoked.ManufacturerNumber				= ManufacturerNumber

				Instance Rules
					LocalBuyUom = PrmBuyUom
					if (PrmBuyUom entered
					and ItemUOMRel !exists)
						invoke Create ItemUOM
							invoked.ItemGroup						= PrmContractGroup
							if (PrmExistingItem entered)
								invoked.Item						= PrmExistingItem 
							else
							if (AutomaticItemNumbering)	
								invoked.Item                        = ItemGroupRel.LastItem
							else
								invoked.Item                        = PrmNewItem
							invoked.UnitOfMeasure                   = PrmBuyUom
							invoked.UOMConversion					= PrmBuyUomConversion
							invoked.ValidForBuying					= PrmBuyFlag
					
					if (!DuplicateGTIN)
						if   (GTINEntered
						and  !GTINAlreadyExists)

							invoke Create ItemGTIN	
								
								invoked.ItemGroup							= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.ItemGTIN            		        = GlobalTradeItemNumber
								invoked.FullDescription						= PrmExistingItemDescription
								invoked.UnitOfMeasure.UnitOfMeasure			= UOM
								invoked.Manufacturer.ManufacturerCode		= Manufacturer.ManufacturerCode
								invoked.Manufacturer.ManufacturerDivision	= Manufacturer.ManufacturerDivision
								invoked.ManufacturerNumber					= ManufacturerNumber
							
					if (!DuplicateUpc)
						if   (UpcEntered
						and  !UpcAlreadyExists)
							invoke Create ItemUPC	
								
								invoked.ItemGroup				= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.ItemUPC                 = Upc
								invoked.UnitOfMeasure.UnitOfMeasure			= UOM
					
					if  (!DuplicateNdc)
						if  (NdcEntered
						and  !NdcAlreadyExists)
							invoke Create NationalDrugCode	
								
								invoked.ItemGroup				= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.NationalDrugCode        = NationalDrugCode
								invoked.UnitOfMeasure.UnitOfMeasure			= UOM
						
					if (!DuplicateUpn1)
						if   (Upn1Entered
						and  !Upn1AlreadyExists)
							invoke Create HIBCCItemUPN								
								invoked.ItemGroup				= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.HIBCCItemUPN            = HibccUpn
								invoked.UnitOfMeasure.UnitOfMeasure			= UOM
					
					if (!DuplicateUpn2)
						if   (Upn2Entered
						and  !Upn2AlreadyExists)
							invoke Create UCCEANItemUPN	
								
								invoked.ItemGroup				= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.UCCEANItemUPN           = UccEanUpn
								invoked.UnitOfMeasure.UnitOfMeasure			= UOM
					
					if (!DuplicateSku)
						if   (SkuEntered
						and  !SkuAlreadyExists)
							invoke Create ItemSKU	
								
								invoked.ItemGroup				= PrmContractGroup
								if (PrmExistingItem entered)
									invoked.Item						= PrmExistingItem 
								else
								if (AutomaticItemNumbering)	
									invoked.Item                        = ItemGroupRel.LastItem
								else
									invoked.Item                        = PrmNewItem
								invoked.ItemSKU                 = Sku
								invoked.UnitOfMeasure.UnitOfMeasure			= UOM
					
					if (!NewItemMaster entered)
						if (AutomaticItemNumbering)
							NewItemMaster   				= ItemGroupRel.LastItem
						else
							NewItemMaster                   = PrmNewItem
					if ( NewItemMaster entered
					and !NewItemMaster exists)
						ItemDescription                     = PrmExistingItemDescription
						ItemDescription2                    = PrmExistingItemDescription2
						ItemDescription3                    = PrmExistingItemDescription3
						CommodityCode                       = PrmCommodityCode
						GenericName                         = PrmGeneric
						MajorPurchasingClass                = PrmMajorPurchasingClass
						MinorPurchasingClass                = PrmMinorPurchasingClass
						MajorInventoryClass                 = PrmMajorInventoryClass
						MinorInventoryClass                 = PrmMinorInventoryClass
						UNSPSCCode                          = PrmUnspscCode
					NewItemUpdated	= true
					
					invoke ChangeSpecialItemToItem ContractLine
						invoked.PrmNewItemNumber        = NewItemMaster
						
					invoke Purge

		UpdateContractLine is an Instance Action
			valid when (CanUpdateContractLine)
			
			Action Rules
				invoke UpdateContractLines ContractLineSpecialItemWork
					invoked.PrmActor         = actor
					invoked.PrmContractGroup = ContractGroup
					
		CreateItemInformationWorksheet is an Instance Action
			restricted
			
			Action Rules
			
				invoke CreateItemWorksheet ItemInformationWorksheet
					invoked.ItemGroup           	= ContractGroup
					invoked.RunGroup                = "FromContract" + Contract + "Line" + ContractLine
					invoked.Description				= ItemDescription
					invoked.Description2			= ItemDescription2
					invoked.Description3        	= ItemDescription3
					invoked.VendorItemNew.VendorItemOccurs.VendorItemDetails[1].Vendor  				= Vendor
					invoked.VendorItemNew.VendorItemOccurs.VendorItemDetails[1].VendorItem 				= VendorItem
					invoked.VendorItemNew.VendorItemOccurs.VendorItemDetails[1].VendorItemDescription   = VendorItemDescription
					invoked.StockUOM				= UOM.UnitOfMeasure
					invoked.GenericName				= GenericName
					invoked.MajorPurchasingClass	= MajorPurchasingClass
					invoked.MinorPurchasingClass	= MinorPurchasingClass
					invoked.CommodityCode			= CommodityCode
					invoked.MajorInventoryClass		= MajorInventoryClass
					invoked.MinorInventoryClass		= MinorInventoryClass
					invoked.Manufacturer 			= Manufacturer
					invoked.ManufacturerNumber 		= ManufacturerNumber
					invoked.UNSPSCCode				= UNSPSCCode
		
				invoke Purge
				
		UpdateContractLines is a Set Action
			run in foreground
			restricted

			Parameters
				PrmActor         is an Actor
				PrmContractGroup is a ContractGroup

			Instance Selection
				where (ContractGroup	= PrmContractGroup 
				and    Actor			= PrmActor)

			Sort Order
				ContractGroup
				Manufacturer
				ManufacturerNumber
				Vendor
				VendorItem
				Contract
				ContractLine

			Action Rules
				Instance Rules
					
					LocalExistingItemCheck    = NewItemMaster
					constraint (ValidBuyUOMRel exists)
						"UOM<UOM>ForContract:<Contract>;Line:<ContractLine>IsNotDefinedAsAValidBuyUOM;YouMustUseTheMaintainItemInformationLinkToAddTheUOM;ThenRunThisAction"
					
					invoke ChangeSpecialItemToItem ContractLine
						invoked.PrmNewItemNumber        = NewItemMaster
												
					invoke Purge
					
