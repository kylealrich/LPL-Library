CustomerCompanyGroup is a BusinessClass
    owned by ar
    prefix is CCGR

    Ontology
        symbolic key is CustomerCompanyGroup

    Patterns
        implements StaticJava
        disable AuditIndex

	Context Fields
		FinanceEnterpriseGroup
		
    Persistent Fields
        Description  is Alpha size 30
        	translatable
        LastSequence is Numeric size 4

    Derived Fields
        ShortDesc is a StringField
            type is Alpha size 15
            restricted
            Description

		RepresentativeText is a StringField
			type is Text
			default label is "CustomerCompanyGroup"
			CustomerCompanyGroup " - " Description
            
	Conditions
		UsedInASecurityGroup
			when (SecurityGroupRel exists)


    Relations
        CustomerCompanyGroupMembersRel
            one-to-many relation to CustomerCompanyGroupMember
            delete cascades
            Field Mapping uses Set2
                related.CustomerCompanyGroup = CustomerCompanyGroup

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = actor.context.FinanceEnterpriseGroup
				related.DimensionGroupType			  = 2
				related.FinanceDimensionStructure	  = CustomerCompanyGroup
				
	Field Rules
		Description
			required

	Actions
		Create is an Action
		Update is an Action
		Delete is an Action

		CopyMembers is an Instance Action
			Parameters
				CopyToGroup is an CustomerCompanyGroup
				
			Parameter Rules
			
				CopyToGroup
					constraint (CopyToGroup != CustomerCompanyGroup)
						"CannotCopyACustomerCompanyGroupToItself"
						
			Action Rules
				for each CustomerCompanyGroupMember set
					invoke Create CustomerCompanyGroupMember
						resume on error
						fill in fields from each
							except invoked.CustomerCompanyGroup
						invoked.CustomerCompanyGroup = CopyToGroup
					commit transaction				

