TaxCalendarPeriod is a BusinessClass
    owned by tx
    prefix is TXB
    classic name is TXCALDTL

    Ontology
        symbolic key is TaxCalendarPeriod
            classic set name is TXBSET1
            classic name is TAX-PERIOD

    Patterns
        implements StaticJava
        disable AuditIndex
 		implements ContextualParent
 		
    Persistent Fields

        PeriodEndDate is Date
            classic name is PER-END-DATE
        Status        is Numeric size 1
            States
                Open     value is 1
                Reported value is 2
                Closed   value is 3

	Derived Fields

        PriorPeriodEndDate is a DerivedField
            type is Date
            if (TaxCalendarPeriod >  1)
                return PreviousPeriodRel.PeriodEndDate
            else
                return last PriorYearPeriodsRel.PeriodEndDate
    


	Field Rules

		PeriodEndDate		
			if (TaxCalendarPeriod > 1
			and PreviousPeriodRel exist)
				constraint (PreviousPeriodRel.PeriodEndDate < PeriodEndDate)
	                "PeriodEndDateMustBeGreaterThanThePreviousPeriodEndDate"	

			if (PeriodEndDate changed)
				constraint (Status.Open)
					"PeriodHasBeenReported;CannotChangePeriodEndDate"	
					
		Status
			default to 1


			if (Status changed)
			    constraint (!Status.Reported)
                    "TaxPeriodReportedOrClosed;CannotBeChanged"					
                    				
            if (action type.Create)        
				constraint (Status.Open)
					"CanOnlyEnterOpenStatus"					


	Relations

		PreviousPeriodRel
			one-to-one relation to TaxCalendarPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.TaxCalendar					= TaxCalendar
				related.TaxCalendarPeriod			= TaxCalendarPeriod - 1
				
		PriorYearPeriodsRel
    		one-to-many relation to TaxCalendarPeriod
   			Field Mapping uses symbolic key
		        related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
		  	Instance Selection
		        where (related.TaxCalendar.TaxEntity  = TaxCalendar.TaxEntity
		        and    related.TaxCalendar.TaxYear    < TaxCalendar.TaxYear)


	Actions

        Create is a Create Action

		T2VCreate is a Create Action
			restricted 
			default label is untranslatable 
			bypass field rules 

   		Update is an Update Action
						
        Delete is a Delete Action
			Entrance Rules
			    constraint (!Status.Reported)
                    "TaxPeriodReported;CannotBeDeleted"			

      	UpdateStatus is an Instance Action		 				
        	restricted
			Parameters
				PrmStatus	is Numeric 1
					default label is "Status"	 
		        	States
		               	Open     value is 1
		               	Reported value is 2
		               	Closed   value is 3
        	Entrance Rules
        	Action Rules
        		Status = PrmStatus   
        		
