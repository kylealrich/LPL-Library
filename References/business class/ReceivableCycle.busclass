ReceivableCycle is a BusinessClass
	owned by ar
	prefix is ACY
	classic name is ARCYCLE

	Ontology
		symbolic key is ReceivableCycle
			classic set name is ACYSET1
			classic name is CYCLE
			classic name for ReceivableCycleType is CYCLE-TYPE

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		CycleRange
			classic name for CycleRange.CycleRange1 is CYCLE-RANGE1
			classic name for CycleRange.CycleRange2 is CYCLE-RANGE2

	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)

		DunningLetterCycleType
			classic name is DUN-TYPE
			restricted
			when (ReceivableCycleType.DunningLetter)

		FinanceChargeCycleType
			classic name is FIN-TYPE
			restricted
			when (ReceivableCycleType.FinanceCharge)

		StatementCycleType
			classic name is STA-TYPE
			restricted
			when (ReceivableCycleType.BalanceForwardStatement
			or	ReceivableCycleType.OpenItemStatement)

	Relations

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company


		ReceivableCycleRange1BetweenRel
			one-to-many relation to ReceivableCycle
			Field Mapping uses symbolic key
				related.Company				= Company
				related.ReceivableCycleType	= ReceivableCycleType
			Instance Selection
				where (((related.CycleRange.CycleRange1 >= ReceivableCycle.CycleRange.CycleRange1)
				and		(related.CycleRange.CycleRange1 <= ReceivableCycle.CycleRange.CycleRange2))
				and		(related.UniqueID != UniqueID))


		ReceivableCycleRange2BetweenRel
			one-to-many relation to ReceivableCycle
			Field Mapping uses symbolic key
				related.Company				= Company
				related.ReceivableCycleType	= ReceivableCycleType
			Instance Selection
				where (((related.CycleRange.CycleRange2 >= ReceivableCycle.CycleRange.CycleRange1)
				and		(related.CycleRange.CycleRange2 <= ReceivableCycle.CycleRange.CycleRange2))
				and		(related.UniqueID != UniqueID))



	Field Rules
		CycleRange
			if (ReceivableCycleType.OpenItemStatement
			or  ReceivableCycleType.DunningLetter
			or  ReceivableCycleType.FinanceCharge)
				cannot be entered
					"AssignmentRangeAllowedOnlyForBalanceForwardStatement"

			if (ReceivableCycleType.BalanceForwardStatement)
				required
					"BeginningAndEndingRangeIsRequiredForBalanceForwardStatementType"
				constraint (CycleRange.CycleRange1 entered)
					"BeginningAssignmentLetterRequiredForBalanceForwardStatmentType"		

				constraint (CycleRange.CycleRange2 entered)
					"EndingAssignmentLetterRequiredForBalanceForwardStatmentType"		
		
				constraint 	(ReceivableCycleRange1BetweenRel 	not exists
					and		 ReceivableCycleRange2BetweenRel 	not exists)
						"BeginningAndEndingRangeCannotOverlap"

	Actions
		Create is a Create Action
			Action Rules
				constraint (ReceivableCycle.ReceivableCycleType entered)
					"MustEnterCycleType"  
		Update is an Update Action

		Delete is a Delete Action

