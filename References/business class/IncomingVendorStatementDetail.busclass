IncomingVendorStatementDetail is a BusinessClass
    owned by ap
    prefix is IVSTD

    Ontology
        symbolic key is IncomingVendorStatementDetail

    Patterns
        disable AuditIndex
        disable Auditing
        disable EffectiveDated

    Persistent Fields
        Vendor
        InvoiceNumber               is AlphaUpper size 22
        StatementDate               is Date
        InvoiceDate                 is like ExchangeDate
        PurchaseOrder               is like PurchaseOrder
        InvoiceAmount               is like CurrencyAmount
        DueDate                     is Date

    Local Fields
        LocalIncomingVendorStatement is a IncomingVendorStatement view
    Derived Fields
        DerivedInvoiceDate is a DerivedField
            type is like ExchangeDate
            return first PayablesInvoiceRel.InvoiceDate
        DerivedPurchaseOrder is a DerivedField
            type is like PurchaseOrder
            return first PayablesInvoiceRel.ExternalPurchaseOrder
        DerivedInvoiceAmount is a DerivedField   
            type is like CurrencyAmount
            return first PayablesInvoiceRel.InvoiceAmount.CurrencyAmount
        DerivedDueDate is a DerivedField         
            type is Date
            return first PayablesInvoiceRel.DueDate
            
    Field Rules
        Vendor
            required
        InvoiceNumber
            required
        StatementDate
            required

    Conditions
        RecordExists
            restricted
            when (IncomingVendorStatementDetail exists
            and IncomingVendorStatement exists)

        InvoiceExists
            when (PayablesInvoiceRel exists)

        InvoicePaid
            restricted
            when (PayablesInvoiceRel.Status = 8
            or    PayablesInvoiceRel.Status = 9)

        InvoiceHasPurchaseOrder
            restricted
            when (PayablesInvoiceRel.ExternalPurchaseOrder != blank)

        InvoiceDateReconSuccess
            when ((InvoiceDate = DerivedInvoiceDate)
            and InvoiceExists)

        PurchaseOrderReconSuccess
            when ((PurchaseOrder = DerivedPurchaseOrder)
            and InvoiceExists)
                  
        InvoiceAmountReconSuccess
            when ((InvoiceAmount = DerivedInvoiceAmount)
            and InvoiceExists)
                  
        DueDateReconSuccess
            when ((DueDate = DerivedDueDate)
            and InvoiceExists)

        SingleInvoiceFound
            when (instance count of PayablesInvoiceRel < 2
            and InvoiceExists)

        MultipleInvoicesFound
            when (instance count of PayablesInvoiceRel > 1
            and InvoiceExists)

        StatementHeaderExists
            when (IncomingVendorStatementRel exists)
                  
    Sets
        ByVendorInvoice
            duplicates
            Sort Order
                IncomingVendorStatement
                Vendor
                InvoiceNumber

    Relations
        PayablesInvoiceRel
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByVendorInvoice
                related.Vendor				= Vendor
                related.Invoice			    = InvoiceNumber
                      
        IncomingVendorStatementRel
            one-to-many relation to IncomingVendorStatement
            Field Mapping uses ByVendorDate
                related.VendorGroup                 = VendorGroup
                related.Vendor                      = Vendor
            Instance Selection
                where (related.StatementDate        = StatementDate)   

        DuplicateIncomingVendorStatementDetailRel       
            one-to-many relation to IncomingVendorStatementDetail
            Field Mapping uses ByVendorInvoice
                related.IncomingVendorStatement     = IncomingVendorStatement
                related.Vendor                      = Vendor
                related.InvoiceNumber               = InvoiceNumber

    Actions
        Create is a Create Action
            Entrance Rules
                if (StatementDate not entered)
                    if (IncomingVendorStatementRel exists)
                        StatementDate = IncomingVendorStatementRel.StatementDate
                    else
                        StatementDate = current date
                if (IncomingVendorStatement not entered)
                    if (IncomingVendorStatementRel exists)
                        IncomingVendorStatement = IncomingVendorStatementRel.IncomingVendorStatement
                    else
                        invoke Create IncomingVendorStatementRel
                            assign result to LocalIncomingVendorStatement
                            invoked.VendorGroup = VendorGroup
                            invoked.Vendor = Vendor
                            invoked.StatementDate = StatementDate
                        IncomingVendorStatement = LocalIncomingVendorStatement.IncomingVendorStatement   
                constraint (DuplicateIncomingVendorStatementDetailRel not exists)
                    "DuplicateVendorStatementDetail"

        Update is an Update Action

        Delete is a Delete Action
