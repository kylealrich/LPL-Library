MassSelectionBookDetail is a BusinessClass
    owned by am
    prefix is AMK
    classic name is AMSELBKDTL

    Ontology
        symbolic key is MassSelectionBookDetail
            classic set name is AMKSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Relations


        ToBookRel									
            one-to-one relation to Book
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 				  = FinanceEnterpriseGroup
                related.Book 								  = MassSelectionBookDetail.ToBook
		
		BookCalendarRel
			one-to-one relation to BookCalendar
			Field Mapping uses Set4
				related.Company 							  = MassSelection.Company
				related.Book								  = MassSelectionBookDetail.Book
		
		ToBookCalendarRel
			one-to-one relation to BookCalendar
			Field Mapping uses Set4
				related.Company 							  = MassSelection.ToCompany
				related.Book								  = MassSelectionBookDetail.ToBook
		
		ToBookCompanyCurrentPostingWithLedgerRel
			one-to-many relation to BookCalendar
			Field Mapping uses Set4	
				related.Company					= MassSelection.ToCompany
				related.Book 					= MassSelectionBookDetail.ToBook
			Instance Selection	
				where (related.YearBeginDate	= BookCalendarRel.YearBeginDate
				and related.Book.Ledger		 	= BookCalendarRel.Book.Ledger
				and related.Book.Currency	 	= MassSelectionBookDetail.Book.Currency
				and related.PostingBook)		

		ToBookCompanyCurrentBookWithLedgerRel
			one-to-many relation to BookCalendar
			Field Mapping uses Set4	
				related.Company				 = MassSelection.ToCompany
				related.Book				 = MassSelectionBookDetail.ToBook
			Instance Selection	
				where (related.YearBeginDate = BookCalendarRel.YearBeginDate
				and related.Book.Ledger		 = BookCalendarRel.Book.Ledger
				and related.Book.Currency	 = MassSelectionBookDetail.Book.Currency
				and (!related.PostingBook))
		
		OtherBookDetailsRel
			one-to-many relation to MassSelectionBookDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				  = FinanceEnterpriseGroup
				related.MassSelectionType					  = MassSelectionType
				related.MassSelection 						  = MassSelection
			Instance Selection
				where (related.MassSelectionBookDetail.Book	  = MassSelectionBookDetail.Book)
				
		OtherToBookDetailsRel
			one-to-many relation to MassSelectionBookDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				  = FinanceEnterpriseGroup
				related.MassSelectionType					  = MassSelectionType
				related.MassSelection 						  = MassSelection
			Instance Selection
				where (related.MassSelectionBookDetail.ToBook = MassSelectionBookDetail.ToBook)					
		
		BookCalendarsForCompanyRel
			one-to-many relation to BookCalendar
			Field Mapping uses Set4
				related.Company 							  = MassSelection.Company
		
		BookCalendarsForToCompanyRel
			one-to-many relation to BookCalendar
			Field Mapping uses Set4
				related.Company								  = MassSelection.ToCompany
				
	Derived Fields
		TotalFromBookRecords is a DerivedField
			type is Numeric 10
			restricted
			return instance count of BookCalendarsForCompanyRel
		
		TotalToBookRecords   is a DerivedField
			type is Numeric 10
			restricted
			return instance count of BookCalendarsForToCompanyRel
			
	Field Rules
		MassSelectionBookDetail
			constraint (MassSelection.Company != MassSelection.ToCompany)
				"BookMappingOnlyDoneForIntercompanyTransfer"							
				
			constraint (BookCalendarRel exist)
				"BookCalendarDoesNotExistForBook<MassSelectionBookDetail.Book>"			

			constraint (ToBookCalendarRel exist)
				"BookCalendarDoesNotExistForToBook<MassSelectionBookDetail.ToBook>"		
			
			constraint (OtherToBookDetailsRel not exist)
				"ToBookHasAlreadyBeenAssigned"											
			
			constraint (!MassSelectionBookDetail.Book.Impairment)	
				"ImpairmentBooksCannotBeMapped"	
			
			constraint (!MassSelectionBookDetail.ToBook.Impairment)	
				"ImpairmentBooksCannotBeMapped"												






			
			if (BookCalendarRel.PostingBook)
				constraint (ToBookCalendarRel.PostingBook)
					"FromBook<MassSelectionBookDetail.Book>IsAPostingBookToBookMustAlsoBeAPostingTypeBook"									

			if (ToBookCalendarRel.PostingBook)
				constraint (BookCalendarRel.PostingBook)
					"FromBook<MassSelectionBookDetail.Book>IsNotAPostingBookCannotTransferToAPostingBook"									
			
			if (MassSelectionBookDetail.Book.ImpairmentBook entered)
				constraint (MassSelectionBookDetail.ToBook.ImpairmentBook entered)
					"TheFromBookHasAnImpairmentBook;TheToBookMustAlsoHaveAnImpairmentBook"
									
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action	
