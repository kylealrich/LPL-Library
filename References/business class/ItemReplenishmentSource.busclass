ItemReplenishmentSource is a BusinessClass
	owned by ic
	prefix is ITM
	classic name is ITEMSRC

	Ontology
		symbolic key is ItemReplenishmentSource
			classic set name is ITMSET1
			classic name for ItemReplenishmentSource.ReplenishmentPriority is REPLENISH-PRI
			classic name for InventoryLocation is LOCATION

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		Vendor
		PurchaseFromLocation 							is a VendorLocation
			classic name is PURCH-FR-LOC
		ReplenishFrom
			classic name for ReplenishFrom.ReplenishFromCompany is REPL-FROM-CO
			classic name for ReplenishFrom.ReplenishFromLocation is REPL-FROM-LOC
			classic name for ReplenishFrom.ReplenishFromBin is REPL-FROM-BIN

	Local Fields
		LocalFromInventoryLocation	is Boolean
		LocalContractGroup			is like ContractGroup
		LocalContract				is like Contract
		LocalItem					is like Item
		LocalCompany				is like InventoryCompany
		LocalLocation				is like InventoryLocation
		LocalVendorGroup			is like VendorGroup
		LocalVendor					is like Vendor
		LocalPurchaseFrom			is like PurchaseFromLocation
		LocalFromCompany			is like InventoryCompany
		LocalFromLocation			is like InventoryLocation
		LocalFromVendor				is like Vendor
		LocalFromPurchaseFrom		is like PurchaseFromLocation
		LocalCurrentVendor			is like Vendor
		LocalCurrentPurchaseFrom	is like PurchaseFromLocation
		LocalFromBin				is like Bin
		LocalPricingGroup           is like PricingGroup 

	Transient Fields
		TransientContractGroup		is like ContractGroup
		TransientContractImport		is like ContractImport
		UseVendorOrReplenishFrom	is Numeric 1
			default label is "UseVendor_orReplenishFrom"
			derive value from UseVendorOrReplenishFromDefault
			States
				Vendor value is 0
				ReplenishFrom value is 1

	Derived Fields
		UseVendorOrReplenishFromDefault is a ConditionalField
			type is Numeric 1
			restricted
			if (Vendor entered)
				UseVendorOrReplenishFrom.Vendor
			else
				UseVendorOrReplenishFrom.ReplenishFrom

		ItemDescription is a DerivedField
			type is Alpha 60
			return Item.Description

	Conditions
			
		RecordExists
			restricted
			when (ItemReplenishmentSource exists)

		HasVendor
			classic name is APCOMPANY
			restricted
			when (Vendor entered)

		ItemIsActive
			restricted
			when (Item.IsActive)
		
		VendorItemExists
			classic name is ITEMVEN-EXISTS
			restricted
			when (first VendorItemsRel exists)

		ConsignmentItem
			restricted
			when (ReplenishFromItemLocationRel.Consignment)

		ItemStockOnHandBelowReorderPoint
			restricted
			when (ReplenishFromItemLocationRel.StockOnHandBelowReorderPoint)

		StockOnHandBelowReorderPoint
			restricted
			when (ItemLocationRel.StockOnHandBelowReorderPoint)

		IsVendorexists
			restricted
			when (Vendor entered)

		HasVendorOrReplenishmentEntered
			restricted
			when (Vendor entered
			and	  ReplenishFrom entered)

		HasVendorOrReplenishmentNotEntered
			restricted
			when (Vendor not entered
			and   PurchaseFromLocation not entered
			and	  ReplenishFrom not entered)

		ContractLineImportExists
			restricted
			when (ContractLineImportRel exists)

		ItemExistsForReplenishFrom
			restricted
			when (ReplenishFromItemLocationRel exists
			and   ReplenishFromItemLocationRel.InventoryTracked)

		NoOtherSourceForThisLocation
			restricted
			when (ItemReplenihmentSourceRel !exists)

		ValidForLocation
			restricted
			when (ValidForManufacturer
			or    ValidForDistributor
			or    ValidForNonManufacturerDistributor)

		WillDefaultFromContract
			restricted
			when (ContractLineItemDefaultRel exists
			and   Vendor entered)

		WillNotDefaultFromContract
			when (ContractLineItemDefaultRel !exists
			and   Vendor entered)

		HasContractLine
			when (ContractLineItemRel exists)

		HasContractLineForOtherVendor
			when (ContractLineOtherVendorRel exists)

		ValidForManufacturer
			restricted
			when (ContractRel.IsManufacturerContract
			and   TierMemberRel exists
			or   (InventoryLocation.PricingGroup entered
			and	  TierMemberPricingGroupRel exists))

		ValidForDistributor
			restricted
			when (ContractRel.IsDistributorContract
			and   PricingMemberRel exists
			or   (InventoryLocation.PricingGroup entered
			and	  PricingMemberPricingGroupRel exists))

		SupplierCanView
			restricted
			when (Company.ItemGroup         = actor.agent(SupplierSourceId).SupplierGroup
 			and   Vendor			 		= actor.agent(SupplierSourceId).Supplier.Vendor			
			and   Vendor entered
			and  ((actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.AllDocuments)
			or  (!actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.AllDocuments
			and   actor.agent(SupplierSourceId).Supplier.PrimaryContactViewAllDocuments = true
			and   actor.agent(SupplierSourceId).PrimaryContact)
			or  ((actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.OnlyLocationDocuments
			or    actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.LocationAndNoLocationDocuments)
			and  (actor.agent(SupplierSourceId).Supplier.PrimaryContactViewAllDocuments = false
			or   !actor.agent(SupplierSourceId).PrimaryContact)
			and   PurchaseFromLocation entered
			and   actor.agent(SupplierSourceId).VendorLocation = PurchaseFromLocation)
			or   (actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.LocationAndNoLocationDocuments
			and   PurchaseFromLocation !entered)))  

		ValidForNonManufacturerDistributor
			restricted
			when (ContractRel.NotDistributorOrManufacturerWithoutParticipants
			or   (ContractRel.IsNotDistributorAndNotManufacturer
			and   ContractRel.ContractParticipantsExist
			and  (ParticipantLocationRel exists
			or   (InventoryLocation.PricingGroup entered
			and   ParticipantLocationPricingGroupRel exists))))

	Relations
		PayablesCompanyRel
			classic name is APCOMPANY
			one-to-one relation to PayablesCompany
			valid when (HasVendor)
			required
			Field Mapping uses symbolic key
				related.Company = Company

		ReplenishFromItemLocationRel
			classic name is REPL-ITEMLOC
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           = ReplenishFrom.ReplenishFromCompany
				related.InventoryLocation = ReplenishFrom.ReplenishFromLocation
				related.Item              = Item

		ReplenishFromInventoryLocationRel
			classic name is REPL-ITEMLOC
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           = RequestingLocationRel.FromCompanyLocation.FromCompany
				related.InventoryLocation = InventoryLocation.ReplenishFromLocation
				related.Item              = Item

		ItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           = Company
				related.InventoryLocation = InventoryLocation
				related.Item              = Item

		NewItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           = LocalFromCompany
				related.InventoryLocation = LocalFromLocation
				related.Item              = Item

		ContractLineImportRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByItemMfgInfo
				related.ContractGroup     = TransientContractGroup
				related.ContractImport    = TransientContractImport
				related.ItemNumber        = Item

		ContractItemRel
			one-to-many relation to ContractLine
			Field Mapping uses ByItemVendorItemForContract
				related.ContractGroup     = LocalContractGroup
				related.Contract          = LocalContract
				related.ItemNumber        = LocalItem

		ContractLineItemDefaultRel
 			one-to-many relation to ContractLine
			Field Mapping uses ByItemContractLine
				related.ContractGroup     = Company.ItemGroup
				related.ItemNumber        = Item
			Instance Selection
				where (related.Contract.Vendor = Vendor
				and    related.UseForCostDefaultingSomewhere)

		ContractLineItemRel
 			one-to-many relation to ContractLine
			Field Mapping uses ByItemContractLine
				related.ContractGroup     = Company.ItemGroup
				related.ItemNumber        = Item
			Instance Selection
				where (related.Contract.Vendor = Vendor)

		ContractLineOtherVendorRel
 			one-to-many relation to ContractLine
			Field Mapping uses ByItemContractLine
				related.ContractGroup     = Company.ItemGroup
				related.ItemNumber        = Item
			Instance Selection
				where (related.Contract.Vendor != Vendor)

		VendorLocationRel
			classic name is VENDLOC
			one-to-one relation to VendorLocation
			valid when (HasVendor)
			required
			Field Mapping uses symbolic key
				related.VendorGroup    = PayablesCompanyRel.VendorGroup
				related.Vendor         = Vendor
				related.VendorLocation = PurchaseFromLocation

		PurchaseFromLocationRel
			one-to-one relation to PurchaseFromLocation
			Field Mapping uses symbolic key
				related.VendorGroup    			= LocalVendorGroup
				related.Vendor         			= LocalVendor
				related.PurchaseFromLocation 	= LocalPurchaseFrom

		CurrentPurchaseFromRel
			one-to-one relation to PurchaseFromLocation
			Field Mapping uses symbolic key
				related.VendorGroup    			= LocalVendorGroup
				related.Vendor         			= LocalCurrentVendor
				related.PurchaseFromLocation 	= LocalCurrentPurchaseFrom

		VendorGroupRel
			classic name is VENDORGROUP
			one-to-one relation to VendorGroup
			valid when (HasVendor)
			required
			Field Mapping uses symbolic key
				related.VendorGroup = PayablesCompanyRel.VendorGroup

		VendorItemsRel
			classic name is POITEMVEN
			one-to-many relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup = Company.ItemGroup
				related.Item             = Item
				related.Vendor           = Vendor

		NewOtherLocationReplenishmentSourcesRel
			one-to-many relation to ItemReplenishmentSource
			Field Mapping uses Set2
				related.ReplenishFrom.ReplenishFromCompany 			= LocalFromCompany
				related.ReplenishFrom.ReplenishFromLocation 		= LocalFromLocation
				related.Company                                     = Company
				related.InventoryLocation                           = InventoryLocation
				related.Item                                        = Item
			Instance Selection
				where (related.ReplenishFrom.ReplenishFromBin = LocalFromBin)

		OtherItemReplenishmentSourcesForVendorRel
			one-to-many relation to ItemReplenishmentSource
			Field Mapping uses Set3
				related.Vendor 										= Vendor
				related.PurchaseFromLocation 						= PurchaseFromLocation
				related.Company 									= Company
				related.InventoryLocation 							= InventoryLocation
				related.Item 										= Item
			Instance Selection
				where (related.ItemReplenishmentSource				!= ItemReplenishmentSource)

		NewOtherVendorReplenishmentSourcesRel
			one-to-many relation to ItemReplenishmentSource
			Field Mapping uses Set3
				related.Vendor							 			= LocalFromVendor
				related.PurchaseFromLocation 						= LocalFromPurchaseFrom
				related.Company                                     = Company
				related.InventoryLocation                           = InventoryLocation
				related.Item                                        = Item

 		OtherItemReplenishmentSourcesForReplenishFromRel
			one-to-many relation to ItemReplenishmentSource
			Field Mapping uses Set2
				related.ReplenishFrom.ReplenishFromCompany 			= ReplenishFrom.ReplenishFromCompany
				related.ReplenishFrom.ReplenishFromLocation 		= ReplenishFrom.ReplenishFromLocation
				related.Company 									= Company
				related.InventoryLocation 							= InventoryLocation
				related.Item 										= Item
			Instance Selection
				where (related.ItemReplenishmentSource				!= ItemReplenishmentSource)

		OtherItemReplenishmentSourcesVendorLocRel
			one-to-many relation to ItemReplenishmentSource
			Field Mapping uses Set3
				related.Vendor										= Vendor
				related.PurchaseFromLocation 						= PurchaseFromLocation
				related.Company 									= Company
				related.InventoryLocation 							= InventoryLocation
				related.Item 										= Item
			Instance Selection
				where (related.ItemReplenishmentSource				!= ItemReplenishmentSource)

		ItemReplenihmentSourceRel
			one-to-many relation to ItemReplenishmentSource
			Field Mapping uses symbolic key
				related.Company 									= Company
				related.InventoryLocation 							= InventoryLocation
				related.Item 										= Item

		RequestingLocationRel
			one-to-one relation to RequestingLocation
			Field Mapping uses symbolic key
				related.Company           	= Company
				related.RequestingLocation 	= InventoryLocation

		ContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup       = LocalContractGroup
				related.Contract            = LocalContract

		ParticipantLocationRel
			one-to-many relation to ContractParticipant
			Field Mapping uses ByReqLocLocation
				related.ContractGroup       			= LocalContractGroup
				related.Contract            			= LocalContract
				related.ContractParticipant.ParticipantLocation.Company 	= Company
			Instance Selection
				where ((related.ContractParticipant.ParticipantLocation.Location = InventoryLocation
				and     related.ContractParticipant.ParticipantLocation.Location entered)
				or      related.ContractParticipant.ParticipantLocation.Location !entered)

		ParticipantLocationPricingGroupRel
			one-to-many relation to ContractParticipant
			Field Mapping uses ByPricingGroup 
				related.ContractGroup       									= LocalContractGroup
				related.ContractParticipant.ParticipantLocation.PricingGroup 	= LocalPricingGroup
				related.Contract            									= LocalContract

		TierMemberRel
			one-to-many relation to ContractTierMember
			Field Mapping uses ByLocation
				related.ContractGroup       			= LocalContractGroup
				related.Contract            			= LocalContract
				related.ContractTierMember.Company 		= Company
			Instance Selection
				where ((related.ContractTierMember.Location = InventoryLocation
				and     related.ContractTierMember.Location entered)
				or      related.ContractTierMember.Location !entered)

		TierMemberPricingGroupRel
			one-to-many relation to ContractTierMember
			Field Mapping uses ByPricingGroup 
				related.ContractGroup       			= LocalContractGroup
				related.ContractTierMember.PricingGroup = LocalPricingGroup
				related.Contract            			= LocalContract

		PricingMemberRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses ByLocation
				related.ContractGroup       						= LocalContractGroup
				related.Contract            						= LocalContract
				related.ContractDistributorPricingMember.Company 	= Company
			Instance Selection
				where ((related.ContractDistributorPricingMember.Location = InventoryLocation
				and     related.ContractDistributorPricingMember.Location entered)
				or      related.ContractDistributorPricingMember.Location !entered)
				
		PricingMemberPricingGroupRel 
			one-to-many relation to ContractDistributorPricingMember	
			Field Mapping uses ByPricingGroup 
				related.ContractGroup       							= LocalContractGroup
				related.ContractDistributorPricingMember.PricingGroup 	= LocalPricingGroup
				related.Contract            							= LocalContract				
		IIHItemLocationClearRecommendationsRel
			one-to-one relation to IIHItemLocation
			Field Mapping uses symbolic key
				related.Company 			= Company
				related.InventoryLocation 	= InventoryLocation
				related.Item 				= Item

	Sets

		Set2
			indexed
			Sort Order
				ReplenishFrom.ReplenishFromCompany
				ReplenishFrom.ReplenishFromLocation
				Company
				InventoryLocation
				Item
				ItemReplenishmentSource

		Set3
			indexed
			Sort Order
				Vendor
				PurchaseFromLocation
				Company
				InventoryLocation
				Item
				ItemReplenishmentSource

		Set4
			indexed
			Instance Selection
				where (IsVendorexists)
			Sort Order
				Company
				InventoryLocation
				Vendor
				Item
				PurchaseFromLocation

		ByVendorAndItem
			Instance Selection
				where (IsVendorexists)
			Sort Order
				Vendor
				Item
				Company
				InventoryLocation
				PurchaseFromLocation

		ByItem
			Sort Order
				Item
				Company
				ItemReplenishmentSource.ReplenishmentPriority
				InventoryLocation

	Field Rules
		Item
			constraint (ItemLocationRel exists)
				"CannotAddItemReplenishmentSourceRecord;Item<Item>DoesNotExistAtLocation<InventoryLocation>"

		ItemReplenishmentSource.ReplenishmentPriority

			if (NoOtherSourceForThisLocation)
				default to 1

		Vendor
			if (ReplenishFrom not entered)
				required
					"Enter_VendorOrReplenishmentFromCompany/location"          
			else
				cannot be entered
					"EnterEither_VendorOrReplenishmentFromCompany/location"

			if (PurchaseFromLocation entered)
				required
					"VendorMustBeEntered"                                     

			constraint (!HasVendorOrReplenishmentEntered)
				"EnterVendorOrReplenishmentFromCompany/location"           

			constraint (OtherItemReplenishmentSourcesForVendorRel not exists)
				"RecordForItem<Item>AlreadyExistsForVendor/purchaseFromLocation"        

		PurchaseFromLocation

			constraint (PurchaseFromLocation exists)
				"PurchaseFromLocationDoesNotExist"

			constraint (PurchaseFromLocation.Status = "A")
				"PurchaseFromLocationIsNotActive"
				
			constraint (PurchaseFromLocation.IsPurchaseFromLocation)
				"PurchaseFromLocationCannotBeARemit-ToOnlyLocation"

		ReplenishFrom
			if (Vendor not entered)
				required
					"EnterVendorOrReplenishmentFromCompany/location"          
			else
				cannot be entered
					"EnterEither_VendorOrReplenishmentFromCompany/location"

			constraint (!ItemLocationRel.AutomaticPurchaseOrder)
				"Item<Item>CannotUseAutoPurchaseOrderWhenReplenishingFromALocation"		

			if (ReplenishFrom.ReplenishFromCompany entered)
				constraint (ReplenishFrom.ReplenishFromLocation entered)
					"ReplenishmentLocationMustBeEntered"                      

			if (ReplenishFrom.ReplenishFromLocation entered)
				constraint (ReplenishFrom.ReplenishFromCompany entered)
					"ReplenishmentCompanyMustBeEntered"                       

			if (ReplenishFrom.ReplenishFromBin entered)
				constraint (ReplenishFrom.ReplenishFromLocation entered)
					"ReplenishmentLocationMustBeEntered"                      

			constraint (ReplenishFrom.ReplenishFromLocation != InventoryLocation
			or          ReplenishFrom.ReplenishFromCompany != Company)
				"ReplenishmentFromCompany<ReplenishFrom.ReplenishFromCompany>/Location<ReplenishFrom.ReplenishFromLocation>CannotEqualOriginatingCompany<Company>/Location<InventoryLocation>"	

			constraint (ReplenishFromItemLocationRel exists)
				"Item<Item>DoesNotExistAtReplenishFromCompany<ReplenishFrom.ReplenishFromCompany>Location<ReplenishFrom.ReplenishFromLocation>"

			if (LocalFromInventoryLocation)
				constraint (ItemExistsForReplenishFrom)
					"EnterEitherVendorOrReplenishmentFromCompany/Location"
			else
				constraint (ItemExistsForReplenishFrom)
					"Item<Item>AtReplenishFromCompany<ReplenishFrom.ReplenishFromCompany>Location<ReplenishFrom.ReplenishFromLocation>IsNotInventoryTracked" 

			constraint (OtherItemReplenishmentSourcesForReplenishFromRel not exists)
				"RecordForItem<Item>AlreadyExistsForReplenishmentFromCompany/Location"       

			constraint (Company.ItemGroup = ReplenishFrom.ReplenishFromCompany.ItemGroup)
				"ReplFromCompany_&OriginatingCoMustUseSameItemGroup"       


			if (ReplenishFromItemLocationRel.CartParLocation)
				constraint (ReplenishFromItemLocationRel.InventoryTracked)
					"Item<Item>IsNonStockAtFromLocation;ReplenishFromVendor"		











	Actions
		Create is a Create Action
			Action Rules
				if (InventoryLocation.ParOrExchangeCart)
					constraint (ItemReplenihmentSourceRel not exists)
						"OnlyOneItemReplenishmentSourceIsAllowed;ReplenishmentSourceAlreadyExists"
				if (!InventoryLocation.LocationType.InventoryLocation
				and ReplenishFromInventoryLocationRel exists
				and Vendor not entered
				and ReplenishFrom not entered)
					ReplenishFrom.ReplenishFromCompany  = RequestingLocationRel.FromCompanyLocation.FromCompany
					ReplenishFrom.ReplenishFromLocation = InventoryLocation.ReplenishFromLocation

		Update is an Update Action
			Action Rules
				if (!InventoryLocation.LocationType.InventoryLocation
				and ReplenishFromInventoryLocationRel exists
				and Vendor not entered
				and ReplenishFrom not entered)
					ReplenishFrom.ReplenishFromCompany  = RequestingLocationRel.FromCompanyLocation.FromCompany
					ReplenishFrom.ReplenishFromLocation = InventoryLocation.ReplenishFromLocation
		 			LocalFromInventoryLocation = true

		 		if (InventoryLocation.ParOrExchangeCart)
		 			constraint (ItemLocationRel.DefaultTransactionUOM.ValidForTransaction or Vendor entered)
			 			"UOMForParItem<Item>MustBeValidForInventoryTransactionsOrParLineMustReplenishFromVendor"

		Delete is a Delete Action
			Entrance Rules
				constraint (not InventoryLocation.ParOrExchangeCart)
		 			"CannotDeleteItemReplenishmentSourceForCartOrParItems"

		UpdateReplenishmentPriority is an Instance Action
			valid when (not InventoryLocation.ParOrExchangeCart)
			Parameters
				NewPriority is Numeric size 6

			Action Rules

				LocalCompany			= Company
				LocalItem				= Item
				LocalLocation			= InventoryLocation
				LocalFromVendor			= Vendor
				LocalFromPurchaseFrom	= PurchaseFromLocation
				LocalFromCompany		= ReplenishFrom.ReplenishFromCompany
				LocalFromLocation		= ReplenishFrom.ReplenishFromLocation
				LocalFromBin			= ReplenishFrom.ReplenishFromBin

				invoke Delete

				invoke Create
					invoked.Company              							= LocalCompany
					invoked.Item                                            = LocalItem
					invoked.InventoryLocation                               = LocalLocation
					invoked.Vendor                                          = LocalFromVendor
					invoked.PurchaseFromLocation                            = LocalFromPurchaseFrom
					invoked.ReplenishFrom.ReplenishFromCompany              = LocalFromCompany
					invoked.ReplenishFrom.ReplenishFromLocation             = LocalFromLocation
					invoked.ReplenishFrom.ReplenishFromBin                  = LocalFromBin
					invoked.ItemReplenishmentSource.ReplenishmentPriority   = NewPriority

		MassUpdateItemReplenishmentSourceFromContract is a Set Action
			restricted
			Parameters
				ParmContractGroup   	is a ContractGroup
				ParmContract        	is a Contract
				CurrentVendor           is a Vendor
				CurrentPurchaseFrom     is a VendorLocation
					context of CurrentVendor
				NewVendor           	is a Vendor
				NewPurchaseFrom     	is a VendorLocation
					context of NewVendor

			Instance Selection
				where (ParmContractGroup = Company.ItemGroup
				and    CurrentVendor = Vendor
				and    CurrentPurchaseFrom = PurchaseFromLocation)

 			Action Rules

 				Instance Rules
					LocalContractGroup 	= ParmContractGroup
					LocalContract      	= ParmContract
					LocalItem   		= Item
					LocalCompany    	= Company
					LocalLocation 		= InventoryLocation
					LocalPricingGroup   = InventoryLocation.PricingGroup
					if (ContractItemRel exists)
						if (ValidForLocation)
							Vendor 					= NewVendor
							PurchaseFromLocation 	= NewPurchaseFrom
							initialize ReplenishFrom.ReplenishFromCompany
							initialize ReplenishFrom.ReplenishFromLocation
							initialize ReplenishFrom.ReplenishFromBin

		MassUpdateItemReplenishmentSource is a Set Action
			Parameters
				ParmCompany                 is an InventoryCompany
					default label is "Company"
				ParmInventoryLocation       is an InventoryLocation
					default label is "InventoryLocation"
					context of ParmCompany
				ParmItem                    is an Item
					default label is "Item"
					context of ParmCompany
				CurrentVendor               is a Vendor
					default label is "Vendor"
					context of ParmCompany
				CurrentPurchaseFrom			is a VendorLocation	
					default label is "PurchaseFrom"
					context of ParmCompany
					context of CurrentVendor
				CurrentFromCompany          is an InventoryCompany
					default label is "Company"
				CurrentFromLocation         is an InventoryLocation
					default label is "Location"
					context of CurrentFromCompany
				CurrentBin                  is a Bin
					default label is "Bin"
					context of CurrentFromCompany
					context of CurrentFromLocation
				NewVendor                   is a Vendor
					default label is "Vendor"
					context of ParmCompany
				NewPurchaseFrom				is a VendorLocation
					default label is "PurchaseFrom"
					context of NewVendor
					context of ParmCompany
				NewFromCompany              is an InventoryCompany
					default label is "Company"
				NewFromLocation             is an InventoryLocation
					context of NewFromCompany
					default label is "Location"
				NewFromBin                  is a Bin
					context of NewFromCompany
					context of NewFromLocation
					default label is "Bin"

			Parameter Rules

				ParmCompany
	   				required
	   					"MustEnterACompanyForSelection"

					if (CurrentVendor entered)
						constraint (CurrentFromCompany !entered)
							"CanOnlyEnterCurrentVendorOrCompany,NotBoth"

					if (NewVendor entered)
						constraint (NewFromCompany !entered)
							"CanOnlyEnterNewVendorOrCompany,NotBoth"

					constraint (NewFromCompany entered
					or          NewVendor entered)
						"MustEnterANewVendorOrCompany"


					constraint (ParmInventoryLocation entered
					or          ParmItem entered
					or          CurrentVendor entered
					or          CurrentFromCompany entered)
						"MustEnterAtLeastOneSelectionCriteriaBesidesCompany"

					if (ParmInventoryLocation entered
					and NewFromCompany entered
					and NewFromLocation entered
					and ParmCompany = NewFromCompany)
						constraint (ParmInventoryLocation != NewFromLocation)
							"WhenInventoryLocationIsEntered,CompanyAndInventoryLocationCannotBeTheSameAsNewFromCompanyAndLocation"

				CurrentVendor
					if (CurrentPurchaseFrom entered)
						required

				CurrentFromCompany
					if (CurrentFromLocation entered)
						required

				CurrentFromLocation
					if (CurrentBin entered)
						required

				NewVendor
					if (NewPurchaseFrom entered)
						required

				NewPurchaseFrom
					if (NewPurchaseFrom entered) 
						constraint (NewPurchaseFrom exists)
							"PurchaseFromDoesNotExist"
						constraint (NewPurchaseFrom.Status = "A")
							"PurchaseFromLocationIsNotActive"

				NewFromCompany
					if (NewFromLocation entered)
						required

					if (NewFromCompany entered)
						constraint (NewFromLocation entered)
							"MustEnterNewLocationIfNewCompanyEntered"

				NewFromLocation

					if (NewFromBin entered)
						required

			Instance Selection
				where ((ParmCompany = Company
				or   ParmCompany !entered)
				and (ParmInventoryLocation = InventoryLocation
				or   ParmInventoryLocation !entered)
				and (ParmItem     = Item
				or   ParmItem !entered)
				and (CurrentVendor = Vendor)
				and (CurrentPurchaseFrom = PurchaseFromLocation)
				and (CurrentFromCompany = ReplenishFrom.ReplenishFromCompany)
				and (CurrentFromLocation = ReplenishFrom.ReplenishFromLocation)
				and (CurrentBin 		= ReplenishFrom.ReplenishFromBin))

			Action Rules

				Instance Rules

					LocalFromCompany          = NewFromCompany
					LocalFromLocation         = NewFromLocation
					LocalFromVendor           = NewVendor
					LocalFromPurchaseFrom     = NewPurchaseFrom
					LocalFromBin              = NewFromBin

	   				if (NewVendor entered
	   				and NewOtherVendorReplenishmentSourcesRel !exists)
	   					Vendor 									= NewVendor
	   					PurchaseFromLocation 					= NewPurchaseFrom
	   					initialize ReplenishFrom.ReplenishFromCompany
	   					initialize ReplenishFrom.ReplenishFromLocation
	   					initialize ReplenishFrom.ReplenishFromBin
	   				else
	   				if (NewFromCompany entered
	   				and NewItemLocationRel exists
	   				and NewItemLocationRel.InventoryTracked
	   				and NewOtherLocationReplenishmentSourcesRel !exists
	   				and (!InventoryLocation.ParOrExchangeCart
	   				or  (InventoryLocation.ParOrExchangeCart
	   				and ((NewItemLocationRel.DefaultTransactionUOM entered
	   				and  NewItemLocationRel.DefaultTransactionUOM.ValidForTransaction)
	   				or   NewItemLocationRel.DefaultTransactionUOM !entered))))
	   					ReplenishFrom.ReplenishFromCompany      = NewFromCompany
	   					ReplenishFrom.ReplenishFromLocation     = NewFromLocation
	   					ReplenishFrom.ReplenishFromBin          = NewFromBin
	   					initialize Vendor
	   					initialize PurchaseFromLocation

		DeleteParItem is a Delete Action
			restricted
			
		ClearRecommendation is an Instance Action
			restricted
			Action Rules
				invoke ClearRecommendation IIHItemLocationClearRecommendationsRel
