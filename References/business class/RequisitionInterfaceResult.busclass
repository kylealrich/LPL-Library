RequisitionInterfaceResult is a BusinessClass
	owned by rq
	
	prefix is RIR
	
	Ontology
		symbolic key is RequisitionInterfaceResult
	
	Patterns
		implements StaticJava
        disable AuditIndex
       	disable EffectiveDated
	
	Persistent Fields
		RunGroup				
		RunTime							is TimeStamp
		Status							is Numeric 1
			States
				InProcess		       	value is 0
				Complete				value is 1
				Incomplete 				value is 2
			disable Auditing
		ErrorRunGroup					is a RunGroup
		OriginalRunGroup				is a RunGroup
		RecordsProcessed				is Numeric 10
		ReleaseRequisitions         	is Boolean
		IncludeRequisitionsInError  	is Boolean
		MoveErrorsToNewRunGroup 		is Boolean
		ErrorRunGroupPrefix     		is AlphaUpper 15
		
	Derived Fields
	
		NoRecordsProcessedMsg is a MessageField
			restricted
			"NoRecordsAreProcessed"
			 
        DerivedRunGroup is a DerivedField
        	type is AlphaUpper 30
        	if (ErrorRunGroup entered)
        		return ErrorRunGroup
        	else
        		return RunGroup
        
        CompleteMessage is a MessageField
        	restricted
        	"Complete"
        	
        IncompleteMessage is a MessageField
        	restricted
        	"Incomplete"
        	
        NoRecordsMessage is a MessageField
        	restricted
        	"AllErrorsProcessed"
        
        InProcessMessage is a MessageField
        	restricted
        	"InProcess"
        
        DerivedStatus is a DerivedField
        	type is Alpha 50
        	if (Status.InProcess)
        		return InProcessMessage
        	else
        	if (Status.Complete)
        		return CompleteMessage
        	else
        	if (Status.Incomplete
        	and NoErrorRecordsToProcess)
        		return NoRecordsMessage
        	else
        		return IncompleteMessage
        
		InterfacedRequisitionCount is a DerivedField
			type is Numeric 7	
			return RecordsProcessed
			
		ErrorRequisitionCountRunGroup is a DerivedField
			type is Numeric 7
			return (instance count of RunGroupProcessedRequisitionImportRel)
			
		ErrorRequisitionCountErrorRunGroup is a DerivedField
			type is Numeric 7
			return (instance count of ErrorRunGroupRequisitionImportRel)
			
	Field Rules
	
		OriginalRunGroup
			if (RunGroup like "*ERRORS_*")
				default to first ErrorRunGroupRel.OriginalRunGroup
			default to RunGroup
	
	Conditions
		IncompleteErrorsNotMoved
			restricted
			when (Status.Incomplete
			and	  ErrorRunGroup not entered)

		IncompleteErrorsMoved
			restricted
			when (Status.Incomplete
			and	  ErrorRunGroup entered)

		IncompleteWithErrors
			restricted
			when ((IncompleteErrorsNotMoved
			and   RunGroupProcessedRequisitionImportRel exists)
			or    (IncompleteErrorsMoved
			and    ErrorRunGroup entered
			and    ErrorRunGroupRequisitionImportRel exists))

		NoErrorRecordsToProcess
			restricted
			when (RunGroupProcessedRequisitionImportRel !exists
			and   ErrorRunGroupRequisitionImportRel !exists)
		
		HasRequisitionsCreated
			restricted
			when (RequisitionsRel exists)

		NoRecordsToProcess
			when (Status entered
			and   RecordsProcessed not entered)

		HasRecordsProcessed
			when (Status entered
			and   RecordsProcessed entered)

		ErrorRunGroupEntered
			when (ErrorRunGroup entered)
			
		ShowRunGroupPanel
			when (RunGroupProcessedRequisitionImportRel exists)
		
		ShowErrorRunGroupPanel
			when (ErrorRunGroupRequisitionImportRel exists)

		RunHasError
			when (RequisitionImportWithErrorInRunGroupRel exists)

	Relations

		
		RunGroupRequisitionImportRel
			one-to-many relation to RequisitionImport
			Field Mapping uses ByRunGroup
				related.RunGroup 		= RunGroup
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = FinanceEnterpriseGroup)
				


		RunGroupProcessedRequisitionImportRel
			one-to-many relation to RequisitionImport
			Field Mapping uses ByRunGroup
				related.RunGroup 		= RunGroup
			Instance Selection
				where (related.ResultProcessedBy = RequisitionInterfaceResult
				and    related.Company.FinanceEnterpriseGroup = FinanceEnterpriseGroup)
		
		ErrorRunGroupRequisitionImportRel
			one-to-many relation to RequisitionImport
			valid when (ErrorRunGroupEntered)
			Field Mapping uses ByRunGroup
				related.RunGroup 		= ErrorRunGroup
			Instance Selection
				where (related.ResultProcessedBy 				= RequisitionInterfaceResult
				and    related.Company.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup)

		RequisitionsRel
			one-to-many relation to Requisition
			Field Mapping uses symbolic key
			Instance Selection
				where (related.OriginatingInterfaceRun			= RequisitionInterfaceResult
				and    related.InterfaceInProcess 				= false
				and    related.Company.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup)
				
		ErrorRunGroupRel
			one-to-many relation to RequisitionInterfaceResult
			Field Mapping uses ByErrorRunGroup
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.ErrorRunGroup		   = RunGroup

		RequisitionImportWithErrorInRunGroupRel
			one-to-many relation to RequisitionImport
			Field Mapping uses ByRunGroup
				related.RunGroup 								= RunGroup
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				and	   related.RecordInError)

		GeneralLedgerSystemCodeRel 
			one-to-many relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode	= "RQ"
				
	Sets
		ByRunTime
			Sort Order
				RunTime descending
				RequisitionInterfaceResult
				FinanceEnterpriseGroup
				UniqueID
		
		ByRunGroup
			Sort Order
				RunGroup
				RequisitionInterfaceResult
				FinanceEnterpriseGroup
				UniqueID
				
		ByOriginalRunGroup
			Sort Order
				FinanceEnterpriseGroup
				OriginalRunGroup
				RunTime descending
				RequisitionInterfaceResult
				
		ByErrorRunGroup
			Sort Order
				FinanceEnterpriseGroup
				ErrorRunGroup
				RequisitionInterfaceResult

	Actions
			
		Create is an Action
			restricted
			
		Update is an Action
			restricted
			
		Delete is an Action
			valid when (!Status.InProcess)
		
		Purge is a Purge Action
			restricted
		
		SetStatusToIncomplete is an Instance Action
			restricted
			Action Rules
				Status = Status.Incomplete
				
		RerunForErrors is an Instance Action
			valid when (IncompleteWithErrors)
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				ReleaseRequisitions 		is Boolean
				PrmMoveErrorsToNewRunGroup  is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is FinanceEnterpriseGroup
				PrmRunGroup
					initial value is DerivedRunGroup
				ReleaseRequisitions
					initial value is true
					
			Action Rules
			
				invoke InterfaceRequisitions RequisitionImport
					invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
					invoked.PrmRunGroup       				= PrmRunGroup
					invoked.ReleaseRequisitions				= ReleaseRequisitions
					invoked.IncludeRequisitionsInError		= true
					invoked.PrmMoveErrorsToNewRunGroup 		= PrmMoveErrorsToNewRunGroup
					invoked.PrmErrorRunGroupPrefix 			= PrmErrorRunGroupPrefix
		
		UpdateErrorInformation is an Instance Action
			restricted	
			Action Rules
				if  (MoveErrorsToNewRunGroup
				and  ErrorRunGroup not entered)
					
					increment first RunGroupRequisitionImportRel.Company.VendorGroupRel.LastErrorRunGroupNumber
					ErrorRunGroup = ErrorRunGroupPrefix + "ERRORS_" + first RunGroupRequisitionImportRel.Company.VendorGroupRel.LastErrorRunGroupNumber
					
					invoke MoveImportErrorsToNewRunGroup RequisitionImport
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.PrmRunGroup					= RunGroup
						invoked.NewRunGroup					= ErrorRunGroup
						invoked.PrmInterfaceResult			= RequisitionInterfaceResult
				else
					Status = Status.Incomplete

						
		Finish is an Instance Action
			restricted
			Parameters
				PrmRecordsProcessed		is like Requisition
			
			Action Rules
				RecordsProcessed = PrmRecordsProcessed						
				if (RunHasError)
					invoke UpdateErrorInformation
				else
					Status = Status.Complete
				
				if (GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit
				or  GeneralLedgerSystemCodeRel.EncumbranceOption.Track)
					invoke ProcessInterfaceBatchEdits BudgetEditBatch 
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup 					
						invoked.PrmBusinessClassName		= "Requisition"
					
	
	
