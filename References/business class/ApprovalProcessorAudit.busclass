ApprovalProcessorAudit is a BusinessClass
	owned by GeneralLedger
    prefix is ApPrA

	Ontology
		symbolic key is ApprovalProcessorAudit

	Patterns
		disable Auditing
		implements CreateStamp
		implements UpdateStamp

	Persistent Fields
#ifdef module sharedfinance
		Resource				is an Employee
        Team					is a FinanceTeam
#endif
#ifndef module sharedfinance
		Resource				is Numeric 13
		Team					is a GlonlyFinanceTeamField
#endif
		Status					is Numeric 2
			States
				Assigned			value is 1
				Reassigned			value is 2
				Rejected			value is 3
				Approved			value is 4
				Disapproved			value is 5
				AutoApproved		value is 6
				PendingApprover		value is 7
				Restored			value is 8
				MatchReleased		value is 9
				SentForPayment		value is 10
				Submitted			value is 11
				Cancelled			value is 12
				ManuallyApproved	value is 13
				ManuallyRejected	value is 14
				Released			value is 15
				Matched				value is 16
				Unmatched			value is 17
				Amended				value is 18
				Rerouted			value is 19
				Requested			value is 20
				Accepted			value is 21
				Unreleased			value is 22
				ManuallyUnreleased	value is 23
				ApprovalNotRequired	value is 24
		UpdateDate				is TimeStamp
			default label is "Date"
        Escalated				is Boolean
		ActionReason			is AlphaUpper 20
		Comment					is Alpha size up to 500
		Role					is AlphaUpper 30
		Workunit				is like PfiWorkunit

	Derived Fields
		DerivedUpdateDate is a DerivedField
			type is Date
			restricted
			return (UpdateDate date)

		DerivedResourceTeamDisplay is a DerivedField
			type is Alpha 250
			default label is "Team/\Resource/\Role"
			if (ResourceEntered)
#ifdef module sharedfinance
				if (FinanceResourceExists)
					return FinanceResourceRel.PreferredSimplePresentationName
				else
					return Resource.PresentationNameSnapshot
#endif
#ifndef module sharedfinance
				return Resource
#endif
			else
			if (Team entered)
#ifdef module sharedfinance
				return Team.Description
#endif
#ifndef module sharedfinance
				return Team
#endif
			else
			if (RoleEntered)
				return Role
			else
				return ""

		DerivedResourceTeamInitials is a DerivedField
			type is AlphaUpper 3
			restricted
			if (ResourceEntered)
#ifdef module sharedfinance
				if (FinanceResourceExists)
					return FinanceResourceRel.PreferredSimplePresentationName
				else
					return Resource.PresentationNameSnapshot
#endif
#ifndef module sharedfinance
				return Resource
#endif
			else
			if (Team entered)
#ifdef module sharedfinance
				return Team.Description
#endif
#ifndef module sharedfinance
				return Team
#endif
			else
			if (RoleEntered)
				return Role
			else
				return "SYS"

		DerivedEscalatedMessage is a DerivedField
			type is MessageField
			if (Escalated)
				return EscalatedMessage
			else
				return blank

		EscalatedMessage is a MessageField
			"Escalated"

	Field Rules

	Conditions
#ifdef module sharedfinance
		CurrentResource
			restricted
			when (actor.agent(Employee).HROrganization 	= Resource.HROrganization
			and	  actor.agent(Employee).Employee 		= Resource)
		EmailAddressEntered
			restricted
			when (Resource.EmployeeWorkEmailAddress entered)
		WorkPhoneEntered
			restricted
			when (Resource.EmployeeWorkTelephone entered)
#endif
		ResourceEntered
			restricted
			when (Resource entered)
		RoleEntered
			restricted
			when (Role entered)
		ApprovalPositionExists
			restricted
			when (ApprovalProcessor.ApprovalPosition entered)
		ApprovalTeamExists
			restricted
			when (ApprovalProcessor.ApproverTeam entered)
#ifdef module sharedfinance
		FinanceResourceExists
			restricted
			when (FinanceResourceRel exists)
#endif

	Relations
		ApprovalProcessorAuditRel
			one-to-many relation to ApprovalProcessorAudit
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ApprovalProcessor   		    = ApprovalProcessor

		WorkAssignmentRel
			one-to-many relation to WorkAssignment
			Field Mapping uses ByPosition
				related.HROrganization			= FinanceEnterpriseGroup.HROrganization
				related.Position				= ApprovalProcessor.ApprovalPosition

#ifdef module sharedfinance
		ApprovalTeamRel
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.FinanceTeam				= ApprovalProcessor.ApproverTeam

		FinanceResourceRel
			one-to-one relation to FinanceResource
			Field Mapping uses symbolic key
				related.HROrganization		= Resource.HROrganization
				related.FinanceResource		= Resource
#endif

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted
