RequisitionStatus is a BusinessClass
    owned by rq
    prefix is RQRS

    Ontology
    	symbolic key is RequisitionStatus
    
    Patterns
    	disable Auditing		
        disable EffectiveDated	
    
    Local Fields
    	LocalDate is Date
        
	Derived Fields
        ClosedMessage                   is a MessageField
        	restricted
            "Closed"
        
        UnreleasedMessage               is a MessageField
        	restricted
            "Unreleased"
        
        NeedsApprovalMessage            is a MessageField
        	restricted
            "Needs_\Approval"
        
        InProcessMessage                is a MessageField
        	restricted
            "In_\Process"
            
        ProcessedMessage                is a MessageField
        	restricted
            "Processed"
        
        RejectedMessage                 is a MessageField
        	restricted
            "Rejected"

		ClosedType is a StringField
            type is Text
            restricted
			ClosedMessage
			
		UnreleasedType is a StringField
            type is Text
            restricted
			UnreleasedMessage
			
		NeedsApprovalType is a StringField
            type is Text
            restricted
			NeedsApprovalMessage
			
		InProcessType is a StringField
            type is Text
            restricted
			InProcessMessage
			
		ProcessedType is a StringField
            type is Text
            restricted
			ProcessedMessage
			
		RejectedType is a StringField
            type is Text
            restricted
			RejectedMessage
			
		Type is a DerivedField
            type is Text
			if (RequisitionStatus.Closed)
				return ClosedType
			else
			if (RequisitionStatus.Unreleased)
				return UnreleasedType
			else
			if (RequisitionStatus.Released)
				return NeedsApprovalType
			else
			if (RequisitionStatus.InProcess)
				return InProcessType
			else
			if (RequisitionStatus.Processed)
				return ProcessedType
			else
			if (RequisitionStatus.Rejected)
				return RejectedType
			else
				return blank

		StatusOfRequisitionsMessage is a LabelField
			restricted
			"StatusOfRequisitions"
		
		StatusCount is a DerivedField
			type is Numeric 5
			return (instance count of RequisitionsRel)
			
		MyStatusCount is a DerivedField
			type is Numeric 5
			return (instance count of MyRequisitionsRel)
			
		MyOneMonthStatusCount is a DerivedField
			type is Numeric 5
			return (instance count of MyOneMonthRequisitionsRel)
						
		MySixMonthStatusCount is a DerivedField
			type is Numeric 5
			restricted
			return (instance count of MySixMonthRequisitionsRel)
			
		LastOneMonth is a DerivedField
			type is Date
			restricted
			LocalDate = current corporate date
			LocalDate month -= 1
			return LocalDate
			
		LastSixMonths is a DerivedField
			type is Date
			restricted
			LocalDate = current corporate date
			LocalDate month -= 6
			return LocalDate
			
	Conditions
		MyRequisitionStatus
			restricted
			when (Company.FinanceEnterpriseGroup.HROrganization = actor.agent(Employee).HROrganization)
			
		NotClosed
			restricted
			when (not RequisitionStatus.Closed)
			
		MyNotClosedRequisitionStatuses
			restricted
			when (MyRequisitionStatus and NotClosed)
			
	Relations
		RequisitionsRel
			one-to-many relation to Requisition
			Field Mapping uses ByRequisitionStatusValue
				related.Company							= Company
				related.RequisitionStatusValue			= RequisitionStatus

		MyRequisitionsRel
			one-to-many relation to Requisition
			Field Mapping uses Set7
				related.Requester						= actor.agent(Employee).Employee
			Instance Selection
				where (related.RequisitionStatusValue	= RequisitionStatus
				and    related.Company					= Company
				and    related.IsValidForActorContext)
				
		MyOneMonthRequisitionsRel
			one-to-many relation to Requisition
			Field Mapping uses Set7
				related.Requester						= actor.agent(Employee).Employee
			Instance Selection
				where (related.RequisitionStatusValue	= RequisitionStatus
				and    related.Company					= Company
				and    related.IsValidForActorContext
				and    related.CreationDate > LastOneMonth)

		MySixMonthRequisitionsRel
			one-to-many relation to Requisition
			Field Mapping uses Set7
				related.Requester						= actor.agent(Employee).Employee
			Instance Selection
				where (related.RequisitionStatusValue	= RequisitionStatus
				and    related.Company					= Company 
				and    related.IsValidForActorContext
				and    related.CreationDate > LastSixMonths)
								
	Actions
		Create is an Action
			restricted
		
		Update is an Action
			restricted
		
		Delete is an Action 
			restricted
		
		Purge is a Purge Action
			restricted
