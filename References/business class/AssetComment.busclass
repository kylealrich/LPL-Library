AssetComment is a BusinessClass
	owned by am
	prefix is ASTC1
	
	Ontology
		symbolic key is AssetComment
		
	Patterns
		implements StaticJava

	Persistent Fields
		Name				is a CommentName
			default label is "Title"
		Type				is AlphaUpper 1
			States
				Addition		value is "A"
				Adjustment		value is "J"
				Disposal		value is "D"
				Transfer		value is "T"
				General			value is "C"
				Source			value is "S"
		Comment				is a NewCommentText
		Attachment			is an AlternateAttachment
		AssetManagementInterfaceResult
			delete ignored
		
	Field Rules
		Name
			required

		Type
			required

	Local Fields
		LocalAttributeCtr   is Numeric 2		
	Sets
		ByType
			Sort Order
				FinanceEnterpriseGroup
				Asset
				Type		
				AssetComment			

        ByAssetManagementInterfaceResult
            indexed
            Sort Order
				FinanceEnterpriseGroup
				AssetManagementInterfaceResult
				Asset
				AssetComment
									
	Conditions
	
		HasAttachment
			restricted
			when (Attachment entered)
		IsSourceComment
			restricted
			when (Type.Source)

		AllowUpdate
			restricted
			when (!Asset.ApprovalStatus.Submitted)
	
	Relations
		AssetRel
			one-to-many relation to Asset
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset    				= Asset		
			Instance Selection
		    	include deleted records
	
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
	
	Actions

		MobileCreateComment is a Create Action
		    Exit Rules
		        invoke BuildTextIndex Asset	
		

		Create is a Create Action
			valid when (AllowUpdate)
			Exit Rules
				invoke BuildTextIndex Asset
								
		Update is an Update Action
			valid when (AllowUpdate)
			Exit Rules
				invoke BuildTextIndex Asset
						
		Delete is a Delete Action
			valid when (AllowUpdate)
		
		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
													
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop


		PurgeAssetComments is a Set Action
			restricted
			Parameters
				PrmCompany	is an AssetCompany
				
			Instance Selection
				include deleted records
				where (AssetRel.Company = PrmCompany)
				
			Action Rules
				Instance Rules
					invoke Purge
					
		PurgeDisposedAssetComments is a Set Action
			restricted
			Parameters
				PrmCompany					is an AssetCompany
				PrmAsset					is an Asset
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmLeaseCompany				is like GeneralLedgerCompany
				PrmLocationName				is like AssetLocation
				PrmDivision					is like AssetDivision
				PrmAssetGroup				is like AssetGroup
				PrmFromDate					is Date
				PrmToDate					is Date
				
			Instance Selection
				include deleted records
				where (Asset.Company = PrmCompany
				and   (PrmAsset not entered or (Asset = PrmAsset and Asset.Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup))
				and   (PrmLeaseCompany not entered or Asset.AssetLease.LeaseCompany = PrmLeaseCompany)
				and   (PrmLocationName not entered or Asset.AssetLocation = PrmLocationName)
				and   (PrmDivision not entered or Asset.AssetDivision = PrmDivision)
				and   (PrmAssetGroup not entered or Asset.AssetGroup = PrmAssetGroup)
				and   (PrmFromDate not entered or Asset.AssetDisposalDate >= PrmFromDate)
				and   (PrmToDate not entered or Asset.AssetDisposalDate <= PrmToDate)
				and   Asset.Status.Disposed)
				
			Action Rules
				Instance Rules
					invoke Purge
		
		Purge is a Purge Action
			restricted
			bypass relational integrity rules
