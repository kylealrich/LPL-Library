WarehouseProofOfDeliveryWHS is a BusinessClass
	owned by wh
	prefix is WPWHS
	
	Ontology
		symbolic key is WarehouseProofOfDeliveryWHS
	
	Patterns
		implements StaticJava
        disable AuditIndex
	
	Persistent Fields
		WarehouseShipment
		CustomerOrder
		IDMDocumentPID is a DocumentPID
		IDMDocumentSequence	is Numeric size 3 
			protected
	
	Field Rules
		WarehouseShipment
			required
			
		CustomerOrder
			required
	
	Relations
		WarehouseShipmentLineRel is a WarehouseShipmentLine set
		
		CustomerOrderAttachmentRel is a CustomerOrderAttachment set

		IDMAdditionalAttributesLinesRel
			one-to-many relation to IDMAdditionalAttributesLines
			Field Mapping uses symbolic key
				related.IDMAdditionalAttributesHeader = "FSM_WarehouseProofOfDelivery"
			Instance Selection
				where(related.IDMAdditionalAttributesHeader.ActivateAdditionalAttributes
				and	  related.ActivateAdditionalAttributes.Active)
	
	Derived Fields
		DerivedFileName is a StringField
			type is Alpha 100
			restricted
			"Warehouse Proof of Delivery for "
			Company
			" "
			InventoryLocation
			" "
			WarehouseShipment
			" "
			IDMDocumentSequence
		
		CustomerOrderAttachmentComment is a DerivedField
			type is Boolean
			if(CustomerOrderAttachmentRel.Comment entered)
				return true
			else
				return false
				
		ShipmentLineDetailsIDM is a DerivedField
			type is Boolean
			return WarehouseShipmentLineRel.ShipmentLineDetailsIDM

		OrderAttachmentOrderEntryComment is a DerivedField
			type is Boolean
			return WarehouseShipmentLineRel.OrderAttachmentOrderEntryComment
	Local Fields
		IDMGenerateDocument
		IDMAttributes		
		IDMXMLDefinition
		AttributeCtr				is Numeric 2
		LocalExecute				is Boolean
	Actions
	
        Create is a Create Action
        	
        	
        Update is an Update Action
        	restricted
        	
		Delete is a Delete Action
			restricted
			
		GenerateIDMDocument is an Instance Action
			restricted
			Parameters
				PrmIDMPrinter				is an IDMPrinter

 			Action Rules
 				increment IDMDocumentSequence by 1
 				initialize IDMGenerateDocument
    			IDMGenerateDocument.IDMXMLDefinition.Busclass		= 	reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName		=	"WarehouseProofOfDeliveryWHSForIDMDocList"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName	=	"WarehouseProofOfDelivery"
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName		= "WarehouseShipmentLineRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName 		= "WarehouseShipmentLineForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1	= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag			= "ShipmentLines"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag			= "ShipmentLine"
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName		= "CustomerOrderAttachmentRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName			= "CustomerOrderAttachmentForIDMList"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1	= 2
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag			= "OrderAttachments"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag			= "OrderAttachment"
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].RelationName		= "WarehouseShipmentLineDetailsRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].ListName			= "WarehouseShipmentLineDetailForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].LevelSection1	= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].LevelSection2	= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].ListTag			= "ShipmentLineDetails"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].ItemTag			= "ShipmentLineDetail"
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].RelationName		= "OrderEntryOrderCommentAttachmentsRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].ListName			= "OrderEntryOrderCommentAttachmentsListForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].LevelSection1	= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].LevelSection2	= 2
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].ListTag			= "OrderCommentAttachments"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].ItemTag			= "OrderCommentAttachment"
    			
    			initialize IDMAttributes
    			
    			IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "InventoryCompany"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue = Company
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "InventoryLocation"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue = InventoryLocation
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "WarehouseShipment"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue = WarehouseShipment
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "InventoryCompanyName"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue = Company.Name
				if (IDMAdditionalAttributesLinesRel exists)
					AttributeCtr = 5
					include IDM.IDMAdditionalAttributes
				
				IDMGenerateDocument.IDMAttributes		= IDMAttributes
				IDMGenerateDocument.TemplateUniqueId	= Company.WarehouseProofOfDeliveryTemplate.IDMUniqueId
				IDMGenerateDocument.DocumentType		= "FSM_WarehouseProofOfDelivery"
    			IDMGenerateDocument.FileName			= DerivedFileName + Company.WarehouseProofOfDeliveryTemplate.DerivedOutputFormat

				if (PrmIDMPrinter entered)
					IDMGenerateDocument.IDMPrinter		= PrmIDMPrinter 

    			IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
    			
    			invoke CreateFromGenerateDocument IDMJob
    				invoked.Actor				= actor
					invoked.Description			= "Warehouse Proof of Delivery"
					invoked.IDMGenerateDocument = IDMGenerateDocument
