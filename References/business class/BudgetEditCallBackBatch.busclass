BudgetEditCallBackBatch is a BusinessClass
	owned by GeneralLedger
	
	prefix is BECBB
	
	Ontology
		symbolic key is BudgetEditCallBackBatch
			
	Patterns
        disable AuditIndex
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		ActionCode
		Success						is Boolean
#ifdef module rq
		BudgetEditCallBackRequisition
#endif
#ifdef module po
		BudgetEditCallBackPurchaseOrder
		BudgetEditCallBackContract
#endif
#ifdef module ap
		BudgetEditCallBackPayables
#endif
#ifdef module cb
		BudgetEditCallBackCBTran
		BudgetEditCallBackCBPayment
#endif

		
	Derived Fields

	Field Rules

	Sets

	Relations

	Conditions
			
	Local Fields
		
	Rule Blocks
																						
	Actions
		Create is an Action
			restricted
		Update is an Action
			restricted
		Delete is an Action
			restricted
		Purge is a Purge Action	
			restricted

		ProcessBudgetEditCallback is a Set Action		
			disable checkpoint
			synchronized on untranslatable:"BudgetEditCallBackBatch_ProcessBudgetEditCallbackThread_<PrmFinanceEnterpriseGroup>"
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmNumberOfThreads		is Numeric 3
					default label is "NumberOfThreads"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmNumberOfThreads
					default to config.BudgetEditCallBackBatchThreads
					default to 4

			Local Fields
				LocalRunGroup	is Numeric 3

			Instance Selection
				where false
		
			Action Rules
				Empty Set Rules
					while (LocalRunGroup < PrmNumberOfThreads)
						invoke ProcessBudgetEditCallbackThread
							resume on error
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= LocalRunGroup
							invoked.PrmNumberOfThreads			= PrmNumberOfThreads
						LocalRunGroup += 1

		ProcessBudgetEditCallbackThread is a Set Action  
			default label is untranslatable
			restricted
			disable checkpoint
			synchronized on untranslatable:"BudgetEditCallBackBatch_ProcessBudgetEditCallbackThread_<PrmFinanceEnterpriseGroup>_<PrmRunGroup>"
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmRunGroup					is Numeric 3
				PrmNumberOfThreads		is Numeric 3

			Parameter Rules

			Instance Selection
				where ((PrmNumberOfThreads	<=1
				or     (BudgetEditCallBackBatch % PrmNumberOfThreads) = PrmRunGroup)		
				and     FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup)

			Sort Order
				
			Action Rules
				Instance Rules
#ifdef module rq
					if (BudgetEditCallBackRequisition entered)
						invoke UpdateBudgetResults BudgetEditCallBackRequisition.Requisition
							if (Success)
								invoked.ParmSuccess = true
#endif
#ifdef module po
					if (BudgetEditCallBackPurchaseOrder entered)
						invoke UpdateBudgetResults BudgetEditCallBackPurchaseOrder.PurchaseOrder
							if (Success)
								invoked.ParmSuccess = true
					if (BudgetEditCallBackContract entered)
						invoke UpdateBudgetResults BudgetEditCallBackContract.Contract
							if (Success)
								invoked.ParmSuccess = true
#endif
#ifdef module ap
					if (BudgetEditCallBackPayables entered)
						invoke UpdateBudgetResults BudgetEditCallBackPayables.PayablesInvoice
							if (Success)
								invoked.ParmSuccess = true
#endif
#ifdef module cb
					if (BudgetEditCallBackCBTran entered)
						invoke UpdateBudgetResults BudgetEditCallBackCBTran.CashLedgerTransaction
							if (Success)
								invoked.ParmSuccess 	= true
								invoked.ParmActionCode 	= ActionCode
					if (BudgetEditCallBackCBPayment entered)
						invoke UpdateBudgetResults BudgetEditCallBackCBPayment.CashLedgerPayment
							if (Success)
								invoked.ParmSuccess 	= true
								invoked.ParmActionCode 	= ActionCode
#endif
					invoke Purge
