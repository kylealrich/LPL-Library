POCloseResult is a BusinessClass
	owned by po
	
	prefix is POCl
	
	Ontology
		symbolic key is POCloseResult
		
	
	Patterns
        disable Auditing
        disable AuditIndex
       	disable EffectiveDated
	
	Persistent Fields
		GeneralLedgerCompanyGroup
		PurchasingCompany
		RunDate										is TimeStamp
		RunTime										is Time 
			protected
		FromDate									is Date
		ToDate										is Date
		PurchaseOrder
		HadOpenAddOnCharge							is Boolean			
			restricted
			protected
		Status										is AlphaUpper size 1
			States
				InProcess							value is "I"
				Processed							value is "P"										
		
	Local Fields
		LocalActor									is an Actor
		
	Derived Fields
		DerivedTotalClosedPurchaseOrderCount is a DerivedField
			type is Numeric 12
			default label is "TotalClosedPurchaseOrder"
			if (not IsResultLine)
				return instance count of ResultLinesRel

	Conditions
		IsResultLine
			when (PurchaseOrder entered)
			
		IsPurchaseOrderExist
			when (PurchaseOrder exists)
			
		HasResult
			when (ResultLinesRel exists)
			
		SecurityGroupAllowsAccess
			when((PurchasingCompany not entered)
				or (PurchasingCompany entered and PurchasingCompany.SecurityGroupAllowsAccess))
			
	Relations
	
		ResultLinesRel
			one-to-many relation to POCloseResult
			Field Mapping uses ResultLines
				related.RunDate								= RunDate
								
	Sets
		ResultHeaders
			Instance Selection
				where (not IsResultLine)
			Sort Order
				RunDate
				RunTime
				GeneralLedgerCompanyGroup
				PurchasingCompany
				
		ResultLines
			Instance Selection
				where (IsResultLine)
			Sort Order
				RunDate
				RunTime
				GeneralLedgerCompanyGroup
				PurchasingCompany
				PurchaseOrder
			
	Field Rules
		RunDate
			default to current timestamp
		
	Actions
		Create is a Create Action
			restricted
			Exit Rules
				if (PurchaseOrder not entered) 
					invoke TransitionToInProcess

		TransitionToInProcess is an Instance Action
			restricted
			Action Rules
				Status = Status.InProcess
				LocalActor = actor
				send notification
					to LocalActor
					description is "Close\Purchase\Order\HeaderInProcess"
					priority is low
					detail is "RefreshToCheckIfProcessComplete."
					linkback(webapp is PurchasingManager navigation is ClosedPurchaseOrderHeaderResult) 
				
			
		TransitionToProcessed is an Instance Action
			restricted
			Action Rules
				Status = Status.Processed
			Exit Rules
				LocalActor = actor
				send notification
					to LocalActor
					description is "Close\Purchase\Order\HeaderResultsAvailableToView"
					priority is low
					linkback(webapp is PurchasingManager navigation is ClosedPurchaseOrderHeaderResult) 
