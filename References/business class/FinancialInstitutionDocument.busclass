FinancialInstitutionDocument is a BusinessClass
	owned by cashmgmt
	prefix is CMFID
	
	Ontology
		part of FinancialInstitution
    		relative key is SeqNumber is a Sequence
	
	Persistent Fields
		Description		is Alpha 250
		Attachment		is an AlternateAttachment

	Local Fields
		LocalAttributeCtr   is Numeric 2
		IDMAttributes
		IDMItem
	
	Field Rules
		SeqNumber
			autosequence
		
		Description
			default to Attachment.Title
				
		Attachment
			required
	
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
	
	Conditions
		HasAttachment
			restricted
			when (Attachment entered)

	Sets
		ByCreationTimeStamp
			Sort Order
				CashManagementGroup
				FinancialInstitution
				create stamp
				SeqNumber

	Actions

		Create is a Create Action

		Delete is a Delete Action

		Update is an Update Action

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop

		UpdateFinancialInstitutionDocumentIDMAttributes is a Set Action
			default label is untranslatable
			restricted

			Instance Selection
				where (Attachment.FSMAttachmentIDM.IDMUniqueId entered)

			Action Rules
				Instance Rules
					invoke UpdateIDMAttributes
						resume on error

		UpdateIDMAttributes is an Instance Action
			default label is untranslatable
			restricted

			Local Fields
				LocalExecute		is Boolean

			Action Rules
				
				IDMItem.DocumentType = "FSM_FinancialInstitution"

				IDMItem.IDMUniqueId = Attachment.FSMAttachmentIDM.IDMUniqueId

				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName  	= "SeqNumber"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue  	= SeqNumber

				IDMItem.IDMAttributes = IDMAttributes
				IDMItem.OverrideUser  = true
				
				LocalExecute = IDMItem.Update
