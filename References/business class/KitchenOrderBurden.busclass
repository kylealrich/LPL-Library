KitchenOrderBurden is a BusinessClass
	owned by recipe
	prefix is KOB
	
	Ontology
		symbolic key is KitchenOrderBurden
		
	Patterns
		disable AuditIndex
	
	Persistent Fields
		BurdenCode
		Vendor
		FlatAmount						is an InternationalAmount
		AmountPerUnit					is an InternationalAmount
		PerBurdenUnitQuantity			is Decimal size 13.2
		PerBurdenUnitMultiplier			is Decimal size 4.1
		PercentageOfInventoryCost		is Percent size 5.2		
		EstimatedBurdenCost				is an InternationalAmount
		ActualFlatAmount				is an InternationalAmount
		ActualAmountPerUnit				is an InternationalAmount
		ActualPerBurdenUnitQuantity		is Decimal size 13.2
		ActualPerBurdenUnitMultiplier	is Decimal size 4.1
		ActualPercentageOfInventoryCost	is Percent size 5.2
		ActualBurdenCost				is an InternationalAmount
		
	Conditions
		IsAmountPerBurdenUnit
			restricted
			when (BurdenCode.BurdenType.AmountPerBurdenUnit)

		IsAmountPerFinishedGoodsUnit
			restricted
			when (BurdenCode.BurdenType.AmountPerFinishedGoodsUnit)

		IsFlatAmountBurden
			restricted
			when (BurdenCode.BurdenType.FlatAmount)

		IsPercentOfFinishedGoodsInventoryCost
			restricted
			when (BurdenCode.BurdenType.PercentOfFinishedGoodsInventoryCost)

		IsAmountPerFinishedGoodsOrPerBurdenUnit
			restricted
			when (IsAmountPerFinishedGoodsUnit
			or    IsAmountPerBurdenUnit)

	Derived Fields
		DerivedEstimatedRecipeItemCost is a DerivedField
			type is like InternationalCost
			restricted
			return (KitchenOrder.EstimatedIngredientTotal + KitchenOrder.EstimatedComponentTotal + KitchenOrder.EstimatedPackagingTotal)

		DerivedActualRecipeItemCost is a DerivedField
			type is like InternationalCost
			restricted
			return (KitchenOrder.ActualIngredientTotal + KitchenOrder.ActualComponentTotal + KitchenOrder.ActualPackagingTotal)
		
		DerivedEstimatedBurdenCost is a DerivedField
			type is like InternationalCost
			restricted
			if (IsFlatAmountBurden)
				return FlatAmount
			else
			if (IsAmountPerFinishedGoodsUnit)
				return (AmountPerUnit * KitchenOrder.PlanTotalYieldQuantity)
			else
			if (IsAmountPerBurdenUnit)
				return (AmountPerUnit * PerBurdenUnitQuantity * PerBurdenUnitMultiplier)
			else
			if (IsPercentOfFinishedGoodsInventoryCost)
				return (PercentageOfInventoryCost * (DerivedEstimatedRecipeItemCost))
				
		DerivedActualBurdenCost is a DerivedField
			type is like InternationalCost
			restricted
			if (IsFlatAmountBurden)
				return FlatAmount
			else
			if (IsAmountPerFinishedGoodsUnit)
				return (AmountPerUnit * KitchenOrder.ActualTotalYieldQuantity)
			else
			if (IsAmountPerBurdenUnit)
				return (AmountPerUnit * PerBurdenUnitQuantity * PerBurdenUnitMultiplier)
			else
			if (IsPercentOfFinishedGoodsInventoryCost)
				return (PercentageOfInventoryCost * (DerivedActualRecipeItemCost))	
	
		DerivedDeltaEstimatedBurdenAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (action type.Create)
				return EstimatedBurdenCost
			else
				if (action type.Delete)
					return (EstimatedBurdenCost * -1)
				else
					if (EstimatedBurdenCost changed)
						return (EstimatedBurdenCost - old EstimatedBurdenCost)
						
		DerivedDeltaActualBurdenAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (action type.Create)
				return ActualBurdenCost
			else
				if (action type.Delete)
					return (ActualBurdenCost * -1)
				else
					if (EstimatedBurdenCost changed)
						return (ActualBurdenCost - old ActualBurdenCost)
					
	Field Rules
		BurdenCode
			required
			cannot be changed
			
		FlatAmount
			initial value is BurdenCode.FlatAmount
			if (IsFlatAmountBurden)
				default to BurdenCode.FlatAmount
				required
					"MustEnterFlatAmountForBurdenType:<BurdenCode.BurdenType>"
			else
				cannot be entered
					"FlatAmountIsNotValidForBurdenType:<BurdenCode.BurdenType>"
				
		AmountPerUnit
			initial value is BurdenCode.AmountPerUnit
			if (IsAmountPerFinishedGoodsUnit
			or  IsAmountPerBurdenUnit)
				default to BurdenCode.AmountPerUnit
				required
					"MustEnterAmountPerUnitForBurdenType:<BurdenCode.BurdenType>"
			else
				cannot be entered
					"AmountPerUnitIsNotValidForBurdenType:<BurdenCode.BurdenType>"
		
		PerBurdenUnitQuantity
			initial value is BurdenCode.PerBurdenUnitQuantity
			if (IsAmountPerBurdenUnit)
				default to BurdenCode.PerBurdenUnitQuantity
				required
					"MustEnterAmountPerBurdenUnitForBurdenType:<BurdenCode.BurdenType>"										
			else
				cannot be entered
					"PerBurdenUnitQuantityIsNotValidForBurdenType:<BurdenCode.BurdenType>"
			
		PerBurdenUnitMultiplier
			initial value is BurdenCode.PerBurdenUnitMultiplier
			if (IsAmountPerBurdenUnit)
				default to BurdenCode.PerBurdenUnitMultiplier
			else
				cannot be entered
					"PerBurdenUnitMultiplierIsNotValidForBurdenType:<BurdenCode.BurdenType>"
		
		PercentageOfInventoryCost
			initial value is BurdenCode.PercentageOfInventoryCost
			if (IsPercentOfFinishedGoodsInventoryCost)
				default to BurdenCode.PercentageOfInventoryCost
			else
				cannot be entered
					"PercentageOfInventoryCostIsOnlyRequiredForBurdenType:<BurdenCode.BurdenType>"
					
		EstimatedBurdenCost
			if (IsFlatAmountBurden)
				EstimatedBurdenCost = FlatAmount
			else
			if (IsAmountPerFinishedGoodsUnit)
				EstimatedBurdenCost = (AmountPerUnit * KitchenOrder.PlanTotalYieldQuantity)
			else
			if (IsAmountPerBurdenUnit)
				EstimatedBurdenCost = (AmountPerUnit * PerBurdenUnitQuantity * PerBurdenUnitMultiplier)
			else
			if (IsPercentOfFinishedGoodsInventoryCost)
				EstimatedBurdenCost = (PercentageOfInventoryCost * (DerivedEstimatedRecipeItemCost))	
				
		ActualBurdenCost
			if (KitchenOrder.IsPreparing)
				if (IsFlatAmountBurden)
					ActualBurdenCost = FlatAmount
				else
				if (IsAmountPerFinishedGoodsUnit)
					ActualBurdenCost = (AmountPerUnit * KitchenOrder.ActualTotalYieldQuantity)
				else
				if (IsAmountPerBurdenUnit)
					ActualBurdenCost = (AmountPerUnit * PerBurdenUnitQuantity * PerBurdenUnitMultiplier)
				else
				if (IsPercentOfFinishedGoodsInventoryCost)
					ActualBurdenCost = (PercentageOfInventoryCost * (DerivedActualRecipeItemCost))	
		
	Rule Blocks
		CreateDistributionsForBurden
			invoke Create InventoryTransactionLineDistribution
				fill in fields from this instance
				invoked.System												= "IC"
				invoked.TransactionSystemCode								= PrmTransactionSystemCode
				invoked.InventoryTransaction								= PrmInventoryTransaction
				invoked.InventoryTransactionLine							= PrmInventoryTransactionLine
				invoked.PostingType											= LocalPostingType
				invoked.PostingDate											= current corporate date
				invoked.GLFinanceCodeBlock									= LocalDistributionAccount
				invoked.GLTransactionAmount									= LocalAmount
				invoked.CurrencyCode										= Company.Currency
				invoked.TransactionDate										= current corporate date
				invoked.GeneralLedgerEvent									= "IC"
				invoked.DocumentNumber										= KitchenOrder
				invoked.BurdenCode											= BurdenCode
				initialize invoked.Status
		
		UpdateKitchenOrderBurdenTotals
			invoke CalculateHeaderBurdenTotal KitchenOrder
				invoked.PrmEstimatedBurdenTotal				= DerivedDeltaEstimatedBurdenAmount
				invoked.PrmActualBurdenTotal				= DerivedDeltaActualBurdenAmount
							
	Actions
		Create is a Create Action
			valid when (KitchenOrder.Status.Open)
			Exit Rules
				include UpdateKitchenOrderBurdenTotals
				
		Update is an Update Action
			valid when (KitchenOrder.Status.Open)
			Exit Rules
				include UpdateKitchenOrderBurdenTotals

		RecalculateBurden is an Instance Action
			restricted
			Action Rules
				if (KitchenOrder.IsOpen)
					EstimatedBurdenCost 		= DerivedEstimatedBurdenCost
				else
					if (KitchenOrder.IsPreparing)
						ActualBurdenCost 		= DerivedActualBurdenCost
				
		Delete is a Delete Action
			valid when (KitchenOrder.Status.Open)
			Entrance Rules
				include UpdateKitchenOrderBurdenTotals
		
		
