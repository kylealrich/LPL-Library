MatchPurchaseOrderInvoiceImport is a BusinessClass
    owned by ma
    prefix is MPOII
    sql name is MatchPOInvoiceImport

    Ontology
        symbolic key is MatchPurchaseOrderInvoiceImport

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
    	RunGroup
        Company						  is a MatchCompany
        Vendor
        OldVendor
        Invoice
        Suffix
        EDINumber
            classic name is EDI-NBR
        PurchaseOrder
        InterfacedPurchaseOrder			is like PurchaseOrderImport
        InterfacedPOCode				is like POCode
        RecordInError                   is Boolean

	Local Fields
		NewMatchPurchaseOrderInvoice  is a MatchPurchaseOrderInvoice view

		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150

		LocalResultID					is like PayablesInvoiceInterfaceResult

		LocalPurchaseOrder				is like PurchaseOrder
		CheckCompany					is like Company
		CheckPurchaseOrder				is like PurchaseOrder
			
	Context Fields
		ContextMatchInvoice is a MatchInvoiceImport

	Field Rules
		RunGroup
			required
			default to ContextMatchInvoice.RunGroup
			initial value is ContextMatchInvoice.RunGroup
	
		Invoice
			required
			initial value is ContextMatchInvoice.Invoice
		
		EDINumber
			initial value is ContextMatchInvoice.EDINumber

		Vendor
			initial value is ContextMatchInvoice.Vendor
			
		OldVendor
			initial value is ContextMatchInvoice.OldVendor
			
		Suffix
			initial value is ContextMatchInvoice.Suffix
				
		PurchaseOrder
			if (InterfacedPurchaseOrder not entered)
				required
					"PurchaseOrderOrInterfacedPurchaseOrderRequired"
					
	Derived Fields
		SomeMessage is a MessageField
			restricted
			"SomeMessage"

    	NumericInterfacedPurchaseOrder is a DerivedField
    		type is like PurchaseOrder
    		if (InterfacedPurchaseOrder is numeric)
    			return InterfacedPurchaseOrder

		DerivedPurchaseOrder is a DerivedField
			type is like PurchaseOrder

			if  (PurchaseOrder entered)
				return PurchaseOrder
			else
			if (InterfacedPurchaseOrder entered)
				if  (InterfacedPurchaseOrderRel exists)
					return InterfacedPurchaseOrderRel.PurchaseOrder
				else
					CheckCompany = Company
					CheckPurchaseOrder = NumericInterfacedPurchaseOrder
					if  (CheckPurchaseOrderRel exists)
						return NumericInterfacedPurchaseOrder
			else
				return MatchInvoiceImportRel.PurchaseOrder	
		 		

    Conditions
						
    Relations
        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
                related.Company			 				= Company

        MatchInvoiceImportRel
            one-to-one relation to MatchInvoiceImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix

        PayablesInvoiceDetailImportRel
            one-to-many relation to PayablesInvoiceDetailImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
			Instance Selection
				where (related.PurchaseOrder 			= PurchaseOrder
				and    related.InterfacedPurchaseOrder 	= InterfacedPurchaseOrder
				and    related.InterfacedPOCode			= InterfacedPOCode)

        PayablesInvoiceAddOnChargeImportRel
            one-to-many relation to PayablesInvoiceAddOnChargeImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
			Instance Selection
				where (related.PurchaseOrder 			= PurchaseOrder
				and    related.InterfacedPurchaseOrder 	= InterfacedPurchaseOrder
				and    related.InterfacedPOCode			= InterfacedPOCode)

		LocalInterfaceResultsRel
			one-to-one relation to PayablesInvoiceInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
				related.PayablesInvoiceInterfaceResult	= LocalResultID

		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
                related.Company                     = Company
                related.PurchaseOrderImport 		= InterfacedPurchaseOrder
                related.POCode						= InterfacedPOCode

        CheckPurchaseOrderRel
            one-to-one relation to PurchaseOrder
            Field Mapping uses symbolic key
                related.Company							= CheckCompany
                related.PurchaseOrder					= CheckPurchaseOrder 


	Sets

        ByRunGroup
            indexed
            Sort Order
                RunGroup
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
                PurchaseOrder
                MatchPurchaseOrderInvoiceImport

		ByCompanyVendorInvoice
            indexed
            Sort Order
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
                PurchaseOrder
				MatchPurchaseOrderInvoiceImport
										
	Create Rules
		constraint (MatchInvoiceImportRel exists)
			"InterfaceInvoiceDoesNotExist"
			
	Actions
		Create is a Create Action
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
					
		CreateNoRules is a Create Action
			restricted
			bypass field rules
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder

		
		Update is an Update Action
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder

		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules
				RunGroup = NewRunGroup
					
		ChangeSecondaryKeyFields is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
		        NewCompany			is a MatchCompany
		        NewVendor			is like Vendor
		        NewOldVendor		is like OldVendor
		        NewInvoice			is like Invoice
		        NewSuffix			is like Suffix
		        NewEDINumber		is like EDINumber
			Action Rules
				RunGroup 			= NewRunGroup
		        Company				= NewCompany
        		Vendor				= NewVendor
		        OldVendor			= NewOldVendor
		        Invoice				= NewInvoice
		        Suffix				= NewSuffix
		        EDINumber			= NewEDINumber
					
		Delete is a Delete Action
					
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		PurchaseOrderLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				PrmVendorGroup				is a VendorGroup		
				PrmVendor					is a Vendor
					context of PrmVendorGroup		
				PrmProcessLevel				is a PayablesProcessLevel
				PrmAuthorityCode			is a PayablesAuthorityCode
					context of PrmVendorGroup		
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				MatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				AllowMatchItemOrVendorItem			is Boolean
		 		AutoCreateDetails					is Numeric 1
		 			States
		 				FromPurchaseOrder				value is 2
		 				FromReceipt						value is 3
		 				FromReceiptThenFromPO			value is 4
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"
			Local Fields
				LocalInterfacedPurchaseOrderCount	is Numeric 10


			Instance Selection
				where (RunGroup	= PrmRunGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (PrmVendor            			  not entered
				or      Vendor	= PrmVendor)
				and	   (PrmVendorGroup					not entered	
				or		Company.PayablesCompany.VendorGroup = PrmVendorGroup))

			Sort Order
				RunGroup
				Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				PurchaseOrder

			Action Rules

				Empty Set Rules
					invoke ContractLoad MatchServiceContractInvoiceImport
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
						invoked.PrmVendor					= PrmVendor
						invoked.PrmProcessLevel				= PrmProcessLevel
						invoked.PrmAuthorityCode			= PrmAuthorityCode
				        invoked.InvoiceProcess				= InvoiceProcess
						invoked.MatchPoint					= MatchPoint
						invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.PrmVendorGroup				= PrmVendorGroup	

				RunGroup Set Rules
						
					Exit Rules

						LocalResultID					= PrmInterfaceRun

						invoke Update LocalInterfaceResultsRel
							invoked.InterfacedPurchaseOrdersCount	+= LocalInterfacedPurchaseOrderCount

						invoke ContractLoad MatchServiceContractInvoiceImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmVendor					= PrmVendor
							invoked.PrmProcessLevel				= PrmProcessLevel
							invoked.PrmAuthorityCode			= PrmAuthorityCode
					        invoked.InvoiceProcess				= InvoiceProcess
							invoked.MatchPoint					= MatchPoint
							invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.PrmVendorGroup				= PrmVendorGroup	

				Instance Rules
					if     ((PrmProcessLevel            			  not entered
					or       MatchInvoiceImportRel.ProcessLevel		= PrmProcessLevel)
					and     (PrmAuthorityCode       			      not entered
					or       MatchInvoiceImportRel.AuthorityCode	= PrmAuthorityCode)
	                and      PayablesCompanyRel.Company.FinanceEnterpriseGroup         = PrmFinanceEnterpriseGroup
					and     (!MatchInvoiceImportRel.RecordInError))


						RecordInError 						= false
						ErrorOccurred						= false
						LocalPurchaseOrder					= DerivedPurchaseOrder



						if  (DerivedPurchaseOrder = MatchInvoiceImportRel.PayablesInvoice.FirstPurchaseOrder)
							invoke FastDelete
						else
							LocalInterfacedPurchaseOrderCount	+= 1					
							if (!ErrorOccurred)
								invoke Create MatchPurchaseOrderInvoice
									assign result to NewMatchPurchaseOrderInvoice
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.Company						= Company
									invoked.PayablesInvoice				= MatchInvoiceImportRel.PayablesInvoice
									invoked.PurchaseOrder				= DerivedPurchaseOrder
									invoked.Vendor						= MatchInvoiceImportRel.DerivedVendor
			
							if (ErrorOccurred)
								RecordInError = true
								invoke SetError MatchInvoiceImportRel
									invoked.PrmErrorMessage			= LocalErrorMessage
							else
								if (MatchInvoiceImportRel.CreateDetails entered
								and  !PayablesInvoiceDetailImportRel exists
								and  !PayablesInvoiceAddOnChargeImportRel exists)
									display "CreateDetails"
									if (MatchInvoiceImportRel.CreateDetails.FromPurchaseOrder)
										invoke CreateDetailFromPurchaseOrder NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
											resume on error
									else
									if (MatchInvoiceImportRel.CreateDetails.FromReceipt)
										invoke CreateDetailFromReceipt NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
											resume on error
									else
									if ((MatchInvoiceImportRel.CreateDetails.FromReceiptThenFromPO) 
									and (MatchInvoiceImportRel.PayablesInvoice.OpenReceiptsPool2Rel exists
									or   MatchInvoiceImportRel.PayablesInvoice.OpenReceiptsPool3Rel exists))
										invoke CreateDetailFromReceipt NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
											resume on error
									else
										invoke CreateDetailFromPurchaseOrder NewMatchPurchaseOrderInvoice.MatchPurchaseOrderInvoice
											resume on error	
								invoke FastDelete
	







