VatUsageCode is a BusinessClass
    owned by tx
    prefix is TVC
    classic name is TXVATRPTCD

    Ontology
        symbolic key is VatUsageCode
            classic set name is TVCSET1

            classic name for VatUsageCode.VatCode is TAX-USAGE-CD
            classic name for VatReportIDForCountry.VatReportID is VAT-RPT-ID

    Patterns
        implements StaticJava
        disable AuditIndex
        implements ContextualParent

    Transient Fields
		TaxCodeSelect		is a TaxCode
        	default label is "TaxCode"
			derive value from DerivedTaxCode		

		TaxUsageCode
        	derive value from DerivedTaxUsageCode	
        VendorGroup
        	derive value from PayablesCompanyRel.VendorGroup
        WithholdingCodeSelect is a IncomeWithholdingCode
        	derive value from DerivedWithholdingCode
        	
		Source
			derive value from VatReportingLineRel.Source 
		InvoiceType
			derive value from VatReportingLineRel.InvoicedOrCreditMemo	 

        TransValueAddedTaxType  is AlphaUpper size 1		
        	default label is "ValueAddedTaxType"
            States
                Domestic value is "D"
                IntraEc  value is "E"
                Import   value is "I"
                Export   value is "X"
          	derive value from DerivedValueAddedTaxType      

        TransGoodsAndServicesInd  is AlphaUpper size 1		
        	default label is "GoodsAndServicesIndicator"
            States
                Services value is "S"
                Goods    value is "G"
          	derive value from DerivedGoodsAndServicesIndicator  
          	                			
    Field Rules
    	TaxCodeSelect			
			if (UseTaxCodes
			and VatUsageCode.VatCode not entered)
				VatUsageCode.VatCode = TaxCodeSelect
				required
		TaxUsageCode
			if (UseTaxUsageCodes
			and VatUsageCode.VatCode not entered)
				VatUsageCode.VatCode = TaxUsageCode
				required	
		WithholdingCodeSelect
			if (UseWithholdingCodes
			and VatUsageCode.VatCode not entered)
				VatUsageCode.VatCode = WithholdingCodeSelect
				required			

	Derived Fields	
		DerivedDescription is a DerivedField
			type is like Description
			if (UseTaxCodes)
				return TaxCodeRel.Description
			if (UseWithholdingCodes)
				return IncomeWithholdingCodeRel.Description		
			else
				return TaxUsageCodeRel.Description



		ReportIDLabel				is a LabelField
    		"ReportIDFor_\Country/\Jurisdiction:<VatReportIDForCountry.VatReportID>"
		CountryCodeLabel			is a LabelField
    		"Country/\JurisdictionCode:<VatReportIDForCountry.CountryCode>"
		TaxCodeLabel				is a LabelField
    		"TaxCode:<VatUsageCode.VatCode>"
		ReportingLineLabel			is a LabelField
    		"ReportingLine:<VatReportingLine>"

		DerivedTaxCode is a DerivedField					
			type is like TaxCode
			restricted
			if (UseTaxCodes
			and VatUsageCode.VatCode entered)
				return VatUsageCode.VatCode
			return blank	
			
		DerivedTaxUsageCode is a DerivedField				
			type is like TaxUsageCode
			restricted
			if (UseTaxUsageCodes
			and VatUsageCode.VatCode entered)
				return VatUsageCode.VatCode
			return blank	
			
		DerivedWithholdingCode is a DerivedField
			type is like TaxCode
			restricted
			if (UseWithholdingCodes
			and VatUsageCode.VatCode entered)
				return VatUsageCode.VatCode
			return blank
			
		DerivedGeneralLedgerCompany is a DerivedField
			type is like GeneralLedgerCompany
			restricted
			return first GeneralLedgerCompanyRel.Company

		DerivedValueAddedTaxType is a DerivedField			
			type is AlphaUpper size 1	 
			restricted
			if (TaxEntityUsesTaxCodeAccounts
			and EntityTaxCodeDetailRel exist)
				return EntityTaxCodeDetailRel.ValueAddedTaxType
			return blank	
							 				
		DerivedGoodsAndServicesIndicator is a DerivedField	
			type is AlphaUpper size 1
			restricted
			if (TaxEntityUsesTaxCodeAccounts
			and EntityTaxCodeDetailRel exist)
				return EntityTaxCodeDetailRel.GoodsAndServicesIndicator
			return blank	
				    		




    Conditions

		UseTaxCodes
			restricted
			when (VatReportingLineRel.TaxUsageIndicator = "T")

		UseTaxUsageCodes
			restricted
			when (VatReportingLineRel.TaxUsageIndicator = "U")
			
		UseWithholdingCodes
			restricted
			when (VatReportingLineRel.TaxUsageIndicator = "W")

		OutputOnly
			restricted
			when (VatReportingLineRel.Source = "O")
			
       	EntityTaxCodeExist		
        	restricted
        	when (VatReportIDForCountry.TaxEntity.UseTaxCodeAccounts
        	and   EntityTaxCodeDetailRel exists)		

       	TaxEntityUsesTaxCodeAccounts	
        	restricted
        	when (VatReportIDForCountry.TaxEntity.UseTaxCodeAccounts)		
        	        		
    Relations

        VatReportingLineRel
            one-to-one relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup					= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity								= VatReportIDForCountry.TaxEntity 	
                related.VatReportIDForCountry.VatReportID 		= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 		= VatReportIDForCountry.CountryCode
				related.VatReportingLine   						= VatUsageCode.VatReportingLine                


       
        TaxCodeRel
        	one-to-one relation to TaxCode
          	Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup	= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup
                related.TaxCode 				= VatUsageCode.VatCode




        TaxUsageCodeRel
        	one-to-one relation to TaxUsageCode
          	Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup	= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup
                related.TaxUsageCode 			= VatUsageCode.VatCode

        IncomeWithholdingCodeRel		
        	one-to-one relation to IncomeWithholdingCode
          	Field Mapping uses symbolic key
		    	related.VendorGroup				= VendorGroup
                related.IncomeWithholdingCode	= VatUsageCode.VatCode


       	VatTransactionsUsingTaxCodesRel
            one-to-many relation to TaxTransaction
            valid when (UseTaxCodes)
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup			= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup 
				related.TaxEntity						= VatReportIDForCountry.TaxEntity
            Instance Selection
                where (related.VatReportIDForCountry	= VatReportIDForCountry
                and    related.VatReportingLine 		= VatUsageCode.VatReportingLine
				and    related.TaxCode					= VatUsageCode.VatCode)



       	VatTransactionsUsingTaxUsageCodesRel
            one-to-many relation to TaxTransaction
            valid when (UseTaxUsageCodes)
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup			= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup 
				related.TaxEntity						= VatReportIDForCountry.TaxEntity
            Instance Selection
                where (related.VatReportIDForCountry	= VatReportIDForCountry
                and    related.VatReportingLine 		= VatUsageCode.VatReportingLine
				and    related.TaxUsageCode				= VatUsageCode.VatCode)
				
       	PayablesInvoiceWithholdingRel
            one-to-many relation to PayablesInvoiceWithholding
            valid when (UseWithholdingCodes)
            Field Mapping uses ByReportableRecords	 
            	related.Reportable						= true
				related.Company							= GeneralLedgerCompanyRel.Company
				related.IncomeWithholdingCode			= VatUsageCode.VatCode




								  
       	EntityTaxCodeDetailRel
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup
                related.TaxEntity				= VatReportIDForCountry.TaxEntity
                related.TaxCode					= VatUsageCode.VatCode
                
		GeneralLedgerCompanyRel
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses ByAccountingEntity
				related.FinanceEnterpriseGroup	= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup
                related.AccountingEntity		= VatReportIDForCountry.TaxEntity
                		
		PayablesCompanyRel
			one-to-one relation to PayablesCompany
			Field Mapping uses symbolic key
				related.Company		= DerivedGeneralLedgerCompany

                               
	Sets


	Actions

		Create is a Create Action
			Entrance Rules
				if (VatUsageCode.VatCode not entered)	
					display "VatCodeNotPopulatedInKeyfield..."	 
					if (UseTaxCodes)
						VatUsageCode.VatCode = TaxCodeSelect
					else
						if (UseWithholdingCodes)
							VatUsageCode.VatCode = WithholdingCodeSelect	
						else
							VatUsageCode.VatCode = TaxUsageCode	

				
			Action Rules

   		Update is an Update Action
						
        Delete is a Delete Action			
