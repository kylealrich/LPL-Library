PaymentApplyDistribution is a BusinessClass
	owned by ar
	prefix is PAD

	Ontology
		symbolic key is PaymentApplyDistribution

	Persistent Fields
		ApplyFundDistributionAccount		is a FinanceCodeBlock
		OriginalCurrency					is a FromCurrency
			default label is "TransactionCurrency"	
		DistributionAmount					is a FinanceCurrencyAmountGroup
		OriginalLastDistributionSequence	is a DistSeq
		OriginalReceivableGLDistribution	is like ReceivableGLDistribution 

	Transient Fields
		TransientSupplyOldFundAppliedAmount is Boolean
		TransientOldFundAppliedAmount is an InternationalAmount

	Relations

		ReceivableGLDistributionRel
			one-to-one relation to ReceivableGLDistribution
			Field Mapping uses Set2
				related.ReceivableCompanyDataGroup.TransactionCompany				 = ReceivableApplicationSelect.TransactionCompany
				related.BatchNumber													 = ReceivableApplicationSelect.ReceivableInvoiceDetailRel.ReceivableInvoiceRel.BatchNumber
				related.TransType													 = ReceivableApplicationSelect.ReceivableInvoiceDetailRel.ReceivableInvoiceRel.ReceivableInvoiceType
				related.Invoice														 = ReceivableApplicationSelect.ReceivableInvoiceDetail.Invoice	
				related.ReceivableCompanyDataGroup.Customer							 = ReceivableApplicationSelect.ReceivableInvoiceDetailRel.ReceivableInvoiceRel.Customer	
				related.LastDistributionSequence									 = OriginalLastDistributionSequence
				related.ReceivableGLDistribution									 = OriginalReceivableGLDistribution

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company


	Conditions
		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	   CompanySecurityGroupMemberRel exists)


	Sets

		ByReceivableGLDistribution
			indexed
			Sort Order
				Company
				ReceivablePaymentHeader
				ReceivablePayment
				ReceivableApplicationSelect
				OriginalLastDistributionSequence
				OriginalReceivableGLDistribution

		PaymentApplyDistributionDescending
			indexed
			Sort Order
				Company
				ReceivablePaymentHeader
				ReceivablePayment
				ReceivableApplicationSelect
				PaymentApplyDistribution descending

	Actions

		Create is a Create Action
			restricted
			Action Rules
				invoke AddToFundAppliedAmount ReceivableGLDistributionRel
					invoked.PrmAmount = DistributionAmount.CurrencyAmount

		Update is an Update Action
			restricted
			Action Rules
				if (TransientSupplyOldFundAppliedAmount)
					invoke AddToFundAppliedAmount ReceivableGLDistributionRel
						invoked.PrmAmount = DistributionAmount.CurrencyAmount - TransientOldFundAppliedAmount

					TransientSupplyOldFundAppliedAmount = false
				else
					invoke AddToFundAppliedAmount ReceivableGLDistributionRel
						invoked.PrmAmount = DistributionAmount.CurrencyAmount - old DistributionAmount.CurrencyAmount

		Delete is a Delete Action
			restricted
			Action Rules 
				invoke AddToFundAppliedAmount ReceivableGLDistributionRel
					invoked.PrmAmount = -1 * DistributionAmount.CurrencyAmount

		Purge is a Purge Action
			restricted
			Action Rules 
				invoke AddToFundAppliedAmount ReceivableGLDistributionRel
					invoked.PrmAmount = -1 * DistributionAmount.CurrencyAmount

