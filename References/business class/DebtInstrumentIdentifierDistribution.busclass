DebtInstrumentIdentifierDistribution is a BusinessClass
	owned by treasury
	prefix is DMISD
	sql name is DebtIdDistribution

	Ontology
		symbolic key is DebtInstrumentIdentifierDistribution

	Persistent Fields								
		DistributionDate			is Date
		PostingDate					is Date
		DistributionAccount			is a FinanceCodeBlock 	
			default label is "AccountInformation"
		DistributionAmount			is an InternationalAmount	
		Status						is Numeric 1
    		States
    			Entered		value is 0
    			Released	value is 1
    			Posted		value is 2    
    	Comment						is Text
    	FinanceJournalInterface
    		delete ignored
    	FinanceJournalInterfaceLine
    		delete ignored
		
	Context Fields
		DistributionDateRange		is a DateRange
	
	Transient Fields
    	GLFromEntity			 	is a AccountingEntity
    		derive value from DebtInstrument.LegalEntityGroup.LegalEntity.AccountingEntity
		GLFinanceCodeBlock		 	is a  TransactionCodeBlock				 		
			derive value from GLTransactionDetailRel.FinanceCodeBlock
			default label is "DistributionAccount"
		GLTransactionAmount		 	is a  CurrencyAmount			
			derive value from GLTransactionDetailRel.TransactionAmount	
			default label is "DistributionAmount"
        GLBaseAmount			 	is a  FinanceCurrencyAmount
        	derive value from GLTransactionDetailRel.ReportCurrencyAmount
		TransientRunGroup 		 	is AlphaUpper size 30
			derive value from GLTransactionDetailRel.JournalizeGroup
			
	Field Rules
		DistributionAccount
			force default to GLTransactionDetailRel.FinanceCodeBlock
		
		DistributionAmount
			force default to GLTransactionAmount
		
	Sets
		ByDistributionStatus
			Sort Order
				CashManagementGroup
				DebtInstrument
				DebtInstrumentIdentifier
				Status
				PostingDate
				DebtInstrumentIdentifierDistribution
				
		ByDefaultSearchOrder
			Sort Order
				CashManagementGroup
				DebtInstrument
				DebtInstrumentIdentifier
				DebtInstrumentIdentifierDistribution
				PostingDate
				Status
				DistributionAccount.Ledger
				DistributionAccount.ToAccountingEntity
				DistributionAccount.AccountingUnit
				DistributionAccount.GeneralLedgerChartAccount
				DistributionAccount.Project
				DistributionAmount
				FinanceJournalInterface
				
	Conditions
		DistributionSearchCondition
			restricted
            when (!DistributionDateRange entered
            or   (DistributionDateRange entered
            and   DistributionDate within DistributionDateRange))
		
		CanViewCommentLink
			restricted
			when (DistributionExists
			and  (Status.Entered
			or    Comment entered))
		
		DistributionExists
			restricted
			when (DebtInstrumentIdentifierDistribution exists)
	
	Relations
		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 
				related.OriginatingTransaction = reference to this instance   
							
  	Actions
		UnreleaseAllIdentifierDistributions is a Set Action
			restricted
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
				PrmDebtInstrument			is a DebtInstrument
				PrmDebtInstrumentIdentifier	is a DebtInstrumentIdentifier
					
			Parameter Rules
				PrmCashManagementGroup		required
				PrmDebtInstrument			required
				PrmDebtInstrumentIdentifier	required
				
			Instance Selection
				where (CashManagementGroup = PrmCashManagementGroup
				and    DebtInstrument = PrmDebtInstrument
				and    PrmDebtInstrumentIdentifier = PrmDebtInstrumentIdentifier
				and    Status.Released)
			
			Action Rules
				Instance Rules
					invoke UnreleaseDistribution Released DebtInstrumentIdentifierDistribution
















































































































		Purge is a Purge Action
			restricted
			
  	StateCycles
		StatementDistributionLifeCycle is a StateCycle
			state field is Status
		
			Entered is a State
		  		Create is a Create Action
		
		  		Update is an Update Action
		
		  		Delete is a Delete Action
		  			Entrance Rules
						if (GLTransactionDetailRel exist) 
							invoke Delete GLTransactionDetailRel
				
		  		ReleaseDistribution is an Instance Action
		  			restricted
		  			Action Rules
		  				make transition to Released
		  		
		  	Released is a State
		  		UnreleaseDistribution is an Instance Action
		  			restricted
		  			Action Rules
		  				make transition to Entered

		  		PostDistribution is an Instance Action
		  			restricted
		  			Action Rules
		  				make transition to Posted
		  				
		  	Posted is a State
		  		UpdateGLAccountInformation is an Update Action
		  			restricted
		  			
		  		UnpostDistribution is an Instance Action
		  			restricted
		  			Action Rules
				    	initialize FinanceJournalInterface
				    	initialize FinanceJournalInterfaceLine
						initialize PostingDate
		  				make transition to Released
