ClosePeriodTaskDocumentation is a BusinessClass
	owned by closemgmt
	prefix is CPTD
	
	Ontology
		symbolic key is ClosePeriodTaskDocumentation
		
	Persistent Fields	
		Description				is Alpha 60
		Notes	 				is Text
		Attachment				is an AlternateAttachment
		Link					is an URL
		Author	 				is a FinanceResource 	
		
	Local Fields
		LocalAttributeCtr   	is Numeric 2
									
	Field Rules
		Attachment
			if (!Link entered
			and !Notes entered)
				required
					"MustEnterAnAttachment,Link,OrNotesWhenAddingProcessDocumentation"
		Author
			initial value is actor.agent(Employee).Employee	
			force default to actor.agent(Employee).Employee	
			cannot be changed

	Derived Fields

	Conditions
		IAmTheAuthor
			restricted
			when (Author = actor.agent(Employee).Employee)
		HasNotes
			restricted
			when (Notes entered)
		HasAttachment
			restricted
			when (Attachment entered)
		HasLink
			restricted
			when (Link entered)
		AllowDelete
			restricted
			when (IAmTheAuthor)
					
	Relations

	Sets
		ByUpdateStamp
			Sort Order
				ClosePeriodTask
				update stamp descending
				ClosePeriodTaskDocumentation			 
						
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment
	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Purge Action
			valid when (AllowDelete)

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
