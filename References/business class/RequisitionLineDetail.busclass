RequisitionLineDetail is a BusinessClass
    owned by rq
    prefix is RQD
    classic name is RQLINEDTL

    Ontology
        symbolic key is RequisitionLineDetail
            classic set name is RQDSET0
            classic name for RequisitionLineDetail.UnitOfMeasure is UOM
            classic name for Requisition is REQ-NUMBER
            classic name for RequisitionLine is LINE-NBR

    Patterns
        implements ContextualParent
        enable explicit context override
        implements StaticJava
        disable AuditIndex

    Persistent Fields

		InventoryLocation			is like InventoryLocation
        LotSerial     is an ItemSerialNumber 
        UOMMultiplier
            classic name is UOM-MULT
        Quantity
        CatchWeightQuantity is a Quantity

	Context Fields
		FromCompanyLocationBin
		
	Local Fields
		LocalWarehouseShipment 		is like WarehouseShipment
		LocalWarehouseShipmentLine	is like WarehouseShipmentLine
		LocalSerialIssued			is Boolean
		LocalLotIssued				is Boolean
		LocalIssuedLotQuantity		is like Quantity
    
    Transient Fields
    	TransientBin 				is a Bin
    		context of FromCompanyLocationBin.FromCompany
    		context of FromCompanyLocationBin.FromLocation
    		derive value from RequisitionLineDetail.Bin
    		default label is "Bin"
    	TransientLot				is an ItemLot
    		context of FromCompanyLocationBin.FromCompany
    		context of FromCompanyLocationBin.FromLocation
    		derive value from RequisitionLineDetail.Lot
    		default label is "Lot"
    	TransientSerial				is an ItemSerialNumber
    		context of FromCompanyLocationBin.FromCompany
    		derive value from RequisitionLineDetail.Serial
    		default label is "Serial"
    
    Relations
    	BinRel
    		one-to-many relation to Bin
    		Field Mapping uses symbolic key
    			related.Company							= RequisitionLine.FromCompanyLocationBin.FromCompany
    			related.InventoryLocation				= RequisitionLine.FromCompanyLocationBin.FromLocation
    			related.Bin								= RequisitionLineDetail.Bin
    
    	BinsRel
    		one-to-many relation to Bin
    		Field Mapping uses symbolic key
    			related.Company							= RequisitionLine.FromCompanyLocationBin.FromCompany
    			related.InventoryLocation				= RequisitionLine.FromCompanyLocationBin.FromLocation
 
     	ItemLotRel
    		one-to-one relation to ItemLot
    		Field Mapping uses symbolic key
    			related.Company							= RequisitionLine.FromCompanyLocationBin.FromCompany
    			related.InventoryLocation				= RequisitionLine.FromCompanyLocationBin.FromLocation
    			related.Item							= RequisitionLine.Item
    			related.ItemLot							= RequisitionLineDetail.Lot
    	
    	ItemLotsRel
    		one-to-many relation to ItemLot
    		Field Mapping uses symbolic key
    			related.Company							= RequisitionLine.FromCompanyLocationBin.FromCompany
    			related.InventoryLocation				= RequisitionLine.FromCompanyLocationBin.FromLocation
    			related.Item							= RequisitionLine.Item

    	ItemSerialNumberRel
    		one-to-one relation to ItemSerialNumber
    		Field Mapping uses symbolic key
    			related.Company							= RequisitionLine.FromCompanyLocationBin.FromCompany
    			related.Item							= RequisitionLine.Item
    			related.ItemSerialNumber				= RequisitionLineDetail.Serial
    
    	ItemSerialNumbersRel
    		one-to-many relation to ItemSerialNumber
    		Field Mapping uses symbolic key
    			related.Company							= RequisitionLine.FromCompanyLocationBin.FromCompany
    			related.Item							= RequisitionLine.Item

        ItemLocationRel
            one-to-one relation to ItemLocation
            required
            Field Mapping uses symbolic key
                related.Company           = RequisitionLine.FromCompanyLocationBin.FromCompany
                related.InventoryLocation = RequisitionLine.FromCompanyLocationBin.FromLocation
                related.Item              = RequisitionLine.Item                
        
        RequisitionLineRel
        	one-to-one relation to RequisitionLine
        	Field Mapping uses symbolic key
        		related.Company			= RequisitionLine.Company
        		related.Requisition		= Requisition
        		related.RequisitionLine	= RequisitionLine
        
		LotRequisitionLineDetailRel	
			one-to-one relation to RequisitionLineDetail
			Field Mapping uses symbolic key
			    related.Company								= RequisitionLine.Company
                related.Requisition							= Requisition
                related.RequisitionLine						= RequisitionLine
				related.RequisitionLineDetail.Bin			= RequisitionLineDetail.Bin
				related.RequisitionLineDetail.Lot			= RequisitionLineDetail.Lot
				related.RequisitionLineDetail.Sublot		= RequisitionLineDetail.Sublot		
				related.RequisitionLineDetail.Serial		= RequisitionLineDetail.Serial				
				related.RequisitionLineDetail.UnitOfMeasure	= RequisitionLine.EnteredUOM
		
		BinRequisitionLineDetailRel	
			one-to-one relation to RequisitionLineDetail
			Field Mapping uses symbolic key
				related.Company								= RequisitionLine.Company
				related.Requisition							= Requisition
				related.RequisitionLine						= RequisitionLine
				related.RequisitionLineDetail.Bin			= RequisitionLineDetail.Bin
				related.RequisitionLineDetail.Lot			= blank
				related.RequisitionLineDetail.Sublot		= blank
				related.RequisitionLineDetail.Serial		= blank				
				related.RequisitionLineDetail.UnitOfMeasure	= RequisitionLine.EnteredUOM
				

		InventoryTransactionLineDetailRel
			one-to-many relation to InventoryTransactionLineDetail
			Field Mapping uses Set2
				related.Company										= Company
            	related.InventoryLocation							= RequisitionLine.FromCompanyLocationBin.FromLocation	

            	related.TransactionSystemCode						= "RQ"

            	related.InventoryTransaction						= RequisitionLine.DocumentField
            	related.InventoryTransactionLine.WarehouseShipment	= 0
            	related.Item										= blank	
            	related.Lot											= RequisitionLineDetail.Lot
            	related.Serial										= blank

            	related.Bin											= blank
            	related.InventoryTransactionLine.LineNumber			= 0
            	related.InventoryTransactionLine.ComponentSequence	= 0
            	related.InventoryTransactionLineDetail				= 0

		InventoryTransactionLineDetailExistRel
			one-to-many relation to InventoryTransactionLineDetail
			Field Mapping uses Set2
				related.Company									= Company
            	related.InventoryLocation						= RequisitionLine.FromCompanyLocationBin.FromLocation	
            	related.TransactionSystemCode					= "RQ"
            	related.InventoryTransaction					= RequisitionLine.DocumentField
			Instance Selection
				where (related.Lot								= RequisitionLineDetail.Lot
				and related.Serial								= RequisitionLineDetail.Serial
				and	related.InventoryTransactionLine.LineNumber = RequisitionLine
				and related.Item								= RequisitionLine.Item)
					 
		WarehouseShipmentLineSerialDetailsRel
			one-to-many relation to WarehouseShipmentLineDetail
			Field Mapping uses symbolic key
				related.Company										= Company
				related.InventoryLocation							= RequisitionLine.FromCompanyLocationBin.FromLocation
				related.WarehouseShipment							= LocalWarehouseShipment
				related.WarehouseShipmentLine						= LocalWarehouseShipmentLine
			Instance Selection
				where (related.WarehouseShipmentLineDetail.Serial	= RequisitionLineDetail.Serial)
				
		WarehouseShipmentLineLotDetailsRel
			one-to-many relation to WarehouseShipmentLineDetail
			Field Mapping uses symbolic key
				related.Company										= Company
				related.InventoryLocation							= RequisitionLine.FromCompanyLocationBin.FromLocation
				related.WarehouseShipment							= LocalWarehouseShipment
				related.WarehouseShipmentLine						= LocalWarehouseShipmentLine
			Instance Selection
				where (related.WarehouseShipmentLineDetail.Lot		= RequisitionLineDetail.Lot
				and    related.WarehouseShipmentLineDetail.Sublot	= RequisitionLineDetail.Sublot)			
			
		ShipmentForReturnDetailRel
			one-to-many relation to WarehouseShipmentLineDetail
			Field Mapping uses symbolic key
				related.Company												= RequisitionLine.FromCompanyLocationBin.FromCompany
				related.InventoryLocation									= RequisitionLine.FromCompanyLocationBin.FromLocation
				related.WarehouseShipment									= LocalWarehouseShipment
				related.WarehouseShipmentLine								= LocalWarehouseShipmentLine
			Instance Selection
				where  (related.WarehouseShipmentLineDetail.Bin				= RequisitionLineDetail.Bin
				and 	related.WarehouseShipmentLineDetail.Serial			= RequisitionLineDetail.Serial
				and		related.WarehouseShipmentLineDetail.Lot				= RequisitionLineDetail.Lot
				and		related.WarehouseShipmentLineDetail.Sublot			= RequisitionLineDetail.Sublot)
				
    Conditions






		
		IsLotBin	
			restricted
			when (ItemLocationRel.IsLotTracked
			or    ItemLocationRel.BinTracked)
		
		IsLotOnly
			restricted
			when (RequisitionLineDetail.Lot entered
			and RequisitionLineDetail.Serial not entered)
		
		IsSerialTracked	
			restricted
			when (ItemLocationRel.IsSerialTracked)	
			
		IsLotTracked
			restricted
			when (ItemLocationRel.IsLotTracked)
			
		IsBinTracked
			restricted	
			when (ItemLocationRel.BinTracked)
			
		IsSerialLot
			restricted
			when (IsLotTracked 
			or 	  IsSerialTracked)
			
		IsCatchWeightItem
			restricted
			when (RequisitionLine.Item.IsCatchWeightItem)
			
	Derived Fields
	
		InventoryTransactionLineDetailQuantity is a DerivedField
			type is like Quantity
				precision is RequisitionLine.DerivedNumberOfDecimalsQuantity
			restricted
			if (InventoryTransactionLineDetailRel exist

			and (RequisitionLine = InventoryTransactionLineDetailRel.InventoryTransactionLine.LineNumber)
			and (RequisitionLine.Item = InventoryTransactionLineDetailRel.Item))
				if (InventoryTransactionLineDetailRel.Quantity <= 0)
					return InventoryTransactionLineDetailRel.Quantity * -1
				else
					return InventoryTransactionLineDetailRel.Quantity	
		
		SerialTracked 	is a LabelField
			restricted
			"Serial_Details"
		LotTracked		is a LabelField
			restricted
			"Lot_Details"
		BinTracked		is a LabelField
			restricted
			"Bin_Details"
		
		FeedbackTitle 	is a DerivedField
			type is Text
			restricted
			if (IsSerialTracked)
				return SerialTracked
			if (IsLotTracked)
				return LotTracked
			if (IsBinTracked)
				return BinTracked	 
						 	
	Rule Blocks
		SerialEditData	
			constraint (ItemSerialNumberRel exists)
				"SerialDoesNotExist"

			RequisitionLineDetail.UnitOfMeasure = RequisitionLine.EnteredUOM
			UOMMultiplier						= RequisitionLine.EnteredUOMMultiplier
			Quantity							= 1
			
			invoke Processed.UpdateReturnDetails RequisitionLine
		
		LotEditData		
			constraint (ItemLotRel exists)
				"LotDoesNotExist"
					
			if (RequisitionLineDetail.UnitOfMeasure = blank)
       			RequisitionLineDetail.UnitOfMeasure = RequisitionLine.EnteredUOM
       		
			RequisitionLineDetail.UnitOfMeasure = RequisitionLine.EnteredUOM

			invoke Processed.UpdateReturnDetails RequisitionLine
       
      	BinEditData
			constraint (BinRel exists)
				"BinDoesNotExist"		

       		if (RequisitionLineDetail.UnitOfMeasure = blank)
       			RequisitionLineDetail.UnitOfMeasure = RequisitionLine.EnteredUOM
       			
       		constraint (RequisitionLine.Quantity != 0)
				"RequisitionLineReturnQuantityDoesNotExist" 
				

    Sets

    Field Rules
    			
		RequisitionLineDetail.Bin
    		if (RequisitionLine.IsBinTrackFeedback)
				required
			else
				cannot be entered
					"ItemIsNotBinTracked;CannotEnterABin"
					
		RequisitionLineDetail.Lot
			if (RequisitionLine.ItemIsLotTrackedAtReceipt)
				required
			else
			if (not RequisitionLine.IsLotTrackFeedback)
				cannot be entered
					"ItemIsNotLotTracked;CannotEnterALot"

		
		RequisitionLineDetail.Sublot
			if (RequisitionLineDetail.Lot not entered)
				cannot be entered
					"LotNotEntered;CannotEnterSublot"
		
		RequisitionLineDetail.Serial
			if (RequisitionLine.ItemIsSerialTrackedAtReceipt)
				required
			else
			if (not RequisitionLine.IsSerialTrackFeedback)
				cannot be entered
					"ItemIsNotSerialTracked;CannotEnterASerial"
		
        Quantity
            required
            constraint (Quantity >= 0)
            	"QuantityReturnedCannotBeNegative"
        	constraint (Quantity <= RequisitionLine.CurrentReturnQuantity)
        		"DetailQuantityCannotBeGreaterThanReturnQuantity"
        	if (RequisitionLineDetail.Serial entered)	
        		force default to 1

		CatchWeightQuantity
			if (RequisitionLine.IsReturned)
				if (IsCatchWeightItem)
					required
				else
					cannot be entered
						"ItemIsNotCatchWeightItem"

	Actions
		Create is a Create Action
			Entrance Rules
				InventoryLocation = RequisitionLine.FromCompanyLocationBin.FromLocation

				if (TransientBin entered)
					RequisitionLineDetail.Bin 		= TransientBin
				if (TransientLot entered)
					RequisitionLineDetail.Lot 		= TransientLot
				if (TransientSerial entered)
					RequisitionLineDetail.Serial 	= TransientSerial
				
			Action Rules
				if (IsSerialTracked)
					for each RequisitionLine.WarehouseShipmentLinesRel
						LocalWarehouseShipment 		= each.WarehouseShipment
						LocalWarehouseShipmentLine	= each.WarehouseShipmentLine
						
						if (ShipmentForReturnDetailRel exists)	
							LocalSerialIssued		= true
							end for each
						
					constraint (LocalSerialIssued)
						"Serial<RequisitionLineDetail.Serial>WasNotIssuedToThisRequisition"
						
					include SerialEditData
				
				if (IsLotTracked)
					for each RequisitionLine.WarehouseShipmentLinesRel
						LocalWarehouseShipment 		= each.WarehouseShipment
						LocalWarehouseShipmentLine	= each.WarehouseShipmentLine
						
						if (ShipmentForReturnDetailRel exists)	
							LocalLotIssued			= true
							
			        		for each ShipmentForReturnDetailRel 
			        			LocalIssuedLotQuantity += each.Quantity
			    			
					constraint (LocalLotIssued)
						"Lot<RequisitionLineDetail.Lot>Sublot<RequisitionLineDetail.Sublot>WasNotIssuedToThisRequisition"
						
					constraint (Quantity <= LocalIssuedLotQuantity)
	    				"LotQuantityCannotBeGreaterThanOriginalLotQuantity"
					
					include LotEditData
				
				if (IsBinTracked)	
					include BinEditData
					
		Update is an Update Action
			Entrance Rules
				
			Action Rules
				if (IsSerialTracked)
					include SerialEditData
				if (IsLotTracked)
					include LotEditData
				if (IsBinTracked)
					include BinEditData
						
		Delete is a Delete Action
			Action Rules
				if (IsSerialTracked) 
					decrement RequisitionLine.DetailReturnQuantity by 1
				
            
