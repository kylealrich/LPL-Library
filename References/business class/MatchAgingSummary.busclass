MatchAgingSummary is a BusinessClass
    owned by ma
    prefix is MAgSu

    Ontology
		symbolic key is MatchAgingSummary

    Patterns
    	disable AuditIndex
    	disable Auditing
    	disable EffectiveDated
		implements CreateStamp
		implements UpdateStamp	

    Persistent Fields
		SummaryType					is Numeric 2
			States
				ByVendor			value is 1
				ByYearMonth			value is 2
				ByVendorUnmatched	value is 3
    	Year
			default label is "CreateYear"
    	Month						is Numeric 2
			default label is "CreateMonth"
    	VendorGroup					is like VendorGroup
    	Vendor						is like Vendor
		TotalCount					is Numeric 9
		TotalAmount					is like InternationalAmount
		Period1Count				is Numeric 9
		Period1Amount				is like InternationalAmount
		Period2Count				is Numeric 9
		Period2Amount				is like InternationalAmount
		Period3Count				is Numeric 9
		Period3Amount				is like InternationalAmount
		Period4Count				is Numeric 9
		Period4Amount				is like InternationalAmount
		Period1AmountPercent 		is like Percent
		Period1CountPercent 		is like Percent
		Period2AmountPercent 		is like Percent
		Period2CountPercent 		is like Percent
		Period3AmountPercent 		is like Percent
		Period3CountPercent 		is like Percent
		Period4AmountPercent 		is like Percent
		Period4CountPercent 		is like Percent

	Transient Fields
		MatchAgingPeriod
		NewAmount is like InternationalAmount	

	Local Fields
		NextSentence				is Boolean	
		LocalFinanceEnterpriseGroup is like FinanceEnterpriseGroup
		LocalPeriod                 is like MatchAgingPeriod 	
		
	Context Fields
		ContextMatchAging is a MatchAging 
		UsageDateRange
		VendorRange
				
	Derived Fields
		YearMonth is a DerivedField
		    type is Alpha 10
			return Year + "_" + Month 	

		YearMonthAndCount is a DerivedField
			type is like Description
			restricted
			return Year + "/" + Month + " (" + TotalCount + ")"

		VendorAndCount is a DerivedField
			type is like Description
			restricted
			return Vendor + " (" + TotalCount + ")"

		VendorYearPeriod is a DerivedField
			type is like Description
			return Vendor + " (" + Year + "," + Month + ")"

		DaysMsg is a MessageField
			"Days"

		OverMsg is a MessageField
			"Over"

		Period1Title is a MessageField
			"<Period1Rel.ThroughDays>_<DaysMsg>"
		Period2Title is a MessageField
			"<Period2Rel.ThroughDays>_<DaysMsg>"
		Period3Title is a MessageField
			"<Period3Rel.ThroughDays>_<DaysMsg>"
		Period4Title is a MessageField
			"<OverMsg>_<Period3Rel.ThroughDays>_<DaysMsg>"

	Sets

 		BySummaryTypeYearMonth
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				SummaryType
		    	Year descending
    			Month descending
				VendorGroup
				Vendor
 				MatchAgingSummary

 		BySummaryTypeVendor
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				SummaryType
 				VendorGroup
 				Vendor
 				MatchAgingSummary

 		ByTotalAmount
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				TotalAmount descending
 				MatchAgingSummary

 		ByTotalCount
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				TotalCount descending
 				MatchAgingSummary

 		ByPeriod4Amount
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				Period4Amount descending
 				MatchAgingSummary

 		ByPeriod4Count
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				Period4Count descending
 				MatchAgingSummary

 		ByPeriod4AmountPercent
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				Period4AmountPercent descending
 				MatchAgingSummary

 		ByPeriod4CountPercent 
 			Sort Order
				FinanceEnterpriseGroup
				MatchAging
				Period4CountPercent descending 
 				MatchAgingSummary


	Relations

		ActorMatchAgingRel  
			one-to-one relation to MatchAging
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= actor.context.FinanceEnterpriseGroup
				related.MatchAging				= 1

        VendorRel
            one-to-one relation to Vendor
            Field Mapping uses symbolic key
                related.VendorGroup	 			= VendorGroup
                related.Vendor            		= Vendor

		YearMonthSummaryInvoicesRel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByYearMonthAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
		    	related.CreateYear				= Year
    			related.CreateMonth				= Month

		VendorSummaryInvoicesRel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByVendor
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor

		VendorUnmatchedSummaryInvoicesRel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByVendor
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
		YearMonthSummaryInvoicesPeriod1Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByYearMonthAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
		    	related.CreateYear				= Year
    			related.CreateMonth				= Month
				related.MatchAgingPeriod 		= 1

		VendorSummaryInvoicesPeriod1Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByVendorAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
				related.MatchAgingPeriod 		= 1

		YearMonthSummaryInvoicesPeriod2Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByYearMonthAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
		    	related.CreateYear				= Year
    			related.CreateMonth				= Month
				related.MatchAgingPeriod 		= 2

		VendorSummaryInvoicesPeriod2Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByVendorAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
				related.MatchAgingPeriod 		= 2

		YearMonthSummaryInvoicesPeriod3Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByYearMonthAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
		    	related.CreateYear				= Year
    			related.CreateMonth				= Month
				related.MatchAgingPeriod 		= 3

		VendorSummaryInvoicesPeriod3Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByVendorAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
				related.MatchAgingPeriod 		= 3

		YearMonthSummaryInvoicesPeriod4Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByYearMonthAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
		    	related.CreateYear				= Year
    			related.CreateMonth				= Month
				related.MatchAgingPeriod 		= 4

		VendorSummaryInvoicesPeriod4Rel
			one-to-many relation to MatchAgingInvoice
			Field Mapping uses ByVendorAgingPeriod
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
				related.MatchAgingPeriod 		= 4


		Period1Rel
			one-to-one relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.MatchAgingPeriod		= 1
		Period2Rel
			one-to-one relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.MatchAgingPeriod		= 2
		Period3Rel
			one-to-one relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.MatchAgingPeriod		= 3
		Period4Rel
			one-to-one relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.MatchAgingPeriod		= 4

		LocalPeriodRel
			one-to-one relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= LocalFinanceEnterpriseGroup
				related.MatchAgingPeriod		= LocalPeriod

	Conditions
		
		MatchAgingExists
			when (MatchAging exists)

		IsTotal
			when (Year not entered
			and    Month not entered)

		IsNotTotal
			when (Year entered
			and    Month entered)

		WithinVendorRange
			restricted
			when (VendorRange not entered
			or	 (VendorRange entered
			and	  Vendor within VendorRange))
	Field Rules

	Create Rules

	Rule Blocks
		IncreaseValues
			TotalAmount += NewAmount
			if (MatchAgingPeriod entered)
				if (MatchAgingPeriod = 1)
				    Period1Count += 1 
					Period1Amount += NewAmount
				else
				if (MatchAgingPeriod = 2)
				    Period2Count += 1 
					Period2Amount += NewAmount
				else
				if (MatchAgingPeriod = 3)
				    Period3Count += 1 
					Period3Amount += NewAmount
				else
				if (MatchAgingPeriod = 4)
				    Period4Count += 1 
					Period4Amount += NewAmount

			if (not SummaryType.ByVendorUnmatched)
				Period1AmountPercent 	= Period1Amount / TotalAmount
				Period1CountPercent 	= Period1Count / TotalCount
				Period2AmountPercent 	= Period2Amount / TotalAmount
				Period2CountPercent 	= Period2Count / TotalCount
				Period3AmountPercent 	= Period3Amount / TotalAmount
				Period3CountPercent 	= Period3Count / TotalCount
				Period4AmountPercent 	= Period4Amount / TotalAmount
				Period4CountPercent 	= Period4Count / TotalCount


		DecreaseValues
			TotalAmount += NewAmount
			if (MatchAgingPeriod entered)
				if (MatchAgingPeriod = 1)
				    Period1Count -= 1 
					Period1Amount += NewAmount
				else
				if (MatchAgingPeriod = 2)
				    Period2Count -= 1 
					Period2Amount += NewAmount
				else
				if (MatchAgingPeriod = 3)
				    Period3Count -= 1 
					Period3Amount += NewAmount
				else
				if (MatchAgingPeriod = 4)
				    Period4Count -= 1 
					Period4Amount += NewAmount

			if (not SummaryType.ByVendorUnmatched)
				Period1AmountPercent 	= Period1Amount / TotalAmount
				Period1CountPercent 	= Period1Count / TotalCount
				Period2AmountPercent 	= Period2Amount / TotalAmount
				Period2CountPercent 	= Period2Count / TotalCount
				Period3AmountPercent 	= Period3Amount / TotalAmount
				Period3CountPercent 	= Period3Count / TotalCount
				Period4AmountPercent 	= Period4Amount / TotalAmount
				Period4CountPercent 	= Period4Count / TotalCount

	Actions
		Create is a Create Action
			restricted
			Action Rules
				include IncreaseValues
				
		Update is an Update Action
			restricted
			Action Rules
				include IncreaseValues

		AddToAging is an Instance Action
			restricted
			Parameters 
				PrmTotalCount			is Numeric 9 	
				PrmNewAmount			is like InternationalAmount		
				PrmMatchAgingPeriod 	is like MatchAgingPeriod
			Action Rules
				TotalCount 			+= PrmTotalCount			
				NewAmount 			= PrmNewAmount					
				MatchAgingPeriod 	= PrmMatchAgingPeriod 	


				include IncreaseValues

		SubtractFromAging is an Instance Action
			restricted
			Parameters 
				PrmTotalCount			is Numeric 9 	
				PrmNewAmount			is like InternationalAmount		
				PrmMatchAgingPeriod 	is like MatchAgingPeriod
			Action Rules
				TotalCount 			-= PrmTotalCount			
				NewAmount 			= PrmNewAmount					
				MatchAgingPeriod 	= PrmMatchAgingPeriod 	


				include DecreaseValues

		Delete is a Delete Action
			restricted

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules

		DeleteSet is a Set Action
			restricted
			default label is "SummarizeMatchAging"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmMatchAging					is a MatchAging

			Instance Selection
				where (PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
				and    PrmMatchAging = MatchAging)
			Action Rules
				Empty Set Rules
					invoke Delete PrmMatchAging

				Set Rules
					Exit Rules
						invoke Delete PrmMatchAging

				Instance Rules
					invoke FastDelete


		Resummarize is a Set Action
			valid when (actor.context.FinanceEnterpriseGroup.MatchAgingComplete)
			default label is "ResummarizeMatchAging"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmMatchAging					is a MatchAging

			Parameter Rules 
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup
				PrmMatchAging 
					initial value is ContextMatchAging 
					default to ContextMatchAging


			Instance Selection
				where (PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
				and    PrmMatchAging = MatchAging)
			Action Rules
				Empty Set Rules
					invoke Update PrmMatchAging 
						invoked.Status = 0 
					invoke Summarize MatchAgingInvoice
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmMatchAging				= PrmMatchAging

				Set Rules
					Exit Rules
						invoke Update PrmMatchAging 
							invoked.Status = 0 
						invoke Summarize MatchAgingInvoice
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmMatchAging				= PrmMatchAging

				Instance Rules
					invoke FastDelete


		DeleteAll is a Set Action
			valid when (actor.context.FinanceEnterpriseGroup.MatchAgingComplete)
			default label is "DeleteAging"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmMatchAging					is a MatchAging

			Parameter Rules 
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup
				PrmMatchAging 
					initial value is ContextMatchAging 
					default to ContextMatchAging
			Action Rules
				Set Rules 
					Exit Rules 
						invoke Update PrmMatchAging 
							invoked.Status = 2 
						invoke DeleteSet MatchAgingInvoice
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmMatchAging				= PrmMatchAging


		RecreateAgingPeriods is a Set Action
			default label is "ModifyAgingPeriods"
			valid when (actor.context.FinanceEnterpriseGroup.MatchAgingComplete)
			run in background
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmMatchAging				is a MatchAging
				ThroughNumberOfDaysPeriod1	is Numeric 7 
				ThroughNumberOfDaysPeriod2	is Numeric 7 
				ThroughNumberOfDaysPeriod3	is Numeric 7 

			Parameter Rules 
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup
				PrmMatchAging 
					initial value is ContextMatchAging 
					default to ContextMatchAging
				ThroughNumberOfDaysPeriod1	
					required  
				ThroughNumberOfDaysPeriod2	
					required  
					constraint (ThroughNumberOfDaysPeriod2 > ThroughNumberOfDaysPeriod1)
						"MatchedWithinDaysForPeriod2MustBeGreaterThanMatchedWithinDaysForPeriod1"
				ThroughNumberOfDaysPeriod3	
					required  
					constraint (ThroughNumberOfDaysPeriod3 > ThroughNumberOfDaysPeriod2)
						"MatchedWithinDaysForPeriod3MustBeGreaterThanMatchedWithinDaysForPeriod2"				
			Action Rules
				Set Rules 
					Exit Rules 
						invoke Update PrmMatchAging 
							invoked.Status = 0 
						LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup 
						LocalPeriod                 = 1
						invoke Update LocalPeriodRel
							invoked.ThroughDays = ThroughNumberOfDaysPeriod1
						LocalPeriod                 = 2
						invoke Update LocalPeriodRel
							invoked.ThroughDays = ThroughNumberOfDaysPeriod2
						LocalPeriod                 = 3
						invoke Update LocalPeriodRel
							invoked.ThroughDays = ThroughNumberOfDaysPeriod3

						invoke Resummarize MatchAgingSummary
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmMatchAging				= PrmMatchAging


		UpdateMigrationDate is a Set Action
			default label is "UpdateMigrationDate"
			valid when (actor.context.FinanceEnterpriseGroup.MatchAgingComplete)
			run in background
			Parameters 
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmMatchAging				is a MatchAging
				NewMigrationDate 			is Date 
			Parameter Rules 
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup
				PrmMatchAging 
					initial value is ActorMatchAgingRel.MatchAging 
					default to ActorMatchAgingRel.MatchAging  
				NewMigrationDate
					constraint (NewMigrationDate not = PrmMatchAging.MigrationDate)
						"NewMigrationDateCannotMatchOldMigrationDate"
			Instance Selection 
				where (false)

			Action Rules
				Empty Set Rules 
					invoke UpdateMigrationDate MatchAgingInvoice 
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmMatchAging				= PrmMatchAging.MatchAging 
						invoked.OldMigrationDate 			= PrmMatchAging.MigrationDate 
						invoked.NewMigrationDate 			= NewMigrationDate 
