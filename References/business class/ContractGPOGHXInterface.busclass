ContractGPOGHXInterface is a BusinessClass
	owned by po
	
	prefix is CoGHX
	
	Ontology
		symbolic key is ContractGPOGHXInterface
		
	Patterns



	Persistent Fields
		ContractNumber                          is Alpha 50   
		ContractDescription                     is Alpha 80   
		TierLevel                               is like ContractImportTier 
		ContractTierDescription                 is Text  
		Manufacturer                            is Alpha 100
		ManufacturerEID                         is Alpha 50
		SourceEID                               is Alpha 50
		SourceType                              is Alpha 100 
		SourceContractType                      is Alpha 20
		ContractStartDate                       is Date
	  	ContractEndDate                         is Date
	  	ContractDMLType                         is AlphaUpper 1
			States
				Insert					value is "I"
				Update     				value is "U"
				Delete				    value is "D"	  		

		ManufacturerPartNumber                  is AlphaUpper 48
		Vendor                                  is Alpha 100
		VendorERPNumber                         is Alpha 50 
		VendorEID                               is Alpha 50
		VendorPartNumber                        is AlphaUpper 50		
		Organization                            is Alpha 100
		OrganizationEID                         is Alpha 50
		ContractOrgID                           is Alpha 50 
		BuyerPartNum                            is AlphaUpper 48
		ItemDescription                         is Alpha 255
		ContractPrice                           is like ContractUnitCost 
		Adjustment                              is Decimal size 7.6
		Surcharge                               is like ContractUnitCost 
		LandedPrice                             is like ContractUnitCost 
		UOM                        				is like UnitOfMeasure 
		Quantity                                is Unsigned Decimal size 13.7
		ItemStartDate                           is Date
		ItemEndDate                             is Date
		IsSUOM                                  is Boolean
		PVRank                                  is Numeric 10

		ItemDMLType                             is AlphaUpper 1		
			States
				Insert					value is "I"
				Update     				value is "U"
				Delete				    value is "D"	

	Local Fields

		LocalContractGroup          is a ContractGroup
		LocalGPOContractNumber      is like GPOContractNumber
		LocalManufacturerSupplier   is like GPOSupplierID
		LocalDistributorSupplier    is like GPOSupplierID
		LocalContractType           is like ContractType
		LocalContractSubtype        is like ContractSubtype
		LocalGPOSupplier            is like Supplier
		LocalStartDate              is Date
		LocalContractImport         is like ContractImport
		LocalTierLevel              is like ContractImportTier
		LocalMappedSupplier         is like Supplier
		LocalMappedDistributorSupplier is like Supplier
		LocalCompany                is like InventoryCompany            
		LocalLocation               is like InventoryLocation         
		LocalRequestingLocation     is like RequestingLocation       
		LocalPricingGroup           is like PricingGroup
		LocalGPOUOM                 is like UnitOfMeasure
		LocalInternalUOM            is like UnitOfMeasure
		LocalManufacturerPart       is like ManufacturerNumber
		NewContractLineImport       is a ContractLineImport view	
		LocalItemDescriptionSize    is Numeric 400
		LocalContractLineImport     is like ContractLineImport
		IsManufacturerContract      is Boolean
		IsDistributorContract       is Boolean
														
	Conditions
	
		NotExpired 
			restricted
			when (ContractEndDate > current corporate date)
		
		HasLinesNotExpired 
			restricted
			when (LinesNotExpiredRel exists)
		
		ContractDoesNotExist
			restricted
			when (ToContractImportRel !exists) 			

		InterfaceContractExists
			restricted
			when (ToContractImportRel exists)
		
		IsContractHeader
			restricted
			when (PreviousContractRel !exists)

		IsSingleContractParticipant
			restricted
			when (PreviousParticipantRel !exists)

		IsSingleContractDistributor 
			restricted 
			when (PreviousDistributorRel !exists)
		
		HasParticipants
			restricted
			when (ParticipantRel exists)

		NewItem
			restricted
			when (ContractDMLType = "U"
			and   ItemDMLType     = "I")
		
		NoItems
			restricted
			when (ItemRel !exists)

		DistributorHasItems
			restricted
			when (DistributorCreateItemRel exists)

		SupplierNotMapped
			restricted
			when (MappingSupplierExists
			and   first ManufacturerBySupplierRel.ContractSupplier !entered) 
			
		SupplierMapped
			restricted
			when (first ManufacturerBySupplierRel.ContractSupplier entered)

		MappedToMember
			restricted
			when (ContractImportGPOMembershipRel.ParticipantEntered)
		
		MappedParticipantsExist
			restricted
			when (MappedContractGPOParticipantRel exists)

		MappingSupplierExists
			restricted
			when (ManufacturerBySupplierRel exists)

		MappingSupplierDoesNotExist
			restricted
			when (ManufacturerBySupplierRel !exists)

		NoDistributorItems
			restricted
			when (DistributorItemRel !exists)
		
		DistributorSupplierMappedOrNoLines
			restricted
			when (first DistributorSupplierMappingRel.ContractSupplier entered
			or    NoDistributorItems)

		DistributorSupplierMapped
			restricted
			when (first DistributorSupplierMappingRel.ContractSupplier entered)

		CanCreateNewContract
			restricted
			when (SupplierMapped
			and   ContractDoesNotExist
			and   HasParticipants)

		BlanketOrStanding
			restricted
			when (((LocalContractSubtype not entered)
			and    (ContractTypeRel.PurchaseType.Blanket
			or      ContractTypeRel.PurchaseType.Standing))
			or    ((LocalContractSubtype entered)
			and    (ContractSubtypeRel.PurchaseType.Blanket
			or      ContractSubtypeRel.PurchaseType.Standing)))

		DirectBuy
			restricted
			when (ManufacturerEID = VendorEID)
			
		HasDistributors 
			restricted 
			when (GHXDistributorSupplierRel exists)
		
		NotDirectBuy
			restricted	
			when (ManufacturerEID != VendorEID)
		
		ServicePurchaseType
			restricted
			when (((LocalContractSubtype not entered)
			and    (ContractTypeRel.PurchaseType.Service))
			or    ((LocalContractSubtype entered)
			and    (ContractSubtypeRel.PurchaseType.Service)))

	Relations
	
		ToContractImportRel
			one-to-many relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = ContractNumber			

		ContractImportRel
			one-to-one relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber 	= LocalGPOContractNumber
				related.ContractGroup       = LocalContractGroup	

		PreviousContractRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber           = ContractNumber
			Instance Selection
				where (related.ContractGPOGHXInterface < ContractGPOGHXInterface)
		
		LinesNotExpiredRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber           = ContractNumber
			Instance Selection
				where (related.ItemEndDate > current corporate date)		
		ContractGPOHeaderForSupplierRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber           = LocalGPOContractNumber

		ManufacturerSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup               = LocalContractGroup
				related.ContractImportGPOSupplier 	= LocalManufacturerSupplier

		DistributorSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup               = LocalContractGroup
				related.ContractImportGPOSupplier 	= LocalDistributorSupplier		
		
		ParticipantRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = ContractNumber
			Instance Selection
				where (related.OrganizationEID entered)
 
		PreviousParticipantRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractOrganizationEID
				related.ContractNumber    = ContractNumber
				related.OrganizationEID   = OrganizationEID
			Instance Selection
				where (related.ContractGPOGHXInterface < ContractGPOGHXInterface)

		PreviousDistributorRel 
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractVendorEID
				related.ContractNumber    	= ContractNumber
				related.VendorEID   		= VendorEID
			Instance Selection
				where (related.ContractGPOGHXInterface < ContractGPOGHXInterface)			

		GHXDistributorSupplierRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = ContractNumber
			Instance Selection
				where (related.VendorEID entered
				and    related.ManufacturerEID != related.VendorEID)			

		GHXDistributorSupplierLocalRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = LocalGPOContractNumber
			Instance Selection
				where (related.VendorEID entered
				and    related.ManufacturerEID != VendorEID)	

		GHXDifferentTierLocalRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = LocalGPOContractNumber
			Instance Selection
				where (related.TierLevel != TierLevel)
		GPOOtherSupplierRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber   = LocalGPOContractNumber 
			Instance Selection
				where (related.VendorEID entered)
		
		SupplierRel
			one-to-one relation to Supplier
			Field Mapping uses symbolic key
				related.SupplierGroup               = LocalContractGroup
				related.Supplier                    = LocalGPOSupplier

 		ItemRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = ContractNumber
			Instance Selection
				where (related.ManufacturerPartNumber entered)

		SingleItemRel 
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = ContractNumber
			Instance Selection
				where (related.ManufacturerPartNumber entered
				and    related.ManufacturerEID = VendorEID)
		ContractGPOHeaderRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber                           = ContractNumber

 		DistributorItemRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = ContractNumber
			Instance Selection
				where (related.VendorPartNumber entered
				and    related.VendorEID  = VendorEID)

 		DistributorCreateItemRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber    = ContractNumber
			Instance Selection
				where (related.VendorPartNumber entered
				and    related.VendorEID  = LocalDistributorSupplier)		

		ManufacturerBySupplierRel
			one-to-many relation to ContractImportGPOSupplier
			Field Mapping uses BySupplierFirst
				related.ContractImportGPOSupplier 	= ManufacturerEID

		DistributorSupplierMappingRel
			one-to-many relation to ContractImportGPOSupplier
			Field Mapping uses BySupplierFirst
				related.ContractImportGPOSupplier 	= VendorEID
			 
		ContractImportGPOMembershipRel
			one-to-one relation to ContractImportGPOMembership
			Field Mapping uses symbolic key
				related.ContractImportGPOMembership = OrganizationEID

		ContractTypeRel
			one-to-one relation to ContractType
			Field Mapping uses symbolic key
				related.ContractGroup      = LocalContractGroup
				related.ContractType       = LocalContractType
		
		ContractSubtypeRel
			one-to-one relation to ContractSubtype
			Field Mapping uses symbolic key
				related.ContractGroup      = LocalContractGroup
				related.ContractType       = LocalContractType
				related.ContractSubtype    = LocalContractSubtype

		ContractImportTierRel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup      = LocalContractGroup
				related.ContractImport     = LocalContractImport
				related.ContractImportTier = LocalTierLevel
				
		ContractImportTierMemberRel
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByMembership
				related.ContractGroup       		= LocalContractGroup
				related.GPOContractNumber   		= LocalGPOContractNumber
				related.ContractImportGPOMembership = OrganizationEID
	
		ContractImportTierMemberByLocationRel
			one-to-one relation to ContractImportTierMember
			Field Mapping uses symbolic key
				related.ContractGroup                     		= LocalContractGroup
				related.ContractImport                    		= LocalContractImport
				related.ContractImportTierMember.Company    	= LocalCompany
				related.ContractImportTierMember.Location       = LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   = LocalPricingGroup

		ContractImportDistributorRel
			one-to-one relation to ContractImportDistributor
			Field Mapping uses symbolic key
				related.ContractGroup      			= LocalContractGroup
				related.ContractImport     			= LocalContractImport
				related.ContractImportDistributor 	= LocalMappedDistributorSupplier

		MappedContractGPOParticipantRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByContractNumber
				related.ContractNumber   = LocalGPOContractNumber
			Instance Selection
				where (related.OrganizationEID entered
				and    related.MappedToMember)

		ContractImportTierDistributorRel
			one-to-one relation to ContractImportTierDistributor
			Field Mapping uses symbolic key
				related.ContractGroup                     		= LocalContractGroup
				related.ContractImport                    		= LocalContractImport
				related.ContractImportTierMember.Company    	= LocalCompany
				related.ContractImportTierMember.Location       = LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   = LocalPricingGroup                  
				related.ContractImportTierDistributor           = LocalMappedDistributorSupplier  

		ContractLineImportRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByGPOContractNumber
				related.ContractGroup  								= LocalContractGroup
				related.GPOContractNumber 							= LocalGPOContractNumber
				related.ManufacturerInformation.ManufacturerNumber	= LocalManufacturerPart
			Instance Selection
				where (related.UOM	= LocalInternalUOM)

		ContractLineImportTierCostRel
			one-to-one relation to ContractLineImportTierCost
			Field Mapping uses symbolic key
				related.ContractGroup  								= LocalContractGroup
				related.ContractImport   							= LocalContractImport
				related.ContractLineImport                          = LocalContractLineImport
				related.ContractImportTier                          = TierLevel

		ContractLineImportDistributorRel
			one-to-many relation to ContractLineImportDistributor
			Field Mapping uses symbolic key
				related.ContractGroup  								= LocalContractGroup
				related.ContractImport  							= LocalContractImport
				related.ContractLineImport							= LocalContractLineImport
			Instance Selection
				where (related.DistributorSupplier                  = LocalMappedDistributorSupplier)

		ManufacturerSupplierUOMRel 
			one-to-many relation to ContractImportGPOSupplierUOM
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalManufacturerSupplier	
				related.ContractImportGPOSupplierUOM                = LocalGPOUOM	
			Instance Selection 
				where (related.UnitOfMeasure entered)

		GPOSupplierUOMCreateRel 
			one-to-one relation to ContractImportGPOSupplierUOM
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalManufacturerSupplier	
				related.ContractImportGPOSupplierUOM                = LocalGPOUOM					

	Sets
	
		ByPartNumberFirst 	
			Sort Order 
				ManufacturerPartNumber
				VendorPartNumber
				UOM	
				ContractGPOGHXInterface			
		
		ByContractNumber
			Sort Order
				ContractNumber
				ContractGPOGHXInterface	
				
		ByItem
			Sort Order
				ContractNumber
				ManufacturerPartNumber
				VendorPartNumber
				UOM
				ContractGPOGHXInterface

		ByOrganizationItem
			Sort Order
				ContractNumber
				OrganizationEID
				ManufacturerPartNumber
				VendorPartNumber
				UOM
				ContractGPOGHXInterface

		ByItemNoVendorItem
			Sort Order
				ContractNumber
				ManufacturerPartNumber
				UOM
				ContractGPOGHXInterface		

		ByContractVendorEID
			Sort Order
				ContractNumber
				VendorEID
				ContractGPOGHXInterface

		ByOrganizationEID
			Sort Order
				OrganizationEID
				ContractNumber
				ContractGPOGHXInterface
	
		ByManufacturerEID
			Sort Order
				ManufacturerEID
				ContractNumber
				ContractGPOGHXInterface

		ByContractOrganizationEID
			Sort Order
				ContractNumber
				OrganizationEID
				ContractGPOGHXInterface

		ByContractDate
			Sort Order
				ContractNumber
				ContractEndDate descending
				ContractGPOGHXInterface

	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
					
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted
			
		CreateNewContractForThisGPOContract is an Instance Action
			completion message is "ContractInterfaceCreated;ViewInInterfaceContractsWithNoContracts"			
			valid when (CanCreateNewContract)
			Parameters
				ContractGroup
				ParmGPOSupplier       					is a Supplier
					default label is "GPOSupplier"
				ParmGPOContractNumber 					is a GPOContractNumber
					default label is "GPOContractNumber"
				VerifyEndDate                           is Date
				TypeOfContract                          is Numeric 1
					States
						Manufacturer                    value is 1
						Distributor                     value is 2
						Service                         value is 3
				ContractType
				ContractSubtype
				ContractClassification
				ContractSubclassification
				ManufacturerCodeDivision 				is a Manufacturer	
				UseGPOContractNumberAsWorkingContract 	is Boolean			
				Contact                  				is an Employee
					default label is "PrimaryInternalContact"
				MemberProcessing                        is Numeric 1
					States
						ProcessOnlyMappedMembers        value is 1
						ErrorIfAllMembersNotMapped      value is 2
				UseTemplate                             is a ContractImport
				DoNotCreateDistributorContract          is Boolean
				DistributorPriority                     is Numeric 1
				StandardMarkupCalculation               is Boolean
			
			Parameter Rules
				ContractGroup
					required
					
					if (ContractStartDate > current date)
						confirmation required
							"ContractWillBeCreatedWithAStartDateInTheFuture;DoYouWantToContinue?"

					constraint (ContractGroup.GPOUsed > 0)
						"MustSelectA_GPO_UsedOnContractGroupToRunThisProcess"
				
					LocalContractGroup   = ContractGroup
					LocalContractType    = ContractType
					LocalContractSubtype = ContractSubtype
					LocalGPOSupplier     = ParmGPOSupplier
					if (ContractType entered)
						if (TypeOfContract.Manufacturer
						or  TypeOfContract.Distributor)
							constraint (!ServicePurchaseType)
								"ContractTypesForManufacturerAndDistributorContractsCannotBeServicePurchaseTypes"
							constraint (!BlanketOrStanding)
								"ContractTypesForManufacturerAndDistributorContractsCannotBeBlanketOrStandingPurchaseTypes"
						if (TypeOfContract.Service)
							constraint (ServicePurchaseType)
								"ContractTypesForServiceContractsMustUseAServicePurchaseType"
					LocalManufacturerSupplier = ManufacturerEID 
					constraint (SupplierMapped)
						"ManufacturerSupplierNotMappedToASupplier"
					LocalGPOContractNumber = ParmGPOContractNumber
					if (TypeOfContract.Manufacturer
					and !DoNotCreateDistributorContract)
						for each GHXDistributorSupplierRel
							constraint (each.DistributorSupplierMapped)
								"DistributorSupplier<each.VendorEID>IsNotMapped"
					if (TypeOfContract.Distributor)
						constraint (GHXDistributorSupplierLocalRel !exists)
							"ContractWithMultipleVendorsMustBeAManufacturerContract"															
						constraint (GHXDifferentTierLocalRel !exists)
							"ContractWithMultipleTiersMustBeAManufacturerContract"

				VerifyEndDate
					initial value is ContractEndDate
					required
				
				ParmGPOSupplier
					initial value is ContractGroup.DefaultGPOSupplier
					required
					constraint (SupplierRel exists)
						"GPOSupplierDoesNotExist"
					constraint (SupplierRel.Supplier.GroupPurchasingOrganization) 
						"GPOSupplierIsNotDefinedAsAGroupPurchasingOrganization" 					

				ParmGPOContractNumber
					initial value is ContractNumber
					default to ContractNumber
					LocalContractGroup 		= ContractGroup
					LocalGPOContractNumber 	= ContractNumber
					constraint (ContractImportRel !exists)
						"ContractInterfaceAlreadyExists"
				
				TypeOfContract
					initial value is TypeOfContract.Manufacturer
					LocalGPOContractNumber = ParmGPOContractNumber
					if (TypeOfContract.Manufacturer)
						if (NoItems)
							confirmation required
								"ThisGPOContractHasNoItems;DoYouWantToCreateAManufacturerContract?"
					if (TypeOfContract.Service)
						constraint (NoItems)
							"CannotCreateServiceContract;GPOContractHasItems"
						constraint (HasParticipants)
							"CannotCreateServiceContract;GPOContractHasNoParticipants"
					required
							 
				ManufacturerCodeDivision
					LocalGPOSupplier     			= ParmGPOSupplier
					LocalGPOContractNumber          = ParmGPOContractNumber
					LocalContractGroup              = ContractGroup
					LocalManufacturerSupplier       = ContractGPOHeaderForSupplierRel.ManufacturerEID
					initial value is UseTemplate.ManufacturerCodeDivision
					default to ManufacturerSupplierRel.ManufacturerCodeDivision

					if (!TypeOfContract.Service
					and ManufacturerCodeDivision !entered
					and UseTemplate.ManufacturerCodeDivision !entered)
						constraint (ManufacturerSupplierRel.ManufacturerCodeDivision entered)
							"ManufacturerCodeDivisionMustBeEntered;OneIsNotDefinedForTheSupplierOrTemplate"	

				ContractType
					initial value is UseTemplate.ContractType
					default to ContractGroup.DefaultGPOContractType
					
				ContractSubtype
					initial value is UseTemplate.ContractSubtype
				
				ContractClassification
					initial value is UseTemplate.ContractClassification
					default to ContractGroup.DefaultGPOContractClassification
					
				ContractSubclassification
					initial value is UseTemplate.ContractSubclassification
					
				Contact
					initial value is UseTemplate.Contact
					default to ContractGroup.DefaultGPOContact
				
				UseGPOContractNumberAsWorkingContract
					initial value is true

				MemberProcessing
					initial value is ContractGroup.DefaultMemberProcessing
					required
					LocalGPOContractNumber = ParmGPOContractNumber
					if (MemberProcessing = 2)
						for each ParticipantRel
							if (each.IsSingleContractParticipant)
								constraint (each.MappedToMember)
									"MembersExistThatAreNotMapped"	
					else
						constraint (MappedParticipantsExist)
							"NoMappedMembersExist;MustHaveAtLeastOneMappedMember"				
				
				UseTemplate
					constraint (UseTemplate.Template)
						"UseTemplateMustBeATemplate"
				
			Action Rules
			
				invoke CreateNewContract
					invoked.ContractGroup     				= ContractGroup
					invoked.ParmGPOSupplier   				= ParmGPOSupplier
					invoked.ParmGPOContractNumber 			= ParmGPOContractNumber
					invoked.ParmSupplier                    = ManufacturerEID
					invoked.ContractType            		= ContractType
					invoked.ContractSubtype         		= ContractSubtype
					invoked.ContractClassification  		= ContractClassification
					invoked.ContractSubclassification       = ContractSubclassification
					invoked.ManufacturerCodeDivision    	= ManufacturerCodeDivision 							
					invoked.UseGPOContractNumberAsWorkingContract = UseGPOContractNumberAsWorkingContract
					invoked.Contact                         = Contact
					invoked.TypeOfContract                  = TypeOfContract
					invoked.MemberProcessing                = MemberProcessing
					invoked.UseTemplate                     = UseTemplate
					invoked.DoNotCreateDistributorContract  = DoNotCreateDistributorContract
					invoked.DistributorPriority             = DistributorPriority
					invoked.StandardMarkupCalculation       = StandardMarkupCalculation
					invoked.VerifyEndDate                   = VerifyEndDate

		CreateNewContract is a Set Action
			restricted
			Parameters
				ContractGroup
				ParmGPOSupplier       					is like Supplier
				ParmGPOContractNumber 					is a GPOContractNumber
				ParmSupplier                            is like Supplier
				TypeOfContract                          is Numeric 1
					States
						Manufacturer                    value is 1
						Distributor                     value is 2
						Service                         value is 3
				ContractType
				ContractSubtype
				ContractClassification
				ContractSubclassification
				ManufacturerCodeDivision 				is a Manufacturer
				UseGPOContractNumberAsWorkingContract 	is Boolean				
				Contact                  				is like Employee
				MemberProcessing                        is Numeric 1
					States
						ProcessOnlyMappedMembers        value is 1
						ErrorIfAllMembersNotMapped      value is 2
				UseTemplate                             is a ContractImport
				DoNotCreateDistributorContract          is Boolean
				DistributorPriority                     is Numeric 1
				StandardMarkupCalculation               is Boolean
				VerifyEndDate                           is Date

			Parameter Rules
			
			Local Fields
				LocalContractImportView		is a ContractImport view	
				
			Instance Selection
				where  (ContractNumber = ParmGPOContractNumber)
			
			Sort Order
				ContractNumber
				ContractGPOGHXInterface
			
			Action Rules
			
				ContractNumber Set Rules

					Exit Rules
		                invoke DeleteOldInterfaceRecordsSet
 			               	invoked.ParmGPOContractNumber           = ParmGPOContractNumber
 			               	invoked.ParmEndDate                     = VerifyEndDate

						invoke Update ToContractImportRel
							invoked.CreationInProgress = false						
				
				Instance Rules

					LocalGPOSupplier     			= ParmGPOSupplier
					LocalContractGroup              = ContractGroup
					LocalManufacturerSupplier       = ManufacturerEID
					LocalDistributorSupplier  		= VendorEID
					LocalStartDate                  = ContractStartDate
					LocalGPOContractNumber          = ContractNumber
					LocalTierLevel 					= TierLevel
					LocalManufacturerPart           = ManufacturerPartNumber
					LocalGPOUOM                     = UOM
					LocalInternalUOM                = UOM
					if (ManufacturerSupplierUOMRel exists)
						LocalInternalUOM  			= first ManufacturerSupplierUOMRel.UnitOfMeasure
					LocalMappedSupplier             = ManufacturerSupplierRel.ContractSupplier
					LocalMappedDistributorSupplier  = DistributorSupplierRel.ContractSupplier
					
					if  (ContractImportRel !exists
        			and (ContractEndDate >= VerifyEndDate
        			or  (ItemEndDate     >= VerifyEndDate
        			and  ItemEndDate     entered)))

						invoke CreateNoRules ContractImport
							assign result to LocalContractImportView
							if (UseTemplate entered)
								invoked.Name            					= UseTemplate.Name
								invoked.Description             			= UseTemplate.Description
								invoked.CurrencyCode            			= UseTemplate.CurrencyCode
								invoked.PerOrderMinimumAmount   			= UseTemplate.PerOrderMinimumAmount
								invoked.PerOrderMaximumAmount   			= UseTemplate.PerOrderMaximumAmount
								invoked.UOMSMustMatch         				= UseTemplate.UOMSMustMatch
								invoked.UOMSMustMatchForSpecials            = UseTemplate.UOMSMustMatchForSpecials
								invoked.UseContractUOM						= UseTemplate.UseContractUOM
								invoked.LifetimeEdit                        = UseTemplate.LifetimeEdit
								invoked.AllowReferenceOfItemsNotOnContract  = UseTemplate.AllowReferenceOfItemsNotOnContract
								invoked.LineLevelBreaks						= UseTemplate.LineLevelBreaks
								invoked.TermsCode							= UseTemplate.TermsCode
								invoked.PoUserField1						= UseTemplate.PoUserField1
								invoked.PoUserField3						= UseTemplate.PoUserField3
								invoked.PoUserField5						= UseTemplate.PoUserField5
								invoked.VendorAgreementReference			= UseTemplate.VendorAgreementReference
								invoked.CommodityCode						= UseTemplate.CommodityCode
								invoked.DefaultAccount						= UseTemplate.DefaultAccount
								invoked.PayablesDistributionCode			= UseTemplate.PayablesDistributionCode
								invoked.DefaultTaxable						= UseTemplate.DefaultTaxable
								invoked.TaxCode								= UseTemplate.TaxCode
								invoked.NotificationPercent                 = UseTemplate.NotificationPercent
								invoked.PreferredContract                   = UseTemplate.PreferredContract
								invoked.EnableClinicalSystemUseLocations    = UseTemplate.EnableClinicalSystemUseLocations
							invoked.ContractGroup       		= ContractGroup
							invoked.GPOContractNumber   		= ContractNumber
							invoked.Supplier           			= LocalMappedSupplier
							invoked.ManufacturerSupplier    	= ManufacturerEID  
							if (UseTemplate.Name !entered)
								invoked.Name               		= ContractDescription
							invoked.ContractType       		    = ContractType
							invoked.ContractSubtype             = ContractSubtype
							invoked.ContractClassification 		= ContractClassification
							invoked.ContractSubclassification   = ContractSubclassification
							if (ManufacturerCodeDivision entered)
								invoked.ManufacturerCodeDivision = ManufacturerCodeDivision
							else
							if  (UseTemplate entered
							and	 UseTemplate.ManufacturerCodeDivision entered)
								invoked.ManufacturerCodeDivision = UseTemplate.ManufacturerCodeDivision
							else
								invoked.ManufacturerCodeDivision = ManufacturerSupplierRel.ManufacturerCodeDivision							
							invoked.GPOSupplier         		= ParmGPOSupplier
							invoked.GPONegotiated       		= true
							invoked.EffectiveDate       		= ContractStartDate
							invoked.ExpirationDate      		= ContractEndDate
							if (TypeOfContract.Manufacturer)
								invoked.ManufacturerOrDistributor.ManufacturerContract = true
							else 
							if (TypeOfContract.Distributor)
								invoked.ManufacturerOrDistributor.DistributorContract = true
							invoked.ContractImportSource        = 2
							if (UseGPOContractNumberAsWorkingContract)
								invoked.WorkingContractID       = ContractNumber
								invoked.WorkingContractType     = 4 
							invoked.Contact                     = Contact
							invoked.SupplierSourceId            = 1
							if (ManufacturerSupplierRel.ContractSupplier.Vendor.InvoiceCurrency entered)
								invoked.CurrencyCode 			= ManufacturerSupplierRel.ContractSupplier.Vendor.InvoiceCurrency
							else
								invoked.CurrencyCode 			= ContractGroup.DefaultCurrency
							if (UseTemplate.Priority !entered)
								invoked.Priority                = 5
							else
								invoked.Priority                = UseTemplate.Priority
							invoked.LineLevelBreaks             = 1
							invoked.DoNotCreateDistributorContract = DoNotCreateDistributorContract
							invoked.CreationInProgress          = true

					LocalContractImport             = LocalContractImportView.ContractImport						
					
					if (TypeOfContract.Manufacturer
					and TierLevel entered
					and ContractImportTierRel !exists)
						invoke Create ContractImportTier
							invoked.ContractGroup       = ContractGroup
							invoked.ContractImport      = LocalContractImport
							invoked.ContractImportTier  = TierLevel
							invoked.Description         = ContractTierDescription
							invoked.TierTitle           = TierLevel
							invoked.TierMarkupDiscount  = "F" 											
							invoked.GPOContractNumber   = ContractNumber	

					if (TypeOfContract.Manufacturer
					and VendorEID entered
					and VendorEID != ManufacturerEID)
						LocalMappedDistributorSupplier	= DistributorSupplierRel.ContractSupplier
						if (LocalMappedDistributorSupplier entered
						and ContractImportDistributorRel !exists
						and DoNotCreateDistributorContract = false)
							invoke Create ContractImportDistributor
								invoked.ContractGroup                                   = ContractGroup
								invoked.ContractImport                                  = LocalContractImport
				        		invoked.GPODistributorSupplier                          = VendorEID
								invoked.ContractImportDistributor                       = LocalMappedDistributorSupplier
								invoked.Priority                                        = DistributorPriority
								if (Adjustment = 1)
									invoked.LeavePercentAtZero							= true
								else 
								if (Adjustment != 1)
									invoked.MarkupPercent                               = Adjustment							
					
					if (OrganizationEID entered
					and MappedToMember)
						LocalCompany            = ContractImportGPOMembershipRel.ParticipantLocation.Company                   
						LocalLocation           = ContractImportGPOMembershipRel.ParticipantLocation.Location             
						LocalRequestingLocation = ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation             
						LocalPricingGroup       = ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup             
						if (ContractImportTierMemberRel !exists
						and ContractImportTierMemberByLocationRel !exists)
							invoke IndirectCreate ContractImportTierMember
								invoked.ContractGroup            					= ContractGroup
								invoked.ContractImport           					= LocalContractImport
								invoked.ContractImportTierMember.Company 			= LocalCompany
								invoked.ContractImportTierMember.Location 			= LocalLocation
								invoked.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
								invoked.ContractImportTierMember.PricingGroup 		= LocalPricingGroup
								if (TypeOfContract.Manufacturer)
									invoked.Tier                   					= TierLevel
								invoked.LocalTierName                               = TierLevel
								invoked.LocalTierDescription                        = ContractTierDescription
								invoked.ContractImportGPOMembership 				= OrganizationEID
								invoked.GPOContractNumber        					= ContractNumber
                                invoked.EffectiveDate            					= ContractStartDate
                                invoked.ExpirationDate           					= ContractEndDate
                                if (TypeOfContract.Distributor)
                                	invoked.FromDistributor                         = true
								if (TypeOfContract.Service) 
									invoked.FromService                             = true
								invoked.FromContractCreation                        = true
								invoked.DistributorPriority                         = DistributorPriority
								invoked.DoNotCreateDistributorContract              = DoNotCreateDistributorContract
								invoked.StandardMarkupCalculation                   = StandardMarkupCalculation
								invoked.FromGHXInterface                            = true
					
					if (!TypeOfContract.Service
					and ManufacturerEID entered
					and ManufacturerPartNumber entered)
						if (ContractLineImportRel exists)
							if (TypeOfContract.Manufacturer)
								LocalContractLineImport = ContractLineImportRel.ContractLineImport
								if (ContractLineImportTierCostRel !exists)
									invoke Create ContractLineImportTierCost
										invoked.ContractGroup                       = LocalContractGroup
										invoked.ContractImport                      = LocalContractImport
										invoked.ContractLineImport                  = LocalContractLineImport
										invoked.ContractImportTier              	= TierLevel
										invoked.TierCost							= ContractPrice	
										invoked.ManufacturerNumber                  = ManufacturerPartNumber
										invoked.VendorItem		                  	= VendorPartNumber
										invoked.GPOContractNumber                   = ContractNumber

							if (NotDirectBuy
							and ContractLineImportDistributorRel !exists
							and DoNotCreateDistributorContract = false)
								invoke Create ContractLineImportDistributor
									invoked.ContractGroup                       = ContractGroup
									invoked.ContractImport                      = LocalContractImport
									invoked.ContractLineImport                  = LocalContractLineImport
									invoked.DistributorSupplier                 = LocalMappedDistributorSupplier
									if (VendorPartNumber entered)
										invoked.VendorItem                    	= VendorPartNumber
									else 
										invoked.VendorItem                      = ManufacturerPartNumber
									invoked.GPOSupplier                         = VendorEID
									invoked.DistributorCost                     = Surcharge
					
						if   (ContractLineImportRel !exists
						and ((ItemStartDate > current corporate date
						and	  ItemStartDate = ContractStartDate)
						or    ItemStartDate <= current corporate date))
							invoke CreateWithTierCosts ContractLineImport
								assign result to NewContractLineImport
								invoked.ContractGroup                           					= ContractGroup
								invoked.ContractImport                          					= LocalContractImport
								invoked.GPOContractNumber                           				= ContractNumber
								invoked.ManufacturerInformation.ManufacturerNumber 					= ManufacturerPartNumber
								if (ContractImportRel.IsDistributorContractForGPO)
									invoked.VendorItem                             					= VendorPartNumber                   
								else
									invoked.VendorItem                              				= ManufacturerPartNumber
								LocalItemDescriptionSize = ItemDescription size
								if (LocalItemDescriptionSize > 60)
									invoked.ItemDescription3                                        = ItemDescription
								if (ContractStartDate <= ItemStartDate)
									invoked.EffectiveDate                                           = ItemStartDate
								else
									invoked.EffectiveDate                                           = ContractStartDate
								if (ContractEndDate >= ItemEndDate)
									invoked.ExpirationDate                                          = ItemEndDate
								else 
									invoked.ExpirationDate                                          = ContractEndDate
								invoked.ItemNumber													= BuyerPartNum
								invoked.ItemDescription              								= ItemDescription
								invoked.BaseCost                                					= ContractPrice                        
								invoked.UOM                  										= LocalInternalUOM        
								if (LocalInternalUOM != LocalGPOUOM)
									invoked.GPOUnitOfMeasure                                        = LocalGPOUOM
								invoked.UOMConversion                           					= Quantity               
								if (TypeOfContract.Manufacturer)
									invoked.TransientTier1                         						= TierLevel
									invoked.TransientTierCost1                                      	= ContractPrice
								invoked.UpdateFunction                          					= "C"
							LocalContractLineImport = NewContractLineImport.ContractLineImport
							if (NotDirectBuy
							and ContractLineImportDistributorRel !exists
							and DoNotCreateDistributorContract = false)
								invoke Create ContractLineImportDistributor
									invoked.ContractGroup                       = ContractGroup
									invoked.ContractImport                      = LocalContractImport
									invoked.ContractLineImport                  = LocalContractLineImport
									invoked.DistributorSupplier                 = LocalMappedDistributorSupplier
									if (VendorPartNumber entered)
										invoked.VendorItem                     	= VendorPartNumber
									else 
										invoked.VendorItem                      = ManufacturerPartNumber
									invoked.GPOSupplier                         = VendorEID
									invoked.DistributorCost                     = Surcharge
						
		CopyRecord is an Instance Action   
			restricted
			Action Rules
			
				invoke Create ContractGPOGHXInterface
					fill in fields from this instance

		CopyToUpdate is an Instance Action   
			restricted
			Action Rules
			
				invoke Create ContractGPOGHXUpdate
					fill in fields from this instance

		Purge is a Purge Action
			restricted

		MassDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Purge

		PurgeDeletedRecords is a Set Action
			restricted	
			Instance Selection
				include only deleted records
			Action Rules
				Instance Rules
					invoke Purge	

		UpdateManufacturerEID is a Set Action 
			restricted 
			Parameters
				ParmContractNumber  		is Alpha 50
				ParmManufacturerEID         is Alpha 50
				ParmOldVendorEID            is Alpha 50 
				ParmNewVendorEID            is Alpha 50

			Instance Selection 
				where (ParmContractNumber = ContractNumber)

			Action Rules 

				Instance Rules 

					if (ParmManufacturerEID entered)
						ManufacturerEID		= ParmManufacturerEID
					if (ParmNewVendorEID entered
					and ParmOldVendorEID    = VendorEID)
						VendorEID           = ParmNewVendorEID					

		UpdateHeaderDescriptions is a Set Action 
			restricted 
			Parameters 
				ParmContractNumber  		is Alpha 50
				ParmContractDescription     is Alpha 80 
				ParmTierDescription         is Text 
				ParmEndDate                 is Date 

			Instance Selection 
				where (ParmContractNumber = ContractNumber)

			Sort Order is ByContractNumber 
			
			Set Is 
				ContractNumber 
				ContractGPOGHXInterface
			
			Action Rules 

				Instance Rules 

					ContractDescription			= ParmContractDescription 
					ContractTierDescription     = ParmTierDescription
					if (ParmEndDate entered)
						ContractEndDate         = ParmEndDate 

		ProcessNewlyMappedMembers is a Set Action
			restricted
			Parameters
				ParmContractGroup   is a ContractGroup
				ParmGPOMembership   is a ContractImportGPOMembership
				AutoUpdateContracts is Boolean
				
			Parameter Rules
			
				ParmContractGroup
					required
					
			Instance Selection
				where   (ParmGPOMembership = OrganizationEID
				and      InterfaceContractExists
				and 	 ParmContractGroup = first ToContractImportRel.ContractGroup)
				
			Sort Order
				ContractNumber
				OrganizationEID
				ContractGPOGHXInterface

			Action Rules
			
				OrganizationEID Set Rules
				
					Entrance Rules
					
						LocalDistributorSupplier  	= VendorEID					
						LocalContractGroup   		= ParmContractGroup
						LocalGPOContractNumber  	= ContractNumber
						if (ContractImportRel.IsManufacturerContract)
							IsManufacturerContract = true
						if (ContractImportRel.IsDistributorContract)
							IsDistributorContract = true

						LocalContractImport     = ContractImportRel.ContractImport 

						LocalCompany            = ContractImportGPOMembershipRel.ParticipantLocation.Company
						LocalLocation           = ContractImportGPOMembershipRel.ParticipantLocation.Location
						LocalRequestingLocation = ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation
						LocalPricingGroup       = ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup
						if (ContractImportTierMemberRel !exists
						and ContractImportTierMemberByLocationRel !exists)
							invoke IndirectCreate ContractImportTierMember
								invoked.ContractGroup            					= LocalContractGroup
								invoked.ContractImport           					= ContractImportRel.ContractImport
								invoked.ContractImportTierMember.Company 			= LocalCompany
								invoked.ContractImportTierMember.Location 			= LocalLocation
								invoked.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
								invoked.ContractImportTierMember.PricingGroup 		= LocalPricingGroup
								if (IsManufacturerContract)
									invoked.Tier                   					= TierLevel
									invoked.LocalTierName                           = TierLevel
									invoked.LocalTierDescription                    = ContractTierDescription
								invoked.ContractImportGPOMembership 				= OrganizationEID
								invoked.GPOContractNumber        					= ContractNumber
	                            invoked.EffectiveDate            					= ContractStartDate
	                            invoked.ExpirationDate           					= ContractEndDate
	                            if (IsDistributorContract)
	                            	invoked.FromDistributor                         = true
								invoked.FromNewMember                               = AutoUpdateContracts
								invoked.FromGHXInterface                            = true

		LoadSuppliers is a Set Action
			Parameters
				SupplierGroup
			
			Instance Selection
				where (VendorEID entered
				or     ManufacturerEID entered)
			
			Sort Order
				ContractNumber
				ContractGPOGHXInterface

			Action Rules
			
				Instance Rules
					LocalContractGroup     		 = SupplierGroup
					LocalManufacturerSupplier    = ManufacturerEID
					LocalDistributorSupplier     = VendorEID
					if (ManufacturerEID entered
					and ManufacturerSupplierRel !exists)
						invoke Create ContractImportGPOSupplier					
							invoked.SupplierGroup      			= SupplierGroup
							invoked.ContractImportGPOSupplier   = ManufacturerEID
							invoked.Name               			= Manufacturer
					
					if (VendorEID entered
					and VendorEID != ManufacturerEID
					and DistributorSupplierRel !exists)
						invoke Create ContractImportGPOSupplier					
							invoked.SupplierGroup      			= SupplierGroup
							invoked.ContractImportGPOSupplier   = VendorEID
							invoked.Name               			= Vendor
							
		LoadMembers is a Set Action
			Parameters
				ContractGroup
				
			Instance Selection
				where (OrganizationEID entered)
			
			Sort Order
				ContractNumber
				ContractGPOGHXInterface

			Action Rules
			
				Instance Rules
				
					if (ContractImportGPOMembershipRel !exists)
						invoke Create ContractImportGPOMembership
							invoked.ContractGroup     			= ContractGroup
							invoked.ContractImportGPOMembership = OrganizationEID
							invoked.Name                        = Organization
							
        DeleteOldInterfaceRecordsSet is a Set Action         		
        	restricted
        	Parameters
        		ParmGPOContractNumber is a GPOContractNumber
        		ParmEndDate           is Date
        		
        	Instance Selection
        		where (ParmGPOContractNumber = ContractNumber)
        		
        	Sort Order
        		ContractNumber
        		ContractEndDate descending
        		ContractGPOGHXInterface
        	
        	Local Fields
        		LatestDate is Date
        	
        	Action Rules
        		ContractNumber Set Rules
        			Entrance Rules
        				LatestDate = ParmEndDate
        				
        			Exit Rules
        				initialize LatestDate
        		
        		Instance Rules
        			if (ContractEndDate < LatestDate
        			or (ItemEndDate     < current corporate date 
        			and ItemEndDate     entered))
        				invoke Delete    

		LoadGPOSupplierUOMs is a Set Action
			restricted  
			Parameters 
				ParmSupplierGroup   is a SupplierGroup 
				ParmSupplier        is a GPOSupplierID 

			Instance Selection 
				where (ParmSupplier = ManufacturerEID
				and    UOM entered)





			
			Action Rules 

				Instance Rules 

					LocalContractGroup				= ParmSupplierGroup
					LocalManufacturerSupplier       = ManufacturerEID 
					LocalGPOUOM                     = UOM 
					if (GPOSupplierUOMCreateRel !exists)
						invoke CreateNoEdits ContractImportGPOSupplierUOM
							invoked.SupplierGroup					= ParmSupplierGroup 
							invoked.ContractImportGPOSupplier		= ManufacturerEID 
							invoked.ContractImportGPOSupplierUOM	= UOM						    				
        			
        			
							
