ProjectRole is a BusinessClass
    owned by Projects
    prefix is ACH
    classic name is ACROLE

    Ontology
        symbolic key is ProjectRole
            classic set name is ACHSET1
            classic name is ROLE

    Patterns
        implements StaticJava

    Persistent Fields

        Description
        RecordType  is AlphaUpper size 1
            States
                Userdefined           value is blank
                    default label is "UserDefined"
                Manager               value is "1"
                Administrator         value is "2"
                PrincipalInvestigator value is "3"
                XMApprover			  value is "4"

	Conditions
	
		IsSystemRole	
			restricted
			when (RecordType entered)
			
		SystemRolesExist
			restricted		
			when (IsManager
			or    IsPrincipalInvestigator
			or    IsAdministrator
			or    IsXMApprover)

		IsManager
			restricted		
			when (first ManagerRoleRel exists)
			
		IsPrincipalInvestigator
			restricted		
			when (first PrincipalInvestigatorRoleRel exists)

		IsAdministrator
			restricted		
			when (first AdministratorRoleRel exists)
			
		HasProjectAssignments
			restricted		
			when (ProjectAssignment set exists)
		
		IsXMApprover
			restricted		
			when (XMApproverRoleRel exists)
									
	Relations
					
		ManagerRoleRel	
			one-to-many relation to ProjectRole
			Field Mapping uses SystemProjectRoles
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup			
				related.RecordType		       = "1"
				
		AdministratorRoleRel
			one-to-many relation to ProjectRole
			Field Mapping uses SystemProjectRoles
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup			
				related.RecordType		       = "2"
		
		PrincipalInvestigatorRoleRel
			one-to-many relation to ProjectRole
			Field Mapping uses SystemProjectRoles
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup			
				related.RecordType		       = "3"

		XMApproverRoleRel
			one-to-many relation to ProjectRole
			Field Mapping uses SystemProjectRoles
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup			
				related.RecordType		       = "4"

    Sets

        Set2
            indexed
            Sort Order
				FinanceEnterpriseGroup            
                RecordType
                ProjectRole

		SystemProjectRoles
            Sort Order
				FinanceEnterpriseGroup            
                RecordType
                ProjectRole
            Instance Selection
            	where (IsSystemRole)

	Field Rules
		Description
			required

    Actions
        Create is a Create Action
			Field Rules
				RecordType
					if (RecordType.Manager)
						constraint (!IsManager)	
							"ManagerRoleAlreadyExists"
					if (RecordType.PrincipalInvestigator)
						constraint (!IsPrincipalInvestigator)
							"PrincipalInvestigatorRoleAlreadyExists"
					if (RecordType.Administrator)
						constraint (!IsAdministrator)
							"AdministratorRoleAlreadyExists"		
					if (RecordType.XMApprover)
						constraint (!IsXMApprover)
							"X_M_ApproverRoleAlreadyExists"		

        Update is an Update Action
			Field Rules
				RecordType
					if (RecordType changed)
						if (old RecordType > 0)
							cannot be changed
								"CannotChangeASystemRolesType"
						if (old RecordType.Userdefined)
							if (RecordType.Manager)
								constraint (!IsManager)	
									"ManagerRoleAlreadyExists"
							if (RecordType.PrincipalInvestigator)
								constraint (!IsPrincipalInvestigator)
									"PrincipalInvestigatorRoleAlreadyExists"
							if (RecordType.Administrator)
								constraint (!IsAdministrator)
									"AdministratorRoleAlreadyExists"
							if (RecordType.XMApprover)
								constraint (!IsXMApprover)
									"X/MApproverRoleAlreadyExists"
						constraint (!HasProjectAssignments)
							"ProjectRoleIsUsedOnAProjectAssignment;CannotChangeType"						
																					
        Delete is a Delete Action
        	valid when (!HasProjectAssignments)
