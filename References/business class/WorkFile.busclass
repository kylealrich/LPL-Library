WorkFile is a BusinessClass
    owned by la
    prefix is wrkfl
    
    Ontology
        symbolic key is WorkFile

    Patterns
        disable Auditing
        disable EffectiveDated
        implements CreateStamp
        implements UpdateStamp

    Persistent Fields
        WorkFileClass       is a BusinessClassName
        WorkFileName        is Alpha size up to 100
        ProgramName         is Alpha size up to 100

        IsOpen              is Boolean
            default label is "Open"

        SortSetName         is Alpha size up to 100 



        AsyncActionRequest  
            delete ignored

        CheckPointRef       is BusinessObjectReference to CheckPoint
            default label is "CheckpointReference"

    Sets



        ByClassNameRequest
            indexed
            Sort Order
                WorkFileClass
                WorkFileName
                AsyncActionRequest

        ByWorkFileName
            indexed
            Sort Order
                WorkFileName
                update stamp
                WorkFileClass
                AsyncActionRequest

        ByWorkFileClass
            not indexed
            Sort Order
                WorkFileClass
                update stamp
                WorkFileName
                AsyncActionRequest


        ByUpdateStamp
            indexed
            Sort Order
                update stamp
                WorkFileClass
                WorkFileName
                AsyncActionRequest


        ByCheckPointRef
            indexed
            Sort Order
                CheckPointRef
                WorkFile

    Conditions









        ScheduledForDeletion
            default label is "WorkFileScheduledForDeletion"
            when (AsyncActionRequest entered and AsyncActionRequest exists)

        ActionRequestIsBlank
            default label is "ActionRequestIsBlank"
            when (AsyncActionRequest not entered)

        MissingActionRequest
            default label is "MissingActionRequest"
            when (AsyncActionRequest entered and not AsyncActionRequest exists)

        ScheduledForDeletionButFailed
            default label is "WorkFileScheduledForDeletionButFailed"
            when (AsyncActionRequest entered and AsyncActionRequest exists and AsyncActionRequest.FailedTriggersExist)

        ScheduledForDeletionButNotSuccessfullyCompleted
            default label is "WorkFileScheduledForDeletionButNotSuccessfullyCompleted"
            when (AsyncActionRequest entered and AsyncActionRequest exists and AsyncActionRequest.NotSuccessfullyCompleted)

        ScheduledForDeletionAndSuccessfullyCompleted
            default label is "WorkFileScheduledForDeletionAndSuccessfullyCompleted"
            when (AsyncActionRequest entered and (AsyncActionRequest not exists or AsyncActionRequest.SuccessfullyCompleted))

    Delete Rules
        constraint (!IsOpen or CheckPointRef not entered or CheckPointRef not exists)
            "DeleteOrPurgeIsNotValidOnAWorkFileThatIsOpenAndHasACheckpoint"

    Actions
        Create is a Create Action 
            restricted

        Close is an Instance Action 
            valid when (!ScheduledForDeletion)

            Action Rules
                IsOpen = false

        RemoveWorkFile is an Instance Action
            valid when (!ScheduledForDeletion)
            Action Rules

                invoke DeleteChildren in background
                	assign async action request id to AsyncActionRequest

        DeleteChildren is an Instance Action 
            restricted
            manual transaction
            manual update


        Purge is a Purge Action
            valid when (ScheduledForDeletionAndSuccessfullyCompleted)
