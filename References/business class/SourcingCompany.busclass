SourcingCompany is a BusinessClass
    owned by ss
    prefix is SSC

    Ontology
    	symbolic key is SourcingCompany 
    
    Patterns
        implements Proxy for SourcingCompanyExtension
    
    Persistent Fields
    	Name
    	EventApprovalRequired			is Boolean
    	AmendmentApprovalRequired		is Boolean
  		AwardApprovalRequired			is Boolean
    	DisputePeriod					is Boolean
    	AmendmentCriteria
    	SourcingGroup 
    	HeaderResponseText				is Alpha size 250
    	QuestionResponseText			is Alpha size 250
    	AttachmentResponseText			is Alpha size 250
    	LineResponseText				is Alpha size 250
    	LineQuestionResponseText		is Alpha size 250
    	BidBondText                     is Alpha size 250
    	EventEmailSubject               is Alpha size 100
    		Text Variables
    			Event					value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    			EventOpenDate           value is SourcingEvent.OpenDate
    	EventEmailContent				is RichText
    		Text Variables
    			Event					value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    			EventOpenDate           value is SourcingEvent.OpenDate
    			EventCloseDate          value is SourcingEvent.CloseDate
    			EventPreviewDate        value is SourcingEvent.PreviewDate
    	AmendmentEmailSubject           is Alpha size 100
    		Text Variables
    			Event					value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	AmendmentEmailContent			is RichText
    		Text Variables
    			Event					value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	CancelEmailSubject              is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	CancelEmailContent				is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
		SuspendEmailSubject              is Alpha size 100
			Text Variables
				Event                   value is SourcingEvent
				EventName               value is SourcingEvent.Name
		SuspendEmailContent				is RichText
			Text Variables
				Event                   value is SourcingEvent
				EventName               value is SourcingEvent.Name
    	AwardEmailSubject               is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	AwardEmailContent				is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	OutputEmailSubject              is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	OutputEmailContent				is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	QANDAEmailSubject               is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	QANDAEmailContent				is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	BuyerQANDAEmailSubject          is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    			Supplier                value is Supplier
    			SupplierContactName     value is SupplierSourceId.MainContact.FirstAndLastName
    	BuyerQANDAEmailContent			is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    			Supplier                value is Supplier
    			SupplierContactName     value is SupplierSourceId.MainContact.FirstAndLastName
    	NeedsApprovalEmailSubject       is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	NeedsApprovalEmailContent		is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	RejectedEmailSubject            is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	RejectedEmailContent			is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	DisapprovedEmailSubject         is Alpha size 100
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	DisapprovedEmailContent			is RichText
    		Text Variables
    			Event                   value is SourcingEvent
    			EventName               value is SourcingEvent.Name
    	SendEventAvailableEmail			is Boolean
    	CreateEventAvailableMessage     is Boolean
    	SendAmendedEmail				is Boolean
    	CreateAmendedMessage            is Boolean
    	SendCanceledEmail				is Boolean
    	CreateCanceledMessage           is Boolean
    	SendLineCanceledEmail			is Boolean
    	CreateLineCanceledMessage       is Boolean
		SendSuspendedEmail              is Boolean
		CreateSuspendedMessage          is Boolean
    	SendQAndAAnsweredEmail			is Boolean
    	CreateQAndAAnsweredMessage      is Boolean
    	SendAwardedEmail				is Boolean
    	CreateAwardedMessage            is Boolean
    	SendOutputEmail					is Boolean
    	CreateOutputMessage             is Boolean
    	ApproverEmailAddress			is an EmailAddress	
    	SendToAwardedOnly				is Boolean
    	ConsolidateEmails				is Boolean
    	OnlyNotifyResponders            is Boolean
    	PostingOptions
    	RequireNonAwardReason			is Boolean
    	RecentlyFinalizeDay				is Numeric size 6
    	YellowAlert						is Numeric size 6
    	RedAlert						is Numeric size 6
		NegotiationText					is Alpha size 250 
		AllowTermsToBeNegotiated		is Boolean
    	ReportLogo						is an Attachment
		ChangeCloseDateWithResponses	is Numeric 1
			default label is "WhenCloseDateCanBeChangedToLaterForSealedBids"
			States
				Always				 		value is 0 
               	IfNoResponsesExist          value is 1 
               	Never                       value is 2
		CloseDateChangeNonSealedEarlier is Numeric 1
			default label is "WhenCloseDateCanBeChangedToEarlierForNonSealedBids"
			States
				Always				 		value is 0 
               	IfNoResponsesExist          value is 1 
               	Never                       value is 2
		CloseDateChangeNonSealedLater   is Numeric 1
			default label is "WhenCloseDateCanBeChangedToLaterForNonSealedBids"		
			States
				Always				 		value is 0 
               	IfNoResponsesExist          value is 1 
               	Never                       value is 2
		CloseDateChangeSealedEarlier    is Numeric 1      
			default label is "WhenCloseDateCanBeChangedToEarlierForSealedBids"		
			States
				Always				 		value is 0 
               	IfNoResponsesExist          value is 1 
               	Never                       value is 2
    	ValidatedSuppliersForEvent      is Boolean
    	IncompleteBuyerScoringEdit
    	UseCommentsForEventLineDesc	    is Boolean
    	VendorItemProcessing       
    	VendorItemDescriptionRequired   is Boolean
    	DeliveryDateResponseRequired    is Boolean
    		default label is "RequireSupplierToEnterDeliveryDateWhenRespondingToLinesWithPOOutput"
    	WizardForTerms                  is Boolean
		WizardForAttachments            is Boolean
		WizardForMeetings               is Boolean
		WizardForQuestions              is Boolean
		WizardForContacts               is Boolean
		WizardForWeighting              is Boolean
		ShowResponsesViewed             is Boolean
			default label is "ShowResponsesNotViewedOnAnalysisForm"
		BuyerCanAddCommodityCode        is Boolean
		CommodityCodeEmail              is an EmailAddress 
			holds pii
		CanRequestResponseModification  is Boolean	
		CanAlwaysCreateEventAttachment  is Boolean
			default label is "AllowEventAndEventLineAttachmentsToBeCreatedAfterEventClosed"
		EmailAddressOption				is Numeric size 1
			States
				UseBuyerEmailAddress	value is 0
				UseCompanyEmailAddress	value is 1
		EventEmailAddress				is an EmailAddress
	Local Fields
		LocalCounter					is Numeric size 2

    Context Fields
    	SourcingEvent
    	Supplier
    	SupplierSourceId
    	
    Derived Fields
 
		RepresentativeText is a StringField
			type is Text
			default label is "Company"
			Company " - " Company.Name

 		ActorSecurityGroup is a DerivedField
			type is like CompanySecurityGroup
			return actor.context.CompanySecurityGroup
    
    	FinalEventEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return EventEmailSubject text
			
		FinalEventEmailContent is a DerivedField
			type is RichText
			restricted
			return EventEmailContent text	
			
		FinalAmendmentEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return AmendmentEmailSubject text
			
		FinalAmendmentEmailContent is a DerivedField
			type is RichText
			restricted
			return AmendmentEmailContent text	
			
		FinalCancelEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return CancelEmailSubject text
			
		FinalCancelEmailContent is a DerivedField
			type is RichText
			restricted
			return CancelEmailContent text	
		
		FinalSuspendEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return SuspendEmailSubject text
			
		FinalSuspendEmailContent is a DerivedField
			type is RichText
			restricted
			return SuspendEmailContent text	

		FinalAwardEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return AwardEmailSubject text
			
		FinalAwardEmailContent is a DerivedField
			type is RichText
			restricted
			return AwardEmailContent text	
		
		FinalOutputEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return OutputEmailSubject text
			
		FinalOutputEmailContent is a DerivedField
			type is RichText
			restricted
			return OutputEmailContent text	
		
		FinalQANDAEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return QANDAEmailSubject text
			
		FinalQANDAEmailContent is a DerivedField
			type is RichText
			restricted
			return QANDAEmailContent text
			
		BuyerFinalQANDAEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return BuyerQANDAEmailSubject text
			
		BuyerFinalQANDAEmailContent is a DerivedField
			type is RichText
			restricted
			return BuyerQANDAEmailContent text
			
		FinalNeedsApprovalEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return NeedsApprovalEmailSubject text
			
		FinalNeedsApprovalEmailContent is a DerivedField
			type is RichText
			restricted
			return NeedsApprovalEmailContent text	
			
		FinalDisapprovedEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return DisapprovedEmailSubject text
			
		FinalDisapprovedEmailContent is a DerivedField
			type is RichText
			restricted
			return DisapprovedEmailContent text	
			
		FinalRejectedEmailSubject is a DerivedField
			type is Alpha size 100
			restricted
			return RejectedEmailSubject text
			
		FinalRejectedEmailContent is a DerivedField
			type is RichText
			restricted
			return RejectedEmailContent text	
			
		DraftEvents is a ComputeField
			type is Numeric size 4
			restricted
			(instance count of DraftEventRel)
			
		OpenEvents is a ComputeField
			type is Numeric size 4
			restricted
			(instance count of OpenEventRel)
			    
    Field Rules
    	Name
    		required

    	SourcingGroup
    		required
    		initial value is Company.BusinessGroup
    		constraint (SourcingGroup = Company.BusinessGroup)
    			"SourcingGroup<SourcingGroup>MustBeSameAsVendorBusinessGroup<Company.BusinessGroup>OnGlobalLedgerCompany"
    	SendCanceledEmail
   			if (SendAmendedEmail
   			and AmendmentCriteria.CancelSourcingEvent)
   				SendCanceledEmail = false
   					
   		SendLineCanceledEmail
   			if (SendAmendedEmail
   			and AmendmentCriteria.CancelSourcingEventLine)
   				SendLineCanceledEmail = false

   		CreateCanceledMessage
   			if (CreateAmendedMessage
   			and AmendmentCriteria.CancelSourcingEvent)
   				CreateCanceledMessage = false

   		CreateLineCanceledMessage
   			if (CreateAmendedMessage
   			and AmendmentCriteria.CancelSourcingEventLine)
   				CreateLineCanceledMessage = false
    	HeaderResponseText
    		default to SourcingGroup.HeaderResponseText
    	QuestionResponseText
    		default to SourcingGroup.QuestionResponseText
    	AttachmentResponseText
    		default to SourcingGroup.AttachmentResponseText
    	LineResponseText
    		default to SourcingGroup.LineResponseText
    	LineQuestionResponseText
    		default to SourcingGroup.LineQuestionResponseText
    	BidBondText
    		default to SourcingGroup.BidBondText
    	SendAmendedEmail
   			if (!SendAmendedEmail
   			and !CreateAmendedMessage)
   				constraint (!OnlyNotifyResponders)
   					"SendAmendedmentNotificationsOnlyToRespondingSuppliersCannotBeSetToTrueIfAmendedEmailAndPortalMessageAreBothSetToFalse"
   		CreateAmendedMessage
   			if (!CreateAmendedMessage
   			and !SendAmendedEmail)
   				constraint (!OnlyNotifyResponders)
   					"SendAmendedmentNotificationsOnlyToRespondingSuppliersCannotBeSetToTrueIfAmendedEmailAndPortalMessageAreBothSetToFalse"
   		OnlyNotifyResponders
   			if (!CreateAmendedMessage
   			and !SendAmendedEmail)
   				constraint (!OnlyNotifyResponders)
   					"SendAmendedmentNotificationsOnlyToRespondingSuppliersCannotBeSetToTrueIfAmendedEmailAndPortalMessageAreBothSetToFalse"
    	EventEmailContent
			default to SourcingGroup.EventEmailContent			
		AmendmentEmailContent
			default to SourcingGroup.AmendmentEmailContent		
		CancelEmailContent
			default to SourcingGroup.CancelEmailContent			
		SuspendEmailContent
			default to SourcingGroup.SuspendEmailContent
		AwardEmailContent
			default to SourcingGroup.AwardEmailContent
		OutputEmailContent
			default to SourcingGroup.OutputEmailContent
		ApproverEmailAddress
			default to SourcingGroup.ApproverEmailAddress
			if (EventApprovalRequired
			or  AwardApprovalRequired)
				required		
		QANDAEmailContent
    		default to SourcingGroup.QANDAEmailContent
    	BuyerQANDAEmailContent
    		default to SourcingGroup.BuyerQANDAEmailContent
    	NeedsApprovalEmailContent
    		default to SourcingGroup.NeedsApprovalEmailContent
    	DisapprovedEmailContent
    		default to SourcingGroup.DisapprovedEmailContent
    	RejectedEmailContent
    		default to SourcingGroup.RejectedEmailContent
    	EventEmailSubject
			default to SourcingGroup.EventEmailSubject			
		AmendmentEmailSubject
			default to SourcingGroup.AmendmentEmailSubject		
		CancelEmailSubject
			default to SourcingGroup.CancelEmailSubject			
		SuspendEmailSubject
			default to SourcingGroup.SuspendEmailSubject	
		AwardEmailSubject
			default to SourcingGroup.AwardEmailSubject
		OutputEmailSubject
			default to SourcingGroup.OutputEmailSubject
		QANDAEmailSubject
    		default to SourcingGroup.QANDAEmailSubject
    	BuyerQANDAEmailSubject
    		default to SourcingGroup.BuyerQANDAEmailSubject
    	NeedsApprovalEmailSubject
    		default to SourcingGroup.NeedsApprovalEmailSubject
    	DisapprovedEmailSubject
    		default to SourcingGroup.DisapprovedEmailSubject
    	RejectedEmailSubject
    		default to SourcingGroup.RejectedEmailSubject
    	ReportLogo
    		default to SourcingGroup.ReportLogo
    	PostingOptions 
			default to SourcingGroup.PostingOptions
		RecentlyFinalizeDay
			default to SourcingGroup.RecentlyFinalizeDay
		NegotiationText
			default to SourcingGroup.NegotiationText
    	IncompleteBuyerScoringEdit
			default to SourcingGroup.IncompleteBuyerScoringEdit
		UseCommentsForEventLineDesc
			default to SourcingGroup.UseCommentsForEventLineDesc
		VendorItemProcessing
			default to SourcingGroup.VendorItemProcessing
		WizardForTerms                  
			default to SourcingGroup.WizardForTerms
		WizardForAttachments            
			default to SourcingGroup.WizardForAttachments
		WizardForMeetings               
			default to SourcingGroup.WizardForMeetings
		WizardForQuestions              
			default to SourcingGroup.WizardForQuestions
		WizardForContacts               
			default to SourcingGroup.WizardForContacts
		WizardForWeighting              
			default to SourcingGroup.WizardForWeighting

		CommodityCodeEmail
			if (!BuyerCanAddCommodityCode)
				initialize
		
		EmailAddressOption
			default to SourcingGroup.EmailAddressOption
			if (EmailAddressOption = 0)
				initialize EventEmailAddress
		EventEmailAddress
			default to SourcingGroup.EventEmailAddress
    Conditions
		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)
			    
    	AmendmentSelected
    		restricted
    		when (any AmendmentCriteria entered)

		HasReportLogo
			restricted
			when (ReportLogo entered)
			
		RecordExists
			restricted
			when (Company entered)

	Relations
		SecurityGroupRel
			one-to-one relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		  = Company.FinanceEnterpriseGroup
				related.DimensionGroupType	   		  = 2
				related.FinanceDimensionSecurityGroup = ActorSecurityGroup
				
		SecurityGroupDetailRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses Set1
				related.Company						= Company
				related.GeneralLedgerCompanyGroup  	= SecurityGroupRel.GeneralLedgerCompanyGroup
					
		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = Company

    	PocompanyRel
    		one-to-one relation to PurchasingCompany
    		Field Mapping uses symbolic key
    			related.Company	= Company
    	
    	InventoryCompanyRel
    		one-to-one relation to InventoryCompany
    		Field Mapping uses symbolic key
    			related.Company	= Company

		SourcingStatusRel
			one-to-one relation to SourcingStatus
			Field Mapping uses symbolic key
				related.Company				= Company
				related.SourcingStatus		= LocalCounter

		SourcingCompanyExtension
			one-to-one relation to SourcingCompanyExtension
			Field Mapping uses symbolic key
				related.Company             = Company
				
		DraftEventRel is a SourcingEvent set
			Instance Selection
				where (related.Status.Draft)
				
		OpenEventRel is a SourcingEvent set
			Instance Selection
				where (related.Status.Open)
	
    Sets
    	BySourcingGroup
    		duplicates
    		Sort Order
 				SourcingGroup
 				Company
    
    Rule Blocks
    	BuildSourcingStatus
    		LocalCounter = 1
			while (LocalCounter < 8)
				if (!SourcingStatusRel exists)
					invoke Create SourcingStatus
						invoked.Company 		= Company
						invoked.SourcingStatus	= LocalCounter
				LocalCounter +=1
			
    Actions
    	Create is a Create Action
    		Field Rules
    			EventApprovalRequired
    				default to SourcingGroup.EventApprovalRequired
    			AmendmentApprovalRequired
    				default to SourcingGroup.AmendmentApprovalRequired
    			AwardApprovalRequired
    				default to SourcingGroup.AwardApprovalRequired	
    			DisputePeriod
    				default to SourcingGroup.DisputePeriod
    			AmendmentCriteria
    				default to SourcingGroup.AmendmentCriteria
				SendToAwardedOnly
    				default to SourcingGroup.SendToAwardedOnly
    			SendEventAvailableEmail			
    				default to SourcingGroup.SendEventAvailableEmail
    			CreateEventAvailableMessage     
    				default to SourcingGroup.CreateEventAvailableMessage
    			SendAmendedEmail				
    				default to SourcingGroup.SendAmendedEmail
    			CreateAmendedMessage            
    				default to SourcingGroup.CreateAmendedMessage
    			SendCanceledEmail				
    				default to SourcingGroup.SendCanceledEmail
    			CreateCanceledMessage           
    				default to SourcingGroup.CreateCanceledMessage
    			SendLineCanceledEmail			
    				default to SourcingGroup.SendLineCanceledEmail
    			CreateLineCanceledMessage       
    				default to SourcingGroup.CreateLineCanceledMessage
    			SendSuspendedEmail				
    				default to SourcingGroup.SendSuspendedEmail
    			CreateSuspendedMessage           
    				default to SourcingGroup.CreateSuspendedMessage
    			SendQAndAAnsweredEmail			
    				default to SourcingGroup.SendQAndAAnsweredEmail
    			CreateQAndAAnsweredMessage      
    				default to SourcingGroup.CreateQAndAAnsweredMessage
    			SendAwardedEmail				
    				default to SourcingGroup.SendAwardedEmail
    			CreateAwardedMessage            
    				default to SourcingGroup.CreateAwardedMessage
    			SendOutputEmail					
    				default to SourcingGroup.SendOutputEmail
    			CreateOutputMessage             
					default to SourcingGroup.CreateOutputMessage    			
				ConsolidateEmails		
    				default to SourcingGroup.ConsolidateEmails
    			OnlyNotifyResponders
    				default to SourcingGroup.OnlyNotifyResponders
    			RequireNonAwardReason
    				default to SourcingGroup.RequireNonAwardReason
				AllowTermsToBeNegotiated
					default to SourcingGroup.AllowTermsToBeNegotiated
				ChangeCloseDateWithResponses
    				default to SourcingGroup.ChangeCloseDateWithResponses
				CloseDateChangeNonSealedEarlier
 					default to SourcingGroup.CloseDateChangeNonSealedEarlier				
				CloseDateChangeNonSealedLater
 					default to SourcingGroup.CloseDateChangeNonSealedLater				
				CloseDateChangeSealedEarlier
 					default to SourcingGroup.CloseDateChangeSealedEarlier					
    			ValidatedSuppliersForEvent
					default to SourcingGroup.ValidatedSuppliersForEvent
				VendorItemDescriptionRequired
					default to SourcingGroup.VendorItemDescriptionRequired
				DeliveryDateResponseRequired
					default to SourcingGroup.DeliveryDateResponseRequired
				WizardForTerms                  
					default to SourcingGroup.WizardForTerms
				WizardForAttachments            
					default to SourcingGroup.WizardForAttachments
				WizardForMeetings               
					default to SourcingGroup.WizardForMeetings
				WizardForQuestions              
					default to SourcingGroup.WizardForQuestions
				WizardForContacts               
					default to SourcingGroup.WizardForContacts
				WizardForWeighting              
					default to SourcingGroup.WizardForWeighting
				BuyerCanAddCommodityCode
					default to SourcingGroup.BuyerCanAddCommodityCode
				CommodityCodeEmail
					default to SourcingGroup.CommodityCodeEmail
				ShowResponsesViewed
					default to SourcingGroup.ShowResponsesViewed								
				CanRequestResponseModification
					default to SourcingGroup.CanRequestResponseModification
				CanAlwaysCreateEventAttachment
					default to SourcingGroup.CanAlwaysCreateEventAttachment
		
			Action Rules
				if (YellowAlert entered)
					if (!RedAlert entered)
						RedAlert = SourcingGroup.RedAlert
					constraint (YellowAlert >= 0)
						"CannotEnterNegativeDaysInYellowAlert"
					constraint (RedAlert entered)
						"MustEnterRedAlertNumberOfDays"
					constraint (YellowAlert > RedAlert)
						"YellowAlertNumberOfDaysMustBeGreaterThanRedAlertNumberOfDays"
				else
	    			YellowAlert = SourcingGroup.YellowAlert

				if (RedAlert entered)
					if (!YellowAlert entered)
						YellowAlert = SourcingGroup.YellowAlert
					constraint (RedAlert >= 0)
						"CannotEnterNegativeDaysInRedAlert"
					constraint (YellowAlert entered)
						"MustEnterYellowAlertNumberOfDays"
					constraint (RedAlert < YellowAlert)
						"YellowAlertNumberOfDaysMustBeGreaterThanRedAlertNumberOfDays"
				else
	    			RedAlert = SourcingGroup.RedAlert

			Exit Rules
				include BuildSourcingStatus
    				
    	Update is an Update Action
    		Action Rules
				if (YellowAlert entered)
					constraint (YellowAlert >= 0)
						"CannotEnterNegativeDaysInYellowAlert"
					constraint (RedAlert entered)
						"MustEnterRedAlertNumberOfDays"
					constraint (YellowAlert > RedAlert)
						"YellowAlertNumberOfDaysMustBeGreaterThanRedAlertNumberOfDays"

				if (RedAlert entered)
					constraint (RedAlert >= 0)
						"CannotEnterNegativeDaysInRedAlert"
					constraint (YellowAlert entered)
						"MustEnterYellowAlertNumberOfDays"
					constraint (RedAlert < YellowAlert)
						"YellowAlertNumberOfDaysMustBeGreaterThanRedAlertNumberOfDays"

			Exit Rules
				include BuildSourcingStatus
				
    	Delete is a Delete Action

		UpdateSourcingGroup is an Instance Action
			restricted
			Parameters
				PrmSourcingGroup		is a SourcingGroup

			Parameter Rules
				PrmSourcingGroup
					required

			Action Rules
				constraint (SourcingEvent set not exists)
					"SourcingEventsExist;CannotChangeSourcingGroupOnSourcingCompany"

				SourcingGroup = PrmSourcingGroup

