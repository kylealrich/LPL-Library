ActorContextTemplateDetail is a BusinessClass
	owned by person
	
	prefix is ACT2
	
	Ontology
		symbolic key is ActorContextTemplateDetail
		
	
	Patterns
	
	Persistent Fields
		ContextProperty
        Value 				is FieldValue	
	
	Local Fields
		LocalActor 				is an Actor
		LocalContextProperty	is a ContextProperty

	Field Rules
	
		ContextProperty
			required
			
	Conditions
	
		NoValue
			restricted
			when (Value = blank)

	Relations
	
		ActorContextRel
			one-to-many relation to ActorContext
			Field Mapping uses symbolic key
				related.Actor			= LocalActor
		 		related.ContextProperty	= LocalContextProperty

		DifferentDataAreaRel
			one-to-many relation to ActorContextTemplateDetail
			Field Mapping uses symbolic key
				related.ActorContextTemplate    = ActorContextTemplate
			Instance Selection
				where (related.ContextProperty.DataArea  != ContextProperty.DataArea)		
		
		DuplicatePropertyRel
			one-to-many relation to ActorContextTemplateDetail
			Field Mapping uses symbolic key
				related.ActorContextTemplate    = ActorContextTemplate
			Instance Selection
				where (related.ContextProperty  = ContextProperty)

	Actions
	
		Create is a Create Action
		
			Action Rules
				constraint (DuplicatePropertyRel !exists)
					"ContextPropertyAlreadyDefinedForThisContextTemplate"
					
				constraint (DifferentDataAreaRel !exists)
					"ContextPropertyDataAreasCannotBeDifferentOnATemplate"
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		CreateActorContext is an Instance Action
			restricted
			Parameters
				ParamActor				is an Actor
					default label is "Actor"
				DuplicateHandling       is Boolean
								
			Action Rules
				
				LocalActor 				= ParamActor
				LocalContextProperty	= ContextProperty
				 
				if (ActorContextRel !exists)
				
					invoke Create ActorContext
						invoked.Actor				= ParamActor
						invoked.ContextProperty		= ContextProperty
						invoked.Value				= Value

				if (ActorContextRel exists
				and DuplicateHandling = true)
					invoke Update ActorContextRel
						invoked.Value = Value

