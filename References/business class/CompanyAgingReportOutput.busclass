CompanyAgingReportOutput is a BusinessClass
	owned by ar
	prefix is CARO

	Ontology
		symbolic key is CompanyAgingReportOutput

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		FileName				is AlphaUpper size 50
		CreateDate				is TimeStamp
		OutputFile				is a PaymentAttachment

	Transient Fields

	Local Fields

	Derived Fields
		DataFileOutputFileName is a StringField
			type is Alpha 100
			"Company"
			Company

		RepresentativeTextCompany is a StringField
			type is Text
			default label is "CompanyAgingReportOutput"
			"Company Aging Report Output - " CompanyAgingReportOutput " for Company " Company.RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			return RepresentativeTextCompany


	Conditions
		HasFileAttachment
			restricted
			when (OutputFile entered)

	Relations
		CompanyAgingOutputDetailRel is a CompanyAgingReportOutputDetail set

	Sets
		ByCreateDate
			Sort Order
				CreateDate				descending
				Company
				CompanyAgingReportOutput

	Field Rules
		CreateDate
			default to current timestamp 

		FileName
			default to "COMPANYAGINGREPORT<CreateDate>"


	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted


		Purge is a Purge Action
			restricted


		CreateCompanyAgingReportOutputFile is an Instance Action
			Action Rules
				trigger "CompanyAgingReport" PA service
					resume on error
					title is "CreateCompanyAgingReportOutputFile"	
					Variables
						Company
						CompanyAgingReportOutput


		PurgeCompanyAgingOutputRecords is a Set Action
			run in foreground
			completion message is "PurgeOfCompanyAgingOutputRecordsHasCompleted"
			Parameters
				PrmReceivableCompany						is a ReceivableCompany
				PrmCreateDateRange							is a DateRange 

			Parameter Rules
				PrmCreateDateRange
					required
				PrmReceivableCompany
					required
			Instance Selection
				where (CreateDate date within PrmCreateDateRange
				and	Company = PrmReceivableCompany)
	
			Action Rules
				Instance Rules
					invoke Purge

