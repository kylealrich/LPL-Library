Manufacturer is a BusinessClass
    owned by ic
    prefix is IMC
    classic name is ICMANFCODE

    Ontology
        symbolic key is Manufacturer
            classic set name is IMCSET1
            classic name for Manufacturer.ManufacturerCode is MANUF-CODE
            classic name for Manufacturer.ManufacturerDivision is MANUF-DIVISION

    Patterns
        implements StaticJava
        disable AuditIndex
		implements BODId
		
    Persistent Fields

        Description
        LabelerIdentificationCode
            classic name is LIC-CODE
		Active
        GlobalLocationNumber
            classic name is GLN-NBR
		DunAndBradstreetNumber			is a DunsNumber
			default label is "Dun&Bradstreet"

	Attach Rules
		if (not BypassRuleForTheseFiles)
			constraint (Active)
				"Manufacturer<Manufacturer>MustBeActive"

    Local Fields
		LocalContractTierGroup
		LocalContractImportTierGroup
		LocalContractRebate
		LocalPeriod					  is Numeric size 5
		ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		NativeLPLCodeDefinitionBODTrigger	is Boolean
		LocalAccountingEntity 				is Alpha size 22
		LocalGeneralLedgerCompany			is Numeric 4
		LocalAccountingentityCompany 		is Alpha 40
		LocalCrossAccountingEntity 			is Alpha 40
		LocalBODFormattedID					is Alpha 100
		LocalCodeDefinitionCleanDocIDXML	is XMLDocument
		NewBODTracker  						is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker			is Numeric 15
		Error            					is Boolean
	    ErrorMessage     					is Alpha 300
	    LocalBODTrigger						is Boolean
		LocalConfigurationParameter			is Alpha size up to 200
		LocalSourceValue					is Alpha 30
    
    Context Fields
    	RecallReport
		RecallReportPeriod
		FSMInboundBODTracker
    
    Derived Fields
    
    	Period1ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 1
			return (instance count of LocalPeriodItemRel)
			
		Period2ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 2
			return (instance count of LocalPeriodItemRel)
			
		Period3ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 3
			return (instance count of LocalPeriodItemRel)
			
		Period4ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 4
			return (instance count of LocalPeriodItemRel)
			
		Period5ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 5
			return (instance count of LocalPeriodItemRel)
			
		Period6ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 6
			return (instance count of LocalPeriodItemRel)
			
		Period7ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 7
			return (instance count of LocalPeriodItemRel)
			
		Period8ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 8
			return (instance count of LocalPeriodItemRel)
			
		Period9ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 9
			return (instance count of LocalPeriodItemRel)
			
		Period10ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 10
			return (instance count of LocalPeriodItemRel)
			
		Period11ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 11
			return (instance count of LocalPeriodItemRel)
			
		Period12ItemCount is a DerivedField
			type is Numeric 5
			LocalPeriod = 12
			return (instance count of LocalPeriodItemRel)
			
		AnyPeriodItemCount is a ComputeField
			type is Numeric 5
			default label is "TotalItemsRecalled"
			(instance count of AnyPeriodItemRel)

		AnyPeriodItemPercent is a ComputeField
			type is Percent 3.0
			(AnyPeriodItemCount / RecallGroupRel.AnyPeriodItemCount)
				
		DescriptionAndCount is a DerivedField
			type is Alpha size 70
			return Description + " (" + AnyPeriodItemCount + ")"
 
 		DerivedCodeDivisionDescription is a DerivedField
 			type is Alpha size up to 75	
 			default label is "Manufacturer"
 			if (CodeAndDivision)
 				return Description + " (" + Manufacturer.ManufacturerCode + ". " + Manufacturer.ManufacturerDivision + ")"
 			else
 			if (CodeOnly)
 				return Description + " (" + Manufacturer.ManufacturerCode + ")"

		DerivedCodeDivision is a DerivedField
			type is Alpha size 11
 			if (CodeAndDivision)
 				return Manufacturer.ManufacturerCode + " - " + Manufacturer.ManufacturerDivision
 			else
 			if (CodeOnly)
 				return Manufacturer.ManufacturerCode
 				

 		
 		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
 		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedCleanDocumentID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "RequiredCleanDocumentID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedMultipleFEG is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "IsMultipleFEG"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTriggerBODBasedOnCrossReference is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "TriggerBODBasedOnCrossReference"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
											
 		DerivedActionCode is a DerivedField
 			type is Alpha size 10
 			restricted
 			if (action type.Create or ActionCode.Create)
 				return "Add"
			else
 			if (action type.Delete or ActionCode.Delete)
 				return "Delete"
 			else
 			if (action type.Update)
 				return "Replace"
 			else
 				return ""
 				
 		DerivedStatusCode is a DerivedField
 			type is Alpha size 10
 			restricted
 			if (action type.Delete or ActionCode.Delete)
 				return "Deleted"
 			else
 			if (Active = true)
 				return "Open"
 			else
 			if (Active != true)
 				return "Closed"
 		
 		DerivedCurrentTimeStamp is a DerivedField
 			type is Alpha size 25
 			restricted
 			DerivedCurrentTimeStamp = current timestamp
 			return DerivedCurrentTimeStamp
 			
 		DerivedFormattedCreateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
 			return DerivedCurrentTimeStamp[1:4] + "-" + DerivedCurrentTimeStamp[5:6] + "-" + DerivedCurrentTimeStamp[7:8] + "T" + DerivedCurrentTimeStamp[9:10] + ":" + DerivedCurrentTimeStamp[11:12] + ":" + DerivedCurrentTimeStamp[13:14] + "Z"
 			
 		DerivedUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedUpdateTimeStamp = update stamp.timestamp
			return DerivedUpdateTimeStamp

		DerivedFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return DerivedUpdateTimeStamp[1:4] + "-" + DerivedUpdateTimeStamp[5:6] + "-" + DerivedUpdateTimeStamp[7:8] + "T" + DerivedUpdateTimeStamp[9:10] + ":" + DerivedUpdateTimeStamp[11:12] + ":" + DerivedUpdateTimeStamp[13:14] + "Z"
 			
 		DerivedDocumentID is a DerivedField
 			type is Alpha size 60
 			restricted
 			return Manufacturer.ManufacturerCode + DerivedDelimiter + Manufacturer.ManufacturerDivision
 			
 		DerivedAccountingEntity is a DerivedField
 			type is Alpha size 20
 			restricted
 			return DerivedFinanceEnterpriseGroup + DerivedDelimiter + ItemGroup
 			
 		DerivedBODID is a DerivedField
 			type is Alpha size 100
 			restricted
 			return "infor-nid:" + DerivedTenantID + ":" + DerivedAccountingEntity + "::" + DerivedDocumentID + ":" + "?CodeDefinition&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
 			
 		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha 20
			restricted
			return ItemGroup.FinanceEnterpriseGroup
    
    	CodeDefinitionXMLBOD is a DerivedField
    		type is XMLDocument
    		restricted
    		CodeDefinitionXMLBOD = template.IONSyncCodeDefiniton_Manufacturer_ST document for this instance
    		
    Conditions

        HasGlobalLocationNumber
            classic name is HAS-GLN-NBR
            restricted
            when (GlobalLocationNumber entered)

        HasLabelerIdentificationCode
            classic name is LIC-CODE
            restricted
            when (LabelerIdentificationCode entered)
            
        CodeOnly
        	restricted
        	when (Manufacturer.ManufacturerCode != blank
        	and   Manufacturer.ManufacturerDivision = blank)
        	
        CodeAndDivision
        	restricted
        	when (Manufacturer.ManufacturerCode != blank
        	and   Manufacturer.ManufacturerDivision != blank)

		GPOSuppliersExist
			restricted
			when (GPOSupplierRel exists)

		ItemsExist
			restricted
			when (ActiveItemsRel exists)
			
		VendorItemsExist
			restricted
			when (ActiveVendorItemsRel exists)
			
		ContractLinesExist
			restricted
			when (OpenContractLinesRel exists)

		RelatedRecordExist
			restricted
			when (ItemsExist
			or    VendorItemsExist
			or    ContractLinesExist)
			
		ItemGroupExist
			restricted
			when (ItemGroup exist)	
			 
		ManufacturerCodeExist
			restricted
			when (Manufacturer.ManufacturerCode entered)
				
		ManufacturerDivisionExist
			restricted
			when (Manufacturer.ManufacturerDivision entered)
		
		BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "PurchaseOrderEDIOutput"
			or    parentcontext.name = "RecallNotice"
			or    parentcontext.name = "RecallProductItem"
			or    parentcontext.name = "RecallProduct"
			or    parentcontext.name = "Manufacturer")

    Relations

        ManufacturerCodeGlobalLocationNumberDetailRel
            classic name is GLN-MFR-CODE
            one-to-one relation to GlobalLocationNumberDetail
            valid when (HasGlobalLocationNumber)
            Field Mapping uses Set5
                related.ItemGroup                                           = ItemGroup
                related.GlobalLocationNumberDetail.GlobalLocationNumberType = 5
                related.Manufacturer.ManufacturerCode                       = Manufacturer.ManufacturerCode
                related.Manufacturer.ManufacturerDivision                   = blank
                related.GlobalLocationNumber                                = GlobalLocationNumber

        ManufacturerDivisionGlobalLocationNumberDetailRel
            classic name is GLN-MFR-DIV
            one-to-one relation to GlobalLocationNumberDetail
            valid when (HasGlobalLocationNumber)
            Field Mapping uses Set5
                related.ItemGroup                                           = ItemGroup
                related.GlobalLocationNumberDetail.GlobalLocationNumberType = 6
                related.Manufacturer.ManufacturerCode                       = Manufacturer.ManufacturerCode
                related.Manufacturer.ManufacturerDivision                   = Manufacturer.ManufacturerDivision
                related.GlobalLocationNumber                                = GlobalLocationNumber
                
        MfgCodeDivImportTierQualifierCodeRel
    		one-to-many relation to ContractImportTierQualCode
            Field Mapping uses symbolic key
          		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
           		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
           		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
           		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
           	Instance Selection
           		where (related.ManufacturerInfo.ManufacturerCode 	  = Manufacturer.ManufacturerCode
           		and   !related.ManufacturerInfo.ManufacturerDivision  = blank)
           		
        MfgOnlyImportTierQualifierCodeRel
    		one-to-many relation to ContractImportTierQualCode
            Field Mapping uses symbolic key
          		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
           		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
           		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
           		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
           	Instance Selection
           		where (related.ManufacturerInfo.ManufacturerCode 	  = Manufacturer.ManufacturerCode
           		and    related.ManufacturerInfo.ManufacturerDivision = blank)
           		
        ContractImportTierQualifierCodeRel
    		one-to-many relation to ContractImportTierQualCode
            Field Mapping uses symbolic key
          		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
           		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
           		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
           		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
           	Instance Selection
           		where (related.ManufacturerInfo = Manufacturer)
           		
        MfgCodeDivRebateQualifierCodeRel
    		one-to-many relation to ContractRebateQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		    = LocalContractRebate.ContractGroup
           		related.Contract			    = LocalContractRebate.Contract
           		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
           		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
           	Instance Selection
           		where (related.ManufacturerInfo.ManufacturerCode 	  = Manufacturer.ManufacturerCode
           		and    !related.ManufacturerInfo.ManufacturerDivision = blank)
           		
        MfgOnlyRebateQualifierCodeRel
    		one-to-many relation to ContractRebateQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		    = LocalContractRebate.ContractGroup
           		related.Contract			    = LocalContractRebate.Contract
           		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
           		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
           	Instance Selection
           		where (related.ManufacturerInfo.ManufacturerCode 	  = Manufacturer.ManufacturerCode
           		and    related.ManufacturerInfo.ManufacturerDivision = blank)
           		
        ContractRebateQualifierCodeRel
    		one-to-many relation to ContractRebateQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		    = LocalContractRebate.ContractGroup
           		related.Contract			    = LocalContractRebate.Contract
           		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
           		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
           	Instance Selection
           		where (related.ManufacturerInfo = Manufacturer)
           		
        MfgCodeDivTierQualifierCodeRel
    		one-to-many relation to ContractTierQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
           		related.Contract			  = LocalContractTierGroup.Contract
           		related.ContractTier		  = LocalContractTierGroup.ContractTier
           		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
           	Instance Selection
           		where (related.ManufacturerInfo.ManufacturerCode 	  = Manufacturer.ManufacturerCode
           		and   !related.ManufacturerInfo.ManufacturerDivision  = blank)
        
		MfgOnlyTierQualifierCodeRel
    		one-to-many relation to ContractTierQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
           		related.Contract			  = LocalContractTierGroup.Contract
           		related.ContractTier		  = LocalContractTierGroup.ContractTier
           		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
           	Instance Selection
           		where (related.ManufacturerInfo.ManufacturerCode 	  = Manufacturer.ManufacturerCode
           		and    related.ManufacturerInfo.ManufacturerDivision  = blank)
         
        ContractTierQualifierCodeRel
    		one-to-many relation to ContractTierQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
           		related.Contract			  = LocalContractTierGroup.Contract
           		related.ContractTier		  = LocalContractTierGroup.ContractTier
           		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
           	Instance Selection
           		where (related.ManufacturerInfo = Manufacturer)
           		
        RecallGroupRel
        	one-to-many relation to RecallGroup
        	Field Mapping uses symbolic key
        		related.RecallGroup				= ItemGroup
        
        LocalPeriodItemRel
			one-to-many relation to RecallProductItem
			Field Mapping uses ByManufInfo
				related.RecallGroup				= ItemGroup
				related.ManufacturerInformation.Manufacturer = Manufacturer
    		Instance Selection
				where  (related.RecallNotice.ApplicableRecallNotice
				and    related.RecallNotice.DerivedReleaseDate >= LocalPeriodRel.ReportFromDate
				and    related.RecallNotice.DerivedReleaseDate <= LocalPeriodRel.ReportThruDate) 

    	AnyPeriodItemRel
			one-to-many relation to RecallProductItem
			Field Mapping uses ByManufInfo
				related.RecallGroup				= ItemGroup
				related.ManufacturerInformation.Manufacturer = Manufacturer
    		Instance Selection
				where  (related.RecallNotice.ApplicableRecallNotice
				and    related.RecallNotice.DerivedReleaseDate >= RecallReport.ReportFromDate
				and    related.RecallNotice.DerivedReleaseDate <= RecallReport.ReportThruDate) 

		LocalPeriodRel
			one-to-one relation to RecallReportPeriod
			Field Mapping uses symbolic key
				related.RecallGroup				= ItemGroup
				related.RecallReport			= RecallReport
				related.RecallReportPeriod		= LocalPeriod
				
		ActiveItemsRel
			one-to-many relation to Item
			Field Mapping uses Set8
				related.ItemGroup               = ItemGroup
				related.Manufacturer            = Manufacturer
			Instance Selection
				where (related.IsActive)
		
		ActiveItemLocationsRel 
			one-to-many relation to ItemLocation
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Manufacturer = Manufacturer
				and    related.IsActive)
		
		ActiveVendorItemsRel
			one-to-many relation to VendorItem
			Field Mapping uses Set6
				related.ProcurementGroup          = ItemGroup
				related.Manufacturer              = Manufacturer
			Instance Selection
				where (related.Active = true)
		
		OpenContractLinesRel 
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgInfoVenItem
				related.ContractGroup             			= ItemGroup
				related.Manufacturer.ManufacturerCode 		= Manufacturer.ManufacturerCode
				related.Manufacturer.ManufacturerDivision	= Manufacturer.ManufacturerDivision
			Instance Selection
				where (related.LineNotClosed
				and    related.ItemType.Special)
			



		
		GPOSupplierRel
			one-to-many relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup                        = ItemGroup
			Instance Selection
				where (related.ManufacturerCodeDivision      = Manufacturer) 
		

		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMCodeDefinitionBODConfigurationRel
        	one-to-many relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
             	related.FSMBODConfiguration.Direction 	= 1
            Instance Selection
				where (related.ProcessingMethod.NativeLPL)
		
		BODGeneralLedgerCompanyRel
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.BusinessGroup = ItemGroup)

		FSMCleanDocIDAccountingEntityRel
			one-to-one relation to AccountingEntity
			Field Mapping uses symbolic key
    			related.FinanceEnterpriseGroup 	= ItemGroup.FinanceEnterpriseGroup
    			related.AccountingEntity		= LocalAccountingentityCompany

		FSMBODCrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
            	related.FSMBODCrossReference.SourceField 		= "CSFGLC"
            	related.FSMBODCrossReference.DestinationField 	= "DESTAE"
		
				
		FSMCleanDocIDCrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
				related.FSMBODCrossReference.SourceField 		= "CSFGLC"
				related.FSMBODCrossReference.DestinationField 	= "DESTAE"
			Instance Selection
				where (related.SourceValue	= LocalAccountingentityCompany)

		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker

			
    Sets

        Set2
            indexed
            Instance Selection
                where (HasLabelerIdentificationCode)
            Sort Order
                ItemGroup
                LabelerIdentificationCode
                Manufacturer

	Field Rules
		Description
			required
			
	Rule Blocks
		TriggerManufacturerServiceRules
    		trigger "ManufacturerService" PA service
				resume on error
				title is "EG:<ItemGroup.BusinessGroup.FinanceEnterpriseGroup>Manufacturer:<Manufacturer>"
				Criteria
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
					Manufacturer
				Variables
					ActionCode
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					include persistent fields from Manufacturer
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
						
		

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
			Action Rules
				constraint (ActiveItemLocationsRel not exists)
					"CannotDelete.ManufacturerAssociatedWithAtLeastOneActiveItemLocation"
					
				constraint (ActiveItemsRel not exists)
					"CannotDelete.ManufacturerAssociatedWithAtLeastOneActiveItem"
			Entrance Rules
				if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger)
					ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerManufacturerServiceRules

					
		UpdateGlobalLocationNumber is an Instance Action
			restricted
			Parameters
				PrmGlobalLocationNumber	is a GlobalLocationNumber
			Action Rules
				GlobalLocationNumber	=	PrmGlobalLocationNumber
				
		AttachToImportTierQualifier is an Instance Action
			Parameters
				ContractGroup
				ContractImport
				ContractImportTier
				ContractImportTierQualifier 
			Parameter Rules
				ContractGroup 
					required
				ContractImport
					required
				ContractImportTier
					required
				ContractImportTierQualifier
					required
			
			Action Rules
				LocalContractImportTierGroup.ContractGroup 		         = ContractGroup
				LocalContractImportTierGroup.ContractImport 			 = ContractImport
				LocalContractImportTierGroup.ContractImportTier 		 = ContractImportTier
				LocalContractImportTierGroup.ContractImportTierQualifier = ContractImportTierQualifier
				if (CodeOnly)
					constraint (!MfgCodeDivImportTierQualifierCodeRel exists)
						"CannotAdd;TierQualifierCodesExistByDivision"
				else
				if (CodeAndDivision)
					constraint (!MfgOnlyImportTierQualifierCodeRel exists)
						"CannotAdd;TierQualifierCodeRecordAlreadyExistsForManufacturerCodeOnly"
				if (!ContractImportTierQualifierCodeRel exists)
					invoke Create ContractImportTierQualCode
						invoked.ContractGroup		        = ContractGroup
						invoked.ContractImport			    = ContractImport
						invoked.ContractImportTier		    = ContractImportTier
						invoked.ContractImportTierQualifier = ContractImportTierQualifier
						invoked.ManufacturerInfo  	        = Manufacturer
						invoked.ItemCategory		        = 2
    	
    	AttachToRebateQualifier is an Instance Action
			Parameters
				ContractGroup
				Contract
   				ContractRebateType			
   				ContractRebate				
   				ContractRebateQualifier
			Parameter Rules
				ContractGroup 
					required
				Contract
					required
				ContractRebateType
					required
				ContractRebate
					required
				ContractRebateQualifier
					required
			
			Action Rules
    			constraint (Contract.CanAmend)
    				"ContractMustBeInDraftOrAddendumOrAmendmentStatusToModifyRebateQualifiers"					
    			constraint (ContractRebate.Active)
    				"ContractRebateMustBeActiveToAddRebateQualifierCodes"
				LocalContractRebate.ContractGroup 		    = ContractGroup
				LocalContractRebate.Contract	            = Contract
				LocalContractRebate.ContractRebateType      = ContractRebateType
				LocalContractRebate.ContractRebateQualifier = ContractRebateQualifier			
				if (CodeOnly)
					constraint (!MfgCodeDivRebateQualifierCodeRel exists)
						"CannotAdd;RebateQualifierCodesExistByDivision"
				else
				if (CodeAndDivision)
					constraint (!MfgOnlyRebateQualifierCodeRel exists)
						"CannotAdd;RebateQualifierCodeRecordAlreadyExistsForManufacturerCodeOnly"
				if (!ContractRebateQualifierCodeRel exists)
					invoke Create ContractRebateQualifierCode
						invoked.ContractGroup		    = ContractGroup
						invoked.Contract			    = Contract
						invoked.ContractRebateType		= ContractRebateType
						invoked.ContractRebateQualifier = ContractRebateQualifier
						invoked.ManufacturerInfo  	    = Manufacturer
						invoked.ItemCategory		    = 2
						
		ChangeThisManufacturer is an Instance Action
			valid when (Active)
			Parameters
				NewManufacturer             is a Manufacturer
				Inactivate                  is Boolean
				ChangeItems                 is Boolean
				ChangeItemLocations         is Boolean
				ChangeVendorItems           is Boolean
				ChangeSpecialItems          is Boolean
				ChangeGPOSuppliers          is Boolean
				
			Parameter Rules
				NewManufacturer
					required
						"MustEnterANewManufacturer"
					constraint (Manufacturer != NewManufacturer)
						"NewManufacturerCodeAndDivisionMustBeDifferent"
					constraint (NewManufacturer exists)
						"NewManufacturerDoesNotExist"
				
				ChangeItems
					initial value is true
					
				ChangeItemLocations
					initial value is true
					
				ChangeVendorItems
					initial value is true
					
				ChangeSpecialItems
					initial value is true
					
				ChangeGPOSuppliers
					initial value is true
						
			Action Rules
				invoke ChangeManufacturer
					invoked.ParmItemGroup		= ItemGroup
					invoked.ParmManufacturer   	= Manufacturer
					invoked.NewManufacturer 	= NewManufacturer
					invoked.Inactivate      	= Inactivate
					invoked.ChangeItems     	= ChangeItems
					invoked.ChangeItemLocations	= ChangeItemLocations
					invoked.ChangeVendorItems   = ChangeVendorItems
					invoked.ChangeSpecialItems  = ChangeSpecialItems
					invoked.ChangeGPOSuppliers  = ChangeGPOSuppliers
		
		ChangeManufacturer is a Set Action
			restricted
			Parameters
				ParmItemGroup       is an ItemGroup
				ParmManufacturer    is a Manufacturer
				NewManufacturer     is a Manufacturer
				Inactivate          is Boolean
				ChangeItems         is Boolean
				ChangeItemLocations is Boolean
				ChangeVendorItems   is Boolean
				ChangeSpecialItems  is Boolean
				ChangeGPOSuppliers  is Boolean
				 
			Instance Selection
				where (ItemGroup 	= ParmItemGroup
				and    Manufacturer = ParmManufacturer)
				
			Action Rules
			
				Instance Rules
				
					if (Inactivate)
						Active = false
					if (ChangeItems)
						for each ActiveItemsRel
							invoke Update each
								invoked.Manufacturer			 = NewManufacturer
					if (ChangeItemLocations)
						for each ActiveItemLocationsRel
							invoke Update each
								invoked.Manufacturer             = NewManufacturer 
					if (ChangeGPOSuppliers)
						for each GPOSupplierRel
							invoke Update each
								invoked.ManufacturerCodeDivision = NewManufacturer
					if (ChangeVendorItems)
						for each ActiveVendorItemsRel
							invoke ChangeManufacturerInformation each
								invoked.PrmManufacturer          = NewManufacturer 
								invoked.PrmChangeFromManufacturer	 = true
					if (ChangeSpecialItems)
						for each OpenContractLinesRel
							invoke UpdateManufacturerInformation each 
								invoked.ParmManufacturer                = NewManufacturer
								invoked.ParmUpdateAllLines              = true
						 		
		AttachToTierQualifier is an Instance Action
			Parameters
				ContractGroup
				Contract
				ContractTier
				ContractTierQualifier 
			Parameter Rules
				ContractGroup 
					required
				Contract
					required
				ContractTier
					required
				ContractTierQualifier
					required
			
			Action Rules
				constraint (Contract.NotClosed)
    				"CannotAdd;ContractIsClosed"	
				LocalContractTierGroup.ContractGroup 		 = ContractGroup
				LocalContractTierGroup.Contract 			 = Contract
				LocalContractTierGroup.ContractTier 		 = ContractTier
				LocalContractTierGroup.ContractTierQualifier = ContractTierQualifier
				if (CodeOnly)
					constraint (!MfgCodeDivTierQualifierCodeRel exists)
						"CannotAdd;TierQualifierCodesExistByDivision"
				else
				if (CodeAndDivision)
					constraint (!MfgOnlyTierQualifierCodeRel exists)
						"CannotAdd;TierQualifierCodeRecordAlreadyExistsForManufacturerCodeOnly"
				if (!ContractTierQualifierCodeRel exists)
					invoke Create ContractTierQualifierCode
						invoked.ContractGroup		  = ContractGroup
						invoked.Contract			  = Contract
						invoked.ContractTier		  = ContractTier
						invoked.ContractTierQualifier = ContractTierQualifier
						invoked.ManufacturerInfo  	  = Manufacturer
						invoked.ItemCategory		  = 2

		
		SendCodeDefinitionBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				if(DerivedCleanDocumentID != "Y")
					LocalAccountingEntity = DerivedAccountingEntity
					send ion bod
						bod is CodeDefinitionXMLBOD
						bod type is "Sync.CodeDefinition"
						accounting entity is DerivedAccountingEntity
						document id is DerivedDocumentID
						variation id is BODVariationID	
				else
					send ion bod
						bod is LocalCodeDefinitionCleanDocIDXML
						bod type is "Sync.CodeDefinition"
						accounting entity is LocalCrossAccountingEntity
						document id is DerivedDocumentID
						variation id is	BODVariationID   	
					
		TriggerCodeDefinitionNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
				RestartFlag is Boolean
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMCodeDefinitionBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "Manufacturer"
					invoked.PrmFinanceEnterpriseGroup	= ItemGroup.BusinessGroup.FinanceEnterpriseGroup
					invoked.PrmBusinessGroup			= ItemGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefiniton_Manufacturer_ST"
				NativeLPLCodeDefinitionBODTrigger = FSMCodeDefinitionBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and NativeLPLCodeDefinitionBODTrigger)
					if(DerivedCleanDocumentID = "Y") 
						if(RestartFlag)
							if (BODGeneralLedgerCompanyRel exists)
								LocalGeneralLedgerCompany = FSMInboundBODTracker.Reference3	
								if(DerivedMultipleFEG = "Y")
									LocalAccountingentityCompany = DerivedFinanceEnterpriseGroup + DerivedDelimiter + LocalGeneralLedgerCompany
								else
									LocalAccountingentityCompany = LocalGeneralLedgerCompany
								initialize LocalCrossAccountingEntity
								if(DerivedTriggerBODBasedOnCrossReference = "Y")
									if (FSMCleanDocIDCrossReferenceDetailRel.DestinationValue entered)
										LocalCrossAccountingEntity = FSMCleanDocIDCrossReferenceDetailRel.DestinationValue
								else
									LocalCrossAccountingEntity = LocalAccountingentityCompany
									
								if(LocalCrossAccountingEntity entered)
									LocalFSMInboundBODTracker		= FSMInboundBODTracker
									invoke Update FSMInboundBODTrackerRel
										invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
										invoked.BODDocumentID			= Manufacturer
										invoked.BODVariationID			= BODVariationID
										invoked.Status					= 1
										invoked.StartDate				= system current timestamp
										invoked.Direction				= 1
										invoked.BODAccountingEntity		= LocalCrossAccountingEntity
										invoked.Reference3				= LocalGeneralLedgerCompany
										invoked.Reference1				= ItemGroup
										invoked.Reference2				= "Manufacturer"
										initialize invoked.Error			
										initialize invoked.ErrorMessage
									LocalBODFormattedID = "infor-nid:" + DerivedTenantID + ":" + LocalCrossAccountingEntity + "::" + DerivedDocumentID + ":" + "?CodeDefinition&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
									LocalCodeDefinitionCleanDocIDXML = template.IONSyncCodeDefinitionCleanDocID_Manufacturer_ST document for this instance
									invoke SendCodeDefinitionBODNativeLPL
										resume on error
											Error            							= true
											ErrorMessage     							= error message
									if(Error)
										invoke Update FSMInboundBODTrackerRel
											invoked.Error 								= Error
											invoked.ErrorMessage 						= ErrorMessage
											invoked.Status								= 2
											invoked.CloseDate							= system current timestamp
											invoked.BODID								= LocalBODFormattedID
											invoked.BODXML								= LocalCodeDefinitionCleanDocIDXML
									else
										invoke Update FSMInboundBODTrackerRel
											invoked.Status									= 3
											invoked.CloseDate								= system current timestamp
											invoked.BODID									= LocalBODFormattedID
											invoked.BODXML									= LocalCodeDefinitionCleanDocIDXML
										
						else
						if(DerivedTriggerBODBasedOnCrossReference = "Y")
							if(FSMBODCrossReferenceDetailRel exists)
								for each FSMBODCrossReferenceDetailRel
									initialize LocalCodeDefinitionCleanDocIDXML
									initialize LocalSourceValue
									LocalSourceValue = each.SourceValue
									if (LocalSourceValue entered)
										for each LocalSourceValue split on "<DerivedDelimiter>"
											initialize LocalAccountingentityCompany
											LocalAccountingentityCompany = each
									initialize LocalCrossAccountingEntity
									initialize LocalBODFormattedID
									if(FSMCleanDocIDAccountingEntityRel exists)
										LocalCrossAccountingEntity = each.DestinationValue
										if(LocalCrossAccountingEntity entered)
											if(FSMInboundBODTracker not entered)
												invoke Create FSMInboundBODTracker
													assign result to NewBODTracker
													invoked.Verb 					= 1
													invoked.Noun 					= "CodeDefinition"					
													invoked.BODDocumentID			= Manufacturer
													invoked.BODVariationID			= BODVariationID
													invoked.Status					= 1
													invoked.StartDate				= system current timestamp
													invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
													invoked.Direction				= 1
													invoked.BODAccountingEntity		= LocalCrossAccountingEntity
													invoked.Reference3				= LocalGeneralLedgerCompany	
													invoked.Reference1				= ItemGroup								
													invoked.Reference2				= "Manufacturer"
													initialize invoked.Error			
													initialize invoked.ErrorMessage					
												LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
												LocalBODFormattedID = "infor-nid:" + DerivedTenantID + ":" + LocalCrossAccountingEntity + "::" + DerivedDocumentID + ":" + "?CodeDefinition&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
												LocalCodeDefinitionCleanDocIDXML = template.IONSyncCodeDefinitionCleanDocID_Manufacturer_ST document for this instance
											invoke SendCodeDefinitionBODNativeLPL
												resume on error
													Error            							= true
													ErrorMessage     							= error message
											if(Error)
												invoke Update FSMInboundBODTrackerRel
													invoked.Error 								= Error
													invoked.ErrorMessage 						= ErrorMessage
													invoked.Status								= 2
													invoked.CloseDate							= system current timestamp
													invoked.BODID								= LocalBODFormattedID
													invoked.BODXML								= LocalCodeDefinitionCleanDocIDXML
											else
												invoke Update FSMInboundBODTrackerRel
													invoked.Status									= 3
													invoked.CloseDate								= system current timestamp
													invoked.BODID									= LocalBODFormattedID
													invoked.BODXML									= LocalCodeDefinitionCleanDocIDXML

						else
						if(DerivedTriggerBODBasedOnCrossReference = "N")
							if (BODGeneralLedgerCompanyRel exists)
								for each BODGeneralLedgerCompanyRel
									initialize LocalGeneralLedgerCompany
									initialize LocalCrossAccountingEntity
									initialize LocalCodeDefinitionCleanDocIDXML
									initialize LocalBODFormattedID
									LocalGeneralLedgerCompany = each.Company	
									if(DerivedMultipleFEG = "Y")
										LocalCrossAccountingEntity = DerivedFinanceEnterpriseGroup + DerivedDelimiter + LocalGeneralLedgerCompany
									else
										LocalCrossAccountingEntity = LocalGeneralLedgerCompany
									
									if(LocalCrossAccountingEntity entered)
										if(FSMInboundBODTracker not entered)
											invoke Create FSMInboundBODTracker
												assign result to NewBODTracker
												invoked.Verb 					= 1
												invoked.Noun 					= "CodeDefinition"					
												invoked.BODDocumentID			= Manufacturer
												invoked.BODVariationID			= BODVariationID
												invoked.Status					= 1
												invoked.StartDate				= system current timestamp
												invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
												invoked.Direction				= 1
												invoked.BODAccountingEntity		= LocalCrossAccountingEntity
												invoked.Reference3					= LocalGeneralLedgerCompany	
												invoked.Reference1				= ItemGroup								
												invoked.Reference2				= "Manufacturer"
												initialize invoked.Error			
												initialize invoked.ErrorMessage					
											LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
											LocalBODFormattedID = "infor-nid:" + DerivedTenantID + ":" + LocalCrossAccountingEntity + "::" + DerivedDocumentID + ":" + "?CodeDefinition&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
											LocalCodeDefinitionCleanDocIDXML = template.IONSyncCodeDefinitionCleanDocID_Manufacturer_ST document for this instance
										invoke SendCodeDefinitionBODNativeLPL
											resume on error
												Error            							= true
												ErrorMessage     							= error message
										if(Error)
											invoke Update FSMInboundBODTrackerRel
												invoked.Error 								= Error
												invoked.ErrorMessage 						= ErrorMessage
												invoked.Status								= 2
												invoked.CloseDate							= system current timestamp
												invoked.BODID								= LocalBODFormattedID
												invoked.BODXML								= LocalCodeDefinitionCleanDocIDXML
										else
											invoke Update FSMInboundBODTrackerRel
												invoked.Status									= 3
												invoked.CloseDate								= system current timestamp
												invoked.BODID									= LocalBODFormattedID
												invoked.BODXML									= LocalCodeDefinitionCleanDocIDXML
					
					else
						if(FSMInboundBODTracker not entered)
							invoke Create FSMInboundBODTracker
								assign result to NewBODTracker
								invoked.Verb 					= 1
								invoked.Noun 					= "CodeDefinition"					
								invoked.BODDocumentID			= Manufacturer
								invoked.BODVariationID			= BODVariationID
								invoked.Status					= 1
								invoked.StartDate				= system current timestamp
								invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
								invoked.Direction				= 1
								invoked.BODAccountingEntity		= DerivedAccountingEntity
								invoked.Reference1				= ItemGroup
								invoked.Reference2				= "Manufacturer"
								initialize invoked.Error			
								initialize invoked.ErrorMessage					
							LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
						else 
							LocalFSMInboundBODTracker		= FSMInboundBODTracker
							invoke Update FSMInboundBODTrackerRel
								invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
								invoked.BODDocumentID			= Manufacturer
								invoked.BODVariationID			= BODVariationID
								invoked.Status					= 1
								invoked.StartDate				= system current timestamp
								invoked.Direction				= 1
								invoked.BODAccountingEntity		= DerivedAccountingEntity
								invoked.Reference1				= ItemGroup
								invoked.Reference2				= "Manufacturer"
								initialize invoked.Error			
								initialize invoked.ErrorMessage
						invoke SendCodeDefinitionBODNativeLPL
							resume on error
								Error            							= true
								ErrorMessage     							= error message
						if(Error)
							invoke Update FSMInboundBODTrackerRel
								invoked.Error 								= Error
								invoked.ErrorMessage 						= ErrorMessage
								invoked.Status								= 2
								invoked.CloseDate							= system current timestamp
								invoked.BODID								= DerivedBODID
								invoked.BODXML								= CodeDefinitionXMLBOD
						else
							invoke Update FSMInboundBODTrackerRel
								invoked.Status									= 3
								invoked.CloseDate								= system current timestamp
								invoked.BODID									= DerivedBODID
								invoked.BODXML									= CodeDefinitionXMLBOD
									
					

	Action Exit Rules
		if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action type.Create)
					ActionCode	= ActionCode.Create				
				else
					if (action type.Update)
						ActionCode	= ActionCode.Update
				increment bod id.VariationID
				include TriggerManufacturerServiceRules

