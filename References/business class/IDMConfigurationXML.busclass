IDMConfigurationXML is a BusinessClass 
	owned by idm
	prefix is IDMCX
   	disable data area copy
    	preserve target data
    		
	Ontology
		symbolic key is IDMConfigurationXML
		
	Persistent Fields
		XMLFile					is an IDMAttachment
		Description
		ConfigurationVersion	is Alpha size 10   
			default label is "LatestConfigurationVersion"
		Status 					is Numeric size 1
			States
				ForImport	 	value is 0
				Imported 		value is 1
				Failed	 		value is 2
		ErrorMessage			is Text		
		Sequence				is Numeric size 3
				
	Field Rules
		XMLFile
			required	
		ConfigurationVersion
			required
		Sequence
			required
		
	Derived Fields
		IsMultiTenant is a NativeField
			type is Boolean
			default label is untranslatable
			restricted	
			
		ImportToIDMMessage is a MessageField
			"ThisWillImportAll_IDMConfiguration_X\M\LsToIDM" 		
			
	Sets
		BySequence
			Sort Order		
				Sequence
				IDMConfigurationXML
						
 	Actions		
 		Create is a Create Action
 			restricted
			
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
			
		ImportConfiguration is an Instance Action	
			restricted
			Parameters
				PrmTenant					is Alpha size 20  
			
		ImportToIDM is a Set Action     
			Parameters
				PrmPostUpgrade				is Boolean   
				PrmTenant					is Alpha size 20  
				
			Instance Selection
				where (Status != Status.Imported)
				
			Sort Order
				Sequence
				IDMConfigurationXML
				
			Local Fields 
				ImportFailed			is Boolean
				
			Action Rules
				Instance Rules

				    if ((PrmPostUpgrade and IsMultiTenant) 
				    or not PrmPostUpgrade                  
				    or PrmTenant entered)				   

						if (not ImportFailed)
						
							invoke ImportConfiguration 
								resume on error
								invoked.PrmTenant = PrmTenant
							
							if (Status.Failed) 
								ImportFailed = true
