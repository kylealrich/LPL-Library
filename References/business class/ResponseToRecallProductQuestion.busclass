ResponseToRecallProductQuestion is a BusinessClass
    sql name is ProductQuestionResponse
    owned by recall
    prefix is RMPQR

    Ontology
    	part of RecallNoticeTaskProduct
			delete cascades
    		relative key is RecallProductQuestion
    			
   	Patterns 
        implements TemplateDriven by RecallProductQuestion
        	create instance
        		when (Response entered)
		implements ContextualParent
   			    			
    Persistent Fields
    	Response 		is a RecallProductQuestionAnswer
   		Attachment 				
    
    Derived Fields
    
    	ProductQuestionResponse is a ConditionalField
    		type is Alpha size 250
    		if (RecallProductQuestion.ResponseType.Text)
    			Response.TextValue
    		else
    		if (RecallProductQuestion.ResponseType.Number)
    			Response.NumericValue
    		else 
    		if (RecallProductQuestion.ResponseType.Date)
    			DerivedDate
    		else
    		if (RecallProductQuestion.ResponseType.List)
    			Response.ListValue.Answer
    		else
    		if (RecallProductQuestion.ResponseType.YesNo)
    			if (Response.YesNoValue.Yes)
    				YesText
    			else 
    				NoText
    		else
    			YesNoTextValue
    			    		
    	YesNoTextValue is a StringField
			type is Alpha size 260
			restricted
			YesNoText
			"-"
			Response.YesNoText.Text
    	
    	YesNoText is a ConditionalField
			type is Text
			restricted
			if (Response.YesNoText.YesNo = 1)
				YesText
			else
				NoText

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"	
			
		DerivedDate is a StringField
			type is Alpha 10
			restricted
			Response.DateValue month
			"/"
			Response.DateValue day
			"/"
			Response.DateValue year
    		    
    	AnswerMessage is a MessageField
    		restricted
    		"Answer"
    	
    	ResponseLabelRequired is a ConditionalField
    		type is Alpha 10
    		restricted
    		if (RecallProductQuestion.ResponseRequired)
    			"*"
    		else
    			blank
    			
    	ResponseLabel is a StringField
    		type is Text
    		restricted
    		ResponseLabelRequired AnswerMessage
    		
    	AttachmentMessage is a MessageField
    		restricted
    		"Attachment"
    		
    	AttachmentRequiredLabel is a ConditionalField
            type is Alpha 15
        	restricted
            if (RecallProductQuestion.AlwaysRequireResponseAttachment)

                "*"
            else
                blank
    			
    	AttachmentLabel is a StringField
    		type is Text
    		restricted
    		AttachmentRequiredLabel AttachmentMessage
    		
    	ResponseRequiredMessage is a MessageField
    		restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
        	restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
        	restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsYes"
            
        NoAttachmentMessage is a MessageField
        	restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsNo"
            
        YesOrNoAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsYes"
            
        NoAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsNo"
            
        AttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsYes"
            
        NoAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsNo"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
            restricted
            if (RecallProductQuestion.ResponseRules.ResponseRequired
            or  RecallProductQuestion.YesNoResponseRules.ResponseRequired)
                ResponseRequiredMessage        
            else
            if (RecallProductQuestion.ResponseRules.ResponseRequiredAndAttachmentRequired
            or  RecallProductQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
                ResponseAndAttachmentRequiredMessage
            else
            if (RecallProductQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
                YesAttachmentMessage
            else
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
                YesOrNoAndTextMessage
            else
            if (RecallProductQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                NoAttachmentMessage
            else
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
                AttachmentOrTextMessage
            else
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                YesAttachmentOrTextMessage
            else
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
                NoAttachmentOrTextMessage
            else 
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
                AttachmentAndTextMessage
            else
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                YesAttachmentAndTextMessage
            else
            if (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
                NoAttachmentAndTextMessage
            else
                blank
    			
    Conditions
    	HasAttachment
    		restricted
    		when (RecallProductQuestion.Attachment entered)
    		
    	AttachmentUploadAllowed
    		restricted
    		when (RecallProductQuestion.AllowResponseAttachment)
    	
		AllowResponse
			restricted
			when (RecallNotice.Status.Active
			and   RecallNoticeTask.Status.Accepted
			and  (RecallNoticeTask.IsOwner 
			or    RecallNotice.MyCoordinatorNotices))
			
    	HasResponseAttachment
    		restricted
    		when (Attachment entered)
    		
    	AttachmentAlwaysRequired   
    		restricted
            when (RecallProductQuestion.AlwaysRequireResponseAttachment
            and  (RecallProductQuestion.NonYesNoResponseType
            or    RecallProductQuestion.ResponseType.YesNo))

    Field Rules
	    Attachment
            if (!Response entered)
                if (HasResponseAttachment)
                    cannot be entered
                        "MustAnswerQuestionToAttachADocument"
                        
        Response
            if (RecallProductQuestion.ResponseType.YesNoText
            and !HasResponseAttachment
            and (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
            or  (Response.YesNoText.YesNo.Yes
            and  RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
            or  (Response.YesNoText.YesNo.No
            and  RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
                constraint (Response.YesNoText.Text entered)
                    "MustEnterEitherTextOrAnAttachmentForQuestion<RecallProductQuestion.Description>"
                    
            if (RecallProductQuestion.ResponseType.YesNo
            and (Response.YesNoValue.Yes
            and  RecallProductQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
            or  (Response.YesNoValue.No
            and  RecallProductQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<RecallProductQuestion.Description>"    
            
            if (AttachmentAlwaysRequired
            and !RecallProductQuestion.ResponseType.YesNoText)
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<RecallProductQuestion.Description>"
                       
            if  (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
            or   RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (Response.YesNoText.YesNo.Yes
            and  RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (Response.YesNoText.YesNo.No
            and  RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (Response.YesNoText.Text entered)
                    "MustEnterTextForQuestion<RecallProductQuestion.Description>"
                    
            if  (RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (Response.YesNoText.YesNo.Yes
            and  RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (Response.YesNoText.YesNo.No
            and  RecallProductQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<RecallProductQuestion.Description>" 
	
	Actions
    	Create is a Create Action
			valid when (AllowResponse)
			Action Rules
								
		Update is an Update Action
			valid when (AllowResponse)
			Action Rules
					
		Delete is a Delete Action
			valid when (AllowResponse)
			Action Rules
					
		Purge is a Purge Action
			restricted
