BirstModelResult is a BusinessClass
	owned by la

	prefix is BMR

	Ontology
		symbolic key is BirstModelResult

	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing

	Persistent Fields
		User 			is Actor
		RunTime			is TimeStamp
		Description		is Alpha size 100

	Field Rules
        RunTime
            default to current timestamp 

	Sets
		ByRunTime
			Sort Order
				RunTime
				BirstModelResult

	Relations
		BirstModelResultDetailErrorRel is a BirstModelResultDetail set
			Instance Selection
				where (related.Status.Error)
				
		BirstModelResultDetailRel is a BirstModelResultDetail set

	Conditions
		ErrorsExist
			when(BirstModelResultDetailErrorRel exists)

	Actions
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Purge is a Purge Action

		PurgeResults is a Set Action
			disable checkpoint
			Parameters
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3

			Local Fields
				LocalThruDate 	is Date
					value is ThruDate		
					


			Parameter Rules
				ThruDate
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThroughFinishDateOrOffset"
					else
						LocalThruDate  = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThroughFinishDateAndOffset"

			Instance Selection
				where (RunTime < LocalThruDate)

			Sort Order is ByRunTime

			Action Rules
				Instance Rules
					invoke Purge
