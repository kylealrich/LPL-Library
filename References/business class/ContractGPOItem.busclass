ContractGPOItem is a BusinessClass
	owned by po
	
	prefix is CIXX
	
	Ontology
		symbolic key is ContractGPOItem
		
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
	
	Persistent Fields
		ManufacturerSupplier         is a GPOSupplierID                
		ItemSupplier                 is a GPOSupplierID                
		GPOContractNumber                                            
		TierLevel                    is like ContractImportTier      
		ClassOfTrade                 is Alpha 200                                                                                  
		ManufacturerPart             is a ManufacturerNumber         
		VendorPart                   is like VendorItem              
		ItemDescription              is a Description3               
		Price                        is Unsigned Decimal size 17.4   
		UOM                          is like UnitOfMeasure           
		QuantityOfEach               is Unsigned Decimal size 13.7   
		StartDate                    is Date                         
		EndDate                      is Date                         



		UniversalItemNumber          is like Item 					 
		UNSPSCCode                   is AlphaUpper 8 
		UOMTypeID                    is Numeric 1                    
			States
				Primary              value is 1
				Low                  value is 2
				Alternate            value is 3
		LowUOMCode                   is like UnitOfMeasure            
		DistributorPriceAmount       is Unsigned Decimal size 15.2    
		ApplyDistributorMarkup       is Boolean                       
		ItemShortDescription         is Alpha 35
		ItemFullDescription          is Alpha 255
		ContainsHazardousMaterials   is Boolean                        
		ContainsLatex                is Boolean                        
		LatexFree                    is Boolean                        
		PVCDEHPFree                  is Boolean                        
		MercuryFree                  is Boolean                        
		ThimerosalFree               is Boolean                        
		RecordModifiedTimestamp      is Alpha 20
		DistributorMarkupType        is Alpha size 50
		CurrencyCode                 is like Currency
		TierName                     is Alpha size 192
		ManufacturerNumber           is a GPOSupplierID                
		OperationFlag                is Alpha 10                       





		
	Local Fields
		LocalContractGroup           is like ContractGroup
		LocalContractImport          is like ContractImport
		LocalGPOContractNumber       is a GPOContractNumber
		LocalManufacturerPart        is like ManufacturerNumber
		LocalVendorPart              is like VendorItem
		LocalTier                    is like ContractImportTier
		LocalTestTier                is like ContractImportTier
		DefaultedTier                is like ContractImportTier
		LocalDistributorSupplier     is like Supplier
		LocalItemSupplier            is like GPOSupplierID
		LocalManufacturerSupplier    is like GPOSupplierID
		LocalHPGManufacturerID       is like GPOSupplierID
		LocalContractLineImport      is like ContractLineImport
		NewContractLineImport        is a ContractLineImport view		
		LocalContract                is like Contract
		LocalManufacturerCode        is like ManufacturerCode
		LocalManufacturerDivision    is like ManufacturerDivision
		LocalItemDescriptionSize     is Numeric 400
		LocalTierName                is Alpha size 50
		LocalGPOUOM                  is like UnitOfMeasure
		LocalInternalUOM             is like UnitOfMeasure
		LocalPrice                   is Unsigned Decimal size 17.4
		LocalContractGPOItem         is like ContractGPOItem
		LocalCostOption              is Numeric 1
		CreatedNowFor2021            is Boolean

	Conditions
		
		ManufacturerItem
			restricted
			when (ManufacturerPart = VendorPart)
			
		VendorItemNotInactive
			restricted
			when (!VendorItemExists
			or   (VendorItemExists
			and   first VendorItemRel.Active))
			
		VendorItemExists
			restricted
			when (VendorItemRel exists)
			
		HasContractImport
			restricted
			when (ContractImportByGPOContractRel exists)

		OperationFlagEntered 
			restricted 
			when (OperationFlag entered)

	Relations
	
		ContractLineImportRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByGPOContractNumber
				related.ContractGroup  								= LocalContractGroup
				related.GPOContractNumber 							= LocalGPOContractNumber
				related.ManufacturerInformation.ManufacturerNumber	= LocalManufacturerPart
			Instance Selection
				where (related.UOM	= LocalInternalUOM)
		
		ContractLineImportByContractManufacturerRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByContractManufacturerNumber
				related.ContractGroup  										= LocalContractGroup
				related.GPOContractNumber 									= LocalGPOContractNumber
				related.ContractManufacturerInformation.ManufacturerNumber	= LocalManufacturerPart 
			Instance Selection
				where (related.ContractManufacturerInformation entered
				and    related.ManufacturerInformation 					!= related.ContractManufacturerInformation
				and    related.UOM	= LocalInternalUOM)

		GPOItemDistributorMarkupRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses ByGPOContractItems
				related.GPOContractNumber   						= LocalGPOContractNumber
				related.ManufacturerPart                            = LocalManufacturerPart
			Instance Selection	
				where (related.UOM = LocalGPOUOM
				and    related.TierLevel        = LocalTier
				and    related.ContractGPOItem != LocalContractGPOItem
				and    related.ApplyDistributorMarkup
				and    related.StartDate        = StartDate)

		GPOItemForManufacturerRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses ByGPOContractItems
				related.GPOContractNumber   						= LocalGPOContractNumber
				related.ManufacturerPart                            = LocalManufacturerPart
			Instance Selection	
				where (related.UOM = LocalGPOUOM
				and    related.TierLevel            = LocalTier
				and    related.ContractGPOItem 	   != LocalContractGPOItem
				and    related.ManufacturerSupplier	= related.ItemSupplier
				and    related.StartDate            = StartDate)

		ContractImportByGPOContractRel
			one-to-many relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber                           = GPOContractNumber			
		
		VendorItemRel
			one-to-many relation to VendorItem
   			Field Mapping uses Set3
				related.ProcurementGroup                            = LocalContractGroup
				related.Vendor                                      = ContractImportRel.Vendor
				related.VendorItem                                  = LocalVendorPart 
		
		ContractLineImportDistributorRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByGPOContractNumber
				related.ContractGroup  								= LocalContractGroup
				related.GPOContractNumber 							= LocalGPOContractNumber
				related.ManufacturerInformation.ManufacturerNumber	= LocalManufacturerPart
				related.VendorItem                                  = LocalVendorPart
			Instance Selection
				where (related.UOM = LocalInternalUOM)

		ContractImportRel
			one-to-one relation to ContractImport
			Field Mapping uses symbolic key
				related.ContractGroup  								= LocalContractGroup
				related.ContractImport	 							= LocalContractImport

        ContractLineMfgUpdateRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgInfoFromImport
				related.ContractGroup    									= LocalContractGroup
				related.Contract	     									= LocalContract
				related.Manufacturer.ManufacturerCode        				= LocalManufacturerCode	
				related.Manufacturer.ManufacturerDivision    				= LocalManufacturerDivision
				related.ManufacturerNumber				    				= ManufacturerPart
				related.UOM				 									= UOM
        		related.ManufacturerContract								= LocalContract
        	Instance Selection
        		where (related.LineNotClosed)

        ContractLineVenUpdateRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVenItemFromImport
				related.ContractGroup    									= LocalContractGroup
				related.Contract	     									= LocalContract
				related.VendorItem		 									= VendorPart	
				related.UOM				 									= UOM
        		related.ManufacturerContract								= LocalContract
        	Instance Selection
        		where (related.LineNotClosed)

		ContractLineImportTierCostRel
			one-to-many relation to ContractLineImportTierCost
			Field Mapping uses ByGPOContractNumber
				related.ContractGroup  								= LocalContractGroup
				related.GPOContractNumber 							= LocalGPOContractNumber
				related.ContractImportTier                          = LocalTier
				related.ManufacturerNumber  						= LocalManufacturerPart

		ContractLineImportTierCost2021Rel
			one-to-many relation to ContractLineImportTierCost
			Field Mapping uses ByGPOContractNumber
				related.ContractGroup  								= LocalContractGroup
				related.GPOContractNumber 							= LocalGPOContractNumber
				related.ContractImportTier                          = DefaultedTier
				related.ManufacturerNumber  						= LocalManufacturerPart

		DistributorRel
			one-to-one relation to ContractImportDistributor
			Field Mapping uses symbolic key
				related.ContractGroup                               = LocalContractGroup
				related.ContractImport                              = LocalContractImport
				related.ContractImportDistributor                   = LocalDistributorSupplier

		DistributorLineRel
			one-to-many relation to ContractLineImportDistributor 
			Field Mapping uses symbolic key
				related.ContractGroup  								= LocalContractGroup
				related.ContractImport  							= LocalContractImport
				related.ContractLineImport							= LocalContractLineImport
			Instance Selection
				where (related.DistributorSupplier                  = LocalDistributorSupplier)

		SupplierMappingRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalManufacturerSupplier
		
		ManufacturerMappingRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalHPGManufacturerID			

		ContractGPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByItemSupplier
				related.GPOContractNumber                           = LocalGPOContractNumber
				related.ItemSupplier                                = LocalItemSupplier

		ContractGPOParticipantTierRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber                           = LocalGPOContractNumber
			Instance Selection
				where (related.TierName                             = LocalTierName)

		ContractGroupRelLocal
			one-to-one relation to ContractGroup
			Field Mapping uses symbolic key
				related.ContractGroup                               = LocalContractGroup

		ItemSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalItemSupplier
				
		ManufacturerSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalManufacturerSupplier
				
		ManufacturerSupplierUOMRel 
			one-to-many relation to ContractImportGPOSupplierUOM
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalManufacturerSupplier	
				related.ContractImportGPOSupplierUOM                = LocalGPOUOM	
			Instance Selection 
				where (related.UnitOfMeasure entered)

		GPOSupplierUOMCreateRel 
			one-to-one relation to ContractImportGPOSupplierUOM
			Field Mapping uses symbolic key
				related.SupplierGroup                               = LocalContractGroup
				related.ContractImportGPOSupplier                   = LocalManufacturerSupplier	
				related.ContractImportGPOSupplierUOM                = LocalGPOUOM	

		ContractImportTierRel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup                               = LocalContractGroup
				related.ContractImport                              = LocalContractImport
				related.ContractImportTier                          = LocalTier

		ContractImportTier2021Rel
			one-to-many relation to ContractImportTier
			Field Mapping uses ByTierName
				related.ContractGroup                               = LocalContractGroup
				related.GPOContractNumber                           = LocalGPOContractNumber
				related.TierTitle                                   = LocalTierName

		ContractImportTestTierRel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup								= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTier  						= LocalTestTier

		ContractImportTestTier1Rel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup								= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTier  						= 1
				
		ContractImportTestTier2Rel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup								= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTier  						= 2
				
		ContractImportTestTier3Rel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup								= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTier  						= 3

		ContractImportTestTier4Rel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup								= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTier  						= 4
				
		ContractImportTestTier5Rel
			one-to-one relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup								= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTier  						= 5

		ContractGPOTierRel
			one-to-many relation to ContractGPOTier
			Field Mapping uses ByTierLevel
				related.TierLevel                                   = LocalTier

		ContractGPOHeaderRel
			one-to-many relation to ContractGPOHeader
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber                           = GPOContractNumber 			

	Sets
		ByGPOContract
			Sort Order
				GPOContractNumber
				TierLevel
				ManufacturerPart
			Instance Selection
				where (ManufacturerItem)
				
		ByGPOContractItems
			Sort Order
				GPOContractNumber
				ManufacturerPart
				VendorPart
				ContractGPOItem
				
		ByManufacturerPartForSupplier
			Sort Order
				GPOContractNumber
				ManufacturerPart
				ItemSupplier
				ContractGPOItem				
	
		BySupplier
			Sort Order
				GPOContractNumber
				ItemSupplier
				ContractGPOItem		






	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Purge 

		Purge is a Purge Action 

		PurgeDeletedRecords is a Set Action
			restricted	
			Instance Selection
				include only deleted records
			Action Rules
				Instance Rules
					invoke Purge		
		
		FixGPOContractNumber is a Set Action 
			default label is untranslatable
			restricted
			
			Instance Selection
				where (GPOContractNumber = "NULL")

			Action Rules
				
				Instance Rules
					initialize GPOContractNumber
		
		FixClassOfTrade is a Set Action 
			default label is untranslatable
			restricted
			
			Instance Selection
				where (ClassOfTrade not entered
				and    VendorPart entered)

			Action Rules
				
				Instance Rules
					ClassOfTrade = VendorPart
		
		AddAndUpdateLines is a Set Action  
			restricted
			Parameters
				ParmContractGroup       		is a ContractGroup
				ParmContractImport      		is a ContractImport
				ParmGPOContractNumber   		is a GPOContractNumber
				AutoUpdateContracts     		is Boolean
				TolerancePercent		   		is Percent size 6.3
				Connector
				ToleranceAmount			   		is an InternationalAmount
				RejectAllCostIncreases	   		is Boolean
				UpdateCostsOnly                 is Boolean
				ProcessItemMasterOnly           is Boolean
				ParmDistributor             	is Boolean
				FromContractCreation        	is Boolean
				DistributorPriority         	is Numeric 1
				DoNotCreateDistributorContract 	is Boolean
				StandardMarkupCalculation       is Boolean
				MultipleContracts               is Boolean
				DefaultTolerance                is Boolean
				ProcessOnlyUpdatedRecords       is Boolean
								
			Instance Selection
				where (GPOContractNumber   = ParmGPOContractNumber
				and   (ProcessOnlyUpdatedRecords = false
				or    (ProcessOnlyUpdatedRecords = true
				and    OperationFlagEntered))
				and   (StartDate !entered
				or     StartDate <= current date
				or    (StartDate > current date
				and    ParmContractImport.EffectiveDate   = StartDate 
				and    FromContractCreation)))
			
			Sort Order
				GPOContractNumber
				ManufacturerPart
				ContractGPOItem
				
			Action Rules
			
				GPOContractNumber Set Rules
					
					Exit Rules
					
						if (FromContractCreation)
							invoke Update ContractImportRel
								invoked.CreationInProgress = false	

						if (AutoUpdateContracts
						and ParmContractImport.NonBlankContractLinesExist
						and ParmContractImport.NewGPOContractExists)
          					invoke ProcessContractImportLinesUpdates ContractLineImport
								invoked.PrmContractGroup        = ParmContractGroup
								invoked.ImportedContract       	= ParmContractImport
								invoked.UpdateContract        	= ParmContractImport.DerivedToContract
								invoked.TolerancePercent      	= TolerancePercent
								invoked.Connector             	= Connector
								invoked.ToleranceAmount		  	= ToleranceAmount
								invoked.RejectAllCostIncreases	= RejectAllCostIncreases
								invoked.FromContractCreation    = FromContractCreation
								invoked.MultipleContracts       = MultipleContracts
								invoked.MultipleUOMHandling     = ParmContractGroup.DefaultGPOMultipleUOMHandling
								invoked.UpdateCostsOnly         = UpdateCostsOnly
								invoked.ProcessItemMasterOnly   = ProcessItemMasterOnly	
								invoked.DefaultTolerance        = DefaultTolerance							
								
				Instance Rules
				
					LocalContractGroup               = ParmContractGroup
					LocalContractImport              = ParmContractImport
					LocalContract                    = ParmContractImport.DerivedToContract
					LocalManufacturerCode            = ParmContractImport.ManufacturerCodeDivision.ManufacturerCode
					LocalManufacturerDivision        = ParmContractImport.ManufacturerCodeDivision.ManufacturerDivision
					LocalVendorPart                  = VendorPart
					LocalManufacturerPart            = ManufacturerPart
					LocalTier                        = TierLevel
					LocalGPOContractNumber           = ParmGPOContractNumber
					LocalItemSupplier                = ItemSupplier
					LocalDistributorSupplier         = ItemSupplierRel.ContractSupplier
					LocalManufacturerSupplier        = ManufacturerSupplier
					LocalHPGManufacturerID           = ManufacturerNumber
					LocalTierName                    = TierName
					LocalGPOUOM                      = UOM
					LocalInternalUOM                 = UOM
					if (ManufacturerSupplierUOMRel exists)
						LocalInternalUOM  			 = first ManufacturerSupplierUOMRel.UnitOfMeasure
					LocalContractGPOItem             = ContractGPOItem

					if (!ParmDistributor)
						if ((ContractGroupRelLocal.GPOIsHealthTrust)
						or  (ContractGroupRelLocal.GPOIsHealthTrust2021
						and  TierLevel entered))
							if (ContractImportTierRel !exists)
								invoke Create ContractImportTier
										invoked.ContractGroup       = ParmContractGroup
										invoked.ContractImport      = ParmContractImport
										invoked.ContractImportTier  = TierLevel
										if (ContractGroupRelLocal.GPOIsHealthTrust)
											invoked.TierTitle           = first ContractGPOTierRel.PricingTierDescription
										else
											invoked.TierTitle           = TierName 
										if (ParmContractImport.MultiTierDistributor
										and first ParmContractImport.ContractImportDistributorRel.MarkupPercent > 0)
											invoked.TierMarkupDiscount  			= "M"
											invoked.TierPricingAmtPct.TierPercent 	= first ParmContractImport.ContractImportDistributorRel.MarkupPercent
										else 
											invoked.TierMarkupDiscount  = "F" 											
										invoked.GPOContractNumber   = GPOContractNumber 
						if (ContractGroupRelLocal.GPOIsHealthTrust2021
						and TierLevel !entered)
							if (ContractImportTier2021Rel !exists)
								if (ContractImportTestTier1Rel !exists)
									DefaultedTier = 1
								else
								if (ContractImportTestTier2Rel !exists)
									DefaultedTier = 2
								else
								if (ContractImportTestTier3Rel !exists)
									DefaultedTier = 3
								else
								if (ContractImportTestTier4Rel !exists)
									DefaultedTier = 4
								else
								if (ContractImportTestTier5Rel !exists)
									DefaultedTier = 5
								invoke Create ContractImportTier
									invoked.ContractGroup       = ParmContractGroup
									invoked.ContractImport      = ParmContractImport
									invoked.ContractImportTier  = DefaultedTier
									invoked.TierTitle        	= TierName 
									invoked.Description      	= first ContractGPOParticipantTierRel.TierDefinition
									if (ParmContractImport.MultiTierDistributor
									and first ParmContractImport.ContractImportDistributorRel.MarkupPercent > 0)
										invoked.TierMarkupDiscount  			= "M"
										invoked.TierPricingAmtPct.TierPercent 	= first ParmContractImport.ContractImportDistributorRel.MarkupPercent
									else 
										invoked.TierMarkupDiscount  = "F" 											
									invoked.GPOContractNumber   = GPOContractNumber 							
							if (ContractImportTier2021Rel exists)
								DefaultedTier = first ContractImportTier2021Rel.ContractImportTier 
								LocalTier = DefaultedTier
						if  (!ContractGroupRelLocal.GPOIsHealthTrust2021)
							LocalPrice = Price
						else
						if  (ContractGroupRelLocal.GPOIsHealthTrust2021
						and  ContractGPOHeaderRel.PurchasingChannel contains "Direct")
							LocalPrice = Price 
						else
						if  (ContractGroupRelLocal.GPOIsHealthTrust2021
						and  ContractGPOHeaderRel.PurchasingChannel contains "Distributed")
							if (ApplyDistributorMarkup)
								LocalPrice = Price
							else
							if (!ApplyDistributorMarkup)
								if (GPOItemDistributorMarkupRel exists)	
									LocalPrice = first GPOItemDistributorMarkupRel.Price
								else	
									LocalPrice = Price
						else
						if  (ContractGroupRelLocal.GPOIsHealthTrust2021
						and  ContractGPOHeaderRel.PurchasingChannel contains "Both")
							if (TierName contains "Package")
								LocalPrice = 0
								LocalCostOption = 3
							else
							if (ManufacturerSupplier = ItemSupplier)
								LocalPrice = Price
							else
							if (GPOItemForManufacturerRel exists)
								LocalPrice = first GPOItemForManufacturerRel.Price
							else
								LocalPrice = Price
						else	
							LocalPrice = Price
						if (ContractLineImportByContractManufacturerRel exists
						and ContractLineImportRel !exists)
							invoke Update ContractLineImportByContractManufacturerRel
								invoked.ManufacturerInformation.ManufacturerNumber 						= ManufacturerPart 
						if (ContractLineImportRel !exists)
							invoke CreateWithTierCosts ContractLineImport
								assign result to NewContractLineImport
								invoked.ContractGroup                           					= ParmContractGroup
								invoked.ContractImport                          					= ParmContractImport
								invoked.GPOContractNumber                           				= ParmGPOContractNumber
								if (ContractGroupRelLocal.GPOIsHealthTrust2021)
									invoked.ManufacturerInformation.Manufacturer.ManufacturerCode 		= ManufacturerMappingRel.ManufacturerCodeDivision.ManufacturerCode 
									invoked.ManufacturerInformation.Manufacturer.ManufacturerDivision 	= ManufacturerMappingRel.ManufacturerCodeDivision.ManufacturerDivision
								else
									invoked.ManufacturerInformation.Manufacturer.ManufacturerCode 		= ManufacturerSupplierRel.ManufacturerCodeDivision.ManufacturerCode 
									invoked.ManufacturerInformation.Manufacturer.ManufacturerDivision 	= ManufacturerSupplierRel.ManufacturerCodeDivision.ManufacturerDivision								
								invoked.ManufacturerInformation.ManufacturerNumber 					= ManufacturerPart
								if (ParmContractImport.IsDistributorContractForGPO
								and ItemSupplier = ParmContractImport.ManufacturerSupplier)
									invoked.VendorItem                             					= VendorPart                   
								else
								if (ItemSupplier = ParmContractImport.ManufacturerSupplier
								and VendorPart != ManufacturerPart
								and VendorPart entered)
									invoked.VendorItem                                              = VendorPart 
								else 
								if (GPOItemForManufacturerRel exists
								and first GPOItemForManufacturerRel.VendorPart != first GPOItemForManufacturerRel.ManufacturerPart
								and first GPOItemForManufacturerRel.VendorPart entered)								
									invoked.VendorItem                                              = first GPOItemForManufacturerRel.VendorPart
								else
									invoked.VendorItem                              				= ManufacturerPart
								LocalItemDescriptionSize = ItemDescription size
								if (LocalItemDescriptionSize > 60)
									invoked.ItemDescription3                                        = ItemDescription
								if (ParmContractImport.EffectiveDate <= StartDate)
									invoked.EffectiveDate                                           = StartDate
								else
									invoked.EffectiveDate                                           = ParmContractImport.EffectiveDate
								if (ParmContractImport.ExpirationDate >= EndDate)
									invoked.ExpirationDate                                          = EndDate
								else 
									invoked.ExpirationDate                                          = ParmContractImport.ExpirationDate
								invoked.ItemDescription              								= ItemDescription
								invoked.BaseCost                                					= LocalPrice                        
								invoked.UOM                                                         = LocalInternalUOM
								if (LocalInternalUOM != LocalGPOUOM)
									invoked.GPOUnitOfMeasure                                        = LocalGPOUOM
								invoked.UOMConversion                           					= QuantityOfEach               
								if (UNSPSCCode != "0")
									invoked.UNSPSCCode                                              = UNSPSCCode
								if (TierLevel entered)
									invoked.TransientTier1                         					= TierLevel
									invoked.TransientTierCost1                                      = LocalPrice
								else
								if (DefaultedTier entered)
									invoked.TransientTier1                                          = DefaultedTier
									invoked.TransientTierCost1										= LocalPrice
								invoked.UpdateFunction		                          				= "C"
								invoked.LowUOMCode                                                  = LowUOMCode
								if (LocalCostOption = 3)
									invoked.CostOption                                              = 3
							LocalContractLineImport = NewContractLineImport.ContractLineImport
						else      
						if (ContractLineImportRel exists)
							if (ContractLineImportTierCostRel !exists)
								invoke Create ContractLineImportTierCost
									invoked.ContractGroup                       = ParmContractGroup
									invoked.ContractImport                      = ParmContractImport
									invoked.ContractLineImport                  = ContractLineImportRel.ContractLineImport
									invoked.ContractImportTier              	= LocalTier
									invoked.TierCost							= LocalPrice	
									invoked.ManufacturerNumber                  = ManufacturerPart
									invoked.VendorItem		                  	= VendorPart
									invoked.GPOContractNumber                   = GPOContractNumber							
							else
							if (!FromContractCreation)
								if  (EndDate entered   
								and  ParmContractImport.ExpirationDate >= EndDate
								and  ContractLineImportRel exists
								and  ContractLineImportRel.ExpirationDate != EndDate)
									invoke FastUpdate ContractLineImportRel
										invoked.ExpirationDate  = EndDate
										invoked.UpdateFunction 	= "U"
								if (ContractLineImportTierCostRel exists)
									if (LocalPrice != ContractLineImportTierCostRel.TierCost)
										invoke Update ContractLineImportTierCostRel
											invoked.TierCost        = LocalPrice
										invoke FastUpdate ContractLineImportRel
											invoked.BaseCost  		= LocalPrice			
											invoked.UpdateFunction = "U"
											if (LocalPrice = 0)
												invoked.CostOption = 2 
											else
											if (LocalPrice > 0)
												invoked.CostOption = 1 
								else
								if (ContractLineImportTierCostRel !exists)
									if (LocalPrice != ContractLineImportRel.BaseCost)
										invoke FastUpdate ContractLineImportRel
											invoked.BaseCost  		= LocalPrice			
											invoked.UpdateFunction 	= "U"
											if (LocalPrice = 0)
												invoked.CostOption = 2 
											else
											if (LocalPrice > 0)
												invoked.CostOption = 1 
							LocalContractLineImport = ContractLineImportRel.ContractLineImport		
						if (ManufacturerSupplier != ItemSupplier
						and ContractLineImportRel exists
						and !ParmDistributor
						and DoNotCreateDistributorContract = false
						and ParmContractImport.DoNotCreateDistributorContract = false)	
							if  (DistributorLineRel !exists
							and (DistributorRel exists
							or   ItemSupplierRel.ContractSupplier entered))
								if (DistributorRel !exists)
									invoke Create ContractImportDistributor
										invoked.ContractGroup				= ParmContractGroup
										invoked.ContractImport      		= ParmContractImport
										invoked.ContractImportDistributor	= ItemSupplierRel.ContractSupplier
										invoked.GPODistributorSupplier      = ItemSupplier
										invoked.Priority                    = DistributorPriority 
										invoked.StandardMarkupCalculation   = StandardMarkupCalculation
										
								invoke Create ContractLineImportDistributor
									invoked.ContractGroup                       = ParmContractGroup
									invoked.ContractImport                      = ParmContractImport
									invoked.ContractLineImport                  = LocalContractLineImport
									invoked.DistributorSupplier                 = ItemSupplierRel.ContractSupplier
									if (VendorPart entered)
										invoked.VendorItem                      = VendorPart
									else
										invoked.VendorItem                      = ManufacturerPart 
									invoked.GPOSupplier                         = ItemSupplier
									if (ApplyDistributorMarkup = false
									and ContractGroupRelLocal.GPOIsHealthTrust2021)
										CreatedNowFor2021 						= true
										invoked.DistributorCost                 = Price
									else
									if (DistributorMarkupType = "Embedded"
									and DistributorPriceAmount entered
									and ContractGroupRelLocal.GPOIsHealthTrust)    
										invoked.DistributorCost                 = DistributorPriceAmount 
								if (ContractLineImportRel.UpdateFunction = blank)
									invoke FastUpdate ContractLineImportRel
										invoked.UpdateFunction 	= "U"
								if (CreatedNowFor2021)
									invoke Update first DistributorLineRel
										invoked.DistributorCost = Price
							else
							if (DistributorLineRel exists
							and ((first DistributorLineRel.DistributorCost ! = DistributorPriceAmount
							and  DistributorMarkupType = "Embedded"
							and  ContractGroupRelLocal.GPOIsHealthTrust)
							or  (first DistributorLineRel.DistributorCost entered
							and  !DistributorMarkupType = "Embedded"
							and  ContractGroupRelLocal.GPOIsHealthTrust)
							or  (ApplyDistributorMarkup = false
							and  ContractGroupRelLocal.GPOIsHealthTrust2021
							and  first DistributorLineRel.DistributorCost != Price
							and  first DistributorLineRel.DistributorCost entered)))
								invoke Update first DistributorLineRel
									if (ContractGroupRelLocal.GPOIsHealthTrust)
										if (DistributorMarkupType = "Embedded")
											invoked.DistributorCost = DistributorPriceAmount
											invoked.LineChanged     = true
										else
											invoked.DistributorCost = 0
											invoked.LineChanged     = true
									if  (ApplyDistributorMarkup = false
									and  ContractGroupRelLocal.GPOIsHealthTrust2021)
										invoked.DistributorCost = Price 
										invoked.LineChanged     = true
								if (ContractLineImportRel.UpdateFunction = blank)
									invoke FastUpdate ContractLineImportRel
										invoked.UpdateFunction 	= "U"	
					else
					if (ParmDistributor)
						if (ContractLineImportDistributorRel !exists
						and VendorItemNotInactive)
							invoke Create ContractLineImport
								invoked.ContractGroup                           	= ParmContractGroup
								invoked.ContractImport                          	= ParmContractImport
								invoked.GPOContractNumber                           = ParmGPOContractNumber
								invoked.ManufacturerInformation.Manufacturer.ManufacturerCode 		= ManufacturerSupplierRel.ManufacturerCodeDivision.ManufacturerCode 
								invoked.ManufacturerInformation.Manufacturer.ManufacturerDivision 	= ManufacturerSupplierRel.ManufacturerCodeDivision.ManufacturerDivision
								invoked.ManufacturerInformation.ManufacturerNumber 	= ManufacturerPart
								if (ParmContractImport.EffectiveDate <= StartDate)
									invoked.EffectiveDate                                           = StartDate
								else
									invoked.EffectiveDate                                           = ParmContractImport.EffectiveDate
								if (ParmContractImport.ExpirationDate >= EndDate)
									invoked.ExpirationDate                                          = EndDate
								else
									invoked.ExpirationDate                                          = ParmContractImport.ExpirationDate
								invoked.VendorItem                              	= VendorPart                   
								invoked.ItemDescription              				= ItemDescription
								invoked.BaseCost                                	= Price                        
								invoked.UOM                                         = LocalInternalUOM
								if (LocalInternalUOM != LocalGPOUOM)
									invoked.GPOUnitOfMeasure                        = LocalGPOUOM     
								invoked.UOMConversion                           	= QuantityOfEach               
								invoked.UNSPSCCode                                  = UNSPSCCode	
								invoked.UpdateFunction                              = "C"								
						else
						if (!FromContractCreation)
							if (ContractLineImportDistributorRel exists)
								if (Price != ContractLineImportDistributorRel.BaseCost)
									invoke FastUpdate ContractLineImportDistributorRel
										if (EndDate entered
										and ParmContractImport.ExpirationDate >= EndDate)
											invoked.ExpirationDate = EndDate
										invoked.BaseCost  		= Price			
										invoked.UpdateFunction 	= "U"

		ClearOperationFlags is a Set Action 

			Instance Selection 
				where (OperationFlag entered)
			
			Action Rules 
				Instance Rules 

					OperationFlag = blank

		LoadGPOSupplierUOMs is a Set Action
			restricted  
			Parameters 
				ParmSupplierGroup   is a SupplierGroup 
				ParmSupplier        is a GPOSupplierID 

			Instance Selection 
				where (ParmSupplier = ManufacturerSupplier
				and    UOM entered)






			Action Rules 

				Instance Rules 

					LocalContractGroup				= ParmSupplierGroup
					LocalManufacturerSupplier       = ManufacturerSupplier
					LocalGPOUOM                     = UOM 
					if (GPOSupplierUOMCreateRel !exists)
						invoke CreateNoEdits ContractImportGPOSupplierUOM
							invoked.SupplierGroup					= ParmSupplierGroup 
							invoked.ContractImportGPOSupplier		= ManufacturerSupplier 
							invoked.ContractImportGPOSupplierUOM	= UOM





			 		
	
