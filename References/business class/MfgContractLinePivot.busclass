MfgContractLinePivot is a BusinessClass
    owned by po
    prefix is MFCL

    Ontology
  		symbolic key is MfgContractLinePivot

    Persistent Fields
    	
    Local Fields
        DistributorContract 	     is Numeric size 15
        DistributorVendor			 is a Vendor
        MfgItemNumber 			 	 is an Item
        MfgVendor					 is a Vendor
        MfgVendorItem                is AlphaUpper size 32
   		DefaultVendorItem			 is AlphaUpper size 32
        LocalManufacturerInformation
        
	Context Fields
		ContextDistributorContract    is a Contract

    Conditions
		        
		ContractLineSelected
			when (ContractLineRel exists)

		ContractLineNotSelected
			restricted
			when (ContractLineRel !exists)		
			
		ContextContractLineSelected
			restricted
			when (ContextContractLineRel exists)

		ContextContractLineNotSelected
			restricted
			when (ContextContractLineRel !exists)

		VendorItemExists
			restricted
			when (DefaultPovenitemRel exists)
	
	Relations
		ContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup 					= ContractGroup
				related.Contract      					= ManufacturerContractPivot
				
		ContractLineRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgContractLine
				related.ContractGroup 					= ContractGroup
				related.Contract      					= DistributorContract
				related.ManufacturerContract 			= ManufacturerContractPivot
            	related.ManufacturerContractLine 		= MfgContractLinePivot
           
		ContextContractLineRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgContractLine
				related.ContractGroup 					= ContractGroup
				related.Contract      					= ContextDistributorContract
				related.ManufacturerContract 			= ManufacturerContractPivot
            	related.ManufacturerContractLine 		= MfgContractLinePivot				
	
		MfgContractLineRel
			one-to-one relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup 					= ContractGroup
				related.Contract      					= ManufacturerContractPivot
				related.ContractLine      				= MfgContractLinePivot
				
		MfgContractRel
			one-to-one relation to ManufacturerContract
			Field Mapping uses ByPivotId
				related.ContractGroup 					= ContractGroup
				related.Contract      					= DistributorContract
				related.ManufacturerContractNumber  	= ManufacturerContractPivot

        PovenitemRel
        	one-to-one relation to VendorItem
        	Field Mapping uses symbolic key
        		related.ProcurementGroup  	    = ContractGroup
        		related.Item				    = MfgItemNumber
        		related.Vendor 				    = MfgVendor
        		related.VendorItem			    = MfgVendorItem

        DefaultPovenitemRel
        	one-to-many relation to VendorItem
        	Field Mapping uses Set4
        		related.ProcurementGroup	= ContractGroup
        		related.Vendor				= DistributorVendor
        		related.Item				= MfgItemNumber
        	Instance Selection
            	where (related.UseAsDefault)
        		
        DefaultItemRel
        	one-to-one relation to VendorItem
        	Field Mapping uses symbolic key
        		related.ProcurementGroup  	    = ContractGroup
        		related.Item				    = MfgItemNumber
        		related.Vendor 				    = DistributorVendor
        		related.VendorItem			    = DefaultVendorItem
        		
        NewPovenitemRel 
        	one-to-many relation to VendorItem
        	Field Mapping uses Set5
        		related.ProcurementGroup					= ContractGroup
        		related.Vendor								= DistributorVendor
    			related.Manufacturer.ManufacturerCode 		= LocalManufacturerInformation.Manufacturer.ManufacturerCode
    			related.Manufacturer.ManufacturerDivision 	= LocalManufacturerInformation.Manufacturer.ManufacturerDivision
    			related.ManufacturerNumber 					= LocalManufacturerInformation.ManufacturerNumber
        		related.Item								= MfgItemNumber
        	Instance Selection
        		where (related.Active)
        	

        NewPovenitemFromManufInfoOnlyRel 
        	one-to-many relation to VendorItem
        	Field Mapping uses Set5
        		related.ProcurementGroup					= ContractGroup
        		related.Vendor								= DistributorVendor
    			related.Manufacturer.ManufacturerCode 		= MfgContractLineRel.Manufacturer.ManufacturerCode
    			related.Manufacturer.ManufacturerDivision 	= MfgContractLineRel.Manufacturer.ManufacturerDivision
    			related.ManufacturerNumber 					= MfgContractLineRel.ManufacturerNumber
    		Instance Selection
        		where (related.Active)
    		

        NewItemmastFromPoitemvenRel
        	one-to-many relation to VendorItem
        	Field Mapping uses Set3
        		related.ProcurementGroup	= ContractGroup
        		related.Vendor				= DistributorVendor
        		related.VendorItem			= MfgVendorItem
        	Instance Selection
        		where (related.Active)
        		
  	Field Rules
  			
  	
  	Actions     	 	
 		Create is a Create Action
 			restricted
 		
 		Update is an Update Action
 			restricted
  		
 		Delete is a Delete Action
 			restricted
 			
 		Purge is a Purge Action
 			restricted

		AddLinesToDistributor is an Instance Action
			Parameters
				Contract
			Parameter Rules
				Contract
					required
			Action Rules
				if (Contract.ActiveContract)
					confirmation required
						"MustReleaseMembersAfterUsingThisAction"	
				initialize DefaultVendorItem
				DistributorContract	 		 = Contract
        		DistributorVendor			 = Contract.Vendor
        		MfgItemNumber				 = MfgContractLineRel.ItemNumber
        		MfgVendor                    = MfgContractLineRel.Contract.Vendor
        		MfgVendorItem				 = MfgContractLineRel.VendorItem
        		constraint (!ContractLineRel exists)
					"ManufacturerContractLineIsAlreadyAttachedToThisContract"
        		constraint (!MfgContractLineRel.SelectedForItemCreation)
        			"ManufacturerContractLineSelectedForItemCreation;CannotAttachToDistributorContractUntilItemCreationIsCompleted"
				constraint (MfgContractLineRel.HasLineMembers)																
					"ManufacturerContractHasUnreleasedMembers;MustReleaseMembersBeforeAddingLinesToDistributorContract"
				invoke Create ContractLine
					fill in fields from MfgContractLineRel
					invoked.ContractGroup   		 		= ContractGroup
					invoked.Contract      		     		= Contract
					invoked.ContractLine			 		= 0
					invoked.Priority				 		= 0 
					invoked.Taxable                         = false
					invoked.TaxCode					 		= blank 
					invoked.Retainage			 	 		= 0     
					invoked.RetainagePercent1	 	 		= 0     
					invoked.RetainageUpToPercent 	 		= 0     
					invoked.RetainagePercent2	 	 		= 0     
					invoked.RetainageOverridePercent 		= 0     
					invoked.HoldManufacturerLineOnly        = false
					invoked.CreateDistributorContractLine	= false								
					invoked.ErrorsExist                     = false
					invoked.HasBeenActivated                = false
					invoked.PreferredLine                   = false
					invoked.FromCreateDistributorLines      = true
					if (NewItemmastFromPoitemvenRel !exists)
						if (MfgItemNumber exists)
							if  (MfgContractLineRel.Manufacturer not entered     
							and  DefaultPovenitemRel exists  
							and  first DefaultPovenitemRel.Manufacturer.ManufacturerCode not entered)
								invoked.VendorItem = blank
						if  (MfgContractLineRel.Manufacturer entered
						and  MfgContractLineRel.ManufacturerNumber entered)
							if (NewPovenitemFromManufInfoOnlyRel exists)
								invoked.VendorItem = blank
					if (Contract.EffectiveDate	> MfgContractLineRel.EffectiveDate)
						invoked.EffectiveDate		  = Contract.EffectiveDate
					else
						invoked.EffectiveDate		  = MfgContractLineRel.EffectiveDate
					if (Contract.ExpirationDate	< MfgContractLineRel.ExpirationDate)
						invoked.ExpirationDate		  = Contract.ExpirationDate
					else
						invoked.ExpirationDate		  = MfgContractLineRel.ExpirationDate
						

				if	 (MfgContractLineRel.ItemType.Special
				and ((NewPovenitemFromManufInfoOnlyRel exists
				and   LocalManufacturerInformation.ManufacturerNumber entered)
				or   (NewItemmastFromPoitemvenRel exists)))
					
					invoke ChangeSpecialItemToItem MfgContractLineRel
						if (NewPovenitemFromManufInfoOnlyRel exists
						and LocalManufacturerInformation.ManufacturerNumber entered)
							invoked.PrmNewItemNumber		= first NewPovenitemFromManufInfoOnlyRel.Item
						else
							invoked.PrmNewItemNumber        = first NewItemmastFromPoitemvenRel.Item

