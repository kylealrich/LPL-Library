FranchiseSalesImport is a BusinessClass
    owned by fr
    prefix is FRI
    classic name is FRISALES

    Ontology
        symbolic key is FranchiseSalesImport
            classic set name is FRISET1
            classic name for FranchiseSalesImport.AdjustmentNumber is ADJ-NBR
            classic name for CustomerShipTo is SHIP-TO
            classic name for FranchiseContract is CONTR-NBR

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields

        SalesClose                     is an InternationalAmount
        SalesTax                       is an InternationalAmount
        Adjustments                    is an InternationalAmount
        ProcessLevel
        PostDate                       is Date
        FranchiseSalesImportUserField1 is a FranchiseSalesUserField
            classic name is FRL-USR-FLD-01
        FranchiseSalesImportUserField2 is a FranchiseSalesUserField
            classic name is FRL-USR-FLD-02
        FranchiseSalesImportUserField3 is a FranchiseSalesUserField
            classic name is FRL-USR-FLD-03
        FranchiseSalesImportUserField4 is a FranchiseSalesUserField
            classic name is FRL-USR-FLD-04
        FranchiseSalesImportUserField5 is a FranchiseSalesUserField
            classic name is FRL-USR-FLD-05

	Local Fields
		LocalFranchiseSalesImportAdjustmentNumber is like AdjustmentNumber 
	
	Transient Fields
		TransSelectableSalesType	is AlphaUpper size 1	//Using for select lists.  Intentionally excluding "Estimatedinvoice".
			States
            	Actualinvoice    value is "A"
                	default label is "ActualInvoice"
         		Callinestimate   value is "C"
            		default label is "CallInEstimate"
         		Aggregatesales   value is "S"
           			default label is "AggregateSales"
			 
    Derived Fields
    	DerivedLastAdjustmentNumber is a DerivedField
    		type is like AdjustmentNumber
    		restricted
			return last PreviousFranchiseSalesImportRel.FranchiseSalesImport.AdjustmentNumber
    
    Conditions
    	RecordExists
    		restricted
    		when (FranchiseSalesImport exists)
    		
    Relations
    	PreviousFranchiseSalesRel
    		one-to-many relation to FranchiseSales
    		Field Mapping uses Set9
    			related.Company 						= Company
    			related.Customer						= Customer
    			related.CustomerShipTo					= CustomerShipTo
    			related.FranchiseContract				= FranchiseContract
    			related.FranchiseSales.Date				= FranchiseSalesImport.Date
    			related.FranchiseSales.SalesType		= FranchiseSalesImport.SalesType

		
		FranchiseSalesRel
    		one-to-one relation to FranchiseSales
    		Field Mapping uses Set9
    			related.Company 						= Company
    			related.Customer						= Customer
    			related.CustomerShipTo					= CustomerShipTo
    			related.FranchiseContract				= FranchiseContract
    			related.FranchiseSales.Date				= FranchiseSalesImport.Date
    			related.FranchiseSales.SalesType		= FranchiseSalesImport.SalesType
    			related.FranchiseSales.AdjustmentNumber = 1
    	
    	PreviousFranchiseSalesImportRel
    		one-to-many relation to FranchiseSalesImport
    		Field Mapping uses Set2
    			related.Company 							= Company
    			related.Customer							= Customer
    			related.CustomerShipTo						= CustomerShipTo
    			related.FranchiseContract					= FranchiseContract
    			related.FranchiseSalesImport.SalesType		= FranchiseSalesImport.SalesType
    			related.FranchiseSalesImport.Date			= FranchiseSalesImport.Date
    			
    	FranchiseSalesImportRel
    		one-to-one relation to FranchiseSalesImport
    		Field Mapping uses Set2
    			related.Company									= Company
    			related.Customer								= Customer
    			related.CustomerShipTo							= CustomerShipTo
    			related.FranchiseContract						= FranchiseContract
    			related.FranchiseSalesImport.SalesType			= FranchiseSalesImport.SalesType
    			related.FranchiseSalesImport.Date				= FranchiseSalesImport.Date
    			related.FranchiseSalesImport.AdjustmentNumber	= 1
    	
    	NextFranchiseSalesImportRel
    		one-to-many relation to FranchiseSalesImport
    		Field Mapping uses Set2
    			related.Company									= Company
    			related.Customer								= Customer
    			related.CustomerShipTo							= CustomerShipTo
    			related.FranchiseContract						= FranchiseContract
    			related.FranchiseSalesImport.SalesType			= FranchiseSalesImport.SalesType
    			related.FranchiseSalesImport.Date				= FranchiseSalesImport.Date
    			related.FranchiseSalesImport.AdjustmentNumber	= FranchiseSalesImport.AdjustmentNumber + 1
    	
    
    Sets

        Set2
            indexed
            Sort Order
                Company
                Customer
                CustomerShipTo
                FranchiseContract
                FranchiseSalesImport.SalesType descending
                FranchiseSalesImport.Date
                FranchiseSalesImport.AdjustmentNumber

	Field Rules
		SalesClose
			if (!Company.AllowNegativeSales
			and FranchiseSalesImport.AdjustmentNumber = 1)
				constraint (SalesClose >= 0)
					"SalesClsoeCannotBeNegative"
	
		SalesTax		
			if (!Company.AllowNegativeSales
			and FranchiseSalesImport.AdjustmentNumber = 1)
				constraint (SalesTax >= 0)
					"SalesTaxCannotBeNegative"		
			
	
	Actions
		CreateNoRules is a Create Action
			restricted
			bypass field rules

		Create is a Create Action
			Action Rules	
				FranchiseSalesImport.SalesType = TransSelectableSalesType
				if (FranchiseSalesImport.AdjustmentNumber > 1)
					constraint (FranchiseSalesRel exists or FranchiseSalesImportRel exists)
						"CreatingOfAdjustmentSalesIsNotValid;SalesRecordDoesNotExist"
				
				if (PreviousFranchiseSalesImportRel exists
				and DerivedLastAdjustmentNumber > 0)
					FranchiseSalesImport.AdjustmentNumber = DerivedLastAdjustmentNumber + 1
				else
				if (PreviousFranchiseSalesRel exists)
					FranchiseSalesImport.AdjustmentNumber = last PreviousFranchiseSalesRel.FranchiseSales.AdjustmentNumber + 1 
				else
				if  (FranchiseSalesImport.AdjustmentNumber > 1
				and  PreviousFranchiseSalesImportRel not exists
				and  PreviousFranchiseSalesRel not exists)
					FranchiseSalesImport.AdjustmentNumber = 1
				
		Update is an Update Action
		Delete is a Delete Action
			Entrance Rules
				LocalFranchiseSalesImportAdjustmentNumber = FranchiseSalesImport.AdjustmentNumber + 1
				constraint (NextFranchiseSalesImportRel not exists)
					"CannotDeleteAdjustment<FranchiseSalesImport.AdjustmentNumber>;Adjusment<LocalFranchiseSalesImportAdjustmentNumber>Exists"	
					
