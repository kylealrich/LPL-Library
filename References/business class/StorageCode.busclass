StorageCode is a BusinessClass
    owned by ic
    prefix is BST
    classic name is BINSTORAGE

    Ontology
        symbolic key is StorageCode
            classic set name is BSTSET1
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        MaximumVolume   is a StockVolume
            classic name is CUBIC-FEET
        MaximumWeight   is a StockWeight
            classic name is WEIGHT
        MaximumQuantity is a Quantity
            classic name is QUANTITY
        Active          is Boolean
            classic name is ACTIVE-STATUS
        UnitOfMeasure
            classic name is UOM
        Length                                  is a LengthDimension
        Width                                   is a WidthDimension
        Height                                  is a HeightDimension
        VolumeEntryMethod                       is Numeric 1
            States
                Enter                     value is 0
                    default label is "EnterVolume"
                Calculate                 value is 1  
                    default label is "CalculateVolume"

	Attach Rules
		
		constraint (Active)
			"StorageCode<StorageCode>MustBeActive"
			
    Conditions

        IsActive
        	restricted
            when (Active)

        IsInactive
        	restricted
            when (not Active)
            
        RecordExists
        	restricted
        	when (StorageCode exists)
        
        RequireLength
            restricted
            when (Width  entered
            or    Height entered)

        RequireWidth
            restricted
            when (Length entered
            or    Height entered)

        RequireHeight
            restricted
            when (Width  entered
            or    Length entered)
        
        RequireUOM
            restricted
            when (InventoryLocation.UseAsWarehouseLocation
            and   MaximumQuantity entered)
        
        RequireMaximumQuantity
            restricted
            when (InventoryLocation.UseAsWarehouseLocation
            and   UnitOfMeasure entered)

    Field Rules

        Description
            required
            
        MaximumVolume
        	constraint (MaximumVolume > 0)
        		"MaximumCubicShouldBeGreaterThanZero"
            if (InventoryLocation.UseAsWarehouseLocation 
            and ComputedVolumeCapacity entered)
                force default to ComputedVolumeCapacity

        MaximumWeight
        	constraint (MaximumWeight > 0)
        		"MaximumWeightShouldBeGreaterThanZero"
        	
        MaximumQuantity
        	constraint (MaximumQuantity > 0)
        		"MaximumQuantityShouldBeGreaterThanZero"

            if (InventoryLocation.UseAsWarehouseLocation
            and UnitOfMeasure entered)
                required
                    "CannotProceed;_Maximum_QuantityIsBlank"
                    

        UnitOfMeasure
            if (InventoryLocation.UseAsWarehouseLocation)
                if (MaximumQuantity entered)
                    required
                        "CannotProceed;_Quantity_UOMIsBlank"   
            else
                required
        
        Length
            if (RequireLength)
                required   

        Width
            if (RequireWidth)
                required

        Height
            if (RequireHeight)
                required  

    Rule Blocks
        VolumeEntryMethodConfirmation
            if  (InventoryLocation.UseAsWarehouseLocation
            and  VolumeEntryMethod.Enter 
            and (Length entered 
            or   Height entered 
            or   Width  entered))
                confirmation required
                    "ThisWillClear_Length,_WidthAnd_Height;Proceed?"
                initialize Length 
                initialize Width  
                initialize Height 

    Derived Fields
        DerivedStorageCodeAndDescription                    is a MessageField
            default label is "Storage_Code"
            "<StorageCode>_-_<Description>"
        
        DerivedMaximumVolumeAndVolumeUOMLabel               is a MessageField
            "<MaximumVolume>_<Company.ItemGroup.VolumeUOM>"
        
        DerivedComputedVolumeCapacityAndVolumeUOMLabel      is a MessageField
            "<ComputedVolumeCapacity>_<Company.ItemGroup.VolumeUOM>"
        
        DerivedMaximumVolumeLabel                           is a MessageField
            "<MaximumVolume>"
        
        DerivedMaximumWeightAndWeightUOMLabel               is a MessageField
            "<MaximumWeight>_<Company.ItemGroup.WeightUOM>"

        DerivedMaximumWeightLabel                           is a MessageField
            "<MaximumWeight>"

        DerivedMaximumQuantityAndUOM                        is a MessageField
            "<MaximumQuantity>_<UnitOfMeasure>"

        DerivedMaximumWeightAndWeightUOM                    is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.WeightUOM.NotSelected)
				return DerivedMaximumWeightAndWeightUOMLabel
			else
				return DerivedMaximumWeightLabel
        
        DerivedMaximumVolumeAndVolumeUOM                    is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.VolumeUOM.NotSelected)
				return DerivedMaximumVolumeAndVolumeUOMLabel
			else
				return DerivedMaximumVolumeLabel
        
        DerivedComputedMaximumVolumeAndVolumeUOM            is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.VolumeUOM.NotSelected)
				return DerivedComputedVolumeCapacityAndVolumeUOMLabel
			else
				return DerivedMaximumVolumeLabel

        ComputedVolumeCapacity                              is a ComputeField
            type is like DimensionSize
            (Length * Width * Height) 
           

	Actions
		
		Create is a Create Action
            Entrance Rules
                include VolumeEntryMethodConfirmation

		Update is an Update Action
            Entrance Rules
                include VolumeEntryMethodConfirmation

		Delete is a Delete Action
