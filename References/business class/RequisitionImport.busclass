RequisitionImport is a BusinessClass
	owned by rq
	
	prefix is RQII
	
	Ontology
		symbolic key is RequisitionImport
	
	Patterns
		implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
	
	Persistent Fields
        RunGroup
        RecordInError           is Boolean
        ErrorMessage			is Alpha 150
        ResultProcessedBy       is a RequisitionInterfaceResult
        RequisitionNumber       is like Requisition
        ErrorLevel              is Numeric 2
        	States
        		Header          value is 1
        		Line            value is 2
        		Distribution    value is 3
        		Comment         value is 4
        		AddOnCharge     value is 5
        Requester               
        RequestingLocation      
        CreationDate            is TimeStamp
        RequisitionDescription  is Alpha size 30
        FromCompanyLocation		    
        VendorName              is Alpha size 30
        PrintRequisition        is Boolean
		OperatorID              is an Operator 
			holds pii
        QuoteRequired           is Boolean
        Dropship                is Boolean
        DropshipName            is a Name	 
        	holds pii
        DropshipAddress         is a PostalAddressV2	
        	holds pii
		DropshipPhoneNumber     is an TelephoneNumber
		DropshipContact         is an AddressContact
		PurchaseOrderBillToName is a Name	 
			holds pii
        PurchaseOrderBillToAddress is a PostalAddressV2	
        	holds pii
        PurchaseOrderBillToContact is an AddressContact
        PurchaseOrderBillToPhoneNumber is a TelephoneNumber 
        	holds pii
        PurchaseOrderBillToEmailAddress is an EmailAddressMulti 
        	holds pii
        DefaultProcedureInformation 
        DefaultBillCode         is a ConsignCode 
        GlobalDocumentType      
        CurrencyTable           
		AssetTemplate           
        Asset                   
        PurchaseTaxCode         is a TaxCode
        PurchaseTaxable         is Boolean
        DefaultDistributionAccount 	is a FinanceCodeBlock  
		Vendor                  
		PurchaseFromLocation    
		Buyer                   is like Buyer                
		RequestedDeliveryDate   is Date
		AllocationPriority      
		DeliverTo 
		OneSourceDocumentToOnePO        is Numeric size 1
            States
                NotApplicable value is 0
                No            value is 1
                Yes           value is 2
		TransactionCurrencyCode is a Currency
		LocationRule            is a WarehouseLocationRule
		PatientProcedure 
		SenderLogicalID				
		SenderComponentID			
		SenderCreationDateTime 		
		SenderBODID					
		SenderOriginalBOD
		EAMWorkOrder
		EAMWorkOrderActivity
		POCode 
		
	Local Fields
	
    	LocalErrorMessage						is Alpha 150
		LocalErrorOccurred                   	is Boolean
		InterfacedRequisitionNumber   			is a Requisition view
		LocalInterfaceResult            		is like RequisitionInterfaceResult
		LocalRunGroup                   		is like RunGroup
		LocalRequisitionImportLine              is like RequisitionImportLine
		LocalFinanceEnterpriseGroup				is like FinanceEnterpriseGroup
		LocalErrorLevel           				is Numeric size 2
		LocalRequisitionInterfaceResult			is a RequisitionInterfaceResult
		LocalConfigurationParameter				is Alpha size up to 200
	
	Derived Fields
	
		NoImportLinesMessage is a MessageField
    		restricted
    		"CannotAddNewRequisition;MustHaveLineRecordsToProcessRequisition"

#ifdef module integration	
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
							
		DerivedRequisitionImportBODID is a DerivedField
			type is Alpha 100
			restricted 
			return	"infor-nid:" + DerivedTenantID +":"+ RequisitionImport + ":"+ RunGroup +"?RequisitionImport&verb=Process"
#endif
		RequisitionImportAlertDescription is a DerivedField
			type is Alpha 200
			restricted 
			return	"Unprocessed RequisitionImport Detail , RunGroup : "+ " "+RunGroup+","+ "RequisitionImport :" +""+RequisitionImport
			
		DerivedRequisitionImportAlertComponentID is a DerivedField
			type is Alpha 50
			restricted 
			if (SenderComponentID != "")
				return SenderComponentID
			else
				return "0"
				
		DerivedRequisitionImportAlertCreationDateTime is a DerivedField
			type is Alpha 50
			restricted 
			if (SenderCreationDateTime != "")
				return SenderCreationDateTime
			else
				return "0"
				
		DerivedRequisitionImportAlertOriginalBOD is a DerivedField
			type is Alpha 100
			restricted 
			if (SenderOriginalBOD != "")
				return SenderOriginalBOD
			else
				return "0"
		
		RequisitionImportAlertXML is a DerivedField
			type is XMLDocument
			restricted
			RequisitionImportAlertXML = template.IONRequisitionImport_RequisitionImport_ST document for this instance	


		DerivedRequisition is a DerivedField
			type is Numeric size 12
			restricted
			return RequisitionRel.Requisition

		NameAndAddressRequiredMessage is a MessageField
			restricted
			"NameAndAddressIsRequiredForAlternateShipToAddress"

	Conditions

        IsInError
            restricted
            when (RecordInError)
        
		ImportLinesExist
			restricted
			when (RequisitionImportLinesRel exists)
		
		ImportDistributionsExist
			restricted
			when (RequisitionImportLineDistributionsRel exists)
			
		ImportCommentsExist
			restricted
			when (RequisitionImportCommentsRel exists)
		
		ImportAddOnChargesExist
			restricted
			when (RequisitionImportAddOnChargesRel exists)
		
		RecordExists
			restricted
			when (RequisitionImport exists)        
			
		RequisitionExists
			restricted
			when (RequisitionNumber entered)

		LineDistributionExists
			restricted
			when (LineDistributionsRel exists)
			
		CompanyExists
			restricted
			when (Company entered)
			
		RequesterExists
			restricted
			when (Requester exists)
			
		RequestingLocationExists
			restricted
			when (RequestingLocation entered)
				
	Relations

        RequisitionImportLinesRel
            one-to-many relation to RequisitionImportLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
                
        RequisitionImportLineDistributionsRel
            one-to-many relation to RequisitionImportLineDistribution
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
        
        LineDistributionsRel
            one-to-many relation to RequisitionImportLineDistribution
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
        		related.RequisitionImportLine       = LocalRequisitionImportLine
        
        RequisitionImportCommentsRel
            one-to-many relation to RequisitionImportComment
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
        	Instance Selection
        		where (related.RequisitionImportLine = 0)
        
        RequisitionImportLineCommentsRel
            one-to-many relation to RequisitionImportComment
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
            Instance Selection
        		where (related.RequisitionImportLine > 0)
                
        RequisitionImportAddOnChargesRel
            one-to-many relation to RequisitionImportAddOnCharge
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
            Instance Selection
            	where (!related.SpreadAddOnCharge)
        
        RequisitionImportHeaderAddOnChargesRel
            one-to-many relation to RequisitionImportAddOnCharge
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
            Instance Selection
            	where (related.RequisitionImportLine = 0)
        
        RequisitionImportSpreadAddOnChargesRel
            one-to-many relation to RequisitionImportAddOnCharge
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
            Instance Selection
            	where (related.SpreadAddOnCharge)
        
        RequisitionImportByRunGroupRel
        	one-to-many relation to RequisitionImport
        	Field Mapping uses ByRunGroup
        		related.RunGroup                    = LocalRunGroup
        
        InterfacedRequisitionRel
         	one-to-many relation to Requisition
         	Field Mapping uses symbolic key
         		related.Company           			= Company
                related.Requisition		        	= RequisitionNumber

        RequisitionRel
         	one-to-many relation to Requisition
         	Field Mapping uses symbolic key
         		related.Company           			= Company
                related.Requisition		        	= RequisitionNumber
         		
        LocalInterfaceResultsRel
			one-to-one relation to RequisitionInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= LocalFinanceEnterpriseGroup
				related.RequisitionInterfaceResult				= LocalInterfaceResult 

		BudgetEditConfigurationRel
			one-to-one relation to BudgetEditConfiguration
			Field Mapping uses symbolic key
				related.BudgetEditConfiguration = Company.FinanceEnterpriseGroup

		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= Company.FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode		= "RQ"

#ifdef module integration		
		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "PulseAlert"
            	related.FSMBODConfiguration.Direction 	= 1
        
        FSMBODConfigurationDetailRel
        	one-to-many relation to FSMBODConfigurationDetail
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 			= 2	
            	related.FSMBODConfiguration.Noun 			= "PulseAlert"
            	related.FSMBODConfiguration.Direction 		= 1
            Instance Selection
				where (related.Alert						= "FSM_ION_RequisitionImportAlert"
				and	   related.Enable)
#endif
	
	Sets
	
		ByRunGroup
			Sort Order
				RunGroup
				Company
				RequisitionImport
	
	Field Rules
		RunGroup
			required
			
	Actions
	
		Create is a Create Action
			Action Rules
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules
		
		Delete is a Delete Action

			Entrance Rules
			
				if (RequisitionNumber entered)
					invoke Delete RequisitionRel
							
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
		
		MassDelete is a Set Action
			Parameters
				PrmRunGroup                 is a RunGroup
					default label is "RunGroup"
					
			Instance Selection
				where (PrmRunGroup !entered
				or     RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				RequisitionImport
				
			Action Rules
				
				Instance Rules
				
					invoke Delete
		
		MoveImportErrorsToNewRunGroup is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
				NewRunGroup					is a RunGroup
				PrmInterfaceResult			is a RequisitionInterfaceResult

			Instance Selection
				where (Company.FinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup
				and    RunGroup 						= PrmRunGroup
				and	   RecordInError)
				
			Sort Order
				RunGroup
				Company
				
			Action Rules
				Empty Set Rules
					invoke SetStatusToIncomplete PrmInterfaceResult
				Set Rules
					Exit Rules
						invoke SetStatusToIncomplete PrmInterfaceResult
									
				Instance Rules
					RunGroup   = NewRunGroup
		
		Update is an Update Action
			valid when (!RequisitionExists)
		
		InterfaceRequisitions is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup							is a RunGroup
					default label is "RunGroup"
				ReleaseRequisitions					is Boolean
				IncludeRequisitionsInError 			is Boolean
				PrmMoveErrorsToNewRunGroup  		is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix				is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				
				PrmRunGroup
					LocalRunGroup = PrmRunGroup
					required
						"RunGroupIsRequired"
				
				ReleaseRequisitions
					initial value is true
				
				IncludeRequisitionsInError
					if (PrmRunGroup like "*ERRORS_*")
						default to true	
			
			Local Fields
				LocalInterfaceResultView		is a RequisitionInterfaceResult view				
			
			Instance Selection			
				where (PrmRunGroup					= RunGroup
				and    PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup)
			Sort Order
				RunGroup
				Company	
			
			Action Rules
			
				Empty Set Rules

					invoke Create RequisitionInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
						invoked.RunTime							= current timestamp
						invoked.RunGroup						= PrmRunGroup
						invoked.IncludeRequisitionsInError 		= IncludeRequisitionsInError
						invoked.ReleaseRequisitions			 	= ReleaseRequisitions
						invoked.MoveErrorsToNewRunGroup     	= PrmMoveErrorsToNewRunGroup	
						invoked.ErrorRunGroupPrefix         	= PrmErrorRunGroupPrefix
				
					invoke FinishInterfaceRequisitions 
						invoked.PrmFinanceEnterpriseGroup 		= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup  					= PrmRunGroup
						invoked.PrmInterfaceResult    			= LocalInterfaceResultView.RequisitionInterfaceResult	

				RunGroup Set Rules
					Entrance Rules
						invoke Create RequisitionInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.IncludeRequisitionsInError 	= IncludeRequisitionsInError
							invoked.ReleaseRequisitions			= ReleaseRequisitions
							invoked.MoveErrorsToNewRunGroup     = PrmMoveErrorsToNewRunGroup	
							invoked.ErrorRunGroupPrefix         = PrmErrorRunGroupPrefix
					Exit Rules
						invoke InterfaceRequisitionsLines RequisitionImportLine
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmInterfaceResult			= LocalInterfaceResultView.RequisitionInterfaceResult






				Instance Rules
					if (IncludeRequisitionsInError
					or  not RecordInError)
						initialize RecordInError
						initialize ErrorLevel                          
						initialize ErrorMessage
						ResultProcessedBy	= LocalInterfaceResultView.RequisitionInterfaceResult

						if (not Dropship)
							if ((DropshipName entered 		and  DropshipAddress.Country not entered)							
							or 	(DropshipName not entered	and  DropshipAddress.Country entered)								
							or  (DropshipName not entered	and (DropshipContact entered or DropshipPhoneNumber entered)))		
								LocalErrorOccurred		= true
								LocalErrorMessage		= NameAndAddressRequiredMessage
						
						if (not LocalErrorOccurred)

							if (RequisitionNumber entered) 
								invoke StartInterface RequisitionRel
									invoked.PrmOriginatingInterfaceRun = ResultProcessedBy
							else
								if (ImportLinesExist)
									invoke Unreleased.Create Requisition
										assign result to InterfacedRequisitionNumber
										resume on error
											LocalErrorOccurred			= true
											LocalErrorMessage			= error message
										fill in fields from this instance
										invoked.RequisitionImport		= RequisitionImport
										invoked.InterfaceInProcess		= true
										invoked.OriginatingInterfaceRun = LocalInterfaceResultView.RequisitionInterfaceResult
										invoked.RequisitionSource		= RqSource.RequisitionInterface									
								else 
									LocalErrorOccurred 	= true
									LocalErrorMessage 	= NoImportLinesMessage
								RequisitionNumber       	= InterfacedRequisitionNumber.Requisition													

						if (LocalErrorOccurred)
							RecordInError				= true
							ErrorMessage				= LocalErrorMessage
							ErrorLevel					= RequisitionImport.ErrorLevel.Header						

#ifdef module integration
		RequisitionImportAlert is an Instance Action
			restricted
			Parameters
			Action Rules	
				invoke TriggerPulseAlert FSMBODConfigurationRel
					invoked.PrmActorGroup 	= "REQUISITIONFAILUREGROUP" 
					invoked.PrmMainXML 		= RequisitionImportAlertXML	
					invoked.PrmDescription	= RequisitionImportAlertDescription
					invoked.PrmBODID		= DerivedRequisitionImportBODID
#endif
								
		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmErrorLevel           is Numeric size 2
			Action Rules
				RecordInError			= true
				ErrorMessage			= PrmErrorMessage
				ErrorLevel				= PrmErrorLevel


		FinishInterfaceRequisitions is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmRunGroup							is a RunGroup
				PrmInterfaceResult					is a RequisitionInterfaceResult
			Local Fields
				LocalRecordsProcessed				is like Requisition
				LocalLastTimeStamp		 is TimeStamp

			Instance Selection
				where (PrmRunGroup					= RunGroup
				and    PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup)
				
			Sort Order
				RunGroup
				Company
				
			Action Rules
				Empty Set Rules
					invoke AcknowledgeForFailureRunGroupBOD
						invoked.PrmRunGroup	= RunGroup
					
					invoke Finish PrmInterfaceResult 
				
				RunGroup Set Rules
					Exit Rules
						invoke AcknowledgeForFailureRunGroupBOD
							invoked.PrmRunGroup	= RunGroup
						
						invoke Finish PrmInterfaceResult 
							invoked.PrmRecordsProcessed	= LocalRecordsProcessed
						
				Instance Rules
					if (not RecordInError)
						increment LocalRecordsProcessed
						
						if (PrmInterfaceResult.ReleaseRequisitions)
							invoke FinishAndReleaseInterface RequisitionRel
						else
							invoke FinishInterface RequisitionRel
						
						invoke FastDelete
					else
						if (RequisitionNumber entered)
							invoke FinishInterface RequisitionRel
							
					if ((BudgetEditConfigurationRel.BatchBudgetEdit)					
					and (GeneralLedgerSystemCodeRel.EncumbranceOption.Track or GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit)) 
						if ((LocalLastTimeStamp not entered) or (current timestamp - LocalLastTimeStamp) > 60)
							invoke InitiateBudgetEdit BudgetEditConfigurationRel in background
							LocalLastTimeStamp = current timestamp
						
		AcknowledgeForFailureRunGroupBOD is a Set Action
			restricted
			Parameters
				PrmRunGroup				  is AlphaUpper 30
					default label is "RunGroup"
			Instance Selection
				where (Company.FinanceEnterpriseGroup    = actor.context.FinanceEnterpriseGroup
                and    RunGroup	= PrmRunGroup
				and	   SenderLogicalID entered 
				and    ErrorMessage entered)
			Action Rules
				Instance Rules
					if (Company.FinanceEnterpriseGroup.BODTrigger)
		            	if (SenderLogicalID entered or SenderComponentID entered or SenderBODID entered)
		                	trigger "RequisitionImportService" PA service 
		                    	resume on error
		                        title is "EG:<Company.FinanceEnterpriseGroup>CO:<Company>RI:<RequisitionImport>"
		                        Criteria
		                        	Company.FinanceEnterpriseGroup      
		                            Company      
		                        Variables
		                        	SenderLogicalID		
									SenderComponentID	
									SenderCreationDateTime 	
									SenderBODID	
									SenderOriginalBOD		
									Company 
									RunGroup	
									Requester
									RequisitionImport
		                            Company.FinanceEnterpriseGroup
		                            	variable name is FinanceEnterpriseGroup				
#ifdef module integration
	       					if (FSMBODConfigurationRel.Enable)
          						if(FSMBODConfigurationDetailRel.Enable)
			   						invoke RequisitionImportAlert
#endif				
