CashManagementAccountResource is a BusinessClass
	owned by cashmgmt
	prefix is CMAR
	representative text is "<FinanceResource.Employee.FirstLastNameID>"
	representative image is FinanceResource.Employee.ResourcePicture.File
		display as portrait image
		missing image is random background
			foreground text is "<FinanceResource.Employee.Name.Initials>"

	Ontology
		symbolic key is CashManagementAccountResource

	Persistent Fields
		MainContact							is Boolean
		SigningAmount						is an InternationalAmount
			default label is "SigningLimit"
		NoLimitSigning						is Boolean
			default label is "NoLimit"
		ApprovalAmount						is an InternationalAmount
			default label is "ACHApprovalLimit"
		NoLimitACHApproval					is Boolean
			default label is "NoLimit"
		WireApprovalAmount					is an InternationalAmount
			default label is "WireApprovalLimit"
		NoLimitWireApproval					is Boolean
			default label is "NoLimit"
		TransferAmount						is an InternationalAmount
			default label is "ACHInitiationLimit"
		NoLimitACHTransfer					is Boolean
			default label is "NoLimit"
		WireTransferAmount					is an InternationalAmount
			default label is "WireInitiationLimit"
		NoLimitWireTransfer					is Boolean
			default label is "NoLimit"
		InternalFundsTransferAmount			is an InternationalAmount
			default label is "InternalFundsTransferInitiationLimit"
		NoLimitInternalFundsTransfer		is Boolean
			default label is "NoLimit"
		CheckOutsourcingTransferAmount		is an InternationalAmount
			default label is "CheckOutsourcingInitiationLimit"
		NoLimitCheckOutsourcingTransfer		is Boolean
			default label is "NoLimit"
		InternalFundsApprovalAmount			is an InternationalAmount
			default label is "InternalFundsTransferApprovalLimit"
		NoLimitInternalFundsApproval		is Boolean
			default label is "NoLimit"
		CheckOutsourcingApprovalAmount		is an InternationalAmount
			default label is "CheckOutsourcingApprovalLimit"
		NoLimitCheckOutsourcingApproval		is Boolean
			default label is "NoLimit"
		SigningComment						is Alpha size up to 500
		ApprovalComment						is Alpha size up to 500
		TransferComment						is Alpha size up to 500
			default label is "InitiationComment"
		Status								is Numeric 1
			default label is "SignerStatus"
			States
				AwaitingSignatureCard	value is 1
				SignatureCardReceived	value is 2
		Active
		ManuallyInitiateFundsTransfer		is Boolean
			default label is "ManuallyInitiateFundsTransfers"
		ApprovalCurrency					is a Currency
			default label is "Currency"
		WireApprovalCurrency				is a Currency
			default label is "Currency"
		TransferCurrency					is a Currency
			default label is "Currency"
		WireTransferCurrency				is a Currency
			default label is "Currency"
		InternalFundsTransferCurrency		is a Currency
			default label is "Currency"
		CheckOutsourcingTransferCurrency	is a Currency
			default label is "Currency"
		InternalFundsApprovalCurrency		is a Currency
			default label is "Currency"
		CheckOutsourcingApprovalCurrency	is a Currency
			default label is "Currency"
		SigningCurrency						is a Currency
			default label is "Currency"
		SignerBeginDate						is Date
			default label is "StartDate"
		SignerInactivationDate				is Date
			default label is "EndDate"
		TransferInitiationBeginDate			is Date
			default label is "StartDate"
		TransferInitiationInactivationDate	is Date
			default label is "EndDate"
		TransferApprovalBeginDate			is Date
			default label is "StartDate"
		TransferApprovalInactivationDate	is Date
			default label is "EndDate"
		Description							is Alpha up to 250
		OnlineSecurityToken					is Alpha 30
			default label is "SecurityToken"
		OnlineSystemAdministrator			is Boolean
			default label is "SystemAdministrator"

	Field Rules
		SigningAmount
			if (NoLimitSigning)
				initialize

		ApprovalAmount
			if (NoLimitACHApproval)
				initialize

		WireApprovalAmount
			if (NoLimitWireApproval)
				initialize

		InternalFundsApprovalAmount
			if (NoLimitInternalFundsApproval)
				initialize

		CheckOutsourcingApprovalAmount
			if (NoLimitCheckOutsourcingApproval)
				initialize

		TransferAmount
			if (NoLimitACHTransfer)
				initialize

		WireTransferAmount
			if (NoLimitWireTransfer)
				initialize

		InternalFundsTransferAmount
			if (NoLimitInternalFundsTransfer)
				initialize

		CheckOutsourcingTransferAmount
			if (NoLimitCheckOutsourcingTransfer)
				initialize

		Active
			initial value is true
			if (MainContact)
				required
					"ReassignMainContactBeforeInactivating"

		ApprovalCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		WireApprovalCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		TransferCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		WireTransferCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		InternalFundsTransferCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		CheckOutsourcingTransferCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		InternalFundsApprovalCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		CheckOutsourcingApprovalCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		SigningCurrency
			initial value is CashManagementAccount.Currency
			default to CashManagementAccount.Currency

		SignerBeginDate
			if (!IsSigner)
				cannot be entered
					"SignerStartDateCannotBeEnteredWhenSignerDetailsAreNotEntered"

		SignerInactivationDate
			if (!IsSigner)
				cannot be entered
					"SignerEndDateCannotBeEnteredWhenSignerDetailsAreNotEntered"

			if (SignerBeginDate entered)
				constraint (SignerInactivationDate >= SignerBeginDate)
					"SignerEndDateCannotBeBeforeSignerStartDate"

		TransferInitiationBeginDate
			if (!CanInitiateTransfers)
				cannot be entered
					"TransferInitiationStartDateCannotBeEnteredWhenTransferInitiationDetailsAreNotEntered"

		TransferInitiationInactivationDate
			if (!CanInitiateTransfers)
				cannot be entered
					"TransferInitiationEndDateCannotBeEnteredWhenTransferInitiationDetailsAreNotEntered"

			if (TransferInitiationBeginDate entered)
				constraint (TransferInitiationInactivationDate >= TransferInitiationBeginDate)
					"TransferInitiationEndDateCannotBeBeforeTransferInitiationStartDate"

		TransferApprovalBeginDate
			if (!CanApproveTransfers)
				cannot be entered
					"TransferApprovalStartDateCannotBeEnteredWhenTransferApprovalDetailsAreNotEntered"

		TransferApprovalInactivationDate
			if (!CanApproveTransfers)
				cannot be entered
					"TransferApprovalEndDateCannotBeEnteredWhenTransferApprovalDetailsAreNotEntered"

			if (TransferApprovalBeginDate entered)
				constraint (TransferApprovalInactivationDate >= TransferApprovalBeginDate)
					"TransferApprovalEndDateCannotBeBeforeTransferApprovalStartDate"

	Derived Fields
		DerivedInactiveResouceMessage is a DerivedField
			type is Alpha 30
			if (!Active)
				return DerivedInactiveAccountResourceMessage
			else
			if (!FinanceResource.Active)
				return DerivedInactiveFinanceResourceMessage
			else
				return ""

		DerivedInactiveAccountResourceMessage is a MessageField
			restricted
			"AccountResourceIsInactive"

		DerivedInactiveFinanceResourceMessage is a MessageField
			restricted
			"FinanceResourceIsInactive"

		MainContactDisplay is a LabelField
			"MainContact"

	Conditions
		InactiveResource
			restricted
			when (!Active
			or	  !FinanceResource.Active)

		IsSigner
			restricted
			when (SigningAmount entered
			or	  NoLimitSigning)

		CanInitiateTransfers
			restricted
			when (TransferAmount entered
			or	  NoLimitACHTransfer
			or	  WireTransferAmount entered
			or	  NoLimitWireTransfer
			or	  InternalFundsTransferAmount entered
			or	  NoLimitInternalFundsTransfer
			or	  CheckOutsourcingTransferAmount entered
			or	  NoLimitCheckOutsourcingTransfer)

		CanApproveTransfers
			restricted
			when (ApprovalAmount entered
			or	  NoLimitACHApproval
			or	  WireApprovalAmount entered
			or	  NoLimitWireApproval
			or	  InternalFundsApprovalAmount entered
			or	  NoLimitInternalFundsApproval
			or	  CheckOutsourcingApprovalAmount entered
			or	  NoLimitCheckOutsourcingApproval)

		ActiveSigner
			restricted
			when (IsSigner
			and	  CashManagementAccount.Active
			and   !InactiveResource
			and  (!SignerBeginDate entered
			or	  current corporate date >= SignerBeginDate)
			and  (!SignerInactivationDate entered
			or	  current corporate date <= SignerInactivationDate))

		ActiveOnlineUser
			restricted
			when ((CashManagementAccount.Active
			and    !InactiveResource
			and	  (OnlineSystemAdministrator
			or	   OnlineSecurityToken entered))
			or	   ActiveTransferInitiator
			or	   ActiveTransferApprover)

		ActiveSystemAdministrator
			restricted
			when (OnlineSystemAdministrator
			and   CashManagementAccount.Active
			and   !InactiveResource)

		ActiveTransferInitiator
			restricted
			when (CanInitiateTransfers
			and   CashManagementAccount.Active
			and   !InactiveResource
			and  (!TransferInitiationBeginDate entered
			or	  current corporate date >= TransferInitiationBeginDate)
			and  (!TransferInitiationInactivationDate entered
			or	  current corporate date <= TransferInitiationInactivationDate))

		ActiveTransferApprover
			restricted
			when (CanApproveTransfers
			and   CashManagementAccount.Active
			and   !InactiveResource
			and  (!TransferApprovalBeginDate entered
			or	  current corporate date >= TransferApprovalBeginDate)
			and  (!TransferApprovalInactivationDate entered
			or	  current corporate date <= TransferApprovalInactivationDate))

		EmailAddressEntered
			restricted
			when (FinanceResource.EmailAddress entered)

		PhoneNumberEntered
			restricted
			when (FinanceResource.FullWorkPhone entered)

		MainContactEntered
			restricted
			when (MainContact entered)

	Attach Rules
		constraint (Active)
			"ResourceIsInactive"

	Relations
		HasAnotherMainContactRel
			one-to-many relation to CashManagementAccountResource
			Field Mapping uses symbolic key
				related.CashManagementGroup 	= CashManagementGroup
				related.CashManagementAccount	= CashManagementAccount
			Instance Selection
				where (related.FinanceResource != FinanceResource
				and    related.MainContact)

	Actions
		Create is a Create Action
			Field Rules
				Active
					default to true

				Status
					if (IsSigner)
						default to 1	

			Action Rules
				if (MainContact)
					if (HasAnotherMainContactRel exists)
						invoke UpdateCashManagementAccountResource HasAnotherMainContactRel
							invoked.MainContact = false

		Update is an Update Action
			Action Rules
				if (MainContact)
					if (HasAnotherMainContactRel exists)
						confirmation required
							"PreviousMainContactWillBeReassigned;DoYouWantToContinue?"
						invoke UpdateCashManagementAccountResource HasAnotherMainContactRel
							invoked.MainContact = false

		UpdateCashManagementAccountResource is an Update Action
			restricted

		Delete is a Delete Action
			valid when (!MainContact)
