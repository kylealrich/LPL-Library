WorkApplicationCloseH is a BusinessClass
	owned by ar
	prefix is r190h

	Ontology
		symbolic key is WorkApplicationCloseH

	Patterns
		disable AuditIndex
		disable Auditing
		disable EffectiveDated
		disable DataTranslations
		disable StaticTranslations

	Persistent Fields
		FinanceEnterpriseGroup	is like FinanceEnterpriseGroup
		ApplicationCloseResult	is like ApplicationCloseResult
		RecKey					is a AR190War190hRecKeyGroup
		BaseAmt					is like InternationalAmount
		OrigNd					is like OrigNd
		ArCode					is like ReceivableGeneralLedgerCode
		Customer				is like Customer	
		RoundingCode			is like ReceivableGeneralLedgerCode
		ProgramSequence

	Local Fields
		LocalActor				is Actor

	Relations
		ARCompanyRel
			one-to-one relation to ReceivableCompany
			Field Mapping uses symbolic key
				related.Company						 = RecKey.Company

		GLCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company						 = RecKey.Company

		ARCodeRel
			one-to-one relation to ReceivableGeneralLedgerCode
			Field Mapping uses symbolic key
				related.Company						 = RecKey.Company
				related.ReceivableGeneralLedgerCode	 = RoundingCode

		OffsetRel
			one-to-one relation to ReceivableGeneralLedgerCode
			Field Mapping uses symbolic key
				related.Company						 = RecKey.Company
				related.ReceivableGeneralLedgerCode	 = RecKey.OffsetCode

		CloseDistributionRel
			one-to-many relation to ReceivableGLDistribution
			Field Mapping uses ByApplicationCloseResult
				related.FinanceEnterpriseGroup		 = FinanceEnterpriseGroup
				related.ApplicationCloseResult		 = ApplicationCloseResult

	Sets

		RecKey
			indexed
			no duplicates
			Sort Order
				FinanceEnterpriseGroup
				ApplicationCloseResult
				RecKey

	Actions

		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		FastUpdate is an Update Action
			restricted
			bypass field rules

		Delete is a Delete Action
			restricted

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules


		Process is a Set Action
			restricted

			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmApplicationCloseResult	is a ApplicationCloseResult
				PrmCreateDate				is TimeStamp

			Instance Selection
				where (FinanceEnterpriseGroup	 = PrmFinanceEnterpriseGroup
				and	ApplicationCloseResult		 = PrmApplicationCloseResult)


			Action Rules
				Empty Set Rules
					invoke Update PrmApplicationCloseResult
						invoked.Status = 1

					LocalActor = actor
					send notification
						to LocalActor
						description is "ApplicationCloseHasCompleted"
						priority is high
						detail is "ResultsCanBeSeenInApplicationCloseResults"

				Set Rules
					Entrance Rules
						display "AR190HBegin"

					Exit Rules
						invoke Update PrmApplicationCloseResult
							invoked.Status = 1

						LocalActor = actor
						send notification
							to LocalActor
							description is "ApplicationCloseHasCompleted"
							priority is high
							detail is "ResultsCanBeSeenInApplicationCloseResults"

				Instance Rules
					display "H<RecKey.Invoice>"
					invoke CreateFromApplicationClose ReceivableGLDistribution
						invoked.FinanceEnterpriseGroup							= FinanceEnterpriseGroup
						invoked.ReceivableGLDistribution.ProgramSequence		= ProgramSequence
						invoked.LastDistributionSequence						= 1
						invoked.CreateDate										= PrmCreateDate

						invoked.ReceivableCompanyGroup.GlCompany				= RecKey.Company
						invoked.ReceivableCompanyDataGroup.TransactionCompany	= RecKey.Company

						invoked.GeneralLedgerAccount							= ARCodeRel.DistributionAccount
						invoked.GeneralLedgerDate								= RecKey.GlDate
						invoked.TransactionDate									= RecKey.GlDate

						if  (RecKey.TransType = "C")
							invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount	= BaseAmt * -1
						else
							invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount	= BaseAmt

						invoked.TransType										= RecKey.TransType
						invoked.Invoice											= RecKey.Invoice
						invoked.BatchNumber										= RecKey.BatchNbr
						invoked.ReceivableCompanyDataGroup.Customer				= Customer
						invoked.SequenceNumber									= RecKey.PaymentSeq
						invoked.OriginalCurrency								= RecKey.AppCurrCd
						invoked.LastDistributionSequence						= RecKey.AppSeq
							
						invoked.Origin											= RecKey.AppSource
						if  (ARCompanyRel.DetailGLTransPerApplication = "Y")
							invoked.AccumulationType							= "D"
						else
							invoked.AccumulationType							= "S"
						invoked.Status											= 2

						invoked.Description										= "Cash App Curr Rounding Adj"

						invoked.ApplicationCloseResult							= PrmApplicationCloseResult
						invoked.TransientRoundingDistrib						= true

					invoke CreateFromApplicationClose ReceivableGLDistribution
						invoked.FinanceEnterpriseGroup							= FinanceEnterpriseGroup
						invoked.ReceivableGLDistribution.ProgramSequence		= ProgramSequence + 1
						invoked.LastDistributionSequence						= 1
						invoked.CreateDate										= PrmCreateDate

						invoked.ReceivableCompanyGroup.GlCompany				= RecKey.Company
						invoked.ReceivableCompanyDataGroup.TransactionCompany	= RecKey.Company

						invoked.GeneralLedgerAccount							= OffsetRel.DistributionAccount
						invoked.GeneralLedgerDate								= RecKey.GlDate
						invoked.TransactionDate									= RecKey.GlDate

						if  (RecKey.TransType = "C")
							invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount	= BaseAmt
						else
							invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount	= BaseAmt * -1

						invoked.TransType										= RecKey.TransType
						invoked.Invoice											= RecKey.Invoice
						invoked.BatchNumber										= RecKey.BatchNbr
						invoked.ReceivableCompanyDataGroup.Customer				= Customer
						invoked.SequenceNumber									= RecKey.PaymentSeq
						invoked.OriginalCurrency								= RecKey.AppCurrCd
						invoked.LastDistributionSequence						= RecKey.AppSeq

						invoked.Origin											= RecKey.AppSource
						if  (ARCompanyRel.DetailGLTransPerApplication = "Y")
							invoked.AccumulationType							= "D"
						else
							invoked.AccumulationType							= "S"
						invoked.Status											= 2

						invoked.Description										= "Cash App Curr Rounding Adj"

						invoked.AuditSummaryIndicator							= "01"	
						invoked.ApplicationCloseResult							= PrmApplicationCloseResult
						invoked.TransientRoundingDistrib						= true


					invoke FastDelete

