SchoolClosedPeriod is a BusinessClass
    owned by studentactivities
    prefix is SSCP

    Ontology
    	symbolic key is SchoolClosedPeriod

 	Patterns
		implements ContextualParent
 		implements DynamicCreation

    Persistent Fields
		CloseDate		is Date

	Transient Fields
		TransactionCurrencyCode is a Currency
			derive value from StudentActivitySchool.DefaultAccountingEntity.FunctionalCurrency

	Derived Fields
		AssetActivityBalance is a DerivedField
			type is like InternationalAmount
			return (sum AssetActivityBalanceDetailRel.PeriodEndingBalance)

		LiabilityActivityBalance is a DerivedField
			type is like InternationalAmount
			return (sum LiabilityActivityBalanceDetailRel.PeriodEndingBalance)

		RevenueActivityBalance is a DerivedField
			type is like InternationalAmount
			return (sum RevenueActivityBalanceDetailRel.PeriodEndingBalance)
			
		ClosedPeriodDisplay is a LabelField
			"Period:<StudentActivityClosedPeriod>"
			
		SchoolDisplay is a LabelField
			"School:<StudentActivitySchool>"
		
		CloseDateDisplay is a LabelField
			"CloseDate:<CloseDate>"
			
		CurrencyCodeDisplay is a LabelField
			"Currency:<TransactionCurrencyCode>"


	Sets
		ByDescendingPeriod
			Sort Order
				StudentActivityDistrict

				StudentActivityClosedPeriod	descending
				StudentActivitySchool
									
		BySchool
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				StudentActivityClosedPeriod
									

	Relations
		AssetActivityBalanceDetailRel is a StudentActivityBalanceDetail set
			Instance Selection
				where (related.StudentActivityAccount.StudentActivityType.Asset)

		LiabilityActivityBalanceDetailRel is a StudentActivityBalanceDetail set
			Instance Selection
				where (related.StudentActivityAccount.StudentActivityType.Liability)

		RevenueActivityBalanceDetailRel is a StudentActivityBalanceDetail set
			Instance Selection
				where (related.StudentActivityAccount.StudentActivityType.Revenue)

		StudentActivityAccountRel
			one-to-many relation to StudentActivityAccount
			Field Mapping uses symbolic key
				related.StudentActivityDistrict		= StudentActivityDistrict

		SchoolActivityAccountRel
			one-to-many relation to SchoolActivityAccount
			Field Mapping uses symbolic key
				related.StudentActivityDistrict		= StudentActivityDistrict
				related.StudentActivitySchool		= StudentActivitySchool

		SchoolActivityAccountBalanceRel
			one-to-many relation to SchoolActivityAccountBalance
			Field Mapping uses ByStudentActivityAccount
				related.StudentActivityDistrict		= StudentActivityDistrict
				related.StudentActivitySchool		= StudentActivitySchool
				related.StudentActivityClosedPeriod	= StudentActivityClosedPeriod









	Conditions



	Field Rules
		CloseDate	
			default to current corporate date
		
	Actions
  		DeleteIfNoDetailsExist is an Instance Action
  			restricted
  			Action Rules
  				if (!StudentActivityBalanceDetail set exists)
  					invoke Delete

		Create is a Create Action
			restricted
										
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted							
