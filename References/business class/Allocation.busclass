Allocation is a BusinessClass
    owned by Allocations
    prefix is ALLC

    Ontology
    	symbolic key is Allocation

    Persistent Fields

        Description
        DefaultStep							is an AllocationStep

        Ledger								
        SourceLedger						is a Ledger
        SourceSystemCode					is a GeneralLedgerSystemCode

        IsReleased							is Boolean 
        AllowMultiplePostings				is Boolean
        AllocationGroup

		AccountingUnitStructure
		FinanceDimension1Structure
		FinanceDimension2Structure
		FinanceDimension3Structure
		FinanceDimension4Structure
		FinanceDimension5Structure
		FinanceDimension6Structure
		FinanceDimension7Structure
		FinanceDimension8Structure
		FinanceDimension9Structure
		FinanceDimension10Structure
		ProjectStructure
		AccountStructure					is a ReportingChart
		GeneralLedgerEvent
			default label is "GlobalLedgerEvent"
		CashOffsetAccount					is a GeneralLedgerChartAccount		
		AutoReverse							is Boolean

		CustodialAccountInterest			is Boolean 
		CustodialAccountBasis				is a ReportingBasis
			default label is "CustodialAccountADBBasis"
		OverthrownAmountOption				is Numeric 1
			States
				LastTransaction			value is 0
				LastHighestWeight		value is 5
		
	Context Fields
		AllocationRunContext				is a AllocationRun

	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforFinancialAllocation"

		ContextMessageText is a MessageField
			restricted
			"Allocation<Allocation>"

		DerivedCustodialAccountInterestEvent is a DerivedField
			type is like GeneralLedgerEvent
			return "INT"

		DerivedTreatBlankAccountAsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.AccountLabel>AsEmpty"
		DerivedTreatBlankDimension1AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension1Label>AsEmpty"
		DerivedTreatBlankDimension2AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension2Label>AsEmpty"
		DerivedTreatBlankDimension3AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension3Label>AsEmpty"
		DerivedTreatBlankDimension4AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension4Label>AsEmpty"
		DerivedTreatBlankDimension5AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension5Label>AsEmpty"
		DerivedTreatBlankDimension6AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension6Label>AsEmpty"
		DerivedTreatBlankDimension7AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension7Label>AsEmpty"
		DerivedTreatBlankDimension8AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension8Label>AsEmpty"
		DerivedTreatBlankDimension9AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension9Label>AsEmpty"
		DerivedTreatBlankDimension10AsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.FinanceDimension10Label>AsEmpty"
		DerivedTreatBlankProjectAsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.ProjectLabel>AsEmpty"
		DerivedTreatBlankAccountingUnitAsEmpty is a MessageField
			"TreatBlank<FinanceEnterpriseGroup.AccountingUnitLabel>AsEmpty"

		DerivedUseCapacityLedger is a DerivedField
			type is Boolean
			return AllocationRunRel.UseCapacityLedger
		DerivedLedger			is a DerivedField
			type is AlphaUpper 20
			return Ledger
		
		DerivedCompositeFormTitle			is a DerivedField
			type is MessageField
			if (Description entered)
				return DerivedAllocationDescription
			else
				return DerivedAllocationDefaultTitle

		DerivedAllocationDefaultTitle is a MessageField
			restricted
			"Allocation<Allocation>"		
		DerivedAllocationDescription is a MessageField
			restricted
			"Allocation<Allocation>_-_<Description>"
			
        DerivedAllocationCopyInitialValue is a MessageField
            restricted
            "<Allocation>\_\COPY"

        DerivedDescriptionCopyInitialValue is a MessageField
            restricted
            "<Description>_COPY"

		AllocationMultiplePostingsErrorMessage is a MessageField
			restricted
			"AllowMultiplePostingsNotEnabledForAllocation<Allocation>-<Description>"

		AllocationMultiplePostingsError is a DerivedField
			type is MessageField
			restricted
			JournalizeLog.AllocationPeriod	= AllocationRunContext.AllocationPeriod
			JournalizeLog.IsCapacity	 	= AllocationRunContext.UseCapacityLedger
			if (!AllowMultiplePostings and JournalizeLog.Count != 0)
				return AllocationMultiplePostingsErrorMessage
			return blank

	Field Rules
		Ledger
			required
			if (AllocationControl.Type.Budget)
				default to FinanceEnterpriseGroup.CoreLedger
			constraint (!Ledger.CurrencyLedger)
				"CannotUseLedger<Ledger>"
		SourceLedger
			default to Ledger
			required
		GeneralLedgerEvent
			initial value is "JE"
			if (CustodialAccountInterest)
				force default to DerivedCustodialAccountInterestEvent
			else
				default to "JE"			


		CashOffsetAccount				
			constraint (FinanceEnterpriseGroup.FundAccounting)
				"CashOffsetAccountOnlyValidWithFundAccounting"
			constraint (CashOffsetAccount.AccountSubType	= "CASH")
				"CashOffsetAccountMustBe_CASH_Subtype"
			if (CustodialAccountInterest)
				required

		CustodialAccountBasis
			if (CustodialAccountInterest)
				required

		CustodialAccountInterest
			cannot be changed

	Conditions
		RecordExists
			restricted
			when (Allocation exists)
#ifdef module cam 			
		CustodialAllowed
			restricted
			when (FinanceEnterpriseGroup.CustodialAccountManagement and AllocationControl.Type.Actuals)
#endif			
	Local Fields
		JournalizeLog						is an AllocationJournalizeLog
		LocalNewAllocation					is like Allocation
		LocalNewAllocationControl			is like AllocationControl

	Relations
		AllocationLinesRel is a AllocationLine set
		ActiveAllocationLinesRel
			one-to-many relation to AllocationLine
			Field Mapping uses ByStepNumber
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
    			related.AllocationSourceSystem		= AllocationSourceSystem
				related.AllocationControl		 	= AllocationControl
				related.Active						= true
			Instance Selection
				where (related.Allocation		= Allocation)

		DuplicateAllocationRel
    		one-to-one relation to Allocation
    		Field Mapping uses symbolic key
    			related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
    			related.AllocationSourceSystem	= AllocationSourceSystem
    			related.AllocationControl		= LocalNewAllocationControl
    			related.Allocation				= LocalNewAllocation

		AllocationRunRel
			one-to-one relation to AllocationRun
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	 = FinanceEnterpriseGroup
				related.AllocationRun			 = AllocationRunContext
	Actions
 		Create is an Action
			Field Rules
			
			Action Rules
#ifdef module cam			

				if (not FinanceEnterpriseGroup.CustodialAccountManagement)
					constraint (not CustodialAccountInterest)
						"CustodialAccountManagementIsNotEnabled"
#endif
			Exit Rules

    	Update is an Update Action
			Field Rules

    		Exit Rules

    	Delete is a Delete Action

    	CopyAllocation is an Instance Action
    		disable multiple instance selection
            Parameters
                PrmAllocationControl					is an AllocationControl
                	default label is "AllocationControl"
                PrmNewAllocation						is like Allocation
                	default label is "NewAllocation"
                PrmNewDescription						is a Description
                	default label is "NewDescription"
                PrmNewLedger							is a Ledger
                	default label is "Ledger"
				PrmNewDefaultStep						is an AllocationStep
					default label is "DefaultStep"
				PrmNewAllocationGroup					is an AllocationGroup
					default label is "AllocationGroup"
				PrmAllowMultiplePostings				is Boolean
					default label is "AllowMultiplePostings"
				PrmNewGeneralLedgerEvent				is a GeneralLedgerEvent
					default label is "GlobalLedgerEvent"

            Local Fields
            	LocalCreatedAllocationView				is an Allocation view

            Parameter Rules
				PrmAllocationControl
					required
                    initial value is AllocationControl

					if (AllocationControl.Type.Budget
					and AllocationControl.Scenario != PrmAllocationControl.Scenario)
						constraint (PrmAllocationControl.Scenario.Basis = AllocationControl.Scenario.Basis)
							"BudgetScenarioShouldHaveTheSameBasis"
						
						constraint (AllocationControl.Scenario.HasAccountingEntity       = PrmAllocationControl.Scenario.HasAccountingEntity
						and         AllocationControl.Scenario.HasPostingAccountingUnit  = PrmAllocationControl.Scenario.HasPostingAccountingUnit
						and         AllocationControl.Scenario.HasPostingAccount         = PrmAllocationControl.Scenario.HasPostingAccount
						and         AllocationControl.Scenario.HasPostingProject         = PrmAllocationControl.Scenario.HasPostingProject
						and         AllocationControl.Scenario.HasPostingDimension1      = PrmAllocationControl.Scenario.HasPostingDimension1
						and         AllocationControl.Scenario.HasPostingDimension2      = PrmAllocationControl.Scenario.HasPostingDimension2
						and         AllocationControl.Scenario.HasPostingDimension3      = PrmAllocationControl.Scenario.HasPostingDimension3
						and         AllocationControl.Scenario.HasPostingDimension4      = PrmAllocationControl.Scenario.HasPostingDimension4
						and         AllocationControl.Scenario.HasPostingDimension5      = PrmAllocationControl.Scenario.HasPostingDimension5
						and         AllocationControl.Scenario.HasPostingDimension6      = PrmAllocationControl.Scenario.HasPostingDimension6
						and         AllocationControl.Scenario.HasPostingDimension7      = PrmAllocationControl.Scenario.HasPostingDimension7
						and         AllocationControl.Scenario.HasPostingDimension8      = PrmAllocationControl.Scenario.HasPostingDimension8
						and         AllocationControl.Scenario.HasPostingDimension9      = PrmAllocationControl.Scenario.HasPostingDimension9
						and         AllocationControl.Scenario.HasPostingDimension10     = PrmAllocationControl.Scenario.HasPostingDimension10)
							"NewScenarioIsNotCompatibleWithTheOldScenario"
						
				PrmNewAllocation
					required
                    initial value is DerivedAllocationCopyInitialValue
					LocalNewAllocationControl			= PrmAllocationControl
					LocalNewAllocation 					= PrmNewAllocation
					constraint (DuplicateAllocationRel not exists)
    					"AllocationAlreadyExists"
    			PrmNewDescription
                    initial value is DerivedDescriptionCopyInitialValue
    			PrmNewLedger
    				initial value is Ledger
				PrmNewDefaultStep
					initial value is DefaultStep
				PrmNewAllocationGroup
					initial value is AllocationGroup
				PrmAllowMultiplePostings
					initial value is AllowMultiplePostings
				PrmNewGeneralLedgerEvent
					initial value is GeneralLedgerEvent

            Action Rules
                invoke Create Allocation
                	assign result to LocalCreatedAllocationView
                	fill in fields from this instance
                    invoked.AllocationControl			= PrmAllocationControl
                    invoked.Allocation      			= PrmNewAllocation
                    invoked.Description					= PrmNewDescription
                    invoked.Ledger						= PrmNewLedger
					invoked.DefaultStep					= PrmNewDefaultStep
					invoked.AllocationGroup				= PrmNewAllocationGroup
					invoked.AllowMultiplePostings		= PrmAllowMultiplePostings
					invoked.GeneralLedgerEvent			= PrmNewGeneralLedgerEvent

                for each AllocationLinesRel
					invoke CopyAllocationLine each
						invoked.PrmAllocationControl	= PrmAllocationControl
						invoked.PrmAllocation			= LocalCreatedAllocationView.Allocation
						invoked.PrmNewAllocationLine	= each.AllocationLine
						invoked.PrmNewDescription		= each.Description

		Journalizing is an Instance Action
			restricted
			Parameters
				PrmAllocationPeriod is a GeneralLedgerCalendarPeriod
					default label is "AllocationPeriod"
				PrmJournalizeGroup	is a JournalizeGroup
					default label is "JournalizeGroup"
				PrmIsCapacity		is Boolean
					default label is "IsCapacity"
			Parameter Rules
				PrmAllocationPeriod
					required
				PrmJournalizeGroup
					required
			Action Rules

				JournalizeLog.AllocationPeriod	= PrmAllocationPeriod
				JournalizeLog.IsCapacity	 	= PrmIsCapacity
				invoke Update JournalizeLog
					invoked.Count = JournalizeLog.Count + 1
				if (!AllowMultiplePostings)
					constraint (JournalizeLog.Count = 1)
						"<AllocationMultiplePostingsErrorMessage>"

		RevertJournalize is an Instance Action
			restricted
			Parameters
				PrmAllocationPeriod is a GeneralLedgerCalendarPeriod
					default label is "AllocationPeriod"
				PrmJournalizeGroup	is a JournalizeGroup
					default label is "JournalizeGroup"
				PrmIsCapacity		is Boolean
					default label is "IsCapacity"
			Parameter Rules
				PrmAllocationPeriod
					required
				PrmJournalizeGroup
					required
			Action Rules
				display "RevertJournalize<Allocation>"
				JournalizeLog.AllocationPeriod	= PrmAllocationPeriod
				JournalizeLog.IsCapacity	 	= PrmIsCapacity
				invoke Update JournalizeLog
					invoked.Count = JournalizeLog.Count - 1
				if (JournalizeLog exists
				and JournalizeLog.Count <= 0)
					invoke Delete JournalizeLog

    	Run is an Instance Action
    		completion message is "AllocationRun<AllocationControl.AllocationRun>Initiated"
			Parameters
				PrmAllocation			is a Allocation
					default label is "Allocation"
				PrmGroup				is a AllocationGroup
					default label is "Group"
				PrmDate					is Date
					default label is "Date"
				PrmFromStep				is a AllocationStepNumber
					default label is "FromStep"
				PrmToStep				is a AllocationStepNumber
					default label is "ToStep"
				PrmGenerateListingData	is Boolean
					default label is "GenerateReports"
				PrmJournalize			is Boolean
					default label is "JournalizeIfComplete"
				PrmGenerateSourceValue	is Boolean
					default label is "IncludeSourceDetails"
				PrmBypassCheckAllocationLine	is Boolean
				PrmNumberOfBatches		is Numeric size 3
					default label is "NumberOfBatches"
			Parameter Rules
				PrmAllocation
                    initial value is Allocation
					constraint(PrmGroup not entered)
						"CannotSpecifyBothGroupAndAllocation"
				PrmGroup
					constraint(PrmAllocation not entered)
						"CannotSpecifyBothGroupAndAllocation"
				PrmDate
					initial value is current corporate date
					default to current corporate date
					required
				PrmToStep
					default to 100
					constraint (PrmToStep <= 100)
						"StepCannotExceed100"
					constraint (PrmToStep >= PrmFromStep)
						"ToStepMustBeGreaterThanOrEqualToFromStep"
				PrmGenerateSourceValue
					if (not PrmGenerateListingData)
						PrmGenerateSourceValue = false

				PrmBypassCheckAllocationLine
					if (AllocationControl.Type.Budget)
						PrmBypassCheckAllocationLine = true

				PrmJournalize
					if (AllocationControl.Type.Budget)
						initialize PrmJournalize
				PrmNumberOfBatches
					initial value is AllocationControl.NumberOfBatches
					constraint (PrmNumberOfBatches <= 100)
						"NumberOfBatchesCannotExceed100"
            Exit Rules
                invoke Run AllocationControl
                    invoked.PrmAllocation                   = PrmAllocation
                    invoked.PrmGroup                        = PrmGroup
                    invoked.PrmDate                         = PrmDate
                    invoked.PrmFromStep                     = PrmFromStep
                    invoked.PrmToStep                       = PrmToStep
                    invoked.PrmJournalize                   = PrmJournalize
                    invoked.PrmGenerateListingData          = PrmGenerateListingData
                    invoked.PrmGenerateSourceValue          = PrmGenerateSourceValue
                    invoked.PrmNumberOfBatches              = PrmNumberOfBatches
                    invoked.PrmBypassCheckAllocationLine    = PrmBypassCheckAllocationLine
