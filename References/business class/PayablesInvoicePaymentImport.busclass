PayablesInvoicePaymentImport is a BusinessClass
    owned by ap
    prefix is CVP
    classic name is APCPAYMENT

    Ontology
        symbolic key is PayablesInvoicePaymentImport
            classic set name is CVPSET1
            classic name is SEQ-NBR


    Patterns
    	implements InlineUserFields
			size is 1000
        implements StaticJava        
		enable explicit context override        
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
		RunGroup
		Company 			   is a PayablesCompany
		Vendor
		EDINumber
		Invoice
		Suffix
		Sequence 			   is Numeric 6
        OldVendor
        DueDate
        PaymentAmount          is an InternationalAmount
            classic name is TRAN-PMT-AMT
        DiscountAmount         is an InternationalAmount
            classic name is TRAN-DISC-AMT
        WithholdingAmount      is an InternationalAmount
            classic name is TRAN-INC-WH
        ReportableIncomeAmount is an InternationalAmount
            classic name is TRAN-INC-AMT
        DiscountPercent
            classic name is DISCOUNT-RT
        DiscountDate           is Date
            classic name is DISC-DATE
        Status                 is Numeric size 1
            classic name is REC-STATUS
            States
                Unreleased value is 0
                Open       value is 1
                History    value is 9
        TransactionNumber
            classic name is TRANS-NBR
        ImportBankTransactionCode is like BankTransactionCode
            classic name is BANK-INST-CODE
        CheckDate              is Date
        DiscountLost           is Boolean
            classic name is DISC-LOST-FLG
        CashCode
		SeparatePayment			is a BooleanOverride		
            classic name is SEP-CHK-FLAG
		PayImmediately			is a BooleanOverride		
            classic name is PAY-IMM-FLAG
        InvoiceGroup			is a PayablesInvoiceGroup	
		Enclosure				is a BooleanOverride		
        IncomeCode             is a PayablesIncomeCode
        BankCheckAmount        is an InternationalAmount
            classic name is BANK-CHK-AMT
        BankNumberOfDecimals   is an AcctNd
            classic name is BANK-ND
        BankConversionRate     is an AcctRate
            classic name is BNK-CNV-RATE
        IncomeWithholdingCode
            classic name is INC-ACCR-CODE
        RecordInError          is Boolean
            classic name is RECORD-ERROR
		VendorPaymentAmount		is an InternationalAmount		
		VendorPaymentCurrency	is a  Currency

	Context Fields
		ContextInvoiceImport is a PayablesInvoiceImport
		
	Field Rules
		RunGroup
			required
			default to PayablesInvoiceImportRel.RunGroup
			initial value is ContextInvoiceImport.RunGroup

		Invoice
			initial value is ContextInvoiceImport.Invoice
		
		EDINumber
			initial value is ContextInvoiceImport.EDINumber

		OldVendor
			initial value is ContextInvoiceImport.OldVendor
			
		Suffix
			initial value is ContextInvoiceImport.Suffix
				
		Sequence
			autosequence using ByRunGroup

	Relations
        PayablesInvoiceImportRunGroupRel
            one-to-one relation to PayablesInvoiceImport
            Field Mapping uses ByRunGroup
				related.RunGroup						= RunGroup
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
	
        PayablesInvoiceImportRel
            one-to-one relation to PayablesInvoiceImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
	

	Sets
        ByCompanyVendorInvoice
            Sort Order
                Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				Sequence

        ByRunGroup
            Sort Order
                RunGroup
                Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				Sequence

	Create Rules
		constraint (PayablesInvoiceImportRel exists)
			"InterfaceInvoiceDoesNotExist"
			
	Actions
		Create is a Create Action
		
		Update is an Update Action
			Action Rules
		
		FastUpdate is an Update Action
			restricted
			bypass field rules

		Delete is a Delete Action
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		InterfaceEdits is an Instance Action
			Entrance Rules
				if (PayablesInvoiceImportRel.Status.Unreleased)
					constraint (Status.Unreleased)
						"PaymentMustBeUnreleasedIfInvoiceUnreleased"
					constraint (!Status.History)
						"HistoricalPaymentsNotAllowedForUnreleasedInvoice"
				else
				if (PayablesInvoiceImportRel.Status.Open)
					constraint (!Status.Unreleased)
						"PaymentMustBeOpenOrHistoricalIfInvoiceIsReleased"


				else
				if (PayablesInvoiceImportRel.Status.History)
					constraint (Status.History)
						"PaymentMustBeHistoricalIfInvoiceIsHistorical"
		
		                
