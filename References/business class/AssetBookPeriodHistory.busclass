AssetBookPeriodHistory is a BusinessClass
    owned by am
    prefix is ABX
    classic name is AMBKHISTX

    Ontology
        symbolic key is AssetBookPeriodHistory
            classic set name is ABXSET1
            classic name for YearEndDate is YR-END-DATE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        CurrentPeriodDepreciation    is an InternationalAmount
            classic name is DEPR-CP
            disable Auditing
        LifeRemaining
            classic name is LIFE-REM
            disable Auditing
        DepreciationMethodSwitched   is Boolean
            classic name is METH-SWITCHED
        Basis                        is an InternationalAmount
            classic name is BOOK-BASIS
            disable Auditing
        YearToDatePeriodDepreciation is an InternationalAmount
            classic name is PD-DEPR-YTD
            disable Auditing

    Derived Fields

        PeriodDepreciationLifeToDate is a DerivedField	
            type is like InternationalAmount
            restricted 			
            classic name is PD-DEPR-LTD
            return (AssetBookHistoryRel.BeginningLifeToDateDepreciation + YearToDatePeriodDepreciation)

        BookValue                    is a DerivedField	
            type is like InternationalAmount
            restricted 			
            return (Basis - PeriodDepreciationLifeToDate)

    Conditions

        AssetBookTransactionsExist
        	restricted
            classic name is BK-TRANS-EXIST
            when (first AssetBookTransactionsRel exists)

        AssetTransactionsExist
        	restricted
            classic name is TRANS-EXIST
            when (first ReleasedTransactionsRel exists)

    Relations

        AssetBookRel
            classic name is AMASTBOOK
            one-to-one relation to AssetBook
            required
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= Asset.FinanceEnterpriseGroup
                related.Asset 					= Asset
                related.Book 					= Book

        AssetBookHistoryRel
            classic name is AMBKHIST
            one-to-one relation to AssetBookHistory
            required
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= Asset.FinanceEnterpriseGroup
                related.Asset			    	= Asset
                related.Book        			= Book
                related.Company     			= Company
                related.YearEndDate 			= YearEndDate

        AssetBookTransactionsRel
            classic name is AMBKTRANS
            one-to-many relation to AssetBookTransaction
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup		= Asset.FinanceEnterpriseGroup
                related.Asset					 	= Asset
                related.Book  						= Book
                related.Company                     = Company
            Instance Selection
                where (related.TransactionStatus.History
                and related.YearEndDate = YearEndDate)    

        AssetTransactionsRel
            classic name is AMTRANS
            one-to-many relation to AssetTransaction
            Field Mapping uses Set1
                related.Asset                       = Asset
                related.TransactionStatus 			= blank
                related.Company                    	= Company
                related.Book                       	= Book
		
        ReleasedTransactionsRel
            one-to-many relation to AssetTransaction        
            Field Mapping uses ByCompanyBookAssetAccountType
				related.Company                    	= Company
				related.Book                       	= Book
				related.Asset                       = Asset
            Instance Selection
                where ((related.TransactionStatus.Released
                or related.TransactionStatus.History)
                and related.YearEndDate = YearEndDate)
    Sets

        Set2
            indexed
            Sort Order
                Book
                Asset
                Company
                YearEndDate
                AssetBookPeriodHistory

        Set3
            indexed
            Sort Order
                Asset
                Book
                YearEndDate
                AssetBookPeriodHistory
                Company

        Set4
            indexed
            Sort Order
                Asset
                Book
                Company
                YearEndDate
                AssetBookPeriodHistory.Period descending
	Actions
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Delete is a Delete Action
			restricted
			
		Purge is a Purge Action
			bypass relational integrity rules
			restricted
		
		AssetBookPeriodHistoryPurge is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
				PrmCompany						is an AssetCompany
				PrmAsset						is an Asset
				PrmBook							is a Book
				PrmPurgeOption					is Numeric 1
					default label is "ActionOption"
					States
						PurgeRecords 	value is 1
						ReportOnly		value is 2
			Parameter Rules
			Local Fields
				LocalCounter					is Numeric 10
			Instance Selection
				include deleted records
				where(Company									= PrmCompany
				and	  (PrmAsset not entered 		or  Asset	= PrmAsset)
				and	  Book										= PrmBook)
			Action Rules
				Set Rules
					Exit Rules

				Instance Rules

					if(PrmPurgeOption.PurgeRecords)
						invoke Purge
						
		PurgeDisposedAssetBookPeriodHistory is a Set Action
			restricted
			Parameters
				PrmCompany					is an AssetCompany
				PrmAsset					is an Asset
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmLeaseCompany				is like GeneralLedgerCompany
				PrmLocationName				is like AssetLocation
				PrmDivision					is like AssetDivision
				PrmAssetGroup				is like AssetGroup
				PrmFromDate					is Date
				PrmToDate					is Date
				
			Instance Selection
				include deleted records
				where (Asset.Company = PrmCompany
				and   (PrmAsset not entered or (Asset = PrmAsset and Asset.Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup))
				and   (PrmLeaseCompany not entered or Asset.AssetLease.LeaseCompany = PrmLeaseCompany)
				and   (PrmLocationName not entered or Asset.AssetLocation = PrmLocationName)
				and   (PrmDivision not entered or Asset.AssetDivision = PrmDivision)
				and   (PrmAssetGroup not entered or Asset.AssetGroup = PrmAssetGroup)
				and   (PrmFromDate not entered or Asset.AssetDisposalDate >= PrmFromDate)
				and   (PrmToDate not entered or Asset.AssetDisposalDate <= PrmToDate)
				and   Asset.Status.Disposed)
				
			Action Rules
				Instance Rules
					invoke Purge
