SupplierQuestions is a BusinessClass
    owned by procurement
    prefix is SUPQS

    Ontology
		symbolic key is SupplierQuestions

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Persistent Fields
		QuestionText					is Text
			sql name is SGQUE
			text searchable
   		ResponseType					is Numeric size 1
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
   				List 				value is 4 
   				YesNo 				value is 5
   				YesNoText			value is 6  
   		Attachment
   		DisplayOrder
   		Active							is Boolean
		LastQuestionValueDisplayOrder	is Numeric size 6
    	Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion             is like Question
		SupplierGroupQuestion
    	ResponseRules            is Numeric size 1
            States
                NotRequired                             value is 1
                ResponseRequired                        value is 2
                ResponseRequiredAndAttachmentRequired   value is 3
        YesNoResponseRules       is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                 value is 1
                ResponseRequired                            value is 2
                ResponseRequiredAndAttachmentRequired       value is 3
                ResponseRequiredAndAttachmentRequiredIfYes  value is 4
                ResponseRequiredAndAttachmentRequiredIfNo   value is 5
        YesNoTextResponseRules   is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                            value is 1
                ResponseRequiredAndTextResponseRequired                value is 2
                ResponseRequiredAndTextOrAttachmentRequired            value is 3
                ResponseRequiredAndTextOrAttachmentRequiredIfYes       value is 4
                ResponseRequiredAndTextOrAttachmentRequiredIfNo        value is 5
                ResponseRequiredAndTextAndAttachmentRequired           value is 6
                ResponseRequiredAndTextAndAttachmentRequiredIfYes      value is 7
                ResponseRequiredAndTextAndAttachmentRequiredIfNo       value is 8
		AllowResponseAttachment			is Boolean
		OnlyRequiredForFuture           is Date
			default label is "OnlyRequiredForSuppliersWhoRegisterAfterThisDate"
		
	Context Fields
		ContextSupplier					is a Supplier

	Transient Fields
		NewDisplayOrder					is a DisplayOrder

	Local Fields
		LocalConditionalQuestion		is like Question
		LocalListValue					is like SupplierGroupQuestionValue 
		LocalYesOrNo					is Numeric 1 
           	States
           		Yes						value is 1
           		No						value is 2 
		LocalQuestion 					is a SupplierQuestions

 	Derived Fields
		SupplierResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (HasAnswer)
				SupplierQuestionsSetResponseRel.SupplierResponseAnswers   	
   			else
   				blank

		BuyerSupplierResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (BuyerHasAnswer)
				BuyerSupplierQuestionsSetResponseRel.SupplierResponseAnswers   	
   			else
   				blank

 		UnansweredRequiredQuestion is a ConditionalField
 			type is Alpha size 2
 			restricted
   			if (RequiredQuestionNoAnswer)
   				"*"
			else
				blank

		DerivedMissingRequiredInformation is a ConditionalField
			type is Alpha 2
			if (MissingRequiredInformation)
				"*"
			else
				blank
				
		DerivedQuestionText is a DerivedField
			type is Alpha 250
			return QuestionText

		ResponseIsRequiredMessage is a MessageField
			"_ResponseIsRequired"
			
		RequiredQuestionMessage is a MessageField
			restricted
			"RequiredQuestionWithNoAnswer"

		QuestionMissingMessage is a MessageField
			restricted
			"QuestionMissingRequiredAttachment"
		
		DerivedMissingInformationAlert is a DerivedField
			type is like Description
			restricted
			if (RequiredQuestionNoAnswer)
				return RequiredQuestionMessage
			else
			if (MissingRequiredAttachment)
				return QuestionMissingMessage
			else
				return ""

   	Conditions
		QuestionExists
			restricted
			when (SupplierQuestions entered)
		
		RepositoryQuestionExists
			restricted
			when (Question entered)
		
		IsYesNo
			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)

		IsYesNoText 
			restricted 
			when (ResponseType.YesNoText)

		IsYesNoOnly 
			restricted 
			when (ResponseType.YesNo)

		HasDirectConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)

		HasAttachment
			restricted
			when (Attachment entered)

		AnswerHasAttachment
			restricted
			when (SupplierQuestionsSetResponseRel.Attachment entered)

		BuyerAnswerHasAttachment
			restricted
			when (BuyerSupplierQuestionsSetResponseRel.Attachment entered)

		BuyerHasAnswer
			restricted
			when (BuyerSupplierQuestionsSetResponseRel exists)

		HasAnswer
			restricted
			when (SupplierQuestionsSetResponseRel exists)

		AnyResponses
			restricted
			when (AnyQuestionResponsesRel exists)
		
		BuyerDisplayResponseRequiredText 
			restricted
			when (BuyerRequiredQuestionNoAnswer
			or    BuyerMayRequireAttachmentNoAttachment)

		DisplayResponseRequiredText 
			restricted
			when (RequiredQuestionNoAnswer
			or    MayRequireAttachmentNoAttachment)

		RequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and   !HasAnswer)
			
		BuyerRequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and   !BuyerHasAnswer)

		MayRequireAttachmentNoAttachment 
			restricted 
			when (MayRequireResponseAttachment
			and  !ResponseAttachmentExists)

		BuyerMayRequireAttachmentNoAttachment 
			restricted 
			when (MayRequireResponseAttachment
			and  !BuyerAnswerHasAttachment)

		ResponseAttachmentExists
			restricted
			when (SupplierQuestionsSetResponseAttachmentRel exists)

		MissingRequiredAttachment
			restricted
			when (AlwaysRequireResponseAttachment
			and	  !ResponseAttachmentExists)

		MissingRequiredInformation
			restricted
			when (RequiredQuestionNoAnswer
			or	  MissingRequiredAttachment)
			
		ResponseRequired
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    ResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        MayRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        AlwaysRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
            
        NonYesNoResponseType
        	restricted
            when (ResponseType.Date
            or    ResponseType.List
            or    ResponseType.Text
            or    ResponseType.Number)

        SuppliersExist
        	restricted
        	when (ActiveSuppliersRel exists)     

		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)

		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)

		AnyQuestionResponses
			restricted
			when (AnyQuestionResponsesRel exists)

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)

		CanCreateConditionalQuestion
			restricted 
			when (YesNoResponseType
			and  !QuestionIsAnswered)
		
		CanCreateConditionalListQuestion
			restricted 
			when (ResponseType.List
			and  !QuestionIsAnswered)

		QuestionIsAnswered
			restricted
			when (SupplierQuestionsSetResponseRel exists)

  	Relations
  		AnyQuestionResponsesRel	is a SupplierQuestionsSetResponse set
  		
  		ActiveSuppliersRel 
  			one-to-many relation to Supplier
  			Field Mapping uses symbolic key
  				related.SupplierGroup          = SupplierGroup
  			Instance Selection
  				where (related.Active)
  		
  		SupplierQuestionsSetResponseRel
  			one-to-one relation to SupplierQuestionsSetResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= actor.agent(SupplierSourceId).Supplier
            	related.SupplierQuestions		= SupplierQuestions

  		BuyerSupplierQuestionsSetResponseRel
  			one-to-one relation to SupplierQuestionsSetResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= ContextSupplier
            	related.SupplierQuestions		= SupplierQuestions

        SupplierQuestionsSetResponseAttachmentRel
  			one-to-many relation to SupplierQuestionsSetResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= actor.agent(SupplierSourceId).Supplier
            	related.SupplierQuestions		= SupplierQuestions
        	Instance Selection
        		where (related.Attachment entered)

		SameSupplierQuestionsRel 
			one-to-many relation to SupplierQuestions 
			Field Mapping uses symbolic key
				related.SupplierGroup		= SupplierGroup
				related.Supplier			= Supplier 
				related.SupplierQuestions	= LocalConditionalQuestion 

		QuestionListValueRel 
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key
				related.ProcurementGroup		= Supplier.SupplierGroup 
				related.Question				= LocalQuestion

		ConditionalQuestionsDirectRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup = Supplier.SupplierGroup 
				related.Question         = SupplierQuestions 		

		CreatedFromThisQuestionRel 
			one-to-many relation to SupplierGroupQuestion 
			Field Mapping uses symbolic key  
				related.SupplierGroup		= SupplierGroup
			Instance Selection 
				where (related.CreatedFromQuestion	= Question)			

		CreatedFromQuestionRel 
			one-to-one relation to SupplierQuestions 
			Field Mapping uses symbolic key 
				related.SupplierGroup		= SupplierGroup
				related.Supplier            = Supplier 
				related.SupplierQuestions	= CreatedFromQuestion

	Sets
		ByDisplayOrder
			duplicates
			Sort Order
				SupplierGroup
 				Supplier
 				DisplayOrder
 				SupplierQuestions
 				
		BySupplierGroupQuestion
			duplicates
			Sort Order
				SupplierGroup
 				Supplier
 				SupplierGroupQuestion

		ByQuestion
			duplicates
			Sort Order
				SupplierGroup
				Question
 				Supplier

		BySupplierGroupQuestions
			duplicates
			Sort Order
				SupplierGroup
 				SupplierGroupQuestion

		BySupplierQuestion
			Sort Order
				Supplier
				SupplierQuestions
 				
   	Field Rules
   		QuestionText
   			required
   		ResponseType
   			required
		Active
			default to true
		ResponseRules
            if (NonYesNoResponseType)
                required
                    "MustSelectAResponseRule"
                YesNoResponseRules = 0
                YesNoTextResponseRules = 0
                    
        YesNoResponseRules
            if (ResponseType.YesNo)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoTextResponseRules= 0
        
        YesNoTextResponseRules
            if (ResponseType.YesNoText)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoResponseRules= 0
		AllowResponseAttachment
			if (AllowResponseAttachment changed
			and AllowResponseAttachment = false)
				constraint (!ResponseAttachmentExists)
					"CannotChangeAllowAttachmentUploadWithAnswerFlag;SupplierQuestionAnswersExistWithAttachments"
			if (MayRequireResponseAttachment)
				AllowResponseAttachment = true

	Actions 
		Create is a Create Action
			restricted
			Field Rules
	 			DisplayOrder
	 				autosequence using SupplierGroup.LastSupplierGroupQuestionDisplayOrder
	
			Action Rules
			
				if (!ResponseRequired)
					OnlyRequiredForFuture = blank
					
		Update is an Update Action
			restricted
			Action Rules
				if (!ResponseRequired)
					OnlyRequiredForFuture = blank

				if (ResponseType changed)
					constraint (!AnyResponses)
						"CannotChangeQuestionTypeWhenAnswersExist"
			
		Delete is a Delete Action
			restricted

		DeleteConditionalSupplierQuestions is a Set Action  
			restricted 
			Parameters 
			 	ParmSupplierGroup		is a SupplierGroup
				ParmSupplier			is a Supplier 
			 	ParmOriginalQuestion    is a SupplierQuestions
				ParmLastOldQuestion     is a SupplierQuestions   

			Instance Selection 
			 	where (ParmSupplierGroup		= SupplierGroup
				and    ParmSupplier				= Supplier
			 	and    ParmOriginalQuestion		= CreatedFromQuestion
				and    SupplierQuestions		<= ParmLastOldQuestion)

			Action Rules
			 	Instance Rules 
					invoke Delete		

		DeleteSupplierQuestions is a Set Action  
			restricted 
			Parameters 
			 	PrmSupplierGroup			is a SupplierGroup
				PrmSupplierGroupQuestion	is a SupplierGroupQuestion 

			Instance Selection 
			 	where (PrmSupplierGroup			= SupplierGroup
				and    PrmSupplierGroupQuestion	= SupplierGroupQuestion)

			Action Rules
			 	Instance Rules 
					invoke Delete		

		NotifyIfRequiredQuestions is an Instance Action
			restricted
			default label is "EmailSuppliersWithRequiredQuestionsToAnswer"
			Action Rules
			
				invoke NotifyIfRequiredQuestions Supplier
					invoked.ParmSupplierGroup = SupplierGroup
