ExternalUserMigration is a BusinessClass
    owned by person
    prefix is EXUSM
    stored in environment

    Ontology
        symbolic key is ExternalUserMigration

    Patterns
    	implements CRUD
    	implements CreateStamp
    	implements UpdateStamp
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        disable Auditing
	
	Persistent Fields
		UserIdentity						is Alpha size 50
		IFSStatus							is Numeric size 1
			States
				Sent						value is 0				
				Success 					value is 1				
				Failed				 		value is 2				
				Skipped						value is 3				
		IFSMsg								is Text
		LMRKStatus							is Numeric size 1
			default label is "Landmark Status"
			States
				New							value is 0
				Success 					value is 1
				Failed				 		value is 2
		LMRKMsg								is Text
			default label is "Landmark Msg"
		ApplicationStatus					is Numeric size 1
			States
				New							value is 0
				Success 					value is 1
				Failed				 		value is 2
		ApplicationMsg						is Text
		ErrorMsg							is Text
	
	Field Rules
		IFSStatus
			initial value is 0
			default to 0
			
		LMRKStatus	
			initial value is 0
			default to 0
			
		ApplicationStatus
			initial value is 0
			default to 0
			
	Conditions
		IsFailed
			default label is "Failed"
			when (IFSStatus.Failed or IFSStatus.Skipped or LMRKStatus.Failed or ApplicationStatus.Failed)
			
		AppRecordNeedsUpdate
			default label is "AppRecordNeedsUpdate"
    		when (LMRKStatus.Success and ApplicationStatus.New)
	
	Relations
		ExternalUserMigrationActorRel
			one-to-one relation to Actor
			Field Mapping uses symbolic key
				related.Actor = ExternalUserMigration.Actor		
				
	Sets
		ByStamp
			indexed
			Sort Order
				create stamp
				ExternalUserMigration
		
		ByEmailAddress
			Sort Order
				ExternalUserMigrationProcess
				ExternalUserTransaction
				ExternalUserMigration.EmailAddress
				ExternalUserMigration.Actor
				
		UpdateAppRecordSet
			Sort Order
				ExternalUserMigrationProcess
				ExternalUserTransaction
			Instance Selection
				where (AppRecordNeedsUpdate)

	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		Purge is a Purge Action
		
		PurgeAll is a Set Action
			Action Rules
				Instance Rules
					invoke Purge				
