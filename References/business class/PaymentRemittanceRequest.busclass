PaymentRemittanceRequest is a BusinessClass
    owned by cxml

    prefix is Prr

    Ontology
        symbolic key is PaymentRemittanceRequest

    Patterns
        implements CRUD
        disable Auditing
        disable EffectiveDated

    Persistent Fields
        PayloadID               is Alpha 100
        ProcessStatus           is AlphaUpper size 10
            States
                New             value is "0"
                Error           value is "1"
                Success         value is "2"
                Warning         value is "3"
        StatusMessage           is Text
        LastUpdatedTimeStamp    is TimeStamp
        DocumentNumber          is AlphaUpper size 22
        RequestXML              is XMLDocument
            default label is "PaymentRemittanceRequest"

    Transient Fields

    Derived Fields

        FullHeaderTemplate is a DerivedField
            type is XMLDocument
        	restricted
            FullHeaderTemplate = template.AribaCXMLFullHeader_ST document for this instance

        LightHeaderTemplate is a DerivedField
            type is XMLDocument
        	restricted
            LightHeaderTemplate = template.AribaCXMLLightHeader_ST document for this instance

        PaymentRemittanceTemplate is a DerivedField
            type is XMLDocument
        	restricted
            PaymentRemittanceTemplate = template.AribaPaymentRemittance_ST document for this instance

        PaymentRemittanceDetailXML is a DerivedField
            type is XMLDocument
        	restricted
            PaymentRemittanceDetailXML = template.AribaPaymentRemittanceDetail_ST document for this instance

        ISODateTimeWithOffSet is a DerivedField       //TimeZone "2007-04-05T12:30-02:00".
            type is Alpha 25
        	restricted
            if (LocalISODateTime = "")
                LocalISODateTime = ISOCurrentTimeStampWithOffset


            if (LocalISODateTime[20] = "Z" )
                LocalISODateTime = LocalISODateTime[1:19] + "+00:00"

                if (LocalISODateTime[12:19] = "00:00:00" )
                    return LocalISODateTime[1:11] + "12:00:00" + LocalISODateTime[20:25]

            return LocalISODateTime[1:22] + ":" + LocalISODateTime[23:24]

        ISOCurrentTimeStampWithOffset is a DerivedField
            type is Alpha 25
            restricted
            if (CurrentTimeStamp = "")
                CurrentTimeStamp = current timestamp
            return (CurrentTimeStamp using"%1$tY-%1$tm-%1$tdT%1$tH:%1$tM:%1$tS%1$tz" )

        ISOInvoiceDate is a DerivedField
            type is Alpha size 10
            restricted
            return InvoiceDate using "%1$tY-%1$tm-%1$td"

        ISOPaymentDate is a DerivedField
            type is Alpha size 10
            restricted
            return PaymentDate using "%1$tY-%1$tm-%1$td"

        PayloadIDString is a StringField
            type is Alpha size 100
            DocumentNumber
            "-"
            ISODateTimeWithOffSet

        RemittanceID is a StringField
            type is Alpha size 100
        	restricted
            DocumentNumber
            "-"
            ISODateTimeWithOffSet

        Doctype is a StringField
            type is Alpha size 100
        	restricted
            "<!DOCTYPE cXML SYSTEM \"http://xml.cxml.org/schemas/cXML/"
            CxmlVersion
            "/PaymentRemittance.dtd\">"

        DerivePaymentIdentifier is a StringField
            type is AlphaUpper size 22
        	restricted
            Invoice
            "-"
            TransactionNumber

        VendorLocationAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine1
            " "
            VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine2
            " "
            VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine3
            " "
            VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine4

        VendorAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine1
            " "
            PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine2
            " "
            PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine3
            " "
            PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine4


        PayorAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            PaymentOutputFileDetailRel.PayorPostalAddress.DeliveryAddress.AddressLine1
            " "
            PaymentOutputFileDetailRel.PayorPostalAddress.DeliveryAddress.AddressLine2
            " "
            PaymentOutputFileDetailRel.PayorPostalAddress.DeliveryAddress.AddressLine3
            " "
            PaymentOutputFileDetailRel.PayorPostalAddress.DeliveryAddress.AddressLine4

        PayeeAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            PaymentOutputFileDetailRel.PayeePostalAddress.DeliveryAddress.AddressLine1
            " "
            PaymentOutputFileDetailRel.PayeePostalAddress.DeliveryAddress.AddressLine2
            " "
            PaymentOutputFileDetailRel.PayeePostalAddress.DeliveryAddress.AddressLine3
            " "
            PaymentOutputFileDetailRel.PayeePostalAddress.DeliveryAddress.AddressLine4


    Local Fields

        UserAgent               is Alpha 10
        FromDomain              is Alpha 20
        FromIdentity            is a NetworkID
        SenderDomain            is Alpha 20
        SenderIdentity          is a NetworkID
        ToDomain                is Alpha 20
        ToIdentity              is a NetworkID
        DeploymentMode          is Alpha size 10
        SharedSecret            is Alpha size 100
        SystemID                is a NetworkID
        CxmlVersion             is Alpha size 11
        WSIEndpoint             is Alpha size 1000

        VendorGroup
        Vendor
        VendorPurchaseFromLocation is like VendorLocation
        Invoice

        CurrentTimeStamp            is TimeStamp
        LineCount                   is Numeric size 6
        LocalISODateTime            is Alpha 25
        PurchasingCompany


        PayorName                   is a Name	 
        	holds pii
        PayorStreet                 is Alpha size up to 160
        PayorCity                   is Alpha size up to 58
        PayorState                  is AlphaUpper size 3
        PayorPostalCode             is Alpha size up to 12
        PayorCountry                is AlphaUpper size 3
        PayorCountryCode            is AlphaUpper size 2
        PayeeName                   is a Name	 
        	holds pii
        PayeeStreet                 is Alpha size up to 160
        PayeeCity                   is Alpha size up to 58
        PayeeState                  is AlphaUpper size 3
        PayeePostalCode             is Alpha size up to 12
        PayeeCountry                is AlphaUpper size 3
        PayeeCountryCode            is AlphaUpper size 2
        PaymentCurrency             is a  Currency

        PaymentReferenceNumber      is like TransactionNumber
        TransactionNumber
        PaymentDate                 is Date
        BankTransactionCode
        PaymentType                 is Alpha size 10
        PaymentNetAmount            is an InternationalAmount
        PaymentGrossAmount          is an InternationalAmount
        PaymentDiscountAmount       is an InternationalAmount
        InvoiceID                   is like Invoice
        InvoiceDate                 is like ExchangeDate
        RemittanceCurrency          is a  Currency
        RemittanceNetAmount         is an InternationalAmount
        RemittanceGrossAmount       is an InternationalAmount
        RemittanceDiscountAmount    is an InternationalAmount

        VendorName                  is a Name	 
        	holds pii
        VendorStreet                 is Alpha size up to 160
        VendorCity                  is Alpha size up to 58
        VendorState                 is AlphaUpper size 3
        VendorPostal                is Alpha size up to 12
        VendorCountry               is AlphaUpper size 3
        VendorCountryCode           is AlphaUpper size 2
        VendorEmail                 is an EmailAddressMulti 
        	holds pii
        BaseCXML                    is XMLDocument
        PaymentRemittanceXML        is XMLDocument
        
        LocalActor					is an Actor

    Relations

        PayablesInvoiceRel
            one-to-many relation to PayablesInvoice
            Field Mapping uses ByVendorInvoice
                related.Vendor  = Vendor
                related.Invoice = Invoice

        VendorLocationRel
            one-to-one relation to VendorLocation
            Field Mapping uses symbolic key
                related.VendorGroup                 = VendorGroup
                related.Vendor                      = Vendor
                related.VendorLocation              = VendorPurchaseFromLocation


        DispatchVendorRel
            one-to-one relation to PurchaseOrderDispatchVendor
            Field Mapping uses Set1
                related.Vendor                          = Vendor
                related.PurchaseFromLocation            = blank

        DispatchVendorLocationRel
            one-to-one relation to PurchaseOrderDispatchVendor
            Field Mapping uses Set1
                related.Vendor                          = Vendor
                related.PurchaseFromLocation            = VendorPurchaseFromLocation

        PurchasingVendorRel
            one-to-one relation to PurchasingVendor
            Field Mapping uses symbolic key
                related.VendorGroup                 = VendorGroup
                related.Vendor                      = Vendor

        PurchaseFromLocRel
            one-to-one relation to PurchaseFromLocation
            Field Mapping uses symbolic key
                related.VendorGroup                 = VendorGroup
                related.Vendor                      = Vendor
                related.PurchaseFromLocation        = VendorPurchaseFromLocation

        PaymentOutputFileDetailRel
            one-to-one relation to PaymentOutputFileDetail
            Field Mapping uses symbolic key
                related.CashManagementGroup         =   CashManagementGroup
                related.CashManagementAccount       =   CashManagementAccount
                related.PaymentOutputFileHeader     =   PaymentOutputFileHeader
                related.PaymentOutputFileDetail     =   PaymentOutputFileDetail

        PaymentOutputFileRemittanceRel
            one-to-many relation to PaymentOutputFileRemittance
            Field Mapping uses symbolic key
                related.CashManagementGroup         =   CashManagementGroup
                related.CashManagementAccount       =   CashManagementAccount
                related.PaymentOutputFileHeader     =   PaymentOutputFileHeader
                related.PaymentOutputFileDetail     =   PaymentOutputFileDetail

        PaymentMethodRel
            one-to-many relation to PaymentMethodMap
            Field Mapping uses ByBankTransactionCode
                related.BankTransactionCode = BankTransactionCode
                
		CXMLConfigurationRel
			one-to-many relation to CXMLConfiguration
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
			Instance Selection
				where (related.Default)

    Conditions

        IsAribaEnabled
            restricted
            when (PurchasingCompany.ProcurementGroup.AribaEnabled
            and  (PurchasingVendorIsAribaEnabled
            or   PurchasingLocationIsAribaEnabled))

        PurchasingVendorIsAribaEnabled
            restricted
            when (PayablesInvoiceRel.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel not exists
            and (PurchasingVendorHasAribaFullAccount
            or PurchasingVendorHasAribaLightAccount))

        PurchasingLocationIsAribaEnabled
            restricted
            when (PurchasingLocationHasAribaFullAccount
            or PurchasingLocationHasAribaLightAccount)

        PurchasingVendorHasAribaFullAccount
            restricted
            when ( PayablesInvoiceRel.PurchasingVendorRel.AribaEnabled.Integrated
            or PayablesInvoiceRel.PurchasingVendorRel.AribaEnabled.Flipper)

        PurchasingVendorHasAribaLightAccount
            restricted
            when ( PayablesInvoiceRel.PurchasingVendorRel.AribaEnabled.Light)

        PurchasingLocationHasAribaFullAccount
            restricted
            when (PayablesInvoiceRel.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel.AribaEnabled.Integrated
            or PayablesInvoiceRel.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel.AribaEnabled.Flipper)

        PurchasingLocationHasAribaLightAccount
            restricted
            when ( PayablesInvoiceRel.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel.AribaEnabled.Light)


		NotifyError
		    when ((CXMLConfigurationRel.NotificationLevel.Error
		    or CXMLConfigurationRel.NotificationLevel.Both)
		    and ProcessStatus.Error or ProcessStatus.Warning)
		NotifySuccess
		    when ((CXMLConfigurationRel.NotificationLevel.Success
		    or CXMLConfigurationRel.NotificationLevel.Both)
		    and ProcessStatus.Success)
		EmailNotification
		    when (CXMLConfigurationRel.NotificationType.Email
		    or CXMLConfigurationRel.NotificationType.Both)
		UserNotification
		    when (CXMLConfigurationRel.NotificationType.User
		    or CXMLConfigurationRel.NotificationType.Both)

    Rule Blocks
        ConvertPaymentType
            if (PaymentMethodRel exists)
                PaymentType = PaymentMethodRel.CXMLPaymentType
            else
                PaymentType = "other"

        Declarations
            RequestXML        = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>"
            RequestXML        += Doctype

        SetHeaderValues
            UserAgent = "INFOR"
            FromDomain              = "NetworkID"
            SenderDomain            = "NetworkID"

            if (VendorPurchaseFromLocation entered
            and DispatchVendorLocationRel exists)
                include SetVendorLocationCredentials
            else
                include SetVendorCredentials

            if (PurchasingVendorHasAribaFullAccount
            or PurchasingLocationHasAribaFullAccount)
                BaseCXML = FullHeaderTemplate
            else
                BaseCXML = LightHeaderTemplate

        SetVendorLocationCredentials
                FromIdentity            = DispatchVendorLocationRel.FromNetworkID
                SenderIdentity          = DispatchVendorLocationRel.SenderNetworkID
                DeploymentMode          = DispatchVendorLocationRel.DeploymentMode
                SharedSecret            = DispatchVendorLocationRel.SharedSecret
                SystemID                = DispatchVendorLocationRel.SystemID
                CxmlVersion             = DispatchVendorLocationRel.CxmlVersion
                WSIEndpoint             = DispatchVendorLocationRel.PostToServer

                include SetVendorLocationToCredentials

        SetVendorCredentials
                FromIdentity            = DispatchVendorRel.FromNetworkID
                SenderIdentity          = DispatchVendorRel.SenderNetworkID
                DeploymentMode          = DispatchVendorRel.DeploymentMode
                SharedSecret            = DispatchVendorRel.SharedSecret
                SystemID                = DispatchVendorRel.SystemID
                CxmlVersion             = DispatchVendorRel.CxmlVersion
                WSIEndpoint             = DispatchVendorRel.PostToServer

                include SetVendorToCredentials

        SetVendorLocationToCredentials
            if (PurchasingLocationHasAribaFullAccount)
                ToDomain                = "NetworkID"
                ToIdentity              = DispatchVendorLocationRel.ToNetworkID
            else
                ToIdentity              = PayablesInvoiceRel.Vendor + "-" + VendorLocationRel.VendorLocation
                VendorName              = VendorLocationRel.VendorLocation.VendorName                                                  
                VendorStreet            = VendorLocationAddressLines
                VendorCity              = VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.Municipality
                VendorState             = VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.StateProvince
                VendorPostal            = VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.PostalCode
                VendorCountry           = VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.Country
                VendorCountryCode       = VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.Country.IsoCountryCode
                VendorEmail             = VendorLocationRel.VendorLocation.PrimeVendorLocationContactRel.EmailAddress                          

        SetVendorToCredentials
            if (PurchasingVendorHasAribaFullAccount)
                ToDomain                = "NetworkID"
                ToIdentity              = DispatchVendorRel.ToNetworkID
            else
                ToIdentity              = PayablesInvoiceRel.Vendor
                VendorName              = PayablesInvoiceRel.Vendor.VendorName
                VendorStreet            = VendorAddressLines
                VendorCity              = PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.Municipality
                VendorState             = PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.StateProvince
                VendorPostal            = PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.PostalCode
                VendorCountry           = PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.Country
                VendorCountryCode       = PayablesInvoiceRel.Vendor.CurrentAddressRel.PostalAddress.Country.IsoCountryCode
                VendorEmail             = PayablesInvoiceRel.Vendor.PrimeVendorContactRel.EmailAddress

        SetPaymentRemittanceRequest
                PayorName                   = PaymentOutputFileDetailRel.PayorName
                PayorStreet                 = PayorAddressLines
                PayorCity                   = PaymentOutputFileDetailRel.PayorPostalAddress.Municipality
                PayorState                  = PaymentOutputFileDetailRel.PayorPostalAddress.StateProvince
                PayorPostalCode             = PaymentOutputFileDetailRel.PayorPostalAddress.PostalCode
                PayorCountry                = PaymentOutputFileDetailRel.PayorPostalAddress.Country
                PayorCountryCode            = PaymentOutputFileDetailRel.PayorPostalAddress.Country.IsoCountryCode

                PayeeName                   = PaymentOutputFileDetailRel.PaidName
                PayeeStreet                 = PayeeAddressLines
                PayeeCity                   = PaymentOutputFileDetailRel.PayeePostalAddress.Municipality
                PayeeState                  = PaymentOutputFileDetailRel.PayeePostalAddress.StateProvince
                PayeePostalCode             = PaymentOutputFileDetailRel.PayeePostalAddress.PostalCode
                PayeeCountry                = PaymentOutputFileDetailRel.PayeePostalAddress.Country
                PayeeCountryCode            = PaymentOutputFileDetailRel.PayeePostalAddress.Country.IsoCountryCode


                TransactionNumber           = PaymentOutputFileDetailRel.TransactionNumber
                BankTransactionCode         = PaymentOutputFileDetailRel.BankTransactionCode
                PaymentDate                 = PaymentOutputFileDetailRel.PaymentDate
                PaymentCurrency             = PaymentOutputFileDetailRel.BankAccountCurrency
                PaymentGrossAmount          = PaymentOutputFileDetailRel.TotalPaymentAmount
                PaymentDiscountAmount       = PaymentOutputFileDetailRel.TotalDiscountAmount
                PaymentNetAmount            = PaymentOutputFileDetailRel.TotalNetPaymentAmount

                DocumentNumber              = DerivePaymentIdentifier
                PayloadID                   = PayloadIDString

                include ConvertPaymentType


        AddPaymentRemittanceDetail
            LineCount = 0
            for each PaymentOutputFileRemittanceRel
                LineCount                += 1
                InvoiceID                = each.Invoice
                InvoiceDate              = each.InvoiceDate
                RemittanceCurrency       = each.InvoiceCurrency
                RemittanceGrossAmount    = each.PaymentAmount
                RemittanceDiscountAmount = each.DiscountAmount
                RemittanceNetAmount      = each.NetPaymentAmount

                PaymentRemittanceXML select "Request/PaymentRemittanceRequest" += PaymentRemittanceDetailXML

		SendNotification
		    if (EmailNotification)
		        send email
		            to CXMLConfigurationRel.ToNotificationEmail
		            from CXMLConfigurationRel.FromNotificationEmail
		            subject "CXML_Payment_Remittance_<ProcessStatus>_-_Payment:<DocumentNumber>_|_Vendor:<Vendor.VendorName>"
		            Contents
		            	"Payment:<DocumentNumber>"
		                "Invoice:<InvoiceID>"
		                "InvoiceDate:<ISOInvoiceDate>"
		                "Vendor:<Vendor.VendorName>"
		                "Status:<ProcessStatus>_-_<StatusMessage>"
		    if (UserNotification and ProcessStatus.Success)
		        LocalActor = actor
		        send notification
		            to LocalActor
		            description is "CXML_Payment_Remittance_<ProcessStatus>_-_Payment:<DocumentNumber>_|_Vendor:<Vendor.VendorName>"
		            priority is low
		            detail is "Payment:<DocumentNumber>_|_Invoice<InvoiceID>_|_InvoiceDate<ISOInvoiceDate>_|_Vendor:<Vendor.VendorName>_|_Status:<ProcessStatus>_-_<StatusMessage>"
		    if (UserNotification and (ProcessStatus.Error or ProcessStatus.Warning))
		        LocalActor = actor
		        send notification
		            to LocalActor
		            description is "CXML_Payment_Remittance_<ProcessStatus>_-_Payment:<DocumentNumber>_|_Vendor:<Vendor.VendorName>"
		            priority is high
		            detail is "Payment:<DocumentNumber>_|_Invoice<InvoiceID>_|_InvoiceDate<ISOInvoiceDate>_|_Vendor:<Vendor.VendorName>_|_Status:<ProcessStatus>_-_<StatusMessage>"	

    Field Rules

    Actions
        Create is a Create Action
            restricted
        Update is an Update Action



        Delete is a Delete Action

        CreatePaymentRemittanceRequest is a Create Action
            Entrance Rules


            Exit Rules
                LastUpdatedTimeStamp = current timestamp

            Action Rules

                CurrentTimeStamp = current timestamp

                if (PaymentOutputFileDetailRel exists)

                    Vendor    = PaymentOutputFileDetailRel.InvoiceVendor
                    Invoice   = PaymentOutputFileDetailRel.Invoice

                    if (PayablesInvoiceRel exists)
                        PurchasingCompany           = PayablesInvoiceRel.Company
                        VendorGroup                 = PayablesInvoiceRel.VendorGroup
                        VendorPurchaseFromLocation  = PayablesInvoiceRel.PurchaseFromLocation

                    if (PurchasingLocationIsAribaEnabled
                    or (PurchasingVendorIsAribaEnabled))

                        include SetPaymentRemittanceRequest
                        include SetHeaderValues
                        include Declarations

                        PaymentRemittanceXML     = PaymentRemittanceTemplate

                        include AddPaymentRemittanceDetail

                        BaseCXML select "/cXML" += PaymentRemittanceXML
                        RequestXML              += BaseCXML

                        if (!DispatchVendorLocationRel exists
                        and !DispatchVendorRel exists )
                            ProcessStatus = 1
                            StatusMessage = "Not Transmitted PaymentRemittanceRequest Created Dispatch Vendor Not Found"
                        else
                            ProcessStatus     = 2
                            StatusMessage     = "PaymentRemittanceRequest Created"

                        if (!DispatchVendorLocationRel exists
                        and !DispatchVendorRel exists )
                            ProcessStatus = 1
                            StatusMessage = "Not Transmitted PaymentRemittanceRequest Created Dispatch Vendor Not Found"
                        else
                            ProcessStatus     = 2
                            StatusMessage     = "PaymentRemittanceRequest Created"
                            
                     	if (ProcessStatus.Error and NotifyError)
                     		include SendNotification

                        if (!ProcessStatus.Error)
                            invoke OutboundCreate CXMLRouter
                                invoked.Data                    = RequestXML
                                invoked.Status                  = "Payment Remittance Request: " + InvoiceID
                                invoked.RunGroup                = ToIdentity
                                invoked.WSIEndpoint             = WSIEndpoint
                                invoked.SharedSecret            = SharedSecret

                                ProcessStatus               = ProcessStatus.Success
                                StatusMessage               = "PaymentRemittanceRequest Created"
                                if (NotifySuccess)
                                	include SendNotification



