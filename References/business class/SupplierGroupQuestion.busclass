SupplierGroupQuestion is a BusinessClass
    owned by procurement
    prefix is SUPGQ

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Ontology
		symbolic key is SupplierGroupQuestion 
		
    Persistent Fields
		QuestionText					is Text
			sql name is SGQUE
			text searchable
   		ResponseType					is Numeric size 1
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
   				List 				value is 4 
   				YesNo 				value is 5
   				YesNoText			value is 6  
   		Attachment
   		DisplayOrder
   		Active							is Boolean
		LastQuestionValueDisplayOrder	is Numeric size 6
    	Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion             is like Question
    	ResponseRules            is Numeric size 1
            States
                NotRequired                             value is 1
                ResponseRequired                        value is 2
                ResponseRequiredAndAttachmentRequired   value is 3
        YesNoResponseRules       is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                 value is 1
                ResponseRequired                            value is 2
                ResponseRequiredAndAttachmentRequired       value is 3
                ResponseRequiredAndAttachmentRequiredIfYes  value is 4
                ResponseRequiredAndAttachmentRequiredIfNo   value is 5
        YesNoTextResponseRules   is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                            value is 1
                ResponseRequiredAndTextResponseRequired                value is 2
                ResponseRequiredAndTextOrAttachmentRequired            value is 3
                ResponseRequiredAndTextOrAttachmentRequiredIfYes       value is 4
                ResponseRequiredAndTextOrAttachmentRequiredIfNo        value is 5
                ResponseRequiredAndTextAndAttachmentRequired           value is 6
                ResponseRequiredAndTextAndAttachmentRequiredIfYes      value is 7
                ResponseRequiredAndTextAndAttachmentRequiredIfNo       value is 8
		AllowResponseAttachment			is Boolean
		OnlyRequiredForFuture           is Date
			default label is "OnlyRequiredForSuppliersWhoRegisterAfterThisDate"
		
	Context Fields
		ContextSupplier					is a Supplier

	Transient Fields
		NewDisplayOrder					is a DisplayOrder

	Local Fields
		LocalConditionalQuestion		is like Question
		LocalListValue					is like SupplierGroupQuestionValue 
		LocalYesOrNo					is Numeric 1 
           	States
           		Yes						value is 1
           		No						value is 2 
		LocalSupplierGroup				is like SupplierGroup
		LocalSupplier					is like Supplier
		LocalQuestion 					is like Question
		LocalSupplierGroupQuestion		is a SupplierGroupQuestion
		LocalQuestionFromSupplierQuestion	is like Question
		NewQuestion						is a Question view
		NewSupplierGroupQuestion		is a SupplierGroupQuestion view
		NewSupplierQuestions			is a SupplierQuestions view
 	Derived Fields

		SupplierResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (HasAnswer)
				if (SupplierGroup.UseConditionalQuestions)
					SupplierQuestionsSetResponseRel.SupplierResponseAnswers   	
				else
					SupplierQuestionResponseRel.SupplierResponseAnswers
   			else
   				blank

		BuyerSupplierResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (BuyerHasAnswer)
				if (SupplierGroup.UseConditionalQuestions)
					BuyerSupplierQuestionsSetResponseRel.SupplierResponseAnswers   	
				else
					BuyerSupplierQuestionResponseRel.SupplierResponseAnswers
   			else
   				blank

 		UnansweredRequiredQuestion is a ConditionalField
 			type is Alpha size 2
 			restricted
   			if (RequiredQuestionNoAnswer)
   				"*"
			else
				blank

		DerivedMissingRequiredInformation is a ConditionalField
			type is Alpha 2
			if (MissingRequiredInformation)
				"*"
			else
				blank
				
		DerivedQuestionText is a DerivedField
			type is Alpha 250
			return QuestionText

		ResponseIsRequiredMessage is a MessageField
			"_ResponseIsRequired"
			
		RequiredQuestionMessage is a MessageField
			restricted
			"RequiredQuestionWithNoAnswer"

		QuestionMissingMessage is a MessageField
			restricted
			"QuestionMissingRequiredAttachment"
		
		DerivedMissingInformationAlert is a DerivedField
			type is like Description
			restricted
			if (RequiredQuestionNoAnswer)
				return RequiredQuestionMessage
			else
			if (MissingRequiredAttachment)
				return QuestionMissingMessage
			else
				return ""

   	Conditions
		QuestionExists
			restricted
			when (SupplierGroupQuestion entered)
		
		RepositoryQuestionExists
			restricted
			when (Question entered)
		
		IsYesNo
			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)
		
		HasAttachment
			restricted
			when (Attachment entered)

		AnswerHasAttachment
			restricted
			when (SupplierQuestionResponseRel.Attachment entered
			and  !SupplierGroup.UseConditionalQuestions)

		AnswerConditionalHasAttachment
			restricted
			when (SupplierQuestionsSetResponseRel.Attachment entered
			and   SupplierGroup.UseConditionalQuestions)

		BuyerAnswerHasAttachment
			restricted
			when (BuyerSupplierQuestionResponseRel.Attachment entered
			and  !SupplierGroup.UseConditionalQuestions)

		BuyerAnswerConditionalHasAttachment
			restricted
			when (BuyerSupplierQuestionsSetResponseRel.Attachment entered
			and   SupplierGroup.UseConditionalQuestions)

		BuyerHasAnswer
			restricted
			when ((BuyerSupplierQuestionResponseRel exists
			and   !SupplierGroup.UseConditionalQuestions)
			or    (BuyerSupplierQuestionsSetResponseRel exists
			and    SupplierGroup.UseConditionalQuestions))

		HasAnswer
			restricted
			when ((SupplierQuestionResponseRel exists
			and    !SupplierGroup.UseConditionalQuestions)
			or    (SupplierQuestionsSetResponseRel exists
			and    SupplierGroup.UseConditionalQuestions))

		AnyResponses
			restricted
			when ((AnyQuestionResponsesRel exists
			and   !SupplierGroup.UseConditionalQuestions)
			or    (AnyQuestionConditionalResponsesRel exists
			and    SupplierGroup.UseConditionalQuestions))

		RequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and   !HasAnswer)
			
		BuyerRequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and   !BuyerHasAnswer)

		ResponseAttachmentExists
			restricted
			when ((SupplierQuestionResponseAttachmentRel exists
			and   !SupplierGroup.UseConditionalQuestions)
			or    (SupplierQuestionsSetResponseAttachmentRel exists
			and    SupplierGroup.UseConditionalQuestions))

		MissingRequiredAttachment
			restricted
			when (AlwaysRequireResponseAttachment
			and	  !ResponseAttachmentExists)

		MissingRequiredInformation
			restricted
			when (RequiredQuestionNoAnswer
			or	  MissingRequiredAttachment)
			
		ResponseRequired
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    ResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        MayRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        AlwaysRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
            
        NonYesNoResponseType
        	restricted
            when (ResponseType.Date
            or    ResponseType.List
            or    ResponseType.Text
            or    ResponseType.Number)
        SuppliersExist
        	restricted
        	when (ActiveSuppliersRel exists)     

		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)

		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)

		CanCopyToRepositoryQuestion
			restricted
			when (YesNoOrList
			and   CreatedFromQuestion = 0
			and   Question = 0
			and   SupplierGroup.UseConditionalQuestions)

		AnyQuestionResponses
			restricted
			when ((AnyQuestionResponsesRel exists
			and   !SupplierGroup.UseConditionalQuestions)
			or    (AnyQuestionConditionalResponsesRel exists
			and    SupplierGroup.UseConditionalQuestions))

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)

		CanCreateConditionalQuestion
			restricted 
			when (YesNoResponseType
			and  !QuestionIsAnswered
			and   Question entered
			and  !Question entered)
		
		CanCreateConditionalListQuestion
			restricted 
			when (ResponseType.List
			and  !QuestionIsAnswered
			and   Question entered
			and  !Question entered)

		QuestionIsAnswered
			restricted
			when (SupplierQuestionsSetResponseRel exists)

		HasDirectConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists
			and   SupplierGroup.UseConditionalQuestions)

		HasAnyDirectConditionalQuestions 
			when (HasDirectConditionalQuestions)

		ReferencedAsConditional
			when (ThisQuestionConditionalRel exists)

		AdHocResponseTypeList
			when (ResponseType.List
			and   Question !entered
			and   SupplierGroup.UseConditionalQuestions)

		RepositoryResponseTypeYesNo
			when (ResponseType.YesNo
			and   Question entered)

		RepositoryResponseTypeYesNoText
			when (ResponseType.YesNoText
			and   Question entered)

		RepositoryResponseTypeList
			when (ResponseType.List
			and   Question entered
			and   SupplierGroup.UseConditionalQuestions)

	Relations

  		AnyQuestionResponsesRel	is a SupplierQuestionResponse set
  		AnyQuestionConditionalResponsesRel	is a SupplierQuestionsSetResponse set

  		ActiveSuppliersRel 
  			one-to-many relation to Supplier
  			Field Mapping uses symbolic key
  				related.SupplierGroup          = SupplierGroup
  			Instance Selection
  				where (related.Active)
  		
  		SupplierQuestionResponseRel
  			one-to-one relation to SupplierQuestionResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= actor.agent(SupplierSourceId).Supplier
            	related.SupplierGroupQuestion	= SupplierGroupQuestion

  		SupplierQuestionsSetResponseRel
  			one-to-one relation to SupplierQuestionsSetResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= actor.agent(SupplierSourceId).Supplier
            	related.SupplierQuestions		= SupplierGroupQuestion

  		BuyerSupplierQuestionResponseRel
  			one-to-one relation to SupplierQuestionResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= ContextSupplier
            	related.SupplierGroupQuestion	= SupplierGroupQuestion

  		BuyerSupplierQuestionsSetResponseRel
  			one-to-one relation to SupplierQuestionsSetResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= ContextSupplier
            	related.SupplierQuestions		= SupplierGroupQuestion

        SupplierQuestionResponseAttachmentRel
  			one-to-many relation to SupplierQuestionResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= actor.agent(SupplierSourceId).Supplier
            	related.SupplierGroupQuestion	= SupplierGroupQuestion
        	Instance Selection
        		where (related.Attachment entered)
        SupplierQuestionsSetResponseAttachmentRel
  			one-to-many relation to SupplierQuestionsSetResponse
            Field Mapping uses part of key
            	related.SupplierGroup			= SupplierGroup
            	related.Supplier				= actor.agent(SupplierSourceId).Supplier
            	related.SupplierQuestions		= SupplierGroupQuestion
        	Instance Selection
        		where (related.Attachment entered)

		QuestionAnswerRel 
			one-to-many relation to QuestionAnswer
			Field Mapping uses symbolic key 
				related.ProcurementGroup				= SupplierGroup
				related.Question						= Question

		ConditionalQuestionsDirectRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup				= SupplierGroup 
				related.Question						= Question 		
		
		ThisQuestionConditionalRel
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses ByConditionalQuestion
				related.ProcurementGroup				= SupplierGroup
				related.ConditionalQuestion				= Question

		QuestionIsAnsweredRel 
			one-to-many relation to SupplierQuestionsSetResponse
			Field Mapping uses part of key 
				related.SupplierGroup						= SupplierGroup
        	Instance Selection
        		where (related.SupplierQuestions.Question	= Question)

  		SupplierRel 
  			one-to-many relation to Supplier
  			Field Mapping uses symbolic key
  				related.SupplierGroup          = SupplierGroup

  		SupplierQuestionsRel 
  			one-to-many relation to SupplierQuestions
			delete cascades
  			Field Mapping uses BySupplierGroupQuestion
				related.SupplierGroup			= SupplierGroup
				related.Supplier				= LocalSupplier
				related.SupplierGroupQuestion	= LocalSupplierGroupQuestion

  		SupplierQuestionsForSupplierGroupQuestionRel 
  			one-to-many relation to SupplierQuestions
			delete cascades
  			Field Mapping uses BySupplierGroupQuestions
				related.SupplierGroup			= SupplierGroup
				related.SupplierGroupQuestion	= SupplierGroupQuestion

  		SupplierGroupQuestionValueRel 
  			one-to-many relation to SupplierGroupQuestionValue
			delete cascades
  			Field Mapping uses symbolic key
				related.SupplierGroup			= SupplierGroup
				related.SupplierGroupQuestion	= SupplierGroupQuestion

		QuestionListValueRel 
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key
				related.ProcurementGroup		= SupplierGroup 
				related.Question				= LocalQuestionFromSupplierQuestion

	Sets
		ByDisplayOrder
			duplicates
			Sort Order
 				SupplierGroup
 				DisplayOrder
 				SupplierGroupQuestion
 				
		ByQuestion
			Sort Order
				SupplierGroup
    			Question
 				SupplierGroupQuestion
 				
   	Field Rules
   		QuestionText
   			required
   		ResponseType
   			required
		Active
			default to true
		ResponseRules
            if (NonYesNoResponseType)
                required
                    "MustSelectAResponseRule"
                YesNoResponseRules = 0
                YesNoTextResponseRules = 0
                    
        YesNoResponseRules
            if (ResponseType.YesNo)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoTextResponseRules= 0
        
        YesNoTextResponseRules
            if (ResponseType.YesNoText)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoResponseRules= 0
		AllowResponseAttachment
			if (AllowResponseAttachment changed
			and AllowResponseAttachment = false)
				constraint (!ResponseAttachmentExists)
					"CannotChangeAllowAttachmentUploadWithAnswerFlag;SupplierQuestionAnswersExistWithAttachments"
			if (MayRequireResponseAttachment)
				AllowResponseAttachment = true

	Rule Blocks

	Actions 
		Create is a Create Action
			Field Rules
	 			DisplayOrder
	 				autosequence using SupplierGroup.LastSupplierGroupQuestionDisplayOrder
	
			Action Rules
			
				if (!ResponseRequired)
					OnlyRequiredForFuture = blank
				
				if (ResponseRequired
				and SuppliersExist)
				
					if ((OnlyRequiredForFuture entered
					and  OnlyRequiredForFuture < current corporate date)
					or   OnlyRequiredForFuture !entered)
						confirmation required
							"SuppliersExistWhoWillNeedToAnswerThisQuestion;RunEmailSuppliersWithRequiredQuestionsToAnswerAfterCreatingRecord" 

			Exit Rules
				if	(SupplierGroup.UseConditionalQuestions)
					invoke CreateSupplierQuestionsFromSupplierGroupQuestions
						invoked.PrmSupplierGroup			= SupplierGroup
						invoked.PrmSupplierGroupQuestion	= SupplierGroupQuestion 

		Update is an Update Action
			Action Rules
				if (!ResponseType.List
				and SupplierGroupQuestionValue set exists)
					invoke Delete SupplierGroupQuestionValue set
					
				if (!ResponseRequired)
					OnlyRequiredForFuture = blank

				if (ResponseType changed)
					constraint (!AnyResponses)
						"CannotChangeQuestionTypeWhenAnswersExist"
					if (ResponseRequired
					and SuppliersExist)
					
						if ((OnlyRequiredForFuture entered
						and  OnlyRequiredForFuture < current corporate date)
						or   OnlyRequiredForFuture !entered)
							confirmation required
								"SuppliersExistWhoWillNeedToAnswerThisQuestion;RunEmailSuppliersWithRequiredQuestionsToAnswerAfterCreatingRecord;EnterOKToContinue" 

				if (SupplierGroup.UseConditionalQuestions)
					invoke UpdateOrDeleteSupplierQuestionFromSupplierGroupQuestions
						invoked.PrmSupplierGroup			= SupplierGroup
						invoked.PrmSupplierGroupQuestion	= SupplierGroupQuestion 	
						invoked.PrmUpdate                   = true 					

		Delete is a Delete Action

			Entrance Rules
				constraint (QuestionIsAnsweredRel !exists)
					"CannotDelete;AtLeastOneSupplierHasAnsweredTheQuestion"

				if (SupplierQuestionsForSupplierGroupQuestionRel exists)
					invoke UpdateOrDeleteSupplierQuestionFromSupplierGroupQuestions
						invoked.PrmSupplierGroup			= SupplierGroup
						invoked.PrmSupplierGroupQuestion	= SupplierGroupQuestion 	
						invoked.PrmDelete                   = true 

		UpdateOrDeleteSupplierQuestionFromSupplierGroupQuestions is a Set Action
			restricted
			Parameters
				PrmSupplierGroup 		 is a SupplierGroup 
				PrmSupplierGroupQuestion is like SupplierGroupQuestion
				PrmUpdate                is Boolean 
				PrmDelete                is Boolean 

			Instance Selection
				where (PrmSupplierGroup 		= SupplierGroup
				and    PrmSupplierGroupQuestion = SupplierGroupQuestion)

			Action Rules
				Instance Rules
					for each SupplierRel
						LocalSupplier 				= each.Supplier 
						LocalSupplierGroupQuestion	= SupplierGroupQuestion
						if (SupplierQuestionsRel exists)
							if (PrmUpdate)
								invoke Update SupplierQuestionsRel
									fill in fields from SupplierGroupQuestion
										except invoked.DisplayOrder
							if (PrmDelete)			
								invoke Delete SupplierQuestionsRel

		CreateSupplierQuestionsFromSupplierGroupQuestions is a Set Action
			restricted
			Parameters
				PrmSupplierGroup 		 is a SupplierGroup 
				PrmSupplierGroupQuestion is a SupplierGroupQuestion

			Instance Selection
				where (SupplierGroup = PrmSupplierGroup
				and   (PrmSupplierGroupQuestion !entered
				or     PrmSupplierGroupQuestion = SupplierGroupQuestion))

			Action Rules
				Instance Rules
					for each SupplierRel
						LocalSupplier 				= each.Supplier
						LocalSupplierGroupQuestion	= SupplierGroupQuestion
						if (SupplierQuestionsRel !exists)
							invoke Create SupplierQuestions
								assign result to NewSupplierQuestions
								invoked.SupplierGroup  			= SupplierGroup
								invoked.Supplier				= each.Supplier
								invoked.SupplierGroupQuestion	= SupplierGroupQuestion
								fill in fields from SupplierGroupQuestion
									except invoked.DisplayOrder

							if (SupplierGroupQuestion.ResponseType.List)
								for each SupplierGroupQuestionValueRel
									invoke CreateDisplay SupplierQuestionsListValue
										invoked.SupplierGroup				= SupplierGroup
										invoked.Supplier					= NewSupplierQuestions.Supplier
										invoked.SupplierQuestions			= NewSupplierQuestions.SupplierQuestions
										invoked.SupplierQuestionsListValue	= each.SupplierGroupQuestionValue
										fill in fields from each

		CopyToRepositoryQuestion is an Instance Action
			valid when (CanCopyToRepositoryQuestion)
			Parameters 
				ParmInactivate is Boolean
					default label is "InactivateCurrentSupplierGroupQuestion"

			Parameter Rules
				ParmInactivate
					initial value is true 
			Action Rules

				invoke Create Question 
					assign result to NewQuestion
					invoked.ProcurementGroup			= SupplierGroup 
					fill in fields from this instance

				for each SupplierGroupQuestionValueRel
					invoke Create QuestionAnswer 
						invoked.ProcurementGroup		= SupplierGroup
						invoked.Question				= NewQuestion.Question 
						invoked.QuestionAnswer			= each.SupplierGroupQuestionValue 
						invoked.DisplayOrder			= each.DisplayOrder

				invoke Create
					assign result to NewSupplierGroupQuestion
					invoked.Question					= NewQuestion.Question
					CreatedFromQuestion					= 0
					fill in fields from this instance

				for each SupplierGroupQuestionValueRel
					invoke CreateDisplayValue SupplierGroupQuestionValue 
						invoked.SupplierGroup				= SupplierGroup
						invoked.SupplierGroupQuestion		= NewSupplierGroupQuestion.SupplierGroupQuestion 
						invoked.SupplierGroupQuestionValue	= each.SupplierGroupQuestionValue 
						invoked.DisplayOrder				= each.DisplayOrder

				if  (ParmInactivate)
					invoke Update
						invoked.Active = false

		NotifyIfRequiredQuestions is an Instance Action
			default label is "EmailSuppliersWithRequiredQuestionsToAnswer"
			Action Rules
			
				invoke NotifyIfRequiredQuestions Supplier
					invoked.ParmSupplierGroup = SupplierGroup
