CloseTaskType is a BusinessClass
    owned by closemgmt
    prefix is CTT

    Ontology
    	symbolic key is CloseTaskType

    Persistent Fields
		Description						is Alpha 250
		IsPrimary						is Boolean
		IsSecondary						is Boolean
		IsTertiary						is Boolean
		IsDefault						is Boolean
		Active
		
	Field Rules
		Description
			default to CloseTaskType

		IsDefault
			constraint (!HasChild)
				"ASummaryTaskTypeCannotBeADefaultTaskType"
				
			if (IsPrimary)
				constraint (!IsDefaultPrimaryTaskTypeRel exists)
					"<first IsDefaultPrimaryTaskTypeRel.CloseTaskType>IsAlreadyDefinedAsTheDefaultPrimaryTaskType"			
		
			if (IsSecondary)
				constraint (!IsDefaultSecondaryTaskTypeRel exists)
					"<first IsDefaultSecondaryTaskTypeRel.CloseTaskType>IsAlreadyDefinedAsTheDefaultSecondaryTaskType"			
		
			if (IsTertiary)
				constraint (!IsDefaultTertiaryTaskTypeRel exists)
					"<first IsDefaultTertiaryTaskTypeRel.CloseTaskType>IsAlreadyDefinedAsTheDefaultTertiaryTaskType"			
		
		Active
			initial value is true
			
	Derived Fields
		TopLevelTaskType is a DerivedField
			type is AlphaUpper 50
			return (first TopLevelAncestorRel.CloseTaskType)

	Conditions
		HasChild
			restricted
    		when (CloseTaskType children exist)
    		
    	HasParent
    		restricted
    		when (ParentCloseTaskType entered)
    		
		PrimaryWithNoChildren
			when (IsPrimary
			and   !HasChild)
    		
		SecondaryWithNoChildren
			when (IsSecondary
			and   !HasChild)
    		
		TertiaryWithNoChildren
			when (IsTertiary
			and   !HasChild)
    		
	Relations
		IsDefaultPrimaryTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup
			Instance Selection
				where (related.CloseTaskType != CloseTaskType
				and    related.IsPrimary
				and    related.IsDefault)

		IsDefaultSecondaryTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup
			Instance Selection
				where (related.CloseTaskType != CloseTaskType
				and    related.IsSecondary
				and    related.IsDefault)

		IsDefaultTertiaryTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup
			Instance Selection
				where (related.CloseTaskType != CloseTaskType
				and    related.IsTertiary
				and    related.IsDefault)

		IsPrimaryTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup
			Instance Selection
				where (related.IsPrimary)

		IsSecondaryTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup
			Instance Selection
				where (related.IsSecondary)

		IsTertiaryTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup
			Instance Selection
				where (related.IsTertiary)

		TopLevelAncestorRel 
			one-to-many relation to CloseTaskType 
			Field Mapping uses symbolic key
				related.CloseManagementGroup	= CloseManagementGroup
				related.CloseTaskType			= CloseTaskType ancestors.CloseTaskType			
			Instance Selection
				where (related.ParentCloseTaskType = 0)

    Attach Rules
		constraint (Active)
			"TaskTypeIsInactive"
			
	Actions

		CreatePrimary is a Create Action
			default label is "Create"
			
			Action Rules
				if (HasParent)
					constraint (!ParentCloseTaskType.IsDefault)
						"CannotAddDetailTaskTypeToATaskTypeThatIsDefinedAsADefaultTaskType"
						
				IsPrimary = true
		
		CreateSecondary is a Create Action
			default label is "Create"
			
			Action Rules
				if (HasParent)
					constraint (!ParentCloseTaskType.IsDefault)
						"CannotAddDetailTaskTypeToATaskTypeThatIsDefinedAsADefaultTaskType"
						
				IsSecondary = true
		
		CreateTertiary is a Create Action
			default label is "Create"
			
			Action Rules
				if (HasParent)
					constraint (!ParentCloseTaskType.IsDefault)
						"CannotAddDetailTaskTypeToATaskTypeThatIsDefinedAsADefaultTaskType"
						
				IsTertiary = true
					
		Update is an Update Action
					
		Delete is a Delete Action
