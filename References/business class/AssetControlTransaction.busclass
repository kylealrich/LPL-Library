AssetControlTransaction is a BusinessClass
    owned by am
    prefix is ACT
    classic name is AMCTLTRANS

    Ontology
        symbolic key is AssetControlTransaction
            classic set name is ACTSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        TransactionDate is Date
            classic name is TRANS-DATE
        Reference
        Description
        DebitBalance    is an InternationalAmount
        CreditBalance   is an InternationalAmount
    
    Local Fields

        	
	Relations
		MemoOrUnreleasedAssetTransactionsRel
			one-to-many relation to AssetTransaction
			Field Mapping uses Set1
				related.Asset	= Asset
			Instance Selection
				where (related.IsMemoOrUnreleased)

		UnreleasedDebitTransactionsRel
			one-to-many relation to AssetTransaction
			Field Mapping uses Set1
				related.Asset					= Asset
				related.TransactionStatus		= "U"
			Instance Selection
				where (related.TransactionAmount > 0)

		UnreleasedCreditTransactionsRel
			one-to-many relation to AssetTransaction
			Field Mapping uses Set1
				related.Asset					= Asset
				related.TransactionStatus		= "U"
			Instance Selection
				where (related.TransactionAmount < 0)
				
	Conditions
		IsInBalance
			when ((DebitBalance + CreditBalance) = 0)				
						
	Field Rules
		TransactionDate
			required
				"TransactionDateIsRequired"			

		Reference
			required
				"ReferenceIsRequired"			

		Description
			required
				"DescriptionIsRequired"			

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		CheckBalance is an Instance Action
			restricted
			Action Rules
				DebitBalance 	= sum UnreleasedDebitTransactionsRel.TransactionAmount
				CreditBalance 	= sum UnreleasedCreditTransactionsRel.TransactionAmount	

		Increment is an Instance Action
			restricted
			Parameters
				InputCreditAmount is an InternationalAmount
				InputDebitAmount is an InternationalAmount
			Action Rules
				DebitBalance += InputDebitAmount
				CreditBalance += InputCreditAmount	
				
		Delete is a Delete Action
			restricted
			Entrance Rules
				invoke DeleteReleasedTrans MemoOrUnreleasedAssetTransactionsRel        

		DeleteFromBatch is a Delete Action
			restricted	
