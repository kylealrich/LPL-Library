DunningLetterDetail is a BusinessClass
    owned by ar
    prefix is DDL
    classic name is ARDUNSLDTL

    Ontology
        symbolic key is DunningLetterDetail
            classic set name is DDLSET2
            classic name for DunningLetterDetail.DunningLetterGroup.ReceivableInvoiceDetailType is TRANS-TYPE
            name for DunningLevel is DunningLevel
            classic name for DunningLevel is DUN-LEVEL

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        DunningCycleID    is a DunCycleId
            classic name is DUN-CYCLE-ID
        DueDate
        PastDueAmount     is an InternationalAmount
            classic name is PAST-DUE-AMT
        DunningPending    is Boolean
            classic name is TOBE-DUNNED-FL
        TransactionAmount is an InternationalAmount
            classic name is ORIG-AMT
        CollectionLevel   is a CollectionLvl
            classic name is COLLECTION-LVL
        CreditAgencyDate  is Date
            classic name is DUNN-DATE
		DueDays
		Currency
	
	Derived Fields

		DerivedReceivableInvoice is a DerivedField
			type is AlphaUpper 22
			return DunningLetterDetail.DunningLetterGroup.Invoice
			
		DerivedTransactionDate is a DerivedField
			type is Date
			return ReceivableInvoiceRel.TransactionDate

		DerivedBaseOpenAmount is a DerivedField
			type is like InternationalAmount
			return ReceivableInvoiceDetailRel.SignedBaseOpenAmount

    Sets

        Set1
            indexed
            Sort Order
                Company
                Customer
                DunningLetterDetail

		Set2
			indexed
			Sort Order
				Company
				Customer
				DunningLevel
				Currency
				DueDate
				DunningLetterDetail				

	Relations
        ReceivableInvoiceRel
            one-to-one relation to ReceivableInvoice
            Field Mapping uses Set6
                related.Company               			= Company
                related.AlternateReceivableInvoiceType 	= DunningLetterDetail.DunningLetterGroup.ReceivableInvoiceDetailType
                related.ReceivableInvoice     			= DunningLetterDetail.DunningLetterGroup.Invoice

        DunningLetterHeaderRel
            one-to-one relation to DunningLetterHeader
            required
            Field Mapping uses symbolic key
                related.Company  				= Company
                related.Customer 				= Customer
                related.DunningLevel			= DunningLevel

		ReceivableInvoiceDetailRel
			one-to-one relation to ReceivableInvoiceDetail
			Field Mapping uses symbolic key
				related.Company												= Company
            	related.ReceivableInvoiceDetail.ReceivableInvoiceDetailType = DunningLetterDetail.DunningLetterGroup.ReceivableInvoiceDetailType
            	related.ReceivableInvoiceDetail.Invoice						= DunningLetterDetail.DunningLetterGroup.Invoice
            	related.ReceivableInvoiceDetail.PaymentSeq                  = DunningLetterDetail.DunningLetterGroup.PaymentSeq

    Actions
        Create is a Create Action
        	restricted
        	Entrance Rules
        		increment DunningLetterHeaderRel.CurrentBalance by PastDueAmount

        Update is an Update Action
        	restricted
        	Action Rules
        		if (PastDueAmount changed)
        			if (PastDueAmount > old PastDueAmount)
        				increment DunningLetterHeaderRel.CurrentBalance by (PastDueAmount - old PastDueAmount)
        			else
        			    decrement DunningLetterHeaderRel.CurrentBalance by (old PastDueAmount - PastDueAmount)

        Delete is a Delete Action
        	Entrance Rules
        		decrement DunningLetterHeaderRel.CurrentBalance by PastDueAmount
