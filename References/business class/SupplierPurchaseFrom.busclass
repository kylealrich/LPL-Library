SupplierPurchaseFrom is a BusinessClass
	owned by procurement
	prefix is Supf
	
	Ontology
		symbolic key is SupplierPurchaseFrom
	
	Patterns
	
	Persistent Fields
		SupplierVendor 		is a Vendor		
		PurchaseFrom		is a VendorLocation
		AllItemTypes 	is Numeric size 1
    		States
    			SpecialsOnly 		value is 0
    			AllItemTypes 		value is 1
	
	Conditions
		LocationCompanyOnly
			when (SupplierPurchaseFrom.ParticipantLocation.Location = blank
			and	  SupplierPurchaseFrom.ParticipantLocation.RequestingLocation = blank)
		HasLocationOrRequestingLocation
			when (SupplierPurchaseFrom.ParticipantLocation.Location != blank
			or    SupplierPurchaseFrom.ParticipantLocation.RequestingLocation != blank)
		PayablesVendorLocationActive
			when (PurchaseFrom.Status.Active)
	Sets
		ByLocationRequestingLocation
			duplicates
     		indexed
      		Sort Order
           		SupplierGroup
            	Supplier
            	SupplierPurchaseFrom.ParticipantLocation.Company
            	SupplierPurchaseFrom.ParticipantLocation.Location
            	SupplierPurchaseFrom.ParticipantLocation.RequestingLocation
            Instance Selection
            	where (HasLocationOrRequestingLocation)
            	
        ByCompanyOnly
   			duplicates
   			Sort Order
   				SupplierGroup
   				Supplier
   				SupplierPurchaseFrom.ParticipantLocation.Company
	
	Relations
		PurchasingCompanyRel
    		one-to-one relation to PurchasingCompany
            Field Mapping uses symbolic key
          		related.Company	  = SupplierPurchaseFrom.ParticipantLocation.Company
          		
  	  	PurchaseFromRel
  	  		one-to-one relation to VendorLocation
  	  		Field Mapping uses symbolic key
  	  			related.VendorGroup       = SupplierGroup
  	  			related.Vendor            = SupplierVendor
  	  			related.VendorLocation    = PurchaseFrom
  	  	
  	  	ContractPurchaseFromRel
  	  		one-to-many relation to ContractPurchaseFrom
  	  		Field Mapping uses BySupplier
  	  			related.ContractGroup				 									= SupplierGroup
           		related.SupplierVendor   			  									= SupplierVendor
           		related.ContractPurchaseFrom.ParticipantLocation.Company  				= SupplierPurchaseFrom.ParticipantLocation.Company
           		related.ContractPurchaseFrom.ParticipantLocation.Location 				= SupplierPurchaseFrom.ParticipantLocation.Location
           		related.ContractPurchaseFrom.ParticipantLocation.RequestingLocation		= SupplierPurchaseFrom.ParticipantLocation.RequestingLocation
  	  	
  	  	CompanyOnlyRel
    		one-to-one relation to SupplierPurchaseFrom
            Field Mapping uses symbolic key
          		related.SupplierGroup				 									= SupplierGroup
           		related.Supplier					  									= Supplier
           		related.SupplierPurchaseFrom.ParticipantLocation.Company  				= SupplierPurchaseFrom.ParticipantLocation.Company
           		related.SupplierPurchaseFrom.ParticipantLocation.Location 				= blank
           		related.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation		= blank
           		related.SupplierPurchaseFrom.ParticipantLocation.PricingGroup           = blank
           		
     	LocationRequestingLocationRel
    		one-to-many relation to SupplierPurchaseFrom
            Field Mapping uses ByLocationRequestingLocation
          		related.SupplierGroup				  						= SupplierGroup
           		related.Supplier					  						= Supplier
           		related.SupplierPurchaseFrom.ParticipantLocation.Company 	= SupplierPurchaseFrom.ParticipantLocation.Company
           		
	Field Rules
		SupplierVendor
			initial value is Supplier.Vendor
			SupplierVendor = Supplier.Vendor

		PurchaseFrom
			if (PurchaseFrom entered)
				constraint (PurchaseFromRel.IsPurchaseFromLocation)
					"PayablesVendorLocationCannotBeARemit-ToOnlyLocation"
					
			required	
			
	Actions
		Create is a Create Action
			Action Rules
				if (SupplierPurchaseFrom.ParticipantLocation.Company entered)
					constraint(PurchasingCompanyRel exists)
						"POCompanyDoesNotExist"
				if (LocationCompanyOnly)
    				if (LocationRequestingLocationRel exists)
    					confirmation required
    						"SupplierPurchaseFromRecordsExistByLocationOrRequestingLocation;DoYouWantToContinue?"
				if (HasLocationOrRequestingLocation)
					if (CompanyOnlyRel exists)
						confirmation required
							"SupplierPurchaseFromRecordExistsForWholeCompany;DoYouWantToContinue?"
		
		Update is an Update Action
		
		Delete is a Delete Action
		
	
	
