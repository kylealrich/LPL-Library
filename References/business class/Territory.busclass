Territory is a BusinessClass
	owned by ar
	prefix is TRY
	classic name is TERITORY

	Ontology
		symbolic key is Territory
			classic set name is TRYSET1

	Patterns
		implements StaticJava
		disable AuditIndex
		implements BODId
		
	Persistent Fields

		Description
		UsedFlag	is Boolean
			classic name is USED-FL
		Active	  is Boolean
			classic name is ACTIVE-STATUS
	 
	Local Fields
		LocalBODCurrentTimeStamp	is a BODCurrentTimeStamp
		ActionCode
		LocalNativeLPLBODTrigger	is Boolean				
		LocalMainUserTemplate			is Alpha 250
		LocalFSMInboundBODTracker		is Numeric 15
		Error							is Boolean
		ErrorMessage	 				is Alpha 300
		LocalBODTrigger					is Boolean
		NewBODTracker  					is a FSMInboundBODTracker view
		LocalConfigurationParameter		is Alpha size up to 200
		LocalAEAlreadyExecuted			is Boolean
		LocalCrossAccountingEntity		is Alpha size 20
		
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforERPEnterpriseCommonSalesTerritory"

		ContextMessageText is a MessageField
			restricted
			"Territory<Territory>"

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or ActionCode.Create)
				return "Add"
			else
				return "Replace"
				
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if(action type.Delete)
				return "Deleted"
			else
			if(Active = "true")
				return "Open"
			else
				return "Closed"
		
		DerivedBODCompany is a DerivedField
		 	type is Alpha size 4
		 	restricted
		 	return Company using "%d"
		
		DerivedMultipleFEG is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "IsMultipleFEG"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedCleanDocumentID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "RequiredCleanDocumentID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		AccountingEntity is a DerivedField
			type is Alpha  12
			restricted
			return GeneralLedgerCompanyRel.AccountingEntity
			
		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha 4
			restricted
			return GeneralLedgerCompanyRel.FinanceEnterpriseGroup
		
		DerivedCrossGLC is a DerivedField
			type is Alpha size 50
			restricted
			if (DerivedMultipleFEG = "Y")
				return DerivedFinanceEnterpriseGroup + DerivedDelimiter + DerivedBODCompany
			else
				return DerivedBODCompany
				
		DerivedCrossAccountingEntity is a DerivedField
			type is Alpha size 50
			restricted
			if(LocalAEAlreadyExecuted != true)
				LocalAEAlreadyExecuted = true
				if (FSMBODCrossReferenceDetailRel.DestinationValue entered)
					LocalCrossAccountingEntity =  FSMBODCrossReferenceDetailRel.DestinationValue
				else
					LocalCrossAccountingEntity =  DerivedCrossGLC
				return LocalCrossAccountingEntity
			else
				return LocalCrossAccountingEntity
		
		DerivedAccountingEntity is a DerivedField
			type is Alpha 20
			restricted
			if (DerivedCleanDocumentID = "Y")
				return DerivedCrossAccountingEntity
			else
				return DerivedFinanceEnterpriseGroup + DerivedDelimiter + AccountingEntity
			
		DerivedBODDocumentID is a DerivedField
			type is Alpha 50
			restricted
			if (DerivedCleanDocumentID = "Y")
				return Territory
			else
				return Company using "%d" + DerivedDelimiter + Territory
			
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return 	"infor-nid:" + DerivedTenantID + ":" + DerivedAccountingEntity + ":"  + ":" + DerivedBODDocumentID + ":" +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker	
	
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
		
		TerritoryXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			if (DerivedCleanDocumentID = "Y")
				TerritoryXMLBOD = template.IONSyncCodeDefination_Territory_CleanDoc_ST document for this instance
			else
				TerritoryXMLBOD = template.IONSyncCodeDefination_Territory_ST document for this instance


	Context Fields
		FSMInboundBODTracker

	Attach Rules
		constraint (Active)
			"TerritoryIsNotActive"

	Field Rules
		Description
			required

	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)


	Relations

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company


		FSMBODConfigurationParameterRel
			one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
				related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
			one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
				related.FSMBODConfiguration.Verb 		= 1
				related.FSMBODConfiguration.Noun 		= "CodeDefinition"
				related.FSMBODConfiguration.Direction 	= 1
				
		FSMBODCrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
				related.FSMBODCrossReference.SourceField 		= "CSFGLC"
				related.FSMBODCrossReference.DestinationField 	= "DESTAE"
			Instance Selection
				where (related.SourceValue	= DerivedCrossGLC )

		FSMInboundBODTrackerRel
			one-to-one relation to FSMInboundBODTracker
			Field Mapping uses symbolic key
				related.FSMInboundBODTracker	= LocalFSMInboundBODTracker




	Rule Blocks 

		TriggerTerritory 
			trigger "TerritoryService" PA service
				resume on error
				title is "EG:<Company.CustomerBusinessGroup.FinanceEnterpriseGroup>TERRITORY:<Territory>"
				Criteria
					Company.CustomerBusinessGroup.FinanceEnterpriseGroup
					Company.AccountingEntity
					Territory
				Variables
					include persistent fields from Territory
					Company.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is LastModTimeStamp 
					Company.AccountingEntity
						variable name is AccountingEntity
					Company
						variable name is Company
					ActionCode
		DeleteRules
			if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger)
				ActionCode		= ActionCode.Delete
				increment bod id.VariationID
				LocalBODCurrentTimeStamp.CurrentTimeStamp = current timestamp
				include TriggerTerritory


	Actions
		Create is a Create Action
  
	  	Update is an Update Action

	   	Delete is a Delete Action
			Entrance Rules 
				include DeleteRules
		
		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmDocumentID		is Alpha size 100
					default label is "DocumentID"
				PrmSystemOfRecord	is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
		
		BODDataInitialLoadTerritoryCriteria1 is a Set Action
			restricted
			Parameters
				PrmFromTerritoryCompany			is a ReceivableCompany
				PrmToTerritoryCompany			is a ReceivableCompany
				PrmFromTerritory				is a Territory
					context of PrmFromTerritoryCompany
				PrmToTerritory					is a Territory
					context of PrmFromTerritoryCompany
			Parameter Rules
			Instance Selection			
				where  (((PrmFromTerritoryCompany entered
				and		  PrmFromTerritoryCompany <= Company)
				or		  PrmFromTerritoryCompany not entered)				
				and	((PrmToTerritoryCompany entered
				and		 PrmToTerritoryCompany >= Company)
				or		 PrmToTerritoryCompany not entered)				
				and		(Active = true))
		
			Sort Order
				Territory
			Action Rules								
				Territory Set Rules
					Entrance Rules
					
					Exit Rules				
				Instance Rules	
					if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger)
						ActionCode	= ActionCode.Create		
						increment bod id.VariationID
						LocalBODCurrentTimeStamp.CurrentTimeStamp = current timestamp
						include TriggerTerritory

						
		BODDataInitialLoadTerritoryCriteria2 is a Set Action
			restricted
			Parameters
				PrmFromTerritoryCompany			is a ReceivableCompany
				PrmToTerritoryCompany			is a ReceivableCompany
				PrmFromTerritory				is a Territory
					context of PrmFromTerritoryCompany
				PrmToTerritory					is a Territory
					context of PrmFromTerritoryCompany
			Parameter Rules
			Instance Selection			
				where  (((PrmFromTerritoryCompany entered
				and		  PrmFromTerritoryCompany = Company)
				or		  PrmFromTerritoryCompany not entered)				
				and	((PrmFromTerritory entered
				and		 PrmFromTerritory <= Territory)
				or		 PrmFromTerritory not entered)	
				and	((PrmToTerritory entered
				and		 PrmToTerritory >= Territory)
				or		 PrmToTerritory not entered)					
				and		(Active = true))
		
			Sort Order
				Territory
			Action Rules								
				Territory Set Rules
					Entrance Rules
					
					Exit Rules				
						
				Instance Rules	
					if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger)
						ActionCode	= ActionCode.Create		
						increment bod id.VariationID
						LocalBODCurrentTimeStamp.CurrentTimeStamp = current timestamp
						include TriggerTerritory


		SendTerritoryBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is TerritoryXMLBOD
					bod type is "Sync.CodeDefinition"
					accounting entity is DerivedAccountingEntity
					document id is Territory
					variation id is	BODVariationID

		TriggerTerritoryNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				if (DerivedCleanDocumentID = "Y")
					LocalMainUserTemplate = "IONSyncCodeDefination_Territory_CleanDoc_ST"
				else
					LocalMainUserTemplate = "IONSyncCodeDefination_Territory_ST"
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "Territory"
					invoked.PrmFinanceEnterpriseGroup	= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.PrmAccountingEntity			= GeneralLedgerCompanyRel.AccountingEntity
					invoked.PrmCompany					= Company
					invoked.PrmMainUserTemplate			= LocalMainUserTemplate
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger	
				LocalBODTrigger = true
				if(Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)	
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"
							invoked.BODDocumentID			= Territory
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= Company
							invoked.Reference2				= "Territory"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= Territory
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= Company
							invoked.Reference2				= "Territory"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendTerritoryBODNativeLPL
						resume on error
							Error										= true
							ErrorMessage	 							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODFormattedId
							invoked.BODXML								= TerritoryXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODFormattedId
							invoked.BODXML									= TerritoryXMLBOD
						
	Action Exit Rules
		if (Company.CustomerBusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode	= ActionCode.Update
					if (action type.Create)
						ActionCode	= ActionCode.Create				
					else
						if (action type.Delete)
							ActionCode	= ActionCode.Delete
		
					increment bod id.VariationID
					LocalBODCurrentTimeStamp.CurrentTimeStamp = current timestamp
					include TriggerTerritory

