RouteHeader is a BusinessClass
    owned by wh
    prefix is RTE
    classic name is ROUTEHDR

    Ontology
        symbolic key is RouteHeader
            classic set name is RTESET1
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        DayOfWeek
        RouteStatus       is Numeric size 1
        	default label is "Status"
        	protected
            classic name is STATUS
            States
                Unreleased  value is 0
                Released    value is 1
        StockVolume
            classic name is CUBIC-FEET
        StockWeight
            classic name is WEIGHT
        NumberOfPieces
            classic name is PIECES
        NumberOfStops     is Numeric size 6
            classic name is STOPS
        NumberOfDocuments
            classic name is DOCUMENTS
		Active
        InUse             is Boolean

	Attach Rules
		constraint (Active)
			"RouteHeader<RouteHeader>MustBeActive"

    Conditions

        IsUnreleased
        	restricted
            classic name is EDIT
            when (RouteStatus.Unreleased)

        IsReleased
        	restricted
            when (RouteStatus.Released)

    Relations

        RouteStopRel
            one-to-many relation to RouteStop
            Field Mapping uses symbolic key
                related.Company           	= Company
                related.InventoryLocation 	= InventoryLocation
                related.RouteHeader.Date 	= RouteHeader.Date
                related.RouteHeader.Route 	= RouteHeader.Route
        
        RouteDocumentRel
        	one-to-many relation to RouteDocument
        	Field Mapping uses symbolic key
        		related.Company			    = Company
        		related.InventoryLocation   = InventoryLocation
        		related.RouteHeader.Date 	= RouteHeader.Date
                related.RouteHeader.Route 	= RouteHeader.Route




                
	Rule Blocks
	
		ReleaseUnreleaseFunction
			for each RouteStopRel
				invoke CheckCustomerOrderStatusAction each
				
			if (NumberOfDocuments = 0)
				invoke Delete RouteHeader

    Sets

        Set2
            indexed
            Sort Order
                Company
                InventoryLocation
                RouteHeader.Route
                RouteHeader.Date

	Field Rules
	
		RouteStatus
			default to 0
	
    Actions
        FastUpdate is an Update Action
            restricted
			bypass field rules
	StateCycles
		RouteHeaderProcessing is a StateCycle
			state field is RouteStatus
			
			Unreleased is a State
	
				Create is a Create Action
					restricted

				Update is an Update Action
					
				Delete is a Delete Action
					
				Release is an Instance Action
					
					Action Rules
						include ReleaseUnreleaseFunction
						make transition to Released
			
			Released is a State		
	
				Unrelease is an Instance Action			
	
					Action Rules
						include ReleaseUnreleaseFunction
						constraint (!InUse)
							"RouteHasBeenPrintedByPickListPrintProgram"
						make transition to Unreleased	
		
