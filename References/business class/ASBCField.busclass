ASBCField is a BusinessClass
	default label is "BusinessClassField"
    owned by appsetup
    prefix is ASBCF
        
    Ontology
     	symbolic key is ASBCField
    	
    Patterns
    	implements LightweightAuditing
    	disable EffectiveDated
    	disable AuditIndex
    	implements ContextualParent
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder		

    Persistent Fields
        Description     is Alpha size 100
        FieldName       is a BusinessField
        Inactive        is Boolean
        DisplayOrder    is Numeric 5
        	disable Auditing
		ValidActions	is Numeric size 1       
			States
				Both 	value is 0
					default label is "Create_and_Update"
				Create	value is 1
					default label is "Create_Only"
				Update	value is 2
					default label is "Update_Only"

	Transient Fields
		NewDisplayOrder is Numeric 5
	
	Conditions
		IsChangeable
			default label is untranslatable
			when (ASConfiguration.CanEditBaseFields)

	Field Rules
		FieldName
			required
			if (!ASConfiguration.CanEditBaseFields)
					cannot be changed
						"DeliveredConfigurationsCannotBeChanged."
			
		DisplayOrder
			default to ASBCField
			if (!ASConfiguration.CanEditBaseFields)
					cannot be changed
						"DeliveredConfigurationsCannotBeChanged."
		
		Description
			if (Description not entered)
				default to FieldName
			else 
				if (!ASConfiguration.CanEditBaseFields)
					cannot be changed
						"DeliveredConfigurationsCannotBeChanged."
		
		ValidActions
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

		Inactive
			if (Inactive changed)
				if (ASConfiguration.WarnOnDeliveredInactiveSetting)
					confirmation required
						"ThisChangeWillBeOverwrittenTheNextTimeThisDeliveredConfigurationIsUpdated.Proceed?"	
	
	Sets
		ByDisplayOrder
			Sort Order
				ASConfiguration
				ASModule
				ASBusinessClass
				DisplayOrder
				ASBCField
	
		ByFieldName
			indexed
			no duplicates
			Sort Order
				ASConfiguration
				ASModule
				ASBusinessClass	
				FieldName

	Actions
	
		Create is an Action
			Exit Rules
				if (NewDisplayOrder entered)
					invoke Update ASBCField

		CreateCopy is a Create Action
			restricted

		Update is an Action
				
		Delete is an Action
			valid when (IsChangeable)
		
		Purge is a Purge Action
			valid when (IsChangeable)

		Copy is an Instance Action
			restricted
			
			Parameters
				NewConfiguration 	is an ASConfiguration
				NewModule   		is an ASModule
				NewBusinessClass 	is an ASBusinessClass
			Parameter Rules
				NewConfiguration
					required
					
				NewModule
					required

				NewBusinessClass
					required		
			Action Rules

				invoke CreateCopy ASBCField
					invoked.ASConfiguration 	= NewConfiguration
					invoked.ASModule			= NewModule
					invoked.ASBusinessClass		= NewBusinessClass
					invoked.Description 		= Description
					invoked.FieldName			= FieldName
					invoked.DisplayOrder		= DisplayOrder
					invoked.ValidActions		= ValidActions

					if (Inactive)
                    	invoked.Inactive 		= Inactive	
