PaymentCreationDefaults is a BusinessClass
    owned by ap
    prefix is PCD
    Ontology
        symbolic key is PaymentCreationDefaults
        
    Persistent Fields

        PaymentDate             is Boolean
        PaymentDays             is Numeric size 3
        EffectiveDate           is Boolean
        EffectiveDays           is Numeric size 3
        DocCurrency             is AlphaUpper size 1
            States
                AccountCurrency value is "A"
                InvoiceCurrency value is "I"
        InclComments            is AlphaUpper size 1
            default label is "IncludeComments"
            States
                Yes value is "Y"
                No  value is "N"
        UseAp150SortOpt         is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"

        Active
        SerialNum               is Numeric size 8
        RecreateOption          is AlphaUpper size 1

        InclFaxEmail            is AlphaUpper size 1
            States
                None            value is "N"			
                EmailOnly       value is "E"
                FaxNumberOnly   value is "F"
                BothEmailAndFax value is "B"
                    default label is "Both Email and Fax"

		CombineOutputByAccount	is Boolean
			default label is "CombineOutputByBankAccount"
		SuppressNoPaymentError  is Boolean
			default label is "SuppressNoPaymentError"		
		PayablesElectronicTransferID		

        PrintRemit              is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        CreatePrenot            is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        BacsSerialNbr           is AlphaRight size 6
        PrintComments           is AlphaUpper size 1
            States
                No  value is "N"
                Yes value is "Y"
        SendBankType            is Numeric size 1
            States
                BankId value is 0
                    default label is "Bank ID"
                Other  value is 1
        CreateLabelsOpt         is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        Format                  is AlphaUpper size 1
            States
                Ppd value is "P"
                    default label is "PPD"
                PpdPlus value is "R"
                    default label is "PPD+"
                Ccd value is "C"
                    default label is "CCD"
                CcdPlus value is "D"
                    default label is "CCD+"
                Iat value is "I"
                    default label is "IAT"
        BalancedTape            is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        Healthcare				is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"



		ImmediateOriginName	 is AlphaUpper size 1
			States
				Bank value is "B"
				PayGroup value is "P"

    Field Rules
    	VendorGroup
			required
		PayGroup
			required
		BankTransactionCode
			required
        CashCode
            if (CashCodeGroup !entered)
                required
                    "CashCodeOrCashCodeGroupRequired"
            else
                if (CashCode entered)
                    constraint (CashCodeGroup !entered)
                        "CannotEnterBothCashCodeAndCashCodeGroup"
		EffectiveDays
            constraint (EffectiveDate)
                "MustSelectCurrentDateForEffectiveDateToEnterAdjustmentDays"
        PaymentDays
            constraint (PaymentDate)
                "MustSelectCurrentDateForPaymentDateToEnterAdjustmentDays"

    Relations
    	CashPaymentFormatRel
			one-to-one relation to CashPaymentFormat
			Field Mapping uses symbolic key
				related.CashManagementGroup 				   = DerivedCashManagementGroup
				related.CashCode               				   = DerivedCashCode
				related.CashPaymentFormat.BankTransactionCode  = BankTransactionCode
				related.CashPaymentFormat.PayablesProcessGroup = PayablesProcessGroup

     	LocalCashPaymentFormatRel
			one-to-one relation to CashPaymentFormat
			Field Mapping uses symbolic key
				related.CashManagementGroup 				   = LocalCashCode.CashManagementGroup
				related.CashCode               				   = LocalCashCode
				related.CashPaymentFormat.BankTransactionCode  = BankTransactionCode
				related.CashPaymentFormat.PayablesProcessGroup = PayablesProcessGroup

        CashCodeGroupDetailsRel
            one-to-many relation to CashCodeGroupDetail    
            Field Mapping uses symbolic key
 				related.CashManagementGroup 				   = CashCodeGroup.CashManagementGroup
				related.CashCodeGroup               		   = CashCodeGroup 

    Sets

    Conditions
        RecordExists
            when (PaymentCreationDefaults exists)

        UseForPaymentClearingCreation
            when (CashPaymentFormatRel.PaymentFormat.AchFile
            or    CashPaymentFormatRel.PaymentFormat.BacsFile)

        SecurityGroupAllowsAccess
            when ((CashCode entered
            and CashCode.SecurityGroupAllowsAccess)
            or (CashCodeGroup entered
            and CashCodeGroup.SecurityGroupAllowsAccess)
            or (CashCode !entered                               
            and CashCodeGroup !entered))
        




    Derived Fields
        DerivedPaymentDate is a DerivedField
            type is Date
            if (PaymentDate)
                return current corporate date + PaymentDays as days 
        
        DerivedEffectiveDate is a DerivedField
            type is Date
            if (EffectiveDate)
                return current corporate date + EffectiveDays as days

        DerivedCashManagementGroup is a DerivedField
            type is like CashManagementGroup
            if (CashCode entered)
                return CashCode.CashManagementGroup
            else 
                return CashCodeGroup.CashManagementGroup

        DerivedCashCode is a DerivedField
            type is like CashCode
            if (CashCode entered)
                return CashCode
            if (CashCodeGroup entered)
                for each CashCodeGroupDetailsRel
                    LocalCashCode = each.CashCode
                    if (LocalCashPaymentFormatRel exists)
                        return LocalCashCode
                        end for each 

    Local Fields
        LocalCashCode is a CashCode

    Attach Rules
        constraint (Active)
            "PaymentCreationDefaultsIsInactive"

    Rule Blocks

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Purge Action

