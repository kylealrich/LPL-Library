IIHInventoryLocation is a BusinessClass
	owned by InventoryIntelligence
	default label is "InventoryIntelligenceInventoryLocation"
	
	prefix is IIHI
	
	Ontology
		symbolic key is IIHInventoryLocation
		
	
	Patterns
		
		disable Auditing
		implements CreateStamp
		implements UpdateStamp
	
	Persistent Fields
		LocationType                              is AlphaUpper size 1
			States
				ExchangeCart      value is "E"
				ParLocation       value is "P"
				InventoryLocation value is blank
		Name
		ReferenceInventoryValue		is like InternationalAmount
		CurrentInventoryValue		is like InternationalAmount
		TargetInventoryValue		is like InternationalAmount
		UnderReviewValue 			is like InternationalAmount
		AchievedSoFarValue			is like InternationalAmount
		
		GeneralItemCount 				is Numeric size 10
		GeneralRemovalCandidatesCount 	is Numeric size 10
		GeneralRemovalCandidatesValue 	is like InternationalAmount
		GeneralEligibleIemCount			is Numeric size 10
			default label is "GeneralEligibleItemCount"
		GeneralEligibleIemValue			is like InternationalAmount
			default label is "GeneralEligibleItemValue"
		
		DecreaseItemCount			is Numeric size 10
		DecreaseReferenceValue 		is like InternationalAmount
		DecreaseTargetValue 		is like InternationalAmount
		DecreasePotentialSavings 	is like InternationalAmount
		DecreaseAchievedSoFar	 	is like InternationalAmount
		
		IncreaseItemCount				is Numeric size 10
		IncreaseReferenceValue 			is like InternationalAmount
		IncreaseTargetValue 			is like InternationalAmount
		IncreaseCostOfImprovedService 	is like InternationalAmount
		IncreaseCostSoFar	 			is like InternationalAmount

		CumulativeCostForAllItems 			is like InternationalAmount					
			default label is "CumulativeCost"
		CumulativeSavingForAllItems			is like InternationalAmount
			default label is "CumulativeSaving"
		CumulativeCostForNonConsignment		is like InternationalAmount
			default label is "CumulativeCost"
		CumulativeSavingForNonConsignment	is like InternationalAmount
			default label is "CumulativeSaving"
		AnalysisDate						is a DateRange
		
	Derived Fields	
		CumulativeChangeForNonConsignment is a DerivedField			
			type is like InternationalAmount
			default label is "CumulativeChange"
			return (CumulativeCostForNonConsignment - CumulativeSavingForNonConsignment)

		CumulativeChange is a DerivedField						
			type is like InternationalAmount
			return (CumulativeCostForAllItems - CumulativeSavingForAllItems)

		HasRecommendations is a DerivedField
			type is Boolean
			restricted
			if ((DecreaseItemCount + IncreaseItemCount) > 0)
				return true
			else
				return false
				
		DerivedAnalysisBeginDate is a DerivedField
        	type is Numeric size 2
        	return AnalysisDate.Begin day
        	
        DerivedAnalysisBeginMonth is a DerivedField
        	type is Alpha size 4
        	return AnalysisDate.Begin month short name
        	
        DerivedAnalysisBeginYear is a DerivedField
        	type is Numeric size 4
        	return AnalysisDate.Begin year
        	
		DerivedAnalysisEndDate is a DerivedField
        	type is Numeric size 2
        	return AnalysisDate.End day
        	
        DerivedAnalysisEndMonth is a DerivedField
        	type is Alpha size 4
        	return AnalysisDate.End month short name
        	
        DerivedAnalysisEndYear is a DerivedField
        	type is Numeric size 4
        	return AnalysisDate.End year
        	
        AnalysisDateString is a StringField
        	type is Alpha size 25
        	DerivedAnalysisBeginDate
        	"-"
        	DerivedAnalysisBeginMonth
        	"-"
        	DerivedAnalysisBeginYear
        	"::"
        	DerivedAnalysisEndDate
        	"-"
        	DerivedAnalysisEndMonth
        	"-"
        	DerivedAnalysisEndYear
        	
        ItemAnalysisDateStr is a StringField
            type is Alpha size 50
            "Analysis"
            blank
            "Window"
            blank
            AnalysisDateString
					
	Field Rules

	Conditions
			
		IsPerpetualLocation							
			restricted
			when (LocationType.InventoryLocation)
		
		ParOrExchangeCart							
			restricted
			when (LocationType.ExchangeCart
			or    LocationType.ParLocation)

		IncludeConsignment					
			restricted
			when (IIHGlobalConfigurationRel.ConsignmentInValuation)

		DoNotIncludeConsignment				
			restricted
			when (!IIHGlobalConfigurationRel.ConsignmentInValuation)
			
		ProcessorWithViewOrEditAccess				
			restricted
			when (ProcessorWithViewOrEditAccessRel exists)

		EditAccess
			restricted
			when (ProcessorWithViewOrEditAccessRel.Access.Edit
			or (InventoryIntelligenceProcessorRel not exists
			and IIHGlobalConfigurationRel.DefaultGlobalAccess.Edit)) 

		ViewAccess
			restricted
			when (ProcessorWithViewOrEditAccess 
			or (InventoryIntelligenceProcessorRel not exists
			and (IIHGlobalConfigurationRel.DefaultGlobalAccess.Edit
			or IIHGlobalConfigurationRel.DefaultGlobalAccess.View)))
	
	Relations
		ActorContextIIHLocationsOverviewRel
			one-to-one relation to IIHLocationsOverview
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				
		IIHItemLocationsRel
			one-to-many relation to IIHItemLocation
			Field Mapping uses symbolic key
				related.Company           = Company
				related.InventoryLocation = IIHInventoryLocation

		ProcessorWithViewOrEditAccessRel			
			one-to-one relation to InventoryIntelligenceProcessorAccessRights
			Field Mapping uses symbolic key
				related.HROrganization                 = actor.agent(Employee).HROrganization
				related.InventoryIntelligenceProcessor = actor.agent(Employee).Employee
				related.InventoryIntelligenceProcessorAccessRights.InventoryCompany = Company
				related.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = IIHInventoryLocation

		IIHGlobalConfigurationRel						
			one-to-one relation to IIHGlobalConfiguration
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup

		InventoryIntelligenceProcessorRel
			one-to-one relation to InventoryIntelligenceProcessor
			Field Mapping uses symbolic key
				related.HROrganization 					= actor.agent(Employee).HROrganization
				related.InventoryIntelligenceProcessor	= actor.agent(Employee).Employee
				
	Sets
		ByRemovalCandidates
			Sort Order
				GeneralRemovalCandidatesCount
				Company
				IIHInventoryLocation
		
		ByUnderReviewValue
			Sort Order
				UnderReviewValue
				Company
				IIHInventoryLocation
				
		ByDecreaseItemCount
			Sort Order
				DecreaseItemCount
				Company
				IIHInventoryLocation

		ByPotentialSavings
			Sort Order
				DecreasePotentialSavings
				Company
				IIHInventoryLocation
				
		ByIncreaseItemCount
			Sort Order
				IncreaseItemCount
				Company
				IIHInventoryLocation
				
		ByImprovedService
			Sort Order
				IncreaseCostOfImprovedService
				Company
				IIHInventoryLocation

		ByCumulativeSavingForNonConsignment				
			Sort Order
				CumulativeSavingForNonConsignment
				Company
				IIHInventoryLocation

		ByCumulativeSaving								
			Sort Order
				CumulativeSavingForAllItems
				Company
				IIHInventoryLocation

	Actions
		CalculateRecommendationsSummary is a Set Action
			restricted
			Local Fields
				LocalPerpetualLocationsCount			is Numeric size 10
				LocalParLocationsCount					is Numeric size 10
				LocalPerpetualItemCount 				is Numeric size 10
				LocalParItemCount 						is Numeric size 10
				LocalPerpetualRefInvValue				is like InternationalAmount
				LocalParRefInvValue						is like InternationalAmount
				LocalPerpetualTargetInvValue			is like InternationalAmount
				LocalParTargetInvValue					is like InternationalAmount	
				LocalPerpetualEligibleIemCount			is Numeric size 10
				LocalParEligibleIemCount				is Numeric size 10
				LocalPerpetualEligibleIemValue			is like InternationalAmount
				LocalParEligibleIemValue				is like InternationalAmount
				LocalPerpetualRemovalCandidatesCount 	is Numeric size 10
				LocalParRemovalCandidatesCount 			is Numeric size 10
				LocalPerpetualRemovalCandidatesValue 	is like InternationalAmount
				LocalParRemovalCandidatesValue 			is like InternationalAmount
				
				LocalPerpetualDecreaseItemCount			is Numeric size 10
				LocalPerpetualDecreaseReferenceValue 	is like InternationalAmount
				LocalPerpetualDecreaseTargetValue 		is like InternationalAmount
				LocalPerpetualDecreasePotentialSavings 	is like InternationalAmount
				LocalPerpetualDecreaseAchievedSoFar	 	is like InternationalAmount
				
				LocalParDecreaseItemCount				is Numeric size 10
				LocalParDecreaseReferenceValue 			is like InternationalAmount
				LocalParDecreaseTargetValue 			is like InternationalAmount
				LocalParDecreasePotentialSavings 		is like InternationalAmount
				LocalParDecreaseAchievedSoFar	 		is like InternationalAmount
				
				LocalPerpetualIncreaseItemCount			is Numeric size 10
				LocalPerpetualIncreaseReferenceValue 	is like InternationalAmount
				LocalPerpetualIncreaseTargetValue 		is like InternationalAmount
				LocalPerpetualCostOfImprovedService 	is like InternationalAmount
				LocalPerpetualIncreaseCostSoFar	 		is like InternationalAmount
				
				LocalParIncreaseItemCount				is Numeric size 10
				LocalParIncreaseReferenceValue 			is like InternationalAmount
				LocalParIncreaseTargetValue 			is like InternationalAmount
				LocalParIncreaseCostOfImprovedService 	is like InternationalAmount
				LocalParIncreaseCostSoFar	 			is like InternationalAmount
				
			Instance Selection
				where (Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
				
			Action Rules
				Set Rules
					Exit Rules
						invoke Update ActorContextIIHLocationsOverviewRel
							invoked.FinanceEnterpriseGroup				= actor.context.FinanceEnterpriseGroup
							invoked.PerpetualLocationsCount 			= LocalPerpetualLocationsCount
							invoked.PerpetualItemCount 					= LocalPerpetualItemCount
							invoked.PerpetualRefInvValue 				= LocalPerpetualRefInvValue
							invoked.PerpetualTargetInvValue				= LocalPerpetualTargetInvValue
							invoked.PerpetualEligibleIemCount 			= LocalPerpetualEligibleIemCount
							invoked.PerpetualEligibleIemValue 			= LocalPerpetualEligibleIemValue
							invoked.PerpetualRemovalCandidatesCount		= LocalPerpetualRemovalCandidatesCount
							invoked.PerpetualRemovalCandidatesValue 	= LocalPerpetualRemovalCandidatesValue
							invoked.PerpetualDecreaseItemCount			= LocalPerpetualDecreaseItemCount
							invoked.PerpetualDecreaseReferenceValue 	= LocalPerpetualDecreaseReferenceValue
							invoked.PerpetualDecreaseTargetValue 		= LocalPerpetualDecreaseTargetValue
							invoked.PerpetualDecreasePotentialSavings 	= LocalPerpetualDecreasePotentialSavings
							invoked.PerpetualDecreaseAchievedSoFar 		= LocalPerpetualDecreaseAchievedSoFar
							invoked.PerpetualIncreaseItemCount 			= LocalPerpetualIncreaseItemCount
							invoked.PerpetualIncreaseReferenceValue 	= LocalPerpetualIncreaseReferenceValue
							invoked.PerpetualIncreaseTargetValue		= LocalPerpetualIncreaseTargetValue
							invoked.PerpetualCostOfImprovedService 		= LocalPerpetualCostOfImprovedService
							invoked.PerpetualIncreaseCostSoFar 			= LocalPerpetualIncreaseCostSoFar
							invoked.ParLocationsCount					= LocalParLocationsCount					
							invoked.ParItemCount						= LocalParItemCount
							invoked.ParRefInvValue						= LocalParRefInvValue
							invoked.ParTargetInvValue					= LocalParTargetInvValue						
							invoked.ParEligibleIemCount					= LocalParEligibleIemCount		
							invoked.ParEligibleIemValue					= LocalParEligibleIemValue		
							invoked.ParRemovalCandidatesCount 			= LocalParRemovalCandidatesCount	
							invoked.ParRemovalCandidatesValue 			= LocalParRemovalCandidatesValue		
							invoked.ParDecreaseItemCount				= LocalParDecreaseItemCount				
							invoked.ParDecreaseReferenceValue 			= LocalParDecreaseReferenceValue		
							invoked.ParDecreaseTargetValue 				= LocalParDecreaseTargetValue		
							invoked.ParDecreasePotentialSavings 		= LocalParDecreasePotentialSavings		
							invoked.ParDecreaseAchievedSoFar	 		= LocalParDecreaseAchievedSoFar		
							invoked.ParIncreaseItemCount				= LocalParIncreaseItemCount	
							invoked.ParIncreaseReferenceValue 			= LocalParIncreaseReferenceValue		
							invoked.ParIncreaseTargetValue 				= LocalParIncreaseTargetValue	
							invoked.ParIncreaseCostOfImprovedService 	= LocalParIncreaseCostOfImprovedService
							invoked.ParIncreaseCostSoFar				= LocalParIncreaseCostSoFar
						log untranslatable:"RecommendationsEngineRunDate:<current timestamp>;PerpetualRecommendations:<ActorContextIIHLocationsOverviewRel.PerpetualRecommendationsCount>;ParRecommendations:<ActorContextIIHLocationsOverviewRel.ParRecommendationsCount>;RemovalCandidates:<ActorContextIIHLocationsOverviewRel.RemovalCandidatesCount>"
				Instance Rules
					if (LocationType.InventoryLocation)
						LocalPerpetualLocationsCount 			= LocalPerpetualLocationsCount + 1
						LocalPerpetualItemCount 				= LocalPerpetualItemCount + GeneralItemCount
						LocalPerpetualRefInvValue 				= LocalPerpetualRefInvValue + ReferenceInventoryValue
						LocalPerpetualTargetInvValue			= LocalPerpetualTargetInvValue + TargetInventoryValue
						LocalPerpetualEligibleIemCount 			= LocalPerpetualEligibleIemCount + GeneralEligibleIemCount
						LocalPerpetualEligibleIemValue 			= LocalPerpetualEligibleIemValue + GeneralEligibleIemValue
						LocalPerpetualRemovalCandidatesCount 	= LocalPerpetualRemovalCandidatesCount + GeneralRemovalCandidatesCount
						LocalPerpetualRemovalCandidatesValue 	= LocalPerpetualRemovalCandidatesValue + GeneralRemovalCandidatesValue
						LocalPerpetualDecreaseItemCount			= LocalPerpetualDecreaseItemCount + DecreaseItemCount
						LocalPerpetualDecreaseReferenceValue 	= LocalPerpetualDecreaseReferenceValue + DecreaseReferenceValue
						LocalPerpetualDecreaseTargetValue 		= LocalPerpetualDecreaseTargetValue + DecreaseTargetValue
						LocalPerpetualDecreasePotentialSavings 	= LocalPerpetualDecreasePotentialSavings + DecreasePotentialSavings
						LocalPerpetualDecreaseAchievedSoFar 	= LocalPerpetualDecreaseAchievedSoFar + DecreaseAchievedSoFar
						LocalPerpetualIncreaseItemCount 		= LocalPerpetualIncreaseItemCount + IncreaseItemCount
						LocalPerpetualIncreaseReferenceValue 	= LocalPerpetualIncreaseReferenceValue + IncreaseReferenceValue
						LocalPerpetualIncreaseTargetValue 		= LocalPerpetualIncreaseTargetValue + IncreaseTargetValue
						LocalPerpetualCostOfImprovedService 	= LocalPerpetualCostOfImprovedService + IncreaseCostOfImprovedService
						LocalPerpetualIncreaseCostSoFar 		= LocalPerpetualIncreaseCostSoFar + IncreaseCostSoFar
					else
						LocalParLocationsCount 					= LocalParLocationsCount + 1				
						LocalParItemCount 						= LocalParItemCount + GeneralItemCount
						LocalParRefInvValue 					= LocalParRefInvValue + ReferenceInventoryValue
						LocalParTargetInvValue					= LocalParTargetInvValue + TargetInventoryValue				
						LocalParEligibleIemCount 				= LocalParEligibleIemCount + GeneralEligibleIemCount				
						LocalParEligibleIemValue				= LocalParEligibleIemValue + GeneralEligibleIemValue			
						LocalParRemovalCandidatesCount 			= LocalParRemovalCandidatesCount + GeneralRemovalCandidatesCount	
						LocalParRemovalCandidatesValue 			= LocalParRemovalCandidatesValue + GeneralRemovalCandidatesValue		
						LocalParDecreaseItemCount 				= LocalParDecreaseItemCount + DecreaseItemCount		
						LocalParDecreaseReferenceValue 			= LocalParDecreaseReferenceValue + DecreaseReferenceValue		
						LocalParDecreaseTargetValue 			= LocalParDecreaseTargetValue +	DecreaseTargetValue	
						LocalParDecreasePotentialSavings 		= LocalParDecreasePotentialSavings + DecreasePotentialSavings		
						LocalParDecreaseAchievedSoFar 			= LocalParDecreaseAchievedSoFar + DecreaseAchievedSoFar		
						LocalParIncreaseItemCount 				= LocalParIncreaseItemCount + IncreaseItemCount			
						LocalParIncreaseReferenceValue 			= LocalParIncreaseReferenceValue + IncreaseReferenceValue	
						LocalParIncreaseTargetValue 			= LocalParIncreaseTargetValue +	IncreaseTargetValue	
						LocalParIncreaseCostOfImprovedService 	= LocalParIncreaseCostOfImprovedService + IncreaseCostOfImprovedService
						LocalParIncreaseCostSoFar 				= LocalParIncreaseCostSoFar + IncreaseCostSoFar

				
					
		Import is an Import Action
			bod type is II_FSM_InventoryLocation
      		Parameters
            	InputData is CSVText
      		Action Rules
            	dbimport InputData into IIHInventoryLocation
            	
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Delete is a Delete Action
			restricted
		FastUpdate is an Update Action
				restricted
				bypass field rules
		
		Purge is a Purge Action
			restricted
			bypass relational integrity rules
		
		PurgeIIHInventoryLocation is a Set Action
			restricted
			run in background
			Action Rules
				Instance Rules
					invoke Purge
	
