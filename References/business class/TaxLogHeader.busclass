TaxLogHeader is a BusinessClass
    owned by tx
    prefix is TxLog

    Ontology
        symbolic key is TaxLogHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		SystemCode				is like GeneralLedgerSystemCode
        LogTaxEntity			is like TaxEntity
        LogThirdParty			is a ThirdParty
      	LogCompany				is like GeneralLedgerCompany
        LogVendor				is like Vendor
		LogCustomer				is like Customer
        LogLocation				is like Location
		LogInvoice				is like Invoice
		LogToAddress			is like PostalAddressV2
		LogFromAddress			is like PostalAddressV2
		LogPOAAddress			is like PostalAddressV2
		JurisdictionResultsKey 	is Alpha 100 
		CreatedBy				is an Operator 
			holds pii
		CreateDate				is Date	 
        LastMessageSequence 	is a LineNumber
        MessageRichText			is a CommentText
        RelatedObjectReference	is BusinessObjectReference 
        WebServiceError 		is Boolean
		WebServiceErrorMessage 	is Text
      	VertexRequestExecuted	is Alpha 30 				  
		LogPostToJournal		is Boolean			 		  
      	LogCreatedTimestamp 	is TimeStamp	  		  	  
      	LogVoucherNumber		is like VoucherNumber		  
		LogDocumentSequenceId	is a VertexDocumentSequenceId 
		LogToTaxCode			is like TaxCode				  
		LogFromTaxCode			is like TaxCode               
		LogCallingRoutine		is like Description			  
		TraceLogRichText		is RichText			  	  	  
		UniqueIdOfRelatedObject is UniqueID					  
      	HTTPInfo				is Text						  
 		LogIsAudited			is Boolean			 		  
 		LogOneSourceVersionType	is Alpha 1			 		  
			States											  
				Original		value is blank				  
				Generic			value is "G"				  
	Transient Fields
		DisplayMessages is a CommentText
			derive value from all TaxLogMessages.MessageLine

    Context Fields
		CreateDateRange			is a DateRange
					
	Field Rules
		CreatedBy
			default to actor
		CreateDate
			default to current corporate date	
		LogCreatedTimestamp
			default to current timestamp	
			
	Relations
        TaxLogMessages	 
            one-to-many relation to TaxLogMessage
			delete cascades
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection	
				where (related.TaxLogHeader 	= TaxLogHeader)		
				
  		JurisdictionResultsRel	
			one-to-many relation to JurisdictionResults					 

			Field Mapping uses symbolic key
				related.VertexConfig									= FinanceEnterpriseGroup 
			Instance Selection	
				where (related.JurisdictionResults.LookupKey 			= TaxLogHeader)


  		TaxLineItemsRel	
			one-to-many relation to TaxLineItem					 
			Field Mapping uses ByUniqueIdOfRelatedObject
				related.FinanceEnterpriseGroup							= FinanceEnterpriseGroup 
			Instance Selection	
				where (related.TaxLineItem.UniqueIdOfRelatedObject				= UniqueIdOfRelatedObject)

  		TaxTransactionsRel	
			one-to-many relation to TaxTransaction					 
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup 
			Instance Selection	
				where (related.UniqueID 		= UniqueIdOfRelatedObject)	
				

  	Conditions
		WithinCreateDateRange
			restricted
			when (CreateDateRange not entered
			or	 (CreateDateRange entered
			and	  CreateDate within CreateDateRange))

		JurisdictionResultsExist
			restricted
			when (JurisdictionResultsRel exist)

		TaxLogMessagesExist		
			restricted
			when (TaxLogMessages exist)
			
		TaxLineItemsExist		
			restricted
			when (TaxLineItemsRel exist)
				
		TaxTransactionsRelExist	
			restricted
			when (TaxTransactionsRel exist)			
									  
	Derived Fields
		MessageHeaderKey is a StringField
			type is Alpha 100
			restricted
			SystemCode
			"-"
			current timestamp

		DerivedVertexRequestExecuted is a DerivedField
			type is Alpha 30
			if (LogThirdParty.VertexOSeries)	
				return VertexRequestExecuted
			return blank
				
		RequestExecutedLabel is a DerivedField  
			type is Alpha 15 
			if (LogThirdParty.VertexOSeries)	
				return VertexLabel
			return blank	 

		DerivedPostToJournalValue is a DerivedField			 
			type is Boolean
			if (LogThirdParty.VertexOSeries)	
				return LogPostToJournal
			if (LogThirdParty.Sabrix)	
				return LogIsAudited	
			return blank

		PostedToJournalLabel is a DerivedField  
			type is Alpha 10 
			if (LogThirdParty.VertexOSeries)	
				return VertexLabel
			if (LogThirdParty.Sabrix)	
				return OneSourceLabel				
			return blank	 

		VertexLabel is a MessageField                     
			"Vertex"		

		OneSourceLabel is a MessageField						 
			"OneSource" 		 
			
		PostToJournalLabel is a MessageField						 
			"PostedToJournal" 		 

		DerivedOneSourceVersionValue is a DerivedField			 
			type is Alpha 8
			if (LogThirdParty.Sabrix)	
				return LogOneSourceVersionType	
			return blank

		DerivedOneSourcVersionLabel is a DerivedField  
			type is MessageField
			if (LogThirdParty.Sabrix)	
				return OneSourceVersionLabel				
			return blank	 
		OneSourceVersionLabel is a MessageField						 
			"OneSourceVersionType"

		DerivedIsAuditedValue is a DerivedField			 
			type is Boolean
			if (LogThirdParty.Sabrix)	
				return LogIsAudited
			if (LogThirdParty.VertexOSeries)	
				return LogPostToJournal								
			return blank	 
			
		OneSourceIsAuditedLabel is a MessageField						 
			"IsAudited"

		DerivedShortDescription is a DerivedField
			type is Alpha 60 
			return HTTPInfo
																		
    Sets





  
		ByRelatedObjectUniqueID
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				UniqueIdOfRelatedObject

		ByTaxEntity					

			Sort Order
				FinanceEnterpriseGroup
				LogTaxEntity 	
				RelatedObjectReference
				TaxLogHeader			
												
	Actions
		Create is an Action
		Update is an Action
		Delete is an Action 
		Purge is a Purge Action
			restricted
					
		EmailTaxLogMessages is an Instance Action

			Parameters
				ToEmail 			is an EmailAddressMulti 
					holds pii
				FromEmail 			is an EmailAddress 
					holds pii
				Subject 	   		is Text
				EmailContents   	is RichText

			Parameter Rules
				ToEmail
					required									
				FromEmail
					required
					initial value is actor.agent(Employee).Employee.EmployeeWorkEmailAddress
				Subject
					required
					initial value is "TaxLogMessages " + " CreatedBy " + actor + " TodaysDate " + current timestamp 
				EmailContents
					required
					initial value is MessageRichText


					if (MessageRichText not entered)
						default to all TaxLogMessages.MessageLine
					else
						default to MessageRichText	


					
            Action Rules                                    
				send email
					to ToEmail
					from FromEmail
					subject "<Subject>"
					Contents
						"<EmailContents>"	


		UpdateMessagesToRichText is an Instance Action	 
 			restricted
           	Action Rules 		
				for each TaxLogMessages
					MessageRichText	+= each.MessageLine	 



		PurgeTaxLog is a Set Action												
			completion message is "<CompletionMessage>"

			run in background		
			Parameters 
				PrmFinEntGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmTaxEntity		is a TaxEntity
					context of PrmFinEntGroup
					default label is "TaxEntity"
				PrmCompany			is a GeneralLedgerCompany	
					default label is "Company"	
				PrmSystemCode		is a GeneralLedgerSystemCode	 
					context of PrmFinEntGroup
					default label is "SystemCode"
				PrmCreateDate		is Date
					default label is "CreateDateIsLessThan"
				PrmCreatedBy		is an Operator 
					holds pii
					default label is "Operator" 
				PrmCreatedDateYear	is a Year
					default label is "YearCreated"
			Parameter Rules
				PrmFinEntGroup
					initial value is actor.context.FinanceEnterpriseGroup	
					required	
				PrmCreatedBy
					initial value is actor
				PrmTaxEntity
					initial value is LogTaxEntity				
			Local Fields
				CompletionMessage	is Alpha 150
				RecordCount			is Numeric 10
			Instance Selection			
				where (FinanceEnterpriseGroup = PrmFinEntGroup			
				and   (CreateDate <= PrmCreateDate						
				or     PrmCreateDate not entered)						
				and   (LogTaxEntity = PrmTaxEntity						
				or     PrmTaxEntity not entered) 						
				and   (SystemCode = PrmSystemCode						
				or     PrmSystemCode not entered)						
			    and   (PrmCompany = LogCompany							
			    or     PrmCompany not entered)							
			    and   (PrmCreatedBy = CreatedBy							
			    or     PrmCreatedBy not entered)						
			    and   (PrmCreatedDateYear = CreateDate year     		
			    or     PrmCreatedDateYear not entered))					
			Sort Order

			Accumulators



			Action Rules								
				Empty Set Rules
					CompletionMessage = "PurgeProcessComplete;NoRecordsFoundToPurge"
				
				Set Rules
					Entrance Rules

					Exit Rules
						CompletionMessage = "PurgeProcessComplete:<RecordCount>...RecordsProcessed"
						
				Instance Rules
					increment RecordCount
					invoke Purge




		PurgeTaxLogMessages is a Set Action												 
			completion message is "<CompletionMessage>"

			run in background		 
			Parameters 
				PrmFinEntGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmTaxEntity		is a TaxEntity

					default label is "TaxEntity"
				PrmCreateDate		is Date
					default label is "CreateDateIsLessThan"
				PrmCreatedDateYear	is a Year
					default label is "YearCreated"					
			Parameter Rules
				PrmFinEntGroup  
					initial value is actor.context.FinanceEnterpriseGroup	
					required	
				PrmTaxEntity
					initial value is LogTaxEntity
				PrmCreateDate
					initial value is current corporate date
					default to current corporate date								
			Local Fields
				CompletionMessage	is Alpha 150
			Instance Selection			
				where (FinanceEnterpriseGroup = PrmFinEntGroup			 
				and   (CreateDate <= PrmCreateDate						 
				or     PrmCreateDate not entered)		
				and   (LogTaxEntity = PrmTaxEntity						 	
				or     PrmTaxEntity not entered) 						 								 				 			    
			    and   (PrmCreatedDateYear = CreateDate year     		 
			    or     PrmCreatedDateYear not entered))				 
			Sort Order

			Accumulators


 
			Action Rules								
				Empty Set Rules
					CompletionMessage = "PurgeProcessComplete;NoRecordsFoundToPurge"
				
				Set Rules
					Entrance Rules

					Exit Rules
						CompletionMessage = "PurgeProcessComplete:<RecordCount>...RecordsProcessed"
						
				Instance Rules					 
					invoke Purge TaxLogMessages



																



		DeleteFixBaseAmountEntries is a Set Action	
			completion message is "<RecordCount>_RecordsProcessed"
			run in background		 
			Parameters 
				PrmFinEntGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"			
				PrmTaxEntity		is a TaxEntity
					default label is "TaxEntity"
				PrmSystemCode		is AlphaUpper size 2	 
		            States
		                Billing            value is "BL"
		                BillingAndRevenue  value is "BR"
		                CashLedger         value is "CB"
		                CashReceipts       value is "CR"
		                AccountsPayable    value is "AP"
							default label is "Payables"
		                AccountsReceivable value is "AR"
							default label is "Receivables"
		            default label is "SystemCode"        			
				PrmTaxCode			is a TaxCode	
					default label is "TaxCode"	
				CutOffDateRange is a DateRange	  
			Parameter Rules
				PrmFinEntGroup
					initial value is FinanceEnterpriseGroup

					required	
				PrmTaxEntity
					required	
				CutOffDateRange
					if (CutOffDateRange.End not entered)
						required
							"TaxPointEndDateMustBeRequired"
					constraint (CutOffDateRange.End entered)	 
						"TaxPointEndDateMustBeEntered"	
			Local Fields
				CompletionMessage	is Alpha 150
				RecordCount			is Numeric 10
			Instance Selection			
				where (TaxLogHeader[1:29] = "TaxTransaction.FixBaseAmounts" 
				and   (LogTaxEntity 	  = PrmTaxEntity
				and   (CutOffDateRange not entered
				or	  (CutOffDateRange entered
				and	   CreateDate within CutOffDateRange))  							
			    and   ((PrmSystemCode entered
			    and    PrmSystemCode = SystemCode)							 
			    or     PrmSystemCode not entered))				 				 					 						 
			    and   ((PrmTaxCode entered
			    and    PrmTaxCode = LogToTaxCode)							 
			    or     PrmTaxCode not entered))
			Sort Order

			Accumulators



			Action Rules								
				Empty Set Rules
					CompletionMessage = "DeleteFixBaseAmountEntriesComplete;NoRecordsFoundToPurge"	 
				
				Set Rules
					Entrance Rules
					Exit Rules
						
				Instance Rules
					increment RecordCount
					invoke Purge
