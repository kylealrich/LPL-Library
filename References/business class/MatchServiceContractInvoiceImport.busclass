MatchServiceContractInvoiceImport is a BusinessClass
    owned by ma
    prefix is MSCII
    sql name is MatchServCntrInvImport

    Ontology
        symbolic key is MatchServiceContractInvoiceImport

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
    	RunGroup
        Company						  is a MatchCompany
        Vendor
        OldVendor
        Invoice
        Suffix
        EDINumber
            classic name is EDI-NBR
        Contract 
        RecordInError                   is Boolean

	Local Fields
		NewMatchServiceContractInvoice  is a MatchServiceContractInvoice view

		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150

		LocalResultID					is like PayablesInvoiceInterfaceResult
			
	Context Fields
		ContextMatchInvoice is a MatchInvoiceImport

	Field Rules
		RunGroup
			required
			default to ContextMatchInvoice.RunGroup
			initial value is ContextMatchInvoice.RunGroup
	
		Invoice
			required
			initial value is ContextMatchInvoice.Invoice
		
		EDINumber
			initial value is ContextMatchInvoice.EDINumber

		Vendor
			initial value is ContextMatchInvoice.Vendor
			
		OldVendor
			initial value is ContextMatchInvoice.OldVendor
			
		Suffix
			initial value is ContextMatchInvoice.Suffix
				
		Contract
			required
					
	Derived Fields	 		

    Conditions
						
    Relations
        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
                related.Company			 				= Company

        MatchInvoiceImportRel
            one-to-one relation to MatchInvoiceImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix

        PayablesInvoiceDetailImportRel
            one-to-many relation to PayablesInvoiceDetailImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
			Instance Selection
				where (related.Contract  				= Contract)

		LocalInterfaceResultsRel
			one-to-one relation to PayablesInvoiceInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
				related.PayablesInvoiceInterfaceResult	= LocalResultID

	Sets

        ByRunGroup
            indexed
            Sort Order
                RunGroup
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
                Contract 
                MatchServiceContractInvoiceImport

		ByCompanyVendorInvoice
            indexed
            Sort Order
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
                Contract 
				MatchServiceContractInvoiceImport
										
	Create Rules
		constraint (MatchInvoiceImportRel exists)
			"InterfaceInvoiceDoesNotExist"
			
	Actions
		Create is a Create Action
			Entrance Rules
					
		CreateNoRules is a Create Action
			restricted
			bypass field rules
			Entrance Rules
		
		Update is an Update Action
			Entrance Rules

		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules
				RunGroup = NewRunGroup
					
		ChangeSecondaryKeyFields is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
		        NewCompany			is a MatchCompany
		        NewVendor			is like Vendor
		        NewOldVendor		is like OldVendor
		        NewInvoice			is like Invoice
		        NewSuffix			is like Suffix
		        NewEDINumber		is like EDINumber
			Action Rules
				RunGroup 			= NewRunGroup
		        Company				= NewCompany
        		Vendor				= NewVendor
		        OldVendor			= NewOldVendor
		        Invoice				= NewInvoice
		        Suffix				= NewSuffix
		        EDINumber			= NewEDINumber
					
		Delete is a Delete Action
					
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		ContractLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				PrmVendorGroup				is a VendorGroup	
				PrmVendor					is a Vendor
					context of PrmVendorGroup		
				PrmProcessLevel				is a PayablesProcessLevel
				PrmAuthorityCode			is a PayablesAuthorityCode
					context of PrmVendorGroup			
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				MatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				AllowMatchItemOrVendorItem			is Boolean
		 		AutoCreateDetails					is Numeric 1
		 			States
		 				FromPurchaseOrder				value is 2
		 				FromReceipt						value is 3
		 				FromReceiptThenFromPO			value is 4					
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"

			Local Fields
				LocalInterfacedContractCount	is Numeric 10


			Instance Selection
				where (RunGroup	= PrmRunGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (PrmVendor            			  not entered
				or      Vendor	= PrmVendor)
				and    (PrmVendorGroup					not entered		
				or      Company.PayablesCompany.VendorGroup = PrmVendorGroup))

			Sort Order
				RunGroup
				Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				Contract 

			Action Rules

				Empty Set Rules
					invoke InvoiceDetailLoad PayablesInvoiceDetailImport
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
						invoked.PrmVendor					= PrmVendor
						invoked.PrmProcessLevel				= PrmProcessLevel
						invoked.PrmAuthorityCode			= PrmAuthorityCode
				        invoked.InvoiceProcess				= InvoiceProcess
						invoked.MatchPoint					= MatchPoint
						invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.PrmVendorGroup				= PrmVendorGroup	

				RunGroup Set Rules
						
					Exit Rules
						LocalResultID					= PrmInterfaceRun



						invoke InvoiceDetailLoad PayablesInvoiceDetailImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmVendor					= PrmVendor
							invoked.PrmProcessLevel				= PrmProcessLevel
							invoked.PrmAuthorityCode			= PrmAuthorityCode
					        invoked.InvoiceProcess				= InvoiceProcess
							invoked.MatchPoint					= MatchPoint
							invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.PrmVendorGroup				= PrmVendorGroup	

				Instance Rules
					if     ((PrmProcessLevel            			  not entered
					or       MatchInvoiceImportRel.ProcessLevel		= PrmProcessLevel)
					and     (PrmAuthorityCode       			      not entered
					or       MatchInvoiceImportRel.AuthorityCode	= PrmAuthorityCode)
	                and      PayablesCompanyRel.Company.FinanceEnterpriseGroup         = PrmFinanceEnterpriseGroup
					and     (!MatchInvoiceImportRel.RecordInError))


						RecordInError 						= false
						ErrorOccurred						= false



						if  (Contract = MatchInvoiceImportRel.PayablesInvoice.ServiceContract)
							invoke FastDelete
						else
							LocalInterfacedContractCount	+= 1					
							if (!ErrorOccurred)
								invoke Create MatchServiceContractInvoice
									assign result to NewMatchServiceContractInvoice
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.Company						= Company
									invoked.PayablesInvoice				= MatchInvoiceImportRel.PayablesInvoice
									invoked.Contract 					= Contract 
			
							if (ErrorOccurred)
								RecordInError = true
								invoke SetError MatchInvoiceImportRel
									invoked.PrmErrorMessage			= LocalErrorMessage
							else
								invoke FastDelete
	
