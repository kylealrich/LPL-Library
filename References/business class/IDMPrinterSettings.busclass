IDMPrinterSettings is a BusinessClass
	owned by idm
	prefix is IDMPS
	
	Ontology
		symbolic key is IDMPrinterSettings

	Patterns
		implements StaticJava
	
	Persistent Fields
		Description	
		PrinterOptions 		is an IDMPrinterPropertyOptions
		Side				is an IDMPrinterPropertySide
		PrinterTray			is an IDMPrinterPropertyTray
		PaperSize			is an IDMPrinterPropertyPaperSize
		Copies				is Numeric size 3
			default label is "No.OfCopies"
		CustomMediaSize 	is an IDMPrinterMediaSize
			
	Conditions
		IEP
			restricted
			when (IDMPrinter.Type.IEP)

		IsActivePrinter
			restricted
			when (IEP and IDMPrinter.Active)

		IsCustomSize
			when (PaperSize = "CustomSize")
			
	Field Rules
		Copies
			constraint (Copies > 0)
				"CopiesMustBeHigherThan0."
			initial value is 1
			default to 1

		Side
			initial value is GetInitialSideValue
			required

		PrinterTray
			initial value is GetInitialTrayValue
			required
		
		PaperSize
			initial value is GetInitialPaperSizeValue
			required

		CustomMediaSize
			if (PaperSize not = "CustomSize")
				CustomMediaSize.Height 	= PaperSize.MediaSize.Height
				CustomMediaSize.Width	= PaperSize.MediaSize.Width

			constraint (CustomMediaSize.Width >= IDMPrinter.PrinterMinMediaSize.Width)
				"MinimumWidthMeasurementIs<IDMPrinter.PrinterMinMediaSize.Width>."
			constraint (CustomMediaSize.Width <= IDMPrinter.PrinterMaxMediaSize.Width)
				"MaximumWidthMeasurementIs<IDMPrinter.PrinterMaxMediaSize.Width>."

			constraint (CustomMediaSize.Height >= IDMPrinter.PrinterMinMediaSize.Height)
				"MinimumHeightMeasurementIs<IDMPrinter.PrinterMinMediaSize.Height>."
			constraint (CustomMediaSize.Height <= IDMPrinter.PrinterMaxMediaSize.Height)
				"MaximumHeightMeasurementIs<IDMPrinter.PrinterMaxMediaSize.Height>."
					
	Derived Fields
		GetInitialSideValue is a NativeField
			type is Alpha size 100 
			restricted

		GetInitialTrayValue is a NativeField
			type is Alpha size 100 
			restricted

		GetInitialPaperSizeValue is a NativeField
			type is Alpha size 100 
			restricted
	
		DerivedFormTitle is a DerivedField
            type is MessageField
            if (IDMPrinterSettings exists)
                return " - " + IDMPrinterSettings

		DerivedWidthSizeMsg is a StringField
			type is Alpha 50
			"Width must be between "
			IDMPrinter.PrinterMinMediaSize.Width
			" and "
			IDMPrinter.PrinterMaxMediaSize.Width
			"."
		
		DerivedHeightSizeMsg is a StringField
			type is Alpha 50
			"Height must be between "
			IDMPrinter.PrinterMinMediaSize.Height
			" and "
			IDMPrinter.PrinterMaxMediaSize.Height
			"."
							
	Actions
		Create is a Create Action
			Entrance Rules
				constraint (IsActivePrinter)
					"IDMPrinterIsInactive"
	
		Update is an Update Action
			valid when (IsActivePrinter)
			confirmation required
				"AllFSMRecordsUsingThisPrinterSettingsWillBeAffectedByThisUpdate\._\Continue?"
			Entrance Rules
				
		Delete is a Delete Action
			valid when (IsActivePrinter)

		PrintTestPage is an Instance Action
			valid when (IsActivePrinter)
			Parameters
				PrmFile		is like IDMAttachment
			Parameter Rules
				PrmFile
					required "FileIsRequired"
					constraint (PrmFile.MimeType = "application/pdf")
						"FileTypeMustBeInPDF"
			Action Rules
				invoke PrintTestPageWithFile IDMPrinter
					invoked.PrmPrinterSettings 	= IDMPrinterSettings
					invoked.PrmFile				= PrmFile
