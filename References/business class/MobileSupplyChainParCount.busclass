MobileSupplyChainParCount is a BusinessClass
    owned by mscm
    prefix is MSPAC

    Ontology
        symbolic key is MobileSupplyChainParCount


    Persistent Fields
        MobileSupplyChainUpload
        TimeSubmitted               is TimeStamp
        TimeStarted                 is TimeStamp
        CountedBy                   is an Actor
        Status                      is Numeric size 1
            States
                Received                value is 1
                Processing              value is 2
                Cancelled		        value is 7
                ProcessedWithErrors     value is 8
                Completed               value is 9
        Requisition
        AdditionalRequisition       is a Requisition
        ReplenishType               is Numeric size 1
            States
                PickForPar      value is 1
                Requisition     value is 2
        ItemsProcessed              is Numeric size 6
        ItemsWithAdditionalQuantity is Numeric size 6
        Notes                       is Text

    Field Rules
        ReplenishType
            if (MobileSupplyChainLocationRel.PickForParEnabled) 
                ReplenishType = ReplenishType.PickForPar
            else
                ReplenishType = ReplenishType.Requisition

    Relations
        PickForParTicketsRel
            one-to-many relation to PickForParTicket
            Field Mapping uses ByMobileSupplyChainParCount
                related.RequestingCompany           = Company
                related.RequestingLocation          = InventoryLocation
                related.MobileSupplyChainParCount   = MobileSupplyChainParCount

        MobileSupplyChainLocationRel
			one-to-one relation to MobileSupplyChainLocation
			Field Mapping uses symbolic key
				related.Company				        = Company
				related.MobileSupplyChainLocation	= InventoryLocation				

    Derived Fields

        DerivedRequisitionStatus is a DerivedField
            type is MessageField
            default label is "Status"
            if (Requisition entered)
                return Requisition.DerivedStatusMessage
            else
                return blank

        DerivedAdditionalRequisitionStatus is a DerivedField
            type is MessageField
            default label is "Status"
            if (AdditionalRequisition entered)
                return AdditionalRequisition.DerivedStatusMessage
            else
                return blank
        
        DerivedDuration is a DerivedField
            type is Numeric size 6
            return TimeSubmitted - TimeStarted

    Conditions
        HasPickForParTicket
            when (ReplenishType.PickForPar)

        ShowPickForParPanel 
            when (HasPickForParTicket and PickForParTicketsRel.IsAccessibleByMobileSupplyChainResource)

    Actions
        Create is a Create Action
            restricted
            Exit Rules

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted

        Reprocess is an Instance Action
            valid when (Status.ProcessedWithErrors)
            Action Rules
                invoke ProcessParCount MobileSupplyChainParCountLine
                    invoked.PrmFinanceEnterpriseGroup       = Company.FinanceEnterpriseGroup
                    invoked.PrmCompany                      = Company
                    invoked.PrmInventoryLocation            = InventoryLocation
                    invoked.PrmMobileSupplyChainParCount    = MobileSupplyChainParCount

    Sets
        ByTimeSubmitted
            Sort Order
                TimeSubmitted descending
                Company
                InventoryLocation
                MobileSupplyChainParCount
