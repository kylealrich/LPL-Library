EFTNotificationOutput is a BusinessClass
	owned by ar
	prefix is EFTN

	Ontology
		symbolic key is EFTNotificationOutput

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		DataFileName				is AlphaUpper size 50
		CreateDate					is TimeStamp

		Customer
		DataFile					is a PaymentAttachment
		EFTNotificationPrintResult	is Numeric size 12

	Transient Fields
	
	Local Fields

	Derived Fields
		DataFileNameOutputFileName is a StringField
			type is Alpha 100
			DataFileName
			"-"
			Company
			"-"
			Customer
			"-"

	Conditions
		HasDataTapeFileAttachment
			restricted
			when (DataFile entered)

	Relations

	Sets
		ByCreateDate
			Sort Order
				CreateDate descending
				Company
				EFTNotificationOutput

		ByEFTNotificationPrintResult	
			Sort Order
				EFTNotificationPrintResult descending
				Company
				EFTNotificationOutput


	Field Rules
		CreateDate
			default to current timestamp
		DataFileName
			default to "EFTNOTIFICATION"

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted

		CreateEFTNotificationFile is an Instance Action
			Action Rules
				trigger "EFTNotificationFile" PA service
					resume on error
					title is "CreateEFTNotificationFile"
					Variables
						Company
						EFTNotificationOutput

		PurgeEFTNotificationOutputRecords is a Set Action
			run in foreground
			completion message is "PurgeOfNotificationOutputRecordsHasCompleted"
			Parameters
				PrmReceivableCompany						is a ReceivableCompany
				PrmCreateDateRange							is a DateRange 

			Parameter Rules
				PrmCreateDateRange
					required
				PrmReceivableCompany
					required
			Instance Selection
				where (CreateDate date within PrmCreateDateRange
				and	Company = PrmReceivableCompany)

			Action Rules
				Instance Rules
					invoke Purge

