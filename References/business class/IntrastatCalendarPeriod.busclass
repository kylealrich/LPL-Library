IntrastatCalendarPeriod is a BusinessClass
    owned by tx
    prefix is INL
    classic name is INCALENDAR

    Ontology
        symbolic key is IntrastatCalendarPeriod
            classic set name is INLSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        PeriodEndDate is Date
            classic name is PER-END-DATE
        PeriodName    is Alpha size 15
            classic name is PER-NAME
        Status        is Numeric size 1
            States
                Open     value is 1
                Reported value is 2

    Conditions

        IsOpenPeriod
        	restricted
            when (Status.Open)

        IsReportedPer
        	restricted
            when (Status.Reported)

    Sets

        Set2
            indexed
            Instance Selection
                where (IsOpenPeriod)
            Sort Order
                TaxCalendar
                IntrastatCalendarPeriod

        Set3
            indexed
            Instance Selection
                where (IsReportedPer)
            Sort Order
				TaxCalendar 
                IntrastatCalendarPeriod

	Field Rules
		PeriodEndDate
			if (IntrastatCalendarPeriod > 1
			and PreviousPeriodRel exist)
				constraint (PreviousPeriodRel.PeriodEndDate < PeriodEndDate)
	                "PeriodEndDateMustBeGreaterThanThePreviousPeriodEndDate"	

			if (PeriodEndDate changed)
				constraint (Status.Open)
					"PeriodHasBeenReported;CannotChangePeriodEndDate"			// 104  Period is "Open"; cannot change period end date

		Status
			initial value is Status.Open
			default to Status.Open
			if (Status changed)
			    constraint (!Status.Reported)
			    	"TaxPeriodReportedOrClosed;CannotBeChanged"	

            if (action type.Create)        
				constraint (Status.Open)
					"CanOnlyEnterOpenStatus"	
					

	Relations

		PreviousPeriodRel
			one-to-one relation to IntrastatCalendarPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.TaxCalendar					= TaxCalendar
				related.IntrastatCalendarPeriod		= IntrastatCalendarPeriod - 1
								

	Actions

        Create is a Create Action

   		Update is an Update Action
						
        Delete is a Delete Action
			Entrance Rules
			    constraint (!Status.Reported)
                    "TaxPeriodReported;CannotBeDeleted"			// 105  Period is "Open"; cannot delete        

      	UpdateStatusNoEdit is an Instance Action		 		
        	restricted
			default label is untranslatable
			Parameters
				PrmStatus	is Numeric 1
					default label is "Status"	 
		           	States
		                Open     value is 1
		                Reported value is 2
        	Entrance Rules
        	Action Rules
        		Status = PrmStatus     	                                                                                   
