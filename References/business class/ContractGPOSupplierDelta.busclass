ContractGPOSupplierDelta is a BusinessClass  
	owned by po

	
	prefix is CXXD
	
	Ontology
		symbolic key is ContractGPOSupplierDelta
		
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
	
	Persistent Fields
		ContractNumber                  is a GPOContractNumber     
		DistributorNumber               is a GPOSupplierID         
		DistributorName                 is Alpha 250               
		DistributorLegalName            is Alpha 250               
		DistributorTaxIDNumber          is AlphaUpper size 25      
		ContractDistributorEffectiveDate is Date       
		ContractDistributorExpireDate   is Date        
		RecordModifiedTimestamp         is Alpha 20   
		OperationFlag                   is Alpha size 10	

	Local Fields
		LocalContractGroup              is like ContractGroup
		LocalGPOContractNumber          is like GPOContractNumber
		LocalDistributorNumber          is like GPOSupplierID

	Conditions

	Relations
	
		ContractImportRel
			one-to-one relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber 	= LocalGPOContractNumber
				related.ContractGroup       = LocalContractGroup			

		ContractGPOSupplierRel 
			one-to-many relation to ContractGPOSupplier 
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = LocalGPOContractNumber 
				related.GPOSupplier         = LocalDistributorNumber

	Sets

		ByGPOContractNumber
			Sort Order
				ContractNumber
				ContractGPOSupplierDelta

	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassSupplierDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Purge 

		Purge is a Purge Action 

		LoadSupplierRecords is a Set Action 
			restricted 
			Parameters 
				ParmContractGroup		is a ContractGroup
			
			Sort Order
				ContractNumber
				ContractGPOSupplierDelta
			
			Action Rules 

				Instance Rules 

					LocalContractGroup              = ParmContractGroup
					LocalGPOContractNumber 			= ContractNumber
					LocalDistributorNumber          = DistributorNumber

					if (OperationFlag = "Add")
						if (ContractGPOSupplierRel !exists)
							invoke Create ContractGPOSupplier 
								invoked.GPOContractNumber         	      	= ContractNumber                
								invoked.GPOSupplier                 	    = DistributorNumber             
								invoked.Name                            	= DistributorName                   
								invoked.LegalName                       	= DistributorLegalName           
								invoked.VendorTaxID                     	= DistributorTaxIDNumber            
								invoked.ContractDistributorEffectiveDate	= ContractDistributorEffectiveDate        
								invoked.ContractDistributorExpireDate       = ContractDistributorExpireDate         
								invoked.RecordModifiedTimestamp             = RecordModifiedTimestamp   

							if (ContractImportRel exists)
								invoke FastUpdate ContractImportRel 
									invoked.HeaderUpdated = true  


					else 
					if (OperationFlag = "Update")
						if (ContractGPOSupplierRel exists)
							invoke Update ContractGPOSupplierRel 
								invoked.Name                            	= DistributorName                   
								invoked.LegalName                       	= DistributorLegalName           
								invoked.VendorTaxID                     	= DistributorTaxIDNumber            
								invoked.ContractDistributorEffectiveDate	= ContractDistributorEffectiveDate        
								invoked.ContractDistributorExpireDate       = ContractDistributorExpireDate         
								invoked.RecordModifiedTimestamp             = RecordModifiedTimestamp   

							if (ContractImportRel exists)
								invoke FastUpdate ContractImportRel 
									invoked.HeaderUpdated = true 

					else 
					if (OperationFlag = "DEL")
						if (ContractGPOSupplierRel exists)
							invoke Delete ContractGPOSupplierRel 

