SourcingEventLineQuestion is a BusinessClass
	owned by ss
	prefix is ELQ

	Ontology
		symbolic key is SourcingEventLineQuestion

	Patterns
		implements CompoundDocument
			Document Components
				SourcingEventLineQuestionValue set
				SourcingEventLineQuestionAllocation set
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Persistent Fields
		QuestionText					is Text
			sql name is SELQUE
			text searchable
		ResponseType					is Numeric size 1
			States
				Text		value is 1
				Number		value is 2
				Date		value is 3
				List		value is 4
				YesNo		value is 5
				YesNoText	value is 6
		Attachment
		DisplayOrder
		Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion				is like Question
		CMQuestion						is Boolean
		QuestionWeighting
		LastQuestionValueDisplayOrder	is Numeric size 6
			disable Auditing
		LastQuestionAnswerDisplayOrder	is Numeric size 6
			disable Auditing
		ResponseRules					is Numeric size 1
			States
				NotRequired								value is 1
				ResponseRequired						value is 2
				ResponseRequiredAndAttachmentRequired	value is 3
		YesNoResponseRules				is Numeric size 1
			default label is "ResponseRules"
			States
				NotRequired									value is 1
				ResponseRequired							value is 2
				ResponseRequiredAndAttachmentRequired		value is 3
				ResponseRequiredAndAttachmentRequiredIfYes	value is 4
				ResponseRequiredAndAttachmentRequiredIfNo	value is 5
		YesNoTextResponseRules			is Numeric size 1
			default label is "ResponseRules"
			States
				NotRequired											value is 1
				ResponseRequiredAndTextResponseRequired				value is 2
				ResponseRequiredAndTextOrAttachmentRequired			value is 3
				ResponseRequiredAndTextOrAttachmentRequiredIfYes	value is 4
				ResponseRequiredAndTextOrAttachmentRequiredIfNo		value is 5
				ResponseRequiredAndTextAndAttachmentRequired		value is 6
				ResponseRequiredAndTextAndAttachmentRequiredIfYes	value is 7
				ResponseRequiredAndTextAndAttachmentRequiredIfNo	value is 8
		AllowResponseAttachment			is Boolean

	Transient Fields
		NewDisplayOrder					is a DisplayOrder
		CreateFromRepository			is Boolean

	Local Fields
		LocalConditionalQuestion	is like Question
		LocalListValue				is like SourcingEventLineQuestionValue
		LocalYesOrNo				is Numeric 1 
           	States
           		Yes	value is 1
           		No	value is 2 
		FromRepository				is Boolean 
		FromConditional				is Boolean		
		LocalFromConditional		is Boolean
		LocalQuestion				is like Question

	Derived Fields
		IsQuestionRequired is a ConditionalField
			type is Alpha size 2
			if (!ResponseRequired)
				blank
			else
				"*"

		DerivedQuestionText is a DerivedField
			type is Alpha 250
			return QuestionText

		AnswerMessage is a MessageField
			restricted
			"Answer"

		AnswerTextWithRequired is a StringField
			type is Text
			restricted
			IsQuestionRequired	AnswerMessage

		IsAttachmentRequired is a ConditionalField
			type is Alpha size 2
			restricted
			if (!AlwaysRequireResponseAttachment)
				blank
			else
				"*"

		AttachDocumentMessage is a MessageField
			restricted
			"AttachDocument"

		AttachmentTextWithRequired is a StringField
			type is Text
			restricted
			IsAttachmentRequired AttachDocumentMessage

		RequiredTag is a MessageField
			"Required"

	Field Groups
		RevisionControlled
			QuestionText
			ResponseType
			Attachment
			AllowResponseAttachment
			ResponseRules
			YesNoResponseRules
			YesNoTextResponseRules

	Conditions
		IsYesNo
			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)

		HasListValues
			restricted
			when (SourcingEventLineQuestionValue set exists)

		EligibleForCM
			restricted
			when (SourcingEventLine.ContractOutput)

		HasAttachment
			restricted
			when (Attachment entered)

		YesNoQuestionWithWeighting
			restricted
			when (ResponseType.YesNo
			and   QuestionWeighting entered)

		YesNoTextQuestionWithWeighting
			restricted
			when (ResponseType.YesNoText
			and   QuestionWeighting entered)

		ListQuestionWithWeighting
			restricted
			when (ResponseType.List
			and   QuestionWeighting entered)

		HasWeighting
   			restricted
			when (QuestionWeighting entered)
			
		ListQuestion
			restricted
			when (ResponseType.List)

		EligibleForWeighting
			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText
			or 	  ResponseType.List)

		ActionsAreValid
			restricted
			when (SourcingEvent.InEditableState
			and	  SourcingEventLine.InLineActionableState)

		QuestionEntered
			restricted
			when (Question entered)

		HasRemainingAttachmentLineQuestions
			restricted
			when (OtherRequiredLineAttachmentsRel exists)

		HasRemainingAttachmentQuestions
			restricted
			when (RequiredAttachmentsRel          exists
			or    HasRemainingAttachmentLineQuestions)

		AnyQuestionAnswers
			restricted
			when (AnyQuestionAnswersRel exists
			or    SELSupplierQuestionResponseRel exists)

		CanCreateConditionalYesNoQuestion
			restricted 
			when (YesNoResponseType
			and  !QuestionIsAnswered)
		
		CanCreateConditionalListQuestion
			restricted 
			when (ResponseType.List
			and  !QuestionIsAnswered)

		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)
		
		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)
		
		SourcingEventLineResponseExists
			restricted
			when (SourcingEvent.AllowsConditionalQuestions
			and   SourcingEventLineResponseRel exists)

		RequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and  !QuestionIsAnswered)

		QuestionIsAnswered
			restricted
			when (SourcingEventQuestionResponseRel exists)

		DisplayAnswerRequiredText 
			restricted
			when (RequiredQuestionNoAnswer
			or    MayRequireAttachmentNoAttachment)

		MayRequireAttachmentNoAttachment 
			restricted 
			when (MayRequireResponseAttachment
			and  !AnswerAttachmentExists)

		HasAnySourcingEventConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		RepositoryQuestion 
			restricted 
			when (Question entered)

		RepositoryHasConditionalQuestions	
			restricted 
			when (RepositoryConditionalQuestionRel exists)

		AllowedToChangeEventLineQuestions	
			restricted 
			when (!RepositoryQuestion
			or    RepositoryConditionalQuestionRel !exists)

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)
		
		HasDirectConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		AnswerAttachmentExists
			restricted
			when (SourcingEventQuestionAnswerAttachmentRel exists)

		ResponseRequired
			when (ResponseRules.ResponseRequiredAndAttachmentRequired
			or    ResponseRules.ResponseRequired
			or    YesNoResponseRules.ResponseRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)

		MayRequireResponseAttachment
			restricted
			when (ResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)

		AlwaysRequireResponseAttachment
			restricted
			when (ResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)

		NonYesNoResponseType
			restricted
			when (ResponseType.Date
			or    ResponseType.List
			or    ResponseType.Text
			or    ResponseType.Number)

	Relations
		AnyQuestionAnswersRel is a SourcingEventLineQuestionResponse set

		SourcingEventLineResponseRel
			one-to-many relation to SourcingEventLineResponse
			Field Mapping uses symbolic key
				related.Company					= Company
				related.SourcingEvent			= SourcingEvent
				related.SourcingEventLine		= SourcingEventLine

		RequiredAttachmentsRel
			one-to-many relation to SourcingEventLineQuestion
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventLine		= SourcingEventLine
			Instance Selection
				where (related.AlwaysRequireResponseAttachment)

		SourcingEventQuestionResponseRel
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company						= Company
				related.SourcingEvent				= SourcingEvent
				related.SourcingEventQuestion		= Question

		SameRepositoryQuestionRel 
			one-to-many relation to SourcingEventLineQuestion 
			Field Mapping uses ByQuestion
				related.Company					= Company 
				related.SourcingEvent			= SourcingEvent
				related.SourcingEventLine		= SourcingEventLine 
				related.Question				= LocalConditionalQuestion

		SourcingEventQuestionAnswerAttachmentRel is a SourcingEventLineQuestionResponse set
			Instance Selection
				where (related.Attachment entered)

		CreatedFromQuestionRel 
			one-to-one relation to SourcingEventLineQuestion 
			Field Mapping uses symbolic key 
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent
				related.SourcingEventLine					= SourcingEventLine 
				related.SourcingEventLineQuestion			= CreatedFromQuestion

		ConditionalQuestionsDirectRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		ConditionalLocalQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= LocalQuestion 

		RepositoryConditionalQuestionRel    
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		OtherRequiredLineAttachmentsRel
			one-to-many relation to SourcingEventLineQuestion
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventLine       = SourcingEventLine
			Instance Selection
				where (related.AlwaysRequireResponseAttachment
				and    related.SourcingEventLineQuestion !=SourcingEventLineQuestion)

		ScoreAllocationEntered is a SourcingEventLineQuestionAllocation set
			Instance Selection
				where (related.ScoreAllocation entered)

		QuestionResponseAttachmentsRel
			one-to-many relation to SourcingEventLineQuestionResponse
			Field Mapping uses ByQuestion
				related.Company         		    = Company
				related.SourcingEvent     		    = SourcingEvent
				related.SourcingEventLine      		= SourcingEventLine
				related.SourcingEventLineQuestion   = SourcingEventLineQuestion
			Instance Selection
				where (related.HasQuestionAnswerAttachment)

		SELSupplierQuestionRel 
			one-to-many relation to SELSupplierQuestion 
			Field Mapping uses ByQuestion
				related.NotifiedSupplier.SupplierGroup		= SourcingEventLineResponseRel.NotifiedSupplier.SupplierGroup
	 			related.NotifiedSupplier.Supplier			= SourcingEventLineResponseRel.NotifiedSupplier.Supplier
	 			related.NotifiedSupplier.SupplierSourceId	= SourcingEventLineResponseRel.NotifiedSupplier.SupplierSourceId
				related.Company								= Company 
				related.SourcingEvent						= SourcingEvent
				related.SourcingEventLine					= SourcingEventLine
			Instance Selection
				where (related.SourcingEventLineQuestion	= SourcingEventLineQuestion) 

		SELSupplierQuestionResponseRel
			one-to-many relation to SELSupplierQuestionResponse
			Field Mapping uses ByQuestion
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent
				related.SourcingEventLine					= SourcingEventLine
				related.SELSupplierQuestion					= SourcingEventLineQuestion

	Sets
		ByDisplayOrder
			duplicates
			Sort Order
				Company
				SourcingEvent
				SourcingEventLine
				DisplayOrder
				SourcingEventLineQuestion

		ByQuestion
			Sort Order
				Company
				SourcingEvent
				SourcingEventLine
				Question
				SourcingEventLineQuestion

		ByQuestionFirst
			Sort Order
				Question
				Company
				SourcingEvent
				SourcingEventLine
				SourcingEventLineQuestion

	Field Rules
		QuestionText
			required
		ResponseType
			required
		QuestionWeighting
			if (QuestionWeighting entered)
				constraint (ResponseRequired)
					"MustEnterAResponseRuleThatRequiresAResponseWhenEnteringQuestionWeighting"
			constraint (EligibleForWeighting)
				"CanOnlyDefineWeightingForList,YesNo,AndYesNoTextQuestions"
			if ((ResponseRules changed
			or  YesNoResponseRules changed
			or  YesNoTextResponseRules changed)
			and !ResponseRequired
			and QuestionWeighting entered
			and ScoreAllocationEntered exists)
				constraint (ResponseRequired)
					"TheQuestionMustRequireAResponseWhenEventScoreAllocationIsDefined"
		ResponseRules
			if (NonYesNoResponseType)
				required
					"MustSelectAResponseRule"
				YesNoResponseRules = 0
				YesNoTextResponseRules = 0

		YesNoResponseRules
			if (ResponseType.YesNo)
				required
					"MustSelectAResponseRule"
				ResponseRules= 0
				YesNoTextResponseRules= 0

		YesNoTextResponseRules
			if (ResponseType.YesNoText)
				required
					"MustSelectAResponseRule"
				ResponseRules= 0
				YesNoResponseRules= 0
		AllowResponseAttachment
			if (MayRequireResponseAttachment)
				AllowResponseAttachment = true

	Rule Blocks
		CreateCorrectAnswer
			if (IsYesNo
			and !CreateFromRepository
			and !SourcingEvent.CreateByCopy
			and	!SourcingEventLineQuestionAllocation set exists)
				invoke InternalCreate SourcingEventLineQuestionAllocation
					fill in fields from SourcingEventLineQuestion
					if (ResponseType.YesNo)
						invoked.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.YesNoValue = 1
					if (ResponseType.YesNoText)
						invoked.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.YesNoText.YesNo = 1

				invoke InternalCreate SourcingEventLineQuestionAllocation
					fill in fields from SourcingEventLineQuestion
					if (ResponseType.YesNo)
						invoked.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.YesNoValue = 2
					if (ResponseType.YesNoText)
						invoked.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.YesNoText.YesNo = 2

	Create Exit Rules

		if (SourcingEventLineResponseExists)
			for each SourcingEventLineResponseRel
				invoke Create SELSupplierQuestion
					fill in fields from SourcingEventLineQuestion
					invoked.NotifiedSupplier		= each.NotifiedSupplier

	Actions
		Create is a Create Action
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEventLine.LastQuestionDisplayOrder

			Action Rules
						
				LocalFromConditional = FromConditional 

				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddQuestionWhileEventIsPendingEventApproval"
				if (!SourcingEvent.TwoStepBiddingUsed
				and !SourcingEvent.BestAndFinalOffer)
					constraint (SourcingEvent.InActionableState)
						"CannotAddQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				else
					if (SourcingEvent.TwoStepBiddingUsed)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterStepTwo_(Pricing)HasStarted"
					else
					if (SourcingEvent.BestAndFinalOffer)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterBestAndFinalProcessHasStarted"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotAddQuestionWhileLineIsPendingAward,Cancelled,OrAwarded"

				constraint (!AnyQuestionAnswers)
					"CannotAddQuestionWhenAnswersExist"
				
				if (!SourcingEvent.AllowsConditionalQuestions)
					constraint (ConditionalQuestionsDirectRel !exists)
						"CannotAddAQuestionWithConditionalQuestionsToThisSourcingEventLine"

				if (ResponseRequired
				and SourcingEvent.TwoStepBidding.Separate)
					confirmation required
						"ARequiredQuestionIsBeingCreatedForATwoStepEventThatWillNotHaveLineLevelInformationEnteredUntilStepTwo_(Pricing);TheQuestionAnswersWillNotBeAbleToBeUsedToAcceptTheTechnicalProposal;DoYouWantToContinue?"

				if (!SourcingEvent.CreateByCopy)
					CMQuestion = true

				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddQuestion)
					invoke Amend Open.Notified SourcingEvent

			Exit Rules
				include CreateCorrectAnswer

		CreateConditionalYesNoQuestion is an Instance Action
			valid when (CanCreateConditionalYesNoQuestion)
			Parameters 
            	ParmYesOrNo     		  is Numeric size 1
					default label is "YesOrNo"
					States
            			Yes	value is 1
            			No	value is 2   
				ParmConditionalQuestion is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmYesOrNo 
					required 
				ParmConditionalQuestion 
					required
			
			Action Rules

				constraint (ParmConditionalQuestion.Active)
					"CannotUseAnInactiveQuestion"
				LocalConditionalQuestion	= ParmConditionalQuestion
				LocalYesOrNo                = ParmYesOrNo
				constraint (SameRepositoryQuestionRel !exists)
					"ConditionalQuestionAlreadyExistsAsASourcingEventLineQuestion"
				constraint (ConditionalQuestionsDirectRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (SourcingEventLineQuestion.Question entered)
					constraint (ParmConditionalQuestion != SourcingEventLineQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsQuestion"

		CreateConditionalListQuestion is an Instance Action
			valid when (CanCreateConditionalListQuestion)
			Parameters 
            	ParmListValue     		  is a SourcingEventLineQuestionValue 
					default label is "ListValue"
				ParmConditionalQuestion 	is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmListValue 
					required 
				ParmConditionalQuestion 
					required
			
			Action Rules

				LocalConditionalQuestion	= ParmConditionalQuestion
				LocalListValue              = ParmListValue
				constraint (ConditionalQuestionsDirectRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (SourcingEventLineQuestion.Question entered)
					constraint (ParmConditionalQuestion != SourcingEventLineQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsRepositoryQuestion"

		Update is an Update Action
		   	Action Rules
				if (YesNoOrList)
					constraint (AllowedToChangeEventLineQuestions)
						"CannotChangeYesNoOrListQuestionThatIsARepositoryQuestionThatHasConditionalQuestions"

				if (Question entered
				and (QuestionText changed
				or	 ResponseType changed
				or	 Attachment changed))
					initialize Question
				if (RevisionControlled changed)
					constraint (SourcingEvent.InActionableState)
						"CannotUpdateQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
					constraint (!SourcingEvent.NeedsApproval)
						"CannotUpdateQuestionWhileEventIsPendingEventApproval"
					constraint (SourcingEventLine.InLineActionableState)
						"CannotUpdateQuestionWhileLineIsPendingAward,Cancelled,OrAwarded"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateQuestion
				and RevisionControlled changed)
					invoke Amend Open.Notified SourcingEvent

				if (!ResponseType.List
				and SourcingEventLineQuestionValue set exists)
					invoke Delete SourcingEventLineQuestionValue set

				if (!EligibleForWeighting
				and SourcingEventLineQuestionAllocation set exists)
					invoke Delete SourcingEventLineQuestionAllocation set

				if (ResponseType.List
				and SourcingEventLineQuestionValue set exists
				and !SourcingEventLineQuestionAllocation set exists)
					for each SourcingEventLineQuestionValue set
						invoke InternalCreate SourcingEventLineQuestionAllocation
							invoked.Company 					= each.Company
							invoked.SourcingEvent 				= each.SourcingEvent
							invoked.SourcingEventLine			= each.SourcingEventLine
							invoked.SourcingEventLineQuestion 	= each.SourcingEventLineQuestion
							invoked.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.SourcingEventLineQuestionValue = each.SourcingEventLineQuestionValue
							fill in fields from each

				if  (AllowResponseAttachment changed
				and !AllowResponseAttachment)
					constraint (!QuestionResponseAttachmentsRel exists)
						"CannotChangeAllowAttachmentUploadWithAnswerFlag:SuppliersHaveAnsweredQuestionWithAttachments"

				if ((ResponseRules changed
				or  YesNoResponseRules changed
				or  YesNoTextResponseRules changed)
				and ResponseRequired
				and SourcingEvent.TwoStepBidding.Separate)
					confirmation required
						"ARequiredQuestionIsBeingCreatedForATwoStepEventThatWillNotHaveLineLevelInformationEnteredUntilStepTwo_(Pricing);TheQuestionAnswersWillNotBeAbleToBeUsedToAcceptTheTechnicalProposal;DoYouWantToContinue?"

				if (ResponseType changed)
					constraint (!AnyQuestionAnswers)
						"CannotChangeQuestionTypeWhenAnswersExist"

				include CreateCorrectAnswer

			Exit Rules
				if (SourcingEventLineResponseExists)
					for each SourcingEventLineResponseRel
					
						for each SELSupplierQuestionRel
							invoke Update each.SELSupplierQuestion
								fill in fields from SourcingEventLineQuestion
									except invoked.DisplayOrder
								initialize invoked.Question

		Delete is a Delete Action
			valid when (ActionsAreValid)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteQuestionWhileEventIsPendingEventApproval"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotDeleteQuestionWhileLineIsPendingAward,Cancelled,OrAwarded"
				constraint (!AnyQuestionAnswers)
					"CannotDeleteQuestionWhenAnswersExist"				
				if (SourcingEvent.Status.Open
				and SourcingEvent.TwoStepBidding.Combined)
					constraint (HasRemainingAttachmentQuestions)
						"CannotDeleteQuestionForTwoStepBiddingEventWhenNoOtherQuestionsWithRequiredAnswerAttachmentsExist"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.DeleteQuestion)
					invoke Amend Open.Notified SourcingEvent

		DeleteConditionalQuestions is a Set Action  
			restricted 
			Parameters 
			 	ParmCompany					is a SourcingCompany 
			 	ParmSourcingEvent			is a SourcingEvent
				ParmSourcingEventLine		is a SourcingEventLine 
			 	ParmOriginalQuestion		is a SourcingEventLineQuestion
				ParmLastOldQuestion			is a SourcingEventLineQuestion   

			Instance Selection 
			 	where (ParmCompany					= Company
			 	and    ParmSourcingEvent			= SourcingEvent
				and    ParmSourcingEventLine		= SourcingEventLine 
			 	and    ParmOriginalQuestion			= CreatedFromQuestion
				and    SourcingEventLineQuestion	<= ParmLastOldQuestion)

			Action Rules
			 	Instance Rules 
					invoke Delete		
