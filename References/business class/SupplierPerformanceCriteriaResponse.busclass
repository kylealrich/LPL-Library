SupplierPerformanceCriteriaResponse is a BusinessClass
    owned by procurement
    prefix is SPCRS
    sql name is SPCRESPONSE

    Ontology
		part of SupplierPerformanceCriteriaGroup
			relative key is SupplierPerformanceCriteria

    Patterns
        implements TemplateDriven by SupplierPerformanceCriteria
        	create instance
        		when (SupplierPerformanceCriteriaOption entered
        		or	  Comments entered
        		or	  Attachment entered)
        		
    Persistent Fields
		SupplierPerformanceCriteriaOption
    	Comments					is Text
    	DisplayCommentsToSupplier	is Boolean
    	Attachment
    	DisplayAttachmentToSupplier	is Boolean

    Derived Fields
    	PerformanceRatingScore is a DerivedField
			type is like InternationalAmount
			restricted
			if (SupplierPerformanceCriteriaOption > 1)
				return SupplierPerformanceCriteriaOption - 1
			else
				return 0

        NumericPerformanceRating is a DerivedField
            type is Numeric size 4
            return SupplierPerformanceCriteriaOption

		DerivedPerformanceRating is a StringField
			type is Alpha size 85
			SupplierPerformanceCriteriaOption + " - " + SupplierPerformanceCriteriaOption.Description

	Relations

	Conditions
		HasDescription
			restricted
			when (SupplierPerformanceCriteria.Description entered)

		HasAttachment
			restricted
			when (Attachment entered)
		
		CompletionButtonValid
			restricted
			when (SupplierPerformanceEvaluation.TotalPerformanceScore > 0
			and	  !SupplierPerformanceEvaluation.IsEvaluationComplete)

	Field Rules
		DisplayCommentsToSupplier
			if (DisplayCommentsToSupplier)
				constraint (Comments entered)
					"CannotDisplayCommentsToSupplierIfNoCommentsAreEntered"

		DisplayAttachmentToSupplier
			if (DisplayAttachmentToSupplier)
				constraint (Attachment entered)
					"CannotDisplayAttachmentsToSupplierIfThereIsNoAttachment"

	Actions
		Create is a Create Action
			valid when (!SupplierPerformanceEvaluation.IsEvaluationComplete)

		Update is an Action
			valid when (!SupplierPerformanceEvaluation.IsEvaluationComplete)
					
		Delete is a Delete Action

		CompleteEvaluation is an Instance Action
			valid when (!SupplierPerformanceEvaluation.IsEvaluationComplete)
