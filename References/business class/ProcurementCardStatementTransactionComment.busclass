ProcurementCardStatementTransactionComment is a BusinessClass
	owned by po
	prefix is PXDct
    sql name is PCardStatementTransactionCmt
	
	Ontology
    	symbolic key is ProcurementCardStatementTransactionComment
	
	Patterns
		implements StaticJava
		
	Persistent Fields
		ProcurementGroup
		ProcurementCardProgram
		ProcurementCardStatement
		ProcurementCardStatementTransaction
		CommentSequence					is Numeric 6
		Title							is like CommentName
    	CommentType						is AlphaUpper size 1
    		States
                ProcurementCardTransactionComment	value is "A"
    	CommentText
		Attachment						is an AlternateAttachment

	Derived Fields	
		DerivedCardProgramNameDescription is a MessageField
			default label is "ProgramName"
			"<ProcurementCardProgram>_-_<ProcurementCardProgram.Description>"

		DerivedCardTransaction is a MessageField
			default label is "Statement"
			"<ProcurementCardStatement>_-_<ProcurementCardStatementTransaction.TransactionNumber>"
		
		DisplayCardNumberAndName is a MessageField
			default label is "CardNumber"
			"<ProcurementCardStatementTransaction.ProcurementCardNumber.DisplayProcurementCardNumber>_-<ProcurementCardStatementTransaction.CardUserName>"

		DerivedMerchantName is a MessageField
			default label is "MerchantName"
			"<ProcurementCardStatementTransaction.Merchant>_-_<ProcurementCardStatementTransaction.Merchant.Name>"

	Local Fields
		LocalAttributeCtr   is Numeric 2

	Conditions
    	HasAttachment
    		restricted
    		when (Attachment entered)

	Sets
		ByTitleSequence
			Sort Order
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatement
				ProcurementCardStatementTransaction
				Title
				CommentSequence
				
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Field Rules
		CommentSequence
			autosequence using ByTitleSequence
		CommentType
			force default to "A"	
    	Title 
    		required

	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Delete Action			

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField with Attachment							

		MoveAttachmentsToIDM is a Set Action
			default label is untranslatable
			restricted
			
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop			
			
