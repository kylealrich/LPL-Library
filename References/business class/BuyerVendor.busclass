BuyerVendor is a BusinessClass
	owned by po
	prefix is BVP
	classic name is BUYVENPF

	Ontology
		symbolic key is BuyerVendor
			classic set name is BVPSET1
			classic name for BuyerVendor.PurchaseFromLocation is PURCH-FR-LOC
			classic name for HROrganization is PROCURE-GROUP
			classic name for Buyer is BUYER-CODE

	Patterns
		implements StaticJava
		disable AuditIndex

	Relations
		SetupAtVendorRel
			one-to-one relation to BuyerVendor
			Field Mapping uses symbolic key
				related.HROrganization						= HROrganization
				related.Buyer								= Buyer
				related.VendorGroup							= VendorGroup
				related.BuyerVendor.Vendor					= BuyerVendor.Vendor
				related.BuyerVendor.PurchaseFromLocation	= blank

		SetupAtVendorLocationRel
			one-to-many relation to BuyerVendor
			Field Mapping uses symbolic key
				related.HROrganization	= HROrganization
				related.Buyer			= Buyer
				related.VendorGroup		= VendorGroup
			Instance Selection
				where (related.BuyerVendor.Vendor = this instance.BuyerVendor.Vendor
				and    related.BuyerVendor.PurchaseFromLocation entered)

	Field Rules
		VendorGroup
			initial value is (Buyer.ProcurementGroup)
			constraint (VendorGroup.FinanceEnterpriseGroup.HROrganization = HROrganization)
				"FinanceEnterpriseGroupAssociatedWithVendorGroupDoesNotUseEnteredHROrganization"
		
		BuyerVendor
			if (BuyerVendor.PurchaseFromLocation entered)
				constraint (SetupAtVendorRel not exists)
					"AlreadySetupAtVendor;CannotAddPurchaseFromLocation"
			else
				constraint (SetupAtVendorLocationRel not exists)
					"AlreadySetupAtPurchaseFromLocation;CannotAddVendor"

	Actions
		Create is a Create Action
			Action Rules
				if (Buyer.ProcurementGroup entered)
					VendorGroup = Buyer.ProcurementGroup

		Update is an Update Action

		Delete is a Delete Action

