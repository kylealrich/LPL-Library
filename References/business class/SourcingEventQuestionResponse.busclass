SourcingEventQuestionResponse is a BusinessClass
    owned by ss
    prefix is ERQ

    Ontology
    	part of SourcingEventResponse 
    		relative key is SourcingEventQuestion
    			
   	Patterns
        implements TemplateDriven by SourcingEventQuestion
        	create instance
        		when (SourcingEventQuestionAnswer entered)

    Persistent Fields
    	SourcingEventQuestionAnswer
    	Attachment
    	FromTechnicalProposal   is Boolean
    	CMAttachment            is Boolean	
    
    Sets
    	ByQuestion
    		duplicates
    		Sort Order
    			Company
    			SourcingEvent
    			SourcingEventQuestion
    			SourcingEventResponse
    			
    	BySupplier
    		Sort Order
    			Company
    			SourcingEvent
    			NotifiedSupplier.SupplierGroup
    			NotifiedSupplier.Supplier
    			NotifiedSupplier.SupplierSourceId
    			SourcingEventResponse
    			SourcingEventQuestion
    
	Derived Fields
		ResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (SourcingEventQuestion.ResponseType.Text)
				SourcingEventQuestionAnswer.TextValue
			else
			if (SourcingEventQuestion.ResponseType.Number)
				SourcingEventQuestionAnswer.NumericValue
			else
			if (SourcingEventQuestion.ResponseType.Date)
				DerivedDate
			else
			if (SourcingEventQuestion.ResponseType.List)
				SourcingEventQuestionAnswer.SourcingEventQuestionValue
			else
			if (SourcingEventQuestion.ResponseType.YesNo)
				if (SourcingEventQuestionAnswer.YesNoValue.Yes)
					YesText
				else
					NoText
			else
				YesNoTextValue
	
		SecondStepYesMessage is a MessageField
			restricted
			"Yes"
			
		SecondStepNoMessage is a MessageField
			restricted
			"No"
					
		TechnicalProposalYesNo is a DerivedField
			type is Alpha 10
			restricted
			if (SourcingEventResponse.TechnicalProposalAccepted)
				return SecondStepYesMessage
			else
				return SecondStepNoMessage
		
		BestAndFinalYesNo is a DerivedField
			type is Alpha 10
			restricted
			if (SourcingEventResponse.BestAndFinalSupplier)
				return SecondStepYesMessage
			else
				return SecondStepNoMessage
		
		TechnicalProposalAccepted is a DerivedField
			type is Alpha 10
			if (SourcingEvent.TwoStepBiddingUsed)
				return (TechnicalProposalYesNo)
			else
				return blank
				
		BestAndFinalSupplier is a DerivedField
			type is Alpha 10
			if (SourcingEvent.BestAndFinalOffer)
				return (BestAndFinalYesNo)
			else
				return blank	
		
		YesNoTextValue is a StringField
			type is Alpha size 260
			restricted
			YesNoText
			"-"
			SourcingEventQuestionAnswer.YesNoText.Text

		YesNoText is a ConditionalField
			type is Text
			restricted
			if (SourcingEventQuestionAnswer.YesNoText.YesNo = 1)
				YesText
			else
				NoText

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"

		DerivedDate is a StringField
			type is Alpha 10
			restricted
			SourcingEventQuestionAnswer.DateValue month
			"/"
			SourcingEventQuestionAnswer.DateValue day
			"/"
			SourcingEventQuestionAnswer.DateValue year

		QuestionScore is a DerivedField
			type is like InternationalAmount
			if (!SourcingEventResponse.NoBid)
				if (SourcingEventQuestion.ResponseType.YesNoText)
					return (first AllocQuestionYesNoTextRel.ScoreAllocation * SourcingEventQuestion.QuestionWeighting)
				else
					return (first AllocQuestionAnswerRel.ScoreAllocation * SourcingEventQuestion.QuestionWeighting)
			else
				return 0 
				
		SetSendToCM is a ConditionalField
			type is Text
			restricted
			if (CMAttachment)
				SetToNoMessage
			else
				SetToYesMessage
		
		SetToNoMessage is a MessageField
			restricted
   			"Do_Not_Email_Attachment"
   			
   		SetToYesMessage is a MessageField
			restricted
   			"Email_Attachment_On_Contract"
   			
   		ResponseRequiredMessage is a MessageField
			restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
			restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
			restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsYes"
            
        NoAttachmentMessage is a MessageField
			restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsNo"
            
        YesOrNoAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsYes"
            
        NoAttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsNo"
            
        AttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsYes"
            
        NoAttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsNo"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
			restricted
            if (SourcingEventQuestion.ResponseRules.ResponseRequired
            or  SourcingEventQuestion.YesNoResponseRules.ResponseRequired)
                ResponseRequiredMessage        
            else
            if (SourcingEventQuestion.ResponseRules.ResponseRequiredAndAttachmentRequired
            or  SourcingEventQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
                ResponseAndAttachmentRequiredMessage
            else
            if (SourcingEventQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
                YesAttachmentMessage
            else
            if (SourcingEventQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                NoAttachmentMessage
            else
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
                YesOrNoAndTextMessage
            else
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
                AttachmentOrTextMessage
            else
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                YesAttachmentOrTextMessage
            else
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
                NoAttachmentOrTextMessage
            else 
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
                AttachmentAndTextMessage
            else
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                YesAttachmentAndTextMessage
            else
            if (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
                NoAttachmentAndTextMessage
            else
                blank
   			
	Conditions
		HasQuestionAttachment
			restricted
			when (SourcingEventQuestion.Attachment entered)
		HasQuestionAnswerAttachment
			when (Attachment entered)
		HasWeightedQuestion
			restricted
			when (SourcingEventQuestion.HasWeighting)
		
		CanSetCMToNo
			restricted
			when (SourcingEvent.ContractOutputExists 
			and   CMAttachment
			and   HasQuestionAnswerAttachment
			and   SourcingEventQuestion.CMQuestion)
			
		CanSetCMToYes
			restricted
			when (SourcingEvent.ContractOutputExists
			and   CMAttachment = false
			and   HasQuestionAnswerAttachment
			and   SourcingEventQuestion.CMQuestion)
			
		DisplayCMAttachmentButton
			restricted
			when (SourcingEvent.ContractOutputExists
			and   HasQuestionAnswerAttachment
			and   SourcingEvent.Status.Open
			and   SourcingEventQuestion.CMQuestion)
			
		HasResponseAttachment
			restricted
            when (Attachment entered)
        
        AttachmentAlwaysRequired   
			restricted
            when (SourcingEventQuestion.AlwaysRequireResponseAttachment
            and  (SourcingEventQuestion.NonYesNoResponseType
            or    SourcingEventQuestion.ResponseType.YesNo))

		
	Relations
		AllocQuestionAnswerRel 
			one-to-many relation to SourcingEventQuestionAllocation
			Field Mapping uses symbolic key
				related.Company														= Company
				related.SourcingEvent												= SourcingEvent
				related.SourcingEventQuestion 										= SourcingEventQuestion
				related.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer = SourcingEventQuestionAnswer
			Instance Selection
				where (SourcingEventQuestion.EligibleForWeighting)

		AllocQuestionYesNoTextRel 
			one-to-many relation to SourcingEventQuestionAllocation
			Field Mapping uses symbolic key
				related.Company														= Company
				related.SourcingEvent												= SourcingEvent
				related.SourcingEventQuestion 										= SourcingEventQuestion
			Instance Selection
				where (SourcingEventQuestion.EligibleForWeighting
				and    related.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.YesNoText.YesNo = SourcingEventQuestionAnswer.YesNoText.YesNo)
		
		EventQuestionsAndResponsesRel	
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
			Instance Selection
				where (related.Company					= Company
				and related.SourcingEvent				= SourcingEvent
				and related.SourcingEventQuestion 		= SourcingEventQuestion			
    			and related.SourcingEventResponse		= SourcingEventResponse)

		SourcingEventQuestionRel 
			one-to-one relation to SourcingEventQuestion
			Field Mapping uses symbolic key
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventQuestion			= SourcingEventQuestion

		SourcingEventSupplierQuestionRel 
			one-to-many relation to SourcingEventSupplierQuestion
			Field Mapping uses symbolic key
				related.NotifiedSupplier.SupplierGroup	= actor.agent(SupplierSourceId).SupplierGroup
	 			related.NotifiedSupplier.Supplier		= actor.agent(SupplierSourceId).Supplier
	 			related.NotifiedSupplier.SupplierSourceId = actor.agent(SupplierSourceId).SupplierSourceId
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion	= SourcingEventQuestion

	Field Rules
        
        SourcingEventQuestionAnswer
            if (!SourcingEventResponse.NoBid)
	            if (SourcingEventQuestion.ResponseRequired)
	                required
	                    "MustAnswerAllRequiredQuestions"
	                    
	            if (SourcingEventQuestion.ResponseType.YesNoText
	            and !HasResponseAttachment
	            and (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
	            or  (SourcingEventQuestionAnswer.YesNoText.YesNo.Yes
	            and  SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
	            or  (SourcingEventQuestionAnswer.YesNoText.YesNo.No
	            and  SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
	                constraint (SourcingEventQuestionAnswer.YesNoText.Text entered)
	                    "MustEnterEitherTextOrAnAttachmentForQuestion<SourcingEventQuestion.QuestionText>"
	                    
	            if (SourcingEventQuestion.ResponseType.YesNo
	            and (SourcingEventQuestionAnswer.YesNoValue.Yes
	            and  SourcingEventQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
	            or  (SourcingEventQuestionAnswer.YesNoValue.No
	            and  SourcingEventQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SourcingEventQuestion.QuestionText>"    
	            
	            if (AttachmentAlwaysRequired
	            and !SourcingEventQuestion.ResponseType.YesNoText)
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SourcingEventQuestion.QuestionText>"
	                       
	            if  (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
	            or   SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SourcingEventQuestionAnswer.YesNoText.YesNo.Yes
	            and  SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SourcingEventQuestionAnswer.YesNoText.YesNo.No
	            and  SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (SourcingEventQuestionAnswer.YesNoText.Text entered)
	                    "MustEnterTextForQuestion<SourcingEventQuestion.QuestionText>"
	                    
	            if  (SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SourcingEventQuestionAnswer.YesNoText.YesNo.Yes
	            and  SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SourcingEventQuestionAnswer.YesNoText.YesNo.No
	            and  SourcingEventQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SourcingEventQuestion.QuestionText>" 
    
        Attachment
            if (SourcingEventQuestion.AlwaysRequireResponseAttachment
            and !SourcingEventResponse.NoBid)
                required
                    "MustAttachADocumentForAllQuestionsRequiringAnAttachment"
            if (!SourcingEventQuestionAnswer entered)
                cannot be entered
                    "MustAnswerQuestionToAttachADocument"   
                    
        CMAttachment
            if (!SourcingEvent.CreateByCopy
            and  HasQuestionAnswerAttachment
            and  SourcingEventQuestion.CMQuestion)
                default to true
        
    Actions
     	Create is a Create Action
     		Action Rules
 				if (!SourcingEventResponse.HasCurrentModificationRequests)
	 				constraint (SourcingEventResponse.CanUpdateResponse)
 						"BuyerCannotAnswerQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
 					constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEvent.BestAndFinalStarted
	 				and !SourcingEvent.StepTwoStarted
	 				and !SourcingEventResponse.HasCurrentModificationRequests)
	 					constraint (SourcingEvent.CanRespond)  
	 						"CanOnlyRespondBetweenEventOpenDateAndTimeAndCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotCreateIfResponseHasBeenSubmitted"
 				
     	Update is an Update Action
     		Action Rules
 				if (!SourcingEventResponse.HasCurrentModificationRequests)
 					constraint (SourcingEventResponse.CanUpdateResponse)
 						"BuyerCannotUpdateQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEvent.BestAndFinalStarted
	 				and !SourcingEvent.StepTwoStarted
	 				and !SourcingEventResponse.HasCurrentModificationRequests)
	 					constraint (SourcingEvent.CanRespond)  
	 						"CannotUpdateResponseAfterEventCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotUpdateIfResponseHasBeenSubmitted"
 				if (SourcingEventQuestionAnswer changed
 				or  Attachment changed)
 					constraint (!FromTechnicalProposal)
	 					"CannotChangeAnswerWhichWasPartOfAnApprovedTechnicalProposal"
 					
     	Delete is a Delete Action
    		Action Rules
 				constraint (SourcingEventResponse.CanUpdateResponse)
 					"BuyerCannotDeleteQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotDeleteIfResponseHasBeenSubmitted"
 				constraint (!FromTechnicalProposal)
	 				"CannotDeleteAnswerWhichWasPartOfAnApprovedTechnicalProposal"
	 				
	 	UpdateFromAcceptTechnicalProposal is an Update Action
	 		restricted
	 		
	 	UpdateCMAttachment is an Instance Action
	 		
	 		Action Rules
	 			if (CanSetCMToNo)
	 				CMAttachment = false
	 			else
	 			if (CanSetCMToYes)
	 				CMAttachment = true
