EDITransactionHistory is a BusinessClass
    owned by edi
    prefix is HTR
    classic name is EDHISTRANS

    Ontology
        symbolic key is EDITransactionHistory

    Patterns
        implements DynamicCreation
        implements StaticJava
        disable Auditing
		implements CreateStamp		
		implements UpdateStamp

    Persistent Fields

        EDITradingPartner
            default label is "EDINumber"
            classic name for EDITradingPartner is EDI-NBR
        TransactionDate
            classic name for TransactionDate is TRANS-DATE

        EDICarrier is a PfiEDICarrier
            default label is "Carrier"
            classic name is EDICARRIER

        InterchangeSender           is an InterchangeID
            default label is "Sender_(ISA06)"
        InterchangeReceiver         is an InterchangeID
            default label is "Receiver_(ISA08)"
        InterchangeControlNumber    is a ControlNumber
            default label is "ISAControlNumber"
            classic name is ISA-CONTRL-NBR
        GroupSender           is an InterchangeID
            default label is "Sender_(GS02)"
        GroupReceiver         is an InterchangeID
            default label is "Receiver_(GS03)"
        GroupControlNumber       is a ControlNumber
            default label is "GSControlNumber"
            classic name is GS-CONTRL-NBR

        Direction

        EDITransactionType
            default label is "TransactionSet"
            classic name is EDITRANS-TYPE

        SetControlNumber         is a ControlNumber
            default label is "SetControlNumber"
        AppValue                is Alpha size 60
            default label is "DocumentNumber"
        AckExpected             is Numeric size 1           
            default label is "AcknowledgementExpected"
            States
                NoneRequested               value is 0
                AcknowledgementRequested    value is 1

        AckStatus                is Numeric size 1
            States
                NoneRequested               value is 0
                Accepted                    value is 1
                Rejected                    value is 2
                Waiting                     value is 3

        AcknowledgementDate      is Date
            classic name is ACK-DATE

        Commstat                is Numeric size 1
            default label is "CommunicationStatus"
            States
                WAITING                    value is 0
                SUCCESS                    value is 1
                ERROR                      value is 2
        EDIArchive
        ConnectionId            is Numeric size 14
        DataExchangeId			is like EDIDataExchange
        
    Derived Fields
        HistoryDetailsTitle is a DerivedField
            type is Alpha size 60
            if (AppValue entered)
                return AppValue + " - " + EDITransactionType + " Transaction Details"
            else
                return EDITransactionType +  " Transaction Details"

        TransactionData is a DerivedField
        	type is BinaryDocument
        	return EDIArchive.ArchiveData

    Sets
        Set1
            indexed
            Sort Order
                EDITradingPartner
                EDITransactionHistory
        Set2
            indexed
            duplicates
            Sort Order
                TransactionDate descending
                EDITradingPartner
                EDITransactionHistory

        Set3
            indexed
            Sort Order
                TransactionDate
                EDITradingPartner
                EDITransactionHistory

        Set4
            indexed
            Sort Order
                AppValue
                EDITradingPartner
                TransactionDate
                EDITransactionHistory

        Set5
			indexed
			Sort Order	
				DataExchangeId
				EDITransactionHistory
			
			
    Relations
        EDITradingPartnerRel is a EDITradingPartner set
        EDIArchiveRel is a EDIArchive set

        DuplicateTransactionHistoryRel
            one-to-many relation to EDITransactionHistory
            Field Mapping uses Set4
                related.AppValue            = AppValue
                related.EDITradingPartner   = EDITradingPartner
                related.TransactionDate     = TransactionDate

            Instance Selection
                where ( related.EDITransactionHistory != EDITransactionHistory
                and related.EDITransactionType = EDITransactionType
                and related.ConnectionId = ConnectionId )
    Actions
        Create is a Create Action
            Exit Rules
                ConnectionId = current timestamp

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted

        DeleteDuplicates is a Set Action
            Instance Selection
                where (DuplicateTransactionHistoryRel exists)
            Action Rules
            
                Instance Rules
                    invoke Delete DuplicateTransactionHistoryRel
