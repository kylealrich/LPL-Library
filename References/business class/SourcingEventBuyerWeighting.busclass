SourcingEventBuyerWeighting is a BusinessClass 
    owned by ss
    prefix is EVBYW
    sql name is EventBuyerWeighting

    Ontology
    	symbolic key is SourcingEventBuyerWeighting
    	   			    			
    Persistent Fields
    	Criteria						is Text
		BestPossibleScore				is Numeric 3
		CriteriaWeighting				is Percent 6.3
		BuyerWeighting

   	Derived Fields
   			
   	Conditions
   		BuyerWeightingEntered
   			restricted
   			when (BuyerWeighting entered)
	
		HasResponseBuyerWeighting
			restricted
			when (ResponseBuyerWeightingRel exists)
	
	Relations
		ResponseBuyerWeightingRel
    		one-to-many relation to SourcingEventResponseBuyerWeighting
    		Field Mapping uses ByEvent
    			related.Company						= Company
    			related.SourcingEvent				= SourcingEvent
    			related.SourcingEventBuyerWeighting	= SourcingEventBuyerWeighting
    			
    	SubmittedResponsesRel
    		one-to-many relation to SourcingEventResponse
    		Field Mapping uses ByEvent
    			related.Company						= Company
    			related.SourcingEvent				= SourcingEvent
    		Instance Selection 
    			where (related.Status.Submitted)
						
	Sets
		ByBuyerWeighting
			Sort Order
				Company
    			SourcingEvent
    			BuyerWeighting
    		Instance Selection
    			where (BuyerWeightingEntered)
 				
   	Field Rules
   		Criteria
   			required
   		BestPossibleScore
   			if (CriteriaWeighting entered)
   				required
   					"BestPossibleScoreMustBeEnteredWhenWeightingEntered"
    	CriteriaWeighting
    		constraint (CriteriaWeighting <= 100%)
				"CriteriaWeightingCannotBeGreaterThan100%"
			constraint (CriteriaWeighting > 0)
				"CriteriaWeightingCannotBeLessThanZero"
   				   					
 	Actions
		Create is a Create Action
			valid when (!SourcingEvent.ClosedOrCancelled)

			Action Rules
				constraint (!SourcingEvent.ClosedOrCancelled)
					"CannotAddBuyerWeightingWhenEventIsClosedOrCancelled"
				
			Exit Rules
				for each SourcingEvent.SubmittedResponsesExcludingNoBids
					invoke CreateBuyerWeighting each
						invoked.ParmNotifiedSupplier			= each.NotifiedSupplier
						invoked.ParmCompany						= Company
						invoked.ParmSourcingEvent				= SourcingEvent
						invoked.SourcingEventBuyerWeighting		= SourcingEventBuyerWeighting

		Update is an Update Action
			valid when (!SourcingEvent.ClosedOrCancelled)
		
			Action Rules
			
				if (BestPossibleScore changed
				or  CriteriaWeighting changed)
					if (HasResponseBuyerWeighting)
						invoke CalculateEventScoreSet SourcingEventResponse
							invoked.ParmCompany		= Company
							invoked.ParmEvent       = SourcingEvent 
																			
		Delete is a Delete Action
			valid when (!SourcingEvent.ClosedOrCancelled)
			
			Action Rules
				if (HasResponseBuyerWeighting)
					for each ResponseBuyerWeightingRel
						invoke Delete each 
					invoke CalculateEventScoreSet SourcingEventResponse
						invoked.ParmCompany		= Company
						invoked.ParmEvent       = SourcingEvent 
