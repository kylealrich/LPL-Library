LandingPageDetail is a BusinessClass
	owned by lpdesigner

	prefix is LPDtl

	Ontology
		symbolic key is LandingPageDetail

	Patterns
		implements DynamicCreation
		disable EffectiveDated
		disable AsOfDateProcessing
		implements Resequence on DisplaySequence
			new sequence field is NewDisplaySequence
			set is ByDisplaySequence

	Persistent Fields
		LandingPageWidget
		DisplaySequence				is Numeric size 2
			disable Auditing

		DataSizeWidth				is Numeric size 2
			default label is "WidgetWidth"
			States
				OneColumn		value is 1
				TwoColumn		value is 2
				ThreeColumn		value is 3
				FourColumn		value is 4
		WidgetHeight				is Numeric size 2
			States
				1X				value is 1
				2X				value is 2
				3X				value is 3
				4X				value is 4
        AllowRemoval                is Boolean

	Transient Fields
		NewDisplaySequence 			is Numeric size 2

	Conditions
		Template
			when (LandingPage.IsTemplate)

	Relations
		ByDisplaySequenceRel
			one-to-many relation to LandingPageDetail
			Field Mapping uses ByDisplaySequence
				related.LandingPage	= LandingPage

		LandingPageWidgetRel								
			one-to-many relation to LandingPageDetail
			Field Mapping uses symbolic key
				related.LandingPage					= LandingPage
			Instance Selection
				where (related.LandingPageWidget 	= LandingPageWidget)

	Sets
		ByDisplaySequence
			Sort Order
				LandingPage
				DisplaySequence
				LandingPageWidget

	Field Rules
		LandingPageWidget
			required

		DisplaySequence
			if (ByDisplaySequenceRel exists)
				default to last ByDisplaySequenceRel.DisplaySequence + 1
			else
				default to 1

		DataSizeWidth
			default to LandingPageWidget.DataSizeWidth

		WidgetHeight
			default to LandingPageWidget.WidgetHeight

        AllowRemoval
            default to true

	Actions
		CreateTemplate is a Create Action
			restricted
			
		UpdateTemplate is an Update Action
			valid when (Template)
		
		DeleteTemplate is a Delete Action
			valid when (Template)

		Create is an Action
			valid when (!Template)

		Update is an Action
			valid when (!Template)

		Delete is an Action
			valid when (!Template)
