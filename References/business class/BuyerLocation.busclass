BuyerLocation is a BusinessClass
    owned by po
    prefix is BYC
    classic name is BUYCMPLOC

    Ontology
        symbolic key is BuyerLocation
            classic set name is BYCSET1
            classic name for HROrganization is PROCURE-GROUP
            classic name for Buyer is BUYER-CODE
            name for InventoryLocation is Location
            classic name for RequestingLocation is REQ-LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex

    Conditions

        IsByCompany
        	restricted
            when (Location not entered
            and   RequestingLocation not entered)

        IsByLocation
        	restricted
            when (Location entered
            and   RequestingLocation not entered)

        IsByRequestingLocation
        	restricted
            classic name is BY-REQ-LOC
            when (RequestingLocation entered
            and   Location not entered)

	Relations
		SetupAtCompanyRel
			one-to-one relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization		= HROrganization
				related.Buyer				= Buyer
				related.Company				= Company
				related.Location			= blank
				related.RequestingLocation	= blank

		SetupAtLocationRel
			one-to-many relation to BuyerLocation
			Field Mapping uses symbolic key
				related.HROrganization	= HROrganization
				related.Buyer			= Buyer
				related.Company			= Company
			Instance Selection
				where (related.Location entered
				or     related.RequestingLocation entered)

    Sets

        Set3
            indexed
            Instance Selection
                where (IsByCompany)
            Sort Order
                HROrganization
                Buyer
                Company

        Set4
            indexed
            Instance Selection
                where (IsByLocation)
            Sort Order
                HROrganization
                Buyer
                Company
                Location

        Set5
            indexed
            Instance Selection
                where (IsByRequestingLocation)
            Sort Order
                HROrganization
                Buyer
                Company
                RequestingLocation

	Field Rules
		Company
			if (Location not entered
			and RequestingLocation not entered)
				constraint (SetupAtLocationRel not exists)
					"AlreadySetupAtLocationLevel;CannotAddCompany"

		Location
			if (RequestingLocation entered)
				cannot be entered
					"RequestingLocationIsEntered;LocationCannotBeEntered"

			constraint (SetupAtCompanyRel not exists)
				"AlreadySetupAtCompanyLevel;CannotAddLocation"

		RequestingLocation
			if (Location entered)
				cannot be entered
					"LocationIsEntered;RequestingLocationCannotBeEntered"

			constraint (SetupAtCompanyRel not exists)
				"AlreadySetupAtCompanyLevel;CannotAddRequestingLocation"

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
