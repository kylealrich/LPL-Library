AmazonS3ObjectReference is a BusinessClass
	owned by la
	prefix is AS3OB
	default label is "AmazonS\3ObjectReference"

	Ontology
		symbolic key is AmazonS3ObjectReference

    Patterns
    	implements CreateStamp
        disable Auditing
        disable EffectiveDated
        disable AsOfDateProcessing

	Persistent Fields
		BusinessObjectReference is BusinessObjectReference
		S3ObjectKey is Alpha size 120
			default label is "S\3ObjectKey"
		CheckLocalField is Alpha size 80

	Local Fields
		LocalAsyncActionRequest is an AsyncActionRequest

	Field Rules
		CheckLocalField
			default to "IsStoredLocally"

	Relations
		AsyncActionRequestRel
			default label is "AsyncActionRequest"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
				related.DataArea = UpperCaseDataArea
				related.ImplementingClass = "AmazonS3ObjectReference"
				related.AsyncAction = "Sweep"
				
		AutoDisableAsyncActionRequestRel
			default label is "AsyncActionRequest"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
				related.DataArea = UpperCaseDataArea
				related.ImplementingClass = "AmazonS3ObjectReference"
				related.AsyncAction = "Sweep"
			Instance Selection
				where (related.SystemRequest.AutoDisable)

	Derived Fields

		UpperCaseDataArea is a DerivedField
			type is DataAreaName
			restricted
			return parentcontext.dataarea  

		CanDelete is a NativeField
			type is Boolean
			default label is untranslatable

	Actions

		Create is a Create Action
			restricted

			Action Rules
				invoke EndWorkUpdateAsyncRequest

		Delete is a Delete Action
			Entrance Rules
				if (CanDelete)
					invoke DeleteFromS3

		DeleteFromS3 is an Instance Action
			default label is "DeleteFromS\3"
			restricted

		Sweep is a Set Action
			disable checkpoint
			Sort Order
				create stamp

			Action Rules
				Instance Rules
					invoke Delete
						resume on error

		EndWorkUpdateAsyncRequest is an Instance Action
			restricted
			manual update


		UpdateAsyncRequest is an Instance Action
			restricted

			Action Rules

				LocalAsyncActionRequest = first AutoDisableAsyncActionRequestRel.AsyncActionRequest
				if (not LocalAsyncActionRequest exists)
					LocalAsyncActionRequest = first AsyncActionRequestRel.AsyncActionRequest

				if (LocalAsyncActionRequest exists)
					invoke SetPendingScheduling LocalAsyncActionRequest
						invoked.ParamPendingScheduling = true
