MatchingRate is a BusinessClass
    prefix is MtRte

    Ontology
        symbolic key is MatchingRate

    Patterns
    	disable AuditIndex
    	disable Auditing
    	disable EffectiveDated
		implements CreateStamp	
		implements UpdateStamp

    Persistent Fields
        FinanceEnterpriseGroup
        Year 
        Month               is Numeric 2
            States 
                January     value is 1
                February    value is 2
                March       value is 3
                April       value is 4
                May         value is 5
                June        value is 6
                July        value is 7
                August      value is 8
                September   value is 9
                October     value is 10
                November    value is 11
                December    value is 12
        OnlineMatchCount    is Numeric 9
        OnlineMatchAmount   is like InternationalAmount 
        AutoMatchCount      is Numeric 9
        AutoMatchAmount     is like InternationalAmount 
        ReconMatchCount     is Numeric 9
        ReconMatchAmount    is like InternationalAmount 

		TotalMatchCount				is Numeric 9
		TotalMatchAmount			is like InternationalAmount
		OnlineMatchAmountPercent 	is like Percent
		OnlineMatchCountPercent 	is like Percent
		AutoMatchAmountPercent 		is like Percent
		AutoMatchCountPercent 		is like Percent
		ReconMatchAmountPercent 	is like Percent
		ReconMatchCountPercent 		is like Percent

        Status              is Numeric 1 
            States 
                InProcess   value is 0
                Complete    value is 1


        VendorGroup
        Vendor 

    Local Fields 
        LocalFinanceEnterpriseGroup is like FinanceEnterpriseGroup

    Derived Fields 

        TotalCount is a DerivedField 
            type is Numeric 12
            return OnlineMatchCount + AutoMatchCount + ReconMatchCount 
        PercentOnlineMatch is a DerivedField 
            type is like Percent 
            return OnlineMatchCount / TotalCount 
        PercentAutoMatch is a DerivedField 
            type is like Percent 
            return AutoMatchCount / TotalCount 
        PercentReconMatch is a DerivedField 
            type is like Percent 
            return ReconMatchCount / TotalCount 

    Sets 

        YearMonth
            indexed 
            Sort Order 
                FinanceEnterpriseGroup
                Year 
                Month
                MatchingRate 

        YearMonthDescending
            indexed 
            Sort Order 
                FinanceEnterpriseGroup
                Year descending
                Month descending
                MatchingRate 

        ByVendor 
            indexed 
            Sort Order 
                FinanceEnterpriseGroup
                VendorGroup 
                Vendor 
                Year 
                Month
                MatchingRate 

        ByVendorDescending 
            indexed 
            Sort Order 
                FinanceEnterpriseGroup
                VendorGroup 
                Vendor 
                Year descending
                Month descending
                MatchingRate 

    Relations 
    
		LocalMatchingRateRel
			one-to-many relation to MatchingRate
			Field Mapping uses YearMonth
				related.FinanceEnterpriseGroup 				= LocalFinanceEnterpriseGroup 

    Actions 
        Create is a Create Action
            restricted

        Update is an Update Action 
            restricted

        Delete is a Delete Action
            restricted

        Purge is a Purge Action
            restricted

        RunReport is a Set Action 
            Parameters
                PrmFinanceEnterpriseGroup  		is a FinanceEnterpriseGroup
            Parameter Rules
                PrmFinanceEnterpriseGroup
                    default to actor.context.FinanceEnterpriseGroup            
            Instance Selection 
                where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
            Action Rules 
                Empty Set Rules  
                    invoke ReportMatchRates PayablesInvoice 
                        invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup 
                Set Rules 
                    Exit Rules                         
                        invoke ReportMatchRates PayablesInvoice 
                            invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup 
                Instance Rules 
                    invoke Delete 

        MarkAllComplete is a Set Action 
            restricted
            Parameters
                PrmFinanceEnterpriseGroup  		is a FinanceEnterpriseGroup
            Parameter Rules
                PrmFinanceEnterpriseGroup
                    default to actor.context.FinanceEnterpriseGroup            
            Instance Selection 
                where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
            Action Rules 
                Instance Rules 
                    Status = 1 

                    TotalMatchCount				= OnlineMatchCount + AutoMatchCount + ReconMatchCount
                    TotalMatchAmount			= OnlineMatchAmount + AutoMatchAmount + ReconMatchAmount
                    OnlineMatchAmountPercent 	= OnlineMatchAmount / TotalMatchAmount
                    OnlineMatchCountPercent 	= OnlineMatchCount / TotalMatchCount
                    AutoMatchAmountPercent 		= AutoMatchAmount / TotalMatchAmount
                    AutoMatchCountPercent 		= AutoMatchCount / TotalMatchCount
                    ReconMatchAmountPercent 	= ReconMatchAmount / TotalMatchAmount
                    ReconMatchCountPercent 		= ReconMatchCount / TotalMatchCount

        DeleteAll is a Set Action 
            Parameters
                PrmFinanceEnterpriseGroup  		is a FinanceEnterpriseGroup
            Parameter Rules
                PrmFinanceEnterpriseGroup
                    default to actor.context.FinanceEnterpriseGroup            
            Instance Selection 
                where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
            Action Rules 
                Instance Rules 
                    invoke Purge
