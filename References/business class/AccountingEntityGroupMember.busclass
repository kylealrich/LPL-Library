AccountingEntityGroupMember is a BusinessClass
	owned by GeneralLedger
	
	prefix is AEGpM
	
	Ontology
		part of AccountingEntityGroup
			relative key is AccountingEntity
	
	Patterns
		implements StaticJava
		implements BODId

	Persistent Fields
		MatrixEntityGroupMember			is Boolean
	
	Local Fields
		ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		LocalNativeLPLBODTrigger		is Boolean				
		LocalMember                     is an AccountingEntity
#ifdef module integration
		NewBODTracker  				is a FSMInboundBODTracker view
#endif
		LocalFSMInboundBODTracker	is Numeric 15
		Error            			is Boolean
	    ErrorMessage     			is Alpha 300
		LocalBODTrigger				is Boolean
		LocalConfigurationParameter	is Alpha size up to 200
	Context Fields
#ifdef module integration
		FSMInboundBODTracker
#endif

	Derived Fields

#ifdef module integration
		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
	
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or ActionCode.Create)
				return "Add"
			else
			if(action type.Update or action type.Delete)
				return "Replace"
			else
				return ""	
				
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create)
				return "Open"
			else
			if(action type.Delete or ActionCode.Delete)
				return "Deleted"
			else
			if(action type.Update)
				return "Open"
			else
				return ""	
			
		DerivedAccountingEntityGroupAccountingEntity is a DerivedField
			type is Alpha 40
			restricted
			return AccountingEntityGroup + DerivedDelimiter  + AccountingEntity
			
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
		
		DerivedFinanceEnterpriseGroupAccountingEntity is a DerivedField
			type is Alpha 20
			restricted
			return FinanceEnterpriseGroup + DerivedDelimiter + AccountingEntity
		
		DerivedAccountingEntityDescription is a DerivedField
			type is Alpha 80
			restricted
			return AccountingEntity.Name
		
		DerivedBODDocumentID is a DerivedField
			type is Alpha 50
			restricted
			return FinanceEnterpriseGroup + DerivedDelimiter + AccountingEntityGroup + DerivedDelimiter + AccountingEntity
			
		DerivedAccountingEntityGroupDescription is a DerivedField
			type is Alpha 80
			restricted
			return AccountingEntityGroup.Description
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
					
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return  "infor-nid:" + DerivedTenantID + ":" + ":"  + ":" + DerivedBODDocumentID + ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
		
		AccountingEntityGroupMemberXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	        AccountingEntityGroupMemberXMLBOD = template.IONSyncCodeDefination_AccountingEntityGroupMember_ST document for this instance
#endif

	Conditions

		MatrixSecurityGroup
			restricted
			when (SecurityGroupRel.MatrixSecurityGroup)

		MatrixSecurityEnabled
            restricted
            when (FinanceEnterpriseGroup.ResponsibilityMatrixSecurity)
			
	Relations

#ifdef module integration
		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1
		
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
		
#endif

		AllForThisMemberRel
			one-to-many relation to AccountingEntityGroupMember
			Field Mapping uses ByMember
				related.AccountingEntity  = AccountingEntity

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 1
				related.FinanceDimensionStructure	  = AccountingEntityGroup

	Sets
	
		ByMember
			Sort Order
				AccountingEntity
				FinanceEnterpriseGroup
				AccountingEntityGroup
	
	Rule Blocks					
		TriggerAccountingEntityGroupServiceRules
			increment bod id.VariationID
			trigger "AccountingEntityGroupService" PA service
				resume on error
				title is "EG:<FinanceEnterpriseGroup>AEG:<AccountingEntityGroup>"
				Criteria
					FinanceEnterpriseGroup
				Variables
					ActionCode
					FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					include persistent fields from AccountingEntityGroup
					include persistent fields from AccountingEntity
					bod id.VariationID
						variable name is VariationID
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
			
					
	Field Rules
		AccountingEntity
			constraint(AccountingEntity.PostingEntity)
				"CannotEnterSummary<FinanceEnterpriseGroup.AccountingEntityLabel>"
	
	Actions
		Create is a Create Action
        							
        CreateNewMemberWhereThisMemberExists is an Instance Action
        	Parameters
        		NewMember is an AccountingEntity
        		
        	Parameter Rules
        		NewMember
        			required
        			constraint (NewMember != AccountingEntity)
        				"NewMemberCannotBeTheSameAsThisMember"
					constraint(AccountingEntity.PostingEntity)
						"CannotEnterSummary<FinanceEnterpriseGroup.AccountingEntityLabel>"
        				
        	Action Rules
        		for each AllForThisMemberRel
					invoke Create AccountingEntityGroupMember
						resume on error
						invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
						invoked.AccountingEntityGroup 		= each.AccountingEntityGroup
						invoked.AccountingEntity        	= NewMember
					commit transaction        			
        				
        Update is an Update Action
        
		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
		
		Delete is a Delete Action
        	Entrance Rules
				if (MatrixSecurityGroup)
					confirmation required
						"Accounting_Entity_Group_MemberIsPartOfSecurityGroupCreatedFromResponsibilityMatrix.AreYouSureYouWantToDeleteThisRecord?"
        		if (FinanceEnterpriseGroup.BODTrigger)
					ActionCode	= ActionCode.Delete
					include TriggerAccountingEntityGroupServiceRules



			Exit Rules
        		invoke BuildEntityList AccountingEntityGroup

		DeleteMatrixEntityMembers is a Set Action
			restricted
			disable checkpoint
			
			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    MatrixEntityGroupMember)
			
			Action Rules
				Instance Rules
					invoke Delete
		
		BODDataInitialLoadCodeDefAccountingEntityCriteria1 is a Set Action
			restricted
			Parameters
				PrmFEG							is a FinanceEnterpriseGroup
				PrmFromAccountingEntityGroup	is a AccountingEntityGroup
				PrmToAccountingEntityGroup		is a AccountingEntityGroup					
			
			Parameter Rules
			
			Instance Selection
				where  (((PrmFEG entered
				and		  PrmFEG = FinanceEnterpriseGroup)
				or		  PrmFEG not entered)	
				and		((PrmFromAccountingEntityGroup entered
				and		  PrmFromAccountingEntityGroup <= AccountingEntityGroup)
				or		  PrmFromAccountingEntityGroup not entered)				
				and    ((PrmToAccountingEntityGroup entered
				and		 PrmToAccountingEntityGroup >= AccountingEntityGroup)
				or		 PrmToAccountingEntityGroup not entered))
				
			Sort Order
				AccountingEntity
			
			Action Rules	
				AccountingEntity Set Rules
					Entrance Rules
					
					Exit Rules				
						
				Instance Rules	
					if (FinanceEnterpriseGroup.BODTrigger)
						increment bod id.VariationID
						ActionCode	= ActionCode.Create
						include TriggerAccountingEntityGroupServiceRules
#ifdef module integration						


		SendAccountingEntityGroupMemberBODNativeLPL is an Instance Action			
			restricted
			Action Rules
				send ion bod
					bod is AccountingEntityGroupMemberXMLBOD
					bod type is "Sync.CodeDefinition"
					document id is DerivedBODDocumentID
					accounting entity is DerivedAccountingEntityGroupAccountingEntity
					variation id is	BODVariationID
				
		TriggerAccountingEntityGroupMemberNativeLPLBOD is an Instance Action			
			restricted
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "AccountingEntityGroupMember"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_AccountingEntityGroupMember_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= DerivedBODDocumentID
							invoked.BODVariationID			= BODVariationID
							invoked.BODAccountingEntity		= DerivedAccountingEntityGroupAccountingEntity
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Reference1				= AccountingEntityGroup
							invoked.Reference2				= AccountingEntity
							invoked.Reference3				= "AccountingEntityGroupMember"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else	 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= DerivedBODDocumentID
							invoked.BODVariationID			= BODVariationID
							invoked.BODAccountingEntity		= DerivedAccountingEntityGroupAccountingEntity
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Reference1				= AccountingEntityGroup
							invoked.Reference2				= AccountingEntity
							invoked.Reference3				= "AccountingEntityGroupMember"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendAccountingEntityGroupMemberBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= AccountingEntityGroupMemberXMLBOD
							invoked.BODID								= DerivedBODFormattedId
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODXML									= AccountingEntityGroupMemberXMLBOD
							invoked.BODID									= DerivedBODFormattedId
						
#endif


		PurgeGLRecordAccountEntityGrp is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup 		is a FinanceEnterpriseGroup
				PrmAccountingEntityGroup 		is an AccountingEntityGroup
				PrmCloseConfiguration			is a GeneralLedgerCloseConfiguration
				PrmGeneralLedgerCloseYear		is a GeneralLedgerCloseYear
				PrmGeneralLedgerSystemCode 		is a GeneralLedgerSystemCode
				PrmRecordType					is a RecordType
				PrmSystemCodeSelectionOption 	is Numeric 1
				PrmEntityOption					is Numeric 1
				PrmPurgeGeneralLedgerTotal		is Boolean
				PrmReportOnly					is Boolean
				PrmPurgeGLRecordsResult			is a PurgeGLRecordsResult
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmAccountingEntityGroup
					required
				PrmCloseConfiguration
					required
				PrmGeneralLedgerCloseYear
					required
				PrmSystemCodeSelectionOption
					required

			Local Fields
		    	LocalPurgeGLRecordsResultDetailView is a PurgeGLRecordsResultDetail view
		    	PurgeGLBackgroundGroup				is AlphaUpper up to 200
		    	LocalPostingDateRange				is a DateRange	
			Instance Selection
		        where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
		        and 	AccountingEntityGroup = PrmAccountingEntityGroup
		        and 	AccountingEntity.Status.Active)
		    Action Rules
		    	Empty Set Rules
		    	Set Rules
		    		Entrance Rules
		    			PurgeGLBackgroundGroup = "PurgeGLTransactionHistoryRecord"
		    			LocalPostingDateRange.Begin	= PrmGeneralLedgerCloseYear.DerivedStartDate
						LocalPostingDateRange.End	= PrmGeneralLedgerCloseYear.DerivedCloseYearEndDate
		    		Exit Rules

		    	Instance Rules
		            invoke Create PurgeGLRecordsResultDetail
						assign result to LocalPurgeGLRecordsResultDetailView
						invoked.AccountingEntity				= AccountingEntity
						invoked.PurgeGLRecordsResult			= PrmPurgeGLRecordsResult.PurgeGLRecordsResult
						invoked.FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
					invoke CheckAllEntityPeriodsFinalClosed PrmGeneralLedgerCloseYear
						invoked.PrmAccountingEntity 			= AccountingEntity
						invoked.PrmPurgeGLRecordsResult			= PrmPurgeGLRecordsResult
						invoked.PrmPurgeGLRecordsResultDetail	= LocalPurgeGLRecordsResultDetailView.PurgeGLRecordsResultDetail
		            if(!LocalPurgeGLRecordsResultDetailView.PurgeGLRecordsResultDetail.Status=1)
						if( ( PrmGeneralLedgerSystemCode entered and (PrmGeneralLedgerSystemCode = "GL" or PrmRecordType.User) )
							or	PrmSystemCodeSelectionOption = 2 )
							invoke PurgeGLTransactionDetailHistoryRecords GLTransactionDetail in background group(PurgeGLBackgroundGroup)
								invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
								invoked.PrmAccountingEntity				= AccountingEntity
								invoked.PrmGeneralLedgerCloseYear		= PrmGeneralLedgerCloseYear
								invoked.PrmSystemCodeSelectionOption	= PrmSystemCodeSelectionOption
								invoked.PrmRecordType					= PrmRecordType
								invoked.PrmGeneralLedgerSystemCode		= PrmGeneralLedgerSystemCode
								invoked.PrmReportOnly					= PrmReportOnly
								invoked.PrmPurgeGLRecordsResult			= PrmPurgeGLRecordsResult
								invoked.PrmPurgeGLRecordsResultDetail	= LocalPurgeGLRecordsResultDetailView.PurgeGLRecordsResultDetail
								invoked.PrmPostingDateRange				= LocalPostingDateRange
			            invoke PurgeGLTransactionPostedRecords GeneralLedgerTransaction in background group(PurgeGLBackgroundGroup)
							invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.PrmAccountingEntity				= AccountingEntity
							invoked.PrmGeneralLedgerCloseYear		= PrmGeneralLedgerCloseYear
							invoked.PrmSystemCodeSelectionOption	= PrmSystemCodeSelectionOption
							invoked.PrmRecordType					= PrmRecordType
							invoked.PrmGeneralLedgerSystemCode		= PrmGeneralLedgerSystemCode
							invoked.PrmReportOnly					= PrmReportOnly
							invoked.PrmPurgeGLRecordsResult			= PrmPurgeGLRecordsResult
							invoked.PrmPurgeGLRecordsResultDetail	= LocalPurgeGLRecordsResultDetailView.PurgeGLRecordsResultDetail
						if(PrmPurgeGeneralLedgerTotal)
							invoke PurgeGLTotalRecords GeneralLedgerTotal in background group(PurgeGLBackgroundGroup)
								invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
								invoked.PrmAccountingEntity				= AccountingEntity
								invoked.PrmGeneralLedgerCloseYear		= PrmGeneralLedgerCloseYear
								invoked.PrmSystemCodeSelectionOption	= PrmSystemCodeSelectionOption
								invoked.PrmRecordType					= PrmRecordType
								invoked.PrmGeneralLedgerSystemCode		= PrmGeneralLedgerSystemCode
								invoked.PrmReportOnly					= PrmReportOnly	
								invoked.PrmPurgeGLRecordsResult			= PrmPurgeGLRecordsResult
								invoked.PrmPurgeGLRecordsResultDetail	= LocalPurgeGLRecordsResultDetailView.PurgeGLRecordsResultDetail   	

		
	Action Exit Rules
		if (FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode	= ActionCode.Update
					if (action type.Create)
						ActionCode	= ActionCode.Create				
					else
						if (action type.Delete)
							ActionCode	= ActionCode.Delete
					include TriggerAccountingEntityGroupServiceRules



	
		
