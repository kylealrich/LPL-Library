GMDNPTCode is a BusinessClass
	owned by ic
	
	prefix is GMDN
	
	Ontology
		symbolic key is GMDNPTCode
		
	Patterns
        disable AuditIndex
	
	Persistent Fields
		Name				is Alpha size 120
		Definition 			is Text
		Active				is Boolean

	Local Fields 
		LocalContractTierGroup	
	
	Attach Rules
		constraint (GMDNPTCode exists)
			"GMDN_Term<GMDNPTCode>DoesNotExist"	
		constraint (Active)
			"GMDN_Term<GMDNPTCode>MustBeActive"
							
	Field Rules
		Name
			required
		
		Definition
			required
		
		Active
			initial value is true
			default to true 

	Conditions
		RecordExists
			restricted
			when (GMDNPTCode exists)

	Relations 
		GMDNCategoryTermLinkRel 
			one-to-many relation to GMDNCategoryTermLink
			Field Mapping uses symbolic key 
				related.ItemGroup		= ItemGroup 
				related.GMDNPTCode      = GMDNPTCode 

		ItemRel 
			one-to-many relation to Item 
			Field Mapping uses ByGMDNTerm 
				related.ItemGroup       = ItemGroup 
				related.GMDNPTCode 		= GMDNPTCode

		ContractLineRel 
			one-to-many relation to ContractLine 
			Field Mapping uses symbolic key 
				related.ContractGroup     = ItemGroup 
			Instance Selection 
				where (related.LineNotClosed
				and    related.GMDNPTCode = GMDNPTCode)		

		ContractTierQualifierCodeRel
    		one-to-many relation to ContractTierQualifierCode
            Field Mapping uses  symbolic key
          		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
           		related.Contract			  = LocalContractTierGroup.Contract
           		related.ContractTier		  = LocalContractTierGroup.ContractTier
           		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
           	Instance Selection
           		where (related.GMDNPTCode = GMDNPTCode)						
	
	Actions
    	Create is a Create Action
    		
    	Update is an Update Action
    		
    	Delete is a Delete Action

		AttachToTierQualifier is an Instance Action
			Parameters
				ParmContractGroup			is a ContractGroup 
				ParmContract                is a Contract 
				ParmContractTier            is a ContractTier 
				ParmContractTierQualifier   is a ContractTierQualifier 
			
			Action Rules
				LocalContractTierGroup.ContractGroup 		 = ParmContractGroup
				LocalContractTierGroup.Contract 			 = ParmContract
				LocalContractTierGroup.ContractTier 		 = ParmContractTier
				LocalContractTierGroup.ContractTierQualifier = ParmContractTierQualifier
				constraint (ParmContract.NotClosed)
    				"CannotAdd;ContractIsClosed"							
				constraint (ContractTierQualifierCodeRel !exists)
					"GMDNTermIsAlreadyAQualifierCodeForThisContract"
				
				if (!ContractTierQualifierCodeRel exists)
					invoke Create ContractTierQualifierCode
						invoked.Contract			  = ParmContract
						invoked.ContractGroup		  = ParmContractGroup
						invoked.ContractTier		  = ParmContractTier
						invoked.ContractTierQualifier = ParmContractTierQualifier
						invoked.GMDNPTCode  	  	  = GMDNPTCode
						invoked.ItemCategory		  = 5		
