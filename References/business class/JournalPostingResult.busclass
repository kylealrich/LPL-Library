JournalPostingResult is a BusinessClass
	owned by GeneralLedger
	
	prefix is GLPST
	
	Ontology
		symbolic key is JournalPostingResult
		
	Patterns

 		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		RunTime					is TimeStamp
        AccountingEntityOption	is Numeric 1
        	States
        		All				value is 1
        		Single			value is 2
        		Group			value is 3
		AccountingEntity
		AccountingEntityGroup

		Period					is like GeneralLedgerCalendarPeriod
		Ledger
		System					is a GeneralLedgerSystemCode
		PeriodOption			is Numeric 1
			States
				CurrentPeriod			value is 1
				PreviousPeriod			value is 2
				NextPeriod				value is 3
				SpecifyYearAndPeriod	value is 4
				AllPeriods				value is 5
		PostThroughDate			is Date		

	Field Rules
	
	Conditions
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))
	
	Relations
		PeriodRel
			one-to-one relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration	= DerivedCloseConfiguration
				related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = Period
	
		AccountingEntityRel
			one-to-many relation to AccountingEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.ActivePostingEntity)
							
		JournalsPostedRel
			one-to-many relation to GeneralLedgerJournalControl
			Field Mapping uses ByJournalPostingResult
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.JournalPostingResult	= JournalPostingResult
				
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup	= actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity		= AccountingEntity
				
	Sets
		ByDescendingJournalPostingResult
    		duplicates
			Sort Order
				FinanceEnterpriseGroup
				RunTime descending
				JournalPostingResult
				 
	Derived Fields
		DerivedCloseConfiguration	is a DerivedField
			type is like GeneralLedgerCloseConfiguration
			restricted
			if (AccountingEntityOption.All)
				return first AccountingEntity.CloseConfiguration
			else
			if (AccountingEntityOption.Group)
				return AccountingEntityGroup.first AccountingEntityGroupMember set.AccountingEntity.CloseConfiguration
			else
				return AccountingEntity.CloseConfiguration 
				
	Actions
		Create is an Action
			restricted
		Update is an Action
			restricted
		Delete is an Action
		Purge is a Purge Action											
