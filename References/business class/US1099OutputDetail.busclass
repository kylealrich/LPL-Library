US1099OutputDetail is a BusinessClass
    owned by ap
    prefix is UOD

    Ontology
        symbolic key is US1099OutputDetail

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		Data 				is Text
		RecordType			is AlphaUpper size 1
			States
				TRecord		value is "T"
				ARecord     value is "A"
				CRecord     value is "C"
				FRecord     value is "F"
				BRecord     value is "B"
				KRecord     value is "K"
		ReportType			is AlphaUpper size 1
            States
                Miscellaneous           value is "M"
                InterestIncome          value is "I"
                Dividend                value is "D"
                GovernmentPayments      value is "G"
                MerchCardAndThirdParty  value is "K"
                NonEmployeeCompensation value is "N"

		PayorName			is a Name	 
			holds pii
		PayorEmailAddress   is an EmailAddressMulti 
			holds pii
		PayorAddress		is like PostalAddressV2
		PayorPhoneNumber	is a TelephoneNumber 
			holds pii
		PayorTaxID			is a TaxID 
			holds pii
		PayorAccountNumber	is Alpha size 20

		VendorTaxID			is a TaxID 
			holds pii
			restricted
		VendorTaxIDType 	is a TaxIDType 
		VendorName			
		VendorEmailAddress  is an EmailAddressMulti 
			holds pii
		VendorAddress		is like PostalAddressV2
		SecondTINNotice		is AlphaUpper 1
		FATCAIndicator		is AlphaUpper 1
		ForeignCountry		is AlphaUpper 1
		DirectSalesOver5000 is AlphaUpper 1

		IncomeAmount1		is Decimal 18.2
		IncomeAmount2		is Decimal 18.2
		IncomeAmount3		is Decimal 18.2
		IncomeAmount4		is Decimal 18.2
		IncomeAmount5		is Decimal 18.2
		IncomeAmount6		is Decimal 18.2
		IncomeAmount7		is Decimal 18.2
		IncomeAmount8		is Decimal 18.2
		IncomeAmount9		is Decimal 18.2
		IncomeAmount10		is Decimal 18.2
		IncomeAmount11		is Decimal 18.2
		IncomeAmount12		is Decimal 18.2
		IncomeAmount13		is Decimal 18.2
		IncomeAmount14		is Decimal 18.2
		IncomeAmount15		is Decimal 18.2
		IncomeAmount16		is Decimal 18.2
		IncomeAmount17		is Decimal 18.2
		IncomeAmount18		is Decimal 18.2
		IDMJobB				is an IDMJob
			protected
			delete ignored	
		IDMJobC				is an IDMJob
			protected
			delete ignored		
		CopyB				is a DocumentPID
			protected
		CopyC				is a DocumentPID
			protected
		CopyBSentToVendor   is Boolean
			protected
		CopyCSentToPayer    is Boolean
			protected

	Transient Fields
				
	Rule Blocks

    Derived Fields
		DerivedIncomeAmount1 is a DerivedField
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG
			or IncomeAmount1 != 0)
				return IncomeAmount1		

		DerivedIncomeAmount2 is a DerivedField
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG
			or IncomeAmount2 != 0)
				return IncomeAmount2		
			
		DerivedIncomeAmount3 is a DerivedField
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount3 != 0)
				return IncomeAmount3	
			
		DerivedIncomeAmount4 is a DerivedField
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount4 != 0)
				return IncomeAmount4		

		DerivedIncomeAmount5 is a DerivedField
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount5 != 0)
				return IncomeAmount5		
			
		DerivedIncomeAmount6	is a DerivedField		
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount6 != 0)
				return IncomeAmount6		
		
		DerivedIncomeAmount7	is a DerivedField	
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount7 != 0)
				return IncomeAmount7	
		
		DerivedIncomeAmount8	is a DerivedField	
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount8 != 0)
				return IncomeAmount8	
			
		DerivedIncomeAmount9	is a DerivedField	
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG		
			or IncomeAmount9 != 0)
				return IncomeAmount9	
			
		DerivedIncomeAmount10	is a DerivedField	
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG
			or IncomeAmount10 != 0)
				return IncomeAmount10	
			
		DerivedIncomeAmount11	is a DerivedField	
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount11 != 0)
				return IncomeAmount11		
			
		DerivedIncomeAmount12	is a DerivedField
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG		
			or IncomeAmount12 != 0)
				return IncomeAmount12	
			
		DerivedIncomeAmount13	is a DerivedField	
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount13 != 0)
				return IncomeAmount13	
			
		DerivedIncomeAmount14	is a DerivedField	
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount14 != 0)
				return IncomeAmount14	
			
		DerivedIncomeAmount15	is a DerivedField	
			type is Alpha 20	
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount15 != 0)
				return IncomeAmount15		
			
		DerivedIncomeAmount16	is a DerivedField	
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount16 != 0)
				return IncomeAmount16		
  
		DerivedIncomeAmount17	is a DerivedField	
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount17 != 0)
				return IncomeAmount17	
				
		DerivedIncomeAmount18	is a DerivedField	
			type is Alpha 20
			if (US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG	
			or IncomeAmount18 != 0)
				return IncomeAmount18				
  
        FormattedVendorTaxID is a DerivedField 
            type is like TaxID
        	holds pii
            if (VendorTaxIDType.SSN)
                return (VendorTaxID[1:3] + "-" + VendorTaxID[4:5] + "-" + VendorTaxID[6:25])
            else
            if (VendorTaxIDType.EIN)
                return (VendorTaxID[1:2] + "-" + VendorTaxID[3:25])
            else
                return VendorTaxID  
    	
    	FormattedPayorTaxID is a DerivedField		
    		type is like TaxID
    		return (PayorTaxID[1:2] + "-" + PayorTaxID[3:25])
    			
     	MaskedVendorTaxID is a DerivedField 
    		type is like TaxID
     		holds pii
            if (VendorTaxIDType.EIN)
                return ("XX-XXX" + FormattedVendorTaxID[7:25])
            else
            if (VendorTaxIDType.SSN)
                return ("XXX-XX-" + FormattedVendorTaxID[8:25])
            else
            	return ("XXXXX" + VendorTaxID[6:25])
 
    	DerivedForeignCountry is a DerivedField 
    		type is like Name
    		holds pii
    		if (ForeignCountry entered)
    			return VendorAddress.Country
    		else
    			return "U.S. Possession"
    
		DerivedReportType	is a DerivedField 
			type is Text
			restricted
			if (ReportType.Miscellaneous)
				return "1099-MISC" 
			else
			if (ReportType.InterestIncome)
				return "1099-INT"
			else
			if (ReportType.Dividend)
				return "1099-DIV"
    		else
    		if (ReportType.GovernmentPayments)
    			return "1099-G"
			else
			if (ReportType.MerchCardAndThirdParty)
				return "1099-K"
			else
			if (ReportType.NonEmployeeCompensation)
				return "1099-NEC"
    
		DerivedReportTypeDescription	is a DerivedField 
			type is Text
			restricted
			if (ReportType.Miscellaneous)
				return "Miscellaneous Income"
			else
			if (ReportType.InterestIncome)
				return "Interest Income"
			else
			if (ReportType.Dividend)
    			return "Dividends And Distributions"
    		else
    		if (ReportType.GovernmentPayments)
				return "Certain Government Payments"
			else
			if (ReportType.MerchCardAndThirdParty)
				return "Payment Card And Third Party Network Transactions"
			else
			if (ReportType.NonEmployeeCompensation)
				return "Non Employee Compensation"

		DerivedCopyBFileName is a DerivedField
			type is like IDMFileName	
			restricted		
			return DerivedReportType + "_Copy-B_" + VendorName + "_" + US1099OutputHeader.DerivedReportYear + "_LaserCopy.pdf"

		DerivedCopyCFileName is a DerivedField
			type is like IDMFileName		
			restricted	
			return DerivedReportType + "_Copy-C_" + VendorName + "_" + US1099OutputHeader.DerivedReportYear + "_LaserCopy.pdf"

		DerivedCopyBEmailSubject is a MessageField
			restricted
			"Form<DerivedReportType>_\Copy_\B"

		DerivedCopyCEmailSubject is a MessageField
			restricted
			"Form<DerivedReportType>_\Copy_\C"

		DerivedEmailFrom is a DerivedField
			type is like EmailAddress
			restricted
			if (US1099OutputHeader.TaxGroup.US1099FromEmail entered)
				return US1099OutputHeader.TaxGroup.US1099FromEmail
			else
				return config.DefaultFromEmailAddress

		DerivedPayorPhoneNumber is a StringField
			type is Text
			DerivedInternationalPrefix
			DerivedSubscriberNumber
			DerivedExtension

		DerivedSubscriberNumber	is a DerivedField
			type is Alpha 16
			restricted
			if (PayorPhoneNumber.SubscriberNumber entered)
				if (PayorPhoneNumber.InternationalPrefix entered)
					return (" " + PayorPhoneNumber.SubscriberNumber)
				else
					return PayorPhoneNumber.SubscriberNumber
				
		DerivedExtension	    is a DerivedField
			type is Alpha 11
			restricted
			if (PayorPhoneNumber.Extension entered)
				return (" " + PayorPhoneNumber.Extension)
		
		DerivedInternationalPrefix is a DerivedField
			type is Alpha 7
			restricted
			if (PayorPhoneNumber.InternationalPrefix entered)
				return ("+" + PayorPhoneNumber.InternationalPrefix)

		DerivedCopyBIDMTemplateDocumentType				is a DerivedField
			type is Numeric 3
			restricted
			if (ReportType.Dividend)
				return 29 
			else
    		if (ReportType.GovernmentPayments)
    			return 32 
			else
			if (ReportType.InterestIncome)
				return 35 
    		else
			if (ReportType.MerchCardAndThirdParty)
				return 38 
			else
			if (ReportType.Miscellaneous)
				return 41 
			else
			if (ReportType.NonEmployeeCompensation)
				return 44 
				

		DerivedCopyCIDMTemplateDocumentType 	    is a DerivedField
			type is Numeric 3
			restricted
			if (ReportType.Dividend)
				return 30 
			else
    		if (ReportType.GovernmentPayments)
    			return 33 
			else
			if (ReportType.InterestIncome)
				return 36 
    		else
			if (ReportType.MerchCardAndThirdParty)
				return 39 
			else
			if (ReportType.Miscellaneous)
				return 42 
			else
			if (ReportType.NonEmployeeCompensation)
				return 45 
				
		DerivedCopyBLink is a DerivedField
			type is Alpha 2083
			restricted
			if (CopyB entered)
				IDMItem.DocumentType	= "FSM_US1099Form"
				IDMItem.IDMUniqueId		= CopyB	
				return IDMItem.GetLink
			return blank

		DerivedCopyCLink is a DerivedField
			type is Alpha 2083
			restricted
			if (CopyC entered)
				IDMItem.DocumentType	= "FSM_US1099Form"
				IDMItem.IDMUniqueId		= CopyC	
				return IDMItem.GetLink
			return blank			

		DerivedReportYearPrint is a DerivedField
			type is Alpha 4
			if (US1099OutputHeader.DerivedReportYear = 2024)
				return US1099OutputHeader.DerivedReportYear
			else
				return US1099OutputHeader.DerivedReportYear[3:4]

		DerivedCopyBYear is a DerivedField 
			type is AlphaUpper 4
			restricted
			return ""

		DerivedCopyCYear is a DerivedField 
			type is AlphaUpper 4
			restricted
			return ""

		DerivedCopyBEmailNotifMsg is a MessageField
			restricted
			"<DerivedCopyBFileName>_wasSentTo_\Vendor"

		DerivedCopyCEmailNotifMsg is a MessageField
			restricted
			"<DerivedCopyCFileName>_wasSentTo_\Payer"

		DerivedCopyACorrected is a DerivedField
			type is AlphaUpper 1
			if (US1099OutputHeader.TapeSelection.OneTransactionCorrectedG
			or  US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG)
				return "O"

		DerivedCopyBOrCCorrected is a DerivedField
			type is AlphaUpper 1
			if (US1099OutputHeader.TapeSelection.OneTransactionCorrectedG
			or  US1099OutputHeader.TapeSelection.TwoTransactionCorrectedG)
				return "O"
					
	Local Fields

		IDMAttributes
		IDMGenerateDocument
		IDMItem		
		IDMBatchOperation
					
	Context Fields

    Conditions
    	HasCopyB
    		restricted
    		when (CopyB entered)
    		
    	HasCopyC
    		restricted
    		when (CopyC entered)

		HasGeneratedCopy
			restricted
			when (HasCopyB
			or 	  HasCopyC)

		HasVendorEmailAddress
			restricted
			when (VendorEmailAddress entered)
			
		HasPayorEmailAddress
			restricted
			when (PayorEmailAddress entered)
		
    Relations


		IDMTemplateCopyBRel
			one-to-many relation to IDMTemplate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = VendorGroup.FinanceEnterpriseGroup
			Instance Selection
				where (related.IDMDocumentType = DerivedCopyBIDMTemplateDocumentType
				and    related.TemplateYear	   = US1099OutputHeader.DerivedReportYear)

		IDMTemplateCopyCRel
			one-to-many relation to IDMTemplate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = VendorGroup.FinanceEnterpriseGroup
			Instance Selection
				where (related.IDMDocumentType = DerivedCopyCIDMTemplateDocumentType
				and    related.TemplateYear	   = US1099OutputHeader.DerivedReportYear)

    Sets
		BRecordType
			Instance Selection
				where (RecordType.BRecord)			
			Sort Order
				VendorGroup
				US1099OutputHeader
				US1099OutputDetail
				
    Field Rules
		RecordType
			default to Data[1:1]
			
		PayorAddress
			default to US1099OutputHeader.TaxGroup.PostalAddress	
				
			
	Actions
        Create is a Create Action
        	restricted

        Update is an Update Action
			restricted
							
        Delete is a Delete Action
			restricted

		FastUpdate is an Update Action
			restricted
			bypass field rules						
			Action Rules

		EmailIDM1099FormSet is a Set Action
			default label is untranslatable
			restricted
			Parameters
        		PrmVendorGroup      	is a VendorGroup
        		PrmUS1099OutputHeader	is a US1099OutputHeader			
				PrmEmailCopyBToVendors  is Boolean
				PrmEmailCopyCToPayer    is Boolean
			
			Instance Selection				
 				where (VendorGroup 		  = PrmVendorGroup
				and    US1099OutputHeader = PrmUS1099OutputHeader
				and    RecordType.BRecord
				and    HasGeneratedCopy)
				
			Action Rules
				Instance Rules
					invoke EmailForm1099 in background
						invoked.EmailCopyBToVendors = PrmEmailCopyBToVendors
						invoked.EmailCopyCToPayer   = PrmEmailCopyCToPayer
	
						
		EmailForm1099 is an Instance Action
			valid when (HasGeneratedCopy)		
			Parameters
				EmailCopyBToVendors is Boolean
				EmailCopyCToPayer   is Boolean

			Parameter Rules
				EmailCopyBToVendors
					if (not EmailCopyBToVendors
					and not EmailCopyCToPayer)
						required
						 	"AtLeastOneEmailCopyIsNeededToProceed"	
						 			
			Local Fields
    			IDMJobBView	 		is an IDMJob view
    			IDMJobCView	 		is an IDMJob view			
		
			Action Rules
    			initialize IDMGenerateDocument
    			IDMGenerateDocument.IDMXMLDefinition.Busclass	  = reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName     = "US1099OutputDetailIDMList"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName = "US1099Form"

				initialize IDMItem
				IDMItem.DocumentType		 = "FSM_US1099Form"
				IDMItem.IDMXMLDefinition	 = IDMGenerateDocument.IDMXMLDefinition
				
				if (EmailCopyBToVendors
				and HasCopyB
				and VendorEmailAddress entered)
					IDMItem.IDMUniqueId		 = CopyB
			    	IDMItem.IDMEmail.From	 = DerivedEmailFrom
					IDMItem.IDMEmail.To		 = VendorEmailAddress
					IDMItem.IDMEmail.Subject = DerivedCopyBEmailSubject
					IDMItem.IDMEmail.Body	 = blank

					constraint (IDMItem.GetItemDetails)
						"DocumentDoesNotExistInIDM"
					
					IDMItem.IDMPID = IDMItem.IDMItemDetails.PID
					
					invoke SendToEmail IDMJob
						assign result to IDMJobBView
						invoked.LongDescription	= DerivedCopyBEmailNotifMsg
						invoked.FileName	 	= IDMItem.IDMItemDetails.FileName
						invoked.IDMItem		 	= IDMItem			

					invoke FastUpdate in background
						run after IDMJobBView.IDMJob.AsyncId	
						if (IDMJobBView.EmailStatus.Finished)
							invoked.CopyBSentToVendor =	true

				initialize IDMItem.IDMUniqueId	
				initialize IDMItem.IDMEmail.From
				initialize IDMItem.IDMEmail.To	
				initialize IDMItem.IDMEmail.Subject 
				initialize IDMItem.IDMEmail.Body

				if (EmailCopyCToPayer
				and HasCopyC
				and PayorEmailAddress entered)
					IDMItem.IDMUniqueId		 = CopyC
					IDMItem.IDMEmail.From	 = DerivedEmailFrom
					IDMItem.IDMEmail.To		 = PayorEmailAddress
					IDMItem.IDMEmail.Subject = DerivedCopyCEmailSubject
					IDMItem.IDMEmail.Body	 = blank

					constraint (IDMItem.GetItemDetails)
						"DocumentDoesNotExistInIDM"
					
					IDMItem.IDMPID = IDMItem.IDMItemDetails.PID
					
					invoke SendToEmail IDMJob
						assign result to IDMJobCView
						invoked.LongDescription = DerivedCopyCEmailNotifMsg
						invoked.FileName	 	= IDMItem.IDMItemDetails.FileName
						invoked.IDMItem		 	= IDMItem								

					invoke FastUpdate in background
						run after IDMJobCView.IDMJob.AsyncId	
						if (IDMJobCView.EmailStatus.Finished)
							invoked.CopyCSentToPayer = true						


		PrintIDM1099FormSet is a Set Action
			default label is untranslatable
			restricted
			Parameters
        		PrmVendorGroup      	is a VendorGroup
        		PrmUS1099OutputHeader	is a US1099OutputHeader
        		PrmPrintCopyB			is Boolean
        		PrmPrintCopyC			is Boolean
				PrmCopyBPrinter			is an IDMPrinter
				PrmCopyCPrinter			is an IDMPrinter
        
        	Instance Selection		
 				where (VendorGroup 		  = PrmVendorGroup
				and    US1099OutputHeader = PrmUS1099OutputHeader
				and    RecordType.BRecord
				and    HasGeneratedCopy)
				       						
			Action Rules
				Instance Rules
					invoke PrintForm1099 in background
        				invoked.PrintCopyB   = PrmPrintCopyB
        				invoked.PrintCopyC   = PrmPrintCopyC
        				invoked.CopyBPrinter = PrmCopyBPrinter
						invoked.CopyCPrinter = PrmCopyCPrinter


		PrintForm1099 is an Instance Action
			valid when (HasGeneratedCopy)
        	Parameters
        		PrintCopyB			is Boolean
        		PrintCopyC			is Boolean
				CopyBPrinter		is an IDMPrinter
					default label is "Printer"
				CopyCPrinter		is an IDMPrinter
					default label is "Printer"
        	
        	Parameter Rules
        		PrintCopyB
        			if (not PrintCopyB
        			and not PrintCopyC)
        				required
        					"AtLeastOnePrintCopyIsNeededToProceed"      

				CopyBPrinter
					if (PrintCopyB)
						required
					else
						initialize
				
				CopyCPrinter
					if (PrintCopyC)
						required 
        			else
        				initialize	
        				
        	Action Rules
				initialize IDMItem
				IDMItem.DocumentType = "FSM_US1099Form"

				if (PrintCopyB
				and HasCopyB)
					IDMItem.IDMUniqueId	 = CopyB
					IDMItem.IDMPrinter   = CopyBPrinter
					
					constraint (IDMItem.GetItemDetails)
						"DocumentDoesNotExistInIDM"
					
					IDMItem.IDMPID       = IDMItem.IDMItemDetails.PID
					
					invoke SendToPrinter IDMJob
						invoked.Description = DerivedReportType
						invoked.FileName	= IDMItem.IDMItemDetails.FileName
						invoked.IDMItem		= IDMItem

				if (PrintCopyC
				and HasCopyC)
					IDMItem.IDMUniqueId	 = CopyC
					IDMItem.IDMPrinter   = CopyCPrinter

					constraint (IDMItem.GetItemDetails)
						"DocumentDoesNotExistInIDM"
	
					IDMItem.IDMPID       = IDMItem.IDMItemDetails.PID
					
					invoke SendToPrinter IDMJob
						invoked.Description = DerivedReportType
						invoked.FileName	= IDMItem.IDMItemDetails.FileName
						invoked.IDMItem		= IDMItem


		GenerateIDM1099FormSet is a Set Action
			default label is untranslatable		
			restricted
        	Parameters
        		PrmVendorGroup      	is a VendorGroup
        		PrmUS1099OutputHeader	is a US1099OutputHeader
				PrmGenerateCopyB		is Boolean
				PrmGenerateCopyC		is Boolean
				PrmPrintCopyB   		is Boolean
				PrmPrintCopyC   		is Boolean
				PrmCopyBPrinter			is an IDMPrinter
				PrmCopyCPrinter			is an IDMPrinter
				PrmEmailCopyBToVendors	is Boolean
				PrmEmailCopyCToPayer	is Boolean
     				
			Instance Selection
				where (VendorGroup 		  = PrmVendorGroup
				and    US1099OutputHeader = PrmUS1099OutputHeader
				and    RecordType.BRecord)

			Action Rules
				Instance Rules
					invoke GenerateForm1099 in background
						invoked.GenerateCopyB		= PrmGenerateCopyB
						invoked.GenerateCopyC		= PrmGenerateCopyC
						invoked.PrintCopyB   		= PrmPrintCopyB
						invoked.PrintCopyC          = PrmPrintCopyC 		
						invoked.CopyBPrinter		= PrmCopyBPrinter
						invoked.CopyCPrinter		= PrmCopyCPrinter
						invoked.EmailCopyBToVendors = PrmEmailCopyBToVendors
						invoked.EmailCopyCToPayer   = PrmEmailCopyCToPayer
        		
			
		GenerateForm1099 is an Instance Action
			valid when (US1099OutputHeader.IsSupportedYear)
        	Parameters
				GenerateCopyB		is Boolean
					default label is "GenerateCopyB_-_ForRecipient"
				GenerateCopyC		is Boolean
					default label is "GenerateCopyC_-_ForPayer"
				PrintCopyB   		is Boolean
				PrintCopyC   		is Boolean
				CopyBPrinter		is an IDMPrinter
					default label is "Printer"
				CopyCPrinter		is an IDMPrinter
					default label is "Printer"
				EmailCopyBToVendors	is Boolean
				EmailCopyCToPayer	is Boolean
        	
        	Parameter Rules
        		GenerateCopyB
        			if (not GenerateCopyB
        			and not GenerateCopyC)
        				required
        					"AtLeastOneGenerateCopyIsNeededToProceed"	
        	
        		PrintCopyB
        			if (not GenerateCopyB)
        				initialize
        		
        		PrintCopyC
        			if (not GenerateCopyC)
        				initialize
				
				CopyBPrinter
					if (PrintCopyB)
						required
				
				CopyCPrinter
					if (PrintCopyC)
						required
				
				EmailCopyBToVendors
        			if (not GenerateCopyB)
        				initialize
        				
				EmailCopyCToPayer					
        			if (not GenerateCopyC)
        				initialize			
        	
    		Local Fields
				LocalExecute 		is Boolean
				IDMJobBView			is an IDMJob view
				IDMJobCView			is an IDMJob view
					 			
    		Action Rules
				if (US1099OutputHeader.ReportYear not entered)
					confirmation required
						"GeneratingUS1099IDMDocumentForReportYear<DerivedCopyBYear>.Continue?"
					invoke Update US1099OutputHeader
						invoked.ReportYear = US1099OutputHeader.DerivedReportYear

    			initialize IDMGenerateDocument
    			
    			IDMGenerateDocument.IDMXMLDefinition.Busclass	     		= reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName     			= "US1099OutputDetailIDMList"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName 			= "US1099Form"
    			
    			initialize IDMAttributes
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "VendorGroup"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= US1099OutputHeader.VendorGroup
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "ReportType"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= DerivedReportType
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "Description"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue	= DerivedReportTypeDescription
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "Copy"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "PayerTaxID" 
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= FormattedPayorTaxID					
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "RecipientTaxID" 
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= MaskedVendorTaxID
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeName	= "PrintType" 
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeValue	= "Laser Copy"		
				IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeName  = "Date"
				IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeDate	= US1099OutputHeader.CreateDate date		
				IDMAttributes.SingleValue.IDMAttribute[9].IDMAttributeName  = "RecipientName"
				IDMAttributes.SingleValue.IDMAttribute[9].IDMAttributeValue = VendorName
				IDMAttributes.SingleValue.IDMAttribute[10].IDMAttributeName  = "TapeType"
				IDMAttributes.SingleValue.IDMAttribute[10].IDMAttributeValue = US1099OutputHeader.TapeSelection			
     			IDMGenerateDocument.DocumentType 							= "FSM_US1099Form"
  			    IDMGenerateDocument.IDMAccessControlList 				    = "CSFDefined"	 
  			    			    
				if (GenerateCopyB)
					if (ReportType.MerchCardAndThirdParty)
						IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= "Copy B - For Payee"
					else
						IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= "Copy B - For Recipient"
				
					IDMGenerateDocument.FileName		 = DerivedCopyBFileName
					IDMGenerateDocument.TemplateUniqueId = IDMTemplateCopyBRel.IDMUniqueId
	    			IDMGenerateDocument.IDMAttributes	 = IDMAttributes
	    							
					if (PrintCopyB)
						IDMGenerateDocument.IDMPrinter   = CopyBPrinter
					else
						initialize IDMGenerateDocument.IDMPrinter
	
					if (EmailCopyBToVendors
					and VendorEmailAddress entered)
						IDMGenerateDocument.IDMEmail.To		 = VendorEmailAddress
						IDMGenerateDocument.IDMEmail.From	 = DerivedEmailFrom
						IDMGenerateDocument.IDMEmail.Subject = DerivedCopyBEmailSubject					
						IDMGenerateDocument.IDMEmail.Body	 = blank
						    			
	    			invoke CreateFromGenerateDocument IDMJob
	    				assign result to IDMJobBView
	    				invoked.Actor = actor
		    			invoked.Description			= DerivedReportType
	    				invoked.IDMGenerateDocument	= IDMGenerateDocument
	
					IDMJobB = IDMJobBView.IDMJob
					
					invoke UpdateCopyB in background
						run after IDMJobB.AsyncId


				if (GenerateCopyC)
					if (ReportType.MerchCardAndThirdParty)
						IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= "Copy C - For Filer"
					else
					    IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= "Copy C - For Payer"			
	    			
	    			IDMGenerateDocument.FileName		 = DerivedCopyCFileName 				
					IDMGenerateDocument.TemplateUniqueId = IDMTemplateCopyCRel.IDMUniqueId
	    			IDMGenerateDocument.IDMAttributes	 = IDMAttributes
	    							
					if (PrintCopyC)
						IDMGenerateDocument.IDMPrinter   = CopyCPrinter
					else
						initialize IDMGenerateDocument.IDMPrinter
					
					initialize IDMGenerateDocument.IDMEmail.To	
					initialize IDMGenerateDocument.IDMEmail.From
					initialize IDMGenerateDocument.IDMEmail.Subject
					initialize IDMGenerateDocument.IDMEmail.Body
					
					if (EmailCopyCToPayer
					and PayorEmailAddress entered)
						IDMGenerateDocument.IDMEmail.To		 = PayorEmailAddress
						IDMGenerateDocument.IDMEmail.From	 = DerivedEmailFrom
						IDMGenerateDocument.IDMEmail.Subject = DerivedCopyCEmailSubject					
						IDMGenerateDocument.IDMEmail.Body	 = blank					
					
	    			invoke CreateFromGenerateDocument IDMJob
	    				assign result to IDMJobCView
	    				invoked.Actor = actor
		    			invoked.Description			= DerivedReportType
	    				invoked.IDMGenerateDocument	= IDMGenerateDocument
	 
	 				IDMJobC = IDMJobCView.IDMJob
	 
					invoke UpdateCopyC in background
						run after IDMJobC.AsyncId

		UpdateCopyB is an Instance Action
			default label is untranslatable	
			restricted
			Action Rules				
				if (IDMJobB.GenerationStatus.Finished
				and IDMJobB.MDSID entered)
           			CopyB = IDMJobB.MDSID


		UpdateCopyC is an Instance Action
			default label is untranslatable	
			restricted
			Action Rules
				if (IDMJobC.GenerationStatus.Finished
				and IDMJobC.MDSID entered)
           			CopyC = IDMJobC.MDSID 


		ArchiveCopyBAndCIDM1099Forms is a Set Action
			default label is untranslatable	
			restricted		
			Parameters
        		PrmVendorGroup      	is a VendorGroup
        		PrmUS1099OutputHeader	is a US1099OutputHeader			

			Local Fields
				LocalExecute			is Boolean
				AttrSearch				is an IDMAttributeOccurs
			
			Instance Selection				
 				where (VendorGroup 		  = PrmVendorGroup
				and    US1099OutputHeader = PrmUS1099OutputHeader
				and    RecordType.BRecord
				and    HasGeneratedCopy)
				
			Action Rules
				Instance Rules
					initialize AttrSearch
					AttrSearch.IDMAttribute[1].IDMAttributeName		= "VendorGroup"
					AttrSearch.IDMAttribute[1].IDMAttributeValue	= US1099OutputHeader.VendorGroup
					AttrSearch.IDMAttribute[2].IDMAttributeName		= "ReportType"
					AttrSearch.IDMAttribute[2].IDMAttributeValue	= DerivedReportType
					AttrSearch.IDMAttribute[3].IDMAttributeName		= "PayerTaxID" 
					AttrSearch.IDMAttribute[3].IDMAttributeValue	= FormattedPayorTaxID
					AttrSearch.IDMAttribute[4].IDMAttributeName		= "RecipientTaxID" 
					AttrSearch.IDMAttribute[4].IDMAttributeValue	= MaskedVendorTaxID				
					AttrSearch.IDMAttribute[5].IDMAttributeName		= "PrintType" 
					AttrSearch.IDMAttribute[5].IDMAttributeValue	= "Laser Copy"
					AttrSearch.IDMAttribute[6].IDMAttributeName     = "Date"
					AttrSearch.IDMAttribute[6].IDMAttributeDate		= US1099OutputHeader.CreateDate date
									
					IDMBatchOperation.AttributeSearch				= AttrSearch
					IDMBatchOperation.IDMDocumentType				= "FSM_US1099Form"
					LocalExecute									= IDMBatchOperation.ArchiveByAttributes
