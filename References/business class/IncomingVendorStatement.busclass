IncomingVendorStatement is a BusinessClass
    owned by ap
    prefix is IVST

    Ontology
        symbolic key is IncomingVendorStatement

    Patterns
        disable AuditIndex
        disable Auditing
        disable EffectiveDated

    Persistent Fields
        StatementDate is Date
            default label is "StatementDate"
        Vendor

    Field Rules
        StatementDate
            required
            default to current date
            initial value is current date
        Vendor
            required

    Conditions
        RecordExists
            restricted
            when (IncomingVendorStatement exists)

    Relations
        IncomingVendorStatementDetailRel is an IncomingVendorStatementDetail set

    Sets
        ByVendorDate
            Sort Order
                VendorGroup
                Vendor
                StatementDate

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action

        PurgeIncomingVendorStatements is a Set Action
			default label is "PurgeIncomingVendorStatements"
			confirmation required

			Parameters
				PrmVendorGroup	is a VendorGroup
					default label is "VendorGroup"
				PrmVendor		is a Vendor
					default label is "Vendor"
                PrmCutoffDate   is Date
                    default label is "PurgeRecordsThrough"

			Parameter Rules
				PrmVendorGroup
					required
                PrmCutoffDate
                    initial value is current date

			Instance Selection
				where (VendorGroup = PrmVendorGroup
				and    (Vendor = PrmVendor or PrmVendor not entered)
				and     StatementDate <= PrmCutoffDate)

			Action Rules
				Instance Rules
					invoke Delete
