ClosePeriodTaskJournal is a BusinessClass
	owned by closemgmt
	
	prefix is CPTJ
	
	Ontology
		symbolic key is ClosePeriodTaskJournal	

	Patterns
		implements DynamicCreation
	
	Conditions
		UnreleasedJournal
			when (GeneralLedgerJournalControlRel.Status.Unreleased)
		
		CanCloseTaskViaGLPosting
			when (ClosePeriodTask.TaskIsOpenNoPrereqsNotLate
			and   DoesNotHaveOtherOpenClosePeriodTaskJournals)
		
		HasOpenPrereqs
			when (ClosePeriodTask.CannotCompleteHasOpenPrereqs)
			
		IsClosingLateRequireReasonCode
			when (ClosePeriodTask.ClosingLateRequireReasonCode)
		
		DoesNotHaveOtherOpenClosePeriodTaskJournals
			when (!OtherOpenClosePeriodTaskJournalRel exists)
					
	Relations
		GeneralLedgerJournalControlRel
			one-to-one relation to GeneralLedgerJournalControl
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= CloseManagementGroup
				related.AccountingEntity			= ClosePeriodTaskJournal.AccountingEntity
				related.GeneralLedgerClosePeriod	= ClosePeriodTaskJournal.GeneralLedgerClosePeriod
				related.GeneralLedgerJournalControl = ClosePeriodTaskJournal.JournalEntryNumber
		
		OtherOpenClosePeriodTaskJournalRel
			one-to-many relation to ClosePeriodTaskJournal
			Field Mapping uses symbolic key
				related.CloseManagementGroup 		= CloseManagementGroup
				related.ClosePeriod					= ClosePeriod
				related.ClosePeriodTask				= ClosePeriodTask
			Instance Selection
				where (related.UniqueID != UniqueID
				and    related.ClosePeriodTaskJournal.JournalEntryNumber.Status <= 3) 
				
	Sets
		ByGeneralLedgerJournalControl
			duplicates
			Sort Order
				CloseManagementGroup
				ClosePeriodTaskJournal.AccountingEntity
				ClosePeriodTaskJournal.GeneralLedgerClosePeriod
				ClosePeriodTaskJournal.JournalEntryNumber
				
	Actions
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Delete is a Purge Action
			valid when (ClosePeriodTask.OpenPeriodOpenTask)
