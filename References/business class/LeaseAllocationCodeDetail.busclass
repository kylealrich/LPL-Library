LeaseAllocationCodeDetail is a BusinessClass
    owned by lm
    prefix is LEX
    classic name is LMEXPALLOX

    Ontology
        symbolic key is LeaseAllocationCodeDetail
            classic set name is LEXNEWPRIMARYINDEX

            classic name for LeaseAllocationCode is ALLOC-CD

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

		DistributionAccount		is a FinanceCodeBlockFull
        AllocationPercent
            classic name is ALLOC-PERCENT





    Sets








	Conditions
		IsUnreleasedLeaseAllocationCode
			restricted
			when (LeaseAllocationCode.Status.Unreleased)
		
		IsReleasedLeaseAllocationCode
			restricted
			when (LeaseAllocationCode.Status.Released)

	Relations
		DuplicateLeaseAllocationCodeDetailsRel
			one-to-many relation to LeaseAllocationCodeDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 							= FinanceEnterpriseGroup
				related.LeaseAllocationCode    							= LeaseAllocationCode
				related.Company											= Company
			Instance Selection
				where (related.LeaseAllocationCodeDetail  				!= LeaseAllocationCodeDetail
				and    related.DistributionAccount						=  DistributionAccount)
				
	Rule Blocks
		ConfirmUnreleaseHeader
			if (IsReleasedLeaseAllocationCode)
				confirmation required
					"Warning!LeaseAllocationCodeWillBeUnreleased"
				invoke Released.Update LeaseAllocationCode


    Derived Fields





	
	Field Rules
		DistributionAccount
			required
			
			constraint (DuplicateLeaseAllocationCodeDetailsRel not exists)
				"LeaseAllocationCodeDetailAlreadyExists"
					
		AllocationPercent
			required
			constraint (AllocationPercent <=100%)
				"AllocationPercentCannotExceed100%"	
			
			constraint (AllocationPercent > 0%)
				"AllocationPercentCannotBeNegative" 
			
				
	Actions
		Create is a Create Action
			
			Action Rules
				include ConfirmUnreleaseHeader

				constraint (IsUnreleasedLeaseAllocationCode)
					"CannotChangeReleasedAllocation"		
			
				invoke Unreleased.Update LeaseAllocationCode
					invoked.TotalAllocationPercentage += AllocationPercent
							
		Update is an Update Action
			
			Action Rules
				include ConfirmUnreleaseHeader

				constraint (IsUnreleasedLeaseAllocationCode) 
					"CannotChangeReleasedAllocation"		
			
				invoke Unreleased.Update LeaseAllocationCode
					invoked.TotalAllocationPercentage +=  (AllocationPercent  - old AllocationPercent)  
				
		Delete is a Delete Action
			
			Entrance Rules
				include ConfirmUnreleaseHeader
				constraint (IsUnreleasedLeaseAllocationCode)
					"CannotChangeReleasedAllocation"		
			
			Action Rules
				invoke Unreleased.Update LeaseAllocationCode
					invoked.TotalAllocationPercentage -= AllocationPercent
					
		Purge is a Purge Action
			restricted					
