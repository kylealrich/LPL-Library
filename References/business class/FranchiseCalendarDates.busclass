FranchiseCalendarDates is a BusinessClass
    owned by fr
    prefix is FRC
    classic name is FRCALENDAR

    Ontology
        symbolic key is FranchiseCalendarDates
            classic set name is FRCSET1
            classic name for FranchiseCalendar is CALENDAR-ID

    Patterns
        implements StaticJava
        disable AuditIndex
		implements Proxy for FranchiseCalendar

    Persistent Fields

        Dates is a FranchiseDates
            classic name is DATE

	Local Fields
		LocalCounter 				is Numeric  2
		LocalCompany				is a Company
		LocalFranchiseCalendar 		is a FranchiseCalendar
		LocalFranchiseCalendarYear 	is a Year
		LocalFrequency				is AlphaUpper 1
		
		
	Relations
		FranchiseContractRel is a FranchiseContract set
			Instance Selection
				where (related.FranchiseCalendar = FranchiseCalendar)
		
		LastYearCalendarDatesRel 
			one-to-one relation to FranchiseCalendarDates
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar 
				related.FranchiseCalendarDates.Year			= (FranchiseCalendarDates.Year - 1)
				related.FranchiseCalendarDates.Frequency	= FranchiseCalendarDates.Frequency
		
		NextYearCalendarDatesRel		
			one-to-one relation to FranchiseCalendarDates
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar 
				related.FranchiseCalendarDates.Year			= (FranchiseCalendarDates.Year + 1)
				related.FranchiseCalendarDates.Frequency	= FranchiseCalendarDates.Frequency				
	
		FranchiseCalendarDatesRel
			one-to-many relation to FranchiseCalendarDates
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar 
				related.FranchiseCalendarDates.Year			= FranchiseCalendarDates.Year
				related.FranchiseCalendarDates.Frequency	= FranchiseCalendarDates.Frequency
				
	
		LastYearCalendarLastPeriodDateRel
			one-to-one relation to FranchiseCalendarDates
			Field Mapping uses symbolic key	
				related.Company								= LocalCompany
				related.FranchiseCalendar					= LocalFranchiseCalendar 
				related.FranchiseCalendarDates.Year			= LocalFranchiseCalendarYear
				related.FranchiseCalendarDates.Frequency	= LocalFrequency
				
		
		
			
	Conditions	
		ShowAdditionalPeriods 
			restricted
			when (FranchiseCalendarDates.Frequency.Weekly)

		RecordExists								
			restricted
			when (FranchiseCalendarDates exists)
	
	
	Sets
		CalendarDatesByYear
            Sort Order
                Company
                FranchiseCalendar		
				FranchiseCalendarDates.Year		
				FranchiseCalendarDates.Frequency
				
		CalendarDatesByFrequency		
			Sort Order
                Company
                FranchiseCalendar		
				FranchiseCalendarDates.Frequency				
				FranchiseCalendarDates.Year	


	Derived Fields
		NumberOfCycles is a DerivedField
			type is Numeric 2
			restricted
    		LocalCounter = 1 
    		while (LocalCounter <= 53)
    			if (Dates.Date[LocalCounter] entered)
    				LocalCounter += 1
    			else
    				NumberOfCycles = LocalCounter

					
	Field Rules

			
				
	Rule Blocks
		EditDates
			constraint (Dates.Date[1] entered)
				"OneCycleDateRequired"//"OneCycleDateRequired"             
    		LocalCounter = 2 
    		while (LocalCounter <= 53)
    			if (Dates.Date[LocalCounter] entered)
    				constraint (Dates.Date[LocalCounter] > Dates.Date[LocalCounter - 1])
    					"CycleDateMustBeGreaterThanPreviousCycleDate"//"CycleDateMustBeGreaterThanPreviousCycleDate"  
    				LocalCounter += 1
				else
					LocalCounter = 54
				
    Actions
        Create is a Create Action
        	Action Rules
				include EditDates

        Update is an Update Action
        	Action Rules
				include EditDates


		Delete is a Delete Action
			Action Rules
				constraint (FranchiseContractRel not exists)
					"CannotDeleteCalendar;ContractExists"//"CannotDeleteCalendar.ContractMasterExists"        
            
            
		CreateWeeklyCalendar is a Create Action
			Parameters
				PrmFranchiseCompany 		is a BillingCompany
				PrmFranchiseCalendar		is a FranchiseCalendar				
				PrmYear						is Year
				PrmFirstWeekEndDate			is Date
				
			Parameter Rules
				PrmFranchiseCompany 
					required
				PrmFranchiseCalendar
					required
				PrmYear
					required	
				PrmFirstWeekEndDate
					required
			
			Local Fields
				LocalEndDate			is Date
				PeriodCounter			is Numeric 2						
					
			Action Rules			
				LocalCompany						= PrmFranchiseCompany
				LocalFranchiseCalendar				= PrmFranchiseCalendar
				LocalFranchiseCalendarYear			= (PrmYear - 1)
				LocalFrequency 						= "W"	
				
				constraint (PrmFirstWeekEndDate > LastYearCalendarLastPeriodDateRel.Dates.Date[53])
					"WeeklyCalendarFirstPeriodDateShouldAlwaysBeGreaterThanLastYearPeriodEndDate<LastYearCalendarLastPeriodDateRel.Dates.Date[53]>"
				
				invoke Create this instance	
					invoked.Company								= PrmFranchiseCompany
					invoked.FranchiseCalendar					= PrmFranchiseCalendar
					invoked.FranchiseCalendarDates.Year			= PrmYear
					invoked.FranchiseCalendarDates.Frequency 	= "W"					

					PeriodCounter 	= 0 
					LocalEndDate	= PrmFirstWeekEndDate
					while (PeriodCounter <= 52)								
						PeriodCounter += 1					
						invoked.FranchiseCalendarDates.Dates.Date[PeriodCounter] = (LocalEndDate + (7*(PeriodCounter-1)))
							




















































































































































