PurchaseOrderDiversity is a BusinessClass
	owned by po
	
	prefix is Pur
	
	Ontology
		symbolic key is PurchaseOrderDiversity
		
	Patterns
	
	Persistent Fields
		Vendor
		PurchaseOrderDate          is a snapshot of PurchaseOrder.PurchaseOrderDate
	
	Local Fields
		LocalPurchaseFromLocation 	is like VendorLocation

	Derived Fields
		TotalOrderAmount is a DerivedField
			type is like InternationalAmount
			return (PurchaseOrder.TotalOrderAmount)
		
		DerivedPurchaseFromLocation is a DerivedField
			type is Alpha size 27
			if (LocalPurchaseFromLocation entered)
				return ("FromLocation" + LocalPurchaseFromLocation)
	
	Context Fields
		PurchaseOrderDateRange							is a DateRange
	
	Conditions
		MultipleDiversityCodes
			when (OtherPurchaseOrderDiversityRel exists)
			
		PurchaseOrderDateWithinRange
    		restricted
    		when (PurchaseOrderDate within PurchaseOrderDateRange)
	
	Relations
		
		VendorGroupRel
			one-to-one relation to VendorGroup
			Field Mapping uses symbolic key
				related.VendorGroup     = Company.VendorGroup
		
		VendorDiversityRel
			one-to-many relation to VendorDiversity
			Field Mapping uses ByDateDescending
				related.VendorGroup    = Company.VendorGroup
				related.Vendor 		   = Vendor
				
		OtherPurchaseOrderDiversityRel
			one-to-many relation to PurchaseOrderDiversity
			Field Mapping uses symbolic key
				related.Company        = Company
				related.PurchaseOrder  = PurchaseOrder
			Instance Selection
				where (related.PayablesDiversityCode != PayablesDiversityCode)
				
		ValidVendorDiversityRel
			one-to-many relation to VendorDiversity
			Field Mapping uses symbolic key
				related.VendorGroup    			= Company.VendorGroup
				related.Vendor 		   			= Vendor
				related.VendorLocation 			= blank
				related.DiversityCode		 	= PayablesDiversityCode
			Instance Selection
				where (((related.DiversityEffectiveDate  <= PurchaseOrderDate
				and    related.DiversityExpirationDate >= PurchaseOrderDate)
				or     VendorGroupRel.DiversityValidWhenWithinDates = false)
				and    related.CanUseForDocument = true)

		ValidVendorAndLocationDiversityRel
			one-to-many relation to VendorDiversity
			Field Mapping uses symbolic key
				related.VendorGroup    			= Company.VendorGroup
				related.Vendor 		   			= Vendor
				related.VendorLocation 			= LocalPurchaseFromLocation
				related.DiversityCode		 	= PayablesDiversityCode
			Instance Selection
				where (((related.DiversityEffectiveDate  <= PurchaseOrderDate
				and    related.DiversityExpirationDate >= PurchaseOrderDate)
				or     VendorGroupRel.DiversityValidWhenWithinDates = false)
				and    related.CanUseForDocument = true)

	Sets
		
		ByDiversityCodeVendor
			allow duplicates
			Sort Order
				PayablesDiversityCode
				Vendor
				
		ByVendorDiversityCode
			allow duplicates
			Sort Order
				Vendor
				PayablesDiversityCode
	
	Field Rules
	Actions
		Create is a Create Action
			restricted
			Action Rules
				Vendor 				= PurchaseOrder.Vendor

				constraint (ValidVendorAndLocationDiversityRel exists)
					"DiversityCode<PayablesDiversityCode>NotValidForVendor<Vendor><DerivedPurchaseFromLocation>"


		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted
