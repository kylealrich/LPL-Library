ContractLineTierMemberQual is a BusinessClass
	owned by po
	prefix is CTLN

	Ontology
		symbolic key is ContractLineTierMemberQual

	Persistent Fields
		Tier						is a ContractTier
		PurchaseType				is Numeric size 1
			States
				TotalPurchases		value is 1
				TargetedPurchases	value is 2
		PurchaseBy 					is Numeric size 1
			States
				PurchasePrice	value is 1
				InvoicePrice	value is 2
		PurchaseOperator			is AlphaUpper size 2
			States
				LessThanOrEqualTo		value is "LE"
				GreaterThanOrEqualTo	value is "GE"
				EqualTo					value is "EQ"
				LessThan				value is "LT"
				GreaterThan				value is "GT"
		PurchaseAmtPct
		OrGroup						is AlphaUpper size 1
		PurchaseQuantity			is an UnsignedItemQuantity
		PurchaseUomQualifier		is a UnitOfMeasure
		PurchaseBeginRange			is Numeric size 8
		PurchaseEndRange			is Numeric size 8
		TargetedPurchasesFromDate	is Date
		TargetedPurchasesToDate		is Date
		Modified					is Boolean
		ItemCodesUsed				is Boolean
		ContractTierQualifier

	Transient Fields
		PurchasePercentOrPurchaseAmount is Numeric 1
			default label is "PurchasePercent_orPurchaseAmount"
			derive value from DerivePurchasePercentOrPurchaseAmount
			States
				PurchasePercent	value is 0
				PurchaseAmount	value is 1

	Derived Fields
		TotalItemSpend is a DerivedField
			type is like InternationalAmount
			restricted
			return (sum PolineFactsAllMemberRel.ExtendedAmt)

		ConvertedPercent is a ComputeField
			type is Decimal size 4.2
			restricted
			(PurchaseAmtPct.PurchasePercent * 100)

		ConvertedAmount is a DerivedField
			type is like InternationalCost
			restricted
			return (PurchaseAmtPct.PurchaseAmount * 1)

		DerivedPercent is a StringField
			type is Alpha 8
			restricted
			ConvertedPercent
			"%"

		FormattedPercentAmount is a ConditionalField
			type is Alpha 30
			if (PurchaseAmtPct.PurchaseAmount > 0)
				ConvertedAmount
			else
			if (PurchaseAmtPct.PurchasePercent > 0)
				DerivedPercent
			else
				blank

		DerivedNextDisplayOrder is a ComputeField
			type is Numeric 8
			restricted
			(Tier.DisplayOrder + 1)

		DerivedPurchasePercent is a DerivedField
			type is Percent 4.1
			restricted
			if (CompanyOnlyMember)
				if (NextTierItemQualifierByAmount)
					return ((sum PolineFactsCompanyItemForContractRel.ExtendedAmt) / first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount)
				else
				if (NextTierCodesQualifierByAmount)
					return ((sum CompanyContractTierMemberQualifierPurchaseOrderLinesRel.ExtendedAmt) / first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount)
				else
					return 0%
			else
			if (CompanyLocationMember)
				if (NextTierItemQualifierByAmount)
					return ((sum PolineFactsCompanyLocationItemForContractRel.ExtendedAmt) / first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount)
				else
				if (NextTierCodesQualifierByAmount)
					return ((sum LocationContractTierMemberQualifierPurchaseOrderLinesRel.ExtendedAmt) / first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount)
				else
					return 0%
			else
			if (CompanyRqLocMember)
				if (NextTierItemQualifierByAmount)
					return ((sum PolineFactsCompanyRqLocItemForContractRel.ExtendedAmt) / first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount)
				else
				if (NextTierCodesQualifierByAmount)
					return ((sum RequestingLocationContractTierMemberQualifierPurchaseOrderLinesRel.ExtendedAmt) / first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount)
				else
					return 0%

		DerivedOnContractPurchases is a DerivedField
			type is like InternationalAmount
			restricted
			if (CompanyOnlyMember)
				return (sum PolineFactsCompanyItemForContractRel.ExtendedAmt)
			else
			if (CompanyLocationMember)
				return (sum PolineFactsCompanyLocationItemForContractRel.ExtendedAmt)
			else
			if (CompanyRqLocMember)
				return (sum PolineFactsCompanyRqLocItemForContractRel.ExtendedAmt)

		DerivedAllPurchases is a DerivedField
			type is like InternationalAmount
			restricted
			if (CompanyOnlyMember)
				return (sum PolineFactsCompanyItemAllRel.ExtendedAmt)
			else
			if (CompanyLocationMember)
				return (sum PolineFactsCompanyLocationItemAllRel.ExtendedAmt)
			else
			if (CompanyRqLocMember)
				return (sum PolineFactsCompanyRqLocItemAllRel.ExtendedAmt)

		PercentOfOnContractPurchases is a ComputeField
			type is Percent 4.1
			restricted
			(DerivedOnContractPurchases / DerivedAllPurchases)

		DerivedPercentOfOnContractPurchasesString is a ConditionalField
			type is Alpha 9
			restricted
			if (PercentOfOnContractPurchases > 0)
				PercentOfOnContractPurchases + " "
			else
				"0%"

		DerivedOnContractPurchasesString is a ConditionalField
			type is Alpha 25
			restricted
			if (DerivedOnContractPurchases > 0)
				DerivedOnContractPurchases
			else
				"0.00"

		DerivedTotalAllPurchasesString is a ConditionalField
			type is Alpha 25
			restricted
			if (DerivedAllPurchases > 0)
				DerivedAllPurchases
			else
				"0.00"

		AllPurchasesMessage is a MessageField
			restricted
			"_ofAllPurchasesForThisTierMember"

		TierMarkUpDiscountValue is a DerivedField
			type is Alpha 25
			restricted
			if (Tier.TierMarkupDiscount.Markup or Tier.TierMarkupDiscount.Discount)
				return "<Tier.TierPricingAmtPct.TierPercent>"
			else
				return "<Tier.TierPricingAmtPct.TierCost>"

		MarkUpDiscount is a DerivedField
			type is Alpha 25
			if (Tier.TierMarkupDiscount.Markup)
				return "MarkUpOf_<TierMarkUpDiscountValue>"
			if (Tier.TierMarkupDiscount.Discount)
				return "DiscountOf_<TierMarkUpDiscountValue>"
			else
				return "FixedAmountOf_<TierMarkUpDiscountValue>"

		PurchaseOperatorLabel is a DerivedField
			type is Alpha 2
			restricted
			if (PurchaseOperator.LessThanOrEqualTo)
				return "<="
			if (PurchaseOperator.GreaterThanOrEqualTo)
				return ">="
			if (PurchaseOperator.EqualTo)
				return "="
			if (PurchaseOperator.LessThan)
				return "<"
			if (PurchaseOperator.GreaterThan)
				return ">"

		PurchaseTypeLabelPrefix is a DerivedField
			type is Alpha 25
			restricted
			if (ContractTierQualifier.PurchaseType.TotalPurchases)
				return "TotalPurchasesBy"
			else
				return "TargetedPurchasesBy"

		PurchaseTypeLabel is a DerivedField
			type is Alpha 25
			return "<PurchaseTypeLabelPrefix>_<ContractTierQualifier.PurchaseType>_<PurchaseOperatorLabel>_<ContractTierQualifier.FormattedPercentAmount>"

		HasNextTierProgress is a DerivedField
			type is Alpha 25
			if (ContractTierQualifier.HasNextHigherTier)
				if (ContractTierQualifier.HasNextTierProgress)
					return "NextTierAvailableWithProgress"
				else
					return "NextTierAvailable"
			else
				return "LastAvailableTier"

		LineMembersAlertText is a DerivedField
			type is Alpha 25
			if (ContractTierQualifier.LineQualifiersExist)
				return "LineQualifiersDefined"
			else
				return "_"

		DerivePurchasePercentOrPurchaseAmount is a DerivedField
			type is Numeric 1
			if (PurchaseAmtPct.PurchaseAmount entered)
				return 1
			else
				return 0

	Conditions
		EmptyQualifier
			restricted
			when (PurchaseType = 0)
		MissingAmtPct
			restricted
			when (PurchaseAmtPct.PurchaseAmount !entered
			and   PurchaseAmtPct.PurchasePercent !entered)
		RequirePurchaseUom
			restricted
			when (PurchaseBeginRange > 0
			or    PurchaseEndRange > 0
			or    PurchaseQuantity > 0)
		Defaulted
			restricted
			when (!Modified)
		RecordModified
			restricted
			when (Modified)
		CompanyOnlyMember
			restricted
			when (ContractLineMember.Company	entered
			and   ContractLineMember.Location	not entered
			and   ContractLineMember.RequestingLocation		not entered)
		CompanyLocationMember
			restricted
			when (ContractLineMember.Company	entered
			and   ContractLineMember.Location	entered
			and   ContractLineMember.RequestingLocation		not entered)
		CompanyRqLocMember
			restricted
			when (ContractLineMember.Company	entered
			and   ContractLineMember.Location	not entered
			and   ContractLineMember.RequestingLocation		entered)
		ContractItemValid
			restricted
			when (ContractLine.PurchClassMajorMinor				    = any TierQualifierCodesRel.PurchClassMajorMinor
			or    ContractLine.Manufacturer 						= any TierQualifierCodesRel.ManufacturerInfo
			or    ContractLine.UNSPSCCode						    = any TierQualifierCodesRel.UnspscCode
			or    ContractLine.CommodityCode					    = any TierQualifierCodesRel.CommodityCode
			or    !TierQualifierCodesRel exists)

		HasNextHigherTier
			restricted
			when (NextHigherTierRel exists)

		PercentTier
			restricted
			when (NextHigherTierRel exists
			and   NextHigherTierRel.TierPricingAmtPct.TierPercent entered)

		CostTier
			restricted
			when (NextHigherTierRel exists
			and   NextHigherTierRel.TierPricingAmtPct.TierCost entered)

		NextTierItemQualifierByAmount
			restricted
			when (HasNextHigherTier
			and   first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount entered
			and  !HasQualifierCodes)

		NextTierCodesQualifierByAmount
			restricted
			when (HasNextHigherTier
			and   first NextTierQualifiersRel.PurchaseAmtPct.PurchaseAmount entered
			and   HasQualifierCodes)

		HasNextTierPercent
			restricted
			when (NextTierItemQualifierByAmount
			or    NextTierCodesQualifierByAmount)

		PercentQualifier
			restricted
			when (PurchaseAmtPct.PurchasePercent	entered)

		HasQualifierCodes
			restricted
			when (ContractTierQualifier.ContractTierQualifierCode set exists)

		ItemAndCompany
			restricted
			when (CompanyOnlyMember
			and   !HasQualifierCodes)

		ItemAndLocation
			restricted
			when (CompanyLocationMember
			and   !HasQualifierCodes)

		ItemAndRequestingLocation
			restricted
			when (CompanyRqLocMember
			and   !HasQualifierCodes)

		CodesAndCompany
			restricted
			when (CompanyOnlyMember
			and   HasQualifierCodes)

		CodesAndLocation
			restricted
			when (CompanyLocationMember
			and   HasQualifierCodes)

		CodesAndRequestingLocation
			restricted
			when (CompanyRqLocMember
			and   HasQualifierCodes)

		NextTierHasBeenReached
			restricted
			when (DerivedPurchasePercent	>= 100%
			and   HasNextHigherTier)

	Sets
		ByTierQual
			duplicates
			Sort Order
				ContractGroup
				Contract
				Tier
				ContractTierQualifier

	Relations
		ContractTierRel
			one-to-one relation to ContractTier
			Field Mapping uses symbolic key
				related.ContractGroup	= ContractGroup
				related.Contract		= Contract
				related.ContractTier	= Tier

		TierQualifierRel
			one-to-many relation to ContractTierQualifier
			Field Mapping uses symbolic key
				related.ContractGroup	= ContractGroup
				related.Contract		= Contract
				related.ContractTier	= Tier

		ContractLineMemberRel
			one-to-one relation to ContractLineMember
			Field Mapping uses symbolic key
				related.ContractGroup				   = ContractGroup
				related.Contract					   = Contract
				related.ContractLine				   = ContractLine
				related.ContractLineMember.Company   = ContractLineMember.Company
				related.ContractLineMember.Location  = ContractLineMember.Location
				related.ContractLineMember.RequestingLocation	   = ContractLineMember.RequestingLocation
				related.ContractLineMember.PricingGroup = ContractLineMember.PricingGroup
				related.ContractLineMember.ManufacturerContract = Contract

		PolineFactsCompanyItemAllRel
			one-to-many relation to PolineFact
			Field Mapping uses ByCompanyItem
				related.PurchasingCompany = ContractLineMemberRel.ContractLineMember.Company
			Instance Selection
				where (related.PoDate	>= Contract.EffectiveDate
				and    related.Item		= ContractLineMemberRel.ItemNumber)

		PolineFactsCompanyLocationItemAllRel
			one-to-many relation to PolineFact
			Field Mapping uses ByCompLocItem
				related.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.Location			= ContractLineMemberRel.ContractLineMember.Location
			Instance Selection
				where (related.PoDate	>= Contract.EffectiveDate
				and    related.Item		= ContractLineMemberRel.ItemNumber)

		PolineFactsCompanyRqLocItemAllRel
			one-to-many relation to PolineFact
			Field Mapping uses ByCmpRqLcItem
				related.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.RequestingLocation	= ContractLineMemberRel.ContractLineMember.RequestingLocation
			Instance Selection
				where (related.PoDate	>= Contract.EffectiveDate
				and    related.Item		= ContractLineMemberRel.ItemNumber)

		PolineFactsCompanyItemForContractRel
			one-to-many relation to PolineFact
			Field Mapping uses ByCompanyItem
				related.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.CmContract			= Contract
				related.Item				= ContractLineMemberRel.ItemNumber
			Instance Selection
				where (related.PoDate >= Contract.EffectiveDate)

		PolineFactsCompanyLocationItemForContractRel
			one-to-many relation to PolineFact
			Field Mapping uses ByCompLocItem
				related.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.Location			= ContractLineMemberRel.ContractLineMember.Location
				related.CmContract			= Contract
				related.Item				= ContractLineMemberRel.ItemNumber
			Instance Selection
				where (related.PoDate >= Contract.EffectiveDate)

		PolineFactsCompanyRqLocItemForContractRel
			one-to-many relation to PolineFact
			Field Mapping uses ByCmpRqLcItem
				related.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.RequestingLocation	= ContractLineMemberRel.ContractLineMember.RequestingLocation
				related.CmContract			= Contract
				related.Item				= ContractLineMemberRel.ItemNumber
			Instance Selection
				where (related.PoDate >= Contract.EffectiveDate)

		TierQualifierCodesRel
			one-to-many relation to ContractTierQualifierCode
			Field Mapping uses symbolic key
				related.ContractGroup			= ContractGroup
				related.Contract				= Contract
				related.ContractTier			= Tier
				related.ContractTierQualifier	= ContractTierQualifier

		PolineFactsAllMemberRel
			if (CompanyOnlyMember)
				PolineFactsCompanyItemAllRel
			else
			if (CompanyLocationMember)
				PolineFactsCompanyLocationItemAllRel
			else
				PolineFactsCompanyRqLocItemAllRel

		NextHigherTierRel
			one-to-many relation to ContractTier
			Field Mapping uses ByDisplayOrder
				related.ContractGroup	= ContractGroup
				related.Contract		= Contract
			Instance Selection
				where (related.DisplayOrder = DerivedNextDisplayOrder)

		NextTierQualifiersRel
			one-to-many relation to ContractTierQualifier
			Field Mapping uses symbolic key
				related.ContractGroup	= ContractGroup
				related.Contract		= Contract
				related.ContractTier	= first NextHigherTierRel.ContractTier

		CompanyContractTierMemberQualifierPurchaseOrderLinesRel
			one-to-many relation to ContractTierMemberQualifierPurchaseOrderLine
			Field Mapping uses ByCompanyLocation
				related.ContractGroup													= ContractGroup
				related.Contract														= Contract
				related.ContractTierMember.Company										= ContractLineMember.Company
				related.ContractTierMember.Location										= blank
				related.ContractTierMember.RequestingLocation							= blank
				related.ContractTierMember.PricingGroup									= blank
				related.ContractTierMemberQualifierPurchaseOrderLine.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company

		LocationContractTierMemberQualifierPurchaseOrderLinesRel
			one-to-many relation to ContractTierMemberQualifierPurchaseOrderLine
			Field Mapping uses ByCompanyLocation
				related.ContractGroup													= ContractGroup
				related.Contract														= Contract
				related.ContractTierMember.Company										= ContractLineMember.Company
				related.ContractTierMember.Location										= ContractLineMember.Location
				related.ContractTierMember.RequestingLocation							= blank
				related.ContractTierMember.PricingGroup									= blank
				related.ContractTierMemberQualifierPurchaseOrderLine.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.Location														= ContractLineMemberRel.ContractLineMember.Location

		RequestingLocationContractTierMemberQualifierPurchaseOrderLinesRel
			one-to-many relation to ContractTierMemberQualifierPurchaseOrderLine
			Field Mapping uses ByCompanyReqLoc
				related.ContractGroup													= ContractGroup
				related.Contract														= Contract
				related.ContractTierMember.Company										= ContractLineMember.Company
				related.ContractTierMember.Location										= blank
				related.ContractTierMember.RequestingLocation							= ContractLineMember.RequestingLocation
				related.ContractTierMember.PricingGroup									= blank
				related.ContractTierMemberQualifierPurchaseOrderLine.PurchasingCompany	= ContractLineMemberRel.ContractLineMember.Company
				related.RequestingLocation												= ContractLineMemberRel.ContractLineMember.RequestingLocation

	Field Rules
		Tier
			required

		PurchaseType
			required
				"PurchaseTypeIsRequired"

		PurchaseBy
			required
				"PurchaseByIsRequired"
			if (PurchaseType.TotalPurchases
			and (ContractLine.ServiceItem))
				constraint (PurchaseBy.InvoicePrice)
					"InvoicePriceOnlyForServiceTypeItems"

		PurchaseOperator
			required
				"PurchaseOperatorIsRequired"

		PurchaseEndRange
			constraint (PurchaseEndRange > PurchaseBeginRange)
				"TotalPurchaseEndRangeCannotBeLessThanBeginRange"

		PurchaseUomQualifier
			if (RequirePurchaseUom)
				required
					"PurchaseUomQualifierIsRequiredWhenQuantityOrBeginOrEndRangeIsEntered"
			else
				cannot be entered
					"PurchaseUomQualifierCannotBeEntered"

		TargetedPurchasesFromDate
			if (PurchaseType.TargetedPurchases)
				required
					"TargetedPurchasesFromDateIsRequiredWhenTargetedPurchasesIsSelected"
			else
				cannot be entered
					"TargetedPurchasesFromDateCannotBeEnteredUnlessTargetedPurchasesIsSelected"
			if (Contract.EffectiveDate > 0)
			   	constraint (TargetedPurchasesFromDate >= Contract.EffectiveDate)
					"TargetedPurchasesFromDateCannotBeLessThanContractEffectiveDate"
			if (Contract.ExpirationDate > 0)
				constraint (TargetedPurchasesFromDate <= Contract.ExpirationDate)
					"TargetedPurchasesFromDateCannotBeGreaterThanContractExpirationDate"

		TargetedPurchasesToDate
			if (PurchaseType.TargetedPurchases)
				required
					"TargetedPurchasesToDateIsRequiredWhenTargetedPurchasesIsSelected"
			else
				cannot be entered
					"TargetedPurchasesToDateCannotBeEnteredUnlessTargetedPurchasesIsSelected"
			constraint (TargetedPurchasesToDate > TargetedPurchasesFromDate)
				"TargetedPurchasesToDateCannotBeLessThanFromDate"
			if (Contract.ExpirationDate > 0)
				constraint (TargetedPurchasesToDate <= Contract.ExpirationDate)
					"TargetedPurchasesToDateCannotBeGreaterThanContractExpirationDate"

		Modified
			if (Modified !entered)
				default to true

	Actions
		Create is a Create Action
			valid when (Contract.InProgress)
			Action Rules
				constraint (Contract.InProgress)
					"ContractStatusCannotBeActive,InactiveOrClosedToAddContractLineTierMemberQualifier"
				constraint (!EmptyQualifier)
					"PurchaseTypeIsRequired"
				constraint (!MissingAmtPct)
					"PurchasePercentAnd/OrAmountIsRequired"
	 			if (Tier entered)
					constraint (ContractTierRel exists)
						"ContractTierDoesNotExist"
					constraint (TierQualifierRel exists)
						"ContractTierInvalid;TierQualifiersDoNotExist"
				if (ContractLineMemberRel.Modified = false)
					invoke UpdateContractLineMember ContractLineMemberRel
						invoked.Modified = true

		Update is an Update Action
			valid when (Contract.InProgress)
			Action Rules
				constraint (Contract.InProgress)
					"ContractStatusCannotBeActive,InactiveOrClosedToUpdateContractLineTierMemberQualifier"
				constraint (!EmptyQualifier)
					"PurchaseTypeIsRequired"
				constraint (!MissingAmtPct)
					"PurchasePercentAnd/OrAmountIsRequired"
				if (Tier changed)
					constraint (ContractTierRel exists)
						"ContractTierDoesNotExist"
					constraint (TierQualifierRel exists)
						"ContractTierInvalid;TierQualifiersDoNotExist"
				Modified = true
				if (ContractLineMemberRel.Modified = false)
					invoke UpdateContractLineMember ContractLineMemberRel
						invoked.Modified = true

		Delete is a Delete Action
			valid when (Contract.InProgress)
			Action Rules
				constraint (Contract.InProgress)
					"ContractStatusCannotBeActive,InactiveOrClosedToDeleteContractLineTierMemberQualifier"
				if (ContractLineMemberRel.Modified = false)
					invoke UpdateContractLineMember ContractLineMemberRel
						invoked.Modified = true

		CalculateQualifierUsagePercentagesForOneContract is an Instance Action
 			Parameters
				PrmContractGroup	is a ContractGroup
				PrmContract			is a Contract
			Parameter Rules
				PrmContractGroup
					initial value is ContractGroup
					default to ContractGroup
				PrmContract
					initial value is Contract
					default to Contract
			Action Rules
				invoke CalculateQualifierUsagePercentagesForOneContract PrmContract
					invoked.PrmContractGroup	= PrmContractGroup
					invoked.PrmContract			= PrmContract
