BudgetGroupAmount is a BusinessClass
	owned by GeneralLedger
	prefix is BUDGA   
   
    Ontology
    	symbolic key is BudgetGroupAmount

    Patterns
    	implements DynamicCreation
 		disable Auditing 
 		disable EffectiveDated

    Persistent Fields
        Amount                  is a TotalAmount

    Transient Fields
		
	Local Fields
		LocalYearStartJulian				is Alpha size 7
		
	Derived Fields
		CurrencyAmount is a DerivedField
			type is like TotalAmount
				precision is BudgetGroup.CurrencyDecimals
			return Amount
		YearToDate is a MessageField
			"YTD"
		LifeToDate is a MessageField
			"LTD"
		YTD is a DerivedField
			type is like TotalAmount
				precision is BudgetGroup.CurrencyDecimals
			LocalYearStartJulian	= first BudgetCalendarYearsContainingLocalJulianDateRel.GeneralLedgerCalendarPeriod.StartDateJulian
			return sum YearToDateBudgetAmountsRel.Amount
		LTD is a DerivedField
			type is like TotalAmount
				precision is BudgetGroup.CurrencyDecimals
			return sum LifeToDateBudgetAmountsRel.Amount

	Conditions
		SecurityGroupAllowsAccess
            when (BudgetGroup.AccountingEntity not entered
            or   (BudgetGroup.AccountingEntity entered 
            and   BudgetGroup.AccountingEntity.AccountingEntitySecurityGroupAllowsAccess))

	Relations
		BudgetCalendarYearsContainingLocalJulianDateRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.TopNode		 				= Scenario.Basis.GeneralLedgerCalendar.TopNode
				related.PeriodType					= 1 
				related.EndDateJulian				>= GeneralLedgerCalendarPeriod.EndDateJulian
			Instance Selection
				where (related.StartDateJulian		<= GeneralLedgerCalendarPeriod.EndDateJulian)
		YearToDateBudgetAmountsRel
			one-to-many relation to BudgetGroupAmount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.Scenario	 				= Scenario
				related.BudgetTemplate 				= BudgetTemplate
				related.BudgetGroup 				= BudgetGroup
				related.GeneralLedgerCalendarPeriod	= YearToDatePeriodsRel.GeneralLedgerCalendarPeriod
		YearToDatePeriodsRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.TopNode		 				= Scenario.Basis.GeneralLedgerCalendar.TopNode
				related.PeriodType		 			= BudgetTemplate.BudgetPeriodType
				related.EndDateJulian 				<= GeneralLedgerCalendarPeriod.EndDateJulian
			Instance Selection
				where (related.StartDateJulian 		>= LocalYearStartJulian)
		LifeToDateBudgetAmountsRel
			one-to-many relation to BudgetGroupAmount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.Scenario	 				= Scenario
				related.BudgetTemplate 				= BudgetTemplate
				related.BudgetGroup 				= BudgetGroup
				related.GeneralLedgerCalendarPeriod	= LifeToDatePeriodsRel.GeneralLedgerCalendarPeriod
		LifeToDatePeriodsRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.TopNode		 				= Scenario.Basis.GeneralLedgerCalendar.TopNode
				related.PeriodType		 			= BudgetTemplate.BudgetPeriodType
				related.EndDateJulian 				<= GeneralLedgerCalendarPeriod.EndDateJulian
        
	Sets


	Field Rules
					
	Attach Rules
		
	Rule Blocks

    Actions
	
    	Create is a Create Action
    		restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		DeleteScenarioAmounts is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmScenario						is a Scenario
					default label is "Scenario"
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and Scenario					= PrmScenario)
			Action Rules
				Instance Rules
					invoke Delete BudgetGroupAmount

		DeleteTemplateAmounts is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmScenario						is a Scenario
					default label is "Scenario"
				PrmBudgetTemplate				is a BudgetTemplate
					default label is "BudgetTemplate"
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and Scenario					= PrmScenario
				and BudgetTemplate				= PrmBudgetTemplate)
			Action Rules
				Instance Rules
					invoke Delete

		PurgeTemplateAmounts is a Set Action
			restricted
			default label is untranslatable
			disable checkpoint
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmScenario						is a Scenario
					default label is "Scenario"
				PrmBudgetTemplate				is a BudgetTemplate
					default label is "BudgetTemplate"
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and    Scenario					= PrmScenario
				and    BudgetTemplate			= PrmBudgetTemplate)
			Action Rules
				Instance Rules
					invoke Purge BudgetGroupAmount
		
		Purge is a Purge Action
			restricted
			bypass relational integrity rules
