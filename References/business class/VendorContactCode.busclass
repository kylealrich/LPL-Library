VendorContactCode is a BusinessClass
    owned by ap
    prefix is VRC
    classic name is APRESPCODE

    Ontology
        symbolic key is VendorContactCode
            classic set name is VRCSET1
            classic name is RESP-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Description

	Derived Fields
		PrimaryContactMessage			is a MessageField
			restricted
			"PrimaryContact"

    Field Rules
        Description
            required

    Relations
        PrimeVendorContactCodesRel
            one-to-one relation to VendorContactCode
            Field Mapping uses symbolic key
                related.VendorGroup       = VendorGroup
                related.VendorContactCode = "PRIME"

	Actions
		Create is a Create Action
            restricted

        CreateNewContactCode is a Create Action
            Parameters
                VendorGroup
                IsPrimaryContact is Boolean
                VendorContactCode
                Description
            Action Rules
                invoke Create this instance
                    invoked.VendorGroup = VendorGroup
                    if (IsPrimaryContact)
                        constraint (PrimeVendorContactCodesRel not exists)
                            "CannotCreatePrimeContactCode.AlreadyExistsFor_\Vendor_\Group<VendorGroup>."
                        invoked.VendorContactCode = "PRIME"
                        invoked.Description       = "Primary contact"
                    else
                        invoked.VendorContactCode = VendorContactCode
                        invoked.Description = Description
		
		CreatePrimeContactCode is a Create Action
			restricted
			Action Rules
				VendorContactCode = "PRIME"
				Description 	  = PrimaryContactMessage

		Update is an Update Action

		Delete is a Delete Action
            Action Rules
                if (VendorContactCode = "PRIME")
                    confirmation required
                        "DeletingThe<PrimaryContactMessage>PreventsTheCreationOfVendorsForThe_Vendor_Group.Continue?"
