TierQualifierCalculation is a BusinessClass
    owned by po
    prefix is TQCX
    
    Ontology
  		symbolic key is TierQualifierCalculation

	Patterns
		
    Persistent Fields
		FromInvoicesOrPurchaseOrders		is Numeric 1
			States
				Invoices                    value is 1
				PurchaseOrders 				value is 2
		Company                         
		Location                        	is like InventoryLocation  
		ContractTier                     
		ContractTierQualifier           
		OnlyForTier                         is Boolean 
			default label is "ProcessOnlyQualifiedItemsForThisTier"
		RunDate                         	is Date 
		DateRange                       	 
		QualifiedAmount                     is an InternationalAmount
		CompetitiveAmount                   is an InternationalAmount
		QualifiedQuantity                   is an UnsignedQuantity
			precision is 0
		CompetitiveQuantity                 is an UnsignedQuantity
			precision is 0
		OffContractAmount                   is an InternationalAmount
			default label is "AmountNotOnContract"

    Local Fields
		
	Transient Fields 
	
	Derived Fields

		QualifiedPercentByAmount is a ComputeField 
			type is Percent size 6.3	
			(QualifiedAmount/(QualifiedAmount + CompetitiveAmount))		

		QualifiedPercentByQuantity is a ComputeField 
			type is Percent size 6.3
			(QualifiedQuantity/(QualifiedQuantity + CompetitiveQuantity))

		QualifiedAmountToQualifierAmountPercent	is a ComputeField 
			type is Percent size 6.3
			(QualifiedAmount/ContractTierQualifierRel.ConvertedAmount)		

	Conditions 

		YellowAlerts 
			when (WithinTenPercentOfQualifierAmount
			or    WithinTenPercentOfQualifierPercent)
		
		GreaterThanOrEqualToQualifierAmount
			restricted
			when (QualifiedAmount >= ContractTierQualifierRel.ConvertedAmount
			and   ContractTierQualifierRel.ConvertedAmount > 0)

		WithinTenPercentOfQualifierAmount
			restricted
			when (QualifiedAmount < ContractTierQualifierRel.ConvertedAmount
			and   QualifiedAmountToQualifierAmountPercent >= 90%
			and   ContractTierQualifierRel.ConvertedAmount > 0)

		NotWithinTenPercentOfQualifierAmount							
			restricted	
			when (QualifiedAmount < ContractTierQualifierRel.ConvertedAmount
			and   QualifiedAmountToQualifierAmountPercent < 90%
			and   ContractTierQualifierRel.ConvertedAmount > 0)

		GreaterThanOrEqualToQualifierPercent
			restricted	
			when (QualifiedPercentByAmount >= ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent
			and   ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent > 0)

		WithinTenPercentOfQualifierPercent
			restricted
			when (QualifiedPercentByAmount < ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent
			and   ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent > 0
			and  (ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent - QualifiedPercentByAmount) <= 10%)	

		NotWithinTenPercentOfQualifierPercent				
			restricted
			when (QualifiedPercentByAmount < ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent
			and   ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent > 0
			and  (ContractTierQualifierRel.PurchaseAmtPct.PurchasePercent - QualifiedPercentByAmount) > 10%)

		HasQualifier 
			restricted 
			when (ContractTierQualifier entered)

		OpenHasQualifier 
			restricted 
			when (!Contract.ContractStatus.Closed
			and   HasQualifier)
		
		HasQuantity 
			restricted 
			when (QualifiedQuantity entered)

		FromPOs 
			restricted 
			when (FromInvoicesOrPurchaseOrders = 2)

		FromPOsAndCompetitive
			restricted 
			when (FromPOs
			and   Contract.HasPercentTierQualifier)
		
		FromInvoices 
			restricted 
			when (FromInvoicesOrPurchaseOrders = 1)

		FromInvoicesAndCompetitive 
			restricted 
			when (FromInvoices
			and   Contract.HasPercentTierQualifier)

		HasOffContractTransactions 
			restricted 
			when (OffContractAmount > 0)

	Relations

		ContractTierQualifierRel 
			one-to-one relation to ContractTierQualifier 
			Field Mapping uses symbolic key 
				related.ContractGroup			= ContractGroup 
				related.Contract            	= Contract 
				related.ContractTier        	= ContractTier 
				related.ContractTierQualifier	= ContractTierQualifier

		TierQualifierCalculationDetailRel 
			one-to-many relation to TierQualifierCalculationDetail  
			delete cascades
			Field Mapping uses symbolic key 
				related.ContractGroup				= ContractGroup 
				related.Contract            		= Contract 		
				related.TierQualifierCalculation	= TierQualifierCalculation

		ContractTierMemberRel 
			one-to-many relation to ContractTierMember 
			Field Mapping uses symbolic key 
				related.ContractGroup 				= ContractGroup
				related.Contract      				= Contract					

	Sets

		ByRunDate 
			Sort Order 
				ContractGroup 
				RunDate descending 
				TierQualifierCalculation descending
				Contract 

		ByQualifier 
			Sort Order 
				ContractGroup 
				Contract 
				ContractTier 
				ContractTierQualifier
				TierQualifierCalculation			

  	Field Rules
  	
  	Actions     	 	
 		Create is a Create Action
			restricted 				

		Update is an Update Action 
			restricted 

		Delete is a Delete Action 
