ProjectContractInvoiceBalance is a BusinessClass
    owned by Projects
    prefix is PCIB

    Ontology
    	part of ProjectFundingSource
    		relative key is SequenceNumber
    		
    Persistent Fields
    	FinanceCodeBlock
    	Amount 					is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
    		default label is "InvoicedAmount"
			disable Auditing
		UnitsAmount
			default label is "Hours"
			disable Auditing
    	TaxAmount				is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
			disable Auditing
    	BillableAmount			is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
			disable Auditing
    	RevenueAmount			is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
			disable Auditing
    	AdjustedAmount 			is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
    		default label is "AdjustedInvoicedAmount"
		AdjustedUnitsAmount		is a UnitsAmount
			default label is "AdjustedHours"
    	AdjustedTaxAmount		is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
    	AdjustedBillableAmount	is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals
    	AdjustedRevenueAmount	is a CurrencyAmount
    		precision is ProjectContract.Currency.NumberOfDecimals

	Derived Fields

    Conditions
    	AdjustmentsEntered
    		restricted
    		when (AdjustedAmount entered
			or    AdjustedUnitsAmount entered
    		or    AdjustedTaxAmount entered
	    	or    AdjustedBillableAmount entered
    		or    AdjustedRevenueAmount entered)
		NoAmountsEntered
			restricted
			when (Amount not entered
			and   UnitsAmount not entered
			and   TaxAmount not entered
			and   BillableAmount not entered
			and   RevenueAmount not entered
			and   !AdjustmentsEntered)

    Relations
				
    Sets
		ByProjectAndAccount
			Sort Order
				FinanceEnterpriseGroup
				ProjectContract
				FinanceDimension2
				FinanceCodeBlock.Project								
				FinanceCodeBlock.GeneralLedgerChartAccount
				SequenceNumber
				    
		ByProject
			Sort Order
				FinanceEnterpriseGroup
				ProjectContract
				FinanceCodeBlock.Project				
				FinanceDimension2				
				SequenceNumber
		
		ByAccount
			Sort Order
				FinanceEnterpriseGroup
				ProjectContract
				FinanceCodeBlock.GeneralLedgerChartAccount
				FinanceDimension2				
				SequenceNumber

    Field Rules
    	SequenceNumber
    		autosequence
				minimize contention
	Delete Rules

	Actions
		Create is a Create Action
			restricted

		Delete is a Delete Action
			valid when (NoAmountsEntered)
			
		Update is an Update Action
			restricted
			refresh and lock this instance
			
		Purge is a Purge Action
			restricted

		InitializeBalances is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmProjectContract			is a ProjectContract
        	Instance Selection
        		where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
        		and    ProjectContract		  = PrmProjectContract)
        	Action Rules
        		Instance Rules
    				if (AdjustmentsEntered)
    					invoke InitializeAmounts
    				else
	   					invoke Purge

		InitializeAmounts is an Instance Action
			restricted
			Action Rules
				Amount 			= AdjustedAmount
				UnitsAmount		= AdjustedUnitsAmount
				TaxAmount		= AdjustedTaxAmount
				BillableAmount	= AdjustedBillableAmount
				RevenueAmount	= AdjustedRevenueAmount

		CreateLTDBalanceAdjustment is an Instance Action
			Parameters
		    	PrmAmount 				is a CurrencyAmount
		    		precision is ProjectContract.Currency.NumberOfDecimals
		    		default label is "InvoicedAmount"
		    	PrmBillableAmount		is a CurrencyAmount
		    		precision is ProjectContract.Currency.NumberOfDecimals
		    		default label is "BillableAmount"
				PrmUnitsAmount			is a UnitsAmount
					default label is "Hours"
		    	PrmTaxAmount			is a CurrencyAmount
		    		precision is ProjectContract.Currency.NumberOfDecimals
		    		default label is "TaxAmount"
		    	PrmRevenueAmount		is a CurrencyAmount
		    		precision is ProjectContract.Currency.NumberOfDecimals
		    		default label is "RevenueAmount"
		    Action Rules
		    	invoke Create ProjectContractBalanceAdjustment
		    		fill in fields from this instance
		    			except invoked.SequenceNumber
	    			invoked.Amount			= PrmAmount
	    			invoked.BillableAmount	= PrmBillableAmount
	    			invoked.UnitsAmount		= PrmUnitsAmount
	    			invoked.TaxAmount		= PrmTaxAmount
	    			invoked.RevenueAmount	= PrmRevenueAmount
		    						
				
