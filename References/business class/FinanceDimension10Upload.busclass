FinanceDimension10Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FDxUP   
    
    Ontology
    	symbolic key is FinanceDimension10Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension10		is like FinanceDimension10
    	FinanceDimension10				is like FinanceDimension10
		SequenceDimension10				is like FinanceDimension10
		ParentSequenceDimension10		is like FinanceDimension10
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description	
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode
#ifdef module cam
 		CustodialManagementRecord	is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>_Record" 		
 		InterestBearingThreshold	is an InternationalAmount
		InterestBearing				is Boolean
		Expendable					is Boolean
		CamClassification
			default label is "Classification"
		ReceiveInterest 			is Boolean
#endif

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension10Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension10 is a DerivedField
			type is like FinanceDimension10
			restricted
			if (ParentFinanceDimension10 entered)
				return DerivedParentFinanceDimension10String
			else
				return TopNodeString
		
		DerivedSequenceParentDimension10 is a DerivedField
			type is like FinanceDimension10
			restricted
			if (ParentSequenceDimension10 entered)
				return ParentSequenceDimension10
			else
				return DerivedParentFinanceDimension10 
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension10Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension10String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension10Structure.StructureSequence
			"_"
			ParentFinanceDimension10
		
		DerivedSummaryFinanceDimension10 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension10Structure.StructureSequence
			"_"
			FinanceDimension10
	
		DerivedSummarySequenceDimension10 is a DerivedField
			type is like FinanceDimension10
			restricted
			if (SequenceDimension10 entered)
				return SequenceDimension10
			else 
				return DerivedSummaryFinanceDimension10
			
	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
 
 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension10Structure
				FinanceDimension10
				FinanceDimension10Upload.RunGroup
				FinanceDimension10Upload.Sequence
 	
	Relations
		FinanceDimension10HierarchyRel
			one-to-one relation to FinanceDimension10Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension10Structure = FinanceDimension10Structure
				related.FinanceDimension10		   = FinanceDimension10
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension10Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension10Structure = FinanceDimension10Structure
			Instance Selection
				where (related.FinanceDimension10Upload.RunGroup = FinanceDimension10Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 15
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 15
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension10
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"
							
    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension10Upload.Sequence entered)
					FinanceDimension10Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension10Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted

		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension10Structure		is a FinanceDimension10Structure
					default label is "FinanceDimension10Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension10Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension10	is a FinanceDimension10
				DynamicRulesExist		is Boolean		
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension10Structure	    = PrmFinanceDimension10Structure
				and    FinanceDimension10Upload.RunGroup = PrmRunGroup)
	
			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension10Structure
												
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension10Structure
				FinanceDimension10Upload.RunGroup
				FinanceDimension10Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
										
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension10 = FinanceDimension10
					else
						LocalFinanceDimension10 = DerivedSummarySequenceDimension10
					if (LocalFinanceDimension10 exists)
						if  (RecordType.Posting
						and !FinanceDimension10HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension10
								resume on error
									LocalErrorMessage		= error message	
								invoked.PrmStructure		= FinanceDimension10Structure						
								invoked.PrmParentDimension  = DerivedSequenceParentDimension10
						else					
							if (DerivedSequenceParentDimension10 != LocalFinanceDimension10.DerivedParentDimension)
								invoke Move LocalFinanceDimension10
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension10
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension10
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension10 
										except invoked.ParentFinanceDimension10
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension10
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension10
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension10
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension10 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension10
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension10Structure		is a FinanceDimension10Structure
					default label is "FinanceDimension10Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension10Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension10Structure	    = PrmFinanceDimension10Structure
				and    FinanceDimension10Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension10Structure   is a FinanceDimension10Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension10Structure	    = PrmFinanceDimension10Structure
				and    FinanceDimension10Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
