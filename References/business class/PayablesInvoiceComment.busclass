PayablesInvoiceComment is a BusinessClass
    owned by ap
    prefix is APAPC

    Ontology
    	symbolic key is PayablesInvoiceComment



  
    Patterns
        implements StaticJava        
        disable AuditIndex 
        implements Archivable
               	
    Persistent Fields
    	Company						is a PayablesCompany 
    	PayablesInvoice									
    	Title 						is a CommentName	
    	CommentSequence 			is a Sequence		
    		default label is "Sequence"					
    	Comment					 	is a NewCommentText 
    	Type							is AlphaUpper 1
    		States
    			All						value is "A"
    			InvoiceNote				value is "N"
    			InvoiceReport			value is "D"
    			InvoiceCheck			value is "C"
				InvoicePaymentAddendum	value is "H"
				VendorReturn			value is "V"
    	SupplierCanView         
	
	Transient Fields
		DisplayCommentType				is AlphaUpper 1
			States
				All						value is "A"
				InvoiceNote				value is "N"
				InvoiceReport			value is "D"
				InvoiceCheck			value is "C"
				InvoicePaymentAddendum	value is "H"
			default label is "Type"
			derive value from DerivedCommentType
	
	Derived Fields
		DerivedCommentType				is a DerivedField
			type is AlphaUpper 1
			restricted
			if (Type entered and not Type.VendorReturn)
				return Type
			else
				return blank
	
	Relations
	
	Conditions
		ValidForManualCreateOrUpdate
			restricted
			when (PayablesInvoice.IsCommentUpdateable and not Type.VendorReturn)
	
    Field Rules
    	CommentSequence 				
    		autosequence using ByType	
		
		Title
    		required
		
		Type
			if (DisplayCommentType entered and ValidForManualCreateOrUpdate)
				force default to DisplayCommentType
			
			required
			
			if (old Type.VendorReturn or Type.VendorReturn)
				cannot be changed
					"CannotChangeCommentTypeToOrFrom_Vendor_Return"
		
		Company
			required
		
		PayablesInvoice
			required

	Sets
		ByType
			indexed
			Sort Order
				Company
				PayablesInvoice
				Type
				Title
				CommentSequence		
					
	Actions
		Create is a Create Action
			valid when (ValidForManualCreateOrUpdate)
		
		SystemCreate is a Create Action
			valid when (PayablesInvoice.IsCommentUpdateable)
			restricted
		
		Update is an Update Action
			valid when (ValidForManualCreateOrUpdate)
		
		Delete is a Delete Action
			valid when (PayablesInvoice.IsCommentUpdateable)
		
		Purge is a Purge Action
			restricted
			bypass relational integrity rules
