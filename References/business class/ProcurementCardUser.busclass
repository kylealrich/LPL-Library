ProcurementCardUser is a BusinessClass
    owned by po
    prefix is PCU
    classic name is PDCARDUSER
	representative image is ProcurementCardUser.Employee.ResourcePicture.File
		missing image is random background
			foreground text is "<ProcurementCardUser.Employee.Name.Initials>"

    Ontology
        symbolic key is ProcurementCardUser
            classic set name is PCUSET1
            classic name is CARD-USER-ID

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		Reference1			is a Reference
		Reference2			is a Reference
        ProcurementGroup
        EmailAddress                          
        	restricted
        PhoneNumber         is a TelephoneNumber 
        	holds pii
        	restricted
        FaxNumber                              
        	restricted
        Note          is a Description
        Active        is Boolean
            classic name is ACTIVE-STATUS
        Administrator is Boolean
        ProxyUser     is Boolean
            classic name is PROXY
            
	Derived Fields
		Name					is a DerivedField
			type is Alpha up to 230
			return ProcurementCardUser.PresentationNameSnapshot

		ContextMessageText is a MessageField
			restricted
			"ProcurementCardUser<ProcurementCardUser><Name>"

		CreateMessage is a LabelField
			restricted
			"CreateProcurementCardUser"
		ProcurementCardUserMessage is a LabelField
			restricted
			"<ProcurementCardUser.Employee.Name.PreferredFirstAndLastName>'sProcurementCardUserProfile"

		DerivedFormTitle is a DerivedField
			type is MessageField
			if (ProcurementCardUserExists)
				return ProcurementCardUserMessage
			else
				return CreateMessage

		ActiveMessage is a MessageField
			restricted
			"Active"

		InactiveMessage is a MessageField
			restricted
			"Inactive"

		DerivedStatusDisplay is a DerivedField
			type is Alpha size 8
			default label is "Status"
			if (Active)
				return ActiveMessage
			else
				return InactiveMessage

    Conditions
        IsActive
        	restricted
            when (Active)

        IsAdministrator
            classic name is IS-ADMIN
            restricted
            when (Administrator)

        IsProxyUser
            classic name is IS-PROXY
            restricted
            when (ProxyUser)

		ProcurementCardUserExists
			when (ProcurementCardUser exists)

    Relations
        ProcurementCardProxyRel
            one-to-many relation to ProcurementCardRequest
            Field Mapping uses Set6
                related.ProcurementCardProxyUser = ProcurementCardUser

		ProcurementCardProgramRel
			one-to-many relation to ProcurementCardProgram
			Field Mapping uses ByAdministrator
				related.ProcurementGroup					 	= ProcurementGroup
				related.ProcurementCardProgramAdministrator 	= ProcurementCardUser            

		EmployeeRel
			one-to-one relation to Employee
			Field Mapping uses symbolic key
				related.HROrganization	= ProcurementGroup.FinanceEnterpriseGroup.HROrganization
				related.Employee		= ProcurementCardUser

    Field Rules
        ProcurementGroup
        	required
        	
        	constraint (ProcurementGroup.FinanceEnterpriseGroup.HROrganization = HROrganization)
				"FinanceEnterpriseGroupAssociatedWithProcurementGroupDoesNotUseEnteredHROrganization"
        
        Active
            initial value is true
        	if (ProcurementCardProgramRel exists)
        		cannot be changed	
        			"CannotBeChanged;UserIsActiveOnProgram<first ProcurementCardProgramRel.ProcurementCardProgram>"  
        
  	    Administrator
  	    	if (ProcurementCardProgramRel exists)
        		cannot be changed	
        			"CannotBeChanged;UserIsAdministratorOnProgram<first ProcurementCardProgramRel.ProcurementCardProgram>"	
        
        ProxyUser
  	    	if (ProcurementCardProxyRel exists)
  	    		cannot be changed
  	    			"CannotBeChanged;ProxyUserIsOnProgram<first ProcurementCardProxyRel.ProcurementCardProgram>;Request<first ProcurementCardProxyRel.ProcurementCardRequest>"	   

	Attach Rules
		constraint (Active)
			"ProcurementCardUserIsNotActive"

    Actions
        Create is a Create Action

			Field Rules
			
				Active
					default to true

        Update is an Update Action

        Delete is a Delete Action
			Entrance Rules
				constraint (ProcurementCardProgramRel not exists)
					"CannotBeDeleted;UserIsAdministratorOnProgram<first ProcurementCardProgramRel.ProcurementCardProgram>"
				constraint (ProcurementCardProxyRel not exists)
					"CannotBeDeleted;ProxyUserIsOnProgram<first ProcurementCardProxyRel.ProcurementCardProgram>;Request<first ProcurementCardProxyRel.ProcurementCardRequest>"
