PayablesInvoiceDiscount is a BusinessClass
    owned by ap
    prefix is DSK
    classic name is APDISCACC

    Ontology
        symbolic key is PayablesInvoiceDiscount
            classic set name is DSKSET1

            classic name for PayablesInvoicePayment.PaymentSequence is SEQ-NBR
            classic name for PayablesInvoicePayment.VoidSequence is VOID-SEQ
            classic name for DiscountType is DISC-TYPE

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable
    
    Transient Fields
		TransientAction		is AlphaUpper size 1
			States
				Adjust			value is "A"
				Reverse			value is "R"
				Create			value is "C"	
    
    Persistent Fields
		Vendor						is a snapshot of PayablesInvoice.Vendor
		Invoice						is a snapshot of PayablesInvoice.Invoice
		Suffix						is a snapshot of PayablesInvoice.Suffix
		CancelSequence				is a snapshot of PayablesInvoice.CancelSequence
        PostDate                  	is an ExchangeDate
        DiscountAmount            	is a FinanceCurrencyAmountGroup // GWT was changed from "is an InternationalAmount" MA66
            classic name is TRAN-DISC-AMT


        ProcessLevel              	is a PayablesProcessLevel
        Status                    	is Numeric size 1
            States
                Open   value is 1
                Posted value is 2
        GltObjId                  	is an ObjId
        JournalBook
            classic name is JRNL-BOOK-NBR
            restricted


        NumberOfDecimals          	is an AcctNd
            classic name is TRAN-BASE-ND
        ProcessLevel2             	is a PayablesProcessLevel
            classic name is PROC-LEVEL
        PullForFinancialReporting 	is Boolean
            classic name is PULL-FOR-FR-FL
    
    Field Rules
		PostDate
			required			
			
		DiscountAmount
			required			
			initialize DiscountAmount.FunctionalAmount.EnteredCurrencyAmount
			DiscountAmount.ExchangeDate							= PostDate
			DiscountAmount.FunctionalAmount.ToCurrency 			= PayablesInvoice.Company.Currency

			



		Status
			if (TransientAction.Adjust)
				constraint (!Status.Posted)
					"MatchDiscountPosted,CannotAdjust"		

		DiscountType
			if (DiscountType.Match)				
				if  (PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.Invoice
				or	 PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.DebitMemo)


					if (DiscountAmount.CurrencyAmount > 0)
						DiscountAmount.CurrencyAmount *= -1


				else	
					if  (PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.CreditMemo
					or	 PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.PrepaymentCredit)

						if (DiscountAmount.CurrencyAmount < 0)
							DiscountAmount.CurrencyAmount *= -1


			else	
				if (DiscountType.Reverse)
					if  (PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.Invoice
					or	 PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.DebitMemo)


						if (DiscountAmount.CurrencyAmount < 0)
							DiscountAmount.CurrencyAmount *= -1


					else	
						if  (PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.CreditMemo
						or	 PayablesInvoiceDiscount.PayablesInvoice.InvoiceType.PrepaymentCredit)

							if (DiscountAmount.CurrencyAmount > 0)
								DiscountAmount.CurrencyAmount *= -1


    
    Conditions
        IsApFinRpting
        	restricted
            when (PullForFinancialReporting)

		VendorInvoiceEntered
			restricted
			when (Vendor entered
			and	  Invoice entered)

    Relations























    
    Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		ReverseMatch is an Instance Action			
			restricted
			Action Rules
				if (Status.Posted)
					invoke Create PayablesInvoiceDiscount
						fill in fields from this instance
						invoked.Status		 							= 1
						invoked.PayablesInvoiceDiscount.DiscountType	= DiscountType.Reverse

						invoked.DiscountAmount.CurrencyAmount 			= DiscountAmount.CurrencyAmount * -1

				invoke Delete
				
		AdjustMatchDiscount is an Instance Action		
			restricted
			Parameters
				PrmPostDate					is Date
				PrmProcessLevel				is a PayablesProcessLevel
				PrmDiscountAmount			is an InternationalAmount

				
			Action Rules
				PayablesInvoiceDiscount.DiscountType	= DiscountType.Match
				Status									= Status.Open

				PostDate								= PrmPostDate
				ProcessLevel							= PrmProcessLevel
				DiscountAmount.CurrencyAmount			= PrmDiscountAmount


		UpdateSnapshotFields is an Instance Action
			restricted
			Action Rules
				Vendor				= PayablesInvoice.Vendor
				Invoice				= PayablesInvoice.Invoice
				Suffix				= PayablesInvoice.Suffix
				CancelSequence		= PayablesInvoice.CancelSequence
				    
		RepairDiscount is an Update Action
			restricted
			bypass field rules
			Action Rules

				initialize DiscountAmount.FunctionalAmount.EnteredCurrencyAmount
				initialize DiscountAmount.AlternateAmount.EnteredCurrencyAmount
				initialize DiscountAmount.AlternateAmount2.EnteredCurrencyAmount
				initialize DiscountAmount.AlternateAmount3.EnteredCurrencyAmount
				initialize DiscountAmount.ToFunctionalAmount.EnteredCurrencyAmount
				initialize DiscountAmount.ToAlternateAmount.EnteredCurrencyAmount
				initialize DiscountAmount.ToAlternateAmount2.EnteredCurrencyAmount
				initialize DiscountAmount.ToAlternateAmount3.EnteredCurrencyAmount
				initialize DiscountAmount.ProjectAmount.EnteredCurrencyAmount
				initialize DiscountAmount.ReportAmount1.EnteredCurrencyAmount
				initialize DiscountAmount.ReportAmount2.EnteredCurrencyAmount
				initialize DiscountAmount.ReportAmount3.EnteredCurrencyAmount
				initialize DiscountAmount.ReportAmount4.EnteredCurrencyAmount
				initialize DiscountAmount.ReportAmount5.EnteredCurrencyAmount

				if  (Company.AccountingEntity.FunctionalCurrency entered)
					DiscountAmount.FunctionalAmount.ToCurrency					= Company.AccountingEntity.FunctionalCurrency 
					DiscountAmount.FunctionalAmount.EnteredCurrencyAmount		= DiscountAmount.FunctionalAmount.OutputCurrencyAmount * -1 

				if  (Company.AccountingEntity.AlternateCurrency entered)
					DiscountAmount.AlternateAmount.ToCurrency					= Company.AccountingEntity.AlternateCurrency
					DiscountAmount.AlternateAmount.EnteredCurrencyAmount		= DiscountAmount.AlternateAmount.OutputCurrencyAmount * -1

				if  (Company.AccountingEntity.AlternateCurrency2 entered)
					DiscountAmount.AlternateAmount2.ToCurrency					= Company.AccountingEntity.AlternateCurrency2
					DiscountAmount.AlternateAmount2.EnteredCurrencyAmount		= DiscountAmount.AlternateAmount2.OutputCurrencyAmount * -1

				if  (Company.AccountingEntity.AlternateCurrency3 entered)
					DiscountAmount.AlternateAmount3.ToCurrency					= Company.AccountingEntity.AlternateCurrency3
					DiscountAmount.AlternateAmount3.EnteredCurrencyAmount		= DiscountAmount.AlternateAmount3.OutputCurrencyAmount * -1

				if  (Company.FinanceEnterpriseGroup.ReportCurrencyOne entered)
					DiscountAmount.ReportAmount1.ToCurrency						= Company.FinanceEnterpriseGroup.ReportCurrencyOne
					DiscountAmount.ReportAmount1.EnteredCurrencyAmount			= DiscountAmount.ReportAmount1.OutputCurrencyAmount * -1

				if  (Company.FinanceEnterpriseGroup.ReportCurrencyTwo entered)
					DiscountAmount.ReportAmount2.ToCurrency						= Company.FinanceEnterpriseGroup.ReportCurrencyTwo
					DiscountAmount.ReportAmount2.EnteredCurrencyAmount			= DiscountAmount.ReportAmount2.OutputCurrencyAmount * -1

				if  (Company.FinanceEnterpriseGroup.ReportCurrencyThree entered)
					DiscountAmount.ReportAmount3.ToCurrency						= Company.FinanceEnterpriseGroup.ReportCurrencyThree
					DiscountAmount.ReportAmount3.EnteredCurrencyAmount			= DiscountAmount.ReportAmount3.OutputCurrencyAmount * -1

				if  (Company.FinanceEnterpriseGroup.ReportCurrencyFour entered)
					DiscountAmount.ReportAmount4.ToCurrency						= Company.FinanceEnterpriseGroup.ReportCurrencyFour
					DiscountAmount.ReportAmount4.EnteredCurrencyAmount			= DiscountAmount.ReportAmount4.OutputCurrencyAmount * -1

				if  (Company.FinanceEnterpriseGroup.ReportCurrencyFive entered)
					DiscountAmount.ReportAmount5.ToCurrency						= Company.FinanceEnterpriseGroup.ReportCurrencyFive
					DiscountAmount.ReportAmount5.EnteredCurrencyAmount			= DiscountAmount.ReportAmount5.OutputCurrencyAmount * -1
 
		Purge is a Purge Action
			restricted
			bypass relational integrity rules
		
    Sets
        Set2
            indexed
            Sort Order
                Status
                Company
                Vendor
                PayablesInvoice
                PayablesInvoicePayment

        Set3
            indexed
            Instance Selection
                where (IsApFinRpting)
            Sort Order
                Company
                Vendor
                PayablesInvoice
                PayablesInvoicePayment
                DiscountType

        ByCompany
            indexed
            Sort Order
                Company
                Vendor
                Status
                PayablesInvoice
                PayablesInvoicePayment

		ByCompanyVendorInvoice
			Instance Selection
				where (VendorInvoiceEntered)
			Sort Order
				Company
				Vendor
				Invoice
				Suffix
				CancelSequence
				PayablesInvoicePayment
				DiscountType

        OldSet3
            indexed
            Instance Selection
                where (IsApFinRpting)
            Sort Order
                Company
                Vendor
				Invoice
				Suffix
				CancelSequence
                PayablesInvoicePayment
                DiscountType

