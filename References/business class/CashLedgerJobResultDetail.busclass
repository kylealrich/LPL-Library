CashLedgerJobResultDetail is a BusinessClass
    owned by cb
    prefix is CLJRD
	
    Ontology
        symbolic key is CashLedgerJobResultDetail

    Patterns
    	disable AuditIndex
    	disable Auditing
    	disable EffectiveDated
        enable explicit context override

    Persistent Fields
		FinanceCodeBlock
			context of CashManagementGroup
		Company							is a GeneralLedgerCompany
		Status							is Numeric 1
			States
				Open			value is 1
				Closed			value is 2
		ErrorReport						is BinaryDocument
		OriginatingTransaction 			is BusinessObjectReference
		CashLedgerJobResultSummary
		CashCode
		BankTransactionCode
		Reference 						is a Reference22
		TransactionAmount 				is an InternationalAmount
		FunctionalAmount 				is an InternationalAmount
		
	Transient Fields
		InputTransactionAmount 			is an InternationalAmount
		InputFunctionalAmount 			is an InternationalAmount
    
    Local Fields

    Derived Fields
		DerivedPeriodEndDate is a DerivedField 
			type is Date
			restricted
			return CashLedgerJobResult.PeriodEndDate	

		ReportMessage is a MessageField
			restricted
			"Period_\Close_\Report"
			
    Conditions

		CanClose
    		restricted
    		when (UnreleasedCashLedgerTransactionsRel not exists
    		and   PendingApprovalCashLedgerTransactionsRel not exists
    		and	  PendingStopPayConfirmationCashLedgerTransactionsRel not exists
    		and	  VoidOrStopPaymentCashLedgerTransactionsRel not exists
    		and   UnreleasedCashLedgerPaymentsRel not exists
    		and	  PendingApprovalCashLedgerPaymentsRel not exists
    		and	  PendingStopPaymentApprovalCashLedgerPaymentsRel not exists
			and   UnreleasedCashLedgerReturnedPaymentsRel not exists
			and   UnreleasedCashLedgerReconciliationAdjustmentsRel not exists
			and	  UnreleasedCashLedgerCashReceiptHeadersRel not exists
			and   PendingApprovalCashLedgerCashReceiptHeadersRel not exists
			and   UnreleasedCashLedgerFundTransferHeadersRel  not exists
			and   UnpostedCashLedgerGLDistributionsRel not exists
			and   UnpostedNonCashLedgerDistributionsRel not exists
			and   PendingApprovalNonCashLedgerDistributionsRel not exists
			and   UnpostedBankStatementLineDistributionsRel not exists
			and   PendingApprovalBankStatementLineDistributionsRel not exists
			and   UnConfirmedWireTransferBatchDetailsRel not exists)
				
        CashLedgerTransactionsUnreleased
        	restricted
        	when (UnreleasedCashLedgerTransactionsRel exists)
 
        CashLedgerTransactionsPendingApproval	
        	restricted
            when (PendingApprovalCashLedgerTransactionsRel exists)
            			
  		CashLedgerTransactionsPendingStopPayConfirmation	
 		    restricted
            when (PendingStopPayConfirmationCashLedgerTransactionsRel exists)

		CashLedgerTransactionsVoidOrStopPayment
        	restricted
            when (VoidOrStopPaymentCashLedgerTransactionsRel exists)
 
		CashPaymentsUnreleased	
        	restricted
            when (UnreleasedCashLedgerPaymentsRel exists)
			
		CashPaymentsPendingApproval		
        	restricted
        	when (PendingApprovalCashLedgerPaymentsRel exists)

		CashPaymentsPendingStopPaymentApproval		
        	restricted
        	when (PendingStopPaymentApprovalCashLedgerPaymentsRel exists)

        ReturnedPaymentsUnreleased	
        	restricted
            when (UnreleasedCashLedgerReturnedPaymentsRel exists)
            
        ReturnedPaymentsOutstanding
        	restricted
        	when (ReturnedPaymentsUnreleased)    	

        ReconciliationAdjustmentsUnreleased
        	restricted
            when (UnreleasedCashLedgerReconciliationAdjustmentsRel exists)

        CashReceiptsUnreleased
        	restricted
            when (UnreleasedCashLedgerCashReceiptHeadersRel exists)

		CashReceiptsPendingApproval
			restricted
			when (PendingApprovalCashLedgerCashReceiptHeadersRel exists)
					
        BankFundTransfersUnreleased
        	restricted
            when (UnreleasedCashLedgerFundTransferHeadersRel exists)

		CashLedgerGLDistributionsUnposted
			restricted
			when (UnpostedCashLedgerGLDistributionsRel exists)
		
		NonCashLedgerDistributionsUnposted
			restricted
			when (UnpostedNonCashLedgerDistributionsRel exists)

		NonCashLedgerDistributionsPendingApproval
			restricted
			when (PendingApprovalNonCashLedgerDistributionsRel exists)
			
		BankStatementLineDistributionsUnposted
			restricted
			when (UnpostedBankStatementLineDistributionsRel exists)

		BankStatementLineDistributionsPendingApproval
			restricted
			when (PendingApprovalBankStatementLineDistributionsRel exists)

		WireTransferBatchDetailsUnConfirmed
			restricted
			when (UnConfirmedWireTransferBatchDetailsRel exists)
		
    Relations
    
		CompanySystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "CB"
				related.Company						= Company	

		CashLedgerTransactionsRel 
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set8
				related.CashManagementGroup	  			= CashManagementGroup 
				related.Company.OrigCompany	  			= Company
			Instance Selection
				where (related.OutstandingTransactionsExist)


		UnreleasedCashLedgerTransactionsRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set8
				related.CashManagementGroup	  			= CashManagementGroup 
				related.Company.OrigCompany	  			= Company
				related.Status							= 0 
            Instance Selection
            	where (related.GeneralLedgerPostDate <= CashLedgerJobResult.PeriodEndDate 
            	and    related.SourceCode != "AP")
		
		PendingApprovalCashLedgerTransactionsRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set8
				related.CashManagementGroup	  			= CashManagementGroup 
				related.Company.OrigCompany	  			= Company
				related.Status							= 7 
            Instance Selection
            	where (((related.ApprovalRequestType.Release 
				and      related.GeneralLedgerPostDate <= CashLedgerJobResult.PeriodEndDate) 
				or     ((related.ApprovalRequestType.StopPayment 
				or       related.ApprovalRequestType.Void)
				and      related.ReconciliationData.ExchangeDate <= CashLedgerJobResult.PeriodEndDate))
            	and      related.SourceCode != "AP")
        
        PendingStopPayConfirmationCashLedgerTransactionsRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set8
				related.CashManagementGroup	  			= CashManagementGroup 
				related.Company.OrigCompany	  			= Company
				related.Status							= 8 
            Instance Selection
            	where (related.ReconciliationData.ExchangeDate <= CashLedgerJobResult.PeriodEndDate 
            	and    related.SourceCode != "AP")
        
        VoidOrStopPaymentCashLedgerTransactionsRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set8
				related.CashManagementGroup	  			= CashManagementGroup 
				related.Company.OrigCompany	  			= Company
            Instance Selection
            	where ((related.Status.Voided
            	or	    related.Status.StopPayment)
            	and     related.ReconciliationData.ExchangeDate <= CashLedgerJobResult.PeriodEndDate 
            	and    !related.ReinstateStatus
            	and     related.SourceCode != "AP")
            			
		CashLedgerPaymentsRel 
			one-to-many relation to CashLedgerPayment
			Field Mapping uses ByCompany
				related.CashManagementGroup				= CashManagementGroup 
				related.CompanyBaseData.OverrideCompany	= Company
			Instance Selection
				where (related.OutstandingCashPaymentsExist)
		
		UnreleasedCashLedgerPaymentsRel
			one-to-many relation to CashLedgerPayment
			Field Mapping uses ByCompany
				related.CashManagementGroup				= CashManagementGroup 
				related.CompanyBaseData.OverrideCompany	= Company
				related.Status							= 0 
            Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 
		
		PendingApprovalCashLedgerPaymentsRel
			one-to-many relation to CashLedgerPayment
			Field Mapping uses ByCompany
				related.CashManagementGroup				= CashManagementGroup 
				related.CompanyBaseData.OverrideCompany	= Company
				related.Status							= 6 
            Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 

		
		PendingStopPaymentApprovalCashLedgerPaymentsRel
			one-to-many relation to CashLedgerPayment
			Field Mapping uses ByCompany
				related.CashManagementGroup				= CashManagementGroup 
				related.CompanyBaseData.OverrideCompany	= Company
				related.Status							= 7 
            Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 

						
		CashLedgerReturnedPaymentsRel 
			one-to-many relation to CashLedgerReturnedPayment
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
			Instance Selection
				where (related.OutstandingReturnedPaymentsExist)
		
		UnreleasedCashLedgerReturnedPaymentsRel
			one-to-many relation to CashLedgerReturnedPayment
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
				related.Status						= 0 
			Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 
							
		CashLedgerReconciliationAdjustmentsRel 
			one-to-many relation to CashLedgerReconciliationAdjustment
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany 		= Company
			Instance Selection
				where (related.OutstandingReconciliationAdjustmentsExist)
		
		UnreleasedCashLedgerReconciliationAdjustmentsRel
			one-to-many relation to CashLedgerReconciliationAdjustment
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany 		= Company
				related.Status						= 0 
			Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 
						
		CashLedgerCashReceiptHeadersRel 
			one-to-many relation to CashLedgerCashReceiptHeader
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
			Instance Selection
				where (related.OutstandingCashReceiptsExist)
		
		UnreleasedCashLedgerCashReceiptHeadersRel
			one-to-many relation to CashLedgerCashReceiptHeader
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
				related.Status						= 0 
			Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 


		PendingApprovalCashLedgerCashReceiptHeadersRel
			one-to-many relation to CashLedgerCashReceiptHeader
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
				related.Status						= 2 
			Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 

							
		CashLedgerFundTransferHeadersRel 
			one-to-many relation to CashLedgerFundTransferHeader
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
			Instance Selection
				where (related.OutstandingBankFundTransfersExist)

		UnreleasedCashLedgerFundTransferHeadersRel
			one-to-many relation to CashLedgerFundTransferHeader
			Field Mapping uses ByCompany
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
				related.Status						= 0 
			Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 

				
		CashLedgerGLDistributionsRel 
			one-to-many relation to CashLedgerGLDistribution
			Field Mapping uses Set4
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
			Instance Selection
				where (related.OutstandingCashLedgerGLDistributionsExist)

		UnpostedCashLedgerGLDistributionsRel
			one-to-many relation to CashLedgerGLDistribution
			Field Mapping uses Set4
				related.CashManagementGroup			= CashManagementGroup 
				related.Company.OrigCompany			= Company
				related.Status						= 1 
			Instance Selection
				where (related.PostDate <= CashLedgerJobResult.PeriodEndDate) 
				
		NonCashLedgerDistributionsRel 
			one-to-many relation to NonCashLedgerDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup						 = CashManagementGroup 
			Instance Selection
				where (related.CashManagementAccount.LegalEntity = Company
				and    related.OutstandingNonCashLedgerDistributionsExist)

		UnpostedNonCashLedgerDistributionsRel
			one-to-many relation to NonCashLedgerDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup						 = CashManagementGroup 
				related.Status									 = 1 
			Instance Selection
				where (related.CashManagementAccount.LegalEntity = Company
				and    related.PostingDate <= CashLedgerJobResult.PeriodEndDate) 

		PendingApprovalNonCashLedgerDistributionsRel
			one-to-many relation to NonCashLedgerDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup						 = CashManagementGroup 
				related.Status									 = 3 
			Instance Selection
				where (related.CashManagementAccount.LegalEntity = Company
				and    related.PostingDate <= CashLedgerJobResult.PeriodEndDate) 
				
		BankStatementLineDistributionsRel 
			one-to-many relation to BankStatementLineDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup						 = CashManagementGroup 
			Instance Selection
				where (related.CashManagementAccount.LegalEntity = Company
				and    related.OutstandingBankStatementLineDistributionsExist)

		UnpostedBankStatementLineDistributionsRel
			one-to-many relation to BankStatementLineDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup				= CashManagementGroup 
				related.Status							= 1 
			Instance Selection
				where (related.CashManagementAccount.LegalEntity = Company
				and    related.PostingDate <= CashLedgerJobResult.PeriodEndDate) 

		PendingApprovalBankStatementLineDistributionsRel
			one-to-many relation to BankStatementLineDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup				= CashManagementGroup 
				related.Status							= 3 
			Instance Selection
				where (related.CashManagementAccount.LegalEntity = Company
				and    related.PostingDate <= CashLedgerJobResult.PeriodEndDate) 
		
		WireTransferBatchDetailsRel 
			one-to-many relation to WireTransferBatchDetail
			Field Mapping uses ByCompany
				related.CashManagementGroup				= CashManagementGroup 
				related.FromCompany						= Company
			Instance Selection
				where (related.OutstandingWireTransferBatchDetailsExist)

		UnConfirmedWireTransferBatchDetailsRel
			one-to-many relation to WireTransferBatchDetail
			Field Mapping uses ByCompany
				related.CashManagementGroup				= CashManagementGroup 
				related.FromCompany						= Company
			Instance Selection
				where (!related.Processed
				and    !related.Rejected
				and     related.PostingDate <= CashLedgerJobResult.PeriodEndDate) 
						
		CashLedgerCompanyPeriodCloseErrorsRel
			one-to-many relation to CashLedgerJobResultError
			Field Mapping uses symbolic key
				related.CashManagementGroup				= CashManagementGroup
				related.CashLedgerJobResult				= CashLedgerJobResult
			Instance Selection	
				where (related.Company					= Company)

		
    Sets
    	ByPeriodEndDate
            Sort Order
            	CashManagementGroup
            	CashLedgerJobResult
            	CashLedgerJobResult.PeriodEndDate descending
                Company	
                CashLedgerJobResultDetail
					
		ByOriginatingTransaction
        	Sort Order
				CashManagementGroup
				CashLedgerJobResult
				OriginatingTransaction
				FinanceCodeBlock	
				CashLedgerJobResultDetail	
     	
    Field Rules
		Status
			initial value is 1	
			default to 1
			
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted
		
		DeleteCompanyPeriodCloseDetails is a Set Action	
			default label is untranslatable
			restricted
			Parameters
				PrmCashManagementGroup			is a CashManagementGroup
				PrmCashLedgerJobResult			is a CashLedgerJobResult
			
			Instance Selection
				where (CashManagementGroup		= PrmCashManagementGroup
				and    CashLedgerJobResult 		= PrmCashLedgerJobResult)	
                
			Action Rules
				Set Rules
					Exit Rules
						invoke Delete PrmCashLedgerJobResult
				Instance Rules
					invoke Delete

		UpdateAmounts is an Update Action 
			restricted
			bypass field rules
			default label is untranslatable 
			Action Rules
				TransactionAmount += InputTransactionAmount
				FunctionalAmount  += InputFunctionalAmount
				
		CloseCompany is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateCompanySystemClosingControl CompanySystemClosingControlRel
					invoked.PrmCompany		= Company
					invoked.PrmSystem		= "CB"
				Status = Status.Closed
										
