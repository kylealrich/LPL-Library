RecallNoticeTaskQuestion is a BusinessClass
    owned by recall
    prefix is RMRTQ

    Ontology
    	symbolic key is RecallNoticeTaskQuestion
    
    Patterns
        implements CompoundDocument
        	Document Components
				RecallNoticeTaskQuestionValue set
					
    Persistent Fields
    	Description				is Text
    		default label is "Question"
   		ResponseType			is Numeric size 2
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
  				List 				value is 4
   				YesNo 				value is 5
   				YesNoText			value is 6 
   		ResponseRules            is Numeric size 1
            States
                NotRequired                             value is 1
                ResponseRequired                        value is 2
                ResponseRequiredAndAttachmentRequired   value is 3
        YesNoResponseRules       is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                 value is 1
                ResponseRequired                            value is 2
                ResponseRequiredAndAttachmentRequired       value is 3
                ResponseRequiredAndAttachmentRequiredIfYes  value is 4
                ResponseRequiredAndAttachmentRequiredIfNo   value is 5
        YesNoTextResponseRules   is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                         value is 1
                ResponseRequiredAndTextResponseRequired             value is 2
                ResponseRequiredAndTextOrAttachmentRequired         value is 3
                ResponseRequiredAndTextOrAttachmentRequiredIfYes    value is 4
                ResponseRequiredAndTextOrAttachmentRequiredIfNo     value is 5
                ResponseRequiredAndTextAndAttachmentRequired        value is 6
                ResponseRequiredAndTextAndAttachmentRequiredIfYes   value is 7
                ResponseRequiredAndTextAndAttachmentRequiredIfNo    value is 8
   		Attachment 
   		AllowResponseAttachment	is Boolean
   			default label is "AllowAttachment"
   	
   	Derived Fields
   		
   		DescriptionMessage is a MessageField
   			restricted
   			"Description"
   		
   		RequiredDescription is a ConditionalField
   			type is Text
   			restricted
   			if (ResponseRequired)
   				"*<DescriptionMessage>"
   			else
   				DescriptionMessage
   				
   	Field Rules
   		Description
			required
				"DescriptionIsRequired"
		
		ResponseType
			required
				"ResponseTypeIsRequired"
				
		ResponseRules
            if (NonYesNoResponseType)
                required
                    "MustSelectAResponseRule"
                YesNoResponseRules = 0
                YesNoTextResponseRules = 0
                    
        YesNoResponseRules
            if (ResponseType.YesNo)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoTextResponseRules= 0
        
        YesNoTextResponseRules
            if (ResponseType.YesNoText)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoResponseRules= 0
			
		AllowResponseAttachment
			if (MayRequireResponseAttachment)
				AllowResponseAttachment = true
	
	Conditions
		IsListResponseType
			restricted
			when (ResponseType.List)
	
    	HasListValues
    		restricted
    		when (RecallNoticeTaskQuestionValue set exists)
    	
    	ListQuestionWithNoValues
    		restricted
    		when (IsListResponseType
    		and	  !RecallNoticeTaskQuestionValue set exists)
    			
    	ResponseRequired
    		restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    ResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        MayRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        AlwaysRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)  
    	
    	HasAttachment
    		restricted
    		when (Attachment entered)
    		
    	QuestionUpdatable
    		restricted
    		when ((RecallNotice.RecallNoticeUpdatable
    		and   (RecallNoticeTask.NotProposedItem
    		or   (RecallNoticeTask.OpenProposedItemTask)))
    		or   (RecallNotice.Status.Active
    		and   RecallNoticeTask.ManuallyCreatedTask
			and   RecallNoticeTask.Status.New
			and   RecallNoticeTask.NotProposedItem))
			
		NonYesNoResponseType
			restricted
			when (ResponseType.Date
			or    ResponseType.List
			or    ResponseType.Text
			or    ResponseType.Number)
    
	Sets
		ByResponseType
			indexed
			Sort Order
				ResponseType
				RecallGroup
				RecallNotice
				RecallNoticeTaskQuestion
				RecallNoticeTask
						
	Actions
		Create is a Create Action
			valid when (QuestionUpdatable)
			
		Update is an Update Action
			valid when (QuestionUpdatable)
			Action Rules
				if (!ResponseType.List
				and RecallNoticeTaskQuestionValue set exists)
					invoke Delete RecallNoticeTaskQuestionValue set
					
		Delete is a Delete Action
			valid when (QuestionUpdatable)
			
		Purge is a Purge Action
			restricted
