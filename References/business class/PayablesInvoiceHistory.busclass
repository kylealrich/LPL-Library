PayablesInvoiceHistory is a BusinessClass
    owned by ap
    prefix is AH1
    classic name is APAPIHIST

    Ontology
        symbolic key is PayablesInvoiceHistory
            classic set name is AH1SET1
            classic name for PayablesInvoiceHistory.CancelSequence is CANCEL-SEQ

    Patterns
        implements StaticJava
        disable AuditIndex
		implements ContextualParent
		implements Archivable
		
    Persistent Fields
    	PayablesInvoiceNumber		  is like PayablesInvoice
        CancelDate                    is Date
        BatchNumber                   is a PayablesBatch
            classic name is BATCH-NUM
        BatchDate                     is Date
        VoucherNumber
            classic name is VOUCHER-NBR
            delete ignored
        Reference
        AuthorityCode                 is a PayablesAuthorityCode
            classic name is AUTH-CODE
        ProcessLevel                  is a PayablesProcessLevel
            classic name is PROC-LEVEL
        AccrualCode                   is a PayablesAccrualCode
            classic name is ACCR-CODE
        InvoiceType                   is AlphaUpper size 1
            States
                Invoice               value is blank
                CreditMemo            value is "C"
                DebitMemo             value is "D"
                Prepayment            value is "P"
                PrepaymentCredit      value is "Y"



                MatchPrepayment       value is "R"
        InvoiceCurrency               is a Currency
            classic name is INV-CURRENCY
        CurrencyTable							
        PayCurrency                   is a Currency
        InvoiceDate                   is Date
            classic name is INVOICE-DTE
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
            context of Vendor
        PurchaseOrder
        Description
        BaseInvoiceAmount             is an InternationalAmount
            classic name is BASE-INV-AMT
        RevaluedBaseAmount            is an InternationalAmount
            classic name is BASE-ACT-AMT
        BaseNumberOfDecimals
            classic name is BASE-ND
        InvoiceAmount                 is an InternationalAmount
            classic name is TRAN-INV-AMT
        AllowableAmountForDiscount    is an InternationalAmount
            classic name is TRAN-ALOW-AMT
        TaxableAmount                 is an InternationalAmount
            classic name is TRAN-TXBL-AMT
        TotalTaxableAmount            is an InternationalAmount
            classic name is TRAN-TOT-TXBL
        NumberOfDecimals              is Numeric size 1
            classic name is TRAN-ND
        TaxAmount                     is an InternationalAmount
            classic name is TRAN-TAX-AMT
        BaseDiscountAmount            is an InternationalAmount
            classic name is BASE-DISC-AMT
        DiscountAmount                is an InternationalAmount
            classic name is TRAN-DISC-AMT
        BaseTotalPaymentAmount        is an InternationalAmount
            classic name is BASE-TOT-PMT
        TotalPaymentAmount            is an InternationalAmount
            classic name is TRAN-TOT-PMT
        BaseTotalDistributionAmount   is an InternationalAmount
            classic name is BASE-TOT-DIST
        TotalDistributionAmount       is an InternationalAmount
            classic name is TRAN-TOT-DIST
        TotalTaxAmount                is an InternationalAmount
            classic name is TRAN-TOT-TAX
        AmountPaid                    is an InternationalAmount
            classic name is TRAN-PAID-AMT
        CurrencyExchangeRate          is a CurrencyRate
            classic name is ORIG-CNV-RATE
        Anticipation                  is Boolean
        DiscountPercent
            classic name is DISCOUNT-RT
        DiscountDate                  is Date
            classic name is DISC-DATE
        DueDate
        NumberOfSplitPayments         is Numeric size 3
            classic name is NBR-SPLIT-PMT
        SplitPaymentSchedule
            classic name is SPLIT-PMT-SCH
        NumberOfRecurringInvoices     is Numeric size 3
            classic name is NBR-RECUR-PMT
        RecurringFrequency
            classic name is RECUR-FREQ
        RemitToCode                   is a VendorLocation
            context of Vendor
        CashCode
        BankTransactionCode
            classic name is BANK-INST-CODE
        RevalueCurrency               is Boolean
            classic name is CURR-RECALC
        TaxCode
        IncomeCode                    is a PayablesIncomeCode
        DistributionCode              is a PayablesDistributionCode
            classic name is DIST-CODE
        Status                        is Numeric size 1
            classic name is REC-STATUS
            States
                Unreleased value is 0
                Released   value is 1
                Historical value is 9
        CreateDate                    is TimeStamp
        DistributionDate              is Date
            classic name is DISTRIB-DATE
        Operator
        PayVendor                     is a Vendor
        PayGroup
        InvoiceGroup				  is a PayablesInvoiceGroup     
        LastDistribution              is a DistributionSequence
            classic name is LAST-DIST-SEQ
        LastPaymentSequence           is a SeqNbr
            classic name is LAST-PMT-SEQ
        DiscountCode                  is a PayablesDiscountCode
        InvoiceSource                 is AlphaUpper size 1
        InvoiceReference              is like PayablesReferenceType	
            classic name is INVC-REF-TYPE
        Approved                      is AlphaUpper size 1
            classic name is APPROVED-FLAG
        ApprovedBy                    is an Operator 
        	holds pii
            classic name is APPRV-OPERATOR
        VendorReturn
            classic name is RETURN-NUMBER
        JournalBook
            classic name is JRNL-BOOK-NBR
            restricted
        TaxPoint                      is AlphaUpper size 1


        ReconciliationDate             is Date
            classic name is RECON-DATE
        ProofOfDeliveryPrinted        is Boolean
            classic name is POD-PRINTED
        MatchInvoice
            classic name is MATCH-FL
        TermCode				      is a TermsCode
            classic name is TERMS-CD
        ReceiptOfInvoiceDate          is Date
            classic name is RCPT-INV-DATE
        RetailAmount                  is an InternationalAmount
            classic name is RETAIL-AMT
        MatchStatus                   is Numeric size 1
        ReasonCode                    is a ProcurementReasonCode
        MatchAmount                   is an InternationalAmount
            classic name is MATCH-AMT
        AddOnChargeAmount             is an InternationalAmount
            classic name is AOC-ALLOW-AMT
        Location                      is an InventoryLocation
        MatchObjectID                 is an ObjId
            classic name is MATCH-OBJ-ID
        PrintChargeback               is Boolean
            classic name is CBPRINT-FL
        MatchTable
        TaxAdjustment                 is AlphaUpper size 1
            classic name is TAX-CODE-CNTL
        LastMatchLine                 is an OrigLine
            classic name is LAST-MATCH-LN
        MatchLevel
        MatchDate                     is Date
		MatchReferenceNumber
        POTaxAmount                   is an InternationalAmount
            classic name is PO-INV-TAX
        BypassMatch                   is Boolean
        ServiceOnlyInvoice            is Boolean
            classic name is SERVICE-FL
        ServiceAmount                 is an InternationalAmount
            classic name is SERVICE-AMT
        NatureOfTransactionCode
            classic name is NOTC
        StatisticalProcedure		  is an IntrastatStatisticalProcedure
            classic name is STAT-PROC
        ShippingMethod                is a ShipVia
            classic name is SHIP-VIA
        UnloadingPort
        IntrastatNumber
            classic name is INTRASTAT-NBR
        Dropship                      is Boolean
            classic name is DROPSHIP-FL
        ShipTerm
            classic name is FOB-CODE
        JournalBookSequenceNumber     is a JbkSeqNbr
            classic name is JBK-SEQ-NBR
            restricted
        DiversityCode                 is a PayablesDiversityCode
            classic name is DIVERSE-CODE
        FlexTerms                     is Boolean
            classic name is FLEX-FLAG
        RuleGroup                     is a MatchRule
        FloatDays
        MatchPrepayment               is Boolean
            classic name is MTCH-PREPAY-FL
        MatchPrepaymentMethod
            classic name is MTCH-PREPAY-MT

        CostComponentMatch            is Boolean
            classic name is COCO-FL
        MatchedAddOnChargeAmount      is an InternationalAmount
            classic name is MATCH-AOC
        HashQuantity
            classic name is HASH-QTY
        CrossReferenceVendor
            classic name is XREF-VENDOR
            context of Vendor
        MatchErrorType                is Numeric size 2
            classic name is MTCH-ERR-TYPE
            States
                MatchInvoice         value is 1
                CostError            value is 2
                NoReceiptInformation value is 3
                HashQuantityError    value is 4
                NoAOCOnPurchaseOrder value is 5
                NoAOCCost            value is 6
        BestTermsDiscountAmount       is an InternationalAmount
            classic name is BT-AMT
        DiscountPoint
            classic name is DISC-POINT
        Buyer
        FinalIntransitDistribution    is Boolean
            classic name is FINAL-DST-FLAG














        TaxInterfaced                 is Boolean
            classic name is TAX-FLAG
		ProcurementCardProgram
        ProcurementCardStatement
            classic name is STATEMENT
        MatchProcessType
            classic name is MTCH-PROC-TYPE
        RetainageAccrualCode          is a PayablesAccrualCode
            classic name is RET-ACCR-CODE
        RetainageAmount               is an InternationalAmount
            classic name is RET-TRAN-AMT
        BaseRetainageAmount           is an InternationalAmount
            classic name is RET-BASE-AMT
        RetainagePaymentAmount        is an InternationalAmount
            classic name is RET-TRAN-PMT
        BaseRetainagePaymentAmount    is an InternationalAmount
            classic name is RET-BASE-PMT
        ContractRebateGroup
        TaxType                       is AlphaUpper size 1
            States
                Invoiced value is "I"
                Accrued  value is "A"
        SecondaryWithholding          is Boolean
            classic name is SEC-WITHHOLD
        ForeignEconomicRegulatoryCode
            classic name is FOR-ECON-CODE
        ReferenceNumber
            classic name is REFERENCE-NO
		VendorPaymentAmount			  is an InternationalAmount		
		VendorPaymentCurrency		  is a  Currency
		LetterOfGuarantee
			classic name is LTR-OF-GUARAN
		CertificationNumber						
		CertificationDate			is Date		
		
	Local Fields
		LocalInvoiceCutOffDate		is Date


    Derived Fields
        TaxOutOfBalance          is a ComputeField
            type is Decimal size 15.2
            restricted
            classic name is TAX-OUT-BAL
            (TaxAmount - TotalTaxAmount)

        TaxableOutOfBalance      is a ComputeField
            type is Decimal size 15.2
            restricted
            classic name is TXB-OUT-BAL
            (TaxableAmount - TotalTaxableAmount)

        PaymentOutOfBalance      is a ComputeField
            type is Decimal size 15.2
            restricted
            classic name is PMT-OUT-BAL
            (InvoiceAmount - TotalPaymentAmount)

        DistributionOutOfBalance is a ComputeField
            type is Decimal size 15.2
            restricted
            classic name is DIST-OUT-BAL
            (InvoiceAmount - TotalDistributionAmount)

        OutOfBalanceFlag         is a ConditionalField
            type is AlphaUpper size 1
     
            classic name is OUT-BAL-FLAG
            if (IsOutOfBalance)
                "*"
            else
                blank

        InvoiceStatus            is a ConditionalField
            type is Alpha size 10
    
            classic name is INV-STATUS
            if (IsUnreleased)
                "Unreleased"
            else
            if (IsReleased)
                "Released"
            else
            if (IsInvoiceHistory)
                "History"
            else
                "Cancelled"

        NetPayable               is a ComputeField
            type is Decimal size 15.2
            restricted
            (InvoiceAmount - DiscountAmount)

        ReverseSignAmount        is a ComputeField
            type is Decimal size 15.2
            restricted
            classic name is REV-SIGN-AMT
            (InvoiceAmount * -1)

        TypeShortName            is a ConditionalField
            type is AlphaUpper size 6
            restricted
            classic name is TYPE-SHORT-NME
            if (IsRegularInvoice)
                blank
            else
            if (IsCreditInvoice)
                "CREDIT"
            else
            if (IsDebitInvoice)
                "DEBIT"
            else
            if (IsPrepayment)
                "PREPAY"
            else






                blank

        PoRelease3               is a ComputeField
            type is Decimal size 3
            restricted
            (PurchaseOrder.PORelease * 1)

    Conditions




        PayablesInvoiceAddOnChargeHistory2RelExists
        	restricted
            classic name is AOC-DTLS-EXIST
            when (first PayablesInvoiceAddOnChargeHistory2Rel exists)

        IsApMatchStatus
        	restricted
            classic name is APMATCHSTATUS
            when ((MatchStatus not entered
                or    MatchStatus = 1)
            and   !MatchProcessType.Expense
            and   (Status.Unreleased
                or    Status.Released))

        PurchaseFromLocationEntered
            classic name is APVENLOCPURCH
            restricted
            when (PurchaseFromLocation entered)

        RemitToCodeEntered
            classic name is APVENLOCREMIT
            restricted
            when (RemitToCode entered)







        IsCancelInvoice
        	restricted
            when (PayablesInvoiceHistory.CancelSequence entered)

        IsCreditInvoice
        	restricted
            when (InvoiceType.CreditMemo
            or    InvoiceType.PrepaymentCredit)


        IsCreditMemo
        	restricted
            when (InvoiceType.CreditMemo)

        IsDebitInvoice
        	restricted
            when (InvoiceType.DebitMemo)

        PayablesInvoiceDetailHistory2RelExists
            classic name is DTLS-EXIST
            restricted
            when (first PayablesInvoiceDetailHistory2Rel exists)

















        IsFactorVendor
        	restricted
            when (PayVendor != Vendor)

        IsInvoiceHistory
            classic name is INV-HISTORY
            restricted
            when (Status.Historical
            and   PayablesInvoiceHistory.CancelSequence not entered)

        IsReleased
            classic name is INV-RELEASED
            restricted
            when (Status.Released)

        IsUnreleased
            classic name is INV-UNRELEASED
            restricted
            when (Status.Unreleased)

        IsLawsonPo
        	restricted
            when (!MatchProcessType.Expense)

        IsMatched
        	restricted
            when (MatchObjectID entered
            and   (Status.Released
                or    Status.Historical))

        IsNonBaseInvoice
        	restricted
            classic name is NON-BASE-INV
            when (InvoiceCurrency != Company.Currency)	

        IsOutOfBalance
        	restricted
            when ((InvoiceAmount != BaseTotalDistributionAmount
                or    InvoiceAmount != TotalPaymentAmount
                or    TaxAmount != TotalTaxAmount)
            and   MatchProcessType.Expense)

        PurchaseOrderPONumberEntered
            classic name is PO-NUMBER-USED
            restricted

            when (PurchaseOrder entered)

        IsPrepayment
        	restricted
            when (InvoiceType.Prepayment)

        IsRegularInvoice
            classic name is REGULAR-INVOIC
            restricted
            when (InvoiceType.Invoice)

        IsRetainageInvoice
            classic name is RETAINAGE-INV
            restricted
            when (RetainageAmount entered)

		IsAPurchaseOrderInvoice
			restricted
			when (MatchProcessType.ReceiptMatch
			or    MatchProcessType.Service
			or    MatchProcessType.DropShip
			or    MatchProcessType.AOCOnly
			or    MatchProcessType.ServiceContract)

    Relations
		PayablesInvoiceDistributionHistoryRel
			one-to-many relation to PayablesInvoiceDistributionHistory
			Field Mapping uses Set1

				related.Company						= Company
				related.PayablesInvoiceHistory		= PayablesInvoiceHistory
	
		PayablesInvoiceSpreadAddOnChargeHistoryRel
			one-to-many relation to PayablesInvoiceSpreadAddOnChargeHistory
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.Vendor  				= Vendor
                related.PayablesInvoiceHistory	= PayablesInvoiceHistory

		PayablesCreditMemoHistoryRel
			one-to-many relation to PayablesCreditMemoHistory
			Field Mapping uses Set2
				related.Company				= Company
				related.Vendor				= Vendor
				related.PayablesCreditMemoHistory.Invoice		= PayablesInvoiceHistory.Invoice
				related.PayablesCreditMemoHistory.Suffix		= PayablesInvoiceHistory.Suffix

		PayablesInvoiceDiscountHistoryRel
			one-to-many relation to PayablesInvoiceDiscountHistory
			Field Mapping uses ByCompanyVendorInvoice
				related.Company									= Company
				related.Vendor									= Vendor
				related.PayablesInvoiceHistory.Invoice			= PayablesInvoiceHistory.Invoice
				related.PayablesInvoiceHistory.Suffix			= PayablesInvoiceHistory.Suffix
				related.PayablesInvoiceHistory.CancelSequence	= PayablesInvoiceHistory.CancelSequence
			
        PayablesInvoicePaymentHistoryRel
            classic name is APAPPHIST
            one-to-many relation to PayablesInvoicePaymentHistory
            delete cascades
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.Vendor  				= Vendor
                related.PayablesInvoiceHistory	= PayablesInvoiceHistory

        PayablesInvoiceRetainageDistributionRel
            classic name is APDISTPAY
            one-to-many relation to PayablesInvoiceRetainageDistribution
            Field Mapping uses Set1
                related.Company = Company
                related.Vendor  = Vendor
                related.Invoice = PayablesInvoiceHistory.Invoice
                related.Suffix  = PayablesInvoiceHistory.Suffix

        PurchaseVendorLocationRel
            classic name is APVENLOCPURCH
            one-to-one relation to VendorLocation
            valid when (PurchaseFromLocationEntered)
            required
            Field Mapping uses symbolic key
                related.VendorGroup    = Company.VendorGroup
                related.Vendor         = Vendor
                related.VendorLocation = PurchaseFromLocation

        RemitVendorLocation
            classic name is APVENLOCREMIT
            one-to-one relation to VendorLocation
            valid when (RemitToCodeEntered)
            required
            Field Mapping uses symbolic key
                related.VendorGroup    = Company.VendorGroup
                related.Vendor         = PayVendor
                related.VendorLocation = RemitToCode

		CancelledPOInvoiceRel
			one-to-many relation to PayablesInvoiceHistory 
			Field Mapping uses symbolic key 
				related.Company 						= Company
				related.Vendor 							= Vendor
			Instance Selection
                where (related.PayablesInvoiceHistory.Invoice = PayablesInvoiceHistory.Invoice
				and   (related.PayablesInvoiceHistory.Suffix	= 888
				or     related.PayablesInvoiceHistory.Suffix	= 999))

        MatchPurchaseOrderInvoiceHistoryRel
            classic name is MAAOIHIST
            one-to-many relation to MatchPurchaseOrderInvoiceHistory
            delete restricted
            Field Mapping uses Set2
                related.Company                                 = Company
                related.Vendor  								= Vendor
            Instance Selection
                where (related.PayablesInvoice.Invoice = PayablesInvoiceHistory.Invoice
                and    related.PayablesInvoice.Suffix  = PayablesInvoiceHistory.Suffix)

        PayablesInvoiceAddOnChargeHistoryRel
            classic name is MAMAAHIST
            one-to-many relation to PayablesInvoiceAddOnChargeHistory
            Field Mapping uses Set1
                related.Company = Company
                related.Vendor  = Vendor
                related.Invoice = PayablesInvoiceHistory.Invoice
                related.Suffix  = PayablesInvoiceHistory.Suffix
            Instance Selection
            	where (related.PurchaseOrder	= PurchaseOrder)




        PayablesInvoiceAddOnChargeHistory2Rel
            classic name is MAMAAHIST-2
            one-to-many relation to PayablesInvoiceAddOnChargeHistory
            Field Mapping uses Set1
                related.Company = Company
                related.Vendor  = Vendor
                related.Invoice = PayablesInvoiceHistory.Invoice
                related.Suffix  = PayablesInvoiceHistory.Suffix














        PayablesInvoiceDetailHistory2Rel
            classic name is MAMADHIST-2
            one-to-many relation to PayablesInvoiceDetailHistory
            Field Mapping uses Set1
                related.Company			= Company
				related.PayablesInvoice	= PayablesInvoiceNumber		
                related.Vendor			= Vendor
            Instance Selection
                where (related.PayablesInvoice.Invoice = PayablesInvoiceHistory.Invoice
                and    related.PayablesInvoice.Suffix  = PayablesInvoiceHistory.Suffix)

        PayablesInvoiceHistoryRel
            classic name is MATCH-TRANS
            one-to-many relation to PayablesInvoiceHistory
            Field Mapping uses Set13
                related.Company       = Company
                related.Vendor        = Vendor
                related.MatchObjectID = MatchObjectID









        PurchaseOrderReceiptLineRel
            classic name is PORECLINE
            one-to-many relation to PurchaseOrderReceiptLine
            Field Mapping uses Set11
                related.Company       = Company
                related.MatchObjectID = MatchObjectID

        PayVendorRel
            classic name is APPAYVENMAST
            one-to-one relation to Vendor
            required
            Field Mapping uses symbolic key
                related.VendorGroup = Company.VendorGroup
                related.Vendor      = PayVendor

        VendorRel
            classic name is APVENMAST
            one-to-one relation to Vendor
            required
            Field Mapping uses symbolic key
                related.VendorGroup = Company.VendorGroup
                related.Vendor      = Vendor

		PayablesInvoiceVoucherRefRel
			one-to-one relation to PayablesInvoiceVoucherRef
			delete restricted
			Field Mapping uses symbolic key
				related.Company 		= Company
				related.PayablesInvoiceVoucherRef = PayablesInvoiceNumber
				
		InvoiceVoucherRefRel
			one-to-one relation to PayablesInvoiceVoucherRef
			Field Mapping uses symbolic key
				related.Company 					= Company
				related.PayablesInvoiceVoucherRef 	= PayablesInvoiceNumber
		            	
        PayablesInvoicePaymentHistoryNotPurgeRel
            one-to-many relation to PayablesInvoicePaymentHistory
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.Vendor  				= Vendor
                related.PayablesInvoiceHistory	= PayablesInvoiceHistory
            Instance Selection
            	where (related.CheckDate > LocalInvoiceCutOffDate)
                
        PayablesInvoiceDistributionHistoryNotPurgeRel
			one-to-many relation to PayablesInvoiceDistributionHistory
            Field Mapping uses Set1
            	related.Company					= Company
            	related.PayablesInvoiceHistory	= PayablesInvoiceHistory
            Instance Selection
                where (related.DistributionDate > LocalInvoiceCutOffDate)
                
        MatchPurchaseOrderInvoiceHistoryPurgeRel
            one-to-many relation to MatchPurchaseOrderInvoiceHistory
            Field Mapping uses Set2
            	related.Company					= Company
                related.Vendor  				= Vendor
            Instance Selection
                where (related.PayablesInvoice	= PayablesInvoiceNumber)
                
        PayablesInvoiceDetailHistoryRel
        	one-to-many relation to PayablesInvoiceDetailHistory
            Field Mapping uses Set1
            	related.Company 					= Company
            	related.PayablesInvoice				= PayablesInvoiceNumber
                related.Vendor						= Vendor
                
        PayablesInvoiceAddOnChargeHistoryPurgeRel
        	one-to-many relation to PayablesInvoiceAddOnChargeHistory
            Field Mapping uses Set1
                related.Company 			= Company
                related.Vendor				= Vendor
                related.Invoice				= PayablesInvoiceHistory.Invoice
                related.Suffix				= PayablesInvoiceHistory.Suffix
                
        PayablesInvoiceSpreadAddOnChargeHistoryPurgeRel
			one-to-many relation to PayablesInvoiceSpreadAddOnChargeHistory
            Field Mapping uses symbolic key
                related.Company 					= Company
                related.Vendor  					= Vendor
                related.PayablesInvoiceHistory     	= PayablesInvoiceHistory.Invoice
                
        PurchaseOrderReceiptInvoiceLinkPurgeRel
        	one-to-many relation to PurchaseOrderReceiptInvoiceLink
			Field Mapping uses Set2
				related.Company 											= Company
				related.PurchaseOrderReceiptInvoiceLink.MatchObjectID		= MatchObjectID
			Instance Selection
				where  (related.Vendor 										= Vendor)
				
		MatchReconQueueRel
			one-to-one relation to MatchReconQueue
			Field Mapping uses ByVendor
				related.Vendor								= Vendor
				related.Company 							= Company
				related.PayablesInvoice						= PayablesInvoiceNumber
				
		MatchInvoiceReferenceRel
			one-to-many relation to MatchInvoiceReference
			Field Mapping uses Set2
				related.Company								= Company
				related.Vendor								= Vendor
				related.OriginalInvoice						= PayablesInvoiceNumber
                
        PayablesInvoiceHistoryCommentsRel
        	one-to-many relation to PayablesInvoiceHistoryComment
        	Field Mapping uses symbolic key
        		related.Company							= Company
        		related.Vendor							= Vendor
        		related.PayablesInvoiceHistory			= PayablesInvoiceHistory

    Sets
        Set10
            indexed
            Sort Order
                Company
                ReconciliationDate
                AccrualCode
                InvoiceCurrency
                Vendor
                PayablesInvoiceHistory
                Status












        Set12
            indexed
            Instance Selection
                where (IsCreditMemo)
            Sort Order
                Company
                PrintChargeback
                Vendor
                InvoiceDate
                PayablesInvoiceHistory

        Set13
            indexed
            Sort Order
                Company
                Vendor
                MatchObjectID
                PayablesInvoiceHistory

        Set3
            indexed
            Sort Order
                Status
                Company
                BatchNumber
                AuthorityCode
                Operator
                CreateDate
                Vendor
                PayablesInvoiceHistory

        Set4
            indexed
            Sort Order
                Company
                VoucherNumber
                Status
                Vendor
                PayablesInvoiceHistory

        Set5
            indexed
            Sort Order
                Vendor
                PayablesInvoiceHistory
                Company

        Set6
            indexed
            Sort Order
                Company
                Vendor
                InvoiceDate
                PayablesInvoiceHistory

        Set9
            indexed
            Sort Order
                Company
                Vendor
                PurchaseOrder
                Location
                PayablesInvoiceHistory

		ByPayablesInvoiceNumber
			Sort Order
				Company
				PayablesInvoiceNumber
				PayablesInvoiceHistory
				
	Rule Blocks
		PurgeInvoiceHistoryData				
			invoke Purge PayablesInvoicePaymentHistoryRel
			invoke Purge PayablesInvoiceDistributionHistoryRel
			invoke Purge PayablesCreditMemoHistoryRel
			invoke Purge PayablesInvoiceHistoryCommentsRel
			invoke Purge MatchPurchaseOrderInvoiceHistoryPurgeRel
			invoke Purge PayablesInvoiceDetailHistoryRel
			invoke Purge PayablesInvoiceAddOnChargeHistory2Rel
			invoke Purge PayablesInvoiceSpreadAddOnChargeHistoryRel
			invoke Purge PurchaseOrderReceiptInvoiceLinkPurgeRel
			invoke Purge MatchReconQueueRel
			invoke Purge MatchInvoiceReferenceRel
			invoke Purge

    Field Rules
        PayGroup
            required

        VoucherNumber
            required

		CurrencyTable
			required
			
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Unarchive is an Instance Action			
			Action Rules
				invoke CreateWithBypassRules PayablesInvoice		
					fill in fields from this instance
					invoked.PayablesInvoice	= PayablesInvoiceNumber
					invoked.Invoice			= PayablesInvoiceHistory.Invoice
					invoked.Suffix			= PayablesInvoiceHistory.Suffix
					invoked.CancelSequence	= PayablesInvoiceHistory.CancelSequence
					invoked.CreatedBy 		= actor
				for each PayablesInvoiceDistributionHistoryRel			
					if (each.PayablesAssetDetailHistoryRel exists)		
						invoke Create PayablesAssetDetail					
							fill in fields from each.PayablesAssetDetailHistoryRel
						invoke Delete each.PayablesAssetDetailHistoryRel
					invoke Create PayablesInvoiceDistribution				
						fill in fields from each
						invoked.Invoice			= PayablesInvoiceHistory.Invoice
						invoked.Suffix			= PayablesInvoiceHistory.Suffix
						invoked.CancelSequence	= PayablesInvoiceHistory.CancelSequence
						invoked.PayablesInvoice	= PayablesInvoiceNumber
						invoked.CreatedBy = actor
					invoke Delete each
				for each PayablesInvoiceDetailHistory2Rel				
					invoke Create PayablesInvoiceDetail						
						fill in fields from each
						invoked.PayablesInvoice	= PayablesInvoiceNumber
						invoked.CreatedBy = actor
					invoke Delete each
				for each PayablesInvoiceAddOnChargeHistory2Rel			
					invoke Create PayablesInvoiceAddOnCharge				
						fill in fields from each
						invoked.PayablesInvoice	= PayablesInvoiceNumber
					invoke Delete each
				for each MatchPurchaseOrderInvoiceHistoryRel			
					invoke Create MatchPurchaseOrderInvoice					
						fill in fields from each
						invoked.PayablesInvoice	= PayablesInvoiceNumber
					invoke Delete each
				for each PayablesInvoiceSpreadAddOnChargeHistoryRel		
					invoke Create PayablesInvoiceSpreadAddOnCharge			
						fill in fields from each
						invoked.PayablesInvoice = PayablesInvoiceNumber
					invoke Delete each
				for each PayablesCreditMemoHistoryRel					
					invoke Create PayablesCreditMemo						
						fill in fields from each
					invoke Delete each
				for each PayablesInvoiceDiscountHistoryRel				
					invoke Create PayablesInvoiceDiscount					
						fill in fields from each
					invoke Delete each


				for each PayablesInvoicePaymentHistoryRel				
					invoke Create PayablesInvoicePayment					
						fill in fields from each
						invoked.PayablesInvoice	= PayablesInvoiceNumber
						invoked.CreatedBy = actor
					invoke Delete each
				invoke Delete
				
		CreatePayablesInvoiceVoucherRef is a Set Action
			restricted
			Parameters
				PrmCompany 		  	 is a PayablesCompany
				PrmPeriodStartDate 	 is Date  
				PrmPeriodEndDate     is Date





			Instance Selection		
				where (Company 			 = PrmCompany
				and    Status.Historical
				and   (DistributionDate >= PrmPeriodStartDate
				and    DistributionDate <= PrmPeriodEndDate)
				and    PayablesInvoiceVoucherRefRel not exists)
								
			Action Rules						
				Instance Rules
					invoke Create PayablesInvoiceVoucherRef
						fill in fields from this instance
						invoked.PayablesInvoiceVoucherRef = PayablesInvoiceNumber
						invoked.Invoice				   	  = PayablesInvoiceHistory.Invoice					
						invoked.VoucherReferenceNumber    = blank	
						
		PurgeInvoiceHistory is a Set Action
			restricted
			Parameters
				PrmCompany					is a PayablesCompany
				PrmFromVendorInv			is a Vendor
				PrmToVendorInv				is a Vendor
				PrmInvCutoffDate			is Date
				
			Parameter Rules
				PrmCompany
					required
				PrmInvCutoffDate
					required
			
			Local Fields
				InvoiceDateYear is Year
				LocalSkip		is Boolean
				CurrentDate		is Date
			
			Instance Selection
				include deleted records
				where (Company = PrmCompany
				and   ((PrmFromVendorInv not entered and PrmToVendorInv not entered)
				or    (PrmFromVendorInv entered and PrmToVendorInv not entered and Vendor >= PrmFromVendorInv)
				or    (PrmFromVendorInv not entered and PrmToVendorInv entered and Vendor <= PrmToVendorInv)
				or    (PrmFromVendorInv entered and PrmToVendorInv entered and Vendor >= PrmFromVendorInv and Vendor <= PrmToVendorInv))
				and   InvoiceDate <= PrmInvCutoffDate 
				and   (Status = 6 or Status = 9))
				
			Action Rules
				Instance Rules
					initialize LocalSkip
					LocalInvoiceCutOffDate = PrmInvCutoffDate
					
					if (IncomeCode entered)
						InvoiceDateYear	= InvoiceDate year
						CurrentDate		= current corporate date
						if (InvoiceDateYear = CurrentDate year)
							LocalSkip	= true
					
					if (!LocalSkip
					and PayablesInvoicePaymentHistoryNotPurgeRel not exists
					and PayablesInvoiceDistributionHistoryNotPurgeRel not exists
					and InvoiceVoucherRefRel not exists)
						include PurgeInvoiceHistoryData
					
		PurgeOneTimeInvoiceHistory is a Set Action
			restricted
			Parameters
				PrmCompany	is a PayablesCompany
				PrmVendor	is a Vendor
				
			Instance Selection
				include deleted records
				where	(Company	= PrmCompany
				and		Vendor 		= PrmVendor
				and		Status		= 9)
			Action Rules
				Instance Rules
					if (InvoiceVoucherRefRel not exists)
						include PurgeInvoiceHistoryData
						
				
		Purge is a Purge Action
			restricted
			bypass relational integrity rules	
				
