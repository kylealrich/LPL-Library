DailyCalendarPeriod is a BusinessClass
	default label is "DailyCalendarPeriod"
	owned by ad
	representative text is "<DerivedRepresentativeText>"
	
	prefix is DCPD
	
	Ontology
		symbolic key is DailyCalendarPeriod
			
	Patterns
		implements ChildHierarchy
			children are ChildrenRel		
			child set is HierarchyDetailRel         
				child is SubordinatePeriod 
				aggregation percent is Weight
			top node when (IsTopNode)

	Context Fields		
		GeneralLedgerCalendar
		GeneralLedgerCalendarYear
		ParentCalendarYearMatch	
		GeneralLedgerCloseConfiguration
		GeneralLedgerCloseYear
		ReportingBasis
		SkipShadow
		AccountingEntity
		CreatingWeeksWithinMonths
		AnalysisYear	  
		FromSummaryPeriod
		InvokedByCalendar
		
	Persistent Fields
		DisplayPeriod		is Alpha 30
			translatable
		PeriodType			is Numeric 1
			States
				Year		     value is 1
				Quarter		     value is 2
				Month		     value is 3
				Week		     value is 4
				Day				 value is 8
				EndDate		     value is 5
				BeginningBalance value is 6
				TopNode          value is 7
				 
		Year				is a GeneralLedgerCalendarYear 
		Date				is Date    
		Quarter				is Numeric 1
			States
				Quarter1	value is 1
				Quarter2	value is 2
				Quarter3	value is 3
				Quarter4	value is 4
		Month				is Numeric 2
			States
				Period1		value is 1
				Period2		value is 2
				Period3     value is 3
				Period4		value is 4
				Period5		value is 5
				Period6		value is 6
				Period7		value is 7
				Period8		value is 8
				Period9		value is 9
				Period10	value is 10
				Period11	value is 11
				Period12	value is 12
				Period13	value is 13
		Week				is Numeric 2
			States
				Week1		value is 1
				Week2		value is 2
				Week3		value is 3
				Week4		value is 4
				Week5		value is 5
				Week6		value is 6	
				Week7		value is 7
				Week8		value is 8
				Week9		value is 9
				Week10		value is 10
				Week11		value is 11
				Week12		value is 12
				Week13		value is 13
				Week14		value is 14
				Week15		value is 15
				Week16		value is 16
				Week17		value is 17
				Week18		value is 18
				Week19		value is 19
				Week20		value is 20
				Week21		value is 21
				Week22		value is 22
				Week23		value is 23
				Week24		value is 24
				Week25		value is 25
				Week26		value is 26
				Week27		value is 27
				Week28		value is 28
				Week29		value is 29
				Week30		value is 30
				Week31		value is 31
				Week32		value is 32
				Week33		value is 33
				Week34		value is 34
				Week35		value is 35
				Week36		value is 36
				Week37		value is 37
				Week38		value is 38
				Week39		value is 39
				Week40		value is 40
				Week41		value is 41
				Week42		value is 42
				Week43		value is 43
				Week44		value is 44
				Week45		value is 45
				Week46		value is 46
				Week47		value is 47
				Week48		value is 48
				Week49		value is 49
				Week50		value is 50
				Week51		value is 51
				Week52		value is 52
				Week53		value is 53
		Day					 is Numeric 3 
		TopNode				 is AlphaUpper 20
		CalendarTopNode		 is AlphaUpper 20 
		IsTopNode			 is Boolean	  
		StartDateJulian		 is AlphaUpper 7
		EndDateJulian		 is AlphaUpper 7
				
	Transient Fields
		CreatePeriodType	is Numeric 1
			States
				Year		 value is 1
				EndDate		 value is 5			
		NewPeriod				is AlphaUpper 20
		CreateEndDate			is Date			
		ParentPeriod			is AlphaUpper 20
		IsClosePeriod			is Boolean
		YearStartDate           is Date
		YearEndDate             is Date
		TransientStartDateJulian	 is AlphaUpper 7
		TransientEndDateJulian		 is AlphaUpper 7
						
	Local Fields
		LocalTopNode			is AlphaUpper 20
		LocalSummaryPeriod		is a DailyCalendarPeriod
		LocalSubordinatePeriod	is a DailyCalendarPeriod
		LocalParentPeriod		is a DailyCalendarPeriod
		LocalYear				is Numeric 4
		LocalDay				is Alpha 3
		LocalDayNumeric			is Numeric 3
		LocalDayCounter			is Numeric 3
		LocalDate				is Date
		LocalCounter			is Numeric 3
		I1						is Numeric 3
		I2						is Numeric 3
		LocalAlphaYear			is Alpha 4
		LocalAlphaDay			is Alpha 3
		LocalMonthNumeric		is Numeric 2
		LocalMonthCounter		is Numeric 2
		LocalJulian				is Numeric 7
		LocalPeriodType			is Alpha 1
		LocalDateAlpha			is Alpha 8

	Derived Fields
		DerivedNumericQuarter is a DerivedField
			type is Numeric 1
			return Quarter
			
		DerivedNumericMonth is a DerivedField
			type is Numeric 2
			return Month
			
		DerivedNumericWeek is a DerivedField
			type is Numeric 2
			return Week
			
		DerivedNumericDay is a DerivedField
			type is Numeric 3
			return Day

		DerivedPaddedMonth is a DerivedField
			type is Alpha 2
			LocalMonthNumeric = Month
			if (LocalMonthNumeric < 10)
				DerivedPaddedMonth	= "0" + LocalMonthNumeric
			else
				DerivedPaddedMonth 	= LocalMonthNumeric

		DerivedPaddedDay is a DerivedField
			type is Alpha 3
			LocalDayNumeric = Day
			if (LocalDayNumeric < 10)
				DerivedPaddedDay = "00" + LocalDayNumeric
			else
			if (LocalDayNumeric < 100)
				DerivedPaddedDay = "0" + LocalDayNumeric
			else
				DerivedPaddedDay = LocalDayNumeric

		DerivedPaddedLocalDay is a DerivedField
			type is Alpha 3
			LocalDayNumeric = LocalDayCounter
			if (LocalDayNumeric < 10)
				DerivedPaddedLocalDay = "00" + LocalDayNumeric
			else
			if (LocalDayNumeric < 100)
				DerivedPaddedLocalDay = "0" + LocalDayNumeric
			else
				DerivedPaddedLocalDay = LocalDayNumeric
		DerivedPaddedLocalMonth is a DerivedField
			type is Alpha 2
			LocalMonthNumeric = LocalMonthCounter
			if (LocalMonthNumeric < 10)
				DerivedPaddedLocalMonth	= "0" + LocalMonthNumeric
			else
				DerivedPaddedLocalMonth = LocalMonthNumeric

		StartDateNumeric is a DerivedField
			type is Numeric 7
			restricted
			return StartDateJulian

		EndDateNumeric is a DerivedField
			type is Numeric 7
			restricted
			return EndDateJulian

		DerivedBeginningBalanceString is a StringField
			type is AlphaUpper 20
			restricted
			CreateEndDate year
			"000"

		DerivedBeginningBalanceForYear is a StringField
			type is AlphaUpper 20
			restricted
			Year
			"000"

		DerivedBeginningBalanceDisplayPeriod is a StringField
			type is Alpha 30
			restricted
			CreateEndDate year
			" Beginning Balance"

		PriorYearAlpha is a DerivedField
			type is Alpha 4
			restricted
			return (Year - 1)

		DerivedCurrentDate is a DerivedField
			type is Date
        	restricted
			return current corporate date
	
		DerivedCurrentYear is a DerivedField
			type is Alpha 4
        	restricted
			return DerivedCurrentDate year

		PrefixString	    	is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_"

		DerivedJulianDate is a DerivedField
			type is AlphaUpper 7
			restricted
			LocalAlphaYear = LocalDate year
			LocalAlphaDay  = LocalDate year day
			DerivedJulianDate = LocalAlphaYear
			if (LocalAlphaDay size = 1)
				DerivedJulianDate += "00"
			else
			if (LocalAlphaDay size = 2)
				DerivedJulianDate += "0"
			DerivedJulianDate += LocalAlphaDay 

		NewEndDateString is a DerivedField
			type is AlphaUpper 20
			restricted
			LocalAlphaYear = LocalDate year
			LocalAlphaDay  = LocalDate year day
			NewEndDateString = LocalAlphaYear
			if (LocalAlphaDay size = 1)
				NewEndDateString += "00"
			else
			if (LocalAlphaDay size = 2)
				NewEndDateString += "0"
			NewEndDateString += LocalAlphaDay

		NewYearString is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_"			
			Year

		NewDayString is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_"		
			Year
			"D"
			LocalDay

		DerivedPeriodType is a DerivedField
			type is Numeric 1
			return PeriodType

		DerivedStartDateJulian is a DerivedField
			type is AlphaUpper 7
			restricted
			if (PeriodType.EndDate)
				return DailyCalendarPeriod
			else
				return blank

		DerivedDisplayStartDateJulian is a DerivedField
			type is AlphaUpper 7
			restricted
			if (StartDateJulian entered)
				return StartDateJulian
			else
				return DerivedStartDateJulian

		DerivedEndDateJulian is a DerivedField
			type is AlphaUpper 7
			restricted
			if (PeriodType.EndDate)
				return DailyCalendarPeriod
			else
				return blank

		DerivedDisplayEndDateJulian is a DerivedField
			type is AlphaUpper 7
			restricted
			if (EndDateJulian entered)
				return EndDateJulian
			else
				return DerivedEndDateJulian

		DerivedStartDate is a DerivedField
			type is Date 
			restricted
			LocalDayNumeric = StartDateJulian[5:7]
			LocalDateAlpha  = StartDateJulian[1:4] + "0101" 
			LocalDate 		= LocalDateAlpha
			LocalDate 	   += (LocalDayNumeric - 1)
			return LocalDate

		DerivedEndDate is a DerivedField
			type is Date 
			restricted
			LocalDayNumeric = EndDateJulian[5:7]
			LocalDateAlpha  = EndDateJulian[1:4] + "0101" 
			LocalDate 		= LocalDateAlpha
			LocalDate 	   += (LocalDayNumeric - 1)
			return LocalDate

		DerivedRepresentativeText	is a DerivedField
			type is Alpha 120
			restricted
			if (!IsTopNode)
				return DisplayPeriod
			if (TopNode = CalendarRel.CBTopNode)
				return CalendarRel.Description + " CB Top Level"
			if (TopNode = CalendarRel.YAGTopNode)
				return CalendarRel.Description + " YAG Top Level"
			if (TopNode = CalendarRel.QAGTopNode)
				return CalendarRel.Description + " QAG Top Level"
			if (TopNode = CalendarRel.MAGTopNode)
				return CalendarRel.Description + " MAG Top Level"

		CBTopNodeString		is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_TOP_NODE_CB"

		YAGTopNodeString		is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_TOP_NODE_YAG"
		
		MAGTopNodeString		is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_TOP_NODE_MAG"

		QAGTopNodeString		is a StringField
			type is AlphaUpper 20
			restricted
			CalendarRel.CalendarSequence
			"_TOP_NODE_QAG"

		DerivedPreviousMonth is a DerivedField
			type is Numeric 2
			restricted
			return (DerivedNumericMonth - 1)

		DerivedPreviousMonthEndDateJulian is a DerivedField
			type is AlphaUpper 7
			restricted
			return last PreviousMonthPeriodsRel.EndDateJulian

		DerivedPreviousQuarter is a DerivedField
			type is Numeric 1
			restricted
			return (DerivedNumericQuarter - 1)

		DerivedPreviousQuarterEndDateJulian is a DerivedField
			type is AlphaUpper 7
			restricted
			return last PreviousQuarterPeriodsRel.EndDateJulian

		CBCrossReference is a DerivedField
			type is Alpha 30
			return FinanceEnterpriseGroup + "/" + first CurrentBalanceNodeRel.DailyCalendarPeriod

	Conditions

	Relations
		EndDatesWithinYearRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNode
				related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
				related.TopNode                     = blank
				related.Year						= Year
				related.PeriodType                  = PeriodType.EndDate
			Instance Selection
				where (related.StartDateNumeric >= StartDateNumeric 
				and    related.EndDateNumeric <= EndDateNumeric)

		ChildrenRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				related.DailyCalendarPeriod		= HierarchyDetailRel.SubordinatePeriod		
		HierarchyDetailRel
			one-to-many relation to DailyPeriodHierarchy
			Field Mapping uses ByParent
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DailyCalendarPeriod		= DailyCalendarPeriod
		ParentPeriodRel
			one-to-one relation to DailyPeriodHierarchy
			Field Mapping uses ByParent
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DailyCalendarPeriod		= LocalParentPeriod
				related.SubordinatePeriod		= LocalSubordinatePeriod
		ParentPeriodInTopNodeRel
			one-to-one relation to DailyPeriodHierarchy
			Field Mapping uses ByParentInTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TopNode					= LocalTopNode
				related.DailyCalendarPeriod		= LocalParentPeriod
				related.SubordinatePeriod		= LocalSubordinatePeriod
		SubordinatePeriodRel
			one-to-one relation to DailyPeriodHierarchy
			Field Mapping uses BySubordinate
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.SubordinatePeriod		= LocalSubordinatePeriod
				related.DailyCalendarPeriod		= LocalParentPeriod
		SubordinateInTopNodeRel
			one-to-one relation to DailyPeriodHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TopNode					= LocalTopNode
				related.SubordinatePeriod		= LocalSubordinatePeriod
				related.DailyCalendarPeriod		= LocalParentPeriod

		CalendarRel
			one-to-one relation to GeneralLedgerCalendar
			Field Mapping uses ByTopNode
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode				   = CalendarTopNode

		BeginningBalanceRel
			one-to-many relation to DailyCalendarPeriod 
			Field Mapping uses ByTopNode
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode                = blank
				related.Year                   = Year
				related.PeriodType             = PeriodType.BeginningBalance

		PreviousMonthPeriodsRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNode
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode                = TopNode
				related.Year                   = Year
			Instance Selection
				where (related.Month = DerivedPreviousMonth)

		MonthBeginningBalanceRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode                = CalendarRel.CBTopNode
				related.PeriodType             = PeriodType.Day
				related.EndDateJulian          = DerivedPreviousMonthEndDateJulian

		PreviousQuarterPeriodsRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNode
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode                = TopNode
				related.Year                   = Year
			Instance Selection
				where (related.Quarter = DerivedPreviousQuarter)

		QuarterBeginningBalanceRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode                = CalendarRel.CBTopNode
				related.PeriodType             = PeriodType.Day
				related.EndDateJulian          = DerivedPreviousQuarterEndDateJulian
		CBPeriodByJulianDatesRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TopNode                = CalendarRel.CBTopNode
				related.PeriodType             = PeriodType.Day
				related.EndDateJulian          = TransientEndDateJulian
				related.StartDateJulian        = TransientStartDateJulian
		CurrentBalanceNodeRel
			one-to-many relation to DailyCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
				related.TopNode                     = CalendarRel.CBTopNode
				related.PeriodType                  = PeriodType.Day
				related.EndDateJulian				= EndDateNumeric

	Sets				
	
		ByTopNode
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				Year
				PeriodType
				DailyCalendarPeriod	
				
		LeafRecords
			Sort Order
				FinanceEnterpriseGroup
				DailyCalendarPeriod
			Instance Selection
				where (PeriodType.EndDate)
		
		ByTopNodeAndType
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				PeriodType
				DailyCalendarPeriod
		
		ByTopNodeTypeAndJulianDates
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				PeriodType
				EndDateJulian
				StartDateJulian
				DailyCalendarPeriod

	Field Rules
	
		PeriodType
			required
			cannot be changed
		
		Date
			if (PeriodType.EndDate)
				required
			cannot be changed
			
		Year
			if (!PeriodType.TopNode)
				required
			if (old Year entered)
				cannot be changed
			
		Quarter
			if (PeriodType.Quarter)
				required
			cannot be changed
			
		Month
			if (PeriodType.Month)
				required
			cannot be changed
			
		Week
			if (PeriodType.Week)
				required
			cannot be changed
			
		Day	
			if (PeriodType.Day)
				required
			cannot be changed
						
		TopNode
			if (PeriodType.TopNode)
				required
			cannot be changed
						


	Rule Blocks


	Actions
		Create is a Create Action
			restricted
			Exit Rules

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted
			bypass relational integrity rules

		CreateBeginningBalance is a Create Action
			restricted
    		Action Rules
        		PeriodType = PeriodType.BeginningBalance

		CreateTopNode is a Create Action
			restricted
			Action Rules
				PeriodType  = PeriodType.TopNode
				IsTopNode   = true

		CreateEndDate is a Create Action
			restricted
			Action Rules
				PeriodType = PeriodType.EndDate
				IsTopNode  = false
				TopNode    = blank
				Quarter    = blank
				Month      = blank
				Week       = blank
				Day        = blank

		CreateCBSummaryNode is a Create Action
			restricted
			Action Rules
				PeriodType = PeriodType.Day
				Quarter    = blank
				Month      = blank
				Week       = blank

			Exit Rules

				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup              = FinanceEnterpriseGroup												
					invoked.DailyCalendarPeriod        			= TopNode														
					invoked.SubordinatePeriod                   = DailyCalendarPeriod														
					invoked.TopNode                             = TopNode
					invoked.Weight			                  	= 1			


				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup              = FinanceEnterpriseGroup												
					invoked.DailyCalendarPeriod        			= DailyCalendarPeriod
					invoked.SubordinatePeriod                   = BeginningBalanceRel.DailyCalendarPeriod														
					invoked.TopNode                             = TopNode
					invoked.Weight			                  	= 1		

		CreateMAGSummaryNode is a Create Action
			restricted
			Local Fields
				LocalPreviousMonthEndDate   is Date
			Action Rules
				PeriodType = PeriodType.Day
				Quarter    = blank
				Week       = blank

			Exit Rules

				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.DailyCalendarPeriod    = TopNode
					invoked.SubordinatePeriod      = DailyCalendarPeriod
					invoked.TopNode                = TopNode
					invoked.Weight                 = 1


				initialize LocalSubordinatePeriod
				if (Month = 1)
					LocalSubordinatePeriod = BeginningBalanceRel.DailyCalendarPeriod	
				else
					LocalPreviousMonthEndDate = (DerivedStartDate - 1)
					LocalDate                 = LocalPreviousMonthEndDate
					TransientEndDateJulian    = NewEndDateString
					LocalSubordinatePeriod    = first CBPeriodByJulianDatesRel.DailyCalendarPeriod
				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.DailyCalendarPeriod    = DailyCalendarPeriod
					invoked.TopNode                = TopNode
					invoked.Weight                 = Day
					invoked.SubordinatePeriod      = LocalSubordinatePeriod											

		CreateQAGSummaryNode is a Create Action
			restricted
			Action Rules
				PeriodType = PeriodType.Day
				Month      = blank
				Week       = blank

			Exit Rules

				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup              = FinanceEnterpriseGroup												
					invoked.DailyCalendarPeriod        			= TopNode														
					invoked.SubordinatePeriod                   = DailyCalendarPeriod														
					invoked.TopNode                             = TopNode
					invoked.Weight			                  	= 1			


				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup          	= FinanceEnterpriseGroup												
					invoked.DailyCalendarPeriod        			= DailyCalendarPeriod
					if (Quarter = 1)
						invoked.SubordinatePeriod           	= BeginningBalanceRel.DailyCalendarPeriod
					else														
						invoked.SubordinatePeriod           	= first QuarterBeginningBalanceRel.DailyCalendarPeriod														
					invoked.TopNode                         	= TopNode
					invoked.Weight			               		= Day

		CreateYAGSummaryNode is a Create Action
			restricted
			Action Rules
				PeriodType = PeriodType.Day
				Quarter    = blank
				Month      = blank
				Week       = blank

			Exit Rules

				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup              = FinanceEnterpriseGroup												
					invoked.DailyCalendarPeriod        			= TopNode														
					invoked.SubordinatePeriod                   = DailyCalendarPeriod														
					invoked.TopNode                             = TopNode
					invoked.Weight			                  	= 1			


				invoke Create DailyPeriodHierarchy
					invoked.FinanceEnterpriseGroup              = FinanceEnterpriseGroup												
					invoked.DailyCalendarPeriod        			= DailyCalendarPeriod
					invoked.SubordinatePeriod                   = BeginningBalanceRel.DailyCalendarPeriod														
					invoked.TopNode                             = TopNode
					invoked.Weight			                  	= Day

		AttachEndDatesToDailyStructure is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmCalendar					is a GeneralLedgerCalendar
					default label is "Calendar"
				PrmYear						is a GeneralLedgerCalendarYear
					default label is "Year"
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmCalendar
					required

			Local Fields
				LocalEndDate			is Numeric size 7
				LocalEndDatePeriod 		is a DailyCalendarPeriod

			Instance Selection
				where (PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
				and    PrmCalendar.TopNode       = CalendarTopNode
				and    PrmYear                   = Year
				and    PeriodType.Day)

			Sort Order	
				FinanceEnterpriseGroup
				TopNode
				Year
				PeriodType
				DailyCalendarPeriod
			
			Accumulators
				Counter

			Action Rules
				Set Rules
					Exit Rules
				TopNode Set Rules
					Entrance Rules
						
				Instance Rules
					initialize LocalEndDatePeriod
					initialize LocalDate
					initialize Counter

					if (PrmCalendar.CBTopNode     = TopNode 
					or  PrmCalendar.YAGTopNode    = TopNode
					or  PrmCalendar.MAGTopNode    = TopNode 
					or  PrmCalendar.QAGTopNode    = TopNode)
						
						LocalEndDate = StartDateNumeric
						Counter 	 = 1

						while (LocalEndDate <= EndDateNumeric)
							invoke Create DailyPeriodHierarchy
								invoked.FinanceEnterpriseGroup          = FinanceEnterpriseGroup												
								invoked.DailyCalendarPeriod        		= DailyCalendarPeriod														
								invoked.SubordinatePeriod               = LocalEndDate														
								invoked.TopNode                         = TopNode
								if (TopNode = CalendarRel.CBTopNode)
									invoked.Weight			            = 1
								else
									invoked.Weight			            = (Day - Counter + 1)
							
							LocalEndDatePeriod = LocalEndDate
							LocalDate 		   = LocalEndDatePeriod.Date
							if (LocalDate year day = LocalDate days in year)

								LocalEndDate = (((LocalDate year + 1) * 1000) + 1)
							else
								LocalEndDate += 1
							Counter += 1

		PurgeDayPeriodType is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmCalendarTopNode				is a GeneralLedgerCalendarPeriod
					default label is "TopNode"
				PrmYear							is a GeneralLedgerCalendarYear
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and    CalendarTopNode		    = PrmCalendarTopNode
				and    Year                     = PrmYear
				and    PeriodType.Day)
			
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				Year
				PeriodType
				DailyCalendarPeriod	

			Action Rules
				Instance Rules
					LocalTopNode			= TopNode
					LocalParentPeriod 		= TopNode
					LocalSubordinatePeriod 	= DailyCalendarPeriod
					invoke Purge ParentPeriodInTopNodeRel

					for each HierarchyDetailRel
						invoke Purge each
		
					invoke Purge

		PurgeData is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup)
			Action Rules
				Instance Rules
					invoke Purge
