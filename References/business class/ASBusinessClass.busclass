ASBusinessClass is a BusinessClass
	default label is "BusinessClass"
    owned by appsetup
    prefix is ASBCL
        
    Ontology
     	symbolic key is ASBusinessClass
    	
    Patterns
    	implements LightweightAuditing
    	disable EffectiveDated
    	disable AuditIndex
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder		

    Persistent Fields
        Description     is Alpha size 100
        BusinessClass
        CreateAction    is a BusinessAction
        UpdateAction    is a BusinessAction
        Inactive      	is Boolean
        DisplayOrder    is Numeric 5
        	disable Auditing
        RecordSelection is LPLText
        	is condition for BusinessClass
        FieldSelection is Alpha 1
        	States
        		AllFields     value is blank
        		IncludeFields value is "I"
        		ExcludeFields value is "E"

		BaseBusinessClass	is like ASBusinessClass 
		CreateRetroVersion 		is Boolean
		DefaultEffectiveDate	is Date

    Transient Fields
		NewDisplayOrder is Numeric 5

	Local Fields
		CopiedDependentBC 		is like ASBusinessClass
		CopiedRelatedModBase	is like ASModule
		CopiedRelatedBCBase		is like ASBusinessClass
		CopiedRelatedMod		is like ASModule
		CopiedRelatedBC 		is like ASBusinessClass
		CopedConfigurationBase 	is like ASConfiguration
		LocalASBusinessClass 	is a ASBusinessClass
		LocalASBCField			is like BusinessField
		CopiedRelatedLink is Alpha size 200
			is related link for ASBusinessClass.BusinessClass

	Derived Fields
		BusClassJSON is a NativeField
			type is JSONObject
		
		CopyMessage is a MessageField
			"Copy:_"
	
	Field Rules
		Description
			if (Description not entered)
				default to BusinessClass
			else 
				if (!ASConfiguration.CanEditBaseFields)
					cannot be changed
						"DeliveredConfigurationsCannotBeChanged."
				
		BusinessClass
			required
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."
			
		DisplayOrder
			default to ASBusinessClass
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

        CreateAction
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

        UpdateAction
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

        RecordSelection
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

        FieldSelection
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

		BaseBusinessClass
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

		CreateRetroVersion
			if (!ASConfiguration.CanEditBaseFields)
				cannot be changed
					"DeliveredConfigurationsCannotBeChanged."

		DefaultEffectiveDate
			if (DefaultEffectiveDate changed)
				if (ASConfiguration.WarnOnDeliveredDefaultEffectiveDate)
					confirmation required
						"ThisChangeWillBeOverwrittenTheNextTimeThisDeliveredConfigurationIsUpdated.Proceed?"
		
		Inactive
			if (Inactive changed)
				if (ASConfiguration.WarnOnDeliveredInactiveSetting)
					confirmation required
						"ThisChangeWillBeOverwrittenTheNextTimeThisDeliveredConfigurationIsUpdated.Proceed?"

	
	Relations
		LocalBusinessFieldsRel
			one-to-many relation to BusinessField
			Field Mapping uses symbolic key
				related.BusinessView		= LocalASBusinessClass.BusinessClass
			Instance Selection
				where (related.Type.Persistent
				and    related.BusinessField != "UniqueID")

		LocalASBCFieldRel
			one-to-many relation to ASBCField
			Field Mapping uses symbolic key
				related.ASConfiguration 	= ASConfiguration
				related.ASModule 			= ASModule
				related.ASBusinessClass 	= LocalASBusinessClass
			Instance Selection
				where (related.FieldName	= LocalASBCField)
		
		CheckBCsRelatedToThisBCRel
            one-to-many relation to ASBusinessClass
            Field Mapping uses symbolic key
                related.ASConfiguration     = ASConfiguration
            Instance Selection
				where (related.HasRelatedBCs) 
		
		CheckBCsDependingOnThisBCRel
            one-to-many relation to ASBusinessClass
            Field Mapping uses symbolic key
                related.ASConfiguration     = ASConfiguration
				related.ASModule			= ASModule
            Instance Selection
				where (related.HasDependentBCs) 

		FieldsByDisplayOrderRel
			one-to-many relation to ASBCField
			Field Mapping uses ByDisplayOrder
                related.ASConfiguration			= ASConfiguration
                related.ASModule				= ASModule
				related.ASBusinessClass			= ASBusinessClass	

		BaseBCRel
			one-to-one relation to ASBusinessClass
			Field Mapping uses symbolic key
				related.ASConfiguration 	= CopedConfigurationBase
				related.ASModule			= ASModule.BaseModule
				related.ASBusinessClass 	= BaseBusinessClass

		BaseBCRelatedBCsRel
			one-to-many relation to ASRelatedBusinessClass
			Field Mapping uses symbolic key
				related.ASConfiguration 	= CopedConfigurationBase
				related.ASModule			= ASModule.BaseModule
				related.ASBusinessClass		= BaseBusinessClass

		BaseBCDependenciesRel
			one-to-many relation to ASDependentBC
			Field Mapping uses part of key
				related.ASConfiguration 	= CopedConfigurationBase
				related.ASModule			= ASModule.BaseModule
				related.ASBusinessClass		= BaseBusinessClass
		
		DependencyNewBusinessClassRel
			one-to-many relation to ASBusinessClass
			Field Mapping uses ByBaseBC
				related.ASConfiguration 	= ASConfiguration
				related.ASModule			= ASModule
				related.BaseBusinessClass	= CopiedDependentBC
		
		RelatedBCModByBaseModRel
			one-to-many relation to ASModule
			Field Mapping uses ByBaseModule
				related.ASConfiguration 	= ASConfiguration
				related.BaseModule			= CopiedRelatedModBase
		
		RelatedBCByBaseBCRel
			one-to-many relation to ASBusinessClass
			Field Mapping uses ByBaseBC
				related.ASConfiguration 	= ASConfiguration
				related.ASModule			= CopiedRelatedMod
				related.BaseBusinessClass 	= CopiedRelatedBCBase
			
	
	Sets
		ByDisplayOrder
			Sort Order
				ASConfiguration
				ASModule
				DisplayOrder
				ASBusinessClass

		ByBaseBC
			duplicates
			Sort Order 
				ASConfiguration
				ASModule
				BaseBusinessClass
				
		ByBusinessClass
			Sort Order
				BusinessClass
				ASConfiguration
				ASModule
				ASBusinessClass
	
	Conditions
		HasDependentBCs
			default label is untranslatable
			when (ASDependentBC set exists)

		HasBaseBC
			default label is untranslatable
			when (BaseBCRel exists)
		
		IsChangeable
			default label is untranslatable
			when (ASConfiguration.CanEditBaseFields and (!HasBaseBC or !ASConfiguration.IsBasedFromDelivered))
		
		HasRelatedBCs
			when (ASRelatedBusinessClass(ASBusinessClass) set exists)

	Actions
		IncludeAllFields is an Instance Action
			Parameters
				ASConfiguration
				ASModule
				ASBusinessClass

			Parameter Rules
				ASConfiguration
					required
				ASModule
					required
				ASBusinessClass
					required
					
			Action Rules

				LocalASBusinessClass 	= ASBusinessClass

				for each LocalBusinessFieldsRel

					LocalASBCField = each.BusinessField 

					if (LocalASBCFieldRel not exists)
						invoke Create ASBCField
							invoked.ASConfiguration 	= ASConfiguration
							invoked.ASModule 			= ASModule
							invoked.ASBusinessClass 	= LocalASBusinessClass
							invoked.FieldName			= each.BusinessField
					else
						invoke Delete LocalASBCFieldRel

				invoke Update ASBusinessClass
					invoked.FieldSelection = "I"
				
				invoke Update ASBusinessClass.first FieldsByDisplayOrderRel
					invoked.NewDisplayOrder = 1
		
		Create is an Action

			Exit Rules
				if (NewDisplayOrder entered)
					invoke Update ASBusinessClass

		CreateCopy is a Create Action
			restricted

		Update is an Action				
		Delete is an Action
			valid when (IsChangeable)
			Entrance Rules
				invoke CleanDependencies

				for each CheckBCsDependingOnThisBCRel
					invoke RemovePrerequisiteBC each
						invoked.BusinessClassBeingDeleted = ASBusinessClass
				
				for each CheckBCsRelatedToThisBCRel
					invoke RemoveRelatedBC each
						invoked.ModuleOfBusinessClassBeingDeleted 	= ASModule
						invoked.BusinessClassBeingDeleted 			= ASBusinessClass

		Purge is a Purge Action
			restricted

		PurgeBusinessClass is an Instance Action
			valid when (IsChangeable)

			Entrance Rules
				if (HasDependentBCs)
					for each ASDependentBC set
						invoke Purge each
				
				if (HasRelatedBCs)
					for each ASRelatedBusinessClass(ASBusinessClass) set
						invoke Purge each
				
				for each CheckBCsDependingOnThisBCRel
					invoke PurgePrerequisiteBC each
						invoked.BusinessClassBeingPurged = ASBusinessClass
				
				for each CheckBCsRelatedToThisBCRel
					invoke PurgeRelatedBC each
						invoked.ModuleOfBusinessClassBeingPurged 	= ASModule
						invoked.BusinessClassBeingPurged 			= ASBusinessClass
				

			Action Rules
				for each FieldsByDisplayOrderRel
					invoke Purge each
				
				invoke Purge
		
		PurgePrerequisiteBC is an Instance Action
			restricted
			Parameters
				BusinessClassBeingPurged is like ASBusinessClass
			
			Parameter Rules
				BusinessClassBeingPurged
					required
			Action Rules
            	for each ASDependentBC set
					if (each.ASDependentBC = BusinessClassBeingPurged)
						invoke Purge each
		
		PurgeRelatedBC is an Instance Action
			restricted

			Parameters
				BusinessClassBeingPurged is like ASBusinessClass
				ModuleOfBusinessClassBeingPurged is like ASModule
			
			Parameter Rules
				BusinessClassBeingPurged
					required
				ModuleOfBusinessClassBeingPurged
					required

			Action Rules
            	for each ASRelatedBusinessClass set
					if (each.ASRelatedBusinessClass.RelatedModule = ModuleOfBusinessClassBeingPurged
					and each.ASRelatedBusinessClass.RelatedBusinessClass = BusinessClassBeingPurged)
						invoke Purge each

		
		Copy is an Instance Action
			Action Rules	
				invoke InternalCopy
					invoked.NewConfiguration 	= ASConfiguration
					invoked.NewModule 			= ASModule
					invoked.BCLevel				= true

		InternalCopy is an Instance Action
			restricted
			Parameters
				NewConfiguration 	is an ASConfiguration
				NewModule   		is an ASModule
				BCLevel				is Boolean
				
			Parameter Rules
				NewConfiguration
					required
					
				NewModule
					required
				
				BCLevel
					default to false

			Local Fields
				NewASBusinesClassView         is an ASBusinessClass view
			
			Action Rules

				invoke CreateCopy ASBusinessClass
					assign result to NewASBusinesClassView 
					invoked.ASConfiguration 	= NewConfiguration
					invoked.ASModule			= NewModule
					invoked.BusinessClass		= BusinessClass
					if (!BCLevel)
						invoked.Description 		= Description
					else
						invoked.Description			= CopyMessage + Description
					invoked.CreateAction		= CreateAction
					invoked.UpdateAction 		= UpdateAction 
					invoked.RecordSelection 	= RecordSelection
					invoked.FieldSelection 		= FieldSelection
					invoked.DisplayOrder		= DisplayOrder
					invoked.CreateRetroVersion	= CreateRetroVersion
					if (!BCLevel)
						invoked.BaseBusinessClass	= ASBusinessClass
					if (Inactive)
                    	invoked.Inactive 		= Inactive
					invoked.DefaultEffectiveDate	= DefaultEffectiveDate
				
				for each FieldsByDisplayOrderRel
					invoke Copy each
						invoked.NewConfiguration 	= NewASBusinesClassView.ASConfiguration
						invoked.NewModule			= NewASBusinesClassView.ASModule
						invoked.NewBusinessClass	= NewASBusinesClassView.ASBusinessClass
				
				if (BCLevel)
					if (HasDependentBCs)
						for each ASDependentBC set
							invoke Create ASDependentBC
								invoked.ASConfiguration 	= NewASBusinesClassView.ASConfiguration
								invoked.ASModule			= NewASBusinesClassView.ASModule
								invoked.ASBusinessClass		= NewASBusinesClassView.ASBusinessClass
								invoked.ASDependentBC 		= each.ASDependentBC 
					
					if (HasRelatedBCs)
						for each ASRelatedBusinessClass set
							invoke Create ASRelatedBusinessClass
								invoked.ASConfiguration 	= NewASBusinesClassView.ASConfiguration
								invoked.ASModule 			= NewASBusinesClassView.ASModule
								invoked.ASBusinessClass 	= NewASBusinesClassView.ASBusinessClass
								invoked.ASRelatedBusinessClass.RelatedModule			= each.ASRelatedBusinessClass.RelatedModule
								invoked.ASRelatedBusinessClass.RelatedBusinessClass 	= each.ASRelatedBusinessClass.RelatedBusinessClass
								invoked.RelatedLink										= each.RelatedLink



		RemovePrerequisiteBC is an Instance Action
			restricted
			Parameters
				BusinessClassBeingDeleted is like ASBusinessClass
			
			Parameter Rules
				BusinessClassBeingDeleted
					required
			Action Rules
            	for each ASDependentBC set
					if (each.ASDependentBC = BusinessClassBeingDeleted)
						invoke Delete each
		
		RemoveRelatedBC is an Instance Action
			restricted

			Parameters
				ModuleOfBusinessClassBeingDeleted is like ASModule
				BusinessClassBeingDeleted is like ASBusinessClass
			
			Parameter Rules
				ModuleOfBusinessClassBeingDeleted
					required

				BusinessClassBeingDeleted
					required

			Action Rules
            	for each ASRelatedBusinessClass set
					if (each.ASRelatedBusinessClass.RelatedModule = ModuleOfBusinessClassBeingDeleted
					and each.ASRelatedBusinessClass.RelatedBusinessClass = BusinessClassBeingDeleted)
						invoke Delete each


		CleanDependencies is an Instance Action	
			restricted
			Action Rules
				if (HasDependentBCs)
					for each ASDependentBC set
						invoke Delete each

		CleanTempImportArtifacts is an Instance Action
			restricted
			Action Rules
				initialize BaseBusinessClass
		
		CopyDependentBCs is an Instance Action
			restricted
			Parameters
				IsModuleLevelCopy is Boolean
					default label is untranslatable
			Parameter Rules				
				IsModuleLevelCopy
					default to false			
			Action Rules

				if (IsModuleLevelCopy)
					CopedConfigurationBase = ASConfiguration
				else
					CopedConfigurationBase = ASConfiguration.BaseConfiguration
				if (BaseBCRel.HasDependentBCs)
					for each BaseBCDependenciesRel
						CopiedDependentBC = each.ASDependentBC
						invoke Create ASDependentBC
							fill in fields from this instance
							invoked.ASDependentBC = first DependencyNewBusinessClassRel.ASBusinessClass
							
		CopyRelatedBCs is an Instance Action
			restricted
		
			Parameters
				IsModuleLevelCopy is Boolean
			Parameter Rules				
				IsModuleLevelCopy
					default to false			
			Action Rules

				if (IsModuleLevelCopy)
					CopedConfigurationBase = ASConfiguration
				else
					CopedConfigurationBase = ASConfiguration.BaseConfiguration
				if (BaseBCRel.HasRelatedBCs)
					for each BaseBCRelatedBCsRel
						CopiedRelatedModBase = each.ASRelatedBusinessClass.RelatedModule
						CopiedRelatedBCBase = each.ASRelatedBusinessClass.RelatedBusinessClass
						CopiedRelatedLink = each.RelatedLink

						if (RelatedBCModByBaseModRel exists)
							CopiedRelatedMod = first RelatedBCModByBaseModRel.ASModule
							if (RelatedBCByBaseBCRel exists)
								invoke Create ASRelatedBusinessClass
									fill in fields from this instance
									invoked.ASRelatedBusinessClass.RelatedModule		= 	RelatedBCByBaseBCRel.ASModule
									invoked.ASRelatedBusinessClass.RelatedBusinessClass = 	RelatedBCByBaseBCRel.ASBusinessClass
									invoked.RelatedLink									= 	CopiedRelatedLink
		
		CopyRelatedBCWithModuleOnlyMove is an Instance Action
			restricted
			
			Parameters
				OldConfiguration is like ASConfiguration
			
			Parameter Rules
				OldConfiguration
					required
			Action Rules
				CopedConfigurationBase = OldConfiguration
				if (BaseBCRel.HasRelatedBCs)
					for each BaseBCRelatedBCsRel
						CopiedRelatedModBase = each.ASRelatedBusinessClass.RelatedModule
						CopiedRelatedBCBase = each.ASRelatedBusinessClass.RelatedBusinessClass
						CopiedRelatedLink = each.RelatedLink

						if (CopiedRelatedModBase = ASModule.BaseModule)
							CopiedRelatedMod = ASModule
							if (RelatedBCByBaseBCRel exists)
								invoke Create ASRelatedBusinessClass
									fill in fields from this instance
									invoked.ASRelatedBusinessClass.RelatedModule		= 	RelatedBCByBaseBCRel.ASModule
									invoked.ASRelatedBusinessClass.RelatedBusinessClass = 	RelatedBCByBaseBCRel.ASBusinessClass
									invoked.RelatedLink									= 	CopiedRelatedLink

							
		BreakFromBase is an Instance Action
			restricted

			Action Rules
				if (BaseBusinessClass entered)
					initialize BaseBusinessClass
			
