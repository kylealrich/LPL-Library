CashAccountComment is a BusinessClass
	owned by cashmgmt
	prefix is CMCC
	
	Ontology
		symbolic key is CashAccountComment
	
	Persistent Fields
		Subject	 			is a CommentName
    	Comment				is Text
		From				is Alpha size 230
			default label is "CommentMadeBy"
		CommentDate			is TimeStamp
    		default label is "Date"
    	Attachment			is an AlternateAttachment

	Local Fields
		LocalAttributeCtr   is Numeric 2				
	
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 50
			restricted
			"InforCashAccountComment"

		ContextMessageText is a MessageField
			restricted
			"CashAccountComment<CashAccountComment>"

	Field Rules
		Subject
    		required
    			"SubjectIsRequired"
    				
    	Comment 
    		required
    			"CommentIsRequired"
    	
    	From
			initial value is actor.agent(Employee).FirstLastName
			required
    	
    	CommentDate
    		default to current timestamp

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
	
	Conditions
    	HasAttachment
    		restricted
    		when (Attachment entered)
    	
    	HasComment
    		restricted
    		when (CommentDate entered)
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
