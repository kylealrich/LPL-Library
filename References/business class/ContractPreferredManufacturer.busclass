ContractPreferredManufacturer is a BusinessClass
    owned by po
    prefix is CPMF 
 
    Ontology
    	symbolic key is ContractPreferredManufacturer
	    	
	Patterns
    	implements DynamicCreation
    	implements StaticJava
	
    Persistent Fields
    	Contract
    	ManufacturerCode 			is a Manufacturer
    	Vendor

    Derived Fields

    Conditions
    	CanDelete
    		restricted
    		when (!Contract entered)
    	ItemMfgCode
    		restricted 
    		when (ItemMfgRel exists)
		PreferredContract
			restricted
    		when (Contract.PreferredContract)
    	ContractItemExists
    		restricted
    		when (ContractItemRel exists)
    	ContractEntered
    		restricted
    		when (Contract entered)
    	ContractOnly
    		restricted
    		when (Contract entered
    		and   ManufacturerCode !entered)
    	ItemNumberEntered
    		restricted
    		when (ContractPreferredManufacturer.ItemNumber entered)
    		
    Relations
		InventoryLocationRel
			one-to-one relation to InventoryLocation
			Field Mapping uses symbolic key
				related.Company 			= ContractPreferredManufacturer.InventoryCompany
				related.InventoryLocation 	= ContractPreferredManufacturer.Location
				
		RequestingLocationRel
			one-to-one relation to RequestingLocation
			Field Mapping uses symbolic key
				related.Company 			= ContractPreferredManufacturer.InventoryCompany
				related.RequestingLocation 	= ContractPreferredManufacturer.ReqLocation
		
		ItemRel 
			one-to-one relation to Item
			Field Mapping uses symbolic key
				related.ItemGroup = ContractGroup
				related.Item  	  = ContractPreferredManufacturer.ItemNumber
				
		ContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup 	= ContractGroup
				related.Contract		= Contract

		PreferredMfgCompanyRel
			one-to-many relation to ContractPreferredManufacturer
			Field Mapping uses symbolic key
				related.ContractGroup                               = ContractGroup
				related.ContractPreferredManufacturer.ItemNumber 	= ContractPreferredManufacturer.ItemNumber
				related.ContractPreferredManufacturer.VendorItem    = ContractPreferredManufacturer.VendorItem
				related.ContractPreferredManufacturer.InventoryCompany	= ContractPreferredManufacturer.InventoryCompany
				related.ContractPreferredManufacturer.Location	 	= blank
				related.ContractPreferredManufacturer.ReqLocation	= blank
				related.ContractPreferredManufacturer.PricingGroup  = blank
			Instance Selection
				where (related.ManufacturerCode = ManufacturerCode
				and	   related.Contract			= Contract)
		
		PreferredMfgLocationRel
			one-to-many relation to ContractPreferredManufacturer
			Field Mapping uses symbolic key
				related.ContractGroup         = ContractGroup
			Instance Selection
				where (related.ManufacturerCode = ManufacturerCode
				and	   related.ContractPreferredManufacturer.InventoryCompany	= ContractPreferredManufacturer.InventoryCompany
				and	   related.Contract			= Contract
				and   (related.ContractPreferredManufacturer.Location		!= blank
				or	   related.ContractPreferredManufacturer.ReqLocation	!= blank))
		
		ContractItemRel
			one-to-many relation to ContractLine
			Field Mapping uses ByItemVendorItemForContract
				related.ContractGroup 	= ContractGroup
				related.Contract		= Contract
				related.ItemNumber		= ContractPreferredManufacturer.ItemNumber
		
		ContractVendorItemRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemVendor
				related.ContractGroup 	= ContractGroup
				related.VendorItem      = ContractPreferredManufacturer.VendorItem
		
		ItemMfgRel
			one-to-many relation to ContractLine
			Field Mapping uses ByItemVendorItemForContract
				related.ContractGroup 	= ContractGroup
				related.Contract		= Contract
				related.ItemNumber		= ContractPreferredManufacturer.ItemNumber
			Instance Selection
				where (related.Manufacturer.ManufacturerCode 	  = ManufacturerCode.ManufacturerCode
				and    related.Manufacturer.ManufacturerDivision = ManufacturerCode.ManufacturerDivision)
				
		ContractParticipantRel
			one-to-one relation to ContractParticipant
			Field Mapping uses symbolic key
				related.ContractGroup														= ContractGroup
           		related.Contract															= Contract
           		related.ContractParticipant.ParticipantLocation.Company					    = ContractPreferredManufacturer.InventoryCompany
           		related.ContractParticipant.ParticipantLocation.Location					= ContractPreferredManufacturer.Location
           		related.ContractParticipant.ParticipantLocation.RequestingLocation			= ContractPreferredManufacturer.ReqLocation
           		related.ContractParticipant.ParticipantLocation.PricingGroup                = blank
           		related.ContractParticipant.ContractCompanyDepartment.ContractCompany		= 0
           		related.ContractParticipant.ContractCompanyDepartment.ContractDepartment	= blank
				
		ContractTierMemberRel
			one-to-one relation to ContractTierMember
			Field Mapping uses symbolic key
				related.ContractGroup														= ContractGroup
           		related.Contract															= Contract
           		related.ContractTierMember.Company					    					= ContractPreferredManufacturer.InventoryCompany
           		related.ContractTierMember.Location											= ContractPreferredManufacturer.Location
           		related.ContractTierMember.RequestingLocation								= ContractPreferredManufacturer.ReqLocation
           		related.ContractTierMember.PricingGroup                						= blank
           		
        ContractDistributorPricingMemberRel
        	one-to-many relation to ContractDistributorPricingMember
        	Field Mapping uses ByLocation
				related.ContractGroup														= ContractGroup
           		related.Contract															= Contract
           		related.ContractDistributorPricingMember.Company					    	= ContractPreferredManufacturer.InventoryCompany
           		related.ContractDistributorPricingMember.Location							= ContractPreferredManufacturer.Location
           		related.ContractDistributorPricingMember.RequestingLocation					= ContractPreferredManufacturer.ReqLocation

	Sets
	
		ByItem
			duplicates
			Sort Order
				ContractGroup
				ContractPreferredManufacturer.ItemNumber

				ContractPreferredManufacturer.InventoryCompany
				ContractPreferredManufacturer.Location
				ContractPreferredManufacturer.ReqLocation
				ContractPreferredManufacturer.PricingGroup

			
		ByPreferredContract
			Sort Order
				ContractGroup
				Contract
				ContractPreferredManufacturer.ItemNumber
				ContractPreferredManufacturer.VendorItem
				ContractPreferredManufacturer.InventoryCompany
				ContractPreferredManufacturer.Location
				ContractPreferredManufacturer.ReqLocation
				ContractPreferredManufacturer.PricingGroup
				
		ByItemForDefault
			Sort Order
				ContractPreferredManufacturer.InventoryCompany
				ContractPreferredManufacturer.ItemNumber
				ContractPreferredManufacturer.Location
				ContractPreferredManufacturer.ReqLocation
				ContractPreferredManufacturer.PricingGroup
				ContractPreferredManufacturer.VendorItem
				ContractGroup
				Contract
		
		ByContractLocation
			Sort Order
				ContractGroup
				Contract
				ContractPreferredManufacturer.InventoryCompany
				ContractPreferredManufacturer.Location
				ContractPreferredManufacturer.ReqLocation
				ContractPreferredManufacturer.PricingGroup
				ContractPreferredManufacturer.ItemNumber
				ContractPreferredManufacturer.VendorItem
				
	Field Rules
		ContractPreferredManufacturer
			if (ContractPreferredManufacturer.Location 		!= blank
			or  ContractPreferredManufacturer.ReqLocation	!= blank)
				constraint (!PreferredMfgCompanyRel exists)	
					"APreferredManufacturerHasAlreadyBeenDefinedAtTheCompanyLevelForThisContract,ItemAndManufacturerCode"
			else
				for each PreferredMfgLocationRel
					constraint (each.ContractPreferredManufacturer.ItemNumber != ContractPreferredManufacturer.ItemNumber)	
						"APreferredManufacturerHasAlreadyBeenDefinedAtTheLocationLevelForThisContract,ItemAndManufacturerCode"
						
			if (ContractPreferredManufacturer.VendorItem entered)
				constraint (Contract entered)
					"ContractMustBeEnteredWithAVendorItem"
				constraint (ContractVendorItemRel exists)
					"VendorItemNotValidForAnyContract"
					
		ContractGroup
			required
		
		Contract
			if (!ManufacturerCode entered)
				required
					"MustEnterContractOrManufacturerCodeOrBoth"

		ManufacturerCode
			if (!Contract entered)
				required
					"MustEnterContractOrManufacturerCodeOrBoth"

	Actions
		Create is a Create Action 
			restricted
			bypass field rules
    		Action Rules
		 		if (ContractPreferredManufacturer.PricingGroup entered)
		 			ContractPreferredManufacturer.InventoryCompany = 0 
		 		
		ManualCreate is a Create Action
			default label is "Create"
    		Action Rules
		 		if (Contract entered)
	    			constraint (!Contract.ContractStatus.Closed)
	    				"CannotCreatePreferredContractRecordForClosedContract"
	    				
	    			constraint (Contract.NonServicePurchaseType)
	    				"CannotCreatePreferredContractRecordForContractThatIsNotUsedForCostDefaulting"
	    				
	    			if (Contract.IsNotDistributorAndNotManufacturer)
	    				constraint (ContractParticipantRel exists)
	    					"CannotCreatePreferredContractRecordForLocationThatIsNotAParticipantOnContract"
	    				
    				if (Contract.IsManufacturerContract)
    					constraint (ContractTierMemberRel exists)
    						"CannotCreatePreferredContractRecordForLocationThatIsNotAMemberOnContract"		
    				
    				if (Contract.IsDistributorContract)
    					constraint (ContractDistributorPricingMemberRel exists)
    						"CannotCreatePreferredContractRecordForLocationThatIsNotAMemberOnContract"
    				
    				constraint (!PreferredContract)
    					"CannotAddRecordForPreferredContractManually;WillBeAddedAutomatically"
    					
    				constraint (!ContractOnly)
    					"CannotAddRecordForAContractOnlyManually;MustAddUsingPreferredLineFieldFromContractLine"
    					
    				constraint (ItemMfgCode)
    					"ItemManufacturerInformationCombinationDoesNotExistOnContract<Contract>"
				 	
    	FastUpdate is an Update Action
    		restricted
    		bypass field rules
       	
    	Update is an Update Action
    		Action Rules
    			if (Contract entered
    			and !PreferredContract)
    				constraint (ItemMfgCode)
    					"ItemManufacturerInformationCombinationDoesNotExistOnContract<Contract>"
    		
    	Delete is a Delete Action
    		valid when (CanDelete)
    		Action Rules
    			
    	PreferredMfgContractDelete is a Set Action
			restricted
			Parameters
				PrmContractGroup is a ContractGroup
				PrmContract	     is a Contract
			Instance Selection
				where (ContractGroup = PrmContractGroup
				and    Contract 	 = PrmContract)
			Sort Order
				ContractGroup
				Contract
			Action Rules
				Instance Rules
					invoke Delete
	    			
		UpdateVendor is a Set Action
			restricted
			Parameters
				PrmContractGroup is a ContractGroup
				PrmContract	     is a Contract
				PrmVendor        is like Vendor
		
			Instance Selection
				where (PrmContractGroup = ContractGroup
				and    PrmContract      = Contract)

			Action Rules
				Instance Rules
					Vendor = PrmVendor				

		DeleteFromParticipant is a Set Action
			restricted
			Parameters
				ParmContractGroup  			is a ContractGroup
				ParmContract                is a Contract
				ParmInventoryCompany		is like Company
				ParmLocation 				is like InventoryLocation 
				ParmReqLocation 			is like RequestingLocation
    			ParmPricingGroup  			is like PricingGroup 
    			
    		Instance Selection
    		
				where (ParmContractGroup  			= ContractGroup
				and    ParmContract                	= Contract
				and    ParmInventoryCompany			= ContractPreferredManufacturer.InventoryCompany
				and    ParmLocation 				= ContractPreferredManufacturer.Location 
				and    ParmReqLocation 				= ContractPreferredManufacturer.ReqLocation
    			and    ParmPricingGroup  			= ContractPreferredManufacturer.PricingGroup) 
    			
    		Action Rules
    		
    			Instance Rules
    			
    				invoke Delete
				

    	Purge is a Purge Action
    		restricted
    		
