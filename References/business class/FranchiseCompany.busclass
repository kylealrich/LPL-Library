FranchiseCompany is a BusinessClass
    owned by fr
    prefix is FRO
    classic name is FROPTIONS

    Ontology
        symbolic key is FranchiseCompany
            classic set name is FROSET1   

    Patterns
        implements StaticJava
        disable AuditIndex



    Persistent Fields

        CashCode
        ProcessLevel					is a BillingProcessLevel				
        BillingInvoiceType
            classic name is INVC-TYPE
        InventoryLocation
        UnitOfMeasure
            classic name is UOM
        CurrentYear                    is Numeric size 4
        PreviousYear                   is Numeric size 4
        NextYear                       is Numeric size 4
        LastInvoiceNumber              is an InvoiceNumber
            classic name is LST-INVC-NBR
			disable Auditing
        InvoicePrefix
            classic name is INVC-PREFIX
        BillingCreditMemoInvoiceType   is a BillingInvoiceType
            classic name is CR-INVC-TYPE
        BillingCreditMemoInvoicePrefix is an InvoicePrefix
            classic name is CR-INVC-PREFIX
        LastCreditMemo                 is Numeric size 8
            classic name is LST-CR-NBR
            disable Auditing
        ReasonCode                     is an OrderCancelCreditReason
        SalesFlag
            classic name is SALES-FL
        FranchiseCalendar
            classic name is CALENDAR-ID
        SalesByDate
		AllowNegativeSales 			   is Boolean
		CalculateTaxOnNetSales 		   is Boolean
		AdvanceCycleDateForNoActivity  is Boolean

					
	Local Fields
		ValidateReceivablePrefix
		InitialChargeFrequency		is AlphaUpper 1
		LocalCompanyGroup			is a GeneralLedgerCompanyGroup		


	Context Fields
		ContextFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
		
	Derived Fields
		ActorSecurityGroup is a DerivedField
			type is like CompanySecurityGroup
			return actor.context.CompanySecurityGroup
				
		PreviousYearPlusOne is a DerivedField
        	type is Year
        	restricted
        	return PreviousYear + 1

		CurrentYearPlusOne is a DerivedField
        	type is Year
        	restricted
        	return CurrentYear + 1

		ValidNumberOfDecimalsCost is a DerivedField
        	type is Boolean
        	restricted
        	if (!IccompanyRel.NumberOfDecimalsCost.0Decimals
			or !IccompanyRel.NumberOfDecimalsCost.1Decimal)
        		return false
        	else
        		return true
        		
        LYWeeklyPeriod1EndDate is a DerivedField
        	type is Date
        	restricted
        	return (LastYearWeeklyRel.Dates.Date[1])
        	
        NYWeeklyPeriod1EndDate is a DerivedField
        	type is Date
        	restricted
        	return (NextYearWeeklyRel.Dates.Date[1])	
        
        AllChargesHaveSameFrequency is a DerivedField
        	type is Boolean
        	restricted
        	InitialChargeFrequency = (first FranchiseStandardCharge set.Frequency)
        	if (any FranchiseStandardCharge set.Frequency not = InitialChargeFrequency)
        		return false
        	else
        		return true		
        			
    Relations
    
		SecurityGroupRel
			one-to-one relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		  = Company.FinanceEnterpriseGroup
				related.DimensionGroupType	   		  = 2
				related.FinanceDimensionSecurityGroup = ActorSecurityGroup
				
		SecurityGroupDetailRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses Set1
				related.Company						= Company
				related.GeneralLedgerCompanyGroup  	= SecurityGroupRel.GeneralLedgerCompanyGroup
				    
		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = Company

		FranchiseContractRel is a FranchiseContract set

		PreviousYearFranchiseSalesCalendarRel
			one-to-one relation to FranchiseSalesCalendar
			Field Mapping uses symbolic key
				related.Company								= Company
				related.FranchiseSalesCalendar.Year			= PreviousYear

		CurrentYearFranchiseSalesCalendarRel
			one-to-one relation to FranchiseSalesCalendar
			Field Mapping uses symbolic key
				related.Company								= Company
				related.FranchiseSalesCalendar.Year			= CurrentYear

		NextYearFranchiseSalesCalendarRel
			one-to-one relation to FranchiseSalesCalendar
			Field Mapping uses symbolic key
				related.Company								= Company
				related.FranchiseSalesCalendar.Year			= NextYear		

		LastYearWeeklyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= PreviousYear	
				related.FranchiseCalendarDates.Frequency	= "W"					

		CurrentYearWeeklyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= CurrentYear	
				related.FranchiseCalendarDates.Frequency	= "W"	
				
		NextYearWeeklyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= NextYear	
				related.FranchiseCalendarDates.Frequency	= "W"					

		LastYearMonthlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= PreviousYear	
				related.FranchiseCalendarDates.Frequency	= "M"					

		CurrentYearMonthlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= CurrentYear	
				related.FranchiseCalendarDates.Frequency	= "M"
				
		NextYearMonthlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= NextYear	
				related.FranchiseCalendarDates.Frequency	= "M"	

		LastYearQuarterlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= PreviousYear	
				related.FranchiseCalendarDates.Frequency	= "Q"					

		CurrentYearQuarterlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= CurrentYear	
				related.FranchiseCalendarDates.Frequency	= "Q"
				
		NextYearQuarterlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= NextYear	
				related.FranchiseCalendarDates.Frequency	= "Q"	

		LastYearYearlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= PreviousYear	
				related.FranchiseCalendarDates.Frequency	= "Y"					

		CurrentYearYearlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= CurrentYear	
				related.FranchiseCalendarDates.Frequency	= "Y"
				
		NextYearYearlyRel
			one-to-one relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
				related.FranchiseCalendarDates.Year			= NextYear	
				related.FranchiseCalendarDates.Frequency	= "Y"
		
		CurrentYearRel
			one-to-many relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
			Instance Selection
				where (related.FranchiseCalendarDates.Year	= CurrentYear)
				
		PreviousYearRel
			one-to-many relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
			Instance Selection
				where (related.FranchiseCalendarDates.Year	= PreviousYear)
		
		NextYearRel
			one-to-many relation to FranchiseCalendarDates	
			Field Mapping uses symbolic key	
				related.Company								= Company
				related.FranchiseCalendar					= FranchiseCalendar
			Instance Selection
				where (related.FranchiseCalendarDates.Year	= NextYear)






			
		





		
		
        IccompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company 				= Company

        IclocationRel
            one-to-one relation to InventoryLocation
            required
            Field Mapping uses symbolic key
                related.Company           		= Company
                related.InventoryLocation 		= InventoryLocation

		CashCodeRel
			one-to-one relation to CashCode
			Field Mapping uses symbolic key
				related.CashManagementGroup		= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
				related.CashCode				= CashCode			

        OeinvctypeRel
            one-to-one relation to BillingInvoiceType
            required
            Field Mapping uses symbolic key
                related.Company            		= Company
                related.BillingInvoiceType 		= BillingInvoiceType

        OeproclevRel
            one-to-one relation to BillingProcessLevel
            required
            Field Mapping uses symbolic key
                related.Company             	= Company
                related.BillingProcessLevel 	= ProcessLevel

        IcvaluomRel
            one-to-one relation to UnitOfMeasure
            required
            Field Mapping uses symbolic key
                related.ItemGroup     			= IccompanyRel.ItemGroup
                related.UnitOfMeasure 			= UnitOfMeasure
	
		FranchiseCompanyRel
			one-to-one relation to FranchiseCompany
			Field Mapping uses symbolic key	
                related.Company             	= Company

		FranchiseCalendarRel
			one-to-one relation to FranchiseCalendar
			Field Mapping uses symbolic key
				related.Company					= Company
				related.FranchiseCalendar		= FranchiseCalendar

		
		ReleasedPrepaymentsPriorToCloseDateRel
			one-to-many relation to FranchisePrepayment
			Field Mapping uses Set4
				related.Company					= Company
			Instance Selection
				where (related.FranchisePrepayment <= FRSystemClosingControlRel.CurrentPeriodEndDate)

		ReleasedSalesInvoicesPriorToCloseDateRel
			one-to-many relation to FranchiseSales
			Field Mapping uses Set4
				related.Company					= Company
			Instance Selection
				where (related.FranchiseSales.Date <= FRSystemClosingControlRel.CurrentPeriodEndDate)

		ReleasedSalesImportsPriorToCloseDateRel
			one-to-many relation to FranchiseSalesImport
			Field Mapping uses Set2
				related.Company					= Company
			Instance Selection
				where (related.FranchiseSalesImport.Date <= FRSystemClosingControlRel.CurrentPeriodEndDate)
		
		FRSystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses symbolic key
				related.Company						= Company
				related.GeneralLedgerSystemCode		= "FR"	
									
		CompanyGroupRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup 	= LocalCompanyGroup
				related.Company						= Company
		
		AdvanceCycleChargeContractsRel
			one-to-many relation to FranchiseContract
			Field Mapping uses Set3
				related.Company					= Company
				
	Conditions
		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)
				
		LastYearWeeklyExists
			restricted
			when (LastYearWeeklyRel exists)
		CurrentYearWeeklyExists
			restricted
			when (CurrentYearWeeklyRel exists)				
		NextYearWeeklyExists
			restricted
			when (NextYearWeeklyRel exists)
			
		WeeklyCalendarRelsExist
			restricted
			when (LastYearWeeklyRel exists
			and CurrentYearWeeklyRel exists
			and NextYearWeeklyRel exists)
			
		MonthlyCalendarRelsExist
			restricted
			when (LastYearMonthlyRel exists
			and CurrentYearMonthlyRel exists
			and NextYearMonthlyRel exists)

		QuarterlyCalendarRelsExist
			restricted
			when (LastYearQuarterlyRel exists
			and CurrentYearQuarterlyRel exists
			and NextYearQuarterlyRel exists)

		YearlyCalendarRelsExist
			restricted
			when (LastYearYearlyRel exists
			and CurrentYearYearlyRel exists
			and NextYearYearlyRel exists)
								
		LastCurrentAndNextCalendarsExist
			when (WeeklyCalendarRelsExist 
			and MonthlyCalendarRelsExist 
			and QuarterlyCalendarRelsExist
			and YearlyCalendarRelsExist)

		ReleasedPrepaymentsPriorToCloseDateExists
			restricted
			when (ReleasedPrepaymentsPriorToCloseDateRel exists)
			
		ReleasedSalesInvoicePriorToCloseDateExists
			restricted
			when (ReleasedSalesInvoicesPriorToCloseDateRel exists) 
			
		ReleasedSalesImportPriorToCloseDateExists
			restricted			
			when (ReleasedSalesImportsPriorToCloseDateRel exists)
		
		CurrentPreviousNextYearExist
			restricted
			when (CurrentYearRel exists
			and PreviousYearRel exists
			and NextYearRel exists)

		InContextFinanceEnterpriseGroup
			restricted
			when (ContextFinanceEnterpriseGroup = Company.GeneralLedgerCompany.FinanceEnterpriseGroup)
		
		IsValidForActorContext
			restricted
			when ((actor.context.FinanceEnterpriseGroup != ""
			and   Company.GeneralLedgerCompany.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
			or   (actor.context.FinanceEnterpriseGroup = ""))
							
    Field Rules
		FranchiseCalendar
			required
			constraint (LastCurrentAndNextCalendarsExist)
				"Weekly,Monthly,QuarterlyAndYearlyCalendarsMustBeCreatedForLast,CurrentAndNextYearsBeforeAddingFranchiseCompany"
				
        CashCode
            required

        BillingCreditMemoInvoicePrefix
            required
            
            constraint (BillingCreditMemoInvoicePrefix != InvoicePrefix)
            	"CreditMemoPrefixCannotBeTheSameAsBillingInvoicePrefix"
            	
		   	if (action type.Create
		   	or (action type.Update 
		   	and BillingCreditMemoInvoicePrefix not = old BillingCreditMemoInvoicePrefix))
			   	initialize ValidateReceivablePrefix
			   	ValidateReceivablePrefix.EditReceivableCompany 	  	= Company
		     	ValidateReceivablePrefix.EditSystem 				= "FR"
			   	ValidateReceivablePrefix.EditReceivablePrefix 	  	= BillingCreditMemoInvoicePrefix	
		    	ValidateReceivablePrefix.EditReceivableInvoiceType	= "C"	 
			   	constraint (ValidateReceivablePrefix.ErrorNumber not entered)
		         	"<ValidateReceivablePrefix.ErrorMessage>"
		         		
        BillingCreditMemoInvoiceType
            required
			constraint (BillingCreditMemoInvoiceType.Active)
				"InvoiceTypeIsInactive"//"InvoiceTypeIsInactive"           
			constraint (BillingCreditMemoInvoiceType.Credit)
				"MustBeCreditInvoiceType"//"MustBeCreditInvoiceTtype"      
			if (LastCreditMemo entered)
				cannot be changed
					"CannotChange_\C\RInvoicePrefix,CreditMemosAlreadyExist"//"CannotChangeCRInvoicePrefix,CreditMemosAlreadyExist"   

        CurrentYear
            required

            PreviousYear 	= (CurrentYear - 1)
            NextYear 		= (CurrentYear + 1)
            constraint (CurrentPreviousNextYearExist)
            	"PreviousYearAndNextYearCalendarDatesAreNotDefinedForTheCurrentYear"     

		PreviousYear 
			default to (CurrentYear - 1)
		
		NextYear
			default to (CurrentYear + 1)

        InvoicePrefix
            required

            constraint (InvoicePrefix != BillingCreditMemoInvoicePrefix)
            	"BillingInvoicePrefixCannotBeTheSameAsCreditMemoPrefix"
            	
			if (LastInvoiceNumber entered)
				cannot be changed
					"CannotChangeInvoicePrefix,InvoicesAlreadyExist"//"CannotChangeInvoicePrefix,InvoicesAlreadyExist" 
		   	if (action type.Create
		   	or (action type.Update 
		   	and InvoicePrefix not = old InvoicePrefix))
			   	initialize ValidateReceivablePrefix
			   	ValidateReceivablePrefix.EditReceivableCompany 	  	= Company
		     	ValidateReceivablePrefix.EditSystem 				= "FR"
			   	ValidateReceivablePrefix.EditReceivablePrefix 	  	= InvoicePrefix	
		    	ValidateReceivablePrefix.EditReceivableInvoiceType 	= "I"	 
			   	constraint (ValidateReceivablePrefix.ErrorNumber not entered)
		        	"<ValidateReceivablePrefix.ErrorMessage>"

							      	


        BillingInvoiceType
            required
			constraint (BillingInvoiceType.Active)
				"InvoiceTypeIsInactive"//"InvoiceTypeIsInactive"           			
			constraint (!BillingInvoiceType.Credit)
				"CreditInvoiceTypeIsNotAllowed"//"CreditInvoiceTypeIsNotAllowed"   	

        InventoryLocation
            required

		ProcessLevel
			required
			constraint (!IccompanyRel.NumberOfDecimalsCost.0Decimals
			and !IccompanyRel.NumberOfDecimalsCost.1Decimal)
				"Invalid_\CostAnd_\Price_\Default_\DecimalsFor_\I\C_\Company"//"InvalidCostAndPriceDefaultDecimalsForICCompany"   
			
        SalesByDate
            required
            default to "N"
			if (SalesFlag.No)
				constraint (SalesByDate.No)
					"SalesByDateNotAllowed,_\Aggregate_\SalesNotRequired"//"SalesByDateNotAllowed,AggregateSalesNotRequired"  
			if (SalesByDate.Yes)
				constraint (PreviousYearFranchiseSalesCalendarRel exists)
					"SalesDatesForPreviousYearAreNotDefined"//"SalesDatesForPreviousYearAreNotDefined"   
				constraint (CurrentYearFranchiseSalesCalendarRel exists)
					"SalesDatesForCurrentYearAreNotDefined"//"SalesDatesForCurrentYearAreNotDefined"     
				constraint (NextYearFranchiseSalesCalendarRel exists)
					"SalesDatesForNextYearAreNotDefined"//"SalesDatesForNextYearAreNotDefined"      

        SalesFlag
            required
            default to "N"
			if (SalesFlag.No)
				constraint (SalesByDate.No)
					"SalesByDateNotAllowed,_\Aggregate_\SalesNotRequired"//"SalesByDateNotAllowed,AggregateSalesNotRequired"  
				constraint (AllChargesHaveSameFrequency)
					"ChargesWithDifferentFrequenciesExistUnderThisCompany;TheAggregateSalesAllContractsCannotBeSetToNo"
				
        UnitOfMeasure
            required

        ReasonCode
        	required
			constraint (ReasonCode.Billing)
				"Reason_\CodeIsInvalidForBilling"//"ReasonCodeIsInvalidForBilling" 
			constraint (ReasonCode.Active)
				"Reason_\CodeIsInactive"//"ReasonCodeIsInactive"           
				
		AdvanceCycleDateForNoActivity
			if(AdvanceCycleDateForNoActivity changed
		    and !AdvanceCycleDateForNoActivity)
		    	constraint(AdvanceCycleChargeContractsRel not exists)
		    		"CannotChange_\Advance\Cycle\Date\For\No\ActivityFlagIsSetToTrueIn_\Contract"
		    	
				
    Actions
        Create is a Create Action
		
       	Update is an Update Action

        Delete is a Delete Action
        	Action Rules
        		constraint (FranchiseContractRel not exists)
        			"CannotDelete;ContractMasterExists"//"CannotDelete.ContractMasterExists"  


		FranchisePeriodClose is a Set Action
			run in foreground
			restricted
			Parameters
				FinanceEnterpriseGroup
				PrmCompany					is a FranchiseCompany
					default label is "Company"
				PrmCompanyGroup				is a GeneralLedgerCompanyGroup		
					default label is "CompanyGroup"

			Parameter Rules
				FinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
				PrmCompany
					if (PrmCompany entered)
						constraint (PrmCompanyGroup not entered)
							"CannotEnterBoth_CompanyAnd_Company_Group"
						constraint (PrmCompany.FRSystemClosingControlRel.Control = true)
							"CompanyIsNotSetUpFor_Close_Control"
						constraint (PrmCompany.FRSystemClosingControlRel.CurrentPeriodNotClosed)
							"PeriodIsAlreadyClosedFor_Franchise_System.ClosePeriodFor_Global_LedgerToRunAgain."
					if (PrmCompany not entered)
						constraint (PrmCompanyGroup entered)
							"The_Company_GroupOr_CompanyParameterIsRequired"
				PrmCompanyGroup
					if (PrmCompanyGroup entered)
						LocalCompanyGroup = PrmCompanyGroup
						constraint (PrmCompanyGroup.FRSystemClosingControlCompaniesWithControlRel exists)
							"NoCompanyInThe_Company_GroupIsSetUpFor_Franchise_System_Close_Control"
						constraint (PrmCompanyGroup.FRSystemClosingControlCompaniesWithOpenPeriodRel exists)
							"PeriodIsAlreadyClosedForThe_Franshise_SystemForAllCompaniesWith_Franshise_System_Closing_ControlInThe_Company_Group"
		
			Instance Selection
				where (((PrmCompany entered 
				and      Company							= PrmCompany)
	       		or		(PrmCompanyGroup entered 
        		and      CompanyGroupRel exists))	
				and     (FRSystemClosingControlRel.Control 	= true
				and      FRSystemClosingControlRel.CurrentPeriodNotClosed))
				
			Sort Order
				Company				
			
			Action Rules				
				Instance Rules
					constraint (!ReleasedPrepaymentsPriorToCloseDateExists) 
						"ReleasedPrepaymentExistsPriorToPeriodEndDateFor_Company<Company>;Run_Prepayment_Update"
					constraint (!ReleasedSalesInvoicePriorToCloseDateExists)
						"ReleasedActualInvoiceExistsFor_Company<Company>;Run_Invoice_Interface"
					constraint (!ReleasedSalesImportPriorToCloseDateExists)
						"SalesInterfaceRecordsExistFor_Company<Company>;Run_Sales_Interface"	
					if (FRSystemClosingControlRel.Control)
						constraint (FRSystemClosingControlRel.CurrentPeriodNotClosed)
							"Period_<FRSystemClosingControlRel.EntityCurrentPeriod>_AlreadyClosed"
					invoke UpdateCompanySystemClosingControl FRSystemClosingControlRel
						invoked.PrmCompany		 		= Company
						invoked.PrmSystem				 = "FR"
			
