BillingInvoiceOutputHeader is a BusinessClass
    owned by bl
    prefix is BLIOH

    Ontology
        symbolic key is BillingInvoiceOutputHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
    	FileName					is AlphaUpper 50
    	OutputFile					is a PaymentAttachment
    	InProcess					is Boolean
		CreateDate					is TimeStamp
		

        Company                 is a BillingCompany
        ProcessLevel            is a BillingProcessLevel
        InvoiceDate             is Date
			default label is "InvoiceCutoffDate"
        AddSubtract             is AlphaUpper size 1
        	default label is "NumberOfDays"
            States
                Add      value is "A"
                Subtract value is "S"
        CutoffDays              is Numeric size 3
        SelectOption            is AlphaUpper size 1
			default label is "PrintOption"
            States
                CreditMemosOnly        value is "1"
                InvoicesOnly           value is "2"
                CodInvoicesOnly        value is "3"
                    default label is "COD Invoices Only"
                InvoicesAndCreditMemos value is "4"
                    default label is "Invoices and Credit Memos"
        InvFormOption           is AlphaUpper size 1
			default label is "InvoiceFormRequired"
            States
                Yes value is "Y"
                No  value is "N"
        OscCommentCode          is a BillingStandardCommentHeader
			default label is "StandardCommentCode"
        Alignments              is Numeric size 2
			default label is "Number_ofAlignments"
        InvcPrefixGrp is a InvoicePrefix
			default label is "ReprintInvoiceRange"
        FrInvcNumber            is a InvoiceNumber
        ToInvcNumber            is a InvoiceNumber
        EdiInvcOption           is AlphaUpper size 1
			default label is "PrintE\D\IInvoice"
            States
                Yes value is "Y"
                No  value is "N"
        InvcFormType            is Numeric size 2
			default label is "InvoiceFormType"
            States
                UsStandard value is 00
                    default label is "US Standard"
                Sweden     value is 01
                
	Transient Fields
    
    Local Fields
		    
    Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 10	
			(instance count of BillingInvoiceOutputDetail set)

    Conditions
    	BillingInvoiceOutputDetailExists
    		when (BillingInvoiceOutputDetail set exists)

		HasAttachment
			restricted
			when (OutputFile entered)

    Relations

    Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			FinanceEnterpriseGroup

    Field Rules
        CreateDate
            default to current timestamp 

		FileName
			default to "BillingInvoiceOutput"            

	Actions
		Create is a Create Action
			restricted
			Action Rules
				InProcess = true
				
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
			restricted

		FinishProcessing is an Instance Action
			restricted
			Action Rules
				if (BillingInvoiceOutputDetailExists)
					trigger "BillingInvoiceOutput" PA service
						resume on error
						title is "CreateBillingInvoiceOutput"
						Variables
							FinanceEnterpriseGroup
							BillingInvoiceOutputHeader
				InProcess = false

		CreateOutputFile is an Instance Action
			valid when (BillingInvoiceOutputDetailExists)
			Action Rules
				trigger "BillingInvoiceOutput" PA service
					resume on error
					title is "CreateBillingInvoiceOutput"
					Variables
						FinanceEnterpriseGroup
						BillingInvoiceOutputHeader
			
		PurgeBillingInvoiceOutputHeaderRecords is a Set Action
			run in foreground
			completion message is "PurgeOfBillingInvoiceOutputHeaderRecordsHasCompleted"
			Parameters
				PrmFinanceEnterpriseGroup					is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmCreateDateRange							is a DateRange
					default label is "CreateDateRange"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmCreateDateRange
					required
			Instance Selection
				where (CreateDate date within PrmCreateDateRange
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
	
			Action Rules
				Instance Rules
					invoke Purge
						
