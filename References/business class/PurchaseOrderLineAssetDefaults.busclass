PurchaseOrderLineAssetDefaults is a BusinessClass
    owned by am
    prefix is AOT
    classic name is AMPODFLTS

    Ontology
        symbolic key is PurchaseOrderLineAssetDefaults
            classic set name is AOTSET1
            classic name is LINE-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        AssetDescription         is a Description
            classic name is ASSET-DESC
        TagNumber 
            classic name is TAG-NBR
        AssetGroup
        AssetAccountingUnitGroup
            classic name is AU-GROUP
        Combine 
        HoldAsset                is Boolean
            classic name is HOLD-AM
      	Project	 		 is a FinanceCodeBlockProjectOnly
        	classic name for Project.Project is ACTIVITY
      		default label is "AssetProject"

        ItemNumber               is an AssetItemNumber
            classic name is ITEM-NBR
        ItemDescription          is a Description
            classic name is ITEM-DESC
        ModelNumber
        PurchaseDate             is Date
        InServiceDate            is Date
            classic name is INSRV-DATE
        TransactionItemTax       is an InternationalAmount
            classic name is ITEM-TAX-TRAN
        AssetLocation
            classic name is LOCATION-NAME
        AssetDivision
            classic name is DIVISION
        LocationDetail           is an AssetLocationDetail
            classic name is ITEM-LOC-DTL

    Field Rules
    	
    	ItemNumber
    		if(PurchaseOrderLine.ItemType="S")
    			constraint (false)
					"CannotCreateAssetDefaultsForServiceTypeItems"
					
			if(PurchaseOrder.Dropship="Y")
    			constraint (false)
					"CannotCreateAssetDefaultsForDropShipments"		
		
		InServiceDate		
			default to PurchaseDate
			constraint(InServiceDate >= PurchaseDate)
				"InServiceDateMustBeGreaterThanOrEqualToPurchaseDate"				
					
    
    Relations

        PurchaseOrderLineAssetRel
            classic name is AMPOASTHDR
            one-to-one relation to PurchaseOrderLineAsset
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.PurchaseOrder			= PurchaseOrder


                related.PurchaseOrderLine       = PurchaseOrderLine
       
        PurchaserOrderLineDistRel
        	one-to-many relation to PurchaseOrderLineDistribution
        	Field Mapping uses symbolic key
        	 	related.Company = Company
        	 	related.PurchaseOrder = PurchaseOrder


        	 	related.PurchaseOrderLine = PurchaseOrderLine    
        	 	
		UniqueAssetsTagRel
			one-to-many relation to Asset
			Field Mapping uses ByCompanyTagAsset
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.Company 				= Company
				related.TagNumber				= TagNumber
		 	 	  
        	 	
        
        
                
    Actions
    
    	Update is an Action
    		
    		Entrance Rules
    			constraint(PurchaserOrderLineDistRel exists) 
    				"MustDefineAnAssetTemplateForPOLine" 
    				
    		Field Rules
    			TagNumber
					if (PurchaseOrder.DefaultAssetTemplate.Company.RequireUniqueTags = true)
						if (TagNumber entered) 
							constraint (UniqueAssetsTagRel not exists)
								"TagNumber<TagNumber>IsInUse" 
    		
    		Action Rules
    			if(PurchaseOrder.DefaultAssetTemplate entered and PurchaseOrderLineAssetRel exists)
    				for each PurchaseOrderLineAssetRel.PurchaseOrderLineAssetDetail set
  						invoke Update each.PurchaseOrderLineAssetDetail    					
	    					invoked.AssetDescription = AssetDescription
	    					invoked.TagNumber = TagNumber
	    					invoked.AssetGroup = AssetGroup
	    					invoked.AssetAccountingUnitGroup = AssetAccountingUnitGroup
	    					invoked.Combine = Combine
	    					invoked.HoldAsset = HoldAsset
	    					invoked.Project = Project
	    					invoked.ItemNumber = ItemNumber
	    					invoked.ItemDescription = ItemDescription
	    					invoked.PurchaseDate = PurchaseDate
	    					invoked.InServiceDate = InServiceDate
	    					invoked.AssetLocation = AssetLocation
	    					invoked.LocationDetail = LocationDetail
	    					invoked.AssetDivision = AssetDivision    					
    	
    	
    	Create is an Action
    		
    		Entrance Rules
    			constraint(PurchaserOrderLineDistRel exists)  
    				"MustDefineAnAssetTemplateForPOLine"
    				
    			display "InEntranceRulesToCreateAssetDefault"
    			display "Company<Company>,PurchaseOrder<PurchaseOrder>,PurchaseOrderLine<PurchaseOrderLine>"    	
    	
    	Delete is an Action			
    					
    
    
                
   
    	
		        	
    		
    	
    	            

