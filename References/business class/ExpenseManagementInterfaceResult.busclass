ExpenseManagementInterfaceResult is a BusinessClass
	owned by ap
	prefix is EMIR
    sql name is XMInterfaceResult
	Ontology
		symbolic key is ExpenseManagementInterfaceResult
		
	Patterns
	
	Persistent Fields
		RunType				is Numeric 1
			States
				ExpenseReportInvoiceInterface	value is 1
				CompanyExpensePosting			value is 2
		RunTime				is TimeStamp
		JournalizeGroup
		Status				is Numeric 1
			States
				Complete		value is 1
				Incomplete 		value is 2
		
	Relations
		ExpenseManagementInterfaceHeaderRel
			one-to-many relation to ExpenseManagementInterfaceHeader
			Field Mapping uses ByExpenseManagementInterfaceResult
				related.VendorGroup 						= VendorGroup
				related.ExpenseManagementInterfaceResult	= ExpenseManagementInterfaceResult
				
		ExpenseManagementInterfaceDetailRel
			one-to-many relation to ExpenseManagementInterfaceDetail
			Field Mapping uses ByExpenseManagementInterfaceResult
				related.VendorGroup 						= VendorGroup
				related.ExpenseManagementInterfaceResult	= ExpenseManagementInterfaceResult
				
	Sets
		ByRunTime
			Sort Order
				RunTime descending
				VendorGroup
				ExpenseManagementInterfaceResult
		ByRunType
			Sort Order
				RunType
				VendorGroup
				ExpenseManagementInterfaceResult

					
	Actions
		Create is an Action
			restricted

		Update is an Action
			restricted

		Purge is a Purge Action
			Action Rules
				invoke PurgeInBackground ExpenseManagementInterfaceHeader
					invoked.PrmVendorGroup						= VendorGroup
					invoked.PrmExpenseManagementInterfaceResult	= ExpenseManagementInterfaceResult
					
				invoke PurgeInBackground ExpenseManagementInterfaceDetail
					invoked.PrmVendorGroup						= VendorGroup
					invoked.PrmExpenseManagementInterfaceResult	= ExpenseManagementInterfaceResult
					
		PurgeRecords is a Set Action
			completion message is "PurgeRecordsSubmitted"
			Parameters
				PrmVendorGroup						is a VendorGroup
					default label is "VendorGroup"
				PrmExpenseManagementInterfaceResult	is a ExpenseManagementInterfaceResult
					default label is "PurgeRecordsThrough"

			Parameter Rules
				PrmVendorGroup
					required
				PrmExpenseManagementInterfaceResult
					required

			Instance Selection
				where (VendorGroup						= PrmVendorGroup
				and    ExpenseManagementInterfaceResult <= PrmExpenseManagementInterfaceResult)
			
			Action Rules
				Instance Rules
					invoke Purge
				
