AssetBookAdditionalImport is a BusinessClass
    owned by am
    prefix is IBK
    classic name is AMASTBKINV

    Ontology
        symbolic key is AssetBookAdditionalImport
            classic set name is IBKSET1

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
        Active                        is Boolean
            classic name is BOOK-STATUS
        BookRegulation 
        Life
        LifeRemaining
            classic name is LIFE-REM
        Basis                         is an InternationalAmount
            classic name is BOOK-BASIS
        Method
        InServiceDate                 is Date
            classic name is INSRV-DATE
        YearPlacedInService
            classic name is METH-TABLE-YR
        Convention
        LifeInYears
            classic name is LIFE-YEARS
        YearToDateDepreciation        is an InternationalAmount
            classic name is DEPR-YTD
        YearToDateDepreciationAllowed is an InternationalAmount
            classic name is DEPR-YTD-ALLOW
        LifeToDateDepreciation        is an InternationalAmount
            classic name is DEPR-LTD
        LifeToDateDepreciationAllowed is an InternationalAmount
            classic name is DEPR-LTD-ALLOW
        YearToDateInactiveMonths
            classic name is INACT-YTD
        LifeToDateInactiveMonths
            classic name is INACT-LTD
        SalvageValue                  is an InternationalAmount
        BusinessPercent
            classic name is BUS-PERCENT
        PersonalUseCode               is AlphaUpper size 10
            classic name is PERSONAL-USE
        FirstYearExpenseAmount        is an InternationalAmount
            classic name is YR1-EXP
        AssetTaxCreditTable
            classic name is TC-TABLE
        TaxCreditPropertyAmount       is an InternationalAmount
            classic name is TC-PROPERTY
        TaxCreditAmount               is an InternationalAmount
            classic name is TC-CREDIT
        InsuranceCalculationMethod    is a Method
            classic name is INS-METHOD
        ActualInsuranceValue          is an InternationalAmount
            classic name is INS-ACT-VAL
        CalculatedInsuranceValue      is an InternationalAmount
            classic name is INS-CMPTD-VAL
        ReplacementCalculationMethod  is a Method
            classic name is RPL-METHOD
        CalculatedReplacementValue    is an InternationalAmount
            classic name is RPL-CMPTD-VAL
		IndianReservation					is Boolean
		PropertyType						is Numeric 2
			States
				1245				value is 01
					default label is "1245"
				1250				value is 02	
					default label is "1250"
		QualifiedImprovement		is Numeric 2
			States
				NotQualified			value is 00
				LeaseholdImprovement	value is 01
				NotLeaseholdImprovement	value is 02
		ForeignProperty						is Boolean
		CostRecoverySystem					is Numeric 2
			States
				ACRS				value is 1
				MACRS				value is 2
		ClassificationOfProperty	is AlphaUpper 5
			States
				Blank									value is " "		
				3YearProperty							value is "03"
				5YearProperty							value is "05"
				7YearProperty							value is "07"
				10YearProperty							value is "10"
				12YearProperty							value is "12"
				15YearProperty							value is "15"
				RealProperty18							value is "18"
					default label is "Real Property (3/17/84 To 5/9/85)"
				RealProperty19 							value is "19"
					default label is "Real Property (3/10/85 To 12/31/86)"	
				20YearProperty							value is "20"
				25YearProperty							value is "25"
				ResidentialReal27						value is "27"	
					default label is "Residential Real 27.5"
			 	NonResidentialReal30					value is "31"	
			 		default label is "NonResidential Real 31.5 (1/1/87 To 5/12/93)"
				NonResidentialReal40 					value is "39"		
					default label is "NonResidential Real 39/40"								
		NonConforming						is Boolean
		Used								is Boolean						
		MineralReserves						is Boolean
		LowIncomeHousing					is Boolean 	
		Farming								is Boolean
		Section179							is Boolean
		Listed								is Boolean
		DepreciableIntangible				is Numeric 1
			States
				Blank					value is 0
				DepreciableIntangible	value is 1
				AmortizedIntangible		value is 2	
		TransitionalRuleFlag				is Boolean			
		BonusAmount							is an InternationalAmount
		Section179Expense					is an InternationalAmount
		SpreadOverRemainingLife				is Boolean
		            
	Field Rules
		InServiceDate
			default to AssetBookAdditionalImport.Asset.LastAssetItemPurchaseDate
			required
    		constraint (BookCalendarRel exists)
    			"BookCalendarsHaveNotBeenDefined"		
            if (InServiceDate > first BookCalendarRel.YearEndDate)
            	constraint (FutureBookCalendarsRel exists)	
            		"InServiceDateBeyondCurrentCalendarRequiresAFutureCalendarToBeDefined"
    		constraint (AssetBookRel not exists)
    			"AssetBookAlreadyExists"




		Convention
			default to "AD"
            if (AssetBookAdditionalImport.Asset.Classification.Finance) 
       			constraint (Convention.FullMonth)
                	"TheConventionMustBeFullMonthForAssetBooksOnAFinanceLease"
			
		BusinessPercent
			default to 100

        Method
            if (AssetBookAdditionalImport.Asset.Classification.Finance) 
       			constraint (Method.MethodFormula = "SL")
                	"TheMethodMustBeStraightLineForAssetBooksOnAFinanceLease"			

	Conditions
		BookRegulationsExist
			restricted
			when (BookRegulationsRel exists)	


    Relations
    	AssetRel
    		one-to-one relation to Asset
    		Field Mapping uses symbolic key
    			related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
    			related.Asset					= AssetBookAdditionalImport.Asset
    	
    	AssetBookRel
    		one-to-one relation to AssetBook
    		Field Mapping uses symbolic key
	    		related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
	    		related.Asset					= AssetBookAdditionalImport.Asset		
	    		related.Book					= AssetBookAdditionalImport.Book		
    		
        BookCalendarRel
            classic name is AMCALENDAR
            one-to-many relation to BookCalendar
            Field Mapping uses Set5
                related.Book    			= AssetBookAdditionalImport.Book
                related.Company 			= AssetBookAdditionalImport.Asset.Company
          	Instance Selection      
    			where (related.IsCurrent)

        FutureBookCalendarsRel
            classic name is AMCALENDAR
            one-to-many relation to BookCalendar
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Book					= AssetBookAdditionalImport.Book
				related.Company					= AssetBookAdditionalImport.Asset.Company
          	Instance Selection      
    			where (related.IsFuture) 

		BookRegulationsRel
			one-to-many relation to BookRegulation
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup    			
	Sets
		ByAsset
			Sort Order
				FinanceEnterpriseGroup
				AssetBookAdditionalImport.Asset
	    		AssetBookAdditionalImport.Book		
	    		AssetBookAdditionalImport.RunGroup				    								  
