ResponsibilityMatrixResource is a BusinessClass
    owned by GeneralLedger
    prefix is MARes

    Ontology
    	symbolic key is ResponsibilityMatrixResource

	Patterns
		implements Resequence on ApprovalLevel
			new sequence field is NewApprovalLevel
			set is ByDocumentType

    Persistent Fields
    	ApprovalLevel						is Numeric size 8
#ifdef module sharedfinance
		Approver							is a FinanceResource
		ApprovalTeam						is a FinanceTeam
#endif
#ifndef module sharedfinance
		Approver							is Numeric 13
		ApprovalTeam						is a GlonlyFinanceTeamField
#endif
		ApprovalAmount						is a CurrencyAmount
		ApprovalCurrency					is a Currency
		ApprovalPosition					is a Position
		EscalationDays						is Numeric 3
		EscalationHours						is Decimal 5.2
		EscalateTo							is Numeric 1
			States
				NextApprovalLevel		value is 1
				SpecificApprovalLevel	value is 2
		EscalationApprovalLevel				is a ResponsibilityMatrixResource
		DocumentType						is an ApprovalTransactionForm
		ApproverShadow						is Alpha 100
		
	Transient Fields
		NewApprovalLevel				is Numeric size 8
		GlobalLedger                    is Boolean
			derive value from DerivedGlobalLedger
        Requisition                     is Boolean
			derive value from DerivedRequisition
        PurchaseOrder                   is Boolean
			derive value from DerivedPurchaseOrder
        PayablesInvoice                 is Boolean
			derive value from DerivedPayablesInvoice
        ContractManagement              is Boolean
			derive value from DerivedContractManagement
		GlobalLedgerChanged				is Boolean
		RequisitionChanged				is Boolean
		PurchaseOrderChanged			is Boolean
		PayablesInvoiceChanged			is Boolean
		ContractManagementChanged		is Boolean

	Local Fields
		LocalApprover 					is Alpha 50
		LocalApproverList				is Alpha 250
		LocalFirstApproverAssigned	  	is Boolean
		LocalCounter					is Numeric 2
		LocalDocumentType				is an ApprovalDocumentType
		LocalGLJournal					is Boolean
		LocalRequisition				is Boolean
		LocalPurchaseOrder				is Boolean
		LocalPayablesInvoice			is Boolean
		LocalContractManagement			is Boolean
		LocalDocType					is an ApprovalTransactionForm
		LocalFinanceDimension			is AlphaUpper up to 26
		LocalEmployee					is an Employee
		LocalDimensionGroupType			is a DimensionGroupType

	Derived Fields

		DerivedApprovalLevelPlusOne is a DerivedField
			type is Numeric 8
			return ApprovalLevel + 1

		DerivedNextApprovalLevelApprover is a DerivedField
			type is Alpha size 230
#ifdef module sharedfinance
			return first NextApprovalLevelNoDocTypeRel.Approver.PreferredSimplePresentationName
#endif
#ifndef module sharedfinance
			return blank
#endif
			
		DerivedNextApprovalLevelTeam is a DerivedField
			type is Alpha 100
#ifdef module sharedfinance
			return first NextApprovalLevelNoDocTypeRel.ApprovalTeam.Description
#endif
#ifndef module sharedfinance
			return blank
#endif
			
		DerivedNextApprovalLevelPosition is a DerivedField
			type is Alpha 100
#ifdef module sharedfinance
			return first NextApprovalLevelNoDocTypeRel.ApprovalPosition.Description
#endif
#ifndef module sharedfinance
			return blank
#endif
			
		InvalidNextLevelEscalationMessage is a MessageField
			restricted
			"Invalid:EscalatingToNextLevelWhenNoNextLevelExists"
			
		InvalidSpecificLevelEscalationMessage is a MessageField
			restricted
			"Invalid:EscalatingToSpecificLevelWhereTheLevelIsNotGreaterThanTheCurrentLevel"

		DerivedDocumentType		is a DerivedField
			type is Alpha 200
			DerivedDocumentType = ""
			if(GlobalLedger)
				DerivedDocumentType = DerivedDocumentType + "GL"
			if(Requisition)
				DerivedDocumentType = DerivedDocumentType + " RQ"
			if(PurchaseOrder)
				DerivedDocumentType = DerivedDocumentType + " PO"
			if(PayablesInvoice)
				DerivedDocumentType = DerivedDocumentType + " INV"
			if(ContractManagement)
				DerivedDocumentType = DerivedDocumentType + " CM"
			return DerivedDocumentType

		DerivedDocument		is a DerivedField
			type is like ApprovalTransactionForm
			if (GlobalLedger)
				return ApprovalTransactionForm.GLJournalEntry
			else
			if (Requisition)
				return ApprovalTransactionForm.Requisition
			else
			if (PurchaseOrder)
				return ApprovalTransactionForm.PurchaseOrder
			else
			if (PayablesInvoice)
				return ApprovalTransactionForm.PayablesInvoice
			else
			if (ContractManagement)
				return ApprovalTransactionForm.ContractManagement

		DerivedGlobalLedger   is a DerivedField
			type is Boolean
			if (GlobalLedgerApprovalLevelRel exists)
				return true
			return false

		DerivedRequisition   is a DerivedField
			type is Boolean
			if (RequisitionApprovalLevelRel exists)
				return true
			return false

		DerivedPurchaseOrder   is a DerivedField
			type is Boolean
			if (PurchaseOrderApprovalLevelRel exists)
				return true
			return false

		DerivedPayablesInvoice   is a DerivedField
			type is Boolean
			if (PayablesInvoiceApprovalLevelRel exists)
				return true
			return false

		DerivedContractManagement   is a DerivedField
			type is Boolean
			if (ContractManagementApprovalLevelRel exists)
				return true
			return false

		OldApproverShadow	is a DerivedField
			type is Alpha 100
			if (old Approver entered)
				return old Approver
			else
			if (old ApprovalTeam entered)
				return old ApprovalTeam
			else
			if (old ApprovalPosition entered)
				return old ApprovalPosition
	Conditions
		EscalationApprovalLevelApproverEntered
			restricted
			when (EscalationApprovalLevel.Approver entered)
		EscalationApprovalLevelApprovalTeamEntered
			restricted
			when (EscalationApprovalLevel.ApprovalTeam entered)
		EscalationApprovalLevelPositionEntered
			restricted
			when (EscalationApprovalLevel.ApprovalPosition entered)
		ShowEscalationApprovalLevel
			restricted
			when (EscalationDays > 0
			and	  EscalateTo.SpecificApprovalLevel)
		IsTeam
			restricted
			when (ApprovalTeam entered)
		IsResource
			restricted
			when (Approver entered)
#ifdef module sharedfinance
		TeamHasNoMembers
			restricted
			when (IsTeam
			and   ApprovalTeam.HasNoMembers)
		ApproverResourceInactive
			restricted
			when (Approver entered
			and   !Approver.Active)
		ApproverTeamMemberInactive
			restricted
			when (IsTeam
			and   ApprovalTeam.HasInactiveMembers)
#endif
		NextApprovalLevelExists
			restricted
			when (first NextApprovalLevelRel exists)
		NextApprovalLevelApproverEntered
			restricted
			when (instance count of NextApprovalLevelFirstInSetRel = 1
			and   first NextApprovalLevelNoDocTypeRel.Approver entered)
		NextApprovalLevelApprovalTeamEntered
			restricted
			when (instance count of NextApprovalLevelFirstInSetRel = 1
			and   first NextApprovalLevelNoDocTypeRel.ApprovalTeam entered)
		NextApprovalLevelPositionEntered
			restricted
			when (instance count of NextApprovalLevelFirstInSetRel = 1
			and   first NextApprovalLevelNoDocTypeRel.ApprovalPosition entered)
		EscalateToNextLevel
			restricted
			when (EscalateTo.NextApprovalLevel)
		InvalidNextLevelEscalation
			restricted
			when (EscalateTo.NextApprovalLevel
			and   !NextApprovalLevelNoDocTypeRel exists)
		InvalidSpecificLevelEscalation
			restricted
			when (EscalateTo.SpecificApprovalLevel
			and   EscalationApprovalLevel.ApprovalLevel <= ApprovalLevel)
		InvalidEscalation
			restricted
			when (InvalidNextLevelEscalation
			or    InvalidSpecificLevelEscalation)
		ApprovalPositionInactive
			restricted
			when (ApprovalPosition entered
			and   !ApprovalPosition.Active)
		FirstInSet
			restricted
			when(ResponsibilityMatrixResource = first ResponsibilityMatrixResourceRel.ResponsibilityMatrixResource)

	Relations

		ApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
			Instance Selection
				where (related.DocumentType = LocalDocType)

		NextApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.ApprovalLevel					= DerivedApprovalLevelPlusOne
			Instance Selection
				where (related.DocumentType = DocumentType)

		NextApprovalLevelNoDocTypeRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.ApprovalLevel					= DerivedApprovalLevelPlusOne

		NextApprovalLevelFirstInSetRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.ApprovalLevel					= DerivedApprovalLevelPlusOne
			Instance Selection
				where (related.FirstInSet)

		GlobalLedgerApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByDocumentType
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.DocumentType					= ApprovalTransactionForm.GLJournalEntry
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= ApproverShadow
			Instance Selection
				where (related.ApprovalAmount = ApprovalAmount)

		RequisitionApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByDocumentType
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.DocumentType					= ApprovalTransactionForm.Requisition
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= ApproverShadow
			Instance Selection
				where (related.ApprovalAmount = ApprovalAmount)

		PurchaseOrderApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByDocumentType
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.DocumentType					= ApprovalTransactionForm.PurchaseOrder
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= ApproverShadow
			Instance Selection
				where (related.ApprovalAmount = ApprovalAmount)

		PayablesInvoiceApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByDocumentType
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.DocumentType					= ApprovalTransactionForm.PayablesInvoice
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= ApproverShadow
			Instance Selection
				where (related.ApprovalAmount = ApprovalAmount)
		ContractManagementApprovalLevelRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByDocumentType
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.DocumentType					= ApprovalTransactionForm.ContractManagement
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= ApproverShadow
			Instance Selection
				where (related.ApprovalAmount = ApprovalAmount)

		ResponsibilityMatrixResourceRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= ApproverShadow
			Instance Selection
				where (related.ApprovalAmount = ApprovalAmount)

		ResponsibilityMatrixAllResourcesRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses symbolic key 
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval

		ResponsibilityMatrixResourceOldRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ResponsibilityMatrix			= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= ResponsibilityMatrixApproval
				related.ApprovalLevel					= ApprovalLevel
				related.ApproverShadow					= OldApproverShadow
			Instance Selection
				where (related.ApprovalAmount = old ApprovalAmount)

		FinanceDimensionSecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		 	= FinanceEnterpriseGroup

		AccountingUnitSecurityGroupRel
			one-to-many relation to AccountingUnitSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup

#ifdef module sharedfinance
		FinanceTeamMembersRel
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.FinanceTeam						= ApprovalTeam
#endif

		PositionTeamMembersRel
			one-to-many relation to WorkAssignment
			Field Mapping uses ByPosition
				related.HROrganization					= FinanceEnterpriseGroup.HROrganization
				related.Position						= ApprovalPosition

	Rule Blocks
		UpdateDocumetTypeOnMatrix
			initialize LocalDocumentType
			for each ResponsibilityMatrixAllResourcesRel
				if (!LocalDocumentType.GLJournalEntry
				and each.DocumentType = 1)
					LocalDocumentType.GLJournalEntry = true
				if (!LocalDocumentType.Requisition
				and each.DocumentType = 2)
					LocalDocumentType.Requisition = true
				if (!LocalDocumentType.PurchaseOrder
				and each.DocumentType = 3)
					LocalDocumentType.PurchaseOrder = true
				if (!LocalDocumentType.PayablesInvoice
				and each.DocumentType = 4)
					LocalDocumentType.PayablesInvoice = true
				if (!LocalDocumentType.ContractManagement
				and each.DocumentType = 5)
					LocalDocumentType.ContractManagement = true

			invoke UpdateDocumentType ResponsibilityMatrixApproval
				invoked.PrmDocumentType = LocalDocumentType

		UpdateApproverShadow
			if (Approver entered)
				ApproverShadow = Approver
			else
			if (ApprovalTeam entered)
				ApproverShadow = ApprovalTeam
			else
			if (ApprovalPosition entered)
				ApproverShadow = ApprovalPosition

		RewriteApprovalOrders
			LocalCounter = 1
			while (LocalCounter <=5)
				LocalDocType = LocalCounter
				invoke UpdateApprovalLevel first ApprovalLevelRel
					invoked.NewApprovalLevel = 1
				LocalCounter += 1

		CreateMatrixResources
			if (GlobalLedger and !GlobalLedgerApprovalLevelRel exists)
				invoke Create
					fill in fields from this instance
					invoked.DocumentType	= ApprovalTransactionForm.GLJournalEntry
			if (Requisition and !RequisitionApprovalLevelRel exists)
				invoke Create
					fill in fields from this instance
					invoked.DocumentType	= ApprovalTransactionForm.Requisition
			if (PurchaseOrder and !PurchaseOrderApprovalLevelRel exists)
				invoke Create
					fill in fields from this instance
					invoked.DocumentType	= ApprovalTransactionForm.PurchaseOrder
			if (PayablesInvoice and !PayablesInvoiceApprovalLevelRel exists)
				invoke Create
					fill in fields from this instance
					invoked.DocumentType	= ApprovalTransactionForm.PayablesInvoice
			if (ContractManagement and !ContractManagementApprovalLevelRel exists)
				invoke Create
					fill in fields from this instance
					invoked.DocumentType	= ApprovalTransactionForm.ContractManagement
				
		BuildMatrixSecurityGroup
			if (Approver entered)
				invoke BuildMatrixSecurityGroup first FinanceDimensionSecurityGroupRel
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension		  = LocalFinanceDimension
					invoked.PrmDimensionGroupType	  = LocalDimensionGroupType
					invoked.PrmEmployee			  	  = Approver
				if (LocalDimensionGroupType = 1
				and ResponsibilityMatrixApproval.AccountingUnit entered)
					invoke BuildMatrixSecurityGroup first AccountingUnitSecurityGroupRel
						invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
						invoked.PrmAccountingEntity	  	  = LocalFinanceDimension
						invoked.PrmAccountingUnit  	      = ResponsibilityMatrixApproval.AccountingUnit
						invoked.PrmEmployee			  	  = Approver
#ifdef module sharedfinance
			else
			if (ApprovalTeam entered)
				for each FinanceTeamMembersRel
					LocalEmployee = each.FinanceTeamMember.TeamMember
					invoke BuildMatrixSecurityGroup first FinanceDimensionSecurityGroupRel
						invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension		  = LocalFinanceDimension
						invoked.PrmDimensionGroupType	  = LocalDimensionGroupType
						invoked.PrmEmployee			  	  = LocalEmployee
					if (LocalDimensionGroupType = 1
					and ResponsibilityMatrixApproval.AccountingUnit entered)
						invoke BuildMatrixSecurityGroup first AccountingUnitSecurityGroupRel
							invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
							invoked.PrmAccountingEntity	  	  = LocalFinanceDimension
							invoked.PrmAccountingUnit  	      = ResponsibilityMatrixApproval.AccountingUnit
							invoked.PrmEmployee			  	  = LocalEmployee
#endif
			else
			if (ApprovalPosition entered)
				for each PositionTeamMembersRel
					LocalEmployee = each.Employee
					invoke BuildMatrixSecurityGroup first FinanceDimensionSecurityGroupRel
						invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension		  = LocalFinanceDimension
						invoked.PrmDimensionGroupType	  = LocalDimensionGroupType
						invoked.PrmEmployee			  	  = LocalEmployee
					if (LocalDimensionGroupType = 1
					and ResponsibilityMatrixApproval.AccountingUnit entered)
						invoke BuildMatrixSecurityGroup first AccountingUnitSecurityGroupRel
							invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
							invoked.PrmAccountingEntity	  	  = LocalFinanceDimension
							invoked.PrmAccountingUnit  	      = ResponsibilityMatrixApproval.AccountingUnit
							invoked.PrmEmployee			  	  = LocalEmployee

	Field Rules
		Approver
			constraint (!ApprovalTeam entered)
				"EnterEitherAnApproverOrAnApprovalTeam"
			constraint (!ApprovalPosition entered)
				"EnterEitherAnApproverOrApprovalPosition"
			if (!ApprovalTeam entered and !ApprovalPosition entered)
				required
					"MustEnterEitherAnApproverOrAnApprovalTeam"

		ApprovalTeam
			constraint (!Approver entered)
				"EnterEitherAnApproverOrAnApprovalTeam"
			constraint (!ApprovalPosition entered)
				"EnterEitherAnApprovalTeamOrApprovalPosition"
#ifdef module sharedfinance 
    		constraint (!TeamHasNoMembers)
    			"ApprovalTeam<ApprovalTeam.Description>HasNoMembers"
    		constraint (!ApproverTeamMemberInactive)
    			"TeamHasMemberThatIsInactive"
#endif


		EscalationDays
			if (EscalationDays entered)
				constraint (EscalationDays >= 0)
					"CannotEnterNegativeEscalationDays"

		EscalationHours
			constraint (EscalationHours >= 0)
				"CannotEnterNegativeEscalationHours"

		EscalateTo
			if (EscalationHours entered)
				default to 1

			constraint (EscalationHours entered)
				"CannotSetAnEscalateToWithoutEnteringEscalationHours"
				
			if (EscalateTo.NextApprovalLevel)
				if (invoking action != "ResponsibilityMatrixApproval.Update")
					constraint (NextApprovalLevelNoDocTypeRel exists)
						"CannotSetToNextApproval;NoNextApprovalLevelExists"

		ApprovalLevel
			autosequence using ByDocumentType

		EscalationApprovalLevel
			if (EscalateTo.SpecificApprovalLevel)
				required
					"EscalationApprovalLevelIsRequiredWhenEscalatingToASpecificApprovalLevel"
				constraint (EscalationApprovalLevel.ApprovalLevel > ApprovalLevel)
					"EscalationApprovalLevel<EscalationApprovalLevel.ApprovalLevel>MustBeGreaterThanCurrentApprovalLevel<ApprovalLevel>"

		ApprovalCurrency
			initial value is ResponsibilityMatrixApproval.AccountingEntity.FunctionalCurrency
			default to ResponsibilityMatrixApproval.AccountingEntity.FunctionalCurrency

		DocumentType
			required
				"DocumentTypeIsRequired"

	Attach Rules

	Sets
		ByApprovalLevel
			duplicates
			Sort Order
 				FinanceEnterpriseGroup
				ResponsibilityMatrix
 				ResponsibilityMatrixApproval
 				ApprovalLevel
				ApproverShadow

		ByDocumentType
			Sort Order
				FinanceEnterpriseGroup
				ResponsibilityMatrix
 				ResponsibilityMatrixApproval
				DocumentType
				ApprovalLevel
				ApproverShadow

		ByApproverShadow
			Sort Order
				FinanceEnterpriseGroup
				ResponsibilityMatrix
 				ResponsibilityMatrixApproval
				DocumentType
				ApproverShadow
 				
	Actions
		Create is a Create Action
			restricted
			Entrance Rules
				include UpdateApproverShadow
			Exit Rules
				LocalDocType = DocumentType
				invoke UpdateApprovalLevel first ApprovalLevelRel
					invoked.NewApprovalLevel = 1
				if (FinanceEnterpriseGroup.ResponsibilityMatrixSecurity)
					invoke BuildMatrixSecurity
						invoked.PrmFinanceEnterpriseGroup 		= FinanceEnterpriseGroup
						invoked.PrmResponsibilityMatrixResource = ResponsibilityMatrixResource
		
		Update is an Update Action
			Entrance Rules
				include UpdateApproverShadow
			Action Rules
				if (EscalateTo changed
				and (EscalateTo not entered
				or   EscalateTo.NextApprovalLevel))
					EscalationApprovalLevel = 0	
			Exit Rules
				if (Approver changed 
				or ApprovalTeam changed 
				or ApprovalPosition changed 
				or ApprovalAmount changed)
					for each ResponsibilityMatrixResourceOldRel
						invoke UpdateApprovalLevel each
							invoked.Approver 			= Approver
							invoked.ApprovalTeam 		= ApprovalTeam
							invoked.ApprovalPosition 	= ApprovalPosition
							invoked.ApprovalAmount 		= ApprovalAmount
							invoked.ApproverShadow 		= ApproverShadow
					if (FinanceEnterpriseGroup.ResponsibilityMatrixSecurity)
						invoke BuildMatrixSecurity
							invoked.PrmFinanceEnterpriseGroup 		= FinanceEnterpriseGroup
							invoked.PrmResponsibilityMatrixResource = ResponsibilityMatrixResource
				else
					include CreateMatrixResources
				if (GlobalLedgerChanged)
					invoke DeleteMatrix GlobalLedgerApprovalLevelRel
				if (RequisitionChanged)
					invoke DeleteMatrix RequisitionApprovalLevelRel
				if (PurchaseOrderChanged)
					invoke DeleteMatrix PurchaseOrderApprovalLevelRel
				if (PayablesInvoiceChanged)
					invoke DeleteMatrix PayablesInvoiceApprovalLevelRel
				if (ContractManagementChanged)
					invoke DeleteMatrix ContractManagementApprovalLevelRel
				include UpdateDocumetTypeOnMatrix
				include RewriteApprovalOrders

		Delete is a Delete Action
			Exit Rules
				invoke DeleteMatrix ResponsibilityMatrixResourceRel
				include RewriteApprovalOrders
				include UpdateDocumetTypeOnMatrix

		UpdateApprovalLevel is an Update Action
			restricted

		DeleteMatrix is a Delete Action
			restricted

		CreateApprovers is a Create Action
			Entrance Rules
				include UpdateApproverShadow
				DocumentType = DerivedDocument
			Exit Rules
				include CreateMatrixResources
				LocalDocType = DocumentType
				invoke UpdateApprovalLevel first ApprovalLevelRel
					invoked.NewApprovalLevel = 1
				include UpdateDocumetTypeOnMatrix
				if (FinanceEnterpriseGroup.ResponsibilityMatrixSecurity)
					invoke BuildMatrixSecurity
						invoked.PrmFinanceEnterpriseGroup 		= FinanceEnterpriseGroup
						invoked.PrmResponsibilityMatrixResource = ResponsibilityMatrixResource

		FillMatrixResourceDocumentType	is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where ((PrmFinanceEnterpriseGroup not entered
				or		FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup)
				and 	DocumentType not entered)
			Action Rules
				Instance Rules
					DocumentType = ApprovalTransactionForm.GLJournalEntry

		BuildMatrixSecurity	is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmResponsibilityMatrixApproval	is like ResponsibilityMatrixApproval
				PrmResponsibilityMatrixResource is like ResponsibilityMatrixResource

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required

			Instance Selection
				where ((FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and     PrmResponsibilityMatrixResource entered
				and     ResponsibilityMatrixResource = PrmResponsibilityMatrixResource)
				or 	   (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and     PrmResponsibilityMatrixApproval entered
				and     ResponsibilityMatrixApproval = PrmResponsibilityMatrixApproval)
				or 	   (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and     PrmResponsibilityMatrixApproval not entered
				and     PrmResponsibilityMatrixResource not entered))
				
			Action Rules
				Set Rules
					Exit Rules
						invoke RebuildGroups FinanceDimensionSecurityGroup
							invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				Instance Rules
					if (FirstInSet)
						if (ResponsibilityMatrixApproval.AccountingEntity entered)
							LocalDimensionGroupType = 1
							LocalFinanceDimension   = ResponsibilityMatrixApproval.AccountingEntity
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension1 entered)
							LocalDimensionGroupType = 3
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension1
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension2 entered)
							LocalDimensionGroupType = 4
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension2
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension3 entered)
							LocalDimensionGroupType = 5
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension3
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension4 entered)
							LocalDimensionGroupType = 6
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension4
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension5 entered)
							LocalDimensionGroupType = 7
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension5
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension6 entered)
							LocalDimensionGroupType = 8
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension6
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension7 entered)
							LocalDimensionGroupType = 9
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension7
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension8 entered)
							LocalDimensionGroupType = 10
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension8
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension9 entered)
							LocalDimensionGroupType = 11
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension9
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.FinanceDimension10 entered)
							LocalDimensionGroupType = 12
							LocalFinanceDimension   = ResponsibilityMatrixApproval.FinanceDimension10
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.Project entered)
							LocalDimensionGroupType = 13
							LocalFinanceDimension   = ResponsibilityMatrixApproval.Project
							include BuildMatrixSecurityGroup
						if (ResponsibilityMatrixApproval.GeneralLedgerChartAccount entered)
							LocalDimensionGroupType = 14
							LocalFinanceDimension   = ResponsibilityMatrixApproval.GeneralLedgerChartAccount
							include BuildMatrixSecurityGroup
