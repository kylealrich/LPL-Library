ProcurementCardNumber is a BusinessClass
    owned by po
    prefix is PDN

    Ontology
        symbolic key is ProcurementCardNumber
			classic set name is PDNSET1

    Patterns
        implements LightweightAuditing
        disable AuditIndex

	Persistent Fields
		Active
        ProcurementCardNumberEffectiveDate  is Date
        ProcurementCardNumberExpirationDate is Date
 		CardType
		TaxOption
		
	Context Fields
		ProcurementGroup
		ProcurementCardProgram
		
	Derived Fields
				
		MaskProcurementCardNumberDisplay	is a DerivedField
			type is Boolean
			restricted

			if (ProcurementCardProgramRel exists)
				return ProcurementCardProgramRel.MaskProcurementCardNumberDisplay  

			return ProcurementGroupRel.MaskProcurementCardNumberDisplay 
			 
		MaskProcurementCardNumberEntry		is a DerivedField
			type is Boolean
			restricted

			if (ProcurementCardProgramRel exists)
				return ProcurementCardProgramRel.MaskProcurementCardNumberEntry  

			return ProcurementGroupRel.MaskProcurementCardNumberEntry 
			 
		DisplayProcurementCardNumber		is a DerivedField
			type is like ProcurementCardNumber

			if (MaskProcurementCardNumberDisplay)
				return "************" + ProcurementCardNumber[13:16]

			return ProcurementCardNumber
	
	Conditions
	
		ActivatedCardForProgram
			restricted
			when (ActivatedProcurementCardRequestsRel exists)
	
	Relations
		ProcurementCardRequestRel
			one-to-many relation to ProcurementCardRequest
			Field Mapping uses Set2
				related.ProcurementCardNumber	=	ProcurementCardNumber
			Instance Selection
				where (related.Status.Activated
				or	   related.Status.Approved)

		ActivatedProcurementCardRequestsRel
			one-to-many relation to ProcurementCardRequest
			Field Mapping uses Set2
				related.ProcurementCardNumber	= ProcurementCardNumber
				related.ProcurementCardProgram  = ProcurementCardProgram 
			Instance Selection
				where (related.Status.Activated)
		
		ProcurementCardRequestsForUserRel
			one-to-many relation to ProcurementCardRequest
			Field Mapping uses Set2
				related.ProcurementCardNumber	=	ProcurementCardNumber
			Instance Selection
				where (related.ProcurementCardUser 		= actor.agent(Employee).Employee
				or     related.ProcurementCardProxyUser = actor.agent(Employee).Employee)

		ProcurementCardProgramRel	
			one-to-one relation to ProcurementCardProgram
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
				related.ProcurementCardProgram 	= ProcurementCardProgram

		ProcurementGroupRel
			one-to-one relation to ProcurementGroup
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup

		BusinessGroupRel
			one-to-many relation to BusinessGroup
			Field Mapping uses symbolic key
			Instance Selection
				where (related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup)

	Field Rules
		ProcurementCardNumber
			must be numeric
			
			constraint (ProcurementCardNumber matches "^\d{16} *")
				"CardNumberMustBe16DigitsInLength"
	
		ProcurementCardNumberEffectiveDate
			constraint (ProcurementCardNumberEffectiveDate <= ProcurementCardNumberExpirationDate)
				"EffectiveDateMustBeLessThanOrEqualToExpirationDate"
				
		ProcurementCardNumberExpirationDate
			constraint (ProcurementCardNumberExpirationDate >= current corporate date)
				"ExpirationDateMustBeGreaterThanEqualToCurrentDate"
				
	Attach Rules
		constraint (Active)
			"ProcurementCardNumberIsNotActive"

	Actions 
		Create is a Create Action 

		Update is an Update Action 

		Delete is a Delete Action 
			Entrance Rules
				constraint (ProcurementCardRequestRel not exists)
					"CannotDelete;AtLeastOneAssociatedApprovedRequestExists" 
