SupplierQuestionResponse is a BusinessClass
    owned by procurement
    prefix is SPQ

    Ontology
    	part of Supplier
    		relative key is SupplierGroupQuestion
    
    Patterns
        implements TemplateDriven by SupplierGroupQuestion
        	create instance
        		when (SupplierQuestionAnswer entered)
        		
    Persistent Fields
		SupplierQuestionAnswer
    	Attachment

   
    Derived Fields
    	SupplierResponseAnswers is a ConditionalField
    		type is Alpha size 250
    		if (SupplierGroupQuestion.ResponseType.Text)
    			SupplierQuestionAnswer.TextValue
    		else
    		if (SupplierGroupQuestion.ResponseType.Number)
    			SupplierQuestionAnswer.NumericValue
    		else 
    		if (SupplierGroupQuestion.ResponseType.Date)
    			DerivedDate
    		else
    		if (SupplierGroupQuestion.ResponseType.List)
    			SupplierQuestionAnswer.SupplierGroupQuestionValue
    		else
    		if (SupplierGroupQuestion.ResponseType.YesNo)
    			if (SupplierQuestionAnswer.YesNoValue.Yes)
    				YesText
    			else
    				NoText
    		else
    			YesNoText
    			
    	YesNoText is a ConditionalField
			type is Alpha size 255
			restricted
			if (SupplierQuestionAnswer.YesNoText.YesNo.Yes)
				YesText + ". " + SupplierQuestionAnswer.YesNoText.Text
			else
				NoText + ". " + SupplierQuestionAnswer.YesNoText.Text

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"

		DerivedDate is a StringField
			type is Alpha 10
			restricted
			SupplierQuestionAnswer.DateValue month
			"/"
			SupplierQuestionAnswer.DateValue day
			"/"
			SupplierQuestionAnswer.DateValue year
			
		ResponseRequiredMessage is a MessageField
			restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
        	restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
        	restricted
            "YesOr_NoRequired;AttachmentRequiredIfAnswerIs_Yes"
            
        NoAttachmentMessage is a MessageField
        	restricted
            "YesOr_NoRequired;AttachmentRequiredIfAnswerIs_No"
            
        YesOrNoAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIs_Yes"
            
        NoAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIs_No"
            
        AttachmentAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;BothTextAndAttachmentAreRequiredIfAnswerIs_Yes"
            
        NoAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;BothTextAndAttachmentAreRequiredIfAnswerIs_No"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
            restricted
            if (SupplierGroupQuestion.OnlyRequiredForFuture < Supplier.DateRegistered)
	            if (SupplierGroupQuestion.ResponseRules.ResponseRequired
	            or  SupplierGroupQuestion.YesNoResponseRules.ResponseRequired)
	                ResponseRequiredMessage        
	            else
	            if (SupplierGroupQuestion.ResponseRules.ResponseRequiredAndAttachmentRequired
	            or  SupplierGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
	                ResponseAndAttachmentRequiredMessage
	            else
	            if (SupplierGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
	                YesAttachmentMessage
	            else
	            if (SupplierGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
	                NoAttachmentMessage
	            else
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
	                YesOrNoAndTextMessage
	            else
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
	                AttachmentOrTextMessage
	            else
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
	                YesAttachmentOrTextMessage
	            else
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
	                NoAttachmentOrTextMessage
	            else 
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
	                AttachmentAndTextMessage
	            else
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
	                YesAttachmentAndTextMessage
	            else
	            if (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
	                NoAttachmentAndTextMessage
	            else 
	            	blank
            else
                blank

    Conditions
    
        CanRespondToQuestion
        	restricted
        	when (Supplier.IsUpdatable
        	and   ActiveSupplierOrNotSupplier)
        
        ActiveSupplierOrNotSupplier
			restricted
			when ((!actor.agent(SupplierSourceId).SupplierSourceId.InactiveAtSomeLevel
			and   actor.agent(SupplierSourceId).SupplierSourceId exists)
			or    actor.agent(SupplierSourceId).SupplierSourceId !exists)
        
        HasResponseAttachment
        	restricted
            when (Attachment entered)
        
        AttachmentAlwaysRequired   
        	restricted
            when (SupplierGroupQuestion.AlwaysRequireResponseAttachment
            and   SupplierGroupQuestion.OnlyRequiredForFuture < Supplier.DateRegistered
            and  (SupplierGroupQuestion.NonYesNoResponseType
            or    SupplierGroupQuestion.ResponseType.YesNo))
            
       	QuestionAnswerEntered
       		restricted
       		when (SupplierQuestionAnswer entered)
       	
       	FromRequiredQuestion
       		restricted
       		when (SupplierGroupQuestion.ResponseRequired
       		and   SupplierGroupQuestion.OnlyRequiredForFuture < Supplier.DateRegistered)
       		
       	UnansweredRequired
       		restricted
       		when ((FromRequiredQuestion
       		and   !QuestionAnswerEntered)
       		or    (AttachmentAlwaysRequired
       		and   !HasResponseAttachment))
       		
       	Required
       		restricted
       		when (FromRequiredQuestion
       		or    AttachmentAlwaysRequired)
       		
    Relations
    	
    	SupplierGroupExtensionRel
			one-to-one relation to SupplierGroupExtension
			Field Mapping uses symbolic key
				related.SupplierGroup   = SupplierGroup
 		SupplierGroupQuestionValueRel
			one-to-many relation to SupplierGroupQuestionValue
			Field Mapping uses ByDisplayOrder
				related.SupplierGroup   		= SupplierGroup
				related.SupplierGroupQuestion	= SupplierGroupQuestion

    Field Rules
    	SupplierQuestionAnswer
            if (SupplierGroupQuestion.OnlyRequiredForFuture < Supplier.DateRegistered)
	            if (SupplierGroupQuestion.ResponseRequired)
	                required
	                    "MustAnswerAllRequiredQuestions"
	            if (SupplierGroupQuestion.ResponseType.YesNoText
	            and !HasResponseAttachment
	            and (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
	            or  (SupplierQuestionAnswer.YesNoText.YesNo.Yes
	            and  SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
	            or  (SupplierQuestionAnswer.YesNoText.YesNo.No
	            and  SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
	                constraint (SupplierQuestionAnswer.YesNoText.Text entered)
	                    "MustEnterEitherTextOrAnAttachmentForQuestion<SupplierGroupQuestion.QuestionText>"
	                    
	            if (SupplierGroupQuestion.ResponseType.YesNo
	            and (SupplierQuestionAnswer.YesNoValue.Yes
	            and  SupplierGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
	            or  (SupplierQuestionAnswer.YesNoValue.No
	            and  SupplierGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SupplierGroupQuestion.QuestionText>"    
	            
	            if (AttachmentAlwaysRequired
	            and !SupplierGroupQuestion.ResponseType.YesNoText)
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SupplierGroupQuestion.QuestionText>"
	                       
	            if  (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
	            or   SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SupplierQuestionAnswer.YesNoText.YesNo.Yes
	            and  SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SupplierQuestionAnswer.YesNoText.YesNo.No
	            and  SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (SupplierQuestionAnswer.YesNoText.Text entered)
	                    "MustEnterTextForQuestion<SupplierGroupQuestion.QuestionText>"
	                    
	            if  (SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SupplierQuestionAnswer.YesNoText.YesNo.Yes
	            and  SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SupplierQuestionAnswer.YesNoText.YesNo.No
	            and  SupplierGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SupplierGroupQuestion.QuestionText>"
        Attachment
            if (SupplierGroupQuestion.AlwaysRequireResponseAttachment
            and SupplierGroupQuestion.OnlyRequiredForFuture < Supplier.DateRegistered)
                required
                    "MustAttachADocumentForAllQuestionsRequiringAnAttachment"
            if (!SupplierQuestionAnswer entered)
                cannot be entered
                    "MustAnswerQuestionToAttachADocument"

    Rule Blocks
		UpdateValidationEmail
			send email
				to SupplierGroup.NotificationEmailAddress
				from SupplierGroup.AdminEmailAddress
				cc Supplier.PrimaryContact.EmailAddress
				subject "<SupplierGroupExtensionRel.FinalUpdatedValidationEmailSubject>"
				Contents
					"<SupplierGroupExtensionRel.FinalUpdatedValidationEmailContent>"
					"QuestionInformationWasModifiedOn<update stamp>"
					
    Actions
		Create is a Create Action
     		valid when (CanRespondToQuestion)
				   								
   			Exit Rules
   				if (Supplier.SupplierStatus.Validated
				and SupplierGroup.RequireUpdatedSupplierValidation)
					invoke StatusUpdate Supplier
 					
 					include UpdateValidationEmail
 											
     	Update is an Update Action
			valid when (CanRespondToQuestion)
				   								
			Exit Rules
				if (Supplier.SupplierStatus.Validated
				and SupplierGroup.RequireUpdatedSupplierValidation
				and SupplierQuestionAnswer changed)
					invoke StatusUpdate Supplier
 					
 					include UpdateValidationEmail
     						
     	Delete is a Delete Action
			valid when (CanRespondToQuestion)
			Exit Rules
     			if (Supplier.SupplierStatus.Validated
				and SupplierGroup.RequireUpdatedSupplierValidation)
					invoke StatusUpdate Supplier
 					
 					include UpdateValidationEmail

		CreateSupplierQuestionsUsingSupplierQuestionResponse is a Set Action
			restricted
			Parameters
				PrmSupplierGroup				is a SupplierGroup			

			Parameter Rules
				PrmSupplierGroup
					required

			Local Fields
				NewQuestion is a SupplierQuestions view

			Instance Selection
				where (SupplierGroup = PrmSupplierGroup)

			Sort Order
				SupplierGroup

			Action Rules

				Empty Set Rules
					invoke CreateSupplierQuestionsFromSupplierGroupQuestions SupplierGroupQuestion				
						invoked.PrmSupplierGroup	= PrmSupplierGroup

				SupplierGroup Set Rules
					Exit Rules
						invoke CreateSupplierQuestionsFromSupplierGroupQuestions SupplierGroupQuestion
							invoked.PrmSupplierGroup	= SupplierGroup

				Instance Rules
					invoke Create SupplierQuestions
						assign result to NewQuestion
						invoked.SupplierGroup  			= PrmSupplierGroup
						invoked.Supplier				= Supplier
						invoked.SupplierGroupQuestion	= SupplierGroupQuestion
						fill in fields from SupplierGroupQuestion
							except invoked.DisplayOrder

					if (SupplierGroupQuestion.ResponseType.List)
						for each SupplierGroupQuestionValueRel
							invoke CreateDisplay SupplierQuestionsListValue
								invoked.SupplierGroup				= PrmSupplierGroup
								invoked.Supplier					= Supplier
								invoked.SupplierQuestions			= NewQuestion.SupplierQuestions
								invoked.SupplierQuestionsListValue	= each.SupplierGroupQuestionValue
								fill in fields from each

					invoke Create SupplierQuestionsSetResponse
						invoked.SupplierGroup  											= PrmSupplierGroup
						invoked.Supplier												= Supplier
						invoked.SupplierQuestions										= NewQuestion.SupplierQuestions
						invoked.SupplierQuestionsSetAnswer.TextValue					= SupplierQuestionAnswer.TextValue
						invoked.SupplierQuestionsSetAnswer.NumericValue					= SupplierQuestionAnswer.NumericValue
						invoked.SupplierQuestionsSetAnswer.DateValue					= SupplierQuestionAnswer.DateValue
						invoked.SupplierQuestionsSetAnswer.SupplierQuestionsListValue	= SupplierQuestionAnswer.SupplierGroupQuestionValue
						invoked.SupplierQuestionsSetAnswer.YesNoValue					= SupplierQuestionAnswer.YesNoValue
						invoked.SupplierQuestionsSetAnswer.YesNoText					= SupplierQuestionAnswer.YesNoText
						invoked.Attachment												= Attachment
						invoked.FromConditionalQuestionAllowance                        = true
