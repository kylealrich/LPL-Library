ReportSubCategory is a BusinessClass
    owned by report
    prefix is RSCAT

    Ontology
    	symbolic key is ReportSubCategory

	Patterns
        disable AsOfDateProcessing
        disable EffectiveDated
        





    Persistent Fields
    	Description is Alpha size up to 256
    		translatable
    	Inactive is Boolean
  
    Actions
    	InternalCreate is a Create Action
    		restricted
    		
    	Create is a Create Action
    		restricted 
    		
    		Entrance Rules
    			constraint (not Inactive)
    				"ReportSubCategoryIsInactiveAndCannotBeUsed" 
    			
    			if (OtherSubCatCaseInSensitiveRel exists)
					confirmation required
						"ReportSubCategoryAlreadyExistsFor_'<ReportCategory>.<ReportSubCategory>'_withDifferentCase.See_'ReportCategory_<first OtherSubCatCaseInSensitiveRel.ReportSubCategory>'.Continue?"
    		
    	Update is an Update Action
    		restricted
	
		Delete is a Delete Action
    		restricted
    		
    	Purge is a Purge Action
    		restricted
    		
    	Activate is an Instance Action
    		valid when (Inactive)
    		
    		Action Rules
    			Inactive = false
    			
    	Deactivate is an Instance Action
    		valid when (not Inactive)
    		
    		Action Rules
    			Inactive = true
    			
    	PurgeSubCategories is a Set Action
    		restricted
    		
			Sort Order is primary
				
			Action Rules
				Instance Rules
					if (not ReportDefinitionRel exists) 
						invoke Purge
    			
    Relations
		OtherSubCatCaseInSensitiveRel
			default label is "LikeSubCategories"
			one-to-many relation to ReportSubCategory 
			Field Mapping uses symbolic key
				related.ReportCategory = ReportCategory
			Instance Selection
				where (related.ReportSubCategory != ReportSubCategory
				and    related.ReportSubCategory like ReportSubCategory)
				
		ReportDefinitionRel
			default label is "ReportDefinitions"
			one-to-many relation to ReportDefinition
			delete restricted
			Field Mapping uses ByCategory
				related.Category = ReportCategory
				related.SubCategory = ReportSubCategory
