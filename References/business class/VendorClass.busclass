VendorClass is a BusinessClass
    owned by ap
    prefix is VCL
    classic name is APVENCLASS

    Ontology
        symbolic key is VendorClass
            classic set name is VCLSET1
            classic name is VEN-CLASS

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        PaymentPriority
            classic name is VEN-PRIORITY
        Description
        ZeroPaymentAllowed     is Boolean
            classic name is ZERO-CHECK
        PostingOption
            classic name is POST-OPTION
        DistributionCode       is a PayablesDistributionCode
            classic name is DIST-CODE
        MaximumPaymentAmount   is an InternationalAmount
            classic name is MAX-PMT-AMT
        MaximumPaymentHoldCode is a PayablesHoldCode
            classic name is MAX-PMT-HOLD
        IncomeCode             is a PayablesIncomeCode
        Currency
            classic name is CURRENCY-CODE
        TaxExemptCode          is AlphaUpper size 1
            classic name is TAX-EXEMPT-CD
        TaxCode
        CashCode
        BankTransactionCode
            classic name is BANK-INST-CODE
        ZeroPaymentCode        is a BankTransactionCode
            classic name is ZERO-PMT-CODE
        Enclosure              is Boolean
        TaxUsageCode
            classic name is TAX-USAGE-CD
        ValidatePurchaseOrder  is AlphaUpper size 1
            classic name is VALIDATE-PO
            States
                CompanyDefault   value is blank
                Yes              value is "Y"
                No               value is "N"
        RequirePurchaseOrder   is AlphaUpper size 1
            classic name is REQUIRE-PO
            States
                CompanyDefault   value is blank
                Yes              value is "Y"
                No               value is "N"
        MatchPrepayment        is Boolean
            classic name is MTCH-PREPAY-FL
        MatchPrepaymentMethod
            classic name is MTCH-PREPAY-MT
        MatchTable
        RuleGroup              is a MatchRule
        CostComponentMatch	   is Boolean
		MSCMSync			   is Boolean
        InterfaceVendor        is Boolean
            classic name is EAM-VEN-FL
            default label is "CreateExpenseManagementUser"
        MinimumPaymentHoldCode is a PayablesHoldCode
            classic name is MIN-PMT-HOLD
        MinimumPaymentAmount   is an InternationalAmount
            classic name is MIN-PMT-AMT
        InvoiceRoutingCategory
        TaxOverride				is Boolean
        	classic name is TAX-OVERRIDE
    	RestrictToOneInvoice 	is Boolean
    		default label is "RestrictToOneInvoicePerVendor" 
		DefaultTaxIndicator		is a TaxIndicator				
		AllowDuplicateInvoiceByDate				is Boolean		
		
	Context Fields
		FinanceResource

	Local Fields
		LocalString							is Alpha 200
		I1									is Numeric 5
		I2									is Numeric 5
		LocalInvoiceTemplate				is like PayablesInvoiceTemplate
			
	Derived Fields
		OneTimeDescription is a MessageField
			restricted
			"OneTimeVendorClass"
	
		DerivedExcludedInvoiceTemplates is a DerivedField
			type is Alpha 50
			initialize LocalString
			I1 = instance count of InvoiceTemplateExclusionRel
			I2 = 1
			if (I1 = 0)
				initialize LocalString
			else				
				for each InvoiceTemplateExclusionRel
					LocalString += each.DerivedTemplate
		
					if (I2 > 10)
						LocalString += "..."
						end for each
						
					if (I1 > I2)
						LocalString += "; "
						
					I2 += 1
	
			return LocalString

		DerivedExcludeBasic is a DerivedField
		    type is Boolean
		    LocalInvoiceTemplate = 0
		    if (LocalInvoiceTemplateExclusionRel exists)
		    	return true
		    	
		DerivedExcludeTaxInvoice is a DerivedField
		    type is Boolean
		    LocalInvoiceTemplate = 1
		    if (LocalInvoiceTemplateExclusionRel exists)
		    	return true
		    	
		DerivedExcludeFreight is a DerivedField
		    type is Boolean
		    LocalInvoiceTemplate = 3
		    if (LocalInvoiceTemplateExclusionRel exists)
		    	return true
		    	
		DerivedExcludeMatchInvoice is a DerivedField
		    type is Boolean
		    LocalInvoiceTemplate = 4
		    if (LocalInvoiceTemplateExclusionRel exists)
		    	return true

		DerivedExcludeServiceContract is a DerivedField
		    type is Boolean
		    LocalInvoiceTemplate = 5
		    if (LocalInvoiceTemplateExclusionRel exists)
		    	return true

		CreateMessage is a LabelField
			restricted
			"CreateVendorClass"
		
		FormLabel is a LabelField
			restricted
			"VendorClass:_"

		RepresentativeText is a StringField
			type is Text
			default label is "VendorClass"
			VendorClass " - " Description
		
		DerivedVendorClassTitle	is a DerivedField
			type is Alpha size up to 132
			default label is "VendorClass"
			return RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return FormLabel + DerivedVendorClassTitle
			else
				return CreateMessage

	Field Rules
		TaxUsageCode
			if (TaxUsageCode entered)
				constraint (TaxCode entered)
					"Tax_\Usage_\CodeNotValidIfNo_\Tax_\CodeIsEntered"        
				constraint (TaxUsageCodeRel exists)
					"AP99.813"

		MaximumPaymentHoldCode
			constraint (MaximumPaymentHoldCode.HoldOnInvoice = "T")
				"MustBeATemporaryInvoiceHoldType"                          

		MinimumPaymentHoldCode
			constraint (MinimumPaymentHoldCode.HoldOnInvoice = "T")
				"MustBeATemporaryInvoiceHoldType"                          

		RuleGroup
			constraint (MatchMessageToleranceRel exists)
				"RuleGroupDoesNotExist"                                    

		ZeroPaymentAllowed
			if (not ZeroPaymentAllowed)
				constraint (ZeroPaymentCode not entered)
					"ZeroPaymentAllowedMust_=TrueIfZeroPaymentCodeEntered"	
			if (ZeroPaymentAllowed changed)		
				constraint (ActivePayablesProcessingMonitorRel !exists)
					"CannotUpdate_\Zero_\Payment_\Allowed;CashRequirementsInProcess"

		MaximumPaymentAmount
			if (MaximumPaymentAmount entered)
				constraint (MaximumPaymentHoldCode entered)
					"HoldCodeRequiredWithMaximumPaymentAmt"                   
			if (MaximumPaymentAmount changed)		
				constraint (ActivePayablesProcessingMonitorRel !exists)
					"CannotUpdate_\Maximum_\Payment_\Amount;CashRequirementsInProcess"

		MinimumPaymentAmount
			if (MinimumPaymentAmount entered)
				constraint (MinimumPaymentHoldCode entered)
					"HoldCodeRequiredWithMinimumPaymentAmt"                   
			if (MinimumPaymentAmount changed)		
				constraint (ActivePayablesProcessingMonitorRel !exists)
					"CannotUpdate_\Minimum_\Payment_\Amount;CashRequirementsInProcess"

		MatchPrepayment
			if (MatchPrepayment  = "Y")
				constraint (MatchPrepaymentMethod entered)
					"Prepay_\MethodCannotBe_\NoneIf_\Prepay_\FlagIs_\Yes"     

			if (MatchPrepayment  = "N")
				constraint (MatchPrepaymentMethod not entered)
					"Prepay_\FlagMustBe_\YIf_\Prepay_\MethodIs_1Or_2"         

		ZeroPaymentCode
			constraint (ZeroPaymentCode.BankTransactionType.CashPayment)
				"ZeroPaymentCodeMustBeACashType"                           

		MatchTable
			if (MatchTable entered)
				constraint (!MatchTable.MatchTableLevel.CompanyOnly)
					"MatchTableLevelCannotBeCompanyOnly"					

		PaymentPriority				
			default to "1"

	Conditions
		RecordExists
			restricted
			when (VendorClass exists)
		
		FinanceResourceInContext
			restricted
			when (FinanceResource in context)
			
 		SecurityGroupAllowsAccess
			when ((actor.context.VendorClassSecurityGroup = "") 
			or     (actor.context.VendorClassSecurityGroup != ""
			and     VendorClassSecurityGroupDetailRel exists))

	Relations
		TaxUsageCodeRel
            one-to-many relation to TaxUsageCode
            Field Mapping uses Set2
                related.TaxUsageCode	= TaxUsageCode

		MatchMessageToleranceRel
            one-to-many relation to MatchMessageTolerance
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= VendorGroup.BusinessGroup.FinanceEnterpriseGroup
                related.MatchRule				= RuleGroup

		VendorClassSecurityGroupDetailRel
			one-to-one relation to VendorClassGroupDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= VendorGroup.BusinessGroup.FinanceEnterpriseGroup
				related.VendorClassGroup		= actor.context.VendorClassSecurityGroup.FinanceDimensionStructure
				related.VendorGroup         	= VendorGroup
				related.VendorClass             = VendorClass

		APClerkSmartReconVendorClassRel
			one-to-many relation to APClerkSmartReconVendorClass
			Field Mapping uses ByVendorClass
				related.VendorClass             = VendorClass
			Instance Selection
				where (related.VendorGroup      = VendorGroup)

		VendorClassGroupDetailRel
			one-to-many relation to VendorClassGroupDetail
			Field Mapping uses ByDetail
				related.VendorClass             = VendorClass
				related.FinanceEnterpriseGroup  = VendorGroup.BusinessGroup.FinanceEnterpriseGroup
				related.VendorGroup             = VendorGroup
		
		InvoiceTemplateExclusionRel
			one-to-many relation to InvoiceTemplateExclusion
			Field Mapping uses Set1
            	related.VendorGroup							= VendorGroup
                related.VendorClass							= VendorClass
                related.Vendor								= blank

		LocalInvoiceTemplateExclusionRel
			one-to-one relation to InvoiceTemplateExclusion
			Field Mapping uses Set1
            	related.VendorGroup							= VendorGroup
                related.VendorClass							= VendorClass
                related.Vendor								= blank
                related.Template							= LocalInvoiceTemplate

		ActivePayablesProcessingMonitorRel		
			one-to-many relation to PayablesProcessingMonitor
			Field Mapping uses symbolic key	
				related.VendorGroup							= VendorGroup

	Actions
		Create is a Create Action
		
		CreateOneTimeClass is a Create Action
			restricted


			Action Rules

				PaymentPriority	= 1
				Description 	= OneTimeDescription				

		Update is an Update Action

		Delete is a Delete Action
		
			Action Rules
				for each VendorClassGroupDetailRel
					invoke Delete each

				for each APClerkSmartReconVendorClassRel
					invoke Delete each
					
		CreateAPClerkSmartReconVendorClass is an Instance Action
			default label is "AddVendorClass"
			valid when (FinanceResourceInContext)
			Action Rules
				invoke Create APClerkSmartReconVendorClass
					invoked.HROrganization					= FinanceResource.HROrganization 
					invoked.FinanceResource					= FinanceResource.FinanceResource
					invoked.VendorGroup						= VendorGroup
					invoked.VendorClass						= VendorClass
														
		ExcludeInvoiceTemplate is an Instance Action
			Parameters
				Basic			is Boolean
				TaxInvoice		is Boolean
				MatchInvoice	is Boolean
				ServiceContract is Boolean
				Freight			is Boolean
				
			Parameter Rules
				Basic
					initial value is DerivedExcludeBasic
				TaxInvoice
					initial value is DerivedExcludeTaxInvoice
				MatchInvoice
					initial value is DerivedExcludeMatchInvoice
				ServiceContract
					initial value is DerivedExcludeServiceContract
				Freight
					initial value is DerivedExcludeFreight
			Action Rules
			    LocalInvoiceTemplate = 0
			    if (Basic)
			        if (LocalInvoiceTemplateExclusionRel not exists)
			            invoke Create LocalInvoiceTemplateExclusionRel
		        else
			        if (LocalInvoiceTemplateExclusionRel exists)
			            invoke Delete LocalInvoiceTemplateExclusionRel

			    LocalInvoiceTemplate = 1
			    if (TaxInvoice)
			        if (LocalInvoiceTemplateExclusionRel not exists)
			            invoke Create LocalInvoiceTemplateExclusionRel
		        else
			        if (LocalInvoiceTemplateExclusionRel exists)
			            invoke Delete LocalInvoiceTemplateExclusionRel

			    LocalInvoiceTemplate = 3
			    if (Freight)
			        if (LocalInvoiceTemplateExclusionRel not exists)
			            invoke Create LocalInvoiceTemplateExclusionRel
		        else
			        if (LocalInvoiceTemplateExclusionRel exists)
			            invoke Delete LocalInvoiceTemplateExclusionRel

			    LocalInvoiceTemplate = 4 
			    if (MatchInvoice)
			        if (LocalInvoiceTemplateExclusionRel not exists)
			            invoke Create LocalInvoiceTemplateExclusionRel
		        else
			        if (LocalInvoiceTemplateExclusionRel exists)
			            invoke Delete LocalInvoiceTemplateExclusionRel

			    LocalInvoiceTemplate = 5
			    if (ServiceContract)
			        if (LocalInvoiceTemplateExclusionRel not exists)
			            invoke Create LocalInvoiceTemplateExclusionRel
		        else
			        if (LocalInvoiceTemplateExclusionRel exists)
			            invoke Delete LocalInvoiceTemplateExclusionRel
						
