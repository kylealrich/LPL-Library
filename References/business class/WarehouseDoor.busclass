WarehouseDoor is a BusinessClass
    owned by wh
    prefix is WHDOR

    Ontology
        symbolic key is WarehouseDoor
    

    Persistent Fields
        Description
        UseAsDefault              is Boolean
            default label is "Use_asDefault"
        Active 
   
    Field Rules
        Description
            required

        UseAsDefault
            if (action type.Create 
            and NoDefaultWarehouseDoorExists)
                default to true
            
            if (UseAsDefault changed)
                constraint(UseAsDefault)
                    "CannotChange;MustHaveAtLeastOneDefaultDoor"


        Active 
            if (action type.Create 
            and NoDefaultWarehouseDoorExists)
                default to true  

            if (Active changed
            and not Active
            and UseAsDefault
            and first AnotherDefaultWarehouseDoorRel.WarehouseDoor  = WarehouseDoor)
                constraint(Active)
                    "CannotInactivateADefaultDoor"
             
            if (UseAsDefault
            and first AnotherDefaultWarehouseDoorRel.WarehouseDoor not = WarehouseDoor)  
                default to true


    Rule Blocks
        CheckDefaultDoorExists
            if (AnotherDefaultWarehouseDoorRel exists)
                confirmation required
                    "Warning,DefaultDoorWillBeChangedFrom<first AnotherDefaultWarehouseDoorRel.WarehouseDoor>To<WarehouseDoor>;Proceed?"
                invoke ResetDefaultWarehouseDoor AnotherDefaultWarehouseDoorRel
            
    Relations
        AnotherDefaultWarehouseDoorRel
			one-to-many relation to WarehouseDoor
			Field Mapping uses symbolic key
				related.Company	                        = Company
				related.InventoryLocation				= InventoryLocation
				related.WarehouseDock				    = WarehouseDock
			Instance Selection
				where (related.UseAsDefault)
        

    Conditions
        NoDefaultWarehouseDoorExists
            restricted
            when (AnotherDefaultWarehouseDoorRel not exists)
        
        ValidForDelete
            restricted
            when (UseAsDefault
            and   AnotherDefaultWarehouseDoorRel exists)

    Actions
        Create is a Create Action
            valid when (WarehouseDock.Active)
            completion message is "Door<WarehouseDoor>Created"
            Action Rules
                if (UseAsDefault)
                    include CheckDefaultDoorExists
                       
              
        Update is an Update Action
            completion message is "Door<WarehouseDoor>Updated"
            Action Rules
                if (UseAsDefault changed
                and UseAsDefault)
                    include CheckDefaultDoorExists
                    UseAsDefault = true

        Delete is a Delete Action 
            Entrance Rules
                constraint(not ValidForDelete)
                    "CannotDelete;MustHaveAtLeastOneDefaultDoor"

        ResetDefaultWarehouseDoor is an Instance Action
            restricted
            Action Rules
                UseAsDefault = false
