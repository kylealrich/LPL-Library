DataCreationResult is a BusinessClass
	owned by framework

	prefix is DCR

	Ontology
		symbolic key is DataCreationResult

	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing

	Persistent Fields
		Type				is Numeric size 1
			States
				ReplicationSet				value is 1
				GLReportDesignerModel		value is 2
				ValidateBirstIntegration	value is 3
				ActionEvent					value is 4
				PurgeCatalog                value is 5
				MSCMData					value is 6
		RunTime				is TimeStamp
		Description

	Field Rules
		Type
			default to Type.ReplicationSet
        RunTime
            default to current timestamp

	Local Fields
		LocalDataCreationResult			is UniqueID
		LocalBusinessClass				is like BusinessClass
		LocalUpdateMessage				is Text
		LocalStatus						is Numeric size 1
			States
				Success	value is 1
				Error	value is 2
		LocalActor						is an Actor				
		LocalBirstModelResultView		is a BirstModelResult view
		LocalBirstModelResult			is UniqueID				

	Derived Fields
		DerivedCurrentTimeStamp is a DerivedField
			type is TimeStamp
			return current timestamp
			
		BirstModelResultDescription is a MessageField
			"Update_\BirstModelBusinessClass:<LocalBusinessClass>"

	Sets
		ByRunTime
			Sort Order
				RunTime
				DataCreationResult

	Relations
		DataCreationResultDetailRel
			one-to-many relation to DataCreationResultDetail
			Field Mapping uses ByBusinessClass
				related.DataCreationResult = DataCreationResult
				
		DataCreationResultDetailErrorRel is a DataCreationResultDetail set
			Instance Selection
				where (related.Status.Error)

	Actions
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Purge is a Purge Action
		
		BirstModelUpdate is an Instance Action
			restricted

			Parameters
				PrmDataCreationResult is UniqueID
				
			Entrance Rules
				LocalDataCreationResult = PrmDataCreationResult
				LocalActor	= actor
				invoke Create BirstModelResult
					assign result to LocalBirstModelResultView
					invoked.User			= LocalActor
					invoked.Description		= "Update GL Report Designer Model"
					
				commit transaction
				LocalBirstModelResult = LocalBirstModelResultView.BirstModelResult
				
			Action Rules


		ValidateBirstModel is an Instance Action
			restricted

			Parameters
				PrmDataCreationResult is UniqueID
				
			Entrance Rules
				LocalDataCreationResult	= PrmDataCreationResult
				LocalActor				= actor
				
			Action Rules


		BirstModelPostUpdate is an Instance Action
			restricted

			Parameters
				PrmDataCreationResult is UniqueID
				
			Entrance Rules
				LocalDataCreationResult = PrmDataCreationResult
				
			Action Rules


		CreateBirstModelResultDetail is an Instance Action
			restricted
			Action Rules
				invoke Create BirstModelResultDetail
					invoked.BirstModelResult	= LocalBirstModelResult
					invoked.Status				= LocalStatus
					invoked.Message				= LocalUpdateMessage
					invoked.BirstModel			= "CSFLA"
					invoked.BusinessClass		= LocalBusinessClass
		
		CreateDataCreationResultDetail is an Instance Action
			restricted

			Action Rules
				invoke Create DataCreationResultDetail
					invoked.DataCreationResult	= LocalDataCreationResult
					invoked.BirstModel			= "CSFLA"
					invoked.Status				= LocalStatus
					invoked.Message				= LocalUpdateMessage
					invoked.BusinessClass		= LocalBusinessClass


		PurgeResults is a Set Action
			disable checkpoint
			Parameters
				PrmThruDate			is Date
					default label is "ThroughDate"
				PurgeOffsetDays		is Numeric size 3
				ResultType			is Numeric size 1
					States
						All							value is 0
						ReplicationSet				value is 1
						GLReportDesignerModel		value is 2
						ValidateBirstIntegration	value is 3
						ActionEvent					value is 4

			Local Fields
				LocalThruDate 	is Date
					value is PrmThruDate



			Parameter Rules
				PrmThruDate
					if (PrmThruDate entered)
						LocalThruDate = (PrmThruDate + 1 day)
					else
					if (not PurgeOffsetDays entered)
						required	
							"MustEnter_\Through_\DateOr_\Offset"
					else
						LocalThruDate  = current corporate date - (PurgeOffsetDays - 1)

				PurgeOffsetDays
					constraint (not PrmThruDate entered)
						"CannotEnterBoth_\Through_\DateAnd_\Offset"

			Instance Selection
				where (RunTime < LocalThruDate
				and   (ResultType not entered 
				or     Type = ResultType))

			Sort Order is ByRunTime

			Action Rules
				Instance Rules
					invoke Purge
