PayablesDiscountCodeDetail is a BusinessClass
    owned by ap
    prefix is DSC
    classic name is APDISCCODE

    Ontology
        symbolic key is PayablesDiscountCodeDetail
            classic set name is DSCSET1
            classic name for PayablesDiscountCode is DISCOUNT-CODE

    Patterns
        implements StaticJava
        disable AuditIndex
    
    Attach Rules
		constraint (Active)
			"DiscountCodeDetailIsNotActive"
    
    Persistent Fields
        DiscountAccount		is a FinanceCodeBlock
            classic name for DiscountAccount.AccountingUnit is DISC-ACCT-UNIT
            classic name for DiscountAccount.GeneralLedgerChartAccount is DISC-ACCOUNT
        Active
            classic name is ACTIVE-FLAG
        InUse                  is Boolean
            classic name is USED-FLAG


        Allocation is Numeric size 2
            States
                ToDiscountCode     value is 0
                ProrateByAccount   value is 1
                LandToExpense      value is 2

	Derived Fields

		LandToExpenseWithSummarizeMsg is a MessageField
			restricted
			"MatchCompany,Options,SummarizeInventoryDistributionsMustEqualDoNotSummarizeToLandTheDiscountForInventoryItems"
		
    Field Rules
		DiscountAccount
			required 
			constraint (DiscountAccount.ToAccountingEntity = Company.AccountingEntity)		
				"AccountingEntity<DiscountAccount.ToAccountingEntity>MustEqualAccountingEntity<Company.AccountingEntity>OnGlobalLedgerCompany"
        Allocation
            if (VendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
                force default to Allocation.ToDiscountCode

			if (LandToExpenseWithSummarizeDistributions)
				confirmation required
					"<LandToExpenseWithSummarizeMsg>"
		Active					
			Active = true 
			
    Sets

		Set1
			indexed
			Sort Order
				VendorGroup
				Company
				PayablesDiscountCode

        Set2
            indexed
            Sort Order
                Company
                PayablesDiscountCode

	Conditions
		LandToExpenseWithSummarizeDistributions
			when (Allocation.LandToExpense
			and Company.MatchCompanyRel.SummarizeDistribution.Summarize)
	
	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
			Action Rules
				constraint (!InUse)
					"CannotDelete;DiscountCodeIsInUse"                        
	
