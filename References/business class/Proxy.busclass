Proxy is a BusinessClass
    owned by security
    prefix is PROXY
    stored in environment


    Ontology
		symbolic key is Proxy 
		        
    Patterns
        implements CRUD
        implements SecurityCache
        disable AuditIndex

	Persistent Fields
		Description is a LSDescription
		FromDate is TimeStamp
		ToDate is TimeStamp
		IsEnabled is Boolean
			default label is "Enabled"
		
	Transient Fields
		TempActor is an Actor
			default label is "TemporaryActor"
				
	Derived Fields	
	
		IsActive is a DerivedField
			type is Boolean
			default label is "Active"
			if (FromDate = blank)
				if (ToDate = blank)
					IsActive = true
				else
					if (current timestamp <= ToDate)
						IsActive = true
					else
						IsActive = false
			else
				if (ToDate = blank)
					if (current timestamp >= FromDate)	
						IsActive = true
					else
						IsActive = false
				else
					if ( (current timestamp >= FromDate) and (current timestamp <= ToDate) )
						IsActive = true
					else
						IsActive = false	
		
	Field Rules
		IsEnabled
			default to true
		Actor
			default to actor
		TempActor
			default to actor	
        	
    Actions
        			
        Create is an Action
            Action Rules
            	if (FromDate != blank and ToDate != blank)
                	constraint (FromDate <= ToDate)
                    	"FromDateIsGreaterThanToDate."           	
        Update is an Action
            Action Rules
            	if (FromDate != blank and ToDate != blank)
                	constraint (FromDate <= ToDate)
                    	"FromDateIsGreaterThanToDate."           	
        Delete is an Action

    Sets
    
        ByProxyNameAndActor
            indexed
            no duplicates
            Sort Order
                Proxy
                Actor

                        

				
					
	

                            			
				
                            			
