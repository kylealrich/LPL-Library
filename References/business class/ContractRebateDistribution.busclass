ContractRebateDistribution is a BusinessClass
    owned by po
    prefix is RDS
    classic name is POREBDIST
	rpg name is DBPORDS
	
    Ontology
    	symbolic key is ContractRebateDistribution
			classic set name is RDSSET1
			sql set name is RDSSET1

    Patterns
        implements StaticJava
        disable AuditIndex
            	
	Persistent Fields
		Company						
		RebateDetail						
		TransactionAmount					is an InternationalAmount
			classic name is TRANS-AMOUNT
		TransactionCurrency					is a Currency
			classic name is TRANS-CURR
		ContractCurrencyAmount				is an InternationalAmount
			classic name is CONT-CURR-AMT
		CurrencyRate
			classic name is CURR-RATE 
		DistributionSequence

	Context Fields
		RebateDueDateRange					is a DateRange
		
	Local Fields
		LocalPostingDate                    is a PostingDate	
        LocalTransactionDate    		    is an ExchangeDate
        
    Transient Fields
    	TransientAccountingEntity           is an AccountingEntity
    		derive value from Company

		TransientDistributionAccount        is a TransactionCodeBlock
    
    Derived Fields    	
     	
	Conditions
		WithinParameterDateRange
			restricted
			when (ContractRebateDue	within RebateDueDateRange)	
			
	Sets
	
 		ByRebateReceived
 			duplicates
 			not indexed
 			Sort Order
 				ContractGroup
				Contract
		        ContractRebateType
		        ContractRebateDue
		        ContractRebateReceived 
		
    Relations

	Field Rules
		Company
			required
				"CompanyIsRequired"
			initial value is ContractRebateReceived.Company
		
		ContractCurrencyAmount
			if (RebateDetail.RebateDistribution entered
			or  RebateDetail.ContractCompanyDepartment entered)
				required
					"DistributionAmountIsRequired"
			
		TransactionCurrency
			force default to Contract.CurrencyCode	
		
		CurrencyRate
			default to 1
				
		TransactionAmount
			force default to ContractCurrencyAmount
			
		RebateDetail    	
			mutually exclusive
				"CannotEnterBothAccountingInformationAndDepartmentInformation"

		TransientDistributionAccount
			TransientDistributionAccount = RebateDetail.RebateDistribution

	Actions
    	Create is a Create Action
    		valid when (Contract.CanModifyRebate)
    		Action Rules
    			constraint (Contract.CanModifyRebate)
    				"ContractCannotBeInactiveToAddRebateDistributions"
				if (RebateDetail.RebateDistribution.Project entered)
					LocalPostingDate 		= ContractRebateReceived.DocumentDate
					LocalTransactionDate	= ContractRebateReceived.DocumentDate

    		Exit Rules
    			invoke UpdateReceivedAmount ContractRebateDue

    	Update is an Update Action
    		valid when (Contract.CanModifyRebate)
    		Action Rules
    			constraint (Contract.CanModifyRebate)
    				"ContractCannotBeInactiveToModifyRebatesDistributions"
				if (RebateDetail.RebateDistribution.Project entered)
					LocalPostingDate = ContractRebateReceived.DocumentDate
					LocalTransactionDate	= ContractRebateReceived.DocumentDate

    		Exit Rules   	
    			invoke UpdateReceivedAmount ContractRebateDue
		
    	
    	Delete is a Delete Action
    		valid when (Contract.CanModifyRebate)
    		Action Rules
    			constraint (Contract.CanModifyRebate)
    				"ContractCannotBeInactiveToDeleteRebatesDistributions"   	
    			constraint (ContractRebate.Active)
    				"ContractRebateMustBeActiveToDeleteRebateDistributions"
    		
    		Exit Rules   	
    			invoke UpdateReceivedAmount ContractRebateDue

