CustomerDraftRemittance is a BusinessClass
    owned by dt
    prefix is DRM
    classic name is DTREMIT

    Ontology
        symbolic key is CustomerDraftRemittance
            classic set name is DRMSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Status              is Numeric size 1
            States
                Selected  value is 0
                Generated value is 1
                Presented value is 2
                Canceled  value is 8
                Cashed    value is 9
        MinimumAmount       			is an InternationalAmount
        	classic name is MIN-AMT
        MaximumAmount       			is an InternationalAmount
        	classic name is MAX-AMT

        FinancialInstitution
        FinancialInstitutionBranch
        CreditLine          			is a BankEntityCreditRisk
        RemitAmount         			is an InternationalAmount
        	classic name is REMIT-AMT

        RemitDate           			is Date
        PresentDate         			is Date
        Currency						is a FromCurrency
            classic name is CURRENCY-CODE
        CashedDate       			  	is an ExchangeDate
        CancelDate          			is Date
        CashedAmount        			is an InternationalAmount
        	classic name is CASHED-AMT
        TotalDishonoredAmount  			is an InternationalAmount
        	classic name is UNPAY-AMT
        AdministrativeExpenseAmount		is an InternationalAmount
        	classic name is ORIG-ADMIN-AMT
        BaseAdministrativeExpenseAmount is an InternationalAmount
        	classic name is ADMIN-AMT
        BankExpenseAmount      			is an InternationalAmount
        	classic name is ORIG-BNK-EXP
        BaseBankExpenseAmount           is an InternationalAmount
        	classic name is BNK-EXP-AMT
        CurrencyRate
        	classic name is ORIG-RATE
        GeneralLedgerStatus    			is AlphaUpper size 1
        	classic name is GL-UPDATE-CODE
        	default label is "GlobalLedgerStatus"
            States
                Cashing      value is "C"
                Presentation value is "P"
                Cancel       value is "X"
                NotProcessed value is blank
        CashRate
        DepositBankCode                 is a BankTransactionCode
        	classic name is	DEP-INST-CODE
        TransactionIDNumber
            classic name is	TRANS-IDENT
        BankTransactionCode
        	classic name is	BANK-INST-CODE
        AdministrativeExpenseAccount    is a FinanceCodeBlock
            classic name for AdministrativeExpenseAccount.AccountingUnit is ADMN-ACCT-UNIT
            classic name for AdministrativeExpenseAccount.GeneralLedgerChartAccount is ADMN-ACCOUNT
        BankExpenseAccount              is a FinanceCodeBlock
            classic name for BankExpenseAccount.AccountingUnit is BEXP-ACCT-UNIT
            classic name for BankExpenseAccount.GeneralLedgerChartAccount is BEXP-ACCOUNT



	Local Fields
		LocalCurrencyTable				is a CurrencyTable
		LocalCurrencyExchange			is a CurrencyExchange
		LocalPrefix						is Alpha 3
		LocalTransactionAmount			is like InternationalAmount
		LocalAccount					is a FinanceCodeBlock
		LocalDate						is Date
		
    Derived Fields

        ProcessAmount is a DerivedField
            type is like InternationalAmount
            restricted
            return (RemitAmount - CashedAmount - TotalDishonoredAmount)

		RepresentativeText is a StringField				
			type is Text
			Company " - " Company.Name
			
		CashCodeText is a StringField					
			type is Text
			default label is "CashCode"
			CustomerDraftRemittance.CashCode " - " CustomerDraftRemittance.CashCode.Description

		BankTransactionCodeText is a StringField		
			type is Text
			default label is "CashCode"
			BankTransactionCode " - " BankTransactionCode.Description

	Field Rules



		
    Conditions

        IsCanceled
        	restricted
            when (Status.Canceled)

        IsCashed
        	restricted
            when (Status.Cashed)

        IsCreate
        	restricted
            when (Status.Selected)

        IsNotCancelled
        	restricted
            when (CancelDate not entered)

        IsDiscount
        	restricted
            when (CustomerDraftRemittance.RemitType.Discounted)

        IsDrmset2
        	restricted
            classic name is DRMSET2
            when (Status >  blank
            and   CustomerDraftRemittance.RemitNumber entered)

        IsDrmset4
        	restricted
            classic name is DRMSET4
            when (DepositBankCode entered)

        IsFullyProcessd
        	restricted
            when (ProcessAmount not entered)

        IsGenerated
        	restricted
            when (Status.Generated)

        IsNotCashed
        	restricted
            when (CashedAmount not entered
            and   TotalDishonoredAmount not entered)

        IsNotCashedYet
        	restricted
            when (GeneralLedgerStatus.NotProcessed
            and   CashedAmount not entered
            and   TotalDishonoredAmount not entered)

        IsNotFullyCashed
        	restricted
            when (ProcessAmount entered)

        IsNotGeneralLedgerUpdated
        	restricted
            when (GeneralLedgerStatus.NotProcessed)

        IsNotUpdCash
        	restricted
            when (not GeneralLedgerStatus.Cashing)

        IsNotUpdForcsh
        	restricted
            when (not GeneralLedgerStatus.Cashing)

        IsPresented
        	restricted
            when (Status.Presented)

        IsPrinted
        	restricted
            when (Status.Presented)

        PresentDateNotEntered
        	restricted
            when (PresentDate not entered)

        RemitAmountExceedsMaximumAmount
        	restricted
            when (MaximumAmount entered
            and   RemitAmount >  MaximumAmount)

        RemitAmountLessThanMinimumAmount
        	restricted
            when (RemitAmount <  MinimumAmount)

        RemitNumberEntered
        	restricted
            when (CustomerDraftRemittance.RemitNumber entered)
            
        BankTransactionCodeEntered
        	restricted
        	when (BankTransactionCode entered)

        IsRemittedDraft
        	restricted
            when (Status >  blank)

        IsSelected
        	restricted
            when (Status.Selected)

        DisplayOnMaintenanceList
        	restricted
            when (Status.Generated
            or    Status.Presented
            or    Status.Cashed)

		CustomerDraftRemittanceExists				
			restricted
			when (CustomerDraftRemittance exists)
            
    Relations

        AvailableCustomerDraftRel
            one-to-many relation to CustomerDraft
            Field Mapping uses symbolic key
            Instance Selection
				where (related.Company					= Company)






        SelectedCustomerDraftRel
            one-to-many relation to CustomerDraft
            Field Mapping uses Set3
                related.RemitGroup.RemitCompany 		= Company
                related.RemitGroup.RemitProcessLevel 	= CustomerDraftRemittance.ProcessLevel
                related.CashCode						= CustomerDraftRemittance.CashCode
                related.RemitGroup.RemitType    		= CustomerDraftRemittance.RemitType
            Instance Selection
            	where (related.Status.Selected)

        IncludedCustomerDraftRel
            one-to-many relation to CustomerDraft
            Field Mapping uses Set5
                related.RemitGroup.RemitCompany			= Company
                related.RemitGroup.RemitProcessLevel	= CustomerDraftRemittance.ProcessLevel
                related.RemitGroup.RemitNumber			= CustomerDraftRemittance.RemitNumber

        CustomerDraftRel
            one-to-many relation to CustomerDraft
            Field Mapping uses Set3
                related.RemitGroup.RemitCompany 		= Company
                related.RemitGroup.RemitProcessLevel 	= CustomerDraftRemittance.ProcessLevel
                related.CashCode						= CustomerDraftRemittance.CashCode
                related.RemitGroup.RemitType    		= CustomerDraftRemittance.RemitType

        RemittedDraftRel
            one-to-many relation to CustomerDraft
            valid when (IsRemittedDraft)
            Field Mapping uses Set5
                related.RemitGroup.RemitCompany 		= Company
                related.RemitGroup.RemitProcessLevel 	= CustomerDraftRemittance.ProcessLevel
                related.RemitGroup.RemitNumber     		= CustomerDraftRemittance.RemitNumber

        SelectedDraftRel
            one-to-many relation to CustomerDraft
            Field Mapping uses Set4
                related.RemitGroup.RemitCompany 		= Company
                related.RemitGroup.RemitProcessLevel 	= CustomerDraftRemittance.ProcessLevel
                related.CashCode     					= CustomerDraftRemittance.CashCode
                related.RemitGroup.RemitType    		= CustomerDraftRemittance.RemitType

        CompanyCashCodeRel
            one-to-one relation to CompanyCashCode
            Field Mapping uses symbolic key
                related.Company  						= Company
                related.CashCode 						= CustomerDraftRemittance.CashCode
    Sets

        Set2
            indexed
            Instance Selection
                where (IsDrmset2)
            Sort Order
                Company
                CustomerDraftRemittance.RemitNumber

        Set3
            indexed
            Instance Selection
                where (RemitNumberEntered)
            Sort Order
                CustomerDraftRemittance.CashCode
                Company
                CustomerDraftRemittance.ProcessLevel
                CustomerDraftRemittance.RemitType
                Status
                CustomerDraftRemittance.RemitNumber

        Set4
            indexed
            Instance Selection
                where (IsDrmset4)
            Sort Order
                DepositBankCode
                TransactionIDNumber

        Set5
            indexed
            Instance Selection
                where (BankTransactionCodeEntered)
            Sort Order
                Company
                CustomerDraftRemittance.ProcessLevel
                CustomerDraftRemittance.CashCode
                CustomerDraftRemittance.RemitType
                BankTransactionCode
                CustomerDraftRemittance.RemitNumber

        Set6
            indexed
            Sort Order
                Company
                CustomerDraftRemittance.ProcessLevel
                CustomerDraftRemittance.CashCode
                BankTransactionCode
                CustomerDraftRemittance.RemitType
                CustomerDraftRemittance.RemitNumber
                CustomerDraftRemittance.MaturityDate

        Set7
            indexed
            Instance Selection
                where (RemitNumberEntered)
            Sort Order
                CustomerDraftRemittance.CashCode
                Company
                CustomerDraftRemittance.ProcessLevel
                CustomerDraftRemittance.RemitType
                CustomerDraftRemittance.RemitNumber
                Status

	Rule Blocks

		CreateDistribution
			invoke Create ReceivableGLDistribution
				invoked.FinanceEnterpriseGroup							= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
            	invoked.ReceivableCompanyGroup.GlCompany				= Company
            	invoked.ReceivableCompanyDataGroup.TransactionCompany	= Company
            	invoked.ReceivableCompanyGroup.OriginCompany			= Company
		        invoked.Invoice											= CustomerDraftRemittance.RemitNumber
		        invoked.TransType										= "R"
				invoked.TransactionDate									= LocalDate
				invoked.GeneralLedgerDate								= LocalDate
				invoked.DistributionSource								= "B"

				invoked.AccumulationType								= "D"
		        invoked.DocumentNumber									= LocalPrefix + CustomerDraftRemittance.RemitNumber
		        invoked.Status											= 2

				invoked.Origin											= "DR"
		        invoked.DistributionAmount.CurrencyAmount				= LocalTransactionAmount
				invoked.GeneralLedgerAccount							= LocalAccount

	StateCycles	

		CustomerDraftLifeCycle is a StateCycle
		
			state field is Status
			
            Selected is a State

				Create is a Create Action
					Entrance Rules
						LocalCurrencyTable = Company.DerivedCurrencyTable

					
				Update is an Update Action

					Entrance Rules
						LocalCurrencyTable = Company.DerivedCurrencyTable
					
				FastUpdate is an Update Action
					restricted
					bypass field rules
					
				Delete is a Delete Action
					restricted
					Action Rules
						invoke Selected.Unselect SelectedCustomerDraftRel
						
				Generate is an Instance Action
					Parameters
						PrmRemitDate is Date
							default label is "RemitDate"
					Parameter Rules
						PrmRemitDate
							default to current corporate date
							initial value is current corporate date
					Action Rules
						constraint (RemitAmount >= MinimumAmount)
							"RemitAmountIsLessThanMinimumAmount"
						constraint (RemitAmount <= MaximumAmount)
							"RemitAmountIsGreaterThanMaximumAmount"
						increment Company.LastBOERemittanceNumber

						invoke Selected.IncludeInRemit SelectedCustomerDraftRel
							invoked.PrmRemitNumber		= Company.LastBOERemittanceNumber
							invoked.PrmRemitDate		= PrmRemitDate
							
						invoke Generated.Create CustomerDraftRemittance
						 	fill in fields from this instance
						 	invoked.CustomerDraftRemittance.RemitNumber = Company.LastBOERemittanceNumber
							invoked.RemitDate							= PrmRemitDate
							
						invoke Selected.Delete

            Generated is a State
            	Create is a Create Action
            		restricted
            		
				FastUpdate is an Update Action
					restricted
					bypass field rules
					Exit Rules
						if (RemitAmount = CashedAmount - TotalDishonoredAmount)
							make transition to Cashed
					
				UpdateCashingOptions is an Instance Action
					Parameters
				        PrmBankExpenseAmount      			is an InternationalAmount
				        	default label is "BankExpenseAmount"
				        PrmBankExpenseAccount              	is a FinanceCodeBlock
				        	default label is "BankExpenseAccount"
				        PrmAdministrativeExpenseAmount 		is an InternationalAmount
				        	default label is "AdministrationExpenseAmount"
				        PrmAdministrativeExpenseAccount    	is a FinanceCodeBlock
				        	default label is "AdministrationExpenseAccount"
				        PrmCashRate						is a CurrencyRate
				        	default label is "CurrencyRate"
				    Parameter Rules
				        PrmBankExpenseAmount
							initial value is BankExpenseAmount
				        PrmBankExpenseAccount
							initial value is BankExpenseAccount
				        PrmAdministrativeExpenseAmount
							initial value is AdministrativeExpenseAmount
				        PrmAdministrativeExpenseAccount
							initial value is AdministrativeExpenseAccount
				        PrmCashRate
							initial value is CashRate
					Action Rules
				        AdministrativeExpenseAmount			= PrmAdministrativeExpenseAmount
				        BankExpenseAmount					= PrmBankExpenseAmount
				        AdministrativeExpenseAccount		= PrmAdministrativeExpenseAccount
				        BankExpenseAccount					= PrmBankExpenseAccount
				        CashRate							= PrmCashRate
					
				Cancel is an Instance Action
					Parameters
						PrmCancelDate		is Date
							default label is "CancelDate"
					Parameter Rules
						PrmCancelDate
							default to current corporate date
							initial value is current corporate date	
					Action Rules
						if (CreditLine entered)
							invoke Update CreditLine
								invoked.ActualAmount	-= RemitAmount

						CancelDate			= PrmCancelDate
						
					Exit Rules
						invoke IncludedInRemit.Exclude IncludedCustomerDraftRel
						make transition to Canceled
						
						
				Present is an Instance Action
					Parameters
						PrmPresentDate		is Date
							default label is "PresentationDate"
					Parameter Rules
						PrmPresentDate
							default to current corporate date
							initial value is current corporate date
					Entrance Rules
						constraint (RemitAmount entered)
							"RemitAmountMustBeGreaterThanZero"
						constraint (PrmPresentDate >= RemitDate)
							"PresentationDateCannotBeLessThanRemitDate"
					Action Rules
						PresentDate		= PrmPresentDate
						invoke IncludedInRemit.Present IncludedCustomerDraftRel
					Exit Rules
						make transition to Presented

            Presented is a State
				FastUpdate is an Update Action
					restricted
					bypass field rules
					Exit Rules
						if (RemitAmount = CashedAmount - TotalDishonoredAmount)
							make transition to Cashed
					
				Cash is an Instance Action
					Parameters
						PrmCashDate		is Date
							default label is "CashDate"
					Parameter Rules
						PrmCashDate
							default to current corporate date
							initial value is current corporate date	
					Entrance Rules
						constraint (RemitAmount entered)
							"RemitAmountMustBeGreaterThanZero"
						constraint (PrmCashDate >= RemitDate)
							"CashDateCannotBeLessThanRemitDate"
						constraint (CustomerDraftRemittance.RemitType.Discounted)
							"OnlyDiscountedRemittancesCanBeCashed"
						constraint (RemitAmount != TotalDishonoredAmount)
							"CannotCash;RemitAmountEqualsTotalDishonoredAmount"
						if (CustomerDraftRemittance.RemitType.Encashed)
							constraint ((RemitAmount - CashedAmount - TotalDishonoredAmount) = 0)
								"RemitAmountLessCashedAmountLessTotalDishonoredAmountNotEqualZero"
						if (Company.CurrencyRateOverride.No)
							constraint (CashRate not entered)
								"CashRateCannotBeEntered;CompanyOverrideSetToNo"
						else
							if (Company.Currency = CustomerDraftRemittance.CashCode.Currency)
								if (CashRate not entered)
									CashRate = 1
								else
									constraint (CashRate = 1)
										"CashRateMustBeSetTo1IfCompanyCurrencyIsEqualToCashCodeCurrency"
							else
								constraint (Company.MultiCurrencyProcessing)
									"CompanyIsNotMultiCurrency"
								 
					Action Rules
					Exit Rules
						invoke Presented.Cash IncludedCustomerDraftRel
							invoked.PrmCashDate				= PrmCashDate
						make transition to Cashed
						
				Cancel is an Instance Action
					Parameters
						PrmCancelDate		is Date
							default label is "CancelDate"
					Parameter Rules
						PrmCancelDate
							default to current corporate date
							initial value is current corporate date	
					Entrance Rules
						constraint (CashedAmount not entered)
							"CannotCancel;RemitIsPartiallyCashed"
						constraint (TotalDishonoredAmount not entered) 
							"CannotCancel;RemitIsPartiallyCashed"
					Action Rules
						if (CreditLine entered)
							invoke Update CreditLine
								invoked.ActualAmount			-= RemitAmount
								
						CancelDate			= PrmCancelDate

						if (GeneralLedgerStatus.Presentation)

							LocalDate							= PrmCancelDate
							LocalPrefix							= "16-"
					        LocalTransactionAmount				= RemitAmount * -1
							if (CustomerDraftRemittance.RemitType.Discounted)
								LocalAccount					= CompanyCashCodeRel.DiscountedRemittanceAccount
						    else
								LocalAccount					= CompanyCashCodeRel.EncashedRemittanceAccount

					        include CreateDistribution
					        
							for each IncludedCustomerDraftRel
								LocalDate						= PrmCancelDate
								LocalPrefix						= "17-"
						        LocalTransactionAmount			= each.TransactionAmount * -1
								if (each.BillOfExchangeAccrualCode entered)
									LocalAccount				= each.BillOfExchangeAccrualCode.DistributionAccount
								else
									LocalAccount				= each.ReceivableGeneralLedgerCode.DistributionAccount
	
						        include CreateDistribution
					        
					Exit Rules
						invoke IncludedInRemit.Exclude IncludedCustomerDraftRel
						make transition to Canceled
						
				GLUpdate is an Instance Action
					restricted
					valid when (!GeneralLedgerStatus.Presentation)
					Action Rules
						GeneralLedgerStatus = "P"
				
				Undo is an Instance Action
					Parameters
						EffectiveDate		is Date
					Parameter Rules
						EffectiveDate
							default to current corporate date
							initial value is current corporate date	
					Action Rules
						invoke Presented.Exclude IncludedCustomerDraftRel

						if (GeneralLedgerStatus.Presentation)

							LocalDate							= EffectiveDate
							LocalPrefix							= "16-"
					        LocalTransactionAmount				= RemitAmount
							if (CustomerDraftRemittance.RemitType.Discounted)
								LocalAccount					= CompanyCashCodeRel.DiscountedRemittanceAccount
						    else
								LocalAccount					= CompanyCashCodeRel.EncashedRemittanceAccount

					        include CreateDistribution
					        
							for each IncludedCustomerDraftRel
								LocalDate						= EffectiveDate
								LocalPrefix						= "17-"
						        LocalTransactionAmount			= each.TransactionAmount * -1
								if (each.BillOfExchangeAccrualCode entered)
									LocalAccount				= each.BillOfExchangeAccrualCode.DistributionAccount
								else
									LocalAccount				= each.ReceivableGeneralLedgerCode.DistributionAccount
	
						        include CreateDistribution
						        
						    initialize GeneralLedgerStatus

				        initialize AdministrativeExpenseAmount
				        initialize BaseAdministrativeExpenseAmount
				        initialize BankExpenseAmount
				        initialize BaseBankExpenseAmount
				        initialize AdministrativeExpenseAccount
				        initialize BankExpenseAccount
				        initialize CashRate

					Exit Rules
						make transition to Generated

            Canceled is a State
				Delete is a Delete Action
					restricted
					Action Rules
						constraint (GeneralLedgerStatus.Cancel)
							"GlobalLedgerUpdateHasNotBeenRun"
					
            	GLUpdate is an Instance Action
					valid when (!GeneralLedgerStatus.Cancel)
            		restricted
					Action Rules
						GeneralLedgerStatus = "X"
            	
            Cashed is a State
            	Entrance Rules
					initialize LocalCurrencyExchange.EnteredCurrencyRate
					LocalCurrencyExchange.ToCurrency					= Company.Currency
					LocalCurrencyExchange.EnteredCurrencyRate			= CashRate
					LocalCurrencyExchange.EnteredCurrencyAmount			= AdministrativeExpenseAmount
					BaseAdministrativeExpenseAmount						= LocalCurrencyExchange.OutputCurrencyAmount
            	
					initialize LocalCurrencyExchange.EnteredCurrencyRate
					LocalCurrencyExchange.ToCurrency					= Company.Currency
					LocalCurrencyExchange.EnteredCurrencyRate			= CashRate
					LocalCurrencyExchange.EnteredCurrencyAmount			= BankExpenseAmount
					BaseBankExpenseAmount								= LocalCurrencyExchange.OutputCurrencyAmount
            	
				FastUpdate is an Update Action
					restricted
					bypass field rules
					Exit Rules
						if (RemitAmount = CashedAmount - TotalDishonoredAmount)
							make transition to Presented
					
				Delete is a Delete Action
					restricted
					Action Rules
						if (CustomerDraftRemittance.RemitType.Discounted)
							constraint (GeneralLedgerStatus.Cashing)
								"GlobalLedgerUpdateHasNotBeenRun"
						else
							constraint (GeneralLedgerStatus.Presentation)
								"GlobalLedgerUpdateHasNotBeenRun"
					
            	GLUpdate is an Instance Action
					valid when (!GeneralLedgerStatus.Cashing)
            		restricted
					Action Rules
						GeneralLedgerStatus = "C"
            	
            	Undo is an Instance Action
					Parameters
						EffectiveDate		is Date
					Parameter Rules
						EffectiveDate
							default to current corporate date
							initial value is current corporate date	
					Entrance Rules							
						constraint (!GeneralLedgerStatus.Cashing)
							"CannotUndo;GlobalLedgerUpdateHasBeenRun"
						constraint (CustomerDraftRemittance.RemitType.Discounted)
							"ActionValidOnlyForDiscountedRemittance"
					Action Rules
						invoke Cashed.Uncash IncludedCustomerDraftRel

						if (GeneralLedgerStatus.Presentation)

							LocalDate							= EffectiveDate
							LocalPrefix							= "16-"
					        LocalTransactionAmount				= RemitAmount
							if (CustomerDraftRemittance.RemitType.Discounted)
								LocalAccount					= CompanyCashCodeRel.DiscountedRemittanceAccount
						    else
								LocalAccount					= CompanyCashCodeRel.EncashedRemittanceAccount

					        include CreateDistribution
					        
							for each IncludedCustomerDraftRel
								LocalDate						= EffectiveDate
								LocalPrefix						= "17-"
						        LocalTransactionAmount			= each.TransactionAmount * -1
								if (each.BillOfExchangeAccrualCode entered)
									LocalAccount				= each.BillOfExchangeAccrualCode.DistributionAccount
								else
									LocalAccount				= each.ReceivableGeneralLedgerCode.DistributionAccount
	
						        include CreateDistribution

						    initialize GeneralLedgerStatus

				        initialize AdministrativeExpenseAmount
				        initialize BaseAdministrativeExpenseAmount
				        initialize BankExpenseAmount
				        initialize BaseBankExpenseAmount
				        initialize AdministrativeExpenseAccount
				        initialize BankExpenseAccount
				        initialize CashRate

					Exit Rules
						make transition to Presented
							
