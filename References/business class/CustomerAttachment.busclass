CustomerAttachment is a BusinessClass
	owned by ar
	prefix is ARCUA
    sql name is ARCUAttachment

	Ontology
		symbolic key is CustomerAttachment

	Patterns
    	implements StaticJava


	Persistent Fields
	    Name                    is a CommentName
	  		default label is "Title"
	    Type                    is AlphaUpper 1
	    Comment                 is a NewCommentText
	    Attachment              is an AlternateAttachment

	Field Rules
		Attachment
			if (Comment not entered)
				required
					"MustProvide_AttachmentOr_Comment"

	Local Fields
		LocalAttributeCtr   is Numeric 2	
					
	Conditions
		HasAttachment	
			restricted
			when (Attachment entered)

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment	

	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
			
	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
	
