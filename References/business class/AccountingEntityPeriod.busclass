AccountingEntityPeriod is a BusinessClass
	owned by GeneralLedger
	prefix is GLAEC

	Ontology
		symbolic key is AccountingEntityPeriod

    Patterns
        implements StaticJava

	Persistent Fields


	Local Fields
		LocalLedger						is a Ledger

	Derived Fields
		DerivedTotalLedgers				is a DerivedField
			type is Numeric 2
			restricted
			return instance count of AllLedgerPeriodForEntityRel
		DerivedOpenLedgers				is a DerivedField
			type is Numeric 2
			restricted
			return instance count of OpenLedgerPeriodForEntityRel
		DerivedClosedLedgers			is a DerivedField
			type is Numeric 2
			restricted
			return instance count of ClosedLedgerPeriodForEntityRel
		DerivedEntityLedgerPercent		is a ComputeField
			type is Percent 6.3
			(DerivedClosedLedgers / DerivedTotalLedgers)
		UpdatedDerivedEntityLedgerPercent		is a ComputeField
			type is Percent 3.0
			(DerivedClosedLedgers / DerivedTotalLedgers)
		RowName							is a StringField
			type is AlphaUpper 35 
			restricted
			AccountingEntity
			"-"
			GeneralLedgerClosePeriod.PeriodName
		CardViewEntityLabel				is a StringField
			type is Alpha 40 
			AccountingEntity
			" - "
			AccountingEntity.Name
		CardViewYearLabel				is a StringField
			type is Alpha 40 
			"Year: "
			GeneralLedgerClosePeriod.GeneralLedgerCloseYear
		CardViewPeriodLabel				is a StringField
			type is Alpha 40 
			"Period: "
			GeneralLedgerClosePeriod.PeriodNumber
		CardViewAllLedgerLabel				is a StringField
			type is Alpha 40 
			restricted
			DerivedTotalLedgers
			" Ledgers Closed"
		CardViewPartialLedgerLabel		is a StringField
			type is Alpha 40 
			restricted
			DerivedOpenLedgers
			" of "
			DerivedTotalLedgers
			" Ledgers Open"
		CardViewLedgerLabel		is a ConditionalField
			type is Alpha 40
			if (DerivedOpenLedgers entered)
				CardViewPartialLedgerLabel
			else
				CardViewAllLedgerLabel
		DerivedEntityLabel				is a DerivedField
			type is Alpha 50
			restricted
			return actor.context.FinanceEnterpriseGroup.AccountingEntityLabel

	Relations
		EntityLedgerPeriodRel
			one-to-one relation to EntityLedgerPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.GeneralLedgerClosePeriod	= GeneralLedgerClosePeriod
				related.Ledger						= LocalLedger
		AllLedgerPeriodForEntityRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.GeneralLedgerClosePeriod	= GeneralLedgerClosePeriod
		OpenLedgerPeriodForEntityRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.GeneralLedgerClosePeriod	= GeneralLedgerClosePeriod
			Instance Selection
				where (related.OpenPeriod)
		ClosedLedgerPeriodForEntityRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.GeneralLedgerClosePeriod	= GeneralLedgerClosePeriod
			Instance Selection
				where (!related.OpenPeriod)
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup   = actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity        = AccountingEntity

	Sets
		ByGeneralLedgerCalendarPeriod
			Sort Order
				FinanceEnterpriseGroup
				AccountingEntity
				GeneralLedgerClosePeriod
		ByEntityLedgerPeriodDescending
			Sort Order
				FinanceEnterpriseGroup
				AccountingEntity
				GeneralLedgerClosePeriod descending
				
	Conditions
		EntityLedgerPeriodExists
			restricted
			when (AllLedgerPeriodForEntityRel exists)
		OpenEntityLedgerPeriodExists
			restricted
			when (OpenLedgerPeriodForEntityRel exists)
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))

	Attach Rules

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		CreateEntityLedgerPeriod is a Set Action
			run in foreground
			restricted
			Parameters
				PrmEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmAccountingEntity			is a AccountingEntity
					default label is "AccountingEntity"
				PrmLedger					is a Ledger
					default label is "Ledger"

			Parameter Rules
				PrmAccountingEntity
					required
				PrmLedger
					required

			Local Fields	

			Instance Selection
				where (FinanceEnterpriseGroup		= PrmEnterpriseGroup
				and	   AccountingEntity				= PrmAccountingEntity)
				
			Sort Order
				FinanceEnterpriseGroup
				AccountingEntity

			Action Rules
				Empty Set Rules

				Set Rules

				AccountingEntity Set Rules

				Instance Rules
					LocalLedger					= PrmLedger
					if (!EntityLedgerPeriodRel exists)
						if ((GeneralLedgerClosePeriod.GeneralLedgerCloseYear < AccountingEntity.FiscalYear)
						or  (GeneralLedgerClosePeriod.GeneralLedgerCloseYear = AccountingEntity.FiscalYear
						and  GeneralLedgerClosePeriod	                     < AccountingEntity.CurrentPeriod))

							invoke Create Limited EntityLedgerPeriodRel

						else
							invoke Create Open EntityLedgerPeriodRel

