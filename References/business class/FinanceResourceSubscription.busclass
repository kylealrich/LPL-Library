FinanceResourceSubscription is a BusinessClass
	owned by sharedfinance
	
	prefix is FRSub
	
	Ontology
		symbolic key is FinanceResourceSubscription
		
	Patterns
		implements CreateStamp
		implements ContextualParent
	
	Persistent Fields

		Subscription						is BusinessObjectReference		  
		SubscriptionTitle					is Alpha 30

		MarkAsViewedTimeStamp				is TimeStamp
		PublishedTimeStamp					is TimeStamp

		FollowResource						is a FinanceResource

		CreatedFromTask						is Numeric 06					
	
	Field Rules


		SubscriptionTitle
			required
		MarkAsViewedTimeStamp
			force default to current timestamp
		PublishedTimeStamp
			force default to current timestamp

	Derived Fields
							
	Conditions
		RaiseRedAlert
			restricted			
			when (false)
		RaiseYellowAlert
			restricted			
			when (false)
		ShowUpdate
			restricted			
			when (ShowNewUpdatesRel exists)	

		PeriodSubscription					
			restricted			
			when (Subscription.BusinessClassName = "FinancePeriod")

		SubscribedToFollowResource					
			restricted			
			when (Subscription.BusinessClassName = "FinanceResource")

		CloseTaskSubscription					
			restricted			
			when (Subscription.BusinessClassName = "CloseTask")		
		
	Relations
		ShowNewUpdatesRel			
			one-to-many relation to SubscriptionUpdates
			Field Mapping uses ByWhenItChanged
				related.FinanceGroup = HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
				related.WhenItChanged >= MarkAsViewedTimeStamp
			Instance Selection
				where ((related.ThisChanged.BusinessClassName = Subscription.BusinessClassName
				and     related.ThisChanged.BusinessObjectKey = Subscription.BusinessObjectKey)
				or     (related.UpdatedByResource = FollowResource))
		AllSubscriptions is a FinanceResourceSubscription set		
														
	Sets
		BySubscription
			Sort Order
				HROrganization
				Subscription
				FinanceResource
				FinanceResourceSubscription
		ByBusinessClassName
			Sort Order
				HROrganization
				Subscription.BusinessClassName
				FinanceResource
				FinanceResourceSubscription
		ByResource
			Sort Order
				HROrganization
				FollowResource
				Subscription
				FinanceResource
				FinanceResourceSubscription
		ByCreatedFrom
			Sort Order
				HROrganization
				CreatedFromTask
				FinanceResource
				FinanceResourceSubscription		
											
	Actions
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Delete is a Delete Action
			restricted
		Purge is a Purge Action
			restricted

		Unsubscribe is an Instance Action
			completion message is "Unsubscribed"
			Action Rules
				invoke Purge	

		MarkAsViewed is an Instance Action
			Action Rules
				MarkAsViewedTimeStamp = current timestamp
				
		MarkAllAsViewed is an Instance Action
			Action Rules
				invoke MarkAsViewed AllSubscriptions
						
		NotifyUser is an Instance Action
			restricted
			Parameters
				Subject				is Alpha 255
				Body				is Text
			Parameter Rules
				Subject
					if (!Body entered)
						required
				Body
					if (!Subject entered)
						required
			Action Rules
				PublishedTimeStamp = current timestamp
			Exit Rules
				if (FinanceResource.SendUpdatesAsNotification)
					display "SendNotification<Subject>"
					if (PeriodSubscription)
						send notification
							to FinanceResource.agent(Actor).Actor 	
							description is "<Subject>"
							priority is medium
							detail is "<Body>"








					if (SubscribedToFollowResource)
						send notification
							to FinanceResource.agent(Actor).Actor 	
							description is "<Subject>"
							priority is medium
							detail is "<Body>"

				if (FinanceResource.SendUpdatesAsEmail)
					display "SendEmail<Subject>"
					send email
						to		FinanceResource.EmailAddress
						from 	config.DefaultFromEmailAddress
						subject "<Subject>"
						Contents
								"<Body>"				
				if (FinanceResource.SendAsDialogue)
					invoke Create FinanceResourceDialogue
						invoked.HROrganization = HROrganization
						invoked.FinanceResource = FinanceResource
						invoked.SendToContact = FinanceResource
						invoked.Author = FinanceResource
						invoked.DialogueTitle =	Subject
						invoked.Dialogue = Body
