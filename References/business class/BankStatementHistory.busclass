BankStatementHistory is a BusinessClass	
	owned by cashmgmt
	prefix is CMBSH
	
	Ontology
		symbolic key is BankStatementHistory
		
	Patterns
		disable Auditing
		implements CreateStamp
		implements UpdateStamp
		
	Persistent Fields
		UpdateDate 						is TimeStamp
			default label is "Date"
		Resource 						is a FinanceResource
		ReconciliationBalancesReport 	is BinaryDocument

	Field Rules
		UpdateDate
			default to current timestamp

		Resource
			default to actor.agent(Employee).Employee

	Conditions
		StatementBalancesReportCreated
			restricted
			when (ReconciliationBalancesReport entered)

		IsCurrentVersion
			restricted
			when (!HistoricalBankStatementHistoryRel exists)

	Relations
		HistoricalBankStatementHistoryRel
			one-to-many relation to BankStatementHistory
			Field Mapping uses symbolic key
				related.CashManagementGroup 	= CashManagementGroup
				related.CashManagementAccount 	= CashManagementAccount
				related.BankStatement		  	= BankStatement
			Instance Selection
				where (related.BankStatementHistory > BankStatementHistory)

	Sets
		ByUpdateDate
			Sort Order
				CashManagementGroup
				CashManagementAccount
				BankStatement
				UpdateDate	descending
				BankStatementHistory
				
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted
					
		EmailReconciliationBalancesReport is an Instance Action
			valid when (StatementBalancesReportCreated)
			run in background
			completion message is "EmailSent"
			disable multiple instance selection
			Parameters
				ToEmail 								is EmailAddressField with multiple addresses 
					holds pii
				FromEmail 								is an EmailAddress 
					holds pii
				CCEmail									is EmailAddressField with multiple addresses 
					holds pii
				BccEmail 								is EmailAddressField with multiple addresses 
					holds pii
				Subject 	   							is Text
				EmailContents   						is RichText
				ToResource								is a FinanceResource

			Parameter Rules
				ToEmail
					constraint (!ToResource entered)
						"EnterEitherAnEmailAddressOrAResource,NotBoth"
					if (ToResource not entered)
						required
							"MustSelectToSendEmailToAResourceOrToAnEmailAddress"
				ToResource
					constraint (ToResource.EmailAddressAvailable)
						"To_\ResourceMustHaveAnEmailAddress"
				FromEmail
					required
					initial value is actor.agent(Employee).Employee.EmployeeWorkEmailAddress
				Subject
					required
					initial value is "ReconciliationBalancesReport"
				EmailContents
					required
					initial value is "ReconciliationBalancesReportForYourReview"

			Local Fields
				LocalToEmail is EmailAddressField with multiple addresses

			Action Rules
				if (ToResource entered)
					LocalToEmail = ToResource.EmailAddress
				else
					LocalToEmail = ToEmail
				send email
					to LocalToEmail
					from FromEmail
					cc CCEmail
					bcc BccEmail
					subject "<Subject>"
					Attachments
						attachment ReconciliationBalancesReport
							name is "ReconciliationBalancesReport"
							mime type is "application/pdf"
					Contents
						"<EmailContents>"
						
		Delete is a Delete Action
			restricted
			
		Purge is a Purge Action
			restricted
