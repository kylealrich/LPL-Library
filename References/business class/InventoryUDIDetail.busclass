InventoryUDIDetail is a BusinessClass
	owned by ic
	prefix is ITT
	classic name is ICUDIDTL
	
	Ontology
		symbolic key is InventoryUDIDetail
            classic set name is ITTSET1
            classic name for InventoryUDIDetail.InventoryTransactionType is DOC-TYPE
            classic name for InventoryUDIDetail.TransactionSystemCode is SYSTEM-CD
            classic name for InventoryUDIDetail.DocumentNumberNumeric is DOC-NBR-NUM
            classic name for InventoryUDIDetail.WarehouseShipment is SHIPMENT-NBR
            classic name for InventoryUDIDetail.LineNumber is LINE-NBR
            classic name for InventoryUDIDetail.TransactionSequence is TRANS-SEQ		
	
	Patterns
		implements StaticJava
		disable AuditIndex
	
	Persistent Fields
		Item
			classic name is ITEM
		SourceDocumentAlpha
			classic name is DOCUMENT
        GTIN				is an ItemGTIN
        	classic name is GTIN
        Manufacturer
        	classic name is MANUF-CODE
        	classic name is MANUF-DIVISION
        ManufacturerNumber
        	classic name is MANUF-NBR
        Lot       			is an ItemLot
			classic name is LOT
        SerialNumber    	is an ItemSerialNumber
        	classic name is SER-NBR
        ManufacturingDate	is Date
        ExpirationDate		is Date
        TransactionDate		is Date
        Quantity
        	precision is DerivedNumberOfDecimalsQuantity
        	classic name is QUANTITY
        UOM					is a UnitOfMeasure
			protected
        	classic name is UOM
		StockUOM			is a UnitOfMeasure
			classic name is STOCK-UOM
        UOMMultiplier
            classic name is UOM-MULT
		Bin
			classic name is BIN

	Local Fields
		LocalUpdateFromWarehouseShipmentLineDetail is Boolean 
		LocalUDICount					is Numeric size 6
		LocalOldLot						is AlphaUpper size 30
		LocalOldSerial					is AlphaUpper size 30
		LocalOldBin						is AlphaUpper size 10
		LocalOldUOM						is like UnitOfMeasure
		TempCounter						is Numeric 2
		LocalBypassUDIRequired			is Boolean
		GTINCheckDigit
		LocalValidCheckDigit			is Boolean
		 
	Transient Fields
		TransientDeleteFromUDIDetail	is Boolean
		TransientCreateFromBatch		is Boolean	
		TransientTrackingNumber			is like TrackingNumber
			derive value from DerivedTrackingNumber

 	Derived Fields

		DerivedNumberOfDecimalsQuantity is a DerivedField
			type is Numeric size 1
			restricted
			return Item.NumberOfDecimalsQuantity

		DerivedSourceDocumentAlpha		is a DerivedField
			type is like SourceDocumentAlpha
			restricted
			if (InventoryUDIDetail.TransactionSystemCode	= "RQ")
				return RequisitionLineRel.Requisition
			else
				return InventoryTransactionRel.InventoryTransaction

		DerivedItem						is a DerivedField
			type is like Item
			restricted
			if (InventoryUDIDetail.TransactionSystemCode.Requisition)
				return RequisitionLineRel.Item
			else
			if (InventoryTransactionLineRel exists)
				return InventoryTransactionLineRel.Item
			else
				return Item
				
		DerivedDetailOpenQuantity		is a DerivedField
			type is like Quantity
			default label is "OpenQuantity"
			return InventoryTransactionLineDetailMappedRel.OpenQuantity
			
		DerivedDetailOriginalQuantity	is a DerivedField
			type is like Quantity
			default label is "OriginalQuantity"
			return InventoryTransactionLineDetailMappedRel.OriginalQuantity
			
		DerivedTransactionSequence 		is a DerivedField
			type is Numeric size 6
			default label is "Sequence"
			return InventoryUDIDetail.TransactionSequence
			
		DerivedUDICount					is a ComputeField
			type is Numeric size 6
			restricted
			(instance count of InventoryUDIDetailRel)


		DerivedWarehouseShipmentStatus is a DerivedField
			type is Numeric size 1
			restricted
			return WarehouseShipmentRel.Status
		
		DerivedTrackingNumber is a DerivedField
            type is like TrackingNumber
			if (MSCMShipmentUDITrackingRel exists)
            	return first MSCMShipmentUDITrackingRel.TrackingNumber
			else
				return blank


	Conditions
		IsValidGTINSize
			restricted
			when (GTIN size = 8
			or	  GTIN size = 12
			or    GTIN size = 13
			or    GTIN size = 14)
	
		NeedUDIFields
			restricted
			when (ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber not entered)

		IsInventoried
			restricted
			when (ItemLocationRel.InventoryTracked)

		IsForLotOrSerial
			restricted
			when (Lot entered
			or    SerialNumber entered)

		HasSerial
			restricted
			when (SerialNumber entered)

		IsLotTracked 
			restricted	
			when (ItemLocationRel.LotTracked.LotRequiredAtReceipt
			and   IsInventoried)

		IsLotRequiredAtReceiving 
			restricted	
			when (ItemLocationRel.LotTracked.LotRequiredAtReceipt
			and   IsInventoried)

		IsDetailRequiredAtReceipt 
			restricted	
			when ((ItemLocationRel.LotTracked.LotRequiredAtReceipt
			or     ItemLocationRel.SerialTracked.SerialRequiredAtReceipt
			or     ItemLocationRel.BinTracked)
			and	   IsInventoried)

		IsDetailRequiredAtIssue 
			restricted	
			when ((ItemLocationRel.LotTracked.LotRequiredAtIssue
			or     ItemLocationRel.SerialTracked.SerialRequiredAtIssue
			or     ItemLocationRel.BinTracked)
			and	   IsInventoried)

		IsSerialTracked 
			restricted	
			when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt
			and   IsInventoried)

		IsSerialRequiredAtReceiving 
			restricted	
			when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt
			and   IsInventoried)

		SerialAdjustmentIsDecrease 
			restricted	
			when (InventoryUDIDetail.InventoryTransactionType.Adjustment
			and	  InventoryTransactionLineRel.Quantity < 0
			and   IsSerialRequiredAtReceiving)

		SerialAdjustmentIsIncrease 
			restricted	
			when (InventoryUDIDetail.InventoryTransactionType.Adjustment
			and	  InventoryTransactionLineRel.Quantity > 0
			and   IsSerialRequiredAtReceiving)

		IsRequisitionType
			restricted
			when (RequisitionLineRel exists)

		IsInventoryReceivingType
			restricted
			when (InventoryTransactionRel.InventoryDocumentType.InventoryReceipt
			and   InventoryTransactionLineRel.TransactionSystemCode.InventoryControl)

		GTINRecordExists
			restricted
			when (ItemGTINRel exists)

		InventoryTransactionLineUnreleased
			restricted
			when (InventoryTransactionLineRel.Status.Unreleased)

		IsInventoryTransactionTypeRCIRBT
			restricted
			when (InventoryUDIDetail.InventoryTransactionType.InventoryReceipt
			or    InventoryUDIDetail.InventoryTransactionType.IntransitReceiving 
			or    InventoryUDIDetail.InventoryTransactionType.BinTransfer)

		IsForLotSerialDocumentType
			restricted
			when (IsForLotOrSerial
			and   IsInventoryTransactionTypeRCIRBT)

		IsNotLotSerialBinTracked
			restricted
			when (!ItemLocationRel.IsLotTracked
			and   !ItemLocationRel.IsSerialTracked
			and   !ItemLocationRel.BinTracked)
		
		DetailWithOpenQuantity
			restricted
			when ((InventoryTransactionLineDetailMappedRel exists
			and    InventoryTransactionLineDetailMappedRel.OpenQuantity > 0)
			or     InventoryTransactionLineDetailMappedRel not exists)
		
		IsValidForCreateUpdateDelete
			restricted
			when (ItemLocationRel.IsUDITracked
			and ((InventoryUDIDetail.TransactionSystemCode.InventoryControl and IsTransactionLineUnreleased)
			or   (InventoryUDIDetail.TransactionSystemCode.Requisition and InventoryUDIDetail.WarehouseShipmentRel.IsPrintedPickedOrPacked)))
		
		IsTransactionLineUnreleased
			restricted
			when ((InventoryTransactionLineRel exists and InventoryTransactionLineRel.Status.Unreleased)
			or    (InventoryTransactionLineFromWHRel exists and InventoryTransactionLineFromWHRel.Status.Unreleased))
		





		
		IsUDIAndBinTracked
			restricted
			when (ItemLocationRel.IsUDITracked
			and ((InventoryUDIDetail.TransactionSystemCode.InventoryControl and ItemLocationRel.BinTracked)
			or   (InventoryUDIDetail.TransactionSystemCode.Requisition and (WarehouseShipmentLineRel.ItemLocation.BinTracked or ItemLocationRel.BinTracked))))
	
		CannotChangeBinDetail
			restricted
			when (InventoryTransactionLineRel exists
			and  (InventoryTransactionLineRel.IsBinTrackedItemWithSingleBin and not LocalBypassUDIRequired))

		IsLotRequired
			restricted
			when (((InventoryTransactionRel.IsInventoryReceipt
			or      InventoryTransactionRel.IsInventoryTransfer
			or      InventoryTransactionRel.IsDirectTransfer
			or      InventoryTransactionRel.IsInventoryReceivingTransfer
			or      InventoryTransactionRel.IsInventoryIntransitReceiving
			or      InventoryTransactionRel.IsInventoryBinTransfer
			or      InventoryTransactionRel.IsInventoryAdjustments
			or		InventoryTransactionRel.IsRequisitionReturn)
			and     InventoryTransactionLineRel.ItemLocation.LotTracked.LotRequiredAtReceipt)
			or     (InventoryTransactionRel.IsInventoryIssues
			and     InventoryTransactionLineRel.ItemLocation.IsLotTracked))
			
		IsSerialRequired
			restricted
			when (((InventoryTransactionRel.IsInventoryReceipt
			or      InventoryTransactionRel.IsInventoryTransfer
			or      InventoryTransactionRel.IsDirectTransfer
			or      InventoryTransactionRel.IsInventoryIntransitReceiving
			or 		InventoryTransactionRel.IsInventoryReceivingTransfer
			or      InventoryTransactionRel.IsInventoryBinTransfer
			or      InventoryTransactionRel.IsInventoryAdjustments
			or		InventoryTransactionRel.IsRequisitionReturn)
			and     InventoryTransactionLineRel.ItemLocation.SerialTracked.SerialRequiredAtReceipt)
			or     (InventoryTransactionRel.IsInventoryIssues
			and     InventoryTransactionLineRel.ItemLocation.IsSerialTracked))
		
		IsLotRequiredForWH
			restricted
			when (WarehouseShipmentLineRel.ItemLocation.IsLotTracked)
			
		IsSerialRequiredForWH
			restricted
			when (WarehouseShipmentLineRel.ItemLocation.IsSerialTracked)
		
		IsValidForSplitDetail
			restricted	
			when (InventoryUDIDetail.TransactionSystemCode.Requisition
			and   InventoryUDIDetail.InventoryTransactionType.IntransitReceiving
			and   ItemLocationRel.BinTracked)


		HasWarehouseShipmentPrinted
			restricted
			when(WarehouseShipmentRel exists and WarehouseShipmentRel.Status.Printed)

				
	Field Rules
		GTIN
			if (not LocalBypassUDIRequired)
				required
					"GTINIsRequired,ItemIsUDITracked"

			constraint (IsValidGTINSize)
				"GTINLengthIsInvalid"

			GTINCheckDigit.ItemGTIN		= GTIN
			LocalValidCheckDigit        = GTINCheckDigit.ValidCheckDigit
			constraint (LocalValidCheckDigit)
				"GTINCheckDigitIsInvalid"
				
		SourceDocumentAlpha
			default to DerivedSourceDocumentAlpha

		Manufacturer
			if (Manufacturer not entered)
				default to ItemGTINRel.Manufacturer
				
		ManufacturerNumber
			if (ManufacturerNumber not entered)
				default to ItemGTINRel.ManufacturerNumber 
				
		Lot
			if (ExpirationDate entered)
				required
					"LotIsRequiredWhenExpirationDateIsEntered"
					
		ExpirationDate
			if (Lot entered)
				required
					"ExpirationDateIsRequiredWhenLotIsEntered"
		
			if (ExpirationDate entered)
				constraint (ExpirationDate not < current corporate date)
					"ExpirationDateCannotBePriorToCurrentDate"
				
		Quantity
		
			if (CannotChangeBinDetail)
				cannot be changed
					"CannotUpdateQuantity;MustUpdateFromInventoryTransactionLine"
		
			if (not InventoryUDIDetail.InventoryTransactionType.IntransitReceiving)
				required
			if (HasSerial)
				if  (SerialAdjustmentIsDecrease)
					force default to -1
				else
					force default to 1













		UOM
			
			if (InventoryUDIDetail.TransactionSystemCode.InventoryControl)
				default to InventoryTransactionLineRel.TransactionUOM
			else
			if (InventoryUDIDetail.TransactionSystemCode.Requisition)
				default to WarehouseShipmentLineRel.TransactionUOM
			
			if (CannotChangeBinDetail)
				cannot be changed
					"CannotUpdateUOM;MustUpdateFromInventoryTransactionLine"

			if (HasSerial)
				constraint (UOM = Item.StockUOM)
					"MustUseStockUOMIfSerialIsIndicated"
		

		UOMMultiplier
			if (UOMMultiplier not entered
			or  UOM changed)
				default to ItemUOMRel.UOMConversion

		Bin

			
			if (CannotChangeBinDetail)
				cannot be changed
					"CannotUpdateBin;MustUpdateFromInventoryTransactionLine"
					
				if (InventoryTransactionRel.IsInventoryBinTransfer)					
					if (InventoryTransactionLineRel.IsItemLocationLotTracked)
						default to InventoryTransactionLineRel.Bin
					else
						if (InventoryTransactionLineRel.IsItemLocationSerialTracked)
							default to InventoryTransactionLineRel.FromToCompanyLocationBin.FromToBin
				else
					if (ItemLocationRel.BinTracked)								








						








		
						if (!InventoryTransactionRel.IsInventoryIntransitReceiving)	
							required
							
						
			













					else 				
						cannot be entered	
							"CannotEnterBinItem<Item>IsNotBinTracked"

    Rule Blocks
    
		CreateInventoryTransactionLineDetail
			invoke SystemCreate InventoryTransactionLineDetail
				invoked.Company													= Company
				invoked.InventoryLocation										= InventoryLocation
				invoked.InventoryTransactionLineDetail							= InventoryUDIDetail.TransactionSequence
				invoked.InventoryTransaction									= InventoryUDIDetail.DocumentNumberNumeric
				invoked.TransactionSystemCode									= TransactionSystemCode.InventoryControl
				invoked.InventoryTransactionLine.WarehouseShipment				= InventoryUDIDetail.WarehouseShipment
				invoked.InventoryTransactionLine.LineNumber						= InventoryUDIDetail.LineNumber
				initialize invoked.InventoryTransactionLine.ComponentSequence
				invoked.UnitOfMeasure											= InventoryUDIDetail.UOM
				if (IsLotRequired)
					invoked.Lot													= InventoryUDIDetail.Lot

					initialize invoked.Serial
				else
				if (IsSerialRequired)
					invoked.Serial												= InventoryUDIDetail.SerialNumber	
					initialize invoked.Lot	
					initialize invoked.LotExpirationDate		
				invoked.Bin														= InventoryUDIDetail.Bin	
				invoked.Quantity												= InventoryUDIDetail.Quantity
				invoked.Item													= InventoryUDIDetail.Item
				invoked.UOMMultiplier											= InventoryUDIDetail.UOMMultiplier
				
		CreateInventoryTransactionLineDetailFromTransfer
			invoke Unreleased.QuickCreate InventoryTransactionLineDetail
				invoked.Company													= Company
				invoked.InventoryLocation										= InventoryLocation
				invoked.InventoryTransactionLineDetail							= InventoryUDIDetail.TransactionSequence
				invoked.InventoryTransaction									= InventoryUDIDetail.DocumentNumberNumeric
				invoked.TransactionSystemCode									= TransactionSystemCode.InventoryControl
				invoked.InventoryTransactionLine.WarehouseShipment				= InventoryUDIDetail.WarehouseShipment
				invoked.InventoryTransactionLine.LineNumber						= InventoryUDIDetail.LineNumber
				initialize invoked.InventoryTransactionLine.ComponentSequence
				invoked.TransactionDate											= InventoryUDIDetail.TransactionDate	
				invoked.UnitOfMeasure											= InventoryUDIDetail.UOM
				if (IsLotRequired)
					invoked.Lot													= InventoryUDIDetail.Lot

					initialize invoked.Serial
				else
				if (IsSerialRequired)
					invoked.Serial												= InventoryUDIDetail.SerialNumber	
					initialize invoked.Lot			
					initialize invoked.LotExpirationDate		
				invoked.Bin														= InventoryUDIDetail.Bin	
				invoked.Quantity												= InventoryUDIDetail.Quantity
				invoked.Item													= InventoryUDIDetail.Item
				invoked.UOMMultiplier											= InventoryUDIDetail.UOMMultiplier
				if (InventoryTransactionRel.IsInventoryIntransitReceiving)
					invoked.OriginalQuantity									= Quantity
					invoked.OpenQuantity										= Quantity	
				else
					initialize invoked.OpenQuantity
					initialize invoked.OriginalQuantity

		UpdateInventoryTransactionLineDetail
			invoke SystemUpdate InventoryTransactionLineDetailMappedRel
				if (IsLotRequired)
					invoked.Lot													= InventoryUDIDetail.Lot

					initialize invoked.Serial
				else
				if (IsSerialRequired)
					invoked.Serial												= InventoryUDIDetail.SerialNumber	
					initialize invoked.Lot			
					initialize invoked.LotExpirationDate		
				invoked.Bin														= InventoryUDIDetail.Bin
				invoked.Quantity												= InventoryUDIDetail.Quantity

		DeleteInventoryTransactionLineDetail
			invoke SystemDelete InventoryTransactionLineDetailMappedRel
				
		CreateDetailsForWarehouse
			if (InventoryUDIDetail.InventoryTransactionType.IntransitReceiving)
				invoke SystemCreate InventoryTransactionLineDetail
					invoked.Company													= Company
					invoked.InventoryLocation										= InventoryLocation
					invoked.InventoryTransactionLineDetail							= InventoryUDIDetail.TransactionSequence
					invoked.InventoryTransaction									= InventoryTransactionLineFromWHRel.InventoryTransaction
					invoked.TransactionSystemCode									= TransactionSystemCode.InventoryControl
					invoked.InventoryTransactionLine.WarehouseShipment				= InventoryTransactionLineFromWHRel.InventoryTransactionLine.WarehouseShipment
					invoked.InventoryTransactionLine.LineNumber						= InventoryTransactionLineFromWHRel.InventoryTransactionLine.LineNumber
					initialize invoked.InventoryTransactionLine.ComponentSequence
					invoked.UnitOfMeasure											= InventoryUDIDetail.UOM
					if (IsLotRequired)
						invoked.Lot													= InventoryUDIDetail.Lot

						initialize invoked.Serial
					else
					if (IsSerialRequired)
						invoked.Serial												= InventoryUDIDetail.SerialNumber	
						initialize invoked.Lot	
						initialize invoked.LotExpirationDate		
					invoked.Bin														= InventoryUDIDetail.Bin	
					invoked.Quantity												= InventoryUDIDetail.Quantity
					invoked.Item													= InventoryUDIDetail.Item
					invoked.UOMMultiplier											= InventoryUDIDetail.UOMMultiplier
			else
				invoke SystemCreate WarehouseShipmentLineDetail
					invoked.Company						   							= Company
					invoked.InventoryLocation										= InventoryLocation
					invoked.WarehouseShipment										= InventoryUDIDetail.WarehouseShipment
					invoked.WarehouseShipmentLine									= InventoryUDIDetail.LineNumber
					invoked.WarehouseShipmentLineComponent 							= blank
					invoked.WarehouseShipmentLineDetail.Bin							= InventoryUDIDetail.Bin
					if (IsLotRequiredForWH)
						invoked.WarehouseShipmentLineDetail.Lot						= InventoryUDIDetail.Lot
						initialize invoked.WarehouseShipmentLineDetail.Serial
					if (IsSerialRequiredForWH)	
						invoked.WarehouseShipmentLineDetail.Serial					= InventoryUDIDetail.SerialNumber
						initialize invoked.WarehouseShipmentLineDetail.Lot
					invoked.WarehouseShipmentLineDetail.UnitOfMeasure				= InventoryUDIDetail.UOM
					invoked.WarehouseShipmentLineDetail.UOMMultiplier				= InventoryUDIDetail.UOMMultiplier
					invoked.Quantity												= InventoryUDIDetail.Quantity
		
		UpdateDetailsForWarehouse
			if (InventoryUDIDetail.InventoryTransactionType.IntransitReceiving)
				invoke SystemUpdate InventoryTransactionLineDetailFromWHRel
					invoked.Bin														= InventoryUDIDetail.Bin
					if (IsLotRequired)
						invoked.Lot													= InventoryUDIDetail.Lot
						initialize invoked.Serial
					if (IsSerialRequired)
						invoked.Serial												= InventoryUDIDetail.SerialNumber	
						initialize invoked.Lot			

					invoked.Quantity												= InventoryUDIDetail.Quantity
			else
				invoke SystemUpdate WarehouseShipmentLineDetailUpdateRel
					invoked.WarehouseShipmentLineDetail.Bin							= InventoryUDIDetail.Bin
					if (IsLotRequiredForWH)
						invoked.WarehouseShipmentLineDetail.Lot						= InventoryUDIDetail.Lot
						initialize invoked.WarehouseShipmentLineDetail.Serial
					if (IsSerialRequiredForWH)
						invoked.WarehouseShipmentLineDetail.Serial					= InventoryUDIDetail.SerialNumber	
						initialize invoked.WarehouseShipmentLineDetail.Lot			

					invoked.Quantity												= InventoryUDIDetail.Quantity
		


			
    Relations

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            required
            Field Mapping uses symbolic key
                related.Company 		= Company

    	ItemUOMRel						
    		one-to-one relation to ItemUOM
    		Field Mapping uses symbolic key
    			related.ItemGroup		= InventoryCompanyRel.ItemGroup
    			related.Item			= Item
    			related.UnitOfMeasure	= UOM
                            
        RequisitionLineRel
            one-to-one relation to RequisitionLine 
            required
            Field Mapping uses symbolic key
                related.Company 					= Company
                related.Requisition					= InventoryUDIDetail.DocumentNumberNumeric
                related.RequisitionLine				= InventoryUDIDetail.LineNumber

		ItemGTINRel
			one-to-one relation to ItemGTIN
			Field Mapping uses symbolic key
				related.ItemGroup			= InventoryCompanyRel.ItemGroup
				related.Item				= DerivedItem
				related.ItemGTIN			= GTIN

        ItemLocationRel
            one-to-one relation to ItemLocation
            required
            Field Mapping uses symbolic key
                related.Company						= Company
                related.InventoryLocation			= InventoryLocation
                related.Item						= DerivedItem
                
        WarehouseShipmentRel
        	one-to-one relation to WarehouseShipment
        	Field Mapping uses symbolic key
        		related.Company						= Company
        		related.InventoryLocation			= InventoryLocation
        		related.WarehouseShipment			= InventoryUDIDetail.WarehouseShipment

        InventoryTransactionRel
            one-to-one relation to InventoryTransaction
            required
            Field Mapping uses symbolic key
                related.Company						= Company
                related.InventoryLocation			= InventoryLocation
                related.InventoryTransaction		= InventoryUDIDetail.DocumentNumberNumeric
		
		InventoryTransactionLineFromWHRel
			one-to-many relation to InventoryTransactionLine
			Field Mapping uses symbolic key
				related.Company						= Company
                related.InventoryLocation			= InventoryLocation
            Instance Selection
            	where (related.InventoryTransaction.InventoryDocumentType.IntransitReceiving
            	and    related.TransactionSystemCode.InventoryControl 
            	and    related.DerivedShipmentForUDI 					  = InventoryUDIDetail.WarehouseShipment
            	and    related.DerivedShipmentLineForUDI 				  = InventoryUDIDetail.LineNumber
            	and    related.InventoryTransactionLine.ComponentSequence = blank)
            	
        InventoryTransactionLineDetailFromWHRel
			one-to-many relation to InventoryTransactionLineDetail
    		Field Mapping uses symbolic key
    			related.Company															= Company
				related.InventoryLocation												= InventoryLocation
			Instance Selection	
				where (related.InventoryTransaction										= first InventoryTransactionLineFromWHRel.InventoryTransaction
				and    related.TransactionSystemCode.InventoryControl
				and    related.InventoryTransactionLine.DerivedShipmentForUDI			= InventoryUDIDetail.WarehouseShipment
				and    related.InventoryTransactionLine.DerivedShipmentLineForUDI		= InventoryUDIDetail.LineNumber
				and    related.InventoryTransactionLineDetail							= InventoryUDIDetail.TransactionSequence)
		
        InventoryTransactionLineRel
            one-to-one relation to InventoryTransactionLine
            required
            Field Mapping uses symbolic key
                related.Company										= Company
                related.InventoryLocation							= InventoryLocation
                related.InventoryTransaction						= InventoryUDIDetail.DocumentNumberNumeric
				related.TransactionSystemCode						= InventoryUDIDetail.TransactionSystemCode
                related.InventoryTransactionLine.WarehouseShipment	= InventoryUDIDetail.WarehouseShipment
				related.InventoryTransactionLine.LineNumber			= InventoryUDIDetail.LineNumber
				related.InventoryTransactionLine.ComponentSequence	= 0
				
		InventoryTransactionLineDetailMappedRel
			one-to-one relation to InventoryTransactionLineDetail
    		Field Mapping uses symbolic key
    			related.Company															= Company
				related.InventoryLocation												= InventoryLocation
				related.InventoryTransaction											= InventoryUDIDetail.DocumentNumberNumeric
				related.TransactionSystemCode											= TransactionSystemCode.InventoryControl
				related.InventoryTransactionLine.WarehouseShipment						= InventoryUDIDetail.WarehouseShipment
				related.InventoryTransactionLine.LineNumber								= InventoryUDIDetail.LineNumber
				related.InventoryTransactionLine.ComponentSequence						= blank
				related.InventoryTransactionLineDetail									= InventoryUDIDetail.TransactionSequence
    	
        InventoryUDIDetailRel  
            one-to-many relation to InventoryUDIDetail
            Field Mapping uses symbolic key
                related.Company 												= Company
                related.InventoryLocation										= InventoryLocation
            Instance Selection
                where (related.InventoryUDIDetail.InventoryTransactionType		= InventoryUDIDetail.InventoryTransactionType
                and    related.InventoryUDIDetail.TransactionSystemCode			= InventoryUDIDetail.TransactionSystemCode
                and    related.InventoryUDIDetail.DocumentNumberNumeric			= InventoryUDIDetail.DocumentNumberNumeric
                and    related.InventoryUDIDetail.WarehouseShipment				= InventoryUDIDetail.WarehouseShipment
                and    related.InventoryUDIDetail.LineNumber					= InventoryUDIDetail.LineNumber) 

        InventoryUDIDetailDuplicateRel  
            one-to-many relation to InventoryUDIDetail
            Field Mapping uses symbolic key
                related.Company 												= Company
                related.InventoryLocation										= InventoryLocation
            Instance Selection 
                where (related.InventoryUDIDetail.InventoryTransactionType		= InventoryUDIDetail.InventoryTransactionType
                and   related.InventoryUDIDetail.TransactionSystemCode			= InventoryUDIDetail.TransactionSystemCode
                and   related.InventoryUDIDetail.DocumentNumberNumeric			= InventoryUDIDetail.DocumentNumberNumeric
                and   related.InventoryUDIDetail.WarehouseShipment				= InventoryUDIDetail.WarehouseShipment
                and   related.InventoryUDIDetail.LineNumber						= InventoryUDIDetail.LineNumber
                and   related.Item												= DerivedItem
                and   related.UOM												= UOM
                and   related.GTIN												= GTIN
                and   related.Lot												= Lot
                and   related.SerialNumber										= SerialNumber) 


		InventoryTransactionLineDetailRel						
    		one-to-many relation to InventoryTransactionLineDetail
    		Field Mapping uses symbolic key
    			related.Company															= Company
				related.InventoryLocation												= InventoryLocation
				related.InventoryTransaction											= InventoryUDIDetail.DocumentNumberNumeric
				related.TransactionSystemCode											= "IC"
				related.InventoryTransactionLine.WarehouseShipment						= InventoryUDIDetail.WarehouseShipment
				related.InventoryTransactionLine.LineNumber								= InventoryUDIDetail.LineNumber
				related.InventoryTransactionLine.ComponentSequence						= 0
      		Instance Selection
    			where (related.InventoryTransactionLine.ComponentSequence				= 0)
		
    	InventoryTransactionLineDetailUpdateRel						
    		one-to-many relation to InventoryTransactionLineDetail
    		Field Mapping uses symbolic key
    			related.Company															= Company
				related.InventoryLocation												= InventoryLocation
				related.InventoryTransaction											= InventoryUDIDetail.DocumentNumberNumeric
				related.TransactionSystemCode											= "IC"
				related.InventoryTransactionLine.WarehouseShipment						= InventoryUDIDetail.WarehouseShipment
				related.InventoryTransactionLine.LineNumber								= InventoryUDIDetail.LineNumber
				related.InventoryTransactionLine.ComponentSequence						= 0
      			related.InventoryTransactionLineDetail									= InventoryUDIDetail.TransactionSequence
      		Instance Selection
    			where (related.Serial													= LocalOldSerial
				and    related.Lot														= LocalOldLot
				and    related.Bin														= LocalOldBin)

    	InventoryTransactionLineDetailDeleteRel						
    		one-to-many relation to InventoryTransactionLineDetail
    		Field Mapping uses symbolic key
    			related.Company															= Company
				related.InventoryLocation												= InventoryLocation
				related.InventoryTransaction											= InventoryUDIDetail.DocumentNumberNumeric
				related.TransactionSystemCode											= "IC"
				related.InventoryTransactionLine.WarehouseShipment						= InventoryUDIDetail.WarehouseShipment
				related.InventoryTransactionLine.LineNumber								= InventoryUDIDetail.LineNumber
				related.InventoryTransactionLine.ComponentSequence						= 0
      		Instance Selection
    			where (related.Serial													= InventoryUDIDetail.SerialNumber
				and    related.Lot														= InventoryUDIDetail.Lot)

    	InventoryTransactionLineDetailBinRel						
    		one-to-many relation to InventoryTransactionLineDetail
    		Field Mapping uses symbolic key
    			related.Company															= Company
				related.InventoryLocation												= InventoryLocation
				related.InventoryTransaction											= InventoryUDIDetail.DocumentNumberNumeric
				related.TransactionSystemCode											= "IC"
				related.InventoryTransactionLine.WarehouseShipment						= InventoryUDIDetail.WarehouseShipment
				related.InventoryTransactionLine.LineNumber								= InventoryUDIDetail.LineNumber
				related.InventoryTransactionLine.ComponentSequence						= 0
      		Instance Selection
    			where (related.Serial													= InventoryUDIDetail.SerialNumber
				and    related.Lot														= InventoryUDIDetail.Lot)
		
		WarehouseShipmentLineDetailRel
			one-to-one relation to WarehouseShipmentLineDetail
			Field Mapping uses symbolic key
				related.Company						   									= Company
				related.InventoryLocation												= InventoryLocation
				related.WarehouseShipment												= InventoryUDIDetail.WarehouseShipment
				related.WarehouseShipmentLine											= InventoryUDIDetail.LineNumber
				related.WarehouseShipmentLineComponent 									= blank
				related.WarehouseShipmentLineDetail.Bin									= InventoryUDIDetail.Bin
				related.WarehouseShipmentLineDetail.Lot									= InventoryUDIDetail.Lot
				related.WarehouseShipmentLineDetail.Sublot						   		= blank
				related.WarehouseShipmentLineDetail.UnitOfMeasure						= InventoryUDIDetail.UOM
				related.WarehouseShipmentLineDetail.Serial								= InventoryUDIDetail.SerialNumber
		
		WarehouseShipmentLineDetailUpdateRel
			one-to-one relation to WarehouseShipmentLineDetail
			Field Mapping uses symbolic key
				related.Company						   									= Company
				related.InventoryLocation												= InventoryLocation
				related.WarehouseShipment												= InventoryUDIDetail.WarehouseShipment
				related.WarehouseShipmentLine											= InventoryUDIDetail.LineNumber
				related.WarehouseShipmentLineComponent 									= blank
				related.WarehouseShipmentLineDetail.Bin									= LocalOldBin
				related.WarehouseShipmentLineDetail.Lot									= LocalOldLot
				related.WarehouseShipmentLineDetail.Sublot						   		= blank
				related.WarehouseShipmentLineDetail.UnitOfMeasure						= LocalOldUOM
				related.WarehouseShipmentLineDetail.Serial								= LocalOldSerial
				
		DuplicateUDIDetailForWarehouseShipmentRel
			one-to-many relation to InventoryUDIDetail
			Field Mapping uses symbolic key
				related.Company 														= Company
                related.InventoryLocation												= InventoryLocation
			Instance Selection
				where (related.InventoryUDIDetail.LineNumber							= InventoryUDIDetail.LineNumber
				and    related.InventoryUDIDetail.InventoryTransactionType				= InventoryUDIDetail.InventoryTransactionType
                and    related.InventoryUDIDetail.TransactionSystemCode					= InventoryUDIDetail.TransactionSystemCode
                and    related.InventoryUDIDetail.DocumentNumberNumeric					= InventoryUDIDetail.DocumentNumberNumeric
                and    related.InventoryUDIDetail.WarehouseShipment						= InventoryUDIDetail.WarehouseShipment
				and	   related.Lot														= InventoryUDIDetail.Lot
				and    related.UOM														= InventoryUDIDetail.UOM
				and    related.SerialNumber												= InventoryUDIDetail.SerialNumber
				and    related.Bin														= InventoryUDIDetail.Bin
				and    related.InventoryUDIDetail.TransactionSequence 				   != InventoryUDIDetail.TransactionSequence)
		
		WarehouseShipmentLineRel
			one-to-one relation to WarehouseShipmentLine
			Field Mapping uses symbolic key
				related.Company															= Company
				related.InventoryLocation												= InventoryLocation
				related.WarehouseShipment												= InventoryUDIDetail.WarehouseShipment
				related.WarehouseShipmentLine											= InventoryUDIDetail.LineNumber		


		MobileSupplyChainConfigurationRel
            one-to-one relation to MobileSupplyChainConfiguration
            Field Mapping uses symbolic key
                related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup
	
		MSCMShipmentUDITrackingRel
			one-to-many relation to MobileSupplyChainShipmentUDITracking
			Field Mapping uses symbolic key
				related.Company                              = Company
				related.InventoryLocation                    = InventoryLocation
				related.WarehouseShipment                    = InventoryUDIDetail.WarehouseShipment
				related.WarehouseShipmentLine                = InventoryUDIDetail.LineNumber
				related.InventoryUDIDetail                   = InventoryUDIDetail

    Sets

        ByItemLocationLotSerial
            indexed
            duplicates
            Instance Selection
                where (IsForLotSerialDocumentType)
            Sort Order
                Company
                Item
                InventoryLocation
				Lot
				SerialNumber

	Actions
		Create is a Create Action
			valid when (IsValidForCreateUpdateDelete)
			Entrance Rules
				if (InventoryUDIDetail.TransactionSystemCode.Requisition)
					if (InventoryUDIDetail.TransactionSequence not entered)
						if (InventoryUDIDetailRel not exists)
							InventoryUDIDetail.TransactionSequence = 1
						else
							InventoryUDIDetail.TransactionSequence = last InventoryUDIDetailRel.InventoryUDIDetail.TransactionSequence + 1
				else
					if (InventoryUDIDetail.TransactionSequence not entered)
						InventoryUDIDetail.TransactionSequence = InventoryTransactionLineRel.UDITransactionSequence + 1
					
					if (InventoryUDIDetail.TransactionSequence > InventoryTransactionLineRel.UDITransactionSequence)
						invoke FastUpdate InventoryTransactionLineRel
							invoked.UDITransactionSequence = InventoryUDIDetail.TransactionSequence
				
				
				
			Field Rules
				Item
					default to DerivedItem
				StockUOM
					default to Item.StockUOM
				TransactionDate = current date					

			Action Rules

				if (IsNotLotSerialBinTracked)
					constraint (InventoryUDIDetailDuplicateRel not exists)
						"UDIDetailAlreadyExists"
						
				if (InventoryUDIDetail.TransactionSystemCode.Requisition)
					constraint (DuplicateUDIDetailForWarehouseShipmentRel not exists)
						"UDIDetailAlreadyExists"

				if (GTIN entered) 
					constraint (!NeedUDIFields)
						"AtLeast1ProductIdentifierIsRequired"
						
				constraint (not InventoryTransactionLineRel.IsBinTrackedItemWithSingleBin)
					"CannotCreate;UDIDetailsForSingleBinExist"
				
			Exit Rules

				if (WarehouseShipmentLineRel.IsTrackedByContainer)
					invoke Create MobileSupplyChainShipmentUDITracking
						invoked.Company                              = Company
						invoked.InventoryLocation                    = InventoryLocation
						invoked.WarehouseShipment                    = InventoryUDIDetail.WarehouseShipment
						invoked.WarehouseShipmentLine                = InventoryUDIDetail.LineNumber
						invoked.InventoryUDIDetail                   = InventoryUDIDetail
						invoked.TrackingNumber                       = TransientTrackingNumber
						invoked.Quantity							 = Quantity
						invoked.UnitOfMeasure						 = UOM

				if(!TransientCreateFromBatch)
					if (InventoryUDIDetail.TransactionSystemCode.Requisition)
						if  (InventoryUDIDetail.WarehouseShipment entered
						and (IsDetailRequiredAtIssue or IsDetailRequiredAtReceipt)
						and  WarehouseShipmentLineDetailRel not exists)
							include CreateDetailsForWarehouse
					else
						if (InventoryTransactionLineDetailMappedRel not exists)
							if  ((IsDetailRequiredAtReceipt
							and  (InventoryTransactionRel.InventoryDocumentType.InventoryReceipt
							or    InventoryTransactionRel.InventoryDocumentType.InventoryIssue
							or    InventoryTransactionRel.InventoryDocumentType.Adjustment
							or    InventoryTransactionRel.InventoryDocumentType.InventoryTransfer
							or	  InventoryTransactionRel.InventoryDocumentType.IntransitReceiving
							or    InventoryTransactionRel.InventoryDocumentType.ReceivingTransfer
							or    InventoryTransactionRel.InventoryDocumentType.BinTransfer))
							or   (IsDetailRequiredAtIssue
							and   InventoryTransactionRel.InventoryDocumentType.InventoryIssue))
								include CreateInventoryTransactionLineDetail
		
		CreateUDIForSingleBin is a Create Action
			restricted
			Entrance Rules
				if (InventoryUDIDetail.TransactionSequence not entered)
					InventoryUDIDetail.TransactionSequence = InventoryTransactionLineRel.UDITransactionSequence + 1
				
				if (InventoryUDIDetail.TransactionSequence > InventoryTransactionLineRel.UDITransactionSequence)
					invoke FastUpdate InventoryTransactionLineRel
						invoked.UDITransactionSequence = InventoryUDIDetail.TransactionSequence
						
			Exit Rules
						
		CreateUDIDetailForTransfer is a Create Action
			restricted
			Entrance Rules
			
				if (InventoryUDIDetail.TransactionSequence not entered)
					InventoryUDIDetail.TransactionSequence = InventoryTransactionLineRel.UDITransactionSequence + 1
				
				if (InventoryUDIDetail.TransactionSequence > InventoryTransactionLineRel.UDITransactionSequence)
					invoke FastUpdate InventoryTransactionLineRel
						invoked.UDITransactionSequence = InventoryUDIDetail.TransactionSequence
			
			Action Rules
			Exit Rules
				if  (InventoryTransactionLineDetailMappedRel not exists)
					if  ((IsDetailRequiredAtReceipt
					and  (InventoryTransactionRel.InventoryDocumentType.InventoryTransfer
					or	  InventoryTransactionRel.InventoryDocumentType.IntransitReceiving
					or    InventoryTransactionRel.InventoryDocumentType.ReceivingTransfer)))
						include CreateInventoryTransactionLineDetailFromTransfer
							
		CreateTransferUDIDetailFromWarehouse is a Create Action
			restricted
			Entrance Rules
				if (InventoryUDIDetail.TransactionSystemCode.Requisition)
					if (InventoryUDIDetail.TransactionSequence not entered)
						if (InventoryUDIDetailRel not exists)
							InventoryUDIDetail.TransactionSequence = 1
						else
							InventoryUDIDetail.TransactionSequence = last InventoryUDIDetailRel.InventoryUDIDetail.TransactionSequence + 1
					
		Update is an Update Action
        	valid when (IsValidForCreateUpdateDelete)
			Local Fields
				LocalTrackingNumber is a TrackingNumber
				
			Field Rules

				Lot
				    if (Lot changed)
				        LocalOldLot		= old Lot
					else
				        LocalOldLot		= Lot
				        						
				SerialNumber
				    if (SerialNumber changed)
				        LocalOldSerial	= old SerialNumber
					else
				        LocalOldSerial	= SerialNumber

				Bin
				    if (Bin changed)
				        LocalOldBin		= old Bin
					else
				        LocalOldBin		= Bin
				
				UOM
					if (UOM changed)
						LocalOldUOM		= old UOM
					else
						LocalOldUOM		= UOM
				
				ExpirationDate
					if (ExpirationDate changed
					and LocalOldLot		= blank
					and LocalOldSerial	= blank
					and LocalOldBin		= blank)
					    LocalOldLot		= Lot
					    LocalOldSerial	= SerialNumber
					    LocalOldBin		= Bin
					    LocalOldUOM		= UOM
				
				Quantity
					if (Quantity changed
					and LocalOldLot		= blank
					and LocalOldSerial	= blank
					and LocalOldBin		= blank)
					    LocalOldLot		= Lot
					    LocalOldSerial	= SerialNumber
					    LocalOldBin		= Bin
					    LocalOldUOM		= UOM

			Action Rules
				if (GTIN entered
				and !LocalUpdateFromWarehouseShipmentLineDetail) 
					constraint (!NeedUDIFields)
						"AtLeast1ProductIdentifierIsRequired"
						
				if (InventoryUDIDetail.TransactionSystemCode.Requisition)
					constraint (DuplicateUDIDetailForWarehouseShipmentRel not exists)
						"UDIDetailAlreadyExists"

			Exit Rules
				if (WarehouseShipmentLineRel.IsTrackedByContainer)

					if (MSCMShipmentUDITrackingRel exists)
						if (Quantity changed or TransientTrackingNumber changed)
							if (TransientTrackingNumber entered)
								LocalTrackingNumber = TransientTrackingNumber

							invoke Update MSCMShipmentUDITrackingRel
								invoked.Quantity = Quantity
								invoked.TrackingNumber = LocalTrackingNumber
					else
						invoke Create MobileSupplyChainShipmentUDITracking
							invoked.Company                              = Company
							invoked.InventoryLocation                    = InventoryLocation
							invoked.WarehouseShipment                    = InventoryUDIDetail.WarehouseShipment
							invoked.WarehouseShipmentLine                = InventoryUDIDetail.LineNumber
							invoked.InventoryUDIDetail                   = InventoryUDIDetail
							invoked.TrackingNumber                       = TransientTrackingNumber
							invoked.Quantity							 = Quantity
							invoked.UnitOfMeasure						 = UOM
						
				if (InventoryUDIDetail.TransactionSystemCode.Requisition)
					if  (InventoryUDIDetail.WarehouseShipment entered
					and (IsDetailRequiredAtIssue or IsDetailRequiredAtReceipt))
						include UpdateDetailsForWarehouse
				else
				if (!InventoryTransactionLineRel.IsBinTrackedItemWithSingleBin)
					if  ((IsDetailRequiredAtReceipt
					and  (InventoryTransactionRel.InventoryDocumentType.InventoryReceipt
					or    InventoryTransactionRel.InventoryDocumentType.InventoryIssue
					or    InventoryTransactionRel.InventoryDocumentType.Adjustment
					or    InventoryTransactionRel.InventoryDocumentType.InventoryTransfer
					or	  InventoryTransactionRel.InventoryDocumentType.IntransitReceiving
					or    InventoryTransactionRel.InventoryDocumentType.BinTransfer))
					or   (IsDetailRequiredAtIssue
					and   InventoryTransactionRel.InventoryDocumentType.InventoryIssue))
						include UpdateInventoryTransactionLineDetail
		
		QuickUpdate is an Update Action
			restricted
				
		Delete is a Delete Action
        	valid when (IsValidForCreateUpdateDelete)

			Action Rules
	        	constraint (not InventoryTransactionLineRel.IsBinTrackedItemWithSingleBin)
					"CannotDelete;UDIDetailsForSingleBinExist"
					
			Exit Rules
				
				if (InventoryUDIDetail.TransactionSystemCode.Requisition)
					if  (InventoryUDIDetail.WarehouseShipment entered
					and (IsDetailRequiredAtIssue or IsDetailRequiredAtReceipt))
						if (InventoryUDIDetail.InventoryTransactionType.IntransitReceiving)
							constraint (DerivedUDICount >= 1)
								"CannotDeleteAllDetails"
							invoke SystemDelete InventoryTransactionLineDetailFromWHRel
						else	
							invoke SystemDelete WarehouseShipmentLineDetailRel
				else
					if  ((IsDetailRequiredAtReceipt
					and  (InventoryTransactionRel.InventoryDocumentType.InventoryReceipt
					or    InventoryTransactionRel.InventoryDocumentType.InventoryIssue
					or    InventoryTransactionRel.InventoryDocumentType.Adjustment
					or    InventoryTransactionRel.InventoryDocumentType.InventoryTransfer
					or	  InventoryTransactionRel.InventoryDocumentType.IntransitReceiving
					or    InventoryTransactionRel.InventoryDocumentType.BinTransfer))
					or   (IsDetailRequiredAtIssue
					and   InventoryTransactionRel.InventoryDocumentType.InventoryIssue))
						include DeleteInventoryTransactionLineDetail
		
		QuickDelete is a Delete Action
			restricted
			bypass relational integrity rules
						
		ReceivingUpdate is an Instance Action
			restricted
			Action Rules
				if (InventoryTransactionLineDetailMappedRel exists)
					if (InventoryTransactionLineDetailMappedRel.OpenQuantity entered)
						Quantity = InventoryTransactionLineDetailMappedRel.OpenQuantity
				else
					if (InventoryTransactionLineRel.OpenQuantity entered)
						Quantity = InventoryTransactionLineRel.OpenQuantity
					else
						if (InventoryTransactionLineDetailFromWHRel exists)
							if (first InventoryTransactionLineDetailFromWHRel.OpenQuantity entered)
								Quantity = first InventoryTransactionLineDetailFromWHRel.OpenQuantity
						else
							if (first InventoryTransactionLineFromWHRel.OpenQuantity entered)
								Quantity = first InventoryTransactionLineFromWHRel.OpenQuantity
								
		SplitBinDetail is an Instance Action
			default label is "SplitDetail"
			valid when (IsValidForSplitDetail)
			Parameters
				PrmBin 	    is a Bin
					default label is "Bin"
				PrmQuantity is like Quantity
					default label is "Quantity"
					
			Parameter Rules
				PrmQuantity
					constraint (PrmQuantity <= Quantity)
						"NewDetailQuantityShouldBeLessThanOrEqualToQuantity"
					
					constraint (PrmQuantity > 0)
						"NewDetailQuantityShouldBeGreaterThan0"	
					
			Action Rules
				invoke Create InventoryUDIDetail
					fill in fields from this instance
					initialize invoked.InventoryUDIDetail.TransactionSequence
					invoked.Bin 	 = PrmBin
					invoked.Quantity = PrmQuantity
					
				Quantity -= PrmQuantity
				invoke SystemUpdate InventoryTransactionLineDetailFromWHRel
					invoked.Quantity = Quantity
				
