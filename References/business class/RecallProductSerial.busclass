RecallProductSerial is a BusinessClass
    owned by recall
    prefix is RMRPS

    Ontology
    	symbolic key is RecallProductSerial
    
    Patterns
    	implements ContextualParent
    		
    Persistent Fields
   		ThruSerial        			is AlphaUpper size 16
   		RecallProductSerialStripped is AlphaUpper size 16
   	
	Derived Fields
		
		ThruMessage is a MessageField
			restricted
			"Thru"
		
		DisplaySerial is a DerivedField
			type is Alpha size 40
			if (ThruSerial != blank)
				return RecallProductSerial + "_" + ThruMessage + "_" + ThruSerial
			else
				return RecallProductSerial

		NumberOfCharacters is a DerivedField
			type is Numeric size 3
			restricted
			return RecallProductSerial size 
			
		DerivedStrippedSerialNumber is a DerivedField
			type is AlphaUpper size 16
			return com.lawson.apps.procurement.base.StringUtils.stripNonAlphaNumeric(RecallProductSerial)
   	
	Sets
		
		ByStrippedSerialNumber
			Sort Order
				RecallGroup
			 	RecallNotice
			 	RecallProduct
			 	RecallProductSerialStripped
				RecallProductSerial
	
	Field Rules
	
		RecallProductSerialStripped
			default to DerivedStrippedSerialNumber
					
	Conditions
		HasRange
			restricted
			when (ThruSerial != blank)

		SingleSerial
			restricted
			when (RecallProductSerial entered
			and   ThruSerial not entered)
						
   	Actions
    	Create is a Create Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			Action Rules
				invoke FastUpdate RecallNotice
					invoked.PrmLastUpdateToProducts = current timestamp
						
		EnterSerialNumberRange is a Create Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			Parameters
				RecallGroup
				RecallNotice
				RecallProduct
				Prefix				 is AlphaUpper size up to 5
				SerialNumberLotRange
				Suffix				 is AlphaUpper size up to 5
				
			Parameter Rules
				RecallGroup 
					required
				RecallNotice	
					required
				RecallProduct	
					required
				SerialNumberLotRange
					required
					
			Local Fields
   				LocalSerialNumber 	is Numeric size 16
   				LocalSerialCheck	is AlphaUpper size 26
   				 
   			Action Rules
   				if (Prefix entered
   				or	Suffix entered)
   					LocalSerialCheck = Prefix + SerialNumberLotRange.EndingNumber + Suffix
   					constraint (!LocalSerialCheck[17] entered)
   						"SerialNumberWithPrefixAndSuffixMustBeLessThan17Characters"
   				
   				invoke Create this instance
					invoked.RecallGroup		 	= RecallGroup
					invoked.RecallNotice 		= RecallNotice
					invoked.RecallProduct 		= RecallProduct
					invoked.RecallProductSerial = Prefix + SerialNumberLotRange.BeginningNumber + Suffix
					invoked.ThruSerial 			= Prefix + SerialNumberLotRange.EndingNumber + Suffix
				
								
		Update is an Update Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			restricted
			
		Delete is a Delete Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			Action Rules
				invoke FastUpdate RecallNotice
					invoked.PrmLastUpdateToProducts = current timestamp
						
		Purge is a Purge Action
			restricted
