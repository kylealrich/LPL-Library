DebtInstrumentIdentifierPrincipal is a BusinessClass
	owned by treasury
	prefix is DMIP
	sql name is DebtIdPrincipal
	
	Ontology
		symbolic key is DebtInstrumentIdentifierPrincipal

	Patterns
		implements DynamicCreation
		
	Persistent Fields								
		PrincipalAmount		  					is an InternationalCost
			default label is "PrincipalPayment"
			precision is 2
	
	Local Fields
		LocalEnterpriseGroup					is an EnterpriseGroup
		LocalDebtCalendar						is a SystemCalendar
		LocalBusinessDayConvention 				is a BusinessDayConvention
		LocalPaymentDate						is Date
		LocalDateRange							is a DateRange
		DebtInstrumentIdentifierPrincipalGroup
							
	Derived Fields
		CummulativePrincipal is a DerivedField
			type is like InternationalCost
				precision is 2
			restricted
			return (sum PreviousPrincipalPaymentsRel.PrincipalAmount)
			
		RemainingPrincipal is a DerivedField
			type is like InternationalCost
				precision is 2
			return (DebtInstrumentIdentifier.OriginalPrincipalAmount - CummulativePrincipal)
	
		PreviousSystemCalendarDate is a DerivedField
			type is Date
			restricted
			if (!LocalPaymentDateBusinessDayRel.IsWorkingDay
			or  LocalPaymentDateBusinessDayRel.IsHoliday)
				return (last LocalPreviousSystemCalendarDateRel.SystemCalendarDate)
			
		FollowingSystemCalendarDate is a DerivedField
			type is Date
			restricted
			if (!LocalPaymentDateBusinessDayRel.IsWorkingDay
			or  LocalPaymentDateBusinessDayRel.IsHoliday)
				return (first LocalFollowingSystemCalendarDateRel.SystemCalendarDate)
				
		AdjustedPaymentDate is a DerivedField
			type is Date
			restricted
			if (LocalBusinessDayConvention.NoAdjustment
			or (LocalPaymentDateBusinessDayRel.IsWorkingDay
			and !LocalPaymentDateBusinessDayRel.IsHoliday))
				return LocalPaymentDate
			else
			if (LocalBusinessDayConvention.Previous)
				return PreviousSystemCalendarDate
			else
			if (LocalBusinessDayConvention.Following)
				return FollowingSystemCalendarDate
			else
			if (LocalBusinessDayConvention.ModifiedPrevious)
				if (PreviousSystemCalendarDate month = LocalPaymentDate month)
					return PreviousSystemCalendarDate
				else
					return FollowingSystemCalendarDate
			else
			if (LocalBusinessDayConvention.ModifiedFollowing)
				if (FollowingSystemCalendarDate month = LocalPaymentDate month)
					return FollowingSystemCalendarDate
				else
					return PreviousSystemCalendarDate
					
	Field Rules
		DebtInstrumentIdentifierPrincipal
			constraint (DebtInstrumentIdentifierPrincipal >= DebtInstrumentIdentifier.FirstPaymentDate
			and			DebtInstrumentIdentifierPrincipal <= DebtInstrumentIdentifier.MaturityDate)
				"PaymentDateMustBeBetween<DebtInstrumentIdentifier.FirstPaymentDate>And<DebtInstrumentIdentifier.MaturityDate>"
			
		PrincipalAmount
			required
	
	Conditions
		IsWorkingDay
			restricted
			when (DebtInstrumentIdentifierPrincipalSystemCalendarDateRel.IsWorkingDay)
			
		IsHoliday
			restricted
			when (DebtInstrumentIdentifierPrincipalSystemCalendarDateRel.IsHoliday)
		
		RaiseYellowAlert
			restricted
			when (DebtInstrumentIdentifierPrincipal exists
			and  (!IsWorkingDay
			or	  IsHoliday))
			
	Relations
		DebtInstrumentIdentifierPrincipalSystemCalendarDateRel
			one-to-one relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= CashManagementGroup.EnterpriseGroup
				related.SystemCalendar 		= DebtInstrument.DebtCalendar
				related.SystemCalendarDate	= DebtInstrumentIdentifierPrincipal
		
		PreviousPrincipalPaymentsRel
			one-to-many relation to DebtInstrumentIdentifierPrincipal
			Field Mapping uses symbolic key
				related.CashManagementGroup			= CashManagementGroup
				related.DebtInstrument				= DebtInstrument
				related.DebtInstrumentIdentifier	= DebtInstrumentIdentifier
			Instance Selection
				where (related.DebtInstrumentIdentifierPrincipal <= DebtInstrumentIdentifierPrincipal)
	
		LocalPaymentDateBusinessDayRel
			one-to-one relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= LocalEnterpriseGroup
				related.SystemCalendar 		= LocalDebtCalendar
				related.SystemCalendarDate	= LocalPaymentDate
		
		LocalPreviousSystemCalendarDateRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= LocalEnterpriseGroup
				related.SystemCalendar 		= LocalDebtCalendar
			Instance Selection
				where (related.SystemCalendarDate < LocalPaymentDate
				and	   related.IsWorkingDay
				and    !related.IsHoliday)
		
		LocalFollowingSystemCalendarDateRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= LocalEnterpriseGroup
				related.SystemCalendar 		= LocalDebtCalendar
			Instance Selection
				where (related.SystemCalendarDate > LocalPaymentDate
				and	   related.IsWorkingDay
				and    !related.IsHoliday)
		
		LocalDebtInstrumentIdentifierPrincipalRel
			one-to-one relation to DebtInstrumentIdentifierPrincipal
			Field Mapping uses symbolic key
				related.CashManagementGroup				  = DebtInstrumentIdentifierPrincipalGroup.CashManagementGroup
				related.DebtInstrument					  = DebtInstrumentIdentifierPrincipalGroup.DebtInstrument
				related.DebtInstrumentIdentifier		  = DebtInstrumentIdentifierPrincipalGroup.DebtInstrumentIdentifier
				related.DebtInstrumentIdentifierPrincipal = DebtInstrumentIdentifierPrincipalGroup.DebtInstrumentIdentifierPrincipal
		
		LocalCalendarFirstPaymentDateRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= LocalEnterpriseGroup
				related.SystemCalendar 		= LocalDebtCalendar
			Instance Selection
				where (related.SystemCalendarDate <= LocalDateRange.Begin)
				
		LocalCalendarLastPaymentDateRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= LocalEnterpriseGroup
				related.SystemCalendar 		= LocalDebtCalendar
			Instance Selection
				where (related.SystemCalendarDate >= LocalDateRange.End)
						
	Rule Blocks
		ValidationRules
			constraint (PrincipalAmount >= 0)
				"CannotEnterNegativePrincipalPayments"
			
			constraint (DebtInstrumentIdentifier.EnteredPrincipalLessThanOriginalPrincipal)
				"TotalPrincipalPaymentsCannotExceedTheOriginalPrincipalAmount"
															
  	Actions
		Create is a Create Action
			Exit Rules
				include ValidationRules
		
		CreatePrincipalSchedule is a Set Action
			run in foreground
			Parameters
				CashManagementGroup
				DebtInstrument
				DebtInstrumentIdentifier
				PrincipalFrequency			is Numeric 2
					States
						Daily					value is 1
						Monthly					value is 2
						Quarterly				value is 3
						SemiAnnually			value is 4
						Annually				value is 5
				EveryNumberOfDays			is Numeric 3
				BusinessDayConvention
				PaymentDateRange			is a DateRange
				Principal					is like InternationalCost
					precision is 2
				
			Parameter Rules
				PrincipalFrequency
					initial value is PrincipalFrequency.Monthly
					
					required
				
				EveryNumberOfDays
					initial value is 1
					
					if (PrincipalFrequency.Daily)
						required
							
				BusinessDayConvention
					initial value is BusinessDayConvention.NoAdjustment
					
					required
					
				PaymentDateRange				
					required
						"BeginningPaymentDateIsRequired"
						
					constraint (PaymentDateRange.Begin entered)
						"BeginningPaymentDateIsRequired"
					
					constraint (PaymentDateRange.End entered)
						"EndingPaymentDateIsRequired"
						
					constraint (PaymentDateRange.Begin >= DebtInstrumentIdentifier.FirstPaymentDate
					and			PaymentDateRange.End <= DebtInstrumentIdentifier.MaturityDate)
						"PaymentDateMustBeBetween<DebtInstrumentIdentifier.FirstPaymentDate>And<DebtInstrumentIdentifier.MaturityDate>"
						
				Principal
					required
			
			Local Fields
				NewPaymentDate				is Date
				
			Instance Selection
				where (false)
							
			Action Rules
				Empty Set Rules
					LocalEnterpriseGroup = CashManagementGroup.EnterpriseGroup
					LocalDebtCalendar = DebtInstrument.DebtCalendar
					LocalBusinessDayConvention = BusinessDayConvention
					LocalDateRange = PaymentDateRange
					LocalPaymentDate = PaymentDateRange.Begin
					DebtInstrumentIdentifierPrincipalGroup.CashManagementGroup = CashManagementGroup
					DebtInstrumentIdentifierPrincipalGroup.DebtInstrument = DebtInstrument
					DebtInstrumentIdentifierPrincipalGroup.DebtInstrumentIdentifier = DebtInstrumentIdentifier
					
					if (!BusinessDayConvention.NoAdjustment)
						constraint (LocalCalendarFirstPaymentDateRel exists)
							"DatesMustBeDefinedOnOrBeforeTheFirstPaymentDateOf<LocalDateRange.Begin>ForCalendar<DebtInstrument.DebtCalendar>"
							
						constraint (LocalCalendarLastPaymentDateRel exists)
							"DatesMustBeDefinedThruTheLastPaymentDateOf<LocalDateRange.End>ForCalendar<DebtInstrument.DebtCalendar>"
							
					while (LocalPaymentDate <= PaymentDateRange.End)
						DebtInstrumentIdentifierPrincipalGroup.DebtInstrumentIdentifierPrincipal = AdjustedPaymentDate
						invoke Update LocalDebtInstrumentIdentifierPrincipalRel
							invoked.PrincipalAmount += Principal
						
						if (PrincipalFrequency.Daily)
							LocalPaymentDate = LocalPaymentDate + EveryNumberOfDays as days
						else
						if (PrincipalFrequency.Monthly)
							LocalPaymentDate = LocalPaymentDate + 1 month
						else
						if (PrincipalFrequency.Quarterly)
							LocalPaymentDate = LocalPaymentDate + 3 months
						else
						if (PrincipalFrequency.SemiAnnually)
							LocalPaymentDate = LocalPaymentDate + 6 months
						else
						if (PrincipalFrequency.Annually)
							LocalPaymentDate = LocalPaymentDate + 1 year
					
		Update is an Update Action
			Exit Rules
				include ValidationRules
					
		Delete is a Delete Action

		PurgePrincipalPayments is an Instance Action
			Parameters
				PaymentDateRange is a DateRange
				
			Parameter Rules
				PaymentDateRange	
                	required
                		"BeginningPaymentDateIsRequired"
                		
                	constraint (PaymentDateRange.Begin entered)
						"BeginningPaymentDateIsRequired"
					
					constraint (PaymentDateRange.End entered)
						"EndingPaymentDateIsRequired"
			
			Action Rules
				invoke PrincipalPaymentsPurge
					invoked.PrmCashManagementGroup		= CashManagementGroup
					invoked.PrmDebtInstrument			= DebtInstrument
					invoked.PrmDebtInstrumentIdentifier	= DebtInstrumentIdentifier
					invoked.PrmPaymentDateRange			= PaymentDateRange
						
		PrincipalPaymentsPurge is a Set Action
			restricted
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
				PrmDebtInstrument			is a DebtInstrument
				PrmDebtInstrumentIdentifier	is a DebtInstrumentIdentifier
				PrmPaymentDateRange		 	is a DateRange
				
			Parameter Rules
				PrmCashManagementGroup		required
				PrmDebtInstrument			required
				PrmDebtInstrumentIdentifier	required
				PrmPaymentDateRange			required
            
			Instance Selection
				where (CashManagementGroup = PrmCashManagementGroup
				and	   DebtInstrument = PrmDebtInstrument
				and	   DebtInstrumentIdentifier = PrmDebtInstrumentIdentifier
				and	   DebtInstrumentIdentifierPrincipal within PrmPaymentDateRange)
				
			Action Rules
				Instance Rules
					invoke Purge
		
		Purge is a Purge Action
			restricted
