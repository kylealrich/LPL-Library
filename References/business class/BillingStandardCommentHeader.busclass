BillingStandardCommentHeader is a BusinessClass
    owned by bl
    prefix is OCH
    classic name is OECOMMHDR

    Ontology
        symbolic key is BillingStandardCommentHeader
            classic set name is OCHSET1
            classic name is STD-COMM

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        LastSequence is a Sequence3
            classic name is LAST-SEQ
            
    Local Fields
    	LocalLanguageCode	is an IsoLocale
            
    Context Fields
    	BillingInvoice
    	BillingInvoiceLine

    Relations

		BillingStandardCommentRel
            one-to-many relation to BillingStandardComment
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingStandardCommentHeader = BillingStandardCommentHeader
            	
        AvailableStandardCommentForCopyRel
            one-to-many relation to BillingStandardComment
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingStandardCommentHeader = BillingStandardCommentHeader
            Instance Selection
            	where (related.BillingStandardComment.LanguageCode 		 = LocalLanguageCode
            	and	   related.Active)

    Field Rules

        Description
            required
            
    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action

		CopyComments is an Instance Action
			valid when (BillingInvoice entered)
        	Parameters
        		PrmLanguageCode			is an IsoLocale
        		
        	Action Rules
        		LocalLanguageCode	= PrmLanguageCode
        		
        		constraint (AvailableStandardCommentForCopyRel exists)
        			"BillingStandardCommentDoesNotExists"
        	
        		for each AvailableStandardCommentForCopyRel
        			invoke Create BillingInvoiceComments
        				invoked.Company				= BillingInvoice.Company
        				invoked.BillingInvoice		= BillingInvoice
        				invoked.BillingInvoiceLine	= BillingInvoiceLine
        				invoked.PrintCode			= each.PrintCode
        				invoked.CommentText			= each.CommentText
        
        

