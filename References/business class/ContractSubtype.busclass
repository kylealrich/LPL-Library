ContractSubtype is a BusinessClass
    owned by po
    prefix is CSTP

    Ontology
    	symbolic key is ContractSubtype
    
    Patterns
    	implements StaticJava 

	Persistent Fields
		Description		  			is a Description4
		PurchaseType
    	CanUseForCostDefaultingOptions	is Numeric size 1
    		default label is "UseForCostDefaultingOptions"     
    		States
    			UseAllItems             	value is 1
    			UseAllNonSpecialItems   	value is 2
    			DoNotUseForCostDefaulting 	value is 3
		Active				 		is Boolean
		SpecialItemOptions			is Numeric size 1
			States
				KeepAsSpecialItem       value is 1
				CreateItemOnActivation  value is 2
				CreateItemWithProcess   value is 3
		ItemCreationMethod    is Numeric size 1    
            States
                Sequential  value is 1
                Assign      value is 2
                ManualInput value is 3
        AssignItemNumberUsing is Numeric size 1
        	States
        		VendorItem  		value is 1
        		ManufacturerNumber 	value is 2
        		ItemGTIN            value is 3
        		ItemUPC             value is 4
        		ItemSKU             value is 5
        		HIBCCItemUPN        value is 6
        		UCCEANItemUPN       value is 7
        		NationalDrugCode    value is 8
        AllowNoMaxServiceItems is Boolean
			default label is "AllowQuantityServicesOnServiceContractsToBeCreatedWithNoMaximumQuantity"
		
	Conditions
		NotCatalogQuotePurchaseType
			restricted
			when (PurchaseType.Contract
			or	  PurchaseType.Blanket
			or    PurchaseType.Service					
			or    PurchaseType.Standing)
		HasAPurchaseType
			restricted
			when (PurchaseType.Contract
			or	  PurchaseType.Blanket
			or    PurchaseType.Service					
			or    PurchaseType.Standing
			or    PurchaseType.CatalogQuote)
			
		NonServicePurchaseType
			restricted
			when (PurchaseType.Contract
			or	  PurchaseType.Blanket
			or    PurchaseType.Standing
			or    PurchaseType.CatalogQuote)
		
		BlanketOrStandingOrServicesType
			restricted
			when (PurchaseType.Blanket
			or    PurchaseType.Service					
			or    PurchaseType.Standing)
		
		ContractOrCatalogType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.CatalogQuote) 

		BlanketOrStandingType
			restricted
			when (PurchaseType.Blanket
			or    PurchaseType.Standing)
		
		StandingOrServiceType
			restricted
			when (PurchaseType.Standing
			or    PurchaseType.Service)
		
		ServiceType
			restricted
			when (PurchaseType.Service)
		
		StandingType
			restricted
			when (PurchaseType.Standing)
			
		ContractPurchaseType
			restricted
			when (PurchaseType.Contract)
		
		BlanketType
			restricted
			when (PurchaseType.Blanket)
		
		CatalogQuoteType
			restricted
			when (PurchaseType.CatalogQuote)
		
		ContractOrBlanketType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket)
		
		ContractOrBlanketOrServiceType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket
			or    PurchaseType.Service)
		
		ContractOrBlanketOrStandingType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket
			or    PurchaseType.Standing)
			
		NoPurchaseType
			restricted
			when (!PurchaseType.Contract
			and	  !PurchaseType.Blanket
			and   !PurchaseType.Service					
			and   !PurchaseType.Standing
			and   !PurchaseType.CatalogQuote)
		
		ContractOrBlanketOrCatalogType
			restricted
			when (PurchaseType.Contract
			or    PurchaseType.Blanket
			or    PurchaseType.CatalogQuote)
			
		HasASpecialItemOption
			restricted
			when (SpecialItemOptions.KeepAsSpecialItem
			or    SpecialItemOptions.CreateItemOnActivation
			or    SpecialItemOptions.CreateItemWithProcess)
			
	Relations
		SubtypeToContractsRel 
			one-to-many relation to Contract
			Field Mapping uses ByContractType
				related.ContractType          = ContractType
				related.ContractSubtype       = ContractSubtype
		
		NonClosedContractsForThisSubtypeRel
			one-to-many relation to Contract
			Field Mapping uses ByContractType
				related.ContractType          = ContractType
				related.ContractSubtype       = ContractSubtype
			Instance Selection
				where (!related.Contract.ContractStatus.Closed)
				
	Field Rules
		PurchaseType
					
		Description
			required
		
		CanUseForCostDefaultingOptions
					
			if (!NonServicePurchaseType)
				force default to 3

		Active
			default to true

		SpecialItemOptions
			if  (!NonServicePurchaseType)
				initialize SpecialItemOptions
			
			if (CanUseForCostDefaultingOptions.UseAllNonSpecialItems
			or  CanUseForCostDefaultingOptions.DoNotUseForCostDefaulting)
				force default to SpecialItemOptions.KeepAsSpecialItem 

		ItemCreationMethod
			if (SpecialItemOptions.CreateItemOnActivation)
				constraint (ItemCreationMethod > 0)
					"MustSelectAnItemCreationMethodIfSpecialItemOptionsIsCreateItemOnActivation"
			else
			if (!SpecialItemOptions.CreateItemOnActivation)
				force default to 0
				
		AssignItemNumberUsing
			if (ItemCreationMethod.Assign)
				constraint (AssignItemNumberUsing > 0)
					"MustSelectAnAssignItemNumberUsingValueIfItemCreationMethodIsAssign"
			else
			if (!ItemCreationMethod.Assign)
				force default to 0
				
	Attach Rules
		constraint (Active)
			"ContractSubtypeIsNotActive"
					
	Actions
		Create is a Create Action
				
			Field Rules
			
				PurchaseType
				
					default to ContractType.PurchaseType
					
				ItemCreationMethod
				
					default to ContractType.ItemCreationMethod
			
			Action Rules
				
				if (NonServicePurchaseType
				and PurchaseType = ContractType.PurchaseType)
					CanUseForCostDefaultingOptions = ContractType.CanUseForCostDefaultingOptions
					SpecialItemOptions             = ContractType.SpecialItemOptions

				if (Active)
					constraint (ContractType.Active)
						"AnActiveContractSubtypeRequiresAnActiveContractType"
				
		Update is an Update Action
			Action Rules
				if (Active)
					constraint (ContractType.Active)
						"AnActiveContractSubtypeRequiresAnActiveContractType"
				
				if (PurchaseType changed) 
					constraint (!NonClosedContractsForThisSubtypeRel exists)
						"CannotChangePurchaseType;ContractsExistForThisContractSubtypeThatAreNotClosed"
					if (!PurchaseType.Service)
						AllowNoMaxServiceItems = false
						
				if (AllowNoMaxServiceItems changed)
					if (AllowNoMaxServiceItems = false)
						constraint (!NonClosedContractsForThisSubtypeRel exists)
							"CannotSetAllowQuantityServicesOnServiceContractsToBeCreatedWithNoMaximumQuantityToFalse;ContractsExistForThisContractSubtypeThatAreNotClosed"
				
				if (NonServicePurchaseType)
					constraint (CanUseForCostDefaultingOptions > 0)
						"MustEnterCanUseForCostDefaultingOptionsValue"

					constraint (SpecialItemOptions > 0)
						"MustEnterSpecialItemOptions"

		Delete is a Delete Action
			Action Rules
				constraint (!SubtypeToContractsRel exists)
					"CannotDelete;ContractsExistForThisContractSubtype"
								
