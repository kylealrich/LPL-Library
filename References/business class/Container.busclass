Container is a BusinessClass
    owned by wh
    prefix is CNT 

    Ontology
        symbolic key is Container

    Patterns 
        disable AuditIndex
    
    Persistent Fields
        Description
        ContainerLength            is a LengthDimension
        ContainerHeight            is a HeightDimension
        ContainerWidth             is a WidthDimension
        ContainerWeightCapacity    is a DimensionSize
        ContainerVolumeCapacity    is a DimensionSize

    Conditions
        RecordExists
            when (Container exists)

        ValidVolumeCapacity
            when (ContainerVolumeCapacity <= ComputedVolumeCapacity)

        IsContainerUsed
            when (ShipmentLoadConsolidationRel exists)

        RequireContainerLength
            restricted
            when (ContainerWidth entered
            or    ContainerHeight entered)

        RequireContainerWidth
            restricted
            when (ContainerLength entered
            or    ContainerHeight entered)

        RequireContainerHeight
            restricted
            when (ContainerWidth entered
            or    ContainerLength entered)

        DisplayComputedVolumeCapacity
            restricted
            when(ContainerLength entered 
            and  ContainerHeight entered 
            and  ContainerWidth  entered
            or   ContainerVolumeCapacity not entered )

        
    Relations
        ShipmentLoadConsolidationRel 
            one-to-many relation to ShipmentLoadConsolidation
            Field Mapping uses ByContainer
                related.Company   = Company
                related.Container = Container 
    
    Derived Fields
        ComputedVolumeCapacity is a ComputeField
            type is like DimensionSize
            (ContainerHeight * ContainerLength * ContainerWidth)    

        DerivedContainerDisplay is a MessageField
			default label is "Container"
			"<Container>_-_<Container.Description>"

	Field Rules
        Container
            required 

        Description
            required

        ContainerLength
            if (RequireContainerLength)
                required   

        ContainerHeight
            if (RequireContainerHeight)
                required

        ContainerWidth
            if (RequireContainerWidth)
                required  

        ContainerWeightCapacity
            required
            
        ContainerVolumeCapacity
            required
            
            if (ContainerLength entered 
            or  ContainerHeight entered 
            or  ContainerWidth entered)
                force default to ComputedVolumeCapacity    

	Actions		
        Create is a Create Action

        Update is an Update Action
            valid when (not IsContainerUsed)
            
        Delete is a Delete Action
            valid when (not IsContainerUsed)
    
				

