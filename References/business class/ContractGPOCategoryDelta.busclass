ContractGPOCategoryDelta is a BusinessClass 
	owned by po
	
	prefix is CMXR
	
	Ontology
		symbolic key is ContractGPOCategoryDelta
		
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated		
	
	Persistent Fields
		ContractNumber                      is a GPOContractNumber 		
		CycleNumber     					is Numeric 10     			
		CycleAmendmentNumber      			is Numeric 10               
		CategoryName                        is Alpha size 255                 
		SubcategoryName                     is Alpha size 255 
		SubcategoryAwardStatus              is Alpha size 32  
		RecordModifiedTimestamp             is Alpha 20       
		OperationFlag                       is Alpha 10         		

	Local Fields 
		LocalGPOContractNumber      	is like GPOContractNumber
		LocalCategoryName               is Alpha size 255	
		LocalSubcategoryName            is Alpha size 255	
		LocalCycleNumber                is Numeric 10
		LocalAmendmentNumber            is Numeric 10
	
	Derived Fields
	
	Relations

		ContractGPOCategoryRel 
			one-to-many relation to ContractGPOCategory 
			Field Mapping uses ByGPOCategory2021
				related.CategoryName		= LocalCategoryName 
				related.SubcategoryName     = LocalSubcategoryName
			Instance Selection 
				where (related.GPOContractNumber 		= LocalGPOContractNumber
				and    related.ContractCycleNumber		= LocalCycleNumber
				and    related.ContractAmendmentNumber	= LocalAmendmentNumber)
	
	Sets
		ByGPOContractNumber	
			Sort Order 
				ContractNumber 
				CategoryName 
				SubcategoryName
				ContractGPOCategoryDelta

	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted
			
		MassCategoryDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Purge

		Purge is a Purge Action 	

		LoadCategoryRecords is a Set Action 
			restricted 
			
			Sort Order
				ContractNumber
				ContractGPOCategoryDelta
			
			Action Rules 

				Instance Rules 

					LocalGPOContractNumber 			= ContractNumber
					LocalCategoryName               = CategoryName 
					LocalSubcategoryName            = SubcategoryName
					LocalCycleNumber                = CycleNumber 
					LocalAmendmentNumber            = CycleAmendmentNumber
					
					if (OperationFlag = "Add")
						if (ContractGPOCategoryRel !exists)
							invoke Create ContractGPOCategory 
								invoked.GPOContractNumber 				= ContractNumber
								invoked.ContractCycleNumber             = CycleNumber
								invoked.ContractAmendmentNumber	        = CycleAmendmentNumber
								invoked.CategoryName                    = CategoryName                      
								invoked.SubcategoryName                 = SubcategoryName     
								invoked.SubcategoryAwardStatus          = SubcategoryAwardStatus      
								invoked.RecordModifiedTimestamp         = RecordModifiedTimestamp           
							
					else 
					if (OperationFlag = "Update")  
						if (ContractGPOCategoryRel exists)
					 		invoke Update ContractGPOCategoryRel 
								invoked.SubcategoryAwardStatus          = SubcategoryAwardStatus


					else 
					if (OperationFlag = "DEL")
						if (ContractGPOCategoryRel exists)
							invoke Delete ContractGPOCategoryRel 


