PayablesInvoiceAudit is a BusinessClass
	owned by ap
	prefix is PIAUD

	Ontology
		symbolic key is PayablesInvoiceAudit

	Patterns
		disable Auditing
		implements CreateStamp		
		implements UpdateStamp		
		implements Archivable

	Persistent Fields
		Resource				is an Employee
		Team					is a FinanceTeam
		Role					is a InvoiceApprovalAuthCode
		Status					is Numeric 2
			default label is "Action"
			States
				Assigned			value is 1
				Reassigned			value is 2
				Rejected			value is 3
				Approved			value is 4
				Disapproved			value is 5
				AutoApproved		value is 6
				PendingApprover		value is 7
				Restored			value is 8
				MatchReleased		value is 9		
				SentForPayment		value is 10
				Submitted			value is 11
				Cancelled			value is 12
				ManuallyApproved	value is 13
				ManuallyRejected	value is 14
				Released			value is 15
				Matched				value is 16
				Unmatched			value is 17
				Amended				value is 18
				Rerouted			value is 19
				Requested			value is 20
				Accepted			value is 21
		UpdateDate				is TimeStamp
			default label is "Date"
		Comment					is Alpha size up to 500
		ActionReason			is AlphaUpper 20
		Escalated				is Boolean
		InvoiceRoutingCodeGroup							
		InvoiceRoutingCodeGroupLevel					
		InvoiceRoutingCode								
		RoutingApprovalLevel	is Numeric 8			
		Workunit				is like PfiWorkunit		//	IEFIN-103495	//	changed from "is a" to "is like" for IEFIN-116942
		InvoiceRoutingCategory							

	Derived Fields
		DerivedUpdateDate is a DerivedField
			type is Date
			restricted
			return (UpdateDate date)

		DerivedResourceTeamDisplay is a DerivedField
			type is Alpha 250
			default label is "Team/\Resource/\Role"		
			if (ResourceEntered)
				if (FinanceResourceExists)
					return FinanceResourceRel.PreferredSimplePresentationName
				else
					return Resource.PresentationNameSnapshot
			else
			if (Team entered)
				return Team.Description
			else
			if (RoleEntered)
				return Role.Description
			else
				return ""

		DerivedResourceTeamInitials is a DerivedField
			type is AlphaUpper 3
			restricted

			if (ResourceEntered)
				if (FinanceResourceExists)
					return FinanceResourceRel.PreferredSimplePresentationName
				else
					return Resource.PresentationNameSnapshot
			else
			if (Team entered)
				return Team.Description
			else
			if (RoleEntered)
				return Role.Description
			else
				return "SYS"	

		DerivedEscalatedMessage is a DerivedField
			type is MessageField
			if (Escalated)
				return EscalatedMessage
			else
				return blank

		EscalatedMessage is a MessageField
			"Escalated"

	Field Rules

	Conditions
		CurrentResource
			restricted
			when (actor.agent(Employee).HROrganization 	= Resource.HROrganization
			and	  actor.agent(Employee).Employee 		= Resource)
		ResourceEntered
			restricted
			when (Resource entered)
		RoleEntered
			restricted
			when (Role entered)
		EmailAddressEntered
			restricted
			when (Resource.EmployeeWorkEmailAddress entered)
		WorkPhoneEntered
			restricted
			when (Resource.EmployeeWorkTelephone entered)
		InvoiceRoutingCodeEntered
			restricted
			when (PayablesInvoice.RoutingCode entered)
		FinanceResourceExists
			restricted
			when (FinanceResourceRel exists)

	Sets
		ByResource
			Sort Order
				Resource
				UpdateDate		descending
				Company
				PayablesInvoice
				PayablesInvoiceAudit

	Relations
		PayablesInvoiceAuditRel
			one-to-many relation to PayablesInvoiceAudit
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PayablesInvoice		= PayablesInvoice

		FinanceResourceRel
			one-to-one relation to FinanceResource
			Field Mapping uses symbolic key
				related.HROrganization		= Resource.HROrganization
				related.FinanceResource		= Resource
	Actions
		Create is a Create Action
			restricted

		CreateNoRules is a Create Action
			default label is untranslatable
			restricted
			bypass field rules
			Entrance Rules
				PayablesInvoiceAudit = last PayablesInvoiceAuditRel.PayablesInvoiceAudit + 1

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted
			
		Purge is a Purge Action
			restricted
