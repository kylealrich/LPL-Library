FranchiseContractBudget is a BusinessClass
    owned by fr
    prefix is FTB
    classic name is FRCONTBUD

    Ontology
        symbolic key is FranchiseContractBudget
            classic set name is FTBSET1
            classic name for CustomerShipTo is SHIP-TO
            classic name for FranchiseContract is CONTR-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        StartDate
        Dates 					is a FranchiseBudgetDates
            classic name is DATE
        TransactionAmounts		
            classic name is TRAN-AMT

	Transient Fields
		CopyBudget 		is a FranchiseContractBudget
		
 	Local Fields
		LocalCounter 	is Numeric size 2
			
	Conditions
		NextYearBudgetExists
			restricted
			when (NextYearBudgetRel exists)
				
		RecordExists
			restricted
			when (FranchiseContractBudget entered)
					
	Relations
		NextYearBudgetRel
			one-to-one relation to FranchiseContractBudget
			Field Mapping uses symbolic key
				related.Company					= Company
				related.Customer				= Customer
            	related.CustomerShipTo			= CustomerShipTo
            	related.FranchiseContract	 	= FranchiseContract
				related.FranchiseContractBudget	= (FranchiseContractBudget + 1)
	
    Field Rules
        StartDate
            required

	Rule Blocks
		EditData
			if (CopyBudget entered)
	   			constraint (CopyBudget.FranchiseContract.Currency = FranchiseContract.Currency)
	   				"CurrencyCodeOfBudgetDoesNotMatchCurrencyOfContract" //"FR10.339"
	    		LocalCounter = 1
	    		while (LocalCounter <= 53 and Dates.Date[LocalCounter] entered)
	    			constraint (CopyBudget.Dates.Date[LocalCounter] > StartDate)
	    				"CopyBudgetPeriodDateCannotBePriorToNewBudgetStartDate"
	   				if (NextYearBudgetExists)
		    			constraint (CopyBudget.Dates.Date[LocalCounter] < NextYearBudgetRel.StartDate)
		    				"NextYearBudgetExists;CopyBudgetPeriodDateMustBePriorToNextYearBudgetStartDate"	    				
    				Dates.Date[LocalCounter] = CopyBudget.Dates.Date[LocalCounter]
    				TransactionAmounts.TransactionAmount[LocalCounter] = CopyBudget.TransactionAmounts.TransactionAmount[LocalCounter]
					LocalCounter += 1
			else
	    		LocalCounter = 1
	    		while (LocalCounter <= 53)
	    			if (LocalCounter = 1) 
	    				constraint (Dates.Date[LocalCounter] entered)
	    					"AtLeastOnePeriodMustBeDefined"	  
						constraint (TransactionAmounts.TransactionAmount[1] entered)
							"AtLeastOneSetOf_\Date/\AmountRequired"//"AtLeastOneSetOfDate/AmountRequired"      
	    			if (Dates.Date[LocalCounter + 1] entered)


	    				constraint (Dates.Date[LocalCounter + 1] > Dates.Date[LocalCounter])
							"Period<LocalCounter>EndDateMustBePriorToSubsequentPeriods"
					LocalCounter += 1

					
    Actions
        Create is a Create Action
        	Action Rules
				include EditData

        Update is an Update Action
        	Action Rules
				include EditData

        Delete is a Delete Action
