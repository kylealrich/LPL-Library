DunningLetterTextCode is a BusinessClass
	owned by ar
	prefix is DNN
	classic name is ARDUNNING

	Ontology
		symbolic key is DunningLetterTextCode
			classic set name is DNNSET1

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		DunningTexts	is a DunningTextX6InArdunning
			classic name is DUNNING-TEXT
		UsedFlag		is Boolean
			classic name is USED-FL


	Transient Fields
		TransientEditNotes	is Boolean
		TransientNotes		is Text
			derive value from TransientNotesDerived


	Local Fields

		LocalNotes				is Text
		LocalNotesLineLimit		is Numeric size 2
		LocalNotesLineTextLimit	is Numeric size 2
		LocalAssignCode			is Numeric size 3
		LocalValidateNotes		is Boolean


	Derived Fields
		AssignNotes				is a NativeField
			type is Numeric size 3
			restricted

		TransientNotesDerived	is a DerivedField
			type is Text
			restricted
			initialize LocalAssignCode
			LocalValidateNotes = false
			LocalAssignCode = AssignNotes
			return LocalNotes

		ExcessLineMessageText is a MessageField
			restricted
			"ExceededMaximumLinesAllowed;LimitIs<LocalNotesLineLimit>"

		ExcessLineTextMessageText is a MessageField
			restricted
			"ExceededMaximumAllowedCharactersInALine;LimitIs<LocalNotesLineTextLimit>"

		AssignErrorMessage is a DerivedField
			type is MessageField
			restricted
			if (LocalAssignCode = 101)
				return ExcessLineMessageText
			if (LocalAssignCode = 102)
				return ExcessLineTextMessageText
			return blank


	Field Rules
		Company
			constraint (!Company.CompanySetUpIsIncomplete)
				"CompanyIsIncomplete"		

		TransientEditNotes
			initial value is true

		TransientNotes
			required
				"AtLeastOneLineOfTextRequired"
			if (TransientNotes entered)
				initialize LocalAssignCode
				LocalValidateNotes = true
				LocalAssignCode = AssignNotes
				constraint (LocalAssignCode = 100)
					"<AssignErrorMessage>"


	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	


	Relations
		DunningLetterTextCodeRel
			one-to-one relation to DunningLetterTextCode
			Field Mapping uses symbolic key
				related.Company 							= Company
				related.IsoLocale							= IsoLocale
				related.DunningLetterTextCode 				= DunningLetterTextCode

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company











	Actions
		Create is a Create Action



		T2VCreate is a Create Action				
			restricted 
			default label is untranslatable 
			bypass field rules 
			
		Update is an Update Action



		Delete is a Delete Action

