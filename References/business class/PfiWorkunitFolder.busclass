PfiWorkunitFolder is a BusinessClass
    owned by pfi
    prefix is PfiWF
    default label is "WorkUnitRelatedLink"
	disable data area copy

    Ontology
        symbolic key is PfiWorkunitFolder

    Patterns
        implements CRUD
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated
        implements IncrementalReplication
            indicator field is IsReplicated
                replicate when false
                    then set to true

    Persistent Fields

        DocType     is a PfiDocType
            default label is "RelatedLinkType"
        Value       is Text
            default label is "Target"
        Name        is a PfiDescription
        AttachDate  is TimeStamp
        AttachBy    is like Actor // LMRK-97977 Was "is Actor". Likely meant to be "is an Actor". Changed to "like" so we have the same dict type but without changing existing functionality.
            default label is "AttachedBy"
        IsReplicated is Boolean
            default label is untranslatable

    Derived Fields

        Url is a NativeField
            type is Text

        AttachByName is a DerivedField
            type is Alpha size 101
            default label is "AttachedByName"
            return PfiActorRel.FirstNameAndLastNameOrActor

    Sets

        ByWorkunitDocTypeFolder
            sql name is PfiWF01
            indexed
            Sort Order
                PfiWorkunit
                DocType
                PfiWorkunitFolder

        ByName
            sql name is PfiWF02
            indexed
            Sort Order
                Name
                PfiWorkunit
                PfiWorkunitFolder

        ByIsReplicatedUniqueID
            sql name is PfiWF03
            indexed
            Sort Order
                IsReplicated
                UniqueID

    Field Rules

        AttachDate
            default to current time

        AttachBy
            default to actor

    Relations

        PfiActorRel
            one-to-one relation to Actor
            Field Mapping uses symbolic key
                related.Actor = AttachBy

    Conditions

        IsDocTypeLandmarkOrURL
            default label is "DocumentTypeIsLandmarkOrURL"
            when (DocType.Landmark or DocType.URL)

        IsDocTypeURL
            default label is "DocumentTypeIsURL"
            when (DocType.Landmark or DocType.URL or DocType.S3)

        IsDocTypeM3
            default label is "DocumentTypeIsM\3"
            when (DocType.M3)

        IsValidURL
            default label is "URLIsValid"
            when (PfiWorkunit.NoActionRequest and IsDocTypeURL)

    Actions

        Create is a Create Action

        Update is an Update Action

            Exit Rules
                IsReplicated = false

        Delete is a Delete Action

        CopyFolders is a Set Action
            restricted
            run in foreground

            Parameters
                SourceWorkUnit      is a PfiWorkunit
                DestinationWorkUnit is a PfiWorkunit

            Instance Selection
                where (PfiWorkunit = SourceWorkUnit)

            Action Rules

                Instance Rules
                    invoke Create
                        invoked.PfiWorkunit = DestinationWorkUnit
                        invoked.DocType     = DocType
                        invoked.Value       = Value
                        invoked.Name        = Name
                        invoked.AttachDate  = AttachDate
                        invoked.AttachBy    = AttachBy
