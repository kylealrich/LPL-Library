PfiQueueTask is a BusinessClass
    owned by pfi
    prefix is PfiQT
    default label is "QueueTask"
    disable data area copy

    Ontology
        symbolic key is PfiQueueTask

    Patterns
        implements CRUD
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated
        implements IncrementalReplication
            indicator field is IsReplicated
                replicate when false
                    then set to true

    Persistent Fields

        Status                         is a PfiStatus
            default label is "ActionStatus"
        StartDate                      is TimeStamp
        DueDate                        is TimeStamp
        IsCurrentActionWaitingSnapshot is Boolean 
            default label is "ActionAwaiting"
        FilterKey                      is a PfiFilterKey
        FilterValue                    is a PfiFilterValue
        DocumentId                     is Numeric size 6


        IsProxy                        is Boolean
            default label is "Proxy"
        ProxiedUser                    is Alpha size 100
        ProxiedTaskName                is Alpha size 50
        ProxiedTaskType                is Numeric size 1
            States
                User  value is 1
                Group value is 2
        IsReplicated is Boolean
            default label is untranslatable

    Derived Fields

        ProcessName is a DerivedField
            type is Alpha size 100
            return PfiWorkunit.FlowDefinition

        Description is a DerivedField
            type is Alpha size 100
            if (PfiQueue.WorkDescription != blank)
                return PfiQueue.WorkDescription
            else
                return PfiWorkunit.WorkTitle

        ApproverState is a DerivedField
            type is Alpha size 30
            if (PfiQueue.DisplayType.ActionRequest)
                return PfiQueue.DisplayExec
            else
                return ""

        ActionRequest is a DerivedField
            type is UniqueID
            if (PfiQueue.DisplayType.ActionRequest and PfiActionRequestRel exists)
                return PfiActionRequestRel.VariableValue
            else
                return blank

        DisplayType is a DerivedField
            type is Numeric size 2 
            return PfiQueue.DisplayType

        PhoneDisplayType is a DerivedField
            type is Numeric size 2 
            return PfiQueue.PhoneDisplayType

        TabletDisplayType is a DerivedField
            type is Numeric size 2 
            return PfiQueue.TabletDisplayType

        DisplayName is a DerivedField
            type is Alpha size 30
            return PfiQueue.DisplayName

        PhoneDisplayName is a DerivedField
            type is Alpha size 30
            return PfiQueue.PhoneDisplayName

        TabletDisplayName is a DerivedField
            type is Alpha size 30
            return PfiQueue.TabletDisplayName

        IONDisplayName is a DerivedField
            type is Text
            return PfiQueue.IONDisplayName

        HideActions is a DerivedField
            type is Boolean
            return PfiQueue.HideActions

        DisplayExec is a DerivedField
            type is Text
            default label is "Content"
            return PfiQueue.DisplayExec

        PhoneDisplayExec is a DerivedField
            type is Text
            default label is "Content"
            if (PfiQueue.PhoneDisplayExecEntered)
                return PfiQueue.PhoneDisplayExec
            else
                return DisplayExec

        TabletDisplayExec is a DerivedField
            type is Text
            default label is "Content"
            if (PfiQueue.TabletDisplayExecEntered)
                return PfiQueue.TabletDisplayExec
            else
                return DisplayExec

        IONDisplayExec is a DerivedField
            type is Text
            default label is "Content"
            return PfiQueue.IONDisplayExec

        DisplayExecHtml is a DerivedField
            type is RichText
            default label is "Content"
            return PfiQueue.DisplayExec

        DisplayExecUrl is a NativeField
            type is Text
            default label is "DisplayInformationUrl"

        PhoneDisplayExecUrl is a NativeField
            type is Text
            default label is "PhoneDisplayInformationUrl"

        TabletDisplayExecUrl is a NativeField
            type is Text
            default label is "TabletDisplayInformationUrl"

        TaskStatus is a DerivedField
            type is Alpha size 10
            if (PfiQueueAssignment < PfiQueue.CurrentAssignment)
                return "Expired"
            else
            if (PfiQueueAssignment = PfiQueue.CurrentAssignment)
                return "Current"
            else
                return "Backup"

        IsCurrentActionWaiting is a DerivedField
            type is Boolean
            default label is "ActionAwaiting"
            if ((Status.ActionAwaiting and PfiQueue.DispStatus.ActionAwaiting and PfiWorkunit.Status.Processing)
            and (PfiQueueAssignment = PfiQueue.CurrentAssignment))
                return true
            else
                return false

        IsCurrentActionTakenPendingInServer is a DerivedField
            type is Boolean
            default label is "ActionTakenPendingInServer"
            if ((Status.ActionTaken and PfiQueue.DispStatus.ActionTakenPendingInServer and PfiWorkunit.Status.Processing)
            and (PfiQueueAssignment = PfiQueue.CurrentAssignment))
                return true
            else
                return false

        SecondsUntilDueDate is a DerivedField
            type is Decimal size 10.0
            if (DueDate != blank)
                return DueDate - current timestamp
            else
                return 0

        IsDueInLessThan24Hours is a DerivedField
            type is Boolean
            default label is "ActionIsDueInLessThan24Hours"
            if (DueDate != blank)
                if (SecondsUntilDueDate < 86400) 
                    return true
                else
                    return false
            else
                return false

        IsDueInLessThan48Hours is a DerivedField
            type is Boolean
            default label is "ActionIsDueInLessThan48Hours"
            if (DueDate != blank)
                if (SecondsUntilDueDate < 172800) 
                    return true
                else
                    return false
            else
                return false

        TaskDescription is a DerivedField
            type is Alpha size 100 
            default label is "TaskDescription"
            return PfiTask.TaskDescription

        ActivityName is a DerivedField
            type is Alpha size 100 
            default label is "ActivityName"
            return PfiActivity.ActivityName

        ConfigName is a DerivedField
            type is Alpha size 200
            return PfiQueue.ConfigName

        HasFilter is a DerivedField
            type is Boolean
            default label is "ActionHasFilter"
            if (FilterKey != blank)
                return true
            else
                return false

        HasDueDate is a DerivedField
            type is Boolean
            default label is "ActionHasDueDate"
            if (DueDate != blank)
                return true
            else
                return false

        ProLogicType is a DerivedField
            type is Numeric size 1
            return PfiQueue.ProLogicType

        ActionTakenOrPendingCount is a DerivedField
            type is Numeric 3
            return PfiQueue.ActionTakenOrPendingCount

        AreUserNotificationsEnabled is a DerivedField
            type is Boolean
            default label is "UserNotificationsAreEnabled"
            if (EnableUserNotificationsRel exists and EnableUserNotificationsRel.Value like "true")
                return true
            return false

        MingleInboxId is a DerivedField
            type is Numeric size 12
            default label is "InboxId"
            return PfiQueueAssignment.MingleInboxId
            
        NotificationCenterStatus is a DerivedField
            type is Numeric size 1
            return PfiQueueAssignment.NotificationCenterStatus        

        IsMingleInboxCompletionMessageSent is a DerivedField
            type is Boolean
            default label is "InboxCompletionMessageSent"
            return PfiQueueAssignment.IsMingleInboxCompletionMessageSent

        HasProcessIndicator is a DerivedField
            type is Boolean
            default label is "ProcessIndicator"
            if (PfiProcessIndicatorRel exists)
                return true
            return false

    Local Fields

        LocalUserProfile  is a PfiUserProfile
        LocalQueueTaskRef is BusinessObjectReference to PfiQueueTask

    Conditions

        DisplayTypeIsContentOrFile
            when (PfiQueue.DisplayType.Content or PfiQueue.DisplayType.File or PfiQueue.DisplayType.HTMLContent)

        DisplayTypeIsURL
            when (PfiQueue.DisplayType.URL)

        DisplayTypeIsLandmarkApp
            when (PfiQueue.DisplayType.LandmarkURL or PfiQueue.DisplayType.LandmarkApplication or PfiQueue.DisplayType.ActionRequest)

        DisplayTypeIsS3
            default label is "DisplayTypeIsInforLawson"
            when (PfiQueue.DisplayType.S3Application or PfiQueue.DisplayType.S3Url or PfiQueue.DisplayType.HTMLDisplay or PfiQueue.DisplayType.S3CustomForm)

        PhoneDisplayTypeIsContentOrFile
            when (PfiQueue.PhoneDisplayType.Content
            or    PfiQueue.PhoneDisplayType.File
            or    PfiQueue.DisplayType.Content
            or    PfiQueue.DisplayType.File)

        PhoneDisplayTypeIsURL
            when (PfiQueue.PhoneDisplayType.URL or PfiQueue.DisplayType.URL)

        TabletDisplayTypeIsContentOrFile
            when (PfiQueue.TabletDisplayType.Content
            or    PfiQueue.TabletDisplayType.File
            or    PfiQueue.DisplayType.Content
            or    PfiQueue.DisplayType.File)

        TabletDisplayTypeIsURL
            when (PfiQueue.TabletDisplayType.URL or PfiQueue.DisplayType.URL)

        ActorEmailAddressEntered
            restricted
            when (PfiWorkunit.Actor.ContactInfo.EmailAddress entered)

        IsValidFilter
            default label is "FilterIsValid"
            when ((PfiUserProfileRel.FilterCategory = false)
            or    (PfiUserTaskRel.FilterCategory = false)
            or    ((FilterKey = blank and FilterValue = blank) or PfiUserCategoryRel exists))

        ActorHasInbasketAccess
            when (AlternativeInbasketRel exists and IsValidFilter)

    Relations

        PfiUserCategoryRel
            one-to-one relation to PfiUserCategory
            Field Mapping uses ByUserTaskValueCategory
                related.PfiUserProfile   = actor
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType
                related.PfiUserCategory  = FilterValue
                related.PfiFilterKey     = FilterKey

        PfiUserProfileRel
            one-to-one relation to PfiUserProfile
            Field Mapping uses symbolic key
                related.PfiUserProfile = actor

        PfiUserTaskRel
            one-to-one relation to PfiUserTask
            Field Mapping uses symbolic key
                related.PfiUserProfile   = actor
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType

        PfiQueueActionRel
            one-to-many relation to PfiQueueAction
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = PfiActivity

        PfiActionRequestRel
            one-to-one relation to PfiWorkunitVariable
            Field Mapping uses symbolic key
                related.PfiWorkunit         = PfiWorkunit
                related.PfiWorkunitVariable = "ActionRequest"

        PfiMetricsRel
            one-to-many relation to PfiMetrics
            Field Mapping uses ByWorkunitActivityQueueAssignmentTaskNameTaskTypeUserProfile
                related.PfiWorkunit                 = PfiWorkunit
                related.PfiActivity                 = PfiActivity
                related.PfiQueueAssignment          = PfiQueueAssignment
                related.PfiMetrics.PfiTask.TaskName = PfiTask.TaskName
                related.PfiMetrics.PfiTask.TaskType = PfiTask.TaskType

        UserNotificationRel
            one-to-many relation to UserNotification
            delete cascades
            Field Mapping uses BySourceObject
                related.SourceObject = LocalQueueTaskRef

        PfiUserCategoryForNotifyRel
            one-to-one relation to PfiUserCategory
            Field Mapping uses ByUserTaskValueCategory
                related.PfiUserProfile   = LocalUserProfile
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType
                related.PfiUserCategory  = FilterValue
                related.PfiFilterKey     = FilterKey

        PfiTaskReportRel
            one-to-one relation to PfiTaskReport
            Field Mapping uses symbolic key
                related.PfiWorkunit        = PfiWorkunit
                related.PfiActivity        = PfiActivity
                related.PfiQueueAssignment = PfiQueueAssignment
                related.PfiTask.TaskName   = PfiTask.TaskName
                related.PfiTask.TaskType   = PfiTask.TaskType

        AlternativeInbasketRel
            one-to-one relation to PfiUserTask
            Field Mapping uses symbolic key
                related.PfiUserProfile   = actor
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType

        LocalUserProfileToActorRel
            one-to-one relation to Actor
            Field Mapping uses symbolic key
                related.Actor = LocalUserProfile

        EnableUserNotificationsRel
            one-to-one relation to PfiConfigurationProperty
            Field Mapping uses symbolic key
                related.PfiConfiguration         = "system"
                related.PfiConfigurationProperty = "enableUserNotifications"

        PfiProcessIndicatorRel
            one-to-one relation to PfiProcessIndicator
            Field Mapping uses symbolic key
                related.PfiProcessIndicator = PfiWorkunit

    Sets

        ByTaskStatusWorkunitActivityQueueAssignment
            sql name is PfiQT01
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                Status
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment

        ByTaskStatusStartDateWorkunitActivityQueueAssignment
            sql name is PfiQT02
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                Status
                StartDate
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment

        ByTaskStatusDueDateWorkunitActivityQueueAssignment
            sql name is PfiQT03
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                Status
                DueDate
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment

        ByWorkunitStatusTaskActivityQueueAssignment
            sql name is PfiQT04
            indexed
            Sort Order
                PfiWorkunit
                Status
                PfiTask.TaskName
                PfiTask.TaskType
                PfiActivity
                PfiQueueAssignment

        ByWorkunitActivityStatusTaskQueueAssignment
            sql name is PfiQT05
            indexed
            Sort Order
                PfiWorkunit
                PfiActivity
                Status
                PfiTask.TaskName
                PfiTask.TaskType
                PfiQueueAssignment

        ByStatusWorkunitActivityQueueAssignmentTask
            sql name is PfiQT06
            indexed
            Sort Order
                Status
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByWorkunitStartDateActivityQueueAssignmentTask
            sql name is PfiQT07
            indexed
            Sort Order
                PfiWorkunit
                StartDate
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByTaskWorkunitActivityQueueAssignment
            sql name is PfiQT08
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                DueDate 

        ByDueDateWorkunitActivityQueueAssignmentTask
            sql name is PfiQT09
            indexed
            Sort Order
                DueDate
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByStartDateWorkunitActivityQueueAssignmentTask
            sql name is PfiQT10
            indexed
            Sort Order
                StartDate
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByStatusIsCurrentTaskWorkunitActivityQueueAssignment
            sql name is PfiQT11
            indexed
            Sort Order
                Status
                IsCurrentActionWaitingSnapshot
                PfiTask.TaskName
                PfiTask.TaskType
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment

        ByStatusTaskWorkunitActivityQueueAssignment
            sql name is PfiQT12
            indexed
            Sort Order
                Status
                PfiTask.TaskName
                PfiTask.TaskType
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
            Instance Selection
                where (IsCurrentActionWaitingSnapshot)

        ByTaskAndFilterWhenIsAwaiting
            sql name is PfiQT13
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                FilterKey
                FilterValue
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
            Instance Selection
                where (IsCurrentActionWaitingSnapshot)

        ByDocumentIdWorkunitActivityQueueAssignmentTaskTaskType
            sql name is PfiQT14
            indexed
            Sort Order
                DocumentId
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField1Value
            sql name is PfiQT15
            Sort Order
                PfiWorkunit.KeyField1Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField2Value
            sql name is PfiQT16
            Sort Order
                PfiWorkunit.KeyField2Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField3Value
            sql name is PfiQT17
            Sort Order
                PfiWorkunit.KeyField3Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField4Value
            sql name is PfiQT18
            Sort Order
                PfiWorkunit.KeyField4Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField5Value
            sql name is PfiQT19
            Sort Order
                PfiWorkunit.KeyField5Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField6Value
            sql name is PfiQT20
            Sort Order
                PfiWorkunit.KeyField6Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField7Value
            sql name is PfiQT21
            Sort Order
                PfiWorkunit.KeyField7Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField8Value
            sql name is PfiQT22
            Sort Order
                PfiWorkunit.KeyField8Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField9Value
            sql name is PfiQT23
            Sort Order
                PfiWorkunit.KeyField9Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByKeyField10Value
            sql name is PfiQT24
            Sort Order
                PfiWorkunit.KeyField10Value
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByWorkDescription
            sql name is PfiQT25
            Sort Order
                PfiQueue.WorkDescription
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByFilterValue
            sql name is PfiQT26
            Sort Order
                FilterValue
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType
                UniqueID        

        ByActor
            sql name is PfiQT27
            Sort Order
                PfiWorkunit.Actor
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByAuthenticatedActor
            sql name is PfiQT28
            Sort Order
                PfiWorkunit.AuthenticatedActor
                PfiWorkunit
                PfiActivity
                PfiQueueAssignment
                PfiTask.TaskName
                PfiTask.TaskType

        ByIsReplicatedUniqueID
            sql name is PfiQT29
            indexed
            Sort Order
                IsReplicated
                UniqueID

    Field Rules

        Status
            if (Status changed)
                invoke BuildIsCurrentActionWaitingSnapshot

        PfiQueueAssignment
            if (PfiQueueAssignment changed)
                invoke BuildIsCurrentActionWaitingSnapshot

    Actions

        RedirectRequest is an Update Action

        Create is a Create Action

            Entrance Rules
                FilterKey = PfiQueue.PfiFilterKey
                FilterValue = PfiQueue.FilterValue

        Update is an Update Action

            Exit Rules
                IsReplicated = false

        Delete is a Delete Action

            Entrance Rules
                LocalQueueTaskRef = reference to PfiQueueTask

        ExecuteAction is an Instance Action

        ExecuteActionServer is an Instance Action
            default label is untranslatable
            restricted
            manual update

        BuildIsCurrentActionWaitingSnapshot is an Instance Action
            default label is untranslatable
            restricted

            Action Rules
                IsCurrentActionWaitingSnapshot = IsCurrentActionWaiting
                if  (IsCurrentActionWaitingSnapshot
                and  not old IsCurrentActionWaitingSnapshot)
                    invoke Notify

            Exit Rules
                IsReplicated = false

        Notify is an Instance Action
            restricted

            Action Rules
                if (AreUserNotificationsEnabled)
                    for each PfiTask.PfiUserTask set
                        LocalUserProfile = each.PfiUserProfile
                        if (not LocalUserProfile.DisableUserNotifications and LocalUserProfileToActorRel exists)
                            if (each.IsUserTaskValid and (each.FilteringDisabled or PfiUserCategoryForNotifyRel exists))
                                invoke Create UserNotification
                                    invoked.Actor        = each.PfiUserProfile
                                    invoked.Description  = this instance(locale of LocalUserProfile.IsoLocale).Description
                                    invoked.SourceType   = 2 
                                    invoked.SourceObject = reference to PfiQueueTask
                                    invoked.SuppressIon  = true


        PopulateIsCurrentActionWaitingSnapshot is a Set Action
            default label is untranslatable
            restricted
            run in foreground

            Action Rules
                Instance Rules
                    invoke BuildIsCurrentActionWaitingSnapshot








        GenerateTaskReportData is an Instance Action
            restricted

            Local Fields
                DispStatusForTaskReport is a PfiQueueStatus

            Action Rules
                if (PfiTaskReportRel.exists)
                    invoke Delete PfiTaskReportRel
                if (PfiQueue.DispStatus.ActionTakenPendingInServer
                or  PfiQueue.DispStatus.ActionInProgress)
                    DispStatusForTaskReport = PfiQueue.DispStatus.ActionAwaiting
                else
                    DispStatusForTaskReport = PfiQueue.DispStatus
                invoke Create PfiTaskReport
                    invoked.PfiWorkunit         = PfiWorkunit
                    invoked.PfiActivity         = PfiActivity
                    invoked.PfiQueueAssignment  = PfiQueueAssignment
                    invoked.PfiTask             = PfiTask
                    invoked.AppsKey             = PfiWorkunit.AppsKey
                    invoked.AppsValue           = PfiWorkunit.AppsValue
                    invoked.ProcessDefinition   = PfiWorkunit.FlowDefinition
                    invoked.ProcessVersion      = PfiWorkunit.FlowVersion
                    invoked.Originator          = PfiWorkunit.Actor
                    invoked.FilterKey           = PfiWorkunit.PfiFilterKey
                    invoked.FilterValue         = PfiWorkunit.FilterValue
                    invoked.WorkunitStartDate   = PfiWorkunit.StartDate
                    invoked.WorkunitCloseDate   = PfiWorkunit.CloseDate
                    invoked.WorkunitStatus      = PfiWorkunit.Status
                    invoked.Criterion1          = PfiWorkunit.Criterion1
                    invoked.Criterion2          = PfiWorkunit.Criterion2
                    invoked.Criterion3          = PfiWorkunit.Criterion3
                    invoked.WorkunitElapsedTime = PfiWorkunit.ElapsedTime
                    invoked.KeyField1Name       = PfiWorkunit.KeyField1Name
                    invoked.KeyField1Value      = PfiWorkunit.KeyField1Value
                    invoked.KeyField2Name       = PfiWorkunit.KeyField2Name
                    invoked.KeyField2Value      = PfiWorkunit.KeyField2Value
                    invoked.KeyField3Name       = PfiWorkunit.KeyField3Name
                    invoked.KeyField3Value      = PfiWorkunit.KeyField3Value
                    invoked.KeyField4Name       = PfiWorkunit.KeyField4Name
                    invoked.KeyField4Value      = PfiWorkunit.KeyField4Value
                    invoked.KeyField5Name       = PfiWorkunit.KeyField5Name
                    invoked.KeyField5Value      = PfiWorkunit.KeyField5Value
                    invoked.KeyField6Name       = PfiWorkunit.KeyField6Name
                    invoked.KeyField6Value      = PfiWorkunit.KeyField6Value
                    invoked.KeyField7Name       = PfiWorkunit.KeyField7Name
                    invoked.KeyField7Value      = PfiWorkunit.KeyField7Value
                    invoked.KeyField8Name       = PfiWorkunit.KeyField8Name
                    invoked.KeyField8Value      = PfiWorkunit.KeyField8Value
                    invoked.KeyField9Name       = PfiWorkunit.KeyField9Name
                    invoked.KeyField9Value      = PfiWorkunit.KeyField9Value
                    invoked.KeyField10Name      = PfiWorkunit.KeyField10Name
                    invoked.KeyField10Value     = PfiWorkunit.KeyField10Value
                    invoked.ActivityName        = PfiActivity.ActivityName
                    invoked.ActivityStatus      = PfiActivity.ActStatus
                    invoked.ActivityStartDate   = PfiActivity.StartDate
                    invoked.ActivityEndDate     = PfiActivity.EndDate
                    invoked.ActivityElapsedTime = PfiActivity.ElapsedTime
                    invoked.ActionTaken         = PfiQueue.ActionTaken
                    invoked.QueueStatus         = DispStatusForTaskReport

        Cancel is an Instance Action
            restricted

            Action Rules
                Status = Status.Canceled

            Exit Rules
                IsReplicated = false
