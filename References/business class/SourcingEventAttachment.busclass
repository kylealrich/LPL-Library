SourcingEventAttachment is a BusinessClass
    owned by ss
    prefix is EAT

    Ontology
    	part of SourcingEvent
    		relative key is SeqNumber is a Sequence
    	
    Persistent Fields
		Attachment
		Name
		Description     is a Description3
		CMAttachment	is Boolean
		SupplierCanView 
	
	Field Rules
		SeqNumber
			autosequence 
		Attachment
			required
		SupplierCanView
			default to true
		Name
			default to Attachment.Title
	
	Conditions
		HasAttachment
			restricted
			when (Attachment entered)
			
		EligibleForCM
			restricted
			when (SourcingEvent.ContractOutputExists
			or    SourcingEvent.ContractOutput)
			
		CanMaintainAttachment
			restricted
			when (SourcingEvent.InEditableState
			or    Company.CanAlwaysCreateEventAttachment)
		
	Actions
		Create is a Create Action
			default label is "Upload"
			valid when (CanMaintainAttachment)
			Field Rules
				CMAttachment
					if (!SourcingEvent.CreateByCopy
					and SourcingEvent.InEditableState)
						default to true
					
			Action Rules
				if (!Company.CanAlwaysCreateEventAttachment)
					constraint (SourcingEvent.InActionableState)
						"CannotAddAttachmentWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddAttachmentWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddAttachment)
					invoke Amend Open.Notified SourcingEvent 
							
		CreateFromWizard is a Create Action
			valid when (SourcingEvent.InEditableState)
			Field Rules
				CMAttachment
					if (!SourcingEvent.CreateByCopy
					and  SourcingEvent.InEditableState)
						default to true
					
			Action Rules
				if (CMAttachment = true)
					constraint (SourcingEvent.InEditableState)
						"CannotSetSendAttachmentToContractManagementToTrue;ContractHasAlreadyBeenCreated" 
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddAttachment)
					invoke Amend Open.Notified SourcingEvent 
		
		Update is an Update Action
			valid when (CanMaintainAttachment)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotUpdateAttachmentWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotUpdateAttachmentWhileEventIsPendingEventApproval"
				if (CMAttachment = true
				and CMAttachment changed)
					constraint (SourcingEvent.InEditableState)
						"CannotSetSendAttachmentToContractManagementToTrue;ContractHasAlreadyBeenCreated" 
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateAttachment)
					invoke Amend Open.Notified SourcingEvent

		Delete is a Delete Action
			valid when (CanMaintainAttachment)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteAttachmentWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteAttachmentWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.DeleteAttachment)
					invoke Amend Open.Notified SourcingEvent
