SystemCalendar is a BusinessClass
	owned by sc
	prefix is Scldr

	Ontology
		symbolic key is SystemCalendar

    Patterns
        implements StaticJava

	Persistent Fields
		Description 			is Alpha size 100
			translatable
		NonWorkingDayText		is Alpha 30
		M3InUse					is Boolean
			default label is "M\3InUse"
		RequiresSynchronization is Boolean
		EarliestDateModified	is Date	
		
	Local Fields
		LocalFromYear is Year
		LocalToYear   is Year
		TempDate	  is Date
		DeleteYear    is Year
		CurrentYear   is Year
		DayIdx		  is Decimal size 2.0
		
	Relations
		DatesForYear is a SystemCalendarDate set
			Instance Selection
				where (related.Year = DeleteYear)
		DatesForYearRange is a SystemCalendarDate set
			Instance Selection
				where (related.Year >= LocalFromYear
				and    related.Year <= LocalToYear)
		DatesBeforeSync is a SystemCalendarDate set
			Instance Selection
				where (related.SystemCalendarDate < EarliestDateModified)

	Conditions
		EnterpriseGroupInContext
			when (actor.context.EnterpriseGroup != blank)
			
	Derived Fields

		SyncStatus is a DerivedField
			type is Alpha size 100
			if (RequiresSynchronization)
				return SyncMessage
			else
				return " "			
		SyncMessage is a MessageField
			"CalendarHasBeenModifiedAndRequiresSynchronization"
		Capacity is a MessageField
			"Capacity"
		WorkingDay is a MessageField
			"WorkingDay"
		BankDay is a MessageField
			"BankDay"
		Holiday is a MessageField
			"Holiday"
		GoodsReceiptDay is a MessageField
			"GoodsReceiptDay"
		DeliveryDay is a MessageField
			"DeliveryDay"
		AdjustPaymentDays is a MessageField
			"AdjustPaymentDays"
		AdjustDueDays is a MessageField
			"AdjustDueDays"
		Sunday is a MessageField
			"Sunday"	
		Monday is a MessageField
			"Monday"		
		Tuesday is a MessageField
			"Tuesday"	
		Wednesday is a MessageField
			"Wednesday"	
		Thursday is a MessageField
			"Thursday"
		Friday is a MessageField
			"Friday"	
		Saturday is a MessageField
			"Saturday"			
		CapacityLabel is a DerivedField
			type is Alpha size 20
			return Capacity
		WorkingDayLabel is a DerivedField
			type is Alpha size 20		
			return WorkingDay
		BankDayLabel is a DerivedField
			type is Alpha size 20		
			return BankDay
		HolidayLabel is a DerivedField
			type is Alpha size 20		
			return Holiday
		GoodsReceiptDayLabel is a DerivedField
			type is Alpha size 20
			return GoodsReceiptDay
		DeliveryDayLabel is a DerivedField
			type is Alpha size 20
			return DeliveryDay
		AdjustPaymentDaysLabel is a DerivedField
			type is Alpha size 20
			return AdjustPaymentDays
		AdjustDueDaysLabel is a DerivedField
			type is Alpha size 20
			return AdjustDueDays
		SundayLabel is a DerivedField
			type is Alpha size 20
			return Sunday	
		MondayLabel is a DerivedField
			type is Alpha size 20
			return Monday		
		TuesdayLabel is a DerivedField
			type is Alpha size 20		
			return Tuesday	
		WednesdayLabel is a DerivedField
			type is Alpha size 20
			return Wednesday	
		ThursdayLabel is a DerivedField
			type is Alpha size 20		
			return Thursday
		FridayLabel is a DerivedField
			type is Alpha size 20
			return Friday	
		SaturdayLabel is a DerivedField
			type is Alpha size 20
			return Saturday					
						
	Actions
		Create is a Create Action
		Update is an Update Action
		Delete is a Delete Action
		
		CreateDates is an Instance Action
		
			Parameters
				FromYear is Year
				ToYear	 is Year
				DayParameters is a SystemCalendarDayParametersArray
				
			Parameter Rules
				FromYear
					required
				ToYear
					default to FromYear
					constraint (ToYear >= FromYear)
						"InvalidYearRange"					
													
			Action Rules		
				LocalFromYear = FromYear
				LocalToYear   = ToYear
				
				constraint (!DatesForYearRange exists)
					"DatesAlreadyExistWithinThisRange"
					
				CurrentYear   = FromYear
				TempDate	  = "20000101"
				TempDate year = FromYear
				while (CurrentYear <= ToYear)
					DayIdx = TempDate week day				
					invoke Create SystemCalendarDate
						invoked.EnterpriseGroup   	 = EnterpriseGroup
						invoked.SystemCalendar    	 = SystemCalendar
						invoked.SystemCalendarDate  = TempDate
						invoked.IsWorkingDay 		 = DayParameters.ParameterGroup[DayIdx].IsWorkingDay
						invoked.IsBankDay			 = DayParameters.ParameterGroup[DayIdx].IsBankDay
						invoked.IsGoodsReceiptDay 	 = DayParameters.ParameterGroup[DayIdx].IsGoodsReceiptDay
						invoked.IsDeliveryDay 		 = DayParameters.ParameterGroup[DayIdx].IsDeliveryDay
						invoked.TimeEntryAllowed     = DayParameters.ParameterGroup[DayIdx].TimeEntryAllowed
						invoked.JobSchedulingAllowed = DayParameters.ParameterGroup[DayIdx].JobSchedulingAllowed
						invoked.Capacity 			 = DayParameters.ParameterGroup[DayIdx].Capacity
						invoked.AdjustPaymentDays 	 = DayParameters.ParameterGroup[DayIdx].AdjustPaymentDays
						invoked.AdjustDueDays 		 = DayParameters.ParameterGroup[DayIdx].AdjustDueDays																																												
					TempDate += 1 day
					CurrentYear = TempDate year						
				
		DeleteDates is an Instance Action
				
			Parameters
				YearToDelete is Year
				
			Parameter Rules
				YearToDelete
					required
					
			Action Rules
				DeleteYear = YearToDelete			
				confirmation required
					"DeleteAllDatesFor<DeleteYear>?"		
				constraint (DatesForYear exists)
					"NoDatesExistFor<DeleteYear>"
				invoke Delete DatesForYear
				
		Synchronize is an Instance Action
			confirmation required
				"SynchronizeDates"
				
			Action Rules
				invoke SynchronizeDates SystemCalendarDate
					invoked.PrmEnterpriseGroup		= EnterpriseGroup
					invoked.PrmSystemCalendar 	 	= SystemCalendar
					invoked.PrmEarliestDateModified = EarliestDateModified
					invoked.PrmLastSyncedDate	    = last DatesBeforeSync.SystemCalendarDate
					
				RequiresSynchronization = false
				initialize EarliestDateModified				
				
