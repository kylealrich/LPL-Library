TaxAttribute is a BusinessClass	 
    owned by tx
	prefix is TxAtt

    Ontology
        symbolic key is TaxAttribute

    Patterns
		implements InlineUserFields
			size is 1000
		implements ContextualParent
		
	Persistent Fields
		Comments							is RichText	  
		HideVertexUserFields				is Boolean
		HideOneSourceUserFields				is Boolean
		HideUnusedOneSourceAttributes 		is Boolean
		ShowAvailableOneSourceAttributes 	is Boolean
		ShowAttributesThat is Alpha 1
			States
				All			value is blank
				Available   value is "A"
				Used        value is "U"
				Hide        value is "H"
		HideComments						is Boolean
		DisplayMonitor						is RichText
		
	Local Fields
		Idx									is Numeric 2
		LocalTrigger						is Alpha 1
		NewTaxAttributeGroup50 				is a TaxAttributeGroup50	
		UserElementGroup					is a TaxAttributeGroup50
		LocalCopyFromBusclass				is a TaxAttribute
		LocalNewTaxAttributeKey				is like TaxAttribute
		
	Transient Fields
		NewUserElementGroup 				is a UserElementGroup
		UserElementAttributeGroup			is a UserElementGroup	
		FlexibleFields 						is a VertexAttributeGroup
				
	Conditions		 
		ShowAttributesWarning
			restricted
			when (!ShowAttributesThat.All)
		1SrcAvailableFields						 
			restricted
			when (!HideOneSourceUserFields
			and   ShowAvailableOneSourceAttributes
			and   OneSourceTaxAttributeNotUsedRel exist)
		1SrcAllAttributes
			restricted
			when (!HideOneSourceUserFields		 
			and   !ShowAvailableOneSourceAttributes
			and   OneSourceTaxAttributeRel exist) 
		VertexFlexibleFieldExist
			restricted
			when (VertexFlexibleFieldsRel exist)


									
	Relations	
		DynamicFieldRel
        	one-to-many relation to DynamicField
        	Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
		
  		OneSourceTaxAttributeRel
        	one-to-many relation to OneSourceTaxAttribute
			delete cascades
        	Field Mapping uses ByAttribute		 
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
			Instance Selection
				where (related.OneSourceTaxAttribute.UniqueIdOfRelatedObject	= UniqueID)

 		OneSourceTaxAttributeByNumberRel
        	one-to-many relation to OneSourceTaxAttribute
			delete cascades
        	Field Mapping uses ByAttributeNumber		 
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
			Instance Selection
				where (related.OneSourceTaxAttribute.UniqueIdOfRelatedObject	= UniqueID)

 		OneSourceProjectTaxAttributeByNumberRel
        	one-to-many relation to OneSourceTaxAttribute
			delete cascades
        	Field Mapping uses ByAttributeNumber		 
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
			Instance Selection
				where (related.OneSourceTaxAttribute.UniqueIdOfRelatedObject	= UniqueID
				and    related.LoadDynamicField.System = "PS")
				
  		OneSourceTaxAttributeNotUsedRel
        	one-to-many relation to OneSourceTaxAttribute
			delete cascades
        	Field Mapping uses symbolic key		 
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
			Instance Selection
				where (related.OneSourceTaxAttribute.UniqueIdOfRelatedObject	= UniqueID
				and    related.Value											= blank
				and    related.LoadDynamicField 								= blank)

  		OneSourceTaxAttributeUsedRel
        	one-to-many relation to OneSourceTaxAttribute
			delete cascades
        	Field Mapping uses symbolic key		 
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
			Instance Selection
				where (related.OneSourceTaxAttribute.UniqueIdOfRelatedObject	= UniqueID
				and   (related.Value											!= blank
				or     related.LoadDynamicField 								!= blank))

    	CopyTaxAttributeRel
			one-to-many relation to TaxAttribute
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup						= FinanceEnterpriseGroup
			Instance Selection	
				where (related.TaxAttribute							= LocalCopyFromBusclass)

		NewTaxAttributeRel
			one-to-many relation to TaxAttribute
			Field Mapping uses symbolic key	  
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection	
				where (related.TaxAttribute 	= LocalNewTaxAttributeKey)
						

		VertexFlexibleFieldsRel							 
			one-to-one relation to VertexFlexibleFields
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 							= FinanceEnterpriseGroup
				related.VertexFlexibleFields.UniqueIdOfRelatedObject	= UniqueID
				related.VertexFlexibleFields.TaxAttributeCode           = TaxAttribute	
				related.VertexFlexibleFields.Type						= blank	

		GetEachVertexFlexibleFieldsRel							 
			one-to-many relation to VertexFlexibleFields
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 								= FinanceEnterpriseGroup
			Instance Selection
				where (related.VertexFlexibleFields.TaxAttributeCode		= TaxAttribute		 
				and    related.VertexFlexibleFields.UniqueIdOfRelatedObject	= UniqueID
				and    related.VertexFlexibleFields.TaxAttributeCode        = TaxAttribute		 
				and    related.VertexFlexibleFields.Type					= blank)	
				
	Field Rules
		HideVertexUserFields	 								
			if (HideVertexUserFields)
				constraint (!HideOneSourceUserFields)
					"YouCannotHideBothUserFields"

		HideOneSourceUserFields	 
			if (HideOneSourceUserFields)
				constraint (!HideVertexUserFields)
					"YouCannotHideBothUserFields"
				
	Derived Fields
		CommentsExist is a DerivedField		 
			type is MessageField
			restricted
			if (Comments entered)
				return CommentExistLabel
			return blank			

		CommentExistLabel is a MessageField                     
			"Exist"	

		LoadAttributesToLocal is a DerivedField		 
			type is Alpha 1
			restricted
			initialize Idx
			initialize DisplayMonitor
			for each OneSourceTaxAttributeRel
				if (each.Value entered
				or  each.LoadDynamicField entered)
					Idx += 1
                    NewUserElementGroup.UserElement[Idx].Number	= each.AttributeNumber				 
					NewUserElementGroup.UserElement[Idx].Name	= each.UserAttributeName							 
					NewUserElementGroup.UserElement[Idx].Value	= each.Value							  									 
			DisplayMonitor = NewUserElementGroup
			return blank




		GetVertexFlexibleFieldsDerived is a DerivedField		 
			type is Alpha 1
			restricted
			initialize Idx
			initialize DisplayMonitor	
			for each GetEachVertexFlexibleFieldsRel
				DisplayMonitor = each.LoadFieldsToLocalArray2	 
				FlexibleFields = each.FlexibleFields
			return blank
					
	Actions
		Create is a Create Action
			Exit Rules

		Update is an Update Action
		Delete is a Delete Action

		CreateDoNotPreLoadAttributes is a Create Action		
			restricted
					
		CreateOneSourceTaxAttribute is an Instance Action		
			completion message is "OneSourceTaxAttributeSuccessfullyCreated"			
			restricted
			Action Rules
				invoke PreLoadAllAttributes OneSourceTaxAttribute  
					invoked.ParmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.ParmUniqueIdOfRelatedObject = UniqueID
					
		RemoveOneSourceTaxAttribute is an Instance Action		
			completion message is "OneSourceTaxAttributeSuccessfullyDeleted"	
			restricted
			Parameters
				RemoveOnlyUnused is Boolean			
			Action Rules
				if (RemoveOnlyUnused)
					invoke Delete OneSourceTaxAttributeNotUsedRel
				else	
					invoke Delete OneSourceTaxAttributeRel

		GetOneSourceTaxAttribute is an Instance Action				 	
			completion message is "OneSourceTaxAttributeSuccessfullyLoadedToComments"			
			restricted
			Action Rules		
				LocalTrigger = LoadAttributesToLocal				 
				UserElementAttributeGroup =	NewUserElementGroup 	 
				UserElementGroup = NewUserElementGroup	
				

		CopyTaxAttributeRecord is an Instance Action	

			completion message is "NewRecordSuccessfullyCreated..."
			restricted
			Parameters
				CopyThisInstance			is a TaxAttribute	
				NewTaxAttributeRecord		is like TaxAttribute 
				CopyOneSourceAttributes		is Boolean

			Parameter Rules
				CopyThisInstance
					initial value is TaxAttribute
			Local Fields
			Entrance Rules
			Action Rules
				LocalCopyFromBusclass = CopyThisInstance
				if (CopyTaxAttributeRel exist)

					invoke CreateDoNotPreLoadAttributes TaxAttribute
						assign result to LocalNewTaxAttributeKey
						fill in fields from this instance
						invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
						invoked.TaxAttribute 			= NewTaxAttributeRecord 
							
				LocalNewTaxAttributeKey	= NewTaxAttributeRecord
				if (CopyOneSourceAttributes
				and OneSourceTaxAttributeUsedRel exist)
					for each OneSourceTaxAttributeUsedRel
						invoke Create OneSourceTaxAttribute
							fill in fields from each					 
			                	except invoked.OneSourceTaxAttribute.UniqueIdOfRelatedObject
							invoked.OneSourceTaxAttribute.UniqueIdOfRelatedObject = NewTaxAttributeRel.UniqueID	 				

		CreateVertexFlexibleFields is an Instance Action	
			valid when (!HideVertexUserFields)
			completion message is "VertexFlexibleFieldSuccessfullyCreated"			
			Action Rules
				invoke Create VertexFlexibleFields  
					invoked.FinanceEnterpriseGroup 							= FinanceEnterpriseGroup
					invoked.VertexFlexibleFields.UniqueIdOfRelatedObject	= UniqueID
					invoked.VertexFlexibleFields.TaxAttributeCode           = TaxAttribute	
					invoked.VertexFlexibleFields.Type						= blank														
