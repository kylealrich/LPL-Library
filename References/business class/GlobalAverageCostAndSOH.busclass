GlobalAverageCostAndSOH is a BusinessClass
	owned by ic
	prefix is Glob
	
	Ontology
		symbolic key is GlobalAverageCostAndSOH

	Patterns
		implements StaticJava
		disable AuditIndex
	
	Persistent Fields
		GlobalAverageCost              is an InternationalCost
			protected
			disable Auditing
		GlobalStockOnHand              is an Quantity
			precision is Item.NumberOfDecimalsQuantity
			protected
			disable Auditing
		AverageComputation		  is Numeric size 1
			default label is "CalculationStatus"
			States
				NotStarted				value is 0
				Ongoing					value is 1
				Finished				value is 2
	
	Local Fields
		Execute							is Boolean
		ActionThread
		AuditError						is Boolean
		AuditErrMsg						is Alpha 150
	
	Context Fields
		InventoryTransactionLine
	
	Sets
		ByFinishedCalculation
			Instance Selection
				where (AverageComputation.Finished)
			Sort Order
				Company
				Item

	Actions
    	Create is a Create Action
			restricted
    		
    	Update is an Update Action
    		restricted
		
    	Delete is a Delete Action
			restricted
		
		UpdateFromTransaction is an Instance Action
			restricted
			refresh and lock this instance
			Parameters
				PrmGlobalStockOnHand 			is a Quantity
				PrmGlobalAverageCost   			is an InternationalCost
			Action Rules
				if (PrmGlobalStockOnHand entered)
					GlobalStockOnHand += PrmGlobalStockOnHand
				if (PrmGlobalAverageCost entered)
					GlobalAverageCost = PrmGlobalAverageCost
			Exit Rules
				AverageComputation    = AverageComputation.Finished
	
	Action Exit Rules
		initialize AuditError
		initialize AuditErrMsg
		Execute = ActionThread.GetDetails
		if (not action type.Delete)
	 		invoke Create GlobalAverageCostAndSOHAudit
				resume on error
					AuditError  = true
					AuditErrMsg = error message
				invoked.Company							= Company
	 			invoked.Item							= Item
				invoked.AfterGlobalStockOnHand			= GlobalStockOnHand
	 			invoked.BeforeGlobalStockOnHand 		= old GlobalStockOnHand
				invoked.AfterGlobalAverageCost			= GlobalAverageCost
	 			invoked.BeforeGlobalAverageCost 		= old GlobalAverageCost
				invoked.RootBusinessClass				= ActionThread.RootBusinessClass
				invoked.RootAction						= ActionThread.RootAction
				invoked.RootBusinessObjectReference		= ActionThread.RootBusObjectRef
				invoked.InvokingAction					= ActionThread.InvokingAction
				invoked.ActionContext					= ActionThread.ActionContext
				invoked.Actor							= actor
				invoked.UpdateDate						= current timestamp
				if (InventoryTransactionLine entered)
					if (InventoryTransactionLine.OriginatingTransaction entered)
						invoked.OriginatingTransaction	= InventoryTransactionLine.OriginatingTransaction
					else
						invoked.OriginatingTransaction	= reference to InventoryTransactionLine
		
		if (AuditError)
			invoke CreateError GlobalAverageCostAndSOHAudit
				resume on error
		 		invoked.Company							= Company
		 		invoked.Item							= Item
				invoked.ActionContext 					= AuditErrMsg
				invoked.Actor							= actor
				invoked.UpdateDate						= current timestamp
