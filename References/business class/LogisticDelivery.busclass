LogisticDelivery is a BusinessClass
    owned by wh
    prefix is WHLD

    Ontology
        symbolic key is LogisticDelivery

    Patterns
        implements StaticJava
        disable AuditIndex
    
    Persistent Fields
        VehicleTrip
        VehicleTripLine
        DeliveryStatus                  is Numeric   1
            States
                NotYetDelivered         value is 0
                DeliveryInProgress      value is 1
                PartiallyDelivered      value is 2
                Delivered               value is 3

    Derived Fields
        TotalLogisticDeliveryLinesNotDelivered is a DerivedField
            type is Numeric 6
            restricted
            return instance count of LogisticsDeliveryLineNotDeliveredRel
    
    Relations
        LogisticsDeliveryLineRel is a LogisticDeliveryLine set

        LogisticsDeliveryLineNotDeliveredRel is a LogisticDeliveryLine set
            Instance Selection
                where (not related.Delivered)

    Conditions
        NotYetDelivered
            restricted
            when (DeliveryStatus.NotYetDelivered)

        DeliveryInProgress
            restricted
            when (DeliveryStatus.DeliveryInProgress)

        PartiallyDelivered
            restricted
            when (DeliveryStatus.PartiallyDelivered)

        DeliveryCompleted
            restricted
            when (DeliveryStatus.Delivered)

    StateCycles
        LogisticDeliveryCycle is a StateCycle
            state field is DeliveryStatus

            NotYetDelivered  is a State   
                Update is an Update Action     
                TransitionToDeliveryInProgress is an Instance Action
                    restricted
                    completion message is "Delivery_In_Progress"
                    Action Rules
                        make transition to DeliveryInProgress
                  
            DeliveryInProgress is a State
                TransitionToPartiallyDelivered is an Instance Action
                    restricted
                    completion message is "Partially_Delivered"
                    Action Rules
                        make transition to PartiallyDelivered
               
            PartiallyDelivered is a State
                TransitionToDelivered is an Instance Action
                    restricted
                    completion message is "Delivered"
                    Action Rules
                        make transition to Delivered

            Delivered is a State
        
    Actions
        Create is a Create Action
        
        ConfirmDelivery is an Instance Action
            completion message is "Delivered"
            confirmation required
                "TheDeliveryStatusOfAllShipmentsWillBeUpdatedToDelivered.DoYouWantToProceed"
            valid when (not DeliveryCompleted)
            Action Rules
                if (TotalLogisticDeliveryLinesNotDelivered > 50 )
                    invoke ConfirmAllDeliveryLines LogisticDeliveryLine
                        invoked.PrmCompany                  = Company
                        invoked.PrmInventoryLocation        = InventoryLocation
                        invoked.PrmDeliveryLogistics        = LogisticDelivery
                        invoked.PrmVehicleTrip              = VehicleTrip
                else
                    invoke ConfirmAllDeliveryLines LogisticDeliveryLine in foreground
                        invoked.PrmCompany                  = Company
                        invoked.PrmInventoryLocation        = InventoryLocation
                        invoked.PrmDeliveryLogistics        = LogisticDelivery
                        invoked.PrmVehicleTrip              = VehicleTrip
