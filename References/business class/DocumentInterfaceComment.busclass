DocumentInterfaceComment is a BusinessClass
	owned by ma
	prefix is DICM

	Ontology
		symbolic key is DocumentInterfaceComment

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

	Persistent Fields
    	Title 						is a CommentName
    	Comment					 	is a NewCommentText
    	Type						is AlphaUpper 1
    		States
    			All						value is "A"
    			InvoiceNote				value is "N"
    			InvoiceReport			value is "D"
    			InvoiceCheck			value is "C"
				InvoicePaymentAddendum	value is "H"
    	SupplierCanView

	Local Fields
    	ImportedComment				is a PayablesInvoiceComment view
    	LocalComment				is like PayablesInvoiceComment
    	LocalErrorMessage			is Alpha 150

	Derived Fields

	Conditions
		RecordInError
			when (DocumentInterfaceErrorRel exists)

	Relations
		DocumentInterfaceErrorRel
			one-to-many relation to DocumentInterfaceError
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice
				related.DocumentInterfaceDetail			= blank
				related.DocumentInterfaceDistribution	= blank
				related.DocumentInterfaceAddOnCharge	= blank
				related.DocumentInterfaceComment		= DocumentInterfaceComment

		LastCommentRel
			one-to-many relation to DocumentInterfaceComment
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice

	Sets
				
	Field Rules

	Rule Blocks

	Actions
		Create is a Create Action
			bypass field rules
			Entrance Rules
				DocumentInterfaceComment = last LastCommentRel.DocumentInterfaceComment + 1

		Update is an Update Action

		Delete is a Delete Action

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		InterfaceComments is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmCompany					is like PayablesCompany
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add            			value is "A"
		                MatchSubmitOrRelease	value is "R"
				PrmProcessType				is AlphaUpper size 1
		            States
		                MatchInvoices    	value is "M"
		                ExpenseInvoices		value is "E"
		                Both				value is "B"
				PrmVendorClass				is like VendorClass		                
				PrmDocumentInterfaceInvoice is like DocumentInterfaceInvoice
				PrmInterfaceRun				is a PayablesInvoiceInterfaceResult
				
			Local Fields
				ErrorOccurred					is Boolean
				LocalInterfacedCommentsCount		is Numeric 10
				LocalInterfaceCommentErrorCount		is Numeric 10

			Instance Selection
				where (DocumentInterfaceInvoice.InterfaceRunID = PrmInterfaceRun
				and    DocumentInterfaceInvoice.SystemAssignedInvoice entered)

			Sort Order
				FinanceEnterpriseGroup
				DocumentInterfaceInvoice

			Action Rules
				Empty Set Rules
					invoke InterfaceDistributions DocumentInterfaceDistribution
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmCompany					= PrmCompany
						invoked.InvoiceProcess				= InvoiceProcess
						invoked.PrmProcessType				= PrmProcessType
						invoked.PrmVendorClass				= PrmVendorClass
						invoked.PrmDocumentInterfaceInvoice = PrmDocumentInterfaceInvoice
						invoked.PrmInterfaceRun				= PrmInterfaceRun

				Set Rules
					Exit Rules
						invoke Update PrmInterfaceRun
							invoked.InterfacedCommentsCount			= LocalInterfacedCommentsCount

						invoke InterfaceDistributions DocumentInterfaceDistribution
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmCompany					= PrmCompany
							invoked.InvoiceProcess				= InvoiceProcess
							invoked.PrmProcessType				= PrmProcessType
							invoked.PrmVendorClass				= PrmVendorClass
							invoked.PrmDocumentInterfaceInvoice = PrmDocumentInterfaceInvoice
							invoked.PrmInterfaceRun				= PrmInterfaceRun

				DocumentInterfaceInvoice Set Rules
					Entrance Rules
						ErrorOccurred = false
						if (DocumentInterfaceInvoice.RecordInError)
							ErrorOccurred = true
		 					
					Exit Rules

				Instance Rules
					if (!ErrorOccurred)
						invoke Create PayablesInvoiceComment
							assign result to ImportedComment
							resume on error
								ErrorOccurred		= true
								LocalErrorMessage	= error message
							fill in fields from this instance
							invoked.Company											= DocumentInterfaceInvoice.Company
							invoked.PayablesInvoice									= DocumentInterfaceInvoice.SystemAssignedInvoice
	
						if (ErrorOccurred)
							LocalInterfaceCommentErrorCount							+= 1						
							invoke Create DocumentInterfaceError
								invoked.FinanceEnterpriseGroup						= FinanceEnterpriseGroup
								invoked.DocumentInterfaceInvoice 					= DocumentInterfaceInvoice
								invoked.DocumentInterfaceComment 					= DocumentInterfaceComment
								invoked.ErrorMessage								= LocalErrorMessage

							invoke Update PrmInterfaceRun
								invoked.Status 										= 2
						else
							LocalInterfacedCommentsCount							+= 1						
							LocalComment											= ImportedComment.PayablesInvoiceComment
							invoke FastDelete
