FranchisePrepayment is a BusinessClass
    owned by fr
    prefix is FRP
    classic name is FRPREPAY

    Ontology
        symbolic key is FranchisePrepayment
            classic set name is FRPNEWPRIMARYINDEX
            classic name for FranchisePrepayment is DATE
            classic name for CustomerShipTo is SHIP-TO
            classic name for FranchiseContract is CONTR-NBR
            classic name for FranchiseContractCharge is SEQ

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Charge
        Status            is Numeric size 1
            States
                Unreleased value is 0
                Released   value is 1
                GlUpdated  value is 9
                    default label is "GlobalLedgerUpdated"
        TransactionAmount is an InternationalAmount
            classic name is TRAN-AMT

    Conditions

        IsNeedsWork
        	restricted
            when (not Status.GlUpdated)

        IsReleasedPymts
        	restricted
            when (Status.Released)

        IsUnposted
        	restricted
            when (Status <  9)

        IsUnprocessed
        	restricted
            when (not Status.GlUpdated)

		RecordExists
			restricted
			when (FranchisePrepayment exists)

	Derived Fields
	
	    ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforFranchisePrepayment"
			
		ContextMessageText is a MessageField
			restricted
			"FranchisePrepayment<FranchisePrepayment>"


    Relations

        ArcustomerRel
            one-to-one relation to CompanyCustomer
            required
            Field Mapping uses symbolic key
                related.Company  = Company
                related.Customer = Customer

        FrchargeRel
            one-to-one relation to FranchiseContractCharge
            required
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.Customer                = Customer
                related.CustomerShipTo          = CustomerShipTo
                related.FranchiseContract       = FranchiseContract
                related.FranchiseContractCharge = FranchiseContractCharge

        FrcontmastRel
            one-to-one relation to FranchiseContract
            required
            Field Mapping uses symbolic key
                related.Company           = Company
                related.Customer          = Customer
                related.CustomerShipTo    = CustomerShipTo
                related.FranchiseContract = FranchiseContract

        ShiptoRel
            one-to-one relation to CustomerShipTo
            required
            Field Mapping uses symbolic key
                related.Company        = Company
                related.Customer       = Customer
                related.CustomerShipTo = CustomerShipTo

        CustdescRel
            one-to-one relation to Customer
            required
            Field Mapping uses symbolic key
                related.CustomerGroup = Company.CustomerGroupField.CustomerGroup	
                related.Customer      = Customer

    Sets

        Set2
            indexed
            Instance Selection
                where (IsUnprocessed)
            Sort Order
                Company
                Customer
                CustomerShipTo
                FranchiseContract
                FranchiseContractCharge
                Charge
                FranchisePrepayment

        Set3
            indexed
            Instance Selection
                where (IsUnposted)
            Sort Order
                Company
                FranchisePrepayment
                Customer
                CustomerShipTo
                FranchiseContract
                FranchiseContractCharge
                Charge

        Set4
            indexed
            Instance Selection
                where (IsReleasedPymts)
            Sort Order
                Company
                FranchisePrepayment
                Customer
                CustomerShipTo
                FranchiseContract
                FranchiseContractCharge
                Charge

        Set1
            not indexed
            Sort Order
                Company
                Customer
                CustomerShipTo
                FranchiseContract
                FranchiseContractCharge
                Charge
                FranchisePrepayment


	Field Rules
		Charge

			initial value is FranchiseContractCharge.FranchiseStandardCharge.Charge
			default to FranchiseContractCharge.FranchiseStandardCharge.Charge
			constraint (FranchiseContractCharge.FranchiseStandardCharge.ChargeType.NoteBased)
				"ChargeMustBeANoteBasedCharge"
			
		TransactionAmount
			required
				"CannotRelease;PrepaymentAmountRequired"//"Cannot release; prepayment amount required"     
			constraint (TransactionAmount <= FranchiseContractCharge.CurrentBalance)
				"PrepaymentAmountCannotBeGreaterThanCurrentBalance"
			constraint (TransactionAmount > 0)
				"Invalid_\Prepayment_\Amount"
		FranchisePrepayment
			constraint (FranchisePrepayment > FranchiseContractCharge.BeginDate)
				"Pre-\Payment_\DateCannotBeBefore_\Charge_\Begin_\Date"
			constraint (FranchisePrepayment < FranchiseContractCharge.EndDate)
				"Pre-\Payment_\DateCannotBeAfter_\Charge_\End_\Date"

    Actions
        Create is a Create Action


	StateCycles
		FranchisePrepaymentLifeCycle is a StateCycle
			state field is Status

			Unreleased is a State
				Update is an Update Action

		        Delete is a Delete Action

		        Release is an Instance Action
					Action Rules
						invoke PrepaymentRelease FranchiseContractCharge
							invoked.TransactionAmount = TransactionAmount
							
						make transition to Released

			Released is a State
		        GlUpdate is an Instance Action
		        	restricted
					Action Rules
						make transition to GlUpdated

			GlUpdated is a State
	
