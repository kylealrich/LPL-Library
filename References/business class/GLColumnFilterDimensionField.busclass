GLColumnFilterDimensionField is a BusinessClass
	owned by GeneralLedger
	prefix is GLCFF

	Ontology
		symbolic key is GLColumnFilterDimensionField

	Patterns

	Persistent Fields
		DisplayOrder		is Numeric 2
    	FilterDisplayValue	is Alpha 100
    	IsLeafLevel			is Boolean
    	DimensionLevel		is Numeric 2
    	StructureDimension	is Alpha 200
    	StructureField		is Alpha 200
    	StructureFilter		is Alpha 50

	Local Fields
		LocalDimFilterValues			is Alpha 1000
		LocalDimFilterDisplayValues		is Alpha 1000

	Derived Fields
		Dimension is a DerivedField
			type is Alpha 200
			return GLColumnFilterDimension.Dimension
	
		DerivedGLColumn is a DerivedField
			type is like GLColumn
			return GLColumnFilterDimension.GLColumn
	
		DerivedSummaryAccountYesNo is a DerivedField
			type is Alpha 3
			if IsLeafLevel
				return "No"
			else
				return "Yes"
		
		DerivedFilterValue is a DerivedField
			type is Alpha 1000

			if (IsLeafLevel)
				LocalDimFilterValues = GLColumnFilterDimension.DimensionLabel + " = \"" + GLColumnFilterDimensionField.FilterValue + "\""
			else
				LocalDimFilterValues = GLColumnFilterDimension.DimensionLabel + " level " + DimensionLevel + " = \"" + GLColumnFilterDimensionField.FilterValue + "\""
				
			return LocalDimFilterValues
			
		DerivedFilterDisplayValue is a DerivedField
			type is Alpha 1000

			LocalDimFilterDisplayValues = " "
			
			if (IsLeafLevel)
				LocalDimFilterDisplayValues = GLColumnFilterDimension.DimensionLabel + " = \"" + FilterDisplayValue + "\""
			else
				LocalDimFilterDisplayValues = GLColumnFilterDimension.DimensionLabel + " level " + DimensionLevel + " = \"" + FilterDisplayValue + "\""
				
			return LocalDimFilterDisplayValues

	Actions
 		Create is an Action
 			restricted
			Exit Rules 
				if (GLReportRel exists)
					for each GLReportRel
						if (each.IntegrationStatus = 1)
							invoke UpdateGLReport each
								invoked.PrmIntegrationStatus = 2

				if(SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2
    	Update is an Update Action
    		restricted
    	Delete is a Delete Action
			Entrance Rules
				if (GLColumnFilterDimension.GLColumnViewRel.Lock)
					constraint (GLColumnFilterDimension.GLColumnViewRel.DerivedCreatedBy = actor)
						"ThisHasBeenLockedForEditing"
			
			Exit Rules 
				if (GLReportRel exists)
					for each GLReportRel
						if (each.IntegrationStatus = 1)
							invoke UpdateGLReport each
								invoked.PrmIntegrationStatus = 2

				if(SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2
								
    	Purge is a Purge Action
    		restricted
	
	Relations

		GLReportRel
			one-to-many relation to GLReport
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where( related.ColumnView				= GLColumnFilterDimensionField.GLColumn.GLColumnView)

		SubReportRel
			one-to-many relation to GLReportElement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.SubReport.ColumnView		= GLColumnFilterDimensionField.GLColumn.GLColumnView)	
