LeasePaymentImport is a BusinessClass
    owned by lm
    prefix is PCN
    classic name is LMPAYCNV

    Ontology
        symbolic key is LeasePaymentImport
            classic set name is PCNSET1
            classic name for LeasePaymentImport.SequenceNumber is SEQ-NUMBER

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
        implements Archivable

    Persistent Fields

        Lease
        VendorAndRemitTo
            classic name for VendorAndRemitTo.Vendor is PMT-VENDOR
        PaymentNumber
            classic name is PMT-NBR
        ExecutoryCostCode
        	default label is "NonLeaseComponent"
            classic name is EXEC-CODE
        Suffix
        Description
        AuthorityCode              is a PayablesAuthorityCode
            classic name is AUTH-CODE
        DueDate
        ProcessLevel
            classic name is PROC-LEVEL
        AccrualCode                is a PayablesAccrualCode
            classic name is ACCR-CODE
        PaymentAmount              is an InternationalAmount
            classic name is PAYMENT-AMT
        ComputeStatement
            classic name is COMPUTE-NAME
        ComputeAccount
            classic name for ComputeAccount.ComputeCompany is COMPUTE-CO
            classic name for ComputeAccount.AccountingUnit is ACCT-UNIT
            classic name for ComputeAccount.ComputeFromAccount is FR-ACCOUNT
            classic name for ComputeAccount.ComputeThroughAccount is TH-ACCOUNT
        GeneralLedgerCompanyGroup
            classic name is COMPANY-GROUP
            default label is "GlobalLedgerCompanyGroup"
        AccountGroup
        GeneralLedgerBudgetNumber  is a BudgetNbr
            classic name is GL-BUDGET-NBR
            default label is "GlobalLedgerBudgetNumber"
        VendorInvoiceNumber
            classic name is VENDOR-INV-NBR
        HistoryFl                  is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
            default label is "HistoryFlag"
        InterestAmount             is an InternationalAmount
            classic name is INTEREST-AMT
        StraightLineFlag           is a Flag
            classic name is STRT-LINE-FLAG
        StraightLineAmount         is an InternationalAmount
            classic name is STRT-LINE-AMT
        ComputeBaseAmount          is an InternationalAmount
            classic name is CMP-PMT-AMT
        MaximumPayment             is an InternationalAmount
            classic name is MAXIMUM-PMT
        AdvancedComputeOption
            classic name is COMPUTE-OPT
        AccrueFlag                 is Boolean
        Period                     is an AccountingPeriod
        Year
        TaxableFlag
        TaxCode
        TaxUsageCode
            classic name is TAX-USAGE-CD
        TransactionTaxableAmount   is an InternationalAmount
            classic name is TRAN-TAXABLE
        PercentTaxable             is a Percent
            classic name is PCNT-TAXABLE
        HistoricalVendorAndRemitTo is a VendorAndRemitTo
            classic name for HistoricalVendorAndRemitTo.Vendor is HIS-PAY-VENDOR
            classic name for HistoricalVendorAndRemitTo.RemitToCode is HIS-REMIT-CODE
        CombineWithLeasePayment	    is Boolean
        DoNotCreateAPInvoice		is Boolean
			default label is "DoNotCreatePayablesInvoice"
        NetChangeToPresentValue		is an InternationalAmount

	Transient Fields
		TransientDateRange						is a ProjectDateRange

    Derived Fields
    	
    	DerivedFormTitle		is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return LeasePaymentImportDisplay
			else
				return CreateMessage
				
		CreateMessage			is a LabelField
			restricted
			"CreateNewLeasePaymentImport"
		
		LeasePaymentImportDisplay	is a LabelField
			"LeasePaymentImport"

        LeasePaymentInterface is a DerivedField
            type is Alpha size 2083
            return base url(webapp is LeaseSpecialist) +"/LeaseSpecialist/form/LeasePaymentScheduleInterface.LeasePaymentScheduleInterface?action=LeasePaymentScheduleInterface"

    Conditions
    
    	RecordExists 
    		restricted
    		when (LeasePaymentImport exists)
    Sets

        Set2
            indexed
            Sort Order
                LeasePaymentImport.RunGroup
                Company
                Lease
                VendorAndRemitTo.Vendor
                PaymentNumber
                ExecutoryCostCode
                LeasePaymentImport.SequenceNumber

        Set3
            indexed
            Sort Order
                LeasePaymentImport.RunGroup
                LeasePaymentImport.SequenceNumber descending

		ByDueDate
			indexed
            Sort Order
                Company
                Lease
                VendorAndRemitTo.Vendor
                DueDate
                ExecutoryCostCode
                LeasePaymentImport.RunGroup
                LeasePaymentImport.SequenceNumber

    Rule Blocks

        CreateAndUpdateRules           
            if (!LeasePaymentImport.SequenceNumber entered)
                LeasePaymentImport.SequenceNumber = (first LeasePaymentImportRel.LeasePaymentImport.SequenceNumber + 1)

            if (ExecutoryCostCode entered)
                constraint (LeaseExecutoryCostCodeRel exists)
                    "LeaseNonLeaseComponentNotAssignedToThisLease"                
                if (CombineWithLeasePayment)
                    if (LeasePaymentImportForDueDateRel exists)
                        constraint (!LeasePaymentImportForDueDateRel.PaymentAmount = "0")
                            "LeasePaymentAmountMustNotBeZeroToBeAbleCombineWithNon-LeasePayment"
                        PaymentNumber   = LeasePaymentImportForDueDateRel.PaymentNumber
                    else
                        if (LeasePaymentDetailForDueDateRel exists)
                            constraint (LeasePaymentDetailForDueDateRel.Released = false)
                                "LeasePaymentHasBeenRelesed.CannotCombineWithPaymentsThatHaveAlreadyBeenReleased"                            
                            constraint (!LeasePaymentDetailForDueDateRel.PaymentAmount = "0")
                                "LeasePaymentAmountMustNotBeZeroToBeAbleCombineWithNon-LeasePayment"
                            PaymentNumber = LeasePaymentDetailForDueDateRel.LeasePaymentDetail.PaymentNumber
                        else
                            constraint (false)
                                "WhenCombineWithLeasePaymentIsSelectedThereMustBeALeasePaymentForTheSameDueDate"
                else
                    constraint (LeasePaymentDetailForPaymentNumberRel not exists
                    and LeasePaymentImportForPaymentNumberRel not exists)
                        "ThereIsALeasePaymentWithThisPaymentNumber<PaymentNumber>,CanOnlyHaveTheSamePaymentNumberIfSelectingCombineWithLeasePayment"               
            else
				constraint (Lease.Status.Unreleased)
                    "LeaseStatusMustBeUnreleasedToAddLeasePayments"
                TransientDateRange.BeginDate    = DueDate - DueDate day
                TransientDateRange.BeginDate    = TransientDateRange.BeginDate  + 1 day
				TransientDateRange.EndDate      = TransientDateRange.BeginDate + DueDate days in month
                TransientDateRange.EndDate      = TransientDateRange.EndDate - 1 day


                constraint (LeasePaymentDetailWithinDateRangeRel not exists
                and LeaseImportPaymentWithinDateRangeRel not exists)
                    "ThereCanOnlyBeOneLeasePaymentPerPayPeriod.ALeasePaymentAlreadyExistForThisPeriod"             
    
    Field Rules

        Company
            required

        DueDate
            required

        Lease
            required

        PaymentNumber
            required







        TaxableFlag
            default to "N"
            
        VendorAndRemitTo
        	required

        CombineWithLeasePayment
            constraint (ExecutoryCostCode entered)
                "WhenCombineWithLeasePaymentCheckedNonLeaseComponentCodeMustBeEntered"



        DoNotCreateAPInvoice
            if (DoNotCreateAPInvoice)
                constraint (LeasePaymentsRel.LeaseInvoiceAccrualAccount entered)
                    "ToNotCreatePayablesInvoicesTheLeaseInvoiceAccrualAccountMustBeEntered"

	Relations
        LeasePaymentImportRel
            one-to-many relation to LeasePaymentImport
            Field Mapping uses Set3
                related.LeasePaymentImport.RunGroup 	= LeasePaymentImport.RunGroup

        LeasePaymentDetailForDueDateRel
			one-to-many relation to LeasePaymentDetail
            Field Mapping uses ByDueDate
                related.Company                 = Company
                related.Lease                   = Lease
                related.Vendor                  = VendorAndRemitTo.Vendor
                related.DueDate                 = DueDate
            Instance Selection
            	where (related.ResidualPayment          = false
            	and    related.PurchaseOptionPayment    = false
            	and    related.LeasePaymentDetail.ExecutoryCostCode not entered)

        LeasePaymentDetailForPaymentNumberRel
			one-to-many relation to LeasePaymentDetail
            Field Mapping uses ByDueDate
                related.Company                 = Company
                related.Lease                   = Lease
                related.Vendor                  = VendorAndRemitTo.Vendor
                related.DueDate                 = DueDate
            Instance Selection
            	where (related.LeasePaymentDetail.PaymentNumber = PaymentNumber
                and    related.LeasePaymentDetail.ExecutoryCostCode = ExecutoryCostCode)

        LeasePaymentImportForDueDateRel
			one-to-many relation to LeasePaymentImport
            Field Mapping uses ByDueDate
                related.Company                 = Company
                related.Lease                   = Lease
                related.VendorAndRemitTo.Vendor = VendorAndRemitTo.Vendor
                related.DueDate                 = DueDate
       		Instance Selection
                where (related.ExecutoryCostCode not entered)   

        LeasePaymentImportForPaymentNumberRel
			one-to-many relation to LeasePaymentImport
            Field Mapping uses ByDueDate
                related.Company                 = Company
                related.Lease                   = Lease
                related.VendorAndRemitTo.Vendor = VendorAndRemitTo.Vendor
                related.DueDate                 = DueDate
       		Instance Selection
                where (related.PaymentNumber    = PaymentNumber
                and    related.ExecutoryCostCode = ExecutoryCostCode)

		LeaseExecutoryCostCodeRel
			one-to-one relation to LeaseExecutoryCostCode
			Field Mapping uses Set1
				related.Company								= Company
				related.Lease								= Lease
				related.ExecutoryCostCode					= ExecutoryCostCode

		LeasePaymentDetailWithinDateRangeRel
			one-to-many relation to LeasePaymentDetail
            Field Mapping uses ByDueDate
                related.Company					= Company
                related.Lease                   = Lease
                related.Vendor                  = VendorAndRemitTo.Vendor
       		Instance Selection
                where (related.DueDate within TransientDateRange
                and    related.LeasePaymentDetail.ExecutoryCostCode not entered)

		LeaseImportPaymentWithinDateRangeRel
			one-to-many relation to LeasePaymentImport
            Field Mapping uses ByDueDate
                related.Company					= Company
                related.Lease                   = Lease
                related.VendorAndRemitTo.Vendor = VendorAndRemitTo.Vendor
       		Instance Selection
                where (related.DueDate within TransientDateRange
                and    related.ExecutoryCostCode not entered)

        LeasePaymentsRel
            one-to-one relation to LeasePayment
            Field Mapping uses symbolic key
            	related.Company		= Company
                related.Lease 		= Lease
                related.Vendor   	= Lease.Lessor

	Actions
		Create is a Create Action
            Action Rules
                include CreateAndUpdateRules

		Update is an Update Action
            Action Rules
                include CreateAndUpdateRules
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted		
