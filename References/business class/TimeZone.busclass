TimeZone is a BusinessClass
    owned by la
    prefix is tz
    contains environment data
	disable data area copy
		preserve target data
    stored in environment

    Ontology
        symbolic key is TimeZone

    Patterns
    	disable EffectiveDated
    	disable AsOfDateProcessing
    	implements ExtendsExternalData
    		using com.lawson.rdtech.framework.TimeZoneStorage
    	implements ConfigurationMetadata
    	
	Persistent Fields
    	DisplayName			 is Alpha 80
    		translatable    
    	InUse				 is Boolean 
    	CurrentOffsetDisplay is Alpha 12
		OffsetShortName		 is Alpha 4
    	
  	Local Fields
  		LocalTimeZone is a TimeZone
  	Transient Fields
		UTCTimeZone is a TimeZone
			derive value from TimeZoneUTCRel.TimeZone	
			
  	Derived Fields
  		UTCMessageField is a MessageField
  			restricted
  			"UTC"
  			
  		DerivedOffsetDisplayName is a DerivedField
  			type is Alpha size 50
  			return "(" + UTCMessageField + OffsetDisplay +") " + TimeZone
  			
  		OffsetDisplay is a NativeField
  			type is Alpha size 12
  			
  		ShortName is a NativeField
  			type is Alpha size 4
  			
    	RawOffset is a NativeField
    	 	type is Numeric size 8  
    	
    	CurrentOffset is a NativeField
    		type is Numeric size 8  
		
		UsesDST is a NativeField
			type is Boolean
		
		CurrentInStdTime is a NativeField
			type is Boolean
  		
  		RawOffsetSeconds is a DerivedField
  			type is Numeric size 5
  			return (RawOffset / 1000)
  			
  		RawOffsetMinutes is a DerivedField
  			type is Numeric size 4
  			return ((RawOffset / 1000) / 60)
  		
  		RawOffsetHours is a DerivedField
  			type is Numeric size 2
  			return (((RawOffset / 1000) / 60) / 60) 
  			
		CurrentOffsetSeconds is a DerivedField
  			type is Numeric size 5
  			return (CurrentOffset / 1000)
  			
  		CurrentOffsetMinutes is a DerivedField
  			type is Numeric size 4
  			return ((CurrentOffset / 1000) / 60)
  		
  		CurrentOffsetHours is a DerivedField
  			type is Numeric size 2
  			return (((CurrentOffset / 1000) / 60) / 60) 
  			
  		CurrentInDST is a DerivedField
  			type is Boolean
  			if (CurrentInStdTime)
  				return false
  			else
	  			return true  			    	
	  		



	  		
	  	IsCorporateTimeZone is a NativeField
	  		type is Boolean
	  		default label is "CorporateTimeZone"
	  		
	  	DefaultTimeZone is a NativeField
	  		type is Alpha size 10
	  	
	  	DefaultTimeZoneOffset is a NativeField
	  		type is Alpha size 10
	  	
	  	UserTimeZoneOffset is a NativeField
	  		type is Alpha size 10
	  	
	  	CurrentTimeZoneOffset is a NativeField
	  		type is Alpha size 10
	  	
	  	DefaultTimeZoneName is a NativeField
    		type is Alpha size 80
	  	
	  	EffectiveDateTimeZoneBasis is a NativeField
    		type is Alpha size 10
    		
    	SystemCurrentCorporateDate is a DerivedField	
            type is Date
            return system current corporate date

        CurrentCorporateDate is a DerivedField	
            type is Date
            return current corporate date		
		
		CurrentTimeZone is a NativeField
    		type is Alpha size 30
		
		CurrentTimeZoneName is a NativeField
    		type is Alpha size 80

		UserTimeZone is a NativeField
    		type is Alpha size 30

		UserTimeZoneName is a NativeField
    		type is Alpha size 80

		SystemCurrentDate is a DerivedField
			type is Date
			return system current date		

		CurrentTime is a DerivedField
			type is Time
			return current time				

		CurrentCorporateTime is a DerivedField
			type is Time
			return current corporate time

		UserTimeStamp is a DerivedField
			type is TimeStamp
			return system current timestamp 

		UTCDate is a DerivedField
			type is Date
			return UserTimeStamp date using first TimeZoneUTCRel.TimeZone
	  
	  	UTCDateTime is a DerivedField
			type is TimeStamp
			return system current timestamp
	  	
	  	TimeZoneFormat is a DerivedField
	  		type is Alpha size 12
	  		return "GMT+/-HH:MM"
	
		Configuration is a NativeField
			type is Boolean
			
		Modified is a NativeField
			type is Boolean
		
		UTCTime is a NativeField
			type is Alpha size 12	
			
	Field Rules
		CurrentOffsetDisplay
           	default to OffsetDisplay
        OffsetShortName
        	default to ShortName   



		InUse
			default InUse to IsCorporateTimeZone

	Sets
		ByDisplayName
			Sort Order
				DisplayName
				TimeZone
				
		ByShortNameOffset
			Sort Order
				CurrentOffsetDisplay		
				OffsetShortName
				TimeZone

		
	Relations
		DefaultRel
			one-to-one relation to ConfigurationParameter
				Field Mapping uses ConfigurationParameter symbolic key 
					related.ConfigurationParameter.ConfigurationID = "TimeZone"
					related.ConfigurationParameter.Name            = "Default"
		
		TimeZoneBasisConfigurationRel
			one-to-many relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID		= "framework"
				related.ConfigurationParameter.Name					= "effectivedate.timezone.basis"
			Instance Selection
				where (related.ConfigurationParameter.ConfigurationID entered)
		
		TimeZoneUTCRel
		    one-to-many relation to TimeZone
		    Field Mapping uses symbolic key
		    Instance Selection
		        where (related.TimeZone = "UTC")		
    Actions
    	
    	Invalidate is an Instance Action
    		restricted
    		     		
		Create is a Create Action
			Parameters
				NewTimeZone is a TimeZone
				DisplayName is Alpha size 80
				
			Action Rules
				constraint (NewTimeZone matches "(GMT)[-+][0-9]([0-9])?(:[0-9][0-9])?")
					"TimeZoneMustBeInTheForm_'<this instance.TimeZoneFormat>'"

				invoke CreateInternal this instance
					invoked.TimeZone 	= NewTimeZone
					invoked.DisplayName = DisplayName
					invoked.InUse		= true
					
			Exit Rules
				invoke Invalidate
		
		CreateInternal is a Create Action
   			restricted
		
		MakeDefault is an Instance Action
			manual update
			Exit Rules
				invoke Invalidate

		Update is an Update Action			
			Exit Rules
				invoke Invalidate
						
		InternalUpdate is an Update Action
			restricted
			
		Delete is a Delete Action
			valid when Configuration
			Exit Rules
				invoke Invalidate			

		ResetConfiguration is an Instance Action
			valid when Modified
			Action Rules
				invoke Delete
				
		SetAllInUse is a Set Action
			run in foreground
			
			Action Rules
				Instance Rules
					InUse = true
					invoke InternalUpdate
			
				Set Rules
					Exit Rules
						invoke Invalidate
					
		DisableAllInUse is a Set Action
			run in foreground
			
			Action Rules
				Instance Rules
					InUse = false
					invoke InternalUpdate
					
				Set Rules
					Exit Rules
						invoke Invalidate
				
