MobileSupplyChainDeliveryTicket is a BusinessClass
    owned by mscm
    prefix is DLVT
    sql name is "MSCMDeliveryTicket"

    Ontology
        symbolic key is MobileSupplyChainDeliveryTicket

    Patterns
        implements StaticJava
        disable AuditIndex
        implements ContextualParent

    Persistent Fields
        DeliveryType
        OriginatingTransaction      is BusinessObjectReference
            protected
        InforTrackingNumber         is a TrackingNumber
        DeliverToCompanyLocation    is a MSCMCompanyAndLocation
        ReceivedBy                  is an Actor
        Status                      is AlphaUpper size 1
            States
                CreationInProgress  value is blank
                ReadyToDeliver      value is "R"
                Delivered           value is "D"
                PartiallyDelivered  value is "P"
        NumberOfLines
        DocumentNumber              is like MiscellaneousReceipt 
        PurchaseOrder               is a MiscellaneousReceiptPurchaseOrder
        DeliverTo

    Transient Fields
        TransientMobileSupplyChainDeliveryEvent         is a MobileSupplyChainDeliveryEvent
    
    Derived Fields
        DerivedVendor is a DerivedField
            type is like Vendor
            default label is "Vendor"
            if (DeliveryType.MiscellaneousReceipt)
                return OriginatingTransaction(MiscellaneousReceipt).Supplier
            else
            if (DeliveryType.PurchaseOrderReceipt)
                return OriginatingTransaction(PurchaseOrderReceipt).Vendor

            return "-"

        DerivedVendorName is a DerivedField
            type is like VendorName
            default label is "Vendor"
            if (DeliveryType.MiscellaneousReceipt)
                return OriginatingTransaction(MiscellaneousReceipt).Supplier.VendorName
            else
            if (DeliveryType.PurchaseOrderReceipt)
                return OriginatingTransaction(PurchaseOrderReceipt).Vendor.VendorName

            return "-"

        DerivedMiscellaneousReceiptNote is a DerivedField
            type is like DeliveryNote
            default label is "DeliveryNoteCode"
            if (DeliveryType.MiscellaneousReceipt)
                return OriginatingTransaction(MiscellaneousReceipt).MiscellaneousReceipt.DeliveryNote

            return blank

        DerivedMiscellaneousReceiptNoteCode is a DerivedField
            type is like DeliveryNoteCode
            default label is "DeliveryNoteCode"
            if (DeliveryType.MiscellaneousReceipt)
                return OriginatingTransaction(MiscellaneousReceipt).MiscellaneousReceipt.DeliveryNoteCode

            return blank
            
        DerivedTimeSubmitted is a DerivedField
            type is TimeStamp
            default label is "TimeSubmitted"
            if (DeliveryType.DockLog)
                return OriginatingTransaction(DockLog).DockLog.TimeSubmitted
            else 
            if (DeliveryType.MiscellaneousReceipt)
                return OriginatingTransaction(MiscellaneousReceipt).MiscellaneousReceipt.TimeSubmitted
            else
            if (DeliveryType.Shipment)
                return OriginatingTransaction(WarehouseShipment).WarehouseShipment.DerivedLastTimeSubmitted
            else
            if (DeliveryType.PurchaseOrderReceipt)
                return OriginatingTransaction(PurchaseOrderReceipt).PurchaseOrderReceipt.ReceivedDate
            return blank

        DerivedPrintingDate is a DerivedField
            type is TimeStamp
            default label is "PrintingDate"
            return current timestamp

        DerivedRequestedDate is a DerivedField
            type is TimeStamp
            default label is "RequestedDate"
            return MobileSupplyChainDeliveryLine set.DerivedRequestedDate

        DerivedBlankDisplay is a MessageField
            "-"
            
        DerivedRequestingLocationAddress1 is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedAddressLine1

        DerivedRequestingLocationAddress2 is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedAddressLine2

        DerivedRequestingLocationAddress3 is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedAddressLine3

        DerivedRequestingLocationAddress4 is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedAddressLine4

        DerivedRequestingLocationMunicipality is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedMunicipality

        DerivedRequestingLocationStateProvince is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedStateProvince

        DerivedRequestingLocationCountry is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedCountry
            
        DerivedRequestingLocationPostalCode is a DerivedField
            type is Text
            return DeliverToCompanyLocation.MobileSupplyChainLocation.DerivedPostalCode

        ProcessDeliveryDocumentLineXML is a DerivedField
            type is XMLDocument
            initialize LocalOriginatingLineNumber
            if (MobileSupplyChainDeliveryLine set exists)
                for each MobileSupplyChainDeliveryLine set
                    if (each.OriginatingLineNumber != LocalOriginatingLineNumber)
                        LocalOriginatingLineNumber  = each.OriginatingLineNumber
                        
                        ProcessDeliveryDocumentLineXML += template.DeliveryTicketDocumentLine_ST document for each

            else
                return ""

        DerivedRequesterName is a DerivedField
            type is like Name
            return MobileSupplyChainDeliveryLine set.DerivedRequesterName

        DerivedRequisition is a DerivedField
            type is like Requisition
            return MobileSupplyChainDeliveryLine set.Requisition

        DerivedPrinter is a DerivedField
            type is like MobileSupplyChainPrinter
            if (DeliveryType.Shipment)
                if (LocalPrintDocumentType.Report)
                    return OriginatingTransaction(WarehouseShipment).DerivedReportPrinter
                else
                if (LocalPrintDocumentType.Label)
                    return OriginatingTransaction(WarehouseShipment).DerivedLabelPrinter
            else
            if (DeliveryType.MiscellaneousReceipt)
                if (LocalPrintDocumentType.Report)
                    return OriginatingTransaction(MiscellaneousReceipt).ReportPrinter
                else
                if (LocalPrintDocumentType.Label)
                    return OriginatingTransaction(MiscellaneousReceipt).LabelPrinter

        ProcessDeliveryRequisitionCommentXML is a DerivedField 
            type is XMLDocument
            return ""
        
        DerivedReceivedByName is a DerivedField
            type is Text
            return ReceivedBy.PersonName.GivenName + " " + ReceivedBy.PersonName.FamilyName 
        
 		DerivedLabelPrintDataFile is a DerivedField
			type is XMLDocument
			restricted
            if (DeliveryType.Shipment)
				DerivedLabelPrintDataFile = template.PickingDeliveryLabel_ST document for this instance
				return DerivedLabelPrintDataFile
			else
            if (DeliveryType.PurchaseOrderReceipt)
				DerivedLabelPrintDataFile = template.PurchaseOrderDeliveryLabel_ST document for this instance
				return DerivedLabelPrintDataFile
        
        DerivedCarrierTrackingNumber is a DerivedField
            type is like TrackingNumber
            if (DeliveryType.PurchaseOrderReceipt)
                return  MobileSupplyChainDeliveryUnit set.CarrierTrackingNumber

        ProcessDeliveryTicketCommentXML is a DerivedField
            type is XMLDocument
            if (DeliveryType.PurchaseOrderReceipt)
                for each PurchaseOrderReceiptCommentRel
                    ProcessDeliveryTicketCommentXML += template.DeliveryTicketDocumentComment_ST document for each
            else
            if (DeliveryType.Shipment)
                for each ShipmentRequisitionCommentRel
                    ProcessDeliveryTicketCommentXML += template.DeliveryTicketDocumentComment_ST document for each
            else
                return ""

    Conditions        
        IsDockLog
            when (DeliveryType.DockLog)

        IsReceipt
            when (DeliveryType.PurchaseOrderReceipt
            or    DeliveryType.MiscellaneousReceipt)

        IsShipment
            when (DeliveryType.Shipment)

        IsShipmentOrPOReceipt
            when (DeliveryType.PurchaseOrderReceipt
            or    DeliveryType.Shipment)

        IsPrintDeliveryReport
            when (((MobileSupplyChainLocation.ActualPrintPickingOptions   = MobileSupplyChainPrintingOptions.PrintDeliveryLabelAndDocument
            or     MobileSupplyChainLocation.ActualPrintPickingOptions   = MobileSupplyChainPrintingOptions.PrintDeliveryDocumentOnly)
            and    DeliveryType.Shipment)
            or   ((MobileSupplyChainConfigurationRel.MiscellaneousReceivingPrintDeliveryTicket = MobileSupplyChainConfigurationRel.MiscellaneousReceivingPrintDeliveryTicket.Enable
            or     MobileSupplyChainConfigurationRel.MiscellaneousReceivingPrintDeliveryTicket = MobileSupplyChainConfigurationRel.MiscellaneousReceivingPrintDeliveryTicket.EnabledAndChecked)
            and    DeliveryType.MiscellaneousReceipt)
            or    (MobileSupplyChainLocation.ActualPrintPurchaseOrderReceivingTicket
            and    DeliveryType.PurchaseOrderReceipt))

        IsPrintDeliveryLabel
            when (((MobileSupplyChainLocation.ActualPrintPickingOptions   = MobileSupplyChainPrintingOptions.PrintDeliveryLabelAndDocument
            or     MobileSupplyChainLocation.ActualPrintPickingOptions   = MobileSupplyChainPrintingOptions.PrintDeliveryLabelOnly)
            and    DeliveryType.Shipment)
            or   ((MobileSupplyChainConfigurationRel.MiscellaneousReceivingPrintDeliveryLabel = MobileSupplyChainConfiguration.MiscellaneousReceivingPrintDeliveryLabel.Enable
            or     MobileSupplyChainConfigurationRel.MiscellaneousReceivingPrintDeliveryLabel = MobileSupplyChainConfiguration.MiscellaneousReceivingPrintDeliveryLabel.EnabledAndChecked)
            and    DeliveryType.MiscellaneousReceipt)
            or    (MobileSupplyChainLocation.ActualPrintPurchaseOrderReceivingLabel
            and    DeliveryType.PurchaseOrderReceipt))
        
        IsTicketValidForPrint
            when (IsPrintDeliveryReport
            or    IsPrintDeliveryLabel)

        IsPrintLabelPerUnit
            when (DeliveryType.MiscellaneousReceipt)

    Field Rules
        OriginatingTransaction
            required
        
        InforTrackingNumber
            required

        DeliveryType
            required
        
        ReceivedBy
            if (not IsDockLog)
                required
        
        DeliverToCompanyLocation
            if (not IsDockLog)
                required

    Rule Blocks
        CreatePrinterJob
            invoke Create MobileSupplyChainPrinterJob
                invoked.FinanceEnterpriseGroup              = Company.FinanceEnterpriseGroup
                invoked.Company                             = Company
                invoked.MobileSupplyChainPrinter            = LocalPrinter
                invoked.TransactionType                     = LocalTransactionType
                invoked.PrintDataFile                       = LocalPrintDataFile

    Local Fields
        LocalVendor                     is a Vendor
        LocalOriginatingLineNumber      is like MiscellaneousReceiptLine
        LocalPrintDocumentType          is Alpha 2
            States
                Label                   value is "L"
                Report                  value is "R"
        LocalTransactionType            is a MobileSupplyChainPrintingTransactionType
        LocalPrintDataFile              is BinaryDocument
        LocalPrinter                    is like MobileSupplyChainPrinter

    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted

        UpdateStatus is an Instance Action
            restricted
            Local Fields
                LocalDeliveredLines is Numeric size 6
            Entrance Rules
                LocalDeliveredLines = instance count of DeliveredLinesRel

                if (LocalDeliveredLines > 0)
                    if (LocalDeliveredLines = NumberOfLines)
                        Status = Status.Delivered
                    else
                        Status = Status.PartiallyDelivered

        CreateEvent is an Instance Action
            restricted
            Parameters
                PrmPerformedAction      is a PerformedAction
                PrmTimeSubmitted        is TimeStamp
                PrmEventCompanyLocation is a MSCMCompanyAndLocation
                PrmEmployeeID           is a EmployeeID
                PrmFirstName            is a Name
                PrmLastName             is a Name
                PrmSignature            is BinaryDocument
                PrmPackageCount         is a PackageCount
                
            Local Fields
                LocalMobileSupplyChainDeliveryEventView         is a MobileSupplyChainDeliveryEvent view
            Action Rules
                invoke Create MobileSupplyChainDeliveryEvent
                    assign result to LocalMobileSupplyChainDeliveryEventView
                    invoked.Company                                         = Company
                    invoked.MobileSupplyChainLocation                       = MobileSupplyChainLocation
                    invoked.MobileSupplyChainDeliveryTicket                 = MobileSupplyChainDeliveryTicket
                    invoked.EventCompanyLocation.MobileSupplyChainCompany   = PrmEventCompanyLocation.MobileSupplyChainCompany
                    invoked.EventCompanyLocation.MobileSupplyChainLocation  = PrmEventCompanyLocation.MobileSupplyChainLocation
                    invoked.PerformedAction                                 = PrmPerformedAction
                    invoked.TimeSubmitted                                   = PrmTimeSubmitted
                    invoked.EmployeeID                                      = PrmEmployeeID
                    invoked.FirstName                                       = PrmFirstName
                    invoked.LastName                                        = PrmLastName
                    if (PrmSignature entered)
                        invoked.Signature.Title                             = "delivery-event-signature.jpg"
                        invoked.Signature.File                              = PrmSignature
                        invoked.Signature.MimeType                          = "image/jpeg"         
                    invoked.PackageCount                                    = PrmPackageCount

                TransientMobileSupplyChainDeliveryEvent         = LocalMobileSupplyChainDeliveryEventView.MobileSupplyChainDeliveryEvent
                
        CreateEventForAllLines is an Instance Action
            restricted
            Parameters
                PrmPerformedAction  is a PerformedAction
                PrmTimeSubmitted    is TimeStamp

            Local Fields
                LocalMobileSupplyChainDeliveryEventView is a MobileSupplyChainDeliveryEvent view

            Action Rules
                invoke Create MobileSupplyChainDeliveryEvent
                    assign result to LocalMobileSupplyChainDeliveryEventView
                    invoked.Company                                         = Company
                    invoked.MobileSupplyChainLocation                       = MobileSupplyChainLocation
                    invoked.MobileSupplyChainDeliveryTicket                 = MobileSupplyChainDeliveryTicket
                    invoked.EventCompanyLocation.MobileSupplyChainCompany   = Company
                    invoked.EventCompanyLocation.MobileSupplyChainLocation  = MobileSupplyChainLocation
                    invoked.PerformedAction                                 = PrmPerformedAction
                    invoked.TimeSubmitted                                   = PrmTimeSubmitted

                invoke CreateAllEventLines MobileSupplyChainDeliveryLine
                    invoked.PrmMobileSupplyChainCompany         = Company
                    invoked.PrmMobileSupplyChainLocation        = MobileSupplyChainLocation
                    invoked.PrmMobileSupplyChainDeliveryTicket  = MobileSupplyChainDeliveryTicket
                    invoked.PrmMobileSupplyChainDeliveryEvent   = LocalMobileSupplyChainDeliveryEventView.MobileSupplyChainDeliveryEvent

        Print is an Instance Action
            
            valid when (IsTicketValidForPrint)
            Parameters
                PrmReportPrinter                    is a MobileSupplyChainPrinter
                    default label is "ReportPrinter"
                PrmLabelPrinter                     is a MobileSupplyChainPrinter
                    default label is "LabelPrinter"
                PrmDefaultValue                     is Boolean
                    default label is "DefaultValue"
            Parameter Rules
                PrmReportPrinter
                    initial value is MobileSupplyChainLocation.DerivedDefaultMobileSupplyChainReportPrinter
                    if (PrmDefaultValue)
                        default to MobileSupplyChainLocation.DerivedDefaultMobileSupplyChainReportPrinter

                    if (IsPrintDeliveryReport
                    and not IsPrintDeliveryLabel)
                        required

                PrmLabelPrinter
                    initial value is MobileSupplyChainLocation.DerivedDefaultMobileSupplyChainLabelPrinter
                    if (PrmDefaultValue)
                        default to MobileSupplyChainLocation.DerivedDefaultMobileSupplyChainLabelPrinter
                            
                    if (IsPrintDeliveryLabel
                    and not IsPrintDeliveryReport)     
                        required
                
            Action Rules

                if (IsTicketValidForPrint)
                    constraint (PrmReportPrinter entered 
                    or PrmLabelPrinter entered)
                        "PleaseSelectA_ReportOr_LabelPrinter"

                if (PrmReportPrinter entered
                and IsPrintDeliveryReport)

                    LocalPrintDocumentType  = LocalPrintDocumentType.Report
                
                    LocalTransactionType    = MobileSupplyChainPrintingTransactionType.PrintForDelivery
                    LocalPrintDataFile      = template.DeliveryTicketDocument_ST document for this instance
                    LocalPrinter            = PrmReportPrinter

                    include CreatePrinterJob
                
                if (PrmLabelPrinter entered
                and IsPrintDeliveryLabel)

                    LocalPrintDocumentType  = LocalPrintDocumentType.Label
                    LocalPrinter            = PrmLabelPrinter
                    LocalTransactionType    = MobileSupplyChainPrintingTransactionType.LabelManagement
                    LocalPrintDataFile      = DerivedLabelPrintDataFile

                    if (IsPrintLabelPerUnit)
                        for each MobileSupplyChainDeliveryUnit set
                            invoke PrintDeliveryLabel each
                                invoked.PrmLabelPrinter     = LocalPrinter
                    else    
                        include CreatePrinterJob
            
    Relations
        DeliveredLinesRel
            one-to-many relation to MobileSupplyChainDeliveryLine
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.MobileSupplyChainLocation       = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket = MobileSupplyChainDeliveryTicket
            Instance Selection
                where (related.IsDelivered)

        MobileSupplyChainDeliveryNoteRel
            one-to-many relation to MobileSupplyChainDeliveryNote
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.MobileSupplyChainLocation       = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket = MobileSupplyChainDeliveryTicket
            Instance Selection
                where (related.MobileSupplyChainDeliveryLine    = 0)

        MobileSupplyChainPurchaseOrderReceiptRel
            one-to-many relation to MobileSupplyChainPurchaseOrderReceipt
            Field Mapping uses symbolic key
                related.Company                     = Company
            Instance Selection
                where (related.PurchaseOrderReceipt = OriginatingTransaction(PurchaseOrderReceipt).PurchaseOrderReceipt)
        
        MobileSupplyChainConfigurationRel
            one-to-one relation to MobileSupplyChainConfiguration
            Field Mapping uses symbolic key
                related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup

        ShipmentRequisitionCommentRel
            one-to-many relation to RequisitionComment
            Field Mapping uses part of key
                related.Company             = DeliverToCompanyLocation.MobileSupplyChainCompany
                related.Requisition         = MobileSupplyChainDeliveryLine set.Requisition
        
        PurchaseOrderReceiptCommentRel
            one-to-many relation to PurchaseOrderReceiptComment
            Field Mapping uses part of key
                related.Company                 = Company
                related.PurchaseOrderReceipt    = DocumentNumber
            Instance Selection
                where (related.CommentType.PrintOnInternalDocuments
                or     related.CommentType.PrintOnDeliveryTicket)
