ReceivingBackOrders is a BusinessClass
	owned by po
	prefix is RBOH
	sql name is RecBackOrdHdr
	classic name is RBOHDR
	
	Ontology
		symbolic key is ReceivingBackOrders
		
	Patterns
		implements StaticJava
	
	Persistent Fields
		SourceDocumentOrigin
		OperatingCompany 			is like InventoryCompany
		SourceDocumentNumeric
		SourceDocumentAlpha
		SourceDocumentLineNumber 	is a LineNumber
		PurchaseOrderLine
		RequestingLocation			is like RequestingLocation
		RunBatchID 					is an IDMPID
		CurrentUser					is like Actor
			default label is "User"
	
	Derived Fields
		DerivedRequester is a DerivedField
			type is Alpha size 13
			if (PurchaseOrderLineSourceRel.Requester entered)
				return PurchaseOrderLineSourceRel.Requester
			else
				return blank
				
		DerivedRequesterName is a DerivedField
			type is Alpha 230
			if (PurchaseOrderLineSourceRel.Requester entered)
				return PurchaseOrderLineSourceRel.Requester.Employee.PresentationNameSnapshot
			else
				return blank
				
		DerivedPurchaseFromName is a DerivedField
			type is like VendorName
			if (PurchaseOrder.PurchaseFromLocation entered)
				return PurchaseOrder.PurchaseFromLocation.VendorName
			else
				return blank
				
		DerivedRequestingLocationName is a DerivedField
			type is like Name
			if (PurchaseOrderLineSourceRel.RequestingLocation entered)
				return PurchaseOrderLineSourceRel.RequestingLocation.Name
			else
				return blank
	
		DerivedRequestingCompany is a DerivedField
			type is like Name
			if (OperatingCompany entered)
				return OperatingCompanyRel.Name
			else
				return blank

	Conditions
		DisplayRequisitionDetails
			when (SourceDocumentOrigin.Requisitions)	
	
	Relations
	
		PurchaseOrderLineSourceRel
			one-to-one relation to PurchaseOrderLineSource
			Field Mapping uses symbolic key
				related.Company												= Company
				related.PurchaseOrder										= PurchaseOrder
				related.PurchaseOrderLine									= PurchaseOrderLine
				related.PurchaseOrderLineSource.SourceDocumentOrigin		= SourceDocumentOrigin
				related.PurchaseOrderLineSource.OperatingCompany			= OperatingCompany
				related.PurchaseOrderLineSource.SourceDocumentNumeric		= SourceDocumentNumeric
				related.PurchaseOrderLineSource.SourceDocumentAlpha			= SourceDocumentAlpha
				related.PurchaseOrderLineSource.SourceDocumentLineNumber	= SourceDocumentLineNumber
				
		ReceivingBackOrderLinesRel is a ReceivingBackOrderLines set

		PurchaseOrderCommentsForIDMRel 
			one-to-many relation to PurchaseOrderComment
    		Field Mapping uses ByCommentType
    			related.Company		  = Company
    			related.PurchaseOrder = PurchaseOrder
    		Instance Selection
    			where (related.CommentType.PrintOnInternalDocuments
    			or     related.CommentType.PrintOnDeliveryTicket)
    	
		OperatingCompanyRel
			one-to-one relation to InventoryCompany
			Field Mapping uses symbolic key
				related.Company												= OperatingCompany 
	
	Field Rules
	
	Sets					
		Set1
            indexed
            Sort Order
                Company
                PurchaseOrder
                RequestingLocation
                ReceivingBackOrders
	
		ByRunBatchID
			Sort Order
				Company
				CurrentUser
				RunBatchID
            	PurchaseOrder
            	RequestingLocation
				ReceivingBackOrders

		BySourceDocument
			Sort Order
				Company
				CurrentUser
				RunBatchID
				SourceDocumentOrigin
				SourceDocumentNumeric
				PurchaseOrder
				RequestingLocation
				ReceivingBackOrders
	
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted

		DeleteReceivingBackOrders is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCompany	  	is like PurchasingCompany
				PrmActor 		is like Actor
				PrmRunBatchID	is like IDMPID

			Instance Selection
				where (Company 		= PrmCompany 
				and	   CurrentUser 	= PrmActor
				and	   RunBatchID 	= PrmRunBatchID)

			Action Rules			
				Instance Rules
					invoke Delete
										
