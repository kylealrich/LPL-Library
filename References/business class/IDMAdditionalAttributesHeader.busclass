IDMAdditionalAttributesHeader is a BusinessClass 
	owned by idm
	prefix is IDMAH

    		
	Ontology
		symbolic key is IDMAdditionalAttributesHeader

    Patterns
        implements StaticJava
	Persistent Fields
        IDMGenerateDocumentTypes
		BusinessClass
		Description
        EntityModelDescription          is a Description
		ActivateAdditionalAttributes	is Boolean
		Attachment
		Status			  				is Numeric 1
			States
				NotImported 	value is 1
				Imported    	value is 2
        Application       				is Numeric 1
            States
                Financials       value is 1
                SupplyManagement value is 2

	Local Fields
		IDMXMLDefinition
        LocalAttributeExistsInIDM   is Boolean
        LocalReturnDocumentType     is Text
        LocalExecute                is Boolean

	Field Rules
		IDMGenerateDocumentTypes
			required
        BusinessClass
            required
		Application
			if (IDMGenerateDocumentTypes < 500)
				default to 1
			else
				default to 2

    Rule Blocks
        SetIDMAdditionalAttributesHeader
            if (IDMGenerateDocumentTypes.IntercompanyBillingInvoice)
                IDMAdditionalAttributesHeader = "FSM_IntercompanyInvoice"
                BusinessClass = "IntercompanyBillingDocumentHeader"
            else
            if (IDMGenerateDocumentTypes.BillingInvoice)
                IDMAdditionalAttributesHeader = "FSM_BillingInvoice"
                BusinessClass = "BillingInvoice"
            else
            if (IDMGenerateDocumentTypes.ProjectContractInvoice)
                IDMAdditionalAttributesHeader = "FSM_ProjectContractInvoice"
                BusinessClass = "ProjectContractInvoice"









            else
            if (IDMGenerateDocumentTypes.ReceivablesStatement)
                IDMAdditionalAttributesHeader = "FSM_ReceivablesStatement"
                BusinessClass = "StatementPrintHeader"
            else
            if (IDMGenerateDocumentTypes.PaymentReceipt)
                IDMAdditionalAttributesHeader = "FSM_PaymentReceipt"
                BusinessClass = "ReceivablePayment"                
            else
            if (IDMGenerateDocumentTypes.VendorStatement)
                IDMAdditionalAttributesHeader = "FSM_VendorStatement"
                BusinessClass = "Vendor"






            else
            if (IDMGenerateDocumentTypes.PaymentRequestNotice)
                IDMAdditionalAttributesHeader = "FSM_ARPaymentRequestNotice"
                BusinessClass = "CompanyCustomer"






            else
            if (IDMGenerateDocumentTypes.PayablesInvoice)
                IDMAdditionalAttributesHeader = "FSM_PayablesInvoice"
                BusinessClass = "PayablesInvoice"
            else
            if (IDMGenerateDocumentTypes.EscheatmentNotice)
                IDMAdditionalAttributesHeader = "FSM_EscheatmentNotice"
                BusinessClass = "EscheatmentNoticeIDMLines"
            else
            if (IDMGenerateDocumentTypes.SubleaseBillingInvoice)
                IDMAdditionalAttributesHeader = "FSM_SubleaseBillingInvoice"
                BusinessClass = "SubleaseBillingInvoice"
            else
            if (IDMGenerateDocumentTypes.ReceivableInvoice)
                IDMAdditionalAttributesHeader = "FSM_ReceivableInvoice"
                BusinessClass = "ReceivableInvoice"
            else
            if (IDMGenerateDocumentTypes.ReceivingDeliveryAndPutaway)
                IDMAdditionalAttributesHeader = "FSM_ReceivingDeliveryAndPutaway"
                BusinessClass = "PurchasingCompany"









            else
            if (IDMGenerateDocumentTypes.LetterOfRetention)
                IDMAdditionalAttributesHeader = "FSM_LetterOfRetention"
                BusinessClass = "CashPaymentWithholding"
            else
            if (IDMGenerateDocumentTypes.Recipe)
                IDMAdditionalAttributesHeader = "FSM_Recipe"
                BusinessClass = "Recipe"
            else
            if (IDMGenerateDocumentTypes.KitchenOrder)
                IDMAdditionalAttributesHeader = "FSM_KitchenOrder"
                BusinessClass = "KitchenOrder"
            else
            if (IDMGenerateDocumentTypes.ContractDocument)
                IDMAdditionalAttributesHeader = "FSM_ContractDocument"
                BusinessClass = "Contract"
            else
            if (IDMGenerateDocumentTypes.ContractRedlineDocument)
                IDMAdditionalAttributesHeader = "FSM_ContractRedlineDocument"
                BusinessClass = "Contract"
            else
            if (IDMGenerateDocumentTypes.Requisition)
                IDMAdditionalAttributesHeader = "FSM_Requisition"
                BusinessClass = "Requisition"
            else
            if (IDMGenerateDocumentTypes.BillOfLading)
                IDMAdditionalAttributesHeader = "FSM_BillOfLading"
                BusinessClass = "BillOfLading"
            else
            if (IDMGenerateDocumentTypes.PurchaseOrder)
                IDMAdditionalAttributesHeader = "FSM_PurchaseOrder"
                BusinessClass = "PurchaseOrder"
	Conditions
		IDMAdditionalAttributesHeaderExists
			when (IDMAdditionalAttributesHeader entered)

        ValidForActivation
            restricted
            when (not ActivateAdditionalAttributes)
        
        ValidForDeactivation
            restricted
            when (ActivateAdditionalAttributes)

        DuplicateAttributeNameExists
            restricted
            when (DuplicateAdditionalLineAttributeNameRel exists)

        AttributeNotExistInIDM
            restricted
            when (AttributeNotExistInIDMRel exists)

        DisplayErrorMessage
            restricted
            when (DuplicateAttributeNameExists 
            or    AttributeNotExistInIDM)

	Relations
		IDMAdditionalAttributesHeaderRel
            one-to-many relation to IDMAdditionalAttributesHeader
            Field Mapping uses symbolic key
				related.IDMAdditionalAttributesHeader = IDMAdditionalAttributesHeader

		IDMAdditionalAttributesLinesRel is an IDMAdditionalAttributesLines set

        IDMAdditionalAttributesLinesNotImportedRel is an IDMAdditionalAttributesLines set
            Instance Selection
                where (related.Status.NotImported
                or     related.ImportedAttributeNotExistInIDM)

        IDMAdditionalAttributesInIDMRel is an IDMAdditionalAttributesLines set

        DuplicateAdditionalLineAttributeNameRel is an IDMAdditionalAttributesLines set
            Instance Selection
                where (related.DuplicateAttributeNameExists)

        AttributeNotExistInIDMRel is an IDMAdditionalAttributesLines set
            Instance Selection
                where (related.ImportedAttributeNotExistInIDM)
                    
		IDMConfigurationRel
            one-to-one relation to IDMConfiguration
            Field Mapping uses symbolic key
                related.IDMConfiguration = actor.context.FinanceEnterpriseGroup


	Derived Fields
		DerivedEntityModelName is a DerivedField
			type is Alpha size 100
			return IDMAdditionalAttributesHeader

		DerivedEntityModelDesc is a DerivedField
			type is Alpha 100
			return EntityModelDescription

		DerivedEntityModelRoot is a DerivedField
			type is Boolean
			return true

		DerivedEntityModelVersionEnabled is a DerivedField
			type is Boolean
			return true

		DerivedEntityModelClassification is a DerivedField
			type is Numeric 1
			return 2

		DerivedEntityModelReprItem is a DerivedField
			type is Alpha 10
			return "filename"

        DerivedStatus  is a DerivedField
            type is Alpha 20
            if (Status.NotImported)
                return "Not Imported"
            else
                return "Imported"

        DerivedActivate  is a DerivedField
            type is Alpha 20
            if (ActivateAdditionalAttributes)
                return "Active"
            else
                return "Inactive"

        DeactivateMessage is a MessageField
            "DeactivatingTheseAdditionalAttributesWillStopPopulatingFutureGeneratedIDMDocuments.Continue?"
  
        ImportMessage is a MessageField
            "ThisActionWillImportAdditionalAttributesTo<IDMGenerateDocumentTypes>DocumentType.Continue?"
   
        ImportNoAttributesInIDM is a MessageField
            "CannotImport.MakeSureTheDocumentTypeExistInIDM." 

        DerivedErrorsExistMessage is a MessageField
            "Errors_Exist_-_See_lines_for_details"

 	Delete Rules
        constraint (IDMAdditionalAttributesLinesRel not exists)
            "CannotDelete.AdditionalAttributesLinesExist"
 
 
    Actions
        Delete is a Delete Action
            
        EntityInformation is an Instance Action
            restricted
            Parameters
				PrmName             is Alpha 100

		ImportToIDM is an Instance Action
            valid when (not DuplicateAttributeNameExists)
            completion message is "AdditionalAttributesSuccessfullyImportedToIDM."
			
			Entrance Rules
                invoke EntityInformation
                    invoked.PrmName = IDMAdditionalAttributesHeader
                confirmation required
                    "<ImportMessage>"
            Action Rules
				initialize IDMXMLDefinition
                IDMXMLDefinition.Namespace      = true
				IDMXMLDefinition.Busclass 		= reference to this instance
				IDMXMLDefinition.ListName 		= "IDMAdditionalAttributesList"
				IDMXMLDefinition.DocumentName	= "configuration"
				IDMXMLDefinition.OutputFileName	= "AdditionalAttributesFor" + IDMGenerateDocumentTypes

				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName			= "IDMAdditionalAttributesHeaderRel"
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName 			= "EntityModelsForIDM"
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1		= 1
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag				= "entityModels"
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag				= "entityModel"

				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName			= "IDMAdditionalAttributesLinesNotImportedRel"
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName 			= "AttrsForIDM"
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1		= 1
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection2		= 1
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag				= "attrs"
				IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag				= "attr"

				LocalExecute = IDMXMLDefinition.GenerateXML

				Attachment = IDMXMLDefinition.OutputAttachment
			Exit Rules
				invoke ImportConfigurationXML IDMConfigurationRel
					invoked.PrmXML = Attachment
				make transition to Imported
                invoke NotImported.MakeTransitionToImported IDMAdditionalAttributesLinesNotImportedRel


        Activate is an Instance Action
            valid when (ValidForActivation)
            Action Rules
                ActivateAdditionalAttributes = true
        
        
        Deactivate is an Instance Action
            valid when (ValidForDeactivation)
            Entrance Rules
                confirmation required
                    "<DeactivateMessage>"
            Action Rules
                ActivateAdditionalAttributes = false
        
        

	StateCycles
		IDMAdditionalAttributeCycle is a StateCycle
			state field is Status
				NotImported is a State
					Create is a Create Action
						Action Rules
                            include SetIDMAdditionalAttributesHeader
							Status = Status.NotImported
                            ActivateAdditionalAttributes = true
                            
			
					Update is an Update Action
						Action Rules
                            if (IDMGenerateDocumentTypes changed)
							    include SetIDMAdditionalAttributesHeader
					
				Imported is a State
					Update is an Update Action

                    MakeTransitionToNotImported is an Instance Action
                        restricted
                        Action Rules
                            make transition to NotImported
