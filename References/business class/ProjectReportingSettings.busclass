ProjectReportingSettings is a BusinessClass
	owned by Projects
	
	prefix is Repo
	
	Ontology
		symbolic key is ProjectReportingSettings
	
	Patterns
	
	Persistent Fields
    	Description				 
		ProjectStructure	
		Scenario	
		ReportingAccount        is a GeneralLedgerChartAccount
			delete ignored 
		ExcludeUndistributedRetainedEarnings is Boolean	
		SuppressZeroRows		is Boolean
		ReportingPeriod			is Numeric 1
			States
				LifeToDate      value is 1
				YearToDate      value is 2
				PeriodActivity  value is 3	
		ReportingDate			is a GeneralLedgerCalendarPeriod
			delete ignored
		ComparisonPeriodEndDate		is a GeneralLedgerCalendarPeriod
			delete ignored			
		PercentOfAvailableLimitRanges	is a ProjectProgressBarDecimalRanges 
      		translatable
		ReportingCurrency		is Numeric 1
			States
				Report1				value is 1
				Report2				value is 2
				Report3				value is 3
				Report4				value is 4
				Report5				value is 5
				Project				value is 6				
			
	Transient Fields	
		ReportingDate1			is a GeneralLedgerCalendarPeriod
			derive value from ReportingDate	
		ReportingDate2			is a GeneralLedgerCalendarPeriod
			derive value from ReportingDate
		ReportingDate3			is a GeneralLedgerCalendarPeriod			
			derive value from ReportingDate
		GeneralLedgerCalendar
			derive value from Scenario.Basis.GeneralLedgerCalendar
		CreatedFromCopy			is Boolean	
		
    Derived Fields		
		DisplayReportingCurrency is a DerivedField
			type is like Currency			
			if (ReportingCurrency.Project)
				return blank	
			if (ReportingCurrency.Report1)
				return FinanceEnterpriseGroup.ReportCurrencyOne			
			else
			if (ReportingCurrency.Report2)	
				return FinanceEnterpriseGroup.ReportCurrencyTwo			
			else
			if (ReportingCurrency.Report3)
				return FinanceEnterpriseGroup.ReportCurrencyThree			
			else
			if (ReportingCurrency.Report4)
				return FinanceEnterpriseGroup.ReportCurrencyFour			
			else
			if (ReportingCurrency.Report5)
				return FinanceEnterpriseGroup.ReportCurrencyFive			
		
		CurrencyMF is a MessageField
			restricted
			"Currency"

		BlankField is a DerivedField
			type is Alpha 1
			return blank
			
		CurrencyDisplayLabel is a ConditionalField
			type is Alpha size 8
			restricted
			if (IsProjectCurrency)
				BlankField
			else	
				CurrencyMF
		
	Local Fields
		LocalProjectReportingSettings is a ProjectReportingSettings
		LocalActor					  is like Actor	
		LocalActorGroup	              is an Employee group				

    Sets
    
	Field Rules
    	Description
		ProjectStructure
			required	
			default to actor.context.ProjectStructure
			initial value is actor.context.ProjectStructure
		Scenario	
			required
				"BudgetScenarioIsRequired"
			constraint (Scenario.HasPostingProject)
				"<FinanceEnterpriseGroup.ProjectLabel>DimensionIsRequiredOnBudgetScenario"	


		ReportingAccount






		ExcludeUndistributedRetainedEarnings
			constraint (ReportingAccount = blank)
				"ExcludeUndistributedRetainedEarningsMustBeFalseIfReportingAccountEntered"	
	
		ReportingPeriod
			required
			initial value is ReportingPeriod.LifeToDate
			default to ReportingPeriod.LifeToDate			
		ReportingDate
			required
				"PeriodEndDateIsRequired"	
		ComparisonPeriodEndDate	
			constraint (ReportingPeriod.YearToDate
			or          ReportingPeriod.PeriodActivity)
				"ComparisonPeriodEndDateIsNotValidForLifeToDateReportingPeriod"
						
		ReportingCurrency
			required
			initial value is ReportingCurrency.Report1
			default to ReportingCurrency.Report1
			if (ReportingCurrency.Report1)
				constraint (FinanceEnterpriseGroup.ReportCurrencyOne entered)
					"Report1CurrencyIsNotDefinedAsAnEnterpriseCurrencyOnFinanceEnterpriseGroup<FinanceEnterpriseGroup>"
			if (ReportingCurrency.Report2)
				constraint (FinanceEnterpriseGroup.ReportCurrencyTwo entered)
					"Report2CurrencyIsNotDefinedAsAnEnterpriseCurrencyOnFinanceEnterpriseGroup<FinanceEnterpriseGroup>"
			if (ReportingCurrency.Report3)
				constraint (FinanceEnterpriseGroup.ReportCurrencyThree entered)
					"Report3CurrencyIsNotDefinedAsAnEnterpriseCurrencyOnFinanceEnterpriseGroup<FinanceEnterpriseGroup>"
			if (ReportingCurrency.Report4)
				constraint (FinanceEnterpriseGroup.ReportCurrencyFour entered)
					"Report4CurrencyIsNotDefinedAsAnEnterpriseCurrencyOnFinanceEnterpriseGroup<FinanceEnterpriseGroup>"
			if (ReportingCurrency.Report5)
				constraint (FinanceEnterpriseGroup.ReportCurrencyFive entered)
					"Report5CurrencyIsNotDefinedAsAnEnterpriseCurrencyOnFinanceEnterpriseGroup<FinanceEnterpriseGroup>"
				
    Conditions
		IsProjectCurrency
			restricted		
			when (ReportingCurrency.Project)   
			
    	CubeOffline
    		when (AnalyticCubeOfflineRel exists)
    		
	Relations
		AnalyticCubeOfflineRel
			one-to-many relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
			Instance Selection
				where (related.BusinessClass = "GeneralLedgerTotal"
				and    related.Mode 		!= 0)

	    FinanceEnterpriseGroupAnalyticCubeRel
			one-to-one relation to AnalyticCube
	        Field Mapping uses AnalyticCubeSet
	        	related.BusinessClass = "FinanceEnterpriseGroup"
	        	
	    GeneralLedgerTotalAnalyticCubeRel
			one-to-one relation to AnalyticCube
	        Field Mapping uses AnalyticCubeSet
	        	related.BusinessClass = "GeneralLedgerTotal"
	        	
		NewProjectReportingSettingsRel
			one-to-one relation to ProjectReportingSettings
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	 = FinanceEnterpriseGroup
				related.Actor                    = LocalActor
				related.ProjectReportingSettings = LocalProjectReportingSettings
				
		ProjectReportingSettingsAllRel
			one-to-one relation to ProjectReportingSettingsAll
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	     = FinanceEnterpriseGroup
				related.ProjectReportingSettingsAll  = ProjectReportingSettings

		EmployeeRel
			one-to-many relation to Employee
			Field Mapping uses symbolic key
				related.HROrganization	= FinanceEnterpriseGroup.HROrganization
			Instance Selection 
				where (related.Employee within LocalActorGroup
				and    related.IsActive)				
		ProjectContractRel
			one-to-many relation to ProjectContract
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup                    = FinanceEnterpriseGroup

	Actions
		Create is a Create Action
			Entrance Rules
				if (!CreatedFromCopy)
					if (ReportingPeriod.YearToDate)
						ReportingDate = ReportingDate1
						ReportingDate2 = ReportingDate1
						ReportingDate3 = ReportingDate1										
					if (ReportingPeriod.LifeToDate)
						ReportingDate = ReportingDate2
						ReportingDate1 = ReportingDate2
						ReportingDate3 = ReportingDate2										
					if (ReportingPeriod.PeriodActivity)
						ReportingDate = ReportingDate3
						ReportingDate1 = ReportingDate3
						ReportingDate2 = ReportingDate3	
				
		Update is an Update Action
			Entrance Rules
				if (ReportingPeriod.YearToDate)
					ReportingDate = ReportingDate1
					ReportingDate2 = ReportingDate1
					ReportingDate3 = ReportingDate1										
				if (ReportingPeriod.LifeToDate)
					ReportingDate = ReportingDate2
					ReportingDate1 = ReportingDate2
					ReportingDate3 = ReportingDate2										
				if (ReportingPeriod.PeriodActivity)
					ReportingDate = ReportingDate3
					ReportingDate1 = ReportingDate3
					ReportingDate2 = ReportingDate3										

		Delete is a Delete Action
		
		CopyProjectReportingSettings is an Instance Action
			Parameters
				NewProjectReportingSettings			is like ProjectReportingSettings
				NewDescription						is like Description
				NewActor                            is an Actor
				NewProjectStructure					is a ProjectStructure
				NewScenario							is a Scenario	
				NewReportingAccount         		is a GeneralLedgerChartAccount
				NewExcludeUndistributedRetainedEarnings is Boolean
				NewSuppressZeroRows				    is Boolean
				NewReportingPeriod					is Numeric 1
					States
						LifeToDate      value is 1
						YearToDate      value is 2
						PeriodActivity  value is 3	
				NewReportingDate					is a GeneralLedgerCalendarPeriod
				NewComparisonPeriodEndDate			is a GeneralLedgerCalendarPeriod
				NewPercentOfAvailableLimitRanges	is a ProjectProgressBarDecimalRanges 
				NewReportingCurrency		is Numeric 1
					States
						Report1				value is 1
						Report2				value is 2
						Report3				value is 3
						Report4				value is 4
						Report5				value is 5
						Project				value is 6				
			Parameter Rules
				NewProjectReportingSettings
					required
					LocalProjectReportingSettings = NewProjectReportingSettings
					LocalActor                    = NewActor
					constraint (NewProjectReportingSettingsRel not exists)
						"<FinanceEnterpriseGroup.ProjectLabel>ReportingSettingsAlreadyExists"
				NewDescription
					required
					initial value is Description + "_Copy"
				NewActor
					required
					initial value is actor
					default to actor		
				NewProjectStructure
					required	
					default to ProjectStructure
					initial value is ProjectStructure
				NewScenario
					default to Scenario
					initial value is Scenario
					constraint (NewScenario.HasPostingProject)
						"<FinanceEnterpriseGroup.ProjectLabel>DimensionIsRequiredOnBudgetScenario"	


				NewExcludeUndistributedRetainedEarnings
					initial value is ExcludeUndistributedRetainedEarnings
					constraint (NewReportingAccount = blank)
						"NewExcludeUndistributedRetainedEarningsMustBeFalseIfNewReportingAccountEntered"
				NewSuppressZeroRows	
					initial value is SuppressZeroRows							
				NewReportingAccount
					initial value is ReportingAccount	


				NewReportingPeriod
					required
					initial value is ReportingPeriod
					default to ReportingPeriod
				NewReportingDate
					initial value is ReportingDate
					default to ReportingDate		
				NewComparisonPeriodEndDate
					initial value is ComparisonPeriodEndDate
				NewReportingCurrency
					required
					initial value is ReportingCurrency
					default to ReportingCurrency
				NewPercentOfAvailableLimitRanges	
					initial value is PercentOfAvailableLimitRanges
			Action Rules
				invoke Create
					fill in fields from this instance
					invoked.ProjectReportingSettings      = NewProjectReportingSettings
					invoked.Actor                         = NewActor
					invoked.Description				      = NewDescription
					invoked.ProjectStructure              = NewProjectStructure 
					invoked.Scenario				      = NewScenario
					invoked.ReportingAccount 		      = NewReportingAccount 
					invoked.ExcludeUndistributedRetainedEarnings = NewExcludeUndistributedRetainedEarnings   
					invoked.SuppressZeroRows			  = NewSuppressZeroRows   	
					invoked.ReportingPeriod			      = NewReportingPeriod
					invoked.ReportingDate			      = NewReportingDate		
					invoked.ComparisonPeriodEndDate		  = NewComparisonPeriodEndDate		
					invoked.PercentOfAvailableLimitRanges = NewPercentOfAvailableLimitRanges		 
					invoked.ReportingCurrency			  = NewReportingCurrency
					invoked.CreatedFromCopy				  = true				

		AssignProjectReportingSettingsToActor is an Instance Action
			Parameters
				NewProjectReportingSettings			is like ProjectReportingSettings
				NewDescription						is like Description
				NewActor                            is an Actor
			Parameter Rules
				NewActor
					required
					initial value is actor
					default to actor		
				NewProjectReportingSettings
					required
					initial value is ProjectReportingSettings
					default to ProjectReportingSettings		
					LocalProjectReportingSettings = NewProjectReportingSettings
					LocalActor                    = NewActor
					constraint (NewProjectReportingSettingsRel not exists)
						"<FinanceEnterpriseGroup.ProjectLabel>ReportingSettingsAlreadyAssignedToActor"
				NewDescription
					required
					initial value is Description 
					default to Description
			Action Rules
				invoke Create
					fill in fields from this instance
					invoked.ProjectReportingSettings      = NewProjectReportingSettings
					invoked.Actor                         = NewActor
					invoked.Description				      = NewDescription
					invoked.CreatedFromCopy				  = true				

		AssignReportingSettingsToEmployeeGroup is an Instance Action
			Parameters
				NewEmployeeGroup	is an Employee group
			Parameter Rules
				NewEmployeeGroup
					required
			Action Rules
        		invoke AssignReportingSettingsToEmployeeGroup Employee
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup        					
					invoked.PrmActor = ProjectReportingSettings.Actor                 
                    invoked.PrmEmployeeGroup = NewEmployeeGroup
                	invoked.PrmProjectReportingSettings = ProjectReportingSettings






















		DeleteReportingSettingsByEmployeeGroup is an Instance Action
			Parameters
				NewEmployeeGroup	is an Employee group
			Parameter Rules
				NewEmployeeGroup
					required
			Local Fields
				NewActor            is an Actor	
			Action Rules
				LocalActorGroup	= NewEmployeeGroup
				for each EmployeeRel
					NewActor = each.agent(Actor).Actor
					if (NewActor entered)
						LocalProjectReportingSettings = ProjectReportingSettings
						LocalActor                    = NewActor
						if (NewProjectReportingSettingsRel exists)
							invoke Delete NewProjectReportingSettingsRel
							
		LoadProjectReportingSettingsAll is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup     is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				or     PrmFinanceEnterpriseGroup not entered)
			Action Rules
				Instance Rules
					if (ProjectReportingSettingsAllRel not exists)
						invoke Create ProjectReportingSettingsAll 
							fill in fields from this instance
	                        invoked.ProjectReportingSettingsAll = ProjectReportingSettings
						
