ReplicationSetHistory is a BusinessClass
	prefix is RSHS
	owned by la

	
	Patterns
        disable Auditing
        disable EffectiveDated
        disable AsOfDateProcessing
        implements UniqueID
        implements UpdateStamp
	
	Persistent Fields
		VariationID 	is a RepSetVariationID
		
	Local Fields
		LocalWorkTimeStamp  is TimeStamp
		LocalStarted		is TimeStamp
		LocalFinished  		is TimeStamp
		LocalReplicationSet is a ReplicationSet
		
	Conditions
		AnyWarnings
			restricted
			when (any RepSetBCHistoryTimeRel.WarningsExist)
			 		
	Derived Fields
		RecordCount is a DerivedField 
			type is Numeric size 12
			return (sum RepSetBCHistoryTimeRel.RecordCount)
			
		NumberCreated is a DerivedField 
			type is Numeric size 12
			return (sum RepSetBCHistoryTimeRel.NumberCreated)
			
		NumberUpdated is a DerivedField 
			type is Numeric size 12
			return (sum RepSetBCHistoryTimeRel.NumberUpdated)
			
		NumberDeleted is a DerivedField 
			type is Numeric size 12
			return (sum RepSetBCHistoryTimeRel.NumberDeleted)
			
		Segments is a DerivedField
			type is Numeric size 12
			return (sum RepSetBCHistoryTimeRel.Segments)
			
		Started is a DerivedField 
			type is TimeStamp
			return (min RepSetBCHistoryTimeRel.Started)
			
		Finished is a DerivedField 
			type is TimeStamp
			initialize LocalWorkTimeStamp
            for each RepSetBCHistoryTimeRel
                if (each.Finished not entered)
                    return blank
                if (each.Finished > LocalWorkTimeStamp)
                    LocalWorkTimeStamp = each.Finished
            return LocalWorkTimeStamp	
            




        ElapsedTime is a DerivedField   
            type is Decimal 10.0
            default label is "LastElapsedTime"
            
            LocalStarted = Started
            
            if (LocalStarted = blank)
                return 0

			LocalFinished = Finished
			
            if (LocalFinished != blank)
                return LocalFinished - LocalStarted
            else
                return system current timestamp - LocalStarted

        DaysInElapsedTime is a DerivedField    
            type is Numeric 6
            default label is "LastDaysInElapsedTime"
            DaysInElapsedTime = ((ElapsedTime / 86400) - .5)
            if (DaysInElapsedTime < 0)
                DaysInElapsedTime = 0

        HoursInElapsedTime is a DerivedField    
            type is Numeric 2
            default label is "LastHoursInElapsedTime"
            HoursInElapsedTime = (((ElapsedTime - (DaysInElapsedTime * 86400)) / 3600) - .5)
            if (HoursInElapsedTime < 0)
                HoursInElapsedTime = 0

        MinutesInElapsedTime is a DerivedField  
            type is Numeric 2
            default label is "LastMinutesInElapsedTime"
            MinutesInElapsedTime = ((((ElapsedTime - (DaysInElapsedTime * 86400)) - (HoursInElapsedTime * 3600)) / 60) - .5)
            if (MinutesInElapsedTime < 0)
                MinutesInElapsedTime = 0

        SecondsInElapsedTime is a DerivedField  
            type is Numeric 2
            default label is "LastSecondsInElapsedTime"
            SecondsInElapsedTime = ((((ElapsedTime - (DaysInElapsedTime * 86400)) - (HoursInElapsedTime * 3600)) - (MinutesInElapsedTime * 60)) - .5)
            if (SecondsInElapsedTime < 0)
                SecondsInElapsedTime = 0



        DaysHoursMinutesSecondsInElapsedTime is a MessageField
            "<DaysInElapsedTime>Days<HoursInElapsedTime>Hours<MinutesInElapsedTime>Minutes<SecondsInElapsedTime>Seconds"

        HoursMinutesSecondsInElapsedTime is a MessageField
            "<HoursInElapsedTime>Hours<MinutesInElapsedTime>Minutes<SecondsInElapsedTime>Seconds"

        MinutesSecondsInElapsedTime is a MessageField
            "<MinutesInElapsedTime>Minutes<SecondsInElapsedTime>Seconds"
            
		SecondsInElapsedTimeMessage is a MessageField
        	"<SecondsInElapsedTime>Seconds"
        	
		SecondInElapsedTimeMessage is a MessageField
        	"<SecondsInElapsedTime>Second"        	
            
        OneSecondInElapsedTimeMessage is a MessageField
            "1Second"            

        ElapsedTimeComponentsText is a DerivedField
        	type is Alpha size 40
        	default label is "LastElapsedTime"
            if (DaysInElapsedTime > 0)
                return DaysHoursMinutesSecondsInElapsedTime
            else
            if (HoursInElapsedTime > 0)
                return HoursMinutesSecondsInElapsedTime
            else
            if (MinutesInElapsedTime > 0)
                return MinutesSecondsInElapsedTime
            else
            if (SecondsInElapsedTime > 0)
            	if (SecondsInElapsedTime = 1)
            		return SecondInElapsedTimeMessage
            		
                return SecondsInElapsedTimeMessage
            else
                if (Started != blank)
                    return "< " + OneSecondInElapsedTimeMessage 
                else
                    return ""               
	
	Ontology
		symbolic key is ReplicationSetHistory
		
	Relations
		RepSetBCHistoryTimeRel
    		one-to-many relation to RepSetBCHistory
			Field Mapping uses ByRepSetStamp
				related.ReplicationSet  = ReplicationSet
				related.RepSetBCHistory.HistoryStamp = ReplicationSetHistory
				related.RepSetBCHistory.Backfill = 0 
				
		RepSetBCHistoryOrphanedRel
    		one-to-many relation to RepSetBCHistory
			Field Mapping uses ByRepSetStamp
				related.ReplicationSet  = ReplicationSet
			Instance Selection
				where (related.Orphaned) 	
		

		RepSetBCHistoryOrphanedLocalRel
    		one-to-many relation to RepSetBCHistory
			Field Mapping uses ByRepSetStamp
				related.ReplicationSet = LocalReplicationSet
			Instance Selection
				where (related.Orphaned) 			 
				
	Rule Blocks
		DeleteOrphanedBCHistory 
			if (RepSetBCHistoryOrphanedLocalRel exists)
				invoke DeleteOrphanedHistory RepSetBCHistory in background
					invoked.ParamReplicationSet = LocalReplicationSet
	
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
			 
		Delete is a Delete Action
			Action Rules
				for each RepSetBCHistoryTimeRel 
					invoke Delete each
			
		DeleteHistory is a Set Action
			restricted
			Parameters
				ParamReplicationSet 	is a ReplicationSet
					default label is "ReplicationSet"
				KeepHistoryCount 		is Numeric size 3
				MinimumStamp			is TimeStamp   
				CurrentStamp			is TimeStamp
				
			Local Fields
				RecordCount is Numeric size 4	
				ForceKeep is Boolean			

			Parameter Rules
				ParamReplicationSet
					required
					
			Queue Mapping Fields
				ReplicationSet
				
			Instance Selection
				where (ReplicationSet = ParamReplicationSet)
			
			Sort Order
				ReplicationSet
				ReplicationSetHistory descending 
				
			Action Rules
				Empty Set Rules
					LocalReplicationSet = ParamReplicationSet
					include DeleteOrphanedBCHistory
				
				Set Rules
					Exit Rules
						LocalReplicationSet = ParamReplicationSet
						include DeleteOrphanedBCHistory
						
				Instance Rules
					if (KeepHistoryCount = 0
					and ReplicationSetHistory = CurrentStamp
					and ReplicationSetHistory.AnyWarnings)
						ForceKeep = true
					else
						ForceKeep = false
				
					if ((KeepHistoryCount > 0
					or  ForceKeep)
				    and RecordCount < 1000
				    and (ReplicationSetHistory <= MinimumStamp
				    or   ReplicationSetHistory = CurrentStamp))
						increment RecordCount
							
					if (not ForceKeep
					and (KeepHistoryCount = 0
					or  RecordCount > KeepHistoryCount
					or  (ReplicationSetHistory > MinimumStamp 
					and  ReplicationSetHistory != CurrentStamp)))
						invoke Delete		
						
	Sets
		ByTimeStamp
			indexed
			Sort Order
				ReplicationSetHistory
				ReplicationSet	
