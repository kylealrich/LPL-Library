IDMRichTextHeader is a BusinessClass
    owned by idm
    prefix is IDMRT

    Ontology
		symbolic key is IDMRichTextHeader

    Patterns
		disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Transient Fields
        Description 				is RichText
            derive value from DerivedDescription
        	translatable

    Local Fields
        LocalBusinessClass          is a BusinessClass
        LocalBusinessField          is a BusinessField

    Derived Fields
        DerivedNewLine              is a DerivedField
            type is RichText
            return "<br>"

        DerivedDescription1         is a MessageField
            "ThisFeatureAllowsYouToChooseBetweenUsing_Plain_TextOr_Rich_TextForDocumentGeneration.ByDefault,SampleDocumentTemplatesInFSMUse_Plain_Text.SelectingThe_Rich_TextOptionWillEnableSupportFor_Rich_TextFields(Primarily_Comments)WithVariousFormattingOptionsInDocumentGeneration."

        DerivedDescription2         is a MessageField
            "PredefinedSupportedFieldsAreAvailableInThe_'Default_Fields'_tab.AdditionalFieldsCanBeAddedInThe_'Custom_Fields'_tab.PleaseNoteThatOnly_Rich_TextFieldsAreSupportedInThisFeature."
        
        DerivedDescription3         is a MessageField
            "KindlyUpdateYour_Document_TemplateToInsertDesiredFieldsAs_Rich_Text.DetailedInstructionsAreAvailableInThe_FactsheetBelow:"
            
        DerivedDescription          is a MessageField
            "<DerivedDescription1><DerivedNewLine><DerivedDescription2><DerivedNewLine><DerivedDescription3>"

        DerivedSampleDocumentTemplate   is a DerivedField
            type is AlphaUpper size 70
            restricted
            if (IDMRichTextHeader.Contract)
                return "CONTRACTSAMPLEIDMTEMPLATEFACTSHEET_ST"
            else
            if (IDMRichTextHeader.ContractAddendum)
                return "CONTRACTADDENDUMDOCUMENTIDMSAMPLETEMPLATEFACTSHEET_ST"
            else
            if (IDMRichTextHeader.PurchaseOrder)
                return "PURCHASEORDERSAMPLEIDMTEMPLATEFACTSHEET_ST"

        DerivedFactSheetLink is a DerivedField
			type is BinaryDocument
            return SampleTemplateFactsheetRel.Attachment.File

    Relations
        IDMRichTextLineDefaultRel is an IDMRichTextLine set
            Instance Selection
                where (related.Type.Default)

        IDMRichTextLineCustomRel is an IDMRichTextLine set
            Instance Selection
                where (related.Type.Custom)

        OptionLineRel
            one-to-many relation to IDMRichTextLine
            Field Mapping uses symbolic key
                related.IDMRichTextHeader = IDMRichTextHeader
            Instance Selection
                where (related.BusinessClass = LocalBusinessClass
                and related.BusinessField = LocalBusinessField)

        SampleTemplateFactsheetRel
            one-to-one relation to SampleDocumentTemplate
            Field Mapping uses symbolic key
                related.SampleDocumentTemplate  = DerivedSampleDocumentTemplate

    Rule Blocks
        CreateLines
            if (IDMRichTextHeader.Contract
            or IDMRichTextHeader.ContractAddendum)
                LocalBusinessClass = "ContractComment"
                LocalBusinessField = "Description"
                include CheckAndCreateRichTextOptionLine

                LocalBusinessClass = "ContractLineComment"
                LocalBusinessField = "Description"
                include CheckAndCreateRichTextOptionLine
            else 
            if (IDMRichTextHeader.PurchaseOrder)
                LocalBusinessClass = "PurchaseOrderComment"
                LocalBusinessField = "CommentText"
                include CheckAndCreateRichTextOptionLine

                LocalBusinessClass = "PurchaseOrderLineComment"
                LocalBusinessField = "CommentText"
                include CheckAndCreateRichTextOptionLine

                LocalBusinessClass = "ItemCommentAttachment"
                LocalBusinessField = "Comment"
                include CheckAndCreateRichTextOptionLine
            
        CheckAndCreateRichTextOptionLine
            if (OptionLineRel not exists)
                invoke Create IDMRichTextLine
                    invoked.IDMRichTextHeader   = IDMRichTextHeader
                    invoked.BusinessClass       = LocalBusinessClass
                    invoked.BusinessField       = LocalBusinessField
                    invoked.TextFormat          = IDMRichTextLine.TextFormat.PlainText
                    invoked.Type                = 1

    Actions
        Create is a Create Action
            restricted
            Exit Rules
                include CreateLines

        Update is an Update Action
            restricted
            Action Rules
                include CreateLines

        Delete is a Delete Action
            restricted

        RestoreDefaults is an Instance Action
            Parameters
                IncludeCustomFields         is Boolean
            
            Action Rules
                invoke Update IDMRichTextLineDefaultRel
                    invoked.TextFormat = IDMRichTextLine.TextFormat.PlainText
                
                if (IncludeCustomFields)
                    invoke Update IDMRichTextLineCustomRel
                        invoked.TextFormat = IDMRichTextLine.TextFormat.PlainText
