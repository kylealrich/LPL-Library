RecallNotification is a BusinessClass
    owned by recall
    prefix is RMRNO

    Ontology
    	symbolic key is RecallNotification
    
    Patterns
    	implements DynamicCreation
    	
    Persistent Fields
   		RecallDomain
	
	Conditions
		HasAssignedTask
			restricted
			when (RecallNoticeTaskRel exists)
						
	Relations
		RecallNoticeTaskRel
			one-to-one relation to RecallNoticeTask
			Field Mapping uses ByOwner
				related.RecallGroup	 = RecallGroup
				related.RecallNotice = RecallNotice
				related.Owner		 = RecallUser
				
	Sets
		ByRecallDomain
			indexed
			Sort Order
				RecallGroup
				RecallNotice
				RecallDomain
				RecallUser
				
	Actions
		Create is a Create Action
			restricted
			valid when (!RecallNotice.HistoricalRecallNotice)
			Action Rules
				if (RecallNotice.ActiveRecallNotice
				and !HasAssignedTask
				and RecallGroup.SendRecallNoticeEmail)
					send email
						to RecallUser.EmployeeWorkEmailAddress
						from RecallNotice.RecallCoordinator.RecallUser.EmployeeWorkEmailAddress
						subject "<RecallGroup.FinalRecallNoticeEmailSubject>"
						Contents
							"<RecallGroup.FinalRecallNoticeEmailContent>"
							"Click<RecallNotice.NotificationLinkback>ToViewTheNotice."
   						
		Update is an Update Action
			valid when (!RecallNotice.HistoricalRecallNotice)
   					
		Delete is a Delete Action
			valid when (!RecallNotice.HistoricalRecallNotice)
			Action Rules
				if (RecallNotice.ActiveRecallNotice
				and !HasAssignedTask
				and RecallGroup.SendRecallNoticeEmail)
					send email
						to RecallUser.EmployeeWorkEmailAddress
						from RecallNotice.RecallCoordinator.RecallUser.EmployeeWorkEmailAddress
						subject "<RecallGroup.FinalRecallNoticeUnsubscribeEmailSubject>"
						Contents
							"<RecallGroup.FinalRecallNoticeUnsubscribeEmailContent>"
							"Click<RecallNotice.NotificationLinkback>ToViewTheNotice."
		
		Purge is a Purge Action
			restricted
