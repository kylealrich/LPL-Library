Provider is a BusinessClass
    owned by ic
    prefix is PROV
    
    Ontology
        symbolic key is Provider
    
    Patterns
        implements StaticJava

    Persistent Fields
        Name            is a PersonName



        Active			is Boolean

    Derived Fields
        ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforProvider"

        ContextMessageText is a MessageField
            restricted
            "Provider<Provider><Name.PreferredFirstAndLastName>"

		DisplayProviderFullName is a MessageField
            default label is "Name"
            "<Name.FamilyName>,_<Name.GivenName>_<Name.MiddleName>"
        
        ActiveMessage is a LabelField
            "Active"
        
        InactiveMessage is a LabelField
            "Inactive"

        ActiveDisplay is a DerivedField
            type is MessageField
            default label is "Active"
            if (Active)
                return ActiveMessage
            else
                return InactiveMessage

    Conditions
		ProviderExists
			restricted
			when (Provider exists)

    Relations
        PreferenceCardProviderRel
            one-to-many relation to PreferenceCardProvider
            Field Mapping uses ByProvider
                related.ItemGroup       = ItemGroup
                related.Provider        = Provider

    Field Rules
        ItemGroup
            constraint (ItemGroup.PreferenceCardsInUse)
                "ItemGroupDoesNotUsePreferenceCard"
        Active
            initial value is true

    Actions
        Create is a Create Action
            Exit Rules
                for each PreferenceCardProviderRel
                    invoke ReviewErrors each

        Update is an Update Action
            Action Rules
                if (Active changed)
                    for each PreferenceCardProviderRel
                        invoke ReviewErrors each

        Delete is a Delete Action
            Entrance Rules
                constraint (PreferenceCardProviderRel not exists)
                    "CannotDelete;ProviderExistsInAPreferenceCard"
