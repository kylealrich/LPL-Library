StudentActivityAccountCode is a BusinessClass
    owned by studentactivities
    prefix is ACTC

    Ontology
    	symbolic key is StudentActivityAccountCode


    Persistent Fields
		Description				is Alpha 100
		CodeType				is Numeric 1
			States
				AllTransactions	value is 0
				Disbursement	value is 1
				Receipt			value is 2
		StudentActivityType
		FinanceCodeBlock		is a FinanceCodeBlockFull
		Active

	Local Fields
		LocalStudentActivitySchool				is a StudentActivitySchool
		LocalStudentActivityAccountCode			is a StudentActivityAccountCode
		LocalSchoolGroup						is a StudentActivitySchool group

	Derived Fields
		
	Context Fields

	Sets

	Relations
		SchoolAccountCodeRel
			one-to-one relation to SchoolAccountCode
			Field Mapping uses symbolic key
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.StudentActivitySchool							= LocalStudentActivitySchool
				related.SchoolAccountCode.StudentActivityAccountCode	= LocalStudentActivityAccountCode

		StudentActivitySchoolRel
			one-to-many relation to StudentActivitySchool
			Field Mapping uses symbolic key
				related.StudentActivityDistrict		= StudentActivityDistrict
			Instance Selection
				where (related.StudentActivitySchool within LocalSchoolGroup)

	Conditions


	Field Rules
		Description
			required
		
		FinanceCodeBlock
			required
			
 
  	Attach Rules
		constraint (Active)
			"AccountCodeIsInactive"

	Actions
		AddAccountCodeToSchool is an Instance Action
			valid when (Active)
			Parameters
				School		is a StudentActivitySchool
				SchoolGroup	is a StudentActivitySchool group
			Local Fields
				LocalAccountCode is like StudentActivityAccountCode
					
			Parameter Rules
				School
					constraint (!SchoolGroup entered)
						"CannotEnterBothSchoolAndSchoolGroup"			

				SchoolGroup
					constraint (!School entered)
						"CannotEnterBothSchoolAndSchoolGroup"			

			Action Rules
				if (School entered)
					LocalAccountCode = StudentActivityAccountCode
					invoke Create SchoolAccountCode
						invoked.StudentActivityDistrict	= StudentActivityDistrict
						invoked.StudentActivitySchool	= School
						invoked.SchoolAccountCode		= LocalAccountCode
						invoked.Active					= true
				else
					LocalSchoolGroup	= SchoolGroup
					for each StudentActivitySchoolRel
						LocalStudentActivitySchool		= each.StudentActivitySchool
						LocalStudentActivityAccountCode	= StudentActivityAccountCode
						if (!SchoolAccountCodeRel exists)
							invoke Create SchoolAccountCode
								invoked.StudentActivityDistrict	= StudentActivityDistrict
								invoked.StudentActivitySchool	= LocalStudentActivitySchool
								invoked.SchoolAccountCode		= LocalStudentActivityAccountCode
								invoked.Active					= true



		Create is a Create Action
		
		Update is an Update Action
			Exit Rules
	 			if (FinanceCodeBlock changed)
					invoke UpdateGLInfoFromDistrictCode SchoolAccountCode set
			
		Delete is a Delete Action
