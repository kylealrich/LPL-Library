MobileSupplyChainInventoryTransaction is a BusinessClass

    owned by mscm
	prefix is MSITR
	sql name is "MSCMITransaction"
	
	
	Ontology
		symbolic key is MobileSupplyChainInventoryTransaction
			
	Persistent Fields
        TimeSubmitted                 	is TimeStamp
		TimeStarted					  	is TimeStamp
		IssuedTo 				      
		FromToCompanyLocation 	              
        InventoryTransaction
		InventoryDocumentType
		MobileSupplyChainPrinter
		IsInTransit					  	is Boolean
        EstimatedDeliveryDate         	is Date
		ErrorMessage 				  	is Text
		OriginatingIntransitRec			is a InventoryTransaction
		IsRelease						is Boolean
		IsPendingTransferReceipt		is Boolean
		Reference

	Local Fields
		InventoryTransactionView        is a InventoryTransaction view
		LocalError						is Boolean
		LocalErrorMessage				is Text
        
	Derived Fields
    	DerivedInventoryCompany is a DerivedField
            type is Text
            restricted
            return Company.RepresentativeText
 
        DerivedInventoryLocation is a DerivedField
            type is Text
            restricted
            return InventoryLocation.RepresentativeText

    	DerivedInventoryCompanyName is a DerivedField
            type is Text
            restricted
            return Company.Name
 
        DerivedInventoryLocationName is a DerivedField
            type is Text
            restricted
            return InventoryLocation.Name

		DerivedActorName is a DerivedField
			type is Actor
			restricted
			return create stamp.actor	

		DerivedRequestingCompany is a DerivedField
            type is Text
            restricted
            return FromToCompanyLocation.FromToCompany.RepresentativeText
 
        DerivedRequestingLocation is a DerivedField
            type is Text
            restricted
			if (IsReceivingTransfer)
				return FromToCompanyLocation.FromToLocation.RepresentativeText
			
			return FromToCompanyLocation.RequestingLocation.RepresentativeText
	
        DerivedTransferToLocation is a DerivedField
            type is Text
            restricted
            return FromToCompanyLocation.FromToLocation.RepresentativeText

		DerivedTransferToCompany is a DerivedField
			type is Text
			restricted
			return FromToCompanyLocation.FromToCompany.RepresentativeText
 
        DerivedPrintingDate is a DerivedField
            type is TimeStamp
            restricted
            return current timestamp
 
        DerivedIssuedTo is a MessageField
            "<IssuedTo.FirstName>_<IssuedTo.LastName>"

		DerivedRequestingLocationIDName	is a MessageField
			default label is "RequestingLocation"
			"<FromToCompanyLocation.RequestingLocation>_-_<FromToCompanyLocation.RequestingLocation.Name>"

		ProcessAdjustmentDocumentLineXML is a DerivedField
			type is XMLDocument
			restricted
			if (InventoryTransactionLinesRel exists)
				for each InventoryTransactionLinesRel
					ProcessAdjustmentDocumentLineXML += template.InventoryAdjustmentDocumentLine_ST document for each
			else
				return ""	

		ProcessIssueDocumentLineXML is a DerivedField
			type is XMLDocument
			restricted
			if (InventoryTransactionLinesRel exists)
				for each InventoryTransactionLinesRel
					ProcessIssueDocumentLineXML += template.InventoryIssueDocumentLine_ST document for each
			else
				return ""	

		ProcessTransferDocumentLineXML is a DerivedField
			type is XMLDocument
			restricted
			if (InventoryTransactionLinesRel exists)
				for each InventoryTransactionLinesRel
					ProcessTransferDocumentLineXML += template.InventoryTransferDocumentLine_ST document for each
			else
				return ""		

		ProcessReceivingTransferDocumentLineXML is a DerivedField
			type is XMLDocument
			restricted
			if (InventoryTransactionLinesRel exists)
				for each InventoryTransactionLinesRel
					ProcessReceivingTransferDocumentLineXML += template.InventoryReceivingTransferDocumentLine_ST document for each
			else
				return ""
		
		DerivedPrintDataFile is a DerivedField
			type is XMLDocument
			restricted
			if (IsInventoryIssues)
				DerivedPrintDataFile = template.InventoryIssueDocument_ST document for this instance
				return DerivedPrintDataFile
			else
			if (IsInventoryAdjustments)
				DerivedPrintDataFile = template.InventoryAdjustmentDocument_ST document for this instance	
				return DerivedPrintDataFile
			else
			if (IsInventoryTransfer)
				DerivedPrintDataFile = template.InventoryTransferDocument_ST document for this instance	
				return DerivedPrintDataFile
			else
			if (IsReceivingTransfer)
				DerivedPrintDataFile = template.InventoryReceivingTransferDocument_ST document for this instance
				return DerivedPrintDataFile

		DerivedMobileSupplyChainPrintingTransactionType	 is a DerivedField
			type is like MobileSupplyChainPrintingTransactionType
			restricted
			if (IsInventoryIssues)
				return MobileSupplyChainPrintingTransactionType.Issue
			else	
			if (IsInventoryAdjustments)
				return MobileSupplyChainPrintingTransactionType.InventoryAdjustment
			else	
			if (IsInventoryTransfer)
				return MobileSupplyChainPrintingTransactionType.InventoryTransfer	
			else
			if (IsReceivingTransfer)
				return MobileSupplyChainPrintingTransactionType.ReceivingTransfer
		
		DerivedStatus is a DerivedField
			type is Text
			restricted
			if (InventoryTransaction.IsReleased)
				return "Released"
			else
				return "Unreleased"
		
		DerivedProcessedWithErrorsMessage is a MessageField
			restricted
			"ProcessedWithErrors"
		
		DerivedTransactionCompletedMessage is a MessageField
			restricted
			"Completed"

		HasLineErrorMessage is a MessageField
            "HasLineError"

		DerivedErrorMessage is a DerivedField
            type is Text
            if (ErrorMessage entered)
                return ErrorMessage
            else 
			if (HasLineOrDetailErrors)
                return HasLineErrorMessage

	Relations
		MobileSupplyChainInventoryTransactionLineRel is a MobileSupplyChainInventoryTransactionLine set

        MobileSupplyChainInventoryTransactionLineErrorsRel
            one-to-many relation to  MobileSupplyChainInventoryTransactionLine
            Field Mapping uses symbolic key
                related.Company                                                     = Company
                related.InventoryLocation                                           = InventoryLocation
                related.MobileSupplyChainInventoryTransaction                       = MobileSupplyChainInventoryTransaction
            Instance Selection
                where(related.ErrorMessage entered)

		MobileSupplyChainInventoryTransactionLineDetailErrorsRel
            one-to-many relation to  MobileSupplyChainInventoryTransactionLineDetail
            Field Mapping uses symbolic key
                related.Company                                                     = Company
                related.InventoryLocation                                           = InventoryLocation
                related.MobileSupplyChainInventoryTransaction                       = MobileSupplyChainInventoryTransaction
            Instance Selection
                where(related.ErrorMessage entered)

		InventoryTransactionRel
			one-to-many relation to InventoryTransaction
			Field Mapping uses symbolic key
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.InventoryTransaction 	= InventoryTransaction

		MobileSupplyChainLocationRel
            one-to-one relation to MobileSupplyChainLocation
            Field Mapping uses symbolic key
                related.Company                                                     = Company
                related.MobileSupplyChainLocation                                   = InventoryLocation

		InventoryTransactionLinesRel
            one-to-many relation to InventoryTransactionLine
            Field Mapping uses symbolic key
                related.Company                                 = Company
                related.InventoryLocation                       = InventoryLocation
                related.InventoryTransaction                    = InventoryTransaction	

		InventoryTransactionBinTrackedIntransitLinesRel
			one-to-many relation to InventoryTransactionLine
			Field Mapping uses symbolic key
				related.Company									= Company
				related.InventoryLocation						= InventoryLocation
				related.InventoryTransaction					= OriginatingIntransitRec
			Instance Selection
				where (related.IsItemLocationBinTracked)


		PreviousSavedTransferReceiptsRel
			one-to-many relation to MobileSupplyChainInventoryTransaction
			Field Mapping uses symbolic key
				related.Company							= Company
				related.InventoryLocation				= InventoryLocation
			Instance Selection
				where (related.OriginatingIntransitRec	= OriginatingIntransitRec
				and	   not related.IsRelease
				and    related.ErrorMessage not entered
				and	   related.InventoryTransaction not entered
				)

		OriginatingIntransitReceivingRel
			one-to-one relation to InventoryTransaction
			Field Mapping uses symbolic key
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.InventoryTransaction	= OriginatingIntransitRec
					
	Conditions
		IsInventoryIssues
			restricted
			when(InventoryDocumentType.InventoryIssue)

		IsInventoryAdjustments
			restricted
			when(InventoryDocumentType.Adjustment)	

		IsInventoryTransfer
			restricted
			when(InventoryDocumentType.InventoryTransfer)

		IsReceivingTransfer
			restricted
			when (InventoryDocumentType.ReceivingTransfer)

		IsPrintingEnabled
            when ((IsInventoryAdjustments and MobileSupplyChainLocationRel.ActualPrintInventoryAdjustmentTicket)
            or 	  (IsInventoryIssues and MobileSupplyChainLocationRel.ActualPrintInventoryIssueTicket)    
            or 	  (IsInventoryTransfer and MobileSupplyChainLocationRel.ActualPrintInventoryTransferTicket)
			or	  (IsReceivingTransfer and MobileSupplyChainLocationRel.ActualPrintInventoryReceivingTransferTicket))

		HasTransactionErrors
			when (ErrorMessage entered
			or    MobileSupplyChainInventoryTransactionLineErrorsRel exists
			or    MobileSupplyChainInventoryTransactionLineDetailErrorsRel exists)
		
		HasLineOrDetailErrors
			when (MobileSupplyChainInventoryTransactionLineErrorsRel exists
			or    MobileSupplyChainInventoryTransactionLineDetailErrorsRel exists)

	Actions

		Create is a Create Action	
			
			Action Rules

				if (not IsReceivingTransfer)
					invoke Unreleased.Create InventoryTransaction
						assign result to InventoryTransactionView
						resume on error
							LocalError					= true
							LocalErrorMessage			= error message
						invoked.Company   					= Company
						invoked.InventoryLocation   		= InventoryLocation
						invoked.InventoryDocumentType 		= InventoryDocumentType  
						invoked.Reference1					= Reference
						if (IsInventoryIssues or IsInventoryTransfer)
							invoked.FromToCompanyLocation   = FromToCompanyLocation
						if (IsInTransit)
							invoked.IntransitTransfer		= IsInTransit  
							invoked.EstimatedDeliveryDate   = EstimatedDeliveryDate	
				if (LocalError)
					ErrorMessage			= LocalErrorMessage
				else
					InventoryTransaction  	= InventoryTransactionView.InventoryTransaction

		PrintInventoryTransactionTicket is an Instance Action
			restricted
			Action Rules
				invoke Create MobileSupplyChainPrinterJob
					invoked.FinanceEnterpriseGroup          = Company.FinanceEnterpriseGroup
					invoked.Company 						= Company
					invoked.MobileSupplyChainPrinter 		= MobileSupplyChainPrinter
					invoked.TransactionType                 = DerivedMobileSupplyChainPrintingTransactionType
					invoked.NumberOfCopies 					= 1
					invoked.PrintDataFile 					= DerivedPrintDataFile

		Update is an Update Action
			restricted

		Delete is a Delete Action	
			restricted	

		ReprintTransaction is an Instance Action			
			Parameters
				PrmMobileSupplyChainPrinter			is a MobileSupplyChainPrinter

			Parameter Rules
				PrmMobileSupplyChainPrinter
					initial value is MobileSupplyChainLocationRel.DerivedDefaultMobileSupplyChainReportPrinter

					if (PrmMobileSupplyChainPrinter not entered)
						required
			
			Action Rules
				invoke Create MobileSupplyChainPrinterJob
					invoked.FinanceEnterpriseGroup          = Company.FinanceEnterpriseGroup
					invoked.Company 						= Company
					invoked.MobileSupplyChainPrinter 		= PrmMobileSupplyChainPrinter
					invoked.TransactionType                 = DerivedMobileSupplyChainPrintingTransactionType
					invoked.NumberOfCopies 					= 1
					invoked.PrintDataFile 					= DerivedPrintDataFile

		UpdateSavedTransferReceipt is an Update Action
			restricted
			Action Rules
				for each PreviousSavedTransferReceiptsRel
					invoke Update each
						invoked.IsPendingTransferReceipt		= false

		UpdateLinesToMultipleBins is an Instance Action
			Action Rules
				invoke Update InventoryTransactionBinTrackedIntransitLinesRel
					invoked.MultipleBins				= true
					invoked.Bin							= blank
					invoked.TransientPreciseUnitCost    = InventoryTransactionBinTrackedIntransitLinesRel.DerivedUnitCost
	
	Sets
        ByTimeSubmitted
            Sort Order
                TimeSubmitted descending
                Company
                MobileSupplyChainInventoryTransaction
