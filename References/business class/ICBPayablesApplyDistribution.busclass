ICBPayablesApplyDistribution is a BusinessClass
	owned by intercobilling
	prefix is IBPAD
	sql name is "ICBPApplyDistribution"

	Ontology
		symbolic key is ICBPayablesApplyDistribution

	Persistent Fields
        IntercompanyBillingDocumentHeader
        PayableEntity
        InvoiceLine                         is a Sequence
        VoidSequence						is a Sequence
		ApplyFundDistributionAccount        is a FinanceCodeBlock
        OriginalCurrency					is a FromCurrency
        DistributionAmount                  is a FinanceCurrencyAmountGroup
        OriginalPayablesInvoiceDistribution is like PayablesInvoiceDistribution 

    Transient Fields
        TransientSupplyOldFundAppliedAmount is Boolean
        TransientOldFundAppliedAmount       is an InternationalAmount
        TransientAccountingEntity           is an AccountingEntity
            derive value from PayableEntity.Company.AccountingEntity
        BypassNegativeRateEdit
            derive value from true

    Conditions
        DistributionWasReleased
            restricted
            when (PayablesInvoiceDistributionRel.Status.Released
            or    PayablesInvoiceDistributionRel.Status.Historical)

    Relations		
        
        PayablesInvoiceDistributionRel
			one-to-one relation to PayablesInvoiceDistribution
			Field Mapping uses symbolic key
				related.Company					    = PayableEntity.Company
				related.PayablesInvoice			    = PayableEntity.PayablesInvoice
                related.PayablesInvoiceDistribution = OriginalPayablesInvoiceDistribution

    Sets

		ByPayablesInvoiceDistribution
			Sort Order
                IntercompanyBillingGroup
                IntercompanyBillingSettlementHeader.SettlementID
                IntercompanyBillingDocumentHeader 
                InvoiceLine
                VoidSequence
                PayableEntity.Company
                PayableEntity.PayablesInvoice
                OriginalPayablesInvoiceDistribution

        ICBPayablesApplyDistributionDescending
			Sort Order
                IntercompanyBillingGroup
                IntercompanyBillingSettlementHeader.SettlementID
                IntercompanyBillingDocumentHeader 
                InvoiceLine
                VoidSequence
                PayableEntity.Company
                PayableEntity.PayablesInvoice
                ICBPayablesApplyDistribution descending

	
    Delete Rules
        invoke AddToFundAppliedAmount PayablesInvoiceDistributionRel
            invoked.PrmAmount = -1 * DistributionAmount.CurrencyAmount
    
    
    Actions
        
        Create is a Create Action
			restricted
            Entrance Rules
                if (not IntercompanyBillingSettlementHeader.BypassReleasedSelectionsValidation)
                    constraint (DistributionWasReleased)
                        "PayablesDistributionHasNotBeenReleased"

            Action Rules
                invoke AddToFundAppliedAmount PayablesInvoiceDistributionRel
                    invoked.PrmAmount = DistributionAmount.CurrencyAmount
        				
		Update is an Update Action
			restricted
            Action Rules
                if (TransientSupplyOldFundAppliedAmount)
                    invoke AddToFundAppliedAmount PayablesInvoiceDistributionRel
                        invoked.PrmAmount = DistributionAmount.CurrencyAmount - TransientOldFundAppliedAmount
                    
                    TransientSupplyOldFundAppliedAmount = false
                else
                    invoke AddToFundAppliedAmount PayablesInvoiceDistributionRel
                        invoked.PrmAmount = DistributionAmount.CurrencyAmount - old DistributionAmount.CurrencyAmount
                     
        Delete is a Delete Action
			restricted

        UpdateVoidSequence is an Instance Action
            restricted
			Parameters
				PrmVoidSequence		is a Sequence
			Action Rules
				VoidSequence = PrmVoidSequence
                invoke AddToFundAppliedAmount PayablesInvoiceDistributionRel
                    invoked.PrmAmount = -1 * DistributionAmount.CurrencyAmount
