EscheatmentOutputFileHeader is a BusinessClass
    owned by cb
    prefix is EDOFH

    Ontology
        symbolic key is EscheatmentOutputFileHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		CreateDate					is TimeStamp
    	CashCode
    	CashCodeGroup
    	BankTransactionCode
    	ProcessLevelProcessing		is Boolean
    	ProcessLevel
    	IncludeAllTransactions		is Boolean
    	PrintAddress				is Boolean	
    	StateProvince				is like StateProvince
    	EscheatDate					is Date
    	Days						is Numeric 4
    	PostDate					is Date
    	FileName					is AlphaUpper 50
    	DetailFile					is an Attachment
    	Status						is Numeric 1
			States
				Created				value is 1
				HasErrors 			value is 2
				ReadyToUpdate 		value is 3
				Completed 			value is 4
				NoRecordsToProcess	value is 5
				Processing 			value is 6
				
	Transient Fields
    
    Local Fields
		EscheatmentBackgroundGroup			is AlphaUpper up to 200
				    
    Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 10	
			(instance count of EscheatmentOutputFileDetail set)

		TotalAmount is a DerivedField
			type is like InternationalAmount
			return (sum EscheatmentOutputFileDetail set.IssuedBankAmount)

        DetailOutputFileName is a StringField
            type is Alpha 100
			FileName
			"-"
			CashCode
			"ExtractDetail"

		ConcatenatedKey is a StringField
			type is Alpha 15
			CashManagementGroup
			"-"
			EscheatmentOutputFileHeader
				
		BackgroundGroupKey is a StringField
			type is Alpha 150
			"EscheatmentProcessing-PerformEscheatment"
			"-"
			ConcatenatedKey
			"-"
			
    Conditions
		HasDetailAttachment
			restricted
			when (DetailFile entered)

		ErrorsExist
			restricted
			when (any CashLedgerTransactionErrorForEscheatmentRel exists)
		
		DetailsExist
			restricted
			when (any EscheatmentOutputFileDetailsRel exists)
			
		IsCreated
			restricted
			when (Status.Created)
			
		HasErrors
			restricted
			when (Status.HasErrors)
		
		IsReadyToUpdate
			restricted
			when (Status.ReadyToUpdate)
		
		IsCompleted
			restricted
			when (Status.Completed)
		
		SelectedWithProcessLevelProcessing
			restricted
			when (ProcessLevelProcessing
			and  (HasErrors	
			or    IsReadyToUpdate))
						
		SelectedWithoutProcessLevelProcessing
			restricted
			when (!ProcessLevelProcessing
			and  (HasErrors	
			or    IsReadyToUpdate))
						
		ProcessedWithProcessLevelProcessing
			restricted
			when (ProcessLevelProcessing
			and   IsCompleted)	
						
		ProcessedWithoutProcessLevelProcessing
			restricted
			when (!ProcessLevelProcessing
			and   IsCompleted)	
			
		SecurityGroupAllowsAccess
    		when((CashCode not entered and CashCodeGroup not entered)
    		or	(CashCode entered and CashCode.SecurityGroupAllowsAccess)
    		or	(CashCodeGroup entered 
    		and (actor.context.CashCodeSecurityGroup.FinanceDimensionStructure = CashCodeGroup
    		or   actor.context.CashCodeSecurityGroup = "")))

    Relations
		CashLedgerTransactionErrorForEscheatmentRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByEscheatmentErrorSource
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 2	
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
		
		EscheatmentOutputFileDetailsRel
			one-to-many relation to EscheatmentOutputFileDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
				
		EscheatmentOutputFileDetailsSelectedRel
			one-to-many relation to EscheatmentOutputFileDetail
			Field Mapping uses ByStatus
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
				related.Status						= 1	
				
		EscheatmentOutputFileDetailsByCashCodeSelectedRel
			one-to-many relation to EscheatmentOutputFileDetail
			Field Mapping uses ByCashCode
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
			Instance Selection
				where (related.Status.Selected)	
				
		EscheatmentOutputFileDetailsByProcessLevelSelectedRel
			one-to-many relation to EscheatmentOutputFileDetail
			Field Mapping uses ByProcessLevel
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
			Instance Selection
				where (related.Status.Selected)
									
		EscheatmentOutputFileDetailsByCashCodeProcessedRel
			one-to-many relation to EscheatmentOutputFileDetail
			Field Mapping uses ByCashCode
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
			Instance Selection
				where (related.Status.Processed)
					
		EscheatmentOutputFileDetailsByProcessLevelProcessedRel
			one-to-many relation to EscheatmentOutputFileDetail
			Field Mapping uses ByProcessLevel
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= EscheatmentOutputFileHeader	
			Instance Selection
				where (related.Status.Processed)
	
		EscheatmentOutputFileHeaderHasErrorsRel
			one-to-many relation to EscheatmentOutputFileHeader
			Field Mapping uses ByStatus
				related.CashManagementGroup			= CashManagementGroup
				related.Status						= 2	
													
    Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			CashManagementGroup
    			CashCode
    			BankTransactionCode
    			ProcessLevel
    			FileName
    			EscheatmentOutputFileHeader

		ByCreateStatus
    		indexed
    		Instance Selection
                where (IsCreated)
			Sort Order
				CashManagementGroup
				Status
				EscheatmentOutputFileHeader		
				
		ByStatus
			indexed
			Sort Order
				CashManagementGroup
				Status
				EscheatmentOutputFileHeader
				
    Field Rules
        CreateDate
            default to current timestamp 

		FileName
			default to "Escheatment"    

		Status
			initial value is 1	
			default to 1
			
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action	
			restricted

		StatusUpdate is an Update Action
			default label is untranslatable
			restricted
			Action Rules
				if (ErrorsExist)
					Status = Status.HasErrors
				else
				if (DetailsExist)
					Status = Status.ReadyToUpdate
				else
					Status = Status.NoRecordsToProcess			

		DeleteEscheatmentOutputFiles is an Instance Action	
			Action Rules
				if (ErrorsExist)
					invoke RemoveEscheatmentErrorFromList CashLedgerTransactionErrorForEscheatmentRel
				if (DetailsExist)
					invoke DeleteEscheatmentOutputFileDetails EscheatmentOutputFileDetail	
						invoked.PrmCashManagementGroup	 		= CashManagementGroup
						invoked.PrmEscheatmentOutputFileHeader	= EscheatmentOutputFileHeader 
								
		Delete is a Delete Action
			restricted	
					
		PerformEscheatment is an Instance Action				
			valid when (IsReadyToUpdate)
			Action Rules
				trigger "CBEscheatmentOutputFiles" PA service
					resume on error
					title is "CreateEscheatmentOutputFiles"
					Variables
						CashManagementGroup
						EscheatmentOutputFileHeader
				if (EscheatmentOutputFileDetailsSelectedRel exists)
					EscheatmentBackgroundGroup	= BackgroundGroupKey
					Status 						= Status.Processing
					invoke ProcessEscheatmentDetails EscheatmentOutputFileDetail in background group(EscheatmentBackgroundGroup)
						invoked.PrmCashManagementGroup	 		= CashManagementGroup
						invoked.PrmEscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
					invoke MakeTransitionToCompleted in background
						run after background group (EscheatmentBackgroundGroup) 
						
		MakeTransitionToCompleted is an Instance Action
			default label is untranslatable
			restricted
			run in background
			Action Rules
				Status = Status.Completed 
								
		ResendDetailFile is an Instance Action				
			valid when (HasDetailAttachment)
			Action Rules
				trigger "CBEscheatmentOutputFiles" PA service
					resume on error
					title is "CreateEscheatmentOutputFiles"
					Variables
						CashManagementGroup
						EscheatmentOutputFileHeader
				
					
