VendorTemplate is a BusinessClass
	owned by ap
	prefix is VENT
	
	Ontology
		symbolic key is VendorTemplate
	
	Patterns
        implements StaticJava
        disable AuditIndex
	
	Persistent Fields
		VendorClass
		Reference1								is a Reference
		Reference2								is a Reference
		Active
		Description
		PaymentPriority
		TermsCode
		InvoiceCurrency							is a Currency
		BalanceCurrency							is a Currency
		RevalueCurrency							is Boolean
		SeparatePayment							is Boolean
		TaxCode									
		DistributionCode						is a PayablesDistributionCode			
		AccrualCode								is a PayablesAccrualCode
		BankTransactionCode
			default label is "PaymentCode"
		PayablesProcessGroup
			default label is "ProcessGroup"
		CashCode
		MaximumInvoiceAmount					is an InternationalAmount
		Operator
		PrimeRate
		InvoiceGroup							is a PayablesInvoiceGroup
		DiscountCode							is a PayablesDiscountCode
		AuthorityCode							is a PayablesAuthorityCode
		Project									is a FinanceCodeBlockProjectOnly
		MatchTable
		TermsCalculation
		CreatePOD								is Boolean
		Enclosure								is Boolean
		PoolOption
		RequireMatchReference					is Boolean
		HoldUntilInspected
		WriteOffAmount							is an InternationalAmount
		VendorClaimType
		VendorClaimHoldCode						is a PayablesHoldCode
		AutomaticChargebackHoldCode				is a PayablesHoldCode
		ChargebackMinimumAmount					is an InternationalAmount
		ReplaceGoods							is Numeric size 1
			States
				UseCompanyValue		value is 0
				Yes					value is 1
				No					value is 2
		ShipOrHold
		LanguageCode							is an IsoLocale
		TaxUsageCode							
		ValidatePurchaseOrder					is AlphaUpper size 1
			States
				CompanyDefault	value is blank
				Yes				value is "Y"
				No				value is "N"
		RequirePurchaseOrder					is AlphaUpper size 1
			States
				CompanyDefault	value is blank
				Yes				value is "Y"
				No				value is "N"
		PayImmediately							is Boolean
		DiversityCode							is a PayablesDiversityCode
		RuleGroup								is a MatchRule
		CostComponentMatch						is Boolean
		FloatDays
		MatchPrepayment							is Boolean
		MatchPrepaymentMethod
		ValidateDiversityDates					is Boolean
		RetainageAccrualCode					is a PayablesAccrualCode
		ForeignEconomicRegulatoryCode
		InvoiceRoutingCategory
		AllowZeroCostInvoice					is AlphaUpper size 1	
			States
				CompanyDefault	value is blank
				Yes				value is "Y"
				No				value is "N"

		ExternalAccountingEntity				is Alpha size 22
		ExternalLocation						is Alpha size 22
		ExpenseDefault							is a FinanceCodeBlock
			default label is "DefaultFinanceStructure"
		DefaultVendorPaymentCurrency			is Numeric size 1
			States
				CashCode			value is 0
				Invoice				value is 1
		CaseNumber								is Alpha 20		
		VendorRestrictions 								
		DefaultTaxIndicator						is a TaxIndicator					
		ApplyFloatDaysToDueDate					is Boolean	
		DefaultTemplate         				is Boolean
		
	Relations
		
		DefaultTemplateRel
            one-to-many relation to VendorTemplate
            Field Mapping uses symbolic key
                related.VendorGroup          = VendorGroup
            Instance Selection 
                where (related.DefaultTemplate
                and    related.VendorTemplate != VendorTemplate)
		
		ContractGroupsRel
			one-to-many relation to ContractGroup
			Field Mapping uses symbolic key

		TaxUsageCodesRel
			one-to-many relation to TaxUsageCode
			Field Mapping uses Set2
				related.TaxUsageCode = TaxUsageCode
	
	Attach Rules
		constraint (Active)
			"VendorTemplateIsNotActive"
				
	Field Rules
		Active
            initial value is true
		
		DefaultTemplate
            constraint (DefaultTemplateRel !exists)
                "DefaultTemplateAlreadyExistsFor<first DefaultTemplateRel.VendorTemplate>"
                	
		MatchPrepaymentMethod
			if (MatchPrepayment)
				required
			else
				cannot be entered		

		RetainageAccrualCode
			constraint (ContractGroupsRel exist)
				"CannotAddRetainageAccrualCode,ReqsContractManagement"	
				
		PrimeRate
			constraint (PrimeRate >= 0)
				"RateCannotBeLessThanZero"								 

		InvoiceCurrency
			default to BalanceCurrency
		
		RevalueCurrency
			initial value is true	
		
		MatchTable
			constraint (not MatchTable.MatchTableLevel.CompanyOnly)
				"MatchTableTypeCannotBeCompanyOnly"
				
		AutomaticChargebackHoldCode
			constraint (AutomaticChargebackHoldCode.HoldOnInvoice entered)
				"HoldCode<AutomaticChargebackHoldCode>DoesNotExistOrIsNotValidForInvoices"
		
		TaxUsageCode
			constraint (TaxCode entered)
				"TaxUsageCodeNotValidIfNoTaxCodeIsEntered"

			constraint (TaxUsageCodesRel exist)
				"TaxUsageCode<TaxUsageCode>NotDefined"
		
		Project
			constraint (Project.Project exists)
				"Project<Project.Project>DoesNotExist"
		
		VendorClaimHoldCode
			constraint (VendorClaimHoldCode.HoldOnInvoice entered)
				"HoldCode<VendorClaimHoldCode>DoesNotExistOrIsNotValidForInvoices"	

			constraint (VendorClaimType.Chargeback)
				"CannotEnterVendorClaimsHoldCodeIfNotDoingChargebacks"
		
	Actions
	 
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action				
			
