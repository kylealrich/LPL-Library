ManufacturerID is a BusinessClass
    owned by ic
    prefix is ICM
    classic name is ICMANUF

    Ontology
        symbolic key is ManufacturerID
            classic set name is ICMSET1
            classic name is MANUF-ID

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Name
        EDINumber
            classic name is EDI-NBR
        ReportFrequency          is Numeric size 1
            classic name is RPT-FREQ
            States
                Daily   value is 0
                Weekly  value is 1
                Monthly value is 2
        NextReportDate           is Date
            classic name is NXT-RPT-DT
        IncludeNonInventoryItems is Boolean
            classic name is NON-INV-ITEM
        DetailOption             is Numeric size 1
            States
                NetDaily  value is 0
                NetPeriod value is 1
                Detail    value is 2
        SortOrder                is Numeric size 1
            classic name is SORT-SEQ
            States
                ByDate value is 0
                ByItem value is 1
        UnitOfMeasureOption      is Numeric size 1
            classic name is UOM-OPTION
            States
                IssueUOM value is 0
                BuyUOM   value is 1
        DecimalHandling          is Numeric size 1
            classic name is DECIMAL-HNDL
            States
                ReportDecimal value is 0
                Truncate      value is 1
        Currency
            classic name is CURRENCY-CD

	Transient Fields
		Manufacturer
		Company           					is an InventoryCompany
		InventoryLocation
		DistCenterName						is a Name	 
			holds pii
		PostalAddress						is a PostalAddressV2	
			holds pii
		ManufacturerCrossReference			is AlphaUpper size 15
		STFacilityCrossReference			is AlphaUpper size 15
		STFacilityName						is a Name	 
			holds pii
		ShipToFacility
		STFacilityPostalAddress				is a PostalAddressV2	
			holds pii

	Derived Fields
		DefaultManufacturerCrossRef 		is a StringField
			type is AlphaUpper size 15
			restricted
			Company
			""
			InventoryLocation

	Conditions
		
		RecordExists
			restricted
			when (ManufacturerID exists)
						
    Field Rules

        EDINumber
            required

        Name
            required

		NextReportDate
            required
			if (ReportFrequency.Monthly)
				constraint (NextReportDate is last day in month)
					"MustBeLastDayOfMonthIfReportFrequencyIsMonthly"
		
    Actions
		Create is a Create Action
        	Field Rules
        		Manufacturer
        			required
        		Company
        			required
        		InventoryLocation
        			required
        		DistCenterName
        			default to InventoryLocation.Name
        		ShipToFacility
        			required
        		STFacilityName
        			default to ShipToFacility.Name
				ManufacturerCrossReference
					default to DefaultManufacturerCrossRef
				STFacilityCrossReference
					default to ShipToFacility
        		
	        Exit Rules

				invoke Create ManufacturerIDManufacturer
					invoked.ItemGroup													= ItemGroup
					invoked.ManufacturerID												= ManufacturerID
					invoked.Manufacturer.ManufacturerCode								= Manufacturer.ManufacturerCode
					invoked.Manufacturer.ManufacturerDivision							= Manufacturer.ManufacturerDivision

				invoke Create ManufacturerIDDistributionCenter
					invoked.ItemGroup													= ItemGroup
					invoked.ManufacturerID												= ManufacturerID				
        			invoked.ManufacturerIDDistributionCenter.Company					= Company
        			invoked.ManufacturerIDDistributionCenter.InventoryLocation			= InventoryLocation
        			invoked.ManufacturerIDDistributionCenter.Name						= DistCenterName
        			invoked.ManufacturerIDDistributionCenter.ManufacturerCrossReference	= ManufacturerCrossReference
        			invoked.ManufacturerIDDistributionCenter.PostalAddress				= PostalAddress
       		
        		invoke Create ManufacturerIDShipToFacility
        			invoked.ItemGroup													= ItemGroup
        			invoked.ManufacturerIDShipToFacility.ManufacturerID					= ManufacturerID
        			invoked.ManufacturerIDShipToFacility.ShipToFacility					= ShipToFacility
        			invoked.ManufacturerIDShipToFacility.ManufacturerCrossReference		= STFacilityCrossReference
        			invoked.ManufacturerIDShipToFacility.Name							= STFacilityName
        			invoked.ManufacturerIDShipToFacility.PostalAddress					= STFacilityPostalAddress

        Update is an Update Action

        Delete is a Delete Action
			Entrance Rules
				for each ManufacturerIDManufacturer set
					invoke RestrictedDelete each
				for each ManufacturerIDDistributionCenter set
					invoke RestrictedDelete each
				for each ManufacturerIDShipToFacility set
					invoke RestrictedDelete each
