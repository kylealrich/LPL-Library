ProcurementTemplateParticipant is a BusinessClass
    owned by po
    prefix is POA
    classic name is PROCTMPPAR

    Ontology
        symbolic key is ProcurementTemplateParticipant
            classic set name is POASET1
            classic name for RequestingLocation is REQ-LOCATION
            classic name for BuyerGroup is BUYER-GRP
            classic name for ProcurementGroup is PROCURE-GROUP
            classic name for ProcurementTemplate is TEMPLATE-REF

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        AccountingUnit				is a FinanceCodeBlock
            classic name is ACCT-UNIT
 
 	Derived Fields
    	DerivedCompany 					is a DerivedField
			type is like InventoryCompany
			restricted
			if (RequisitionRel exists)
				return RequisitionRel.Company
			else
				return RequesterRel.Company
	
 		DerivedRequestingLocation		is a DerivedField
			type is like RequestingLocation
			restricted
			if (RequisitionRel exists)
				return RequisitionRel.RequestingLocation
			else
				return RequesterRel.RequestingLocation
				
		Description 					is a DerivedField
			type is like Description
			return ProcurementTemplate.Description
			
		NoOfLines   					is a DerivedField
			type is Numeric 6
			return ProcurementTemplate.NoOfLines
			
		Locked 							is a DerivedField
			type is Boolean
			return ProcurementTemplate.Locked
			
		ProcurementTemplateUpdateAllowed is a DerivedField
			type is Boolean
			return ProcurementTemplate.ProcurementTemplateUpdateAllowed

		AddAllMessage 					is a MessageField
			"ItemsAdded_toRequisition"

		AddAllBatchMessage				is a MessageField
			"AddOfAllItemsSubmittedToBatch;RefreshPageToUpdateShoppingCart"
		
		AddAllCompletionMessage 		is a DerivedField
			type is MessageField
			restricted
			if (ProcurementTemplate.NoOfLines > 50)
				return AddAllBatchMessage 
			else
				return AddAllMessage
    
    Conditions

        HasBuyer
            classic name is BUYER-EXISTS
            restricted
            when (Buyer entered)

        HasBuyerGroup
            classic name is BUYERGR-EXISTS
            restricted
            when (BuyerGroup entered)

        HasCompany
            classic name is COMPANY-EXISTS
            restricted
            when (Company entered
            and   RequestingLocation not entered)

        HasRequestingLocation
            classic name is REQLOC-EXISTS
            restricted
            when (RequestingLocation entered
            and   Company entered)

        HasRequester
            classic name is REQUESTR-EXIST
            restricted
            when (Requester entered)

        HasParticipant
            classic name is TEMPPART-EXIST
            restricted
            when (Company entered
            or    (Company entered
            and    RequestingLocation entered)
            or	  Requester entered
            or    BuyerGroup entered
            or    Buyer entered
			or    Vendor entered)
            
        IsValidForActorContext
			restricted
			when ((actor.context.FinanceEnterpriseGroup != ""
			and   ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
			or   (actor.context.FinanceEnterpriseGroup = ""))
            
		HasRequesterParticipant
			restricted
			when (HasRequester
			and   Requester = actor.agent(Employee).Employee.Requester.Requester)
			
		HasCompanyParticipant
			restricted
			when (HasCompany
			and   Company = DerivedCompany)
			
		HasVendor
			restricted
			when (Vendor entered)
		
		HasRequestingLocationParticipant			
			restricted
			when (HasRequestingLocation
			and   Company = DerivedCompany
			and   RequestingLocation = DerivedRequestingLocation)
			
		IsRequesterFavoritePOTemplate
			when (RequesterFavoritePOTemplateRel exists)
			
		RequesterParticipantexists
			restricted
			when (ProcurementTemplateParticipantsRequesterRel exists)

		RSSLPLCurrentRequisitionForRQCEntered
			restricted
			when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC entered)

		RSSLPLCurrentRequisitionMultiplier
			restricted
			when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC entered
			and   actor.agent(Employee).Employee.Requester.QuantityMultiplierOnShoppingListAllowed)

		RSSLPLCurrentRequisitionNoMultiplier
			restricted
			when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC entered
			and   !actor.agent(Employee).Employee.Requester.QuantityMultiplierOnShoppingListAllowed)

		DisplayRequisitionDescription
			restricted
			when (RequesterRel.DisplayRequisitionDescription)

		DisplayAddToFavorites
			restricted
			when (!ProcurementTemplate.IsRequesterFavoritePOTemplate
			and RequesterRel.DerivedEnableFavorites = "Y")

		DisplayRemoveToFavorites
			restricted
			when (ProcurementTemplate.IsRequesterFavoritePOTemplate
			and RequesterRel.DerivedEnableFavorites = "Y")

		DisplayRequestedDeliveryDate
			restricted
			when (RequesterRel.DisplayRequestedDeliveryDate)

		RSSLPLAllocationPriorityLevelEnabled
			restricted
            when (RSSLPLConfigurationParameterRel exists)
		
		RSSLPLShowRequiredFields
			restricted
			when (RequesterRel.RequireBuyer
			or RequesterRel.RequirePOCode
			or RequesterRel.RequireCommodityCodeDisplay
			or RequesterRel.RequireDeliverTo
			or RequesterRel.RequireVendor)
		
		RSSLPLDisplayAllocationPriorityLevel
			restricted
			when (RSSLPLAllocationPriorityLevelEnabled
			and RequesterRel.RSSLPLDisplayPriority)
		
		RSSLPLDisplayPriority
			restricted
			when (not RSSLPLAllocationPriorityLevelEnabled
			and RequesterRel.RSSLPLDisplayPriority)
		
		RSSLPLDisplayFromCompany
			restricted
			when (RequesterRel.RSSLPLDisplayFromCompany)
		
		RSSLPLDisplayFromLocation
			restricted
			when (RequesterRel.RSSLPLDisplayFromLocation)
		
		RSSLPLDisplayCompany
			restricted
			when (RequesterRel.RSSLPLDisplayCompany)
		
		RSSLPLDisplayRequestingLocation
			restricted
			when (RequesterRel.RSSLPLDisplayRequestingLocation)

    Sets

        Set2
            indexed
            Instance Selection
                where (HasBuyerGroup)
            Sort Order
                ProcurementGroup
                BuyerGroup
                ProcurementTemplate

        Set3
            indexed
            Instance Selection
                where (HasBuyer)
            Sort Order
                ProcurementGroup
                Buyer
                ProcurementTemplate

        Set4
            indexed
            Instance Selection
                where (HasCompany)
            Sort Order
                ProcurementGroup
                Company
                ProcurementTemplate
                RequestingLocation
                Requester
                Buyer
                BuyerGroup

        Set5
            indexed
            Instance Selection
                where (HasRequestingLocation)
            Sort Order
                ProcurementGroup
                Company
                RequestingLocation
                ProcurementTemplate

        Set6
            indexed
            Instance Selection
                where (HasRequester)
            Sort Order
                ProcurementGroup
                Requester
                ProcurementTemplate

		ByRequester
            indexed
            Instance Selection
                where (HasRequester)
            Sort Order
                ProcurementGroup
                ProcurementTemplate
                Requester
                Company
                RequestingLocation
                BuyerGroup
                Buyer
                			
	Relations
		ProcurementTemplateParticipantsRel
			one-to-many relation to ProcurementTemplateParticipant
			Field Mapping uses symbolic key
				related.ProcurementGroup 	= ProcurementGroup
				related.ProcurementTemplate = ProcurementTemplate
				related.Company			 	= Company

		CompanyParticipantRel
			one-to-many relation to ProcurementTemplateParticipant
			Field Mapping uses Set4
				related.ProcurementGroup 	= ProcurementGroup
				related.Company			 	= Company
				related.ProcurementTemplate = ProcurementTemplate
			Instance Selection
				where (related.RequestingLocation not entered
				and    related.Requester not entered
				and    related.Buyer not entered
				and    related.BuyerGroup not entered)
				
		OtherVendorParticipantRel
			one-to-many relation to ProcurementTemplateParticipant
			Field Mapping uses symbolic key
				related.ProcurementGroup 	= ProcurementGroup
				related.ProcurementTemplate = ProcurementTemplate
			Instance Selection
				where (related.HasVendor)		

		ProcurementTemplateLineOtherVendorRel
			one-to-many relation to ProcurementTemplateLine
			Field Mapping uses symbolic key
				related.ProcurementGroup 	= ProcurementGroup
				related.ProcurementTemplate = ProcurementTemplate
			Instance Selection
				where (related.Contract entered
				and    related.Contract.Vendor != Vendor)

		RequesterRel
            one-to-one relation to Requester
            Field Mapping uses symbolic key
            	related.HROrganization = actor.agent(Employee).HROrganization
                related.Requester = actor.agent(Employee).Employee.Requester.Requester
                
        RequisitionRel
			one-to-one relation to Requisition
			Field Mapping uses symbolic key
				related.Company				= RequesterRel.CurrentCompanyForRQC
				related.Requisition			= RequesterRel.CurrentRequisitionForRQC
        				
		ProcurementTemplateParticipantsRequesterRel
			one-to-many relation to ProcurementTemplateParticipant
            Field Mapping uses symbolic key
                related.ProcurementGroup 		= ProcurementGroup
				related.ProcurementTemplate 	= ProcurementTemplate
                related.Company					= blank
                related.RequestingLocation		= blank
                related.Requester				= actor.agent(Employee).Employee
			Instance Selection
                where (related.Company not entered
                and	   related.RequestingLocation not entered)
        
        RequesterFavoritePOTemplateRel
			one-to-one relation to RequesterFavoritePOTemplate
			Field Mapping uses symbolic key
				related.Company				= RequesterRel.Company
				related.Requester			= actor.agent(Employee).Employee.Requester.Requester
				related.ProcurementTemplate	= ProcurementTemplate
				
		ProcurementTemplateLines2Rel
            one-to-many relation to ProcurementTemplateLine
            Field Mapping uses Set2
                related.ProcurementGroup    	= ProcurementGroup
                related.ProcurementTemplate 	= ProcurementTemplate
		
		RSSLPLConfigurationParameterRel
            one-to-many relation to ConfigurationParameter
            Field Mapping uses symbolic key
                related.ConfigurationParameter.ConfigurationID  = "config"
                related.ConfigurationParameter.Name             = "RSS_LPLAllocationPriorityLevel"
			Instance Selection
				where (related.ConfigurationParameter.Value = "true")

		RSSLPLAllocationPriorityLevelRel
			one-to-many relation to AllocationPriorityLevel
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= actor.context.FinanceEnterpriseGroup
			Instance Selection
				where (related.UseAsDefault)

	Field Rules
		ProcurementTemplate
			constraint (HasParticipant)
				"MustEnterAtLeastOneParticipant"  

		Company
			if (RequestingLocation entered)										
				required

			if (HasBuyer
			or  HasRequester 
			or  HasBuyerGroup
			or  HasVendor)
				cannot be entered 
					"CannotEnterCompanyWithRequester,BuyerGroup,Buyer,OrVendor" 							 
			
			constraint (Company.ProcurementGroup = ProcurementGroup)
				"CompanyDoesNotBelongToTheProcurementGroup<ProcurementGroup>"
					
			if (RequestingLocation not entered)
				constraint (ProcurementTemplateParticipantsRel not exists)
					"CannotAdd,ParticipantRecordAlreadyExistsForThisCompany"				
					
			
				
		RequestingLocation	
			if (HasRequester
			or  HasBuyerGroup 
			or  HasBuyer)
				cannot be entered
					"CannotEnterRequestingLocationWithRequester,BuyerGroupOrBuyer" 		
			
			constraint (CompanyParticipantRel not exists)
				"CannotAdd,ParticipantRecordExistsForTheCompany"				
		
		Requester
			if (HasBuyerGroup 
			or  HasBuyer)
				cannot be entered
					"CannotEnterRequesterWithBuyerGroupOrBuyer" 		
		
		BuyerGroup
			if (HasBuyer)
				cannot be entered 
					"CannotEnterBuyerGroupWithBuyer" 						

	Actions
	
		Create is a Create Action
			Action Rules
				constraint (!ProcurementTemplate.Locked)
					"ProcurementTemplateLocked,ProcurementTemplateParticipantCanNotBeCreated"

				if (HasVendor)
					constraint (OtherVendorParticipantRel !exists)
						"CanOnlyCreateOneVendorParticipantForATemplate"
					constraint (ProcurementTemplateLineOtherVendorRel !exists)
						"CannotCreateVendorParticipant;LinesExistForAContractForADifferentVendor"

		CreateFromWizard is a Create Action
		
		Delete is a Delete Action
			Entrance Rules
				constraint (!ProcurementTemplate.Locked)
					"ProcurementTemplateLocked,ProcurementTemplateParticipantCanNotBeDeleted"

		RSSLPLDeleteProcurementTemplate is an Instance Action
			restricted
			completion message is "ProcurementTemplateHasBeenDeleted"
			Entrance Rules
				constraint (!ProcurementTemplate.Locked)
					"ProcurementTemplateLocked,CanNotBeDeleted"
				constraint (ProcurementTemplateUpdateAllowed)
					"OtherParticipantsExist,CannotDeleteShoppingList"
				confirmation required
					"ShoppingListWillBeDeleted.DoYouWantToContinue?"
			Action Rules
				invoke Delete ProcurementTemplate
						
		RSSLPLMarkAsFavorite is an Instance Action
			restricted
			completion message is "ShoppingListMarked_asFavorite"
			Action Rules
				constraint (!ProcurementTemplate.IsRequesterFavoritePOTemplate)
					"ProcurementTemplateIsAlreadyAFavorite"
				invoke Create RequesterFavoritePOTemplate
							invoked.Company 			= RequesterRel.Company
							invoked.Requester     		= actor.agent(Employee).Employee.Requester.Requester
							invoked.ProcurementTemplate	= ProcurementTemplate
							
		RSSLPLRemoveAsFavorite is an Instance Action
			restricted
			completion message is "ShoppingListRemoved_asFavorite"
			Action Rules
				invoke Delete RequesterFavoritePOTemplateRel
					
	    RSSLPLCopyToNewRequisition is an Instance Action
	    	restricted
            completion message is "<AddAllCompletionMessage>"
			Local Fields
            	LocalRequisitionView 	 is a Requisition view
            	LocalRequisitionLineView is a RequisitionLine view
                LocalRequisition		 is a Requisition
			Parameters
				PrmCompany             	is an InventoryCompany
	            PrmRequisition		  	is a Requisition
	        	PrmLimitCopy			is Boolean
	            PrmRequestingLocation  	is a RequestingLocation
				PrmFromCompanyLocation 	is a FromCompanyLocation  
	            PrmDate                	is Date
	            PrmVendor               is a Vendor
	            PrmBuyer				is a Buyer
	            PrmDeliverTo			is a DeliverTo
	            PrmPOCode				is a POCode
	            PrmAllocationPriority	is a AllocationPriority
	            PrmPurchaseFromLocation	is a VendorLocation
	            PrmPurchaseTaxCode		is a TaxCode
				PrmPurchaseTaxable		is Boolean
				PrmCommodityCode		is a CommCodes
				PrmQuantityMultiplier	is an UnsignedQuantity
				PrmAddToRequisition		is Numeric size 1
					States
						AddToExistingRequisition	value is 0
						StartNewRequisition			value is 1
				PrmRequisitionDescription		is Alpha size 30
				PrmAllocationPriorityLevel		is an AllocationPriorityLevel
	        Parameter Rules 
	        	PrmCompany
	        		default to actor.agent(Employee).Employee.Requester.CurrentCompanyForRQC
				    initial value is actor.agent(Employee).Employee.Requester.CurrentCompanyForRQC
				    default to actor.agent(Employee).Employee.Requester.Company
				    initial value is actor.agent(Employee).Employee.Requester.Company
				PrmRequestingLocation
            		default to PrmRequisition.RequestingLocation
            		initial value is PrmRequisition.RequestingLocation
            		default to actor.agent(Employee).Employee.Requester.RequestingLocation
            		initial value is actor.agent(Employee).Employee.Requester.RequestingLocation
	        	PrmRequisition
	        		if (PrmAddToRequisition.AddToExistingRequisition)
						required
							"FieldIsRequired"
						constraint(PrmRequisition.Status.Unreleased)
	        				"RequistionMustBeUnreleased"
					else
						PrmRequisition = 0
				PrmQuantityMultiplier
					if (actor.agent(Employee).Employee.Requester.QuantityMultiplierOnShoppingListAllowed)
						required
							"FieldIsRequired"
				PrmAllocationPriority							
					if (RSSLPLConfigurationParameterRel exists)
						default to PrmAllocationPriorityLevel
						default to RSSLPLAllocationPriorityLevelRel.AllocationPriorityLevel
				PrmDate
					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequireRequestedDeliveryDate)
						required
							"Field_Requested_Delivery_DateIsRequired"
				PrmRequisitionDescription
					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequireRequisitionDescription)
						required
							"Field_Requisition_DescriptionIsRequired"
				PrmBuyer
					initial value is PrmRequestingLocation.Buyer

					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequireBuyer)
						required
							"Field_BuyerIsRequired"			
				PrmPOCode
					initial value is PrmRequestingLocation.POCode
					
					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequirePOCode)
						required
							"Field_PO_CodeIsRequired"				
				PrmCommodityCode
					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequireCommodityCodeDisplay)
						required
							"Field_Commodity_TemplateIsRequired"			
				PrmDeliverTo
					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequireDeliverTo)
						required
							"Field_Deliver_ToIsRequired"
	            PrmVendor
					if (PrmAddToRequisition.StartNewRequisition
					and RequesterRel.RequireVendor)
						required
							"Field_VendorIsRequired"
			Action Rules
				if (ProcurementTemplate.NoOfLines > 50)	
					confirmation required
						"ShoppingListHasMoreThan_50_Items;WillBeSubmittedInABatch.Continue?"
				if (PrmRequisition not entered)
	            	invoke Create Requisition
    	            	assign result to LocalRequisitionView
	    	            invoked.Company					= PrmCompany
	        	        invoked.RequestingLocation		= PrmRequestingLocation
						invoked.AllocationPriority		= PrmAllocationPriority
						if (RSSLPLConfigurationParameterRel exists)
							invoked.TransientAllocationPriorityLevel 	= PrmAllocationPriority
						invoked.RequestedDeliveryDate	= PrmDate
						invoked.RequisitionDescription	= PrmRequisitionDescription
						if (PrmFromCompanyLocation entered)
							invoked.FromCompanyLocation				= PrmFromCompanyLocation
						invoked.RequisitionSource 		= RqSource.RequisitionSelfService
	               	LocalRequisition = LocalRequisitionView.Requisition
	               	PrmRequisition   = LocalRequisitionView.Requisition
	           	else
	          		LocalRequisition = PrmRequisition
			
	            if (ProcurementTemplate.NoOfLines > 50)
					invoke BatchCopyToRequisitionAllRSS ProcurementTemplateLine
						invoked.PrmProcurementTemplate			    = ProcurementTemplate
						invoked.PrmCompany							= PrmCompany
						invoked.PrmRequisition						= LocalRequisition
						invoked.PrmQuantityMultiplier				= PrmQuantityMultiplier
						invoked.PrmDeliverTo						= PrmDeliverTo
						invoked.PrmBuyer							= PrmBuyer
						invoked.PrmPOCode							= PrmPOCode
						invoked.PrmCommodityCode					= PrmCommodityCode
				else
					for each ProcurementTemplate.ProcurementTemplateLine set
						invoke CopyToRequisitionAll each
							invoked.PrmCompany							= PrmCompany
							invoked.PrmRequisition						= LocalRequisition
							invoked.PrmRequestingLocation				= PrmRequestingLocation
							invoked.PrmFromCompanyLocation				= PrmFromCompanyLocation
							invoked.PrmDate								= PrmDate
							invoked.PrmVendor							= PrmVendor
							invoked.PrmBuyer							= PrmBuyer
							invoked.PrmDeliverTo						= PrmDeliverTo
							invoked.PrmPOCode							= PrmPOCode
							invoked.PrmAllocationPriority				= PrmAllocationPriority
							invoked.PrmPurchaseFromLocation				= PrmPurchaseFromLocation
							invoked.PrmPurchaseTaxCode					= PrmPurchaseTaxCode
							invoked.PrmPurchaseTaxable					= PrmPurchaseTaxable
							invoked.PrmCommodityCode					= PrmCommodityCode
							invoked.PrmQuantityMultiplier				= PrmQuantityMultiplier
			Exit Rules
				invoke UpdateCurrentRequisitionForRQC RequesterRel
						invoked.PrmCompany				= PrmCompany
						invoked.PrmRequisition     		= LocalRequisition

		RSSLPLCopyToRequisition is an Instance Action
	    	restricted
            completion message is "<AddAllCompletionMessage>"
			Action Rules
				if (ProcurementTemplate.NoOfLines > 50)	
					confirmation required
						"ShoppingListHasMoreThan_50_Items;WillBeSubmittedInABatch.Continue?"
				if (ProcurementTemplate.NoOfLines > 50)
					invoke BatchCopyToRequisitionAllRSS ProcurementTemplateLine
						invoked.PrmProcurementTemplate			    = ProcurementTemplate
						invoked.PrmCompany							= actor.agent(Employee).Employee.Requester.CurrentCompanyForRQC
						invoked.PrmRequisition						= actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC
				else
					for each ProcurementTemplate.ProcurementTemplateLine set
						invoke CopyToRequisitionAll each
							invoked.PrmCompany							= actor.agent(Employee).Employee.Requester.CurrentCompanyForRQC
							invoked.PrmRequisition						= actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC

		RSSLPLCopyQuantityMultiplier is an Instance Action
	    	restricted
            completion message is "<AddAllCompletionMessage>"
			Parameters
				PrmQuantityMultiplier	is an UnsignedQuantity
			Parameter Rules
				PrmQuantityMultiplier
					if (actor.agent(Employee).Employee.Requester.QuantityMultiplierOnShoppingListAllowed)
						required
							"FieldIsRequired"	
			Action Rules
				if (ProcurementTemplate.NoOfLines > 50)	
					confirmation required
						"ShoppingListHasMoreThan_50_Items;WillBeSubmittedInABatch.Continue?"
				if (ProcurementTemplate.NoOfLines > 50)
					invoke BatchCopyToRequisitionAllRSS ProcurementTemplateLine
						invoked.PrmProcurementTemplate			    = ProcurementTemplate
						invoked.PrmCompany							= actor.agent(Employee).Employee.Requester.CurrentCompanyForRQC
						invoked.PrmRequisition						= actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC
						invoked.PrmQuantityMultiplier				= PrmQuantityMultiplier
				else
					for each ProcurementTemplate.ProcurementTemplateLine set
						invoke CopyToRequisitionAll each
							invoked.PrmCompany							= actor.agent(Employee).Employee.Requester.CurrentCompanyForRQC
							invoked.PrmRequisition						= actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC
							invoked.PrmQuantityMultiplier				= PrmQuantityMultiplier
