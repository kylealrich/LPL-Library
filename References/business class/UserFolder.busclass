UserFolder is a BusinessClass
    owned by la
    prefix is UFO

    Ontology
    	symbolic key is UserFolder

    Patterns
    	implements CRUD
    	disable Auditing
		disable EffectiveDated
		implements CreateStamp
		implements UpdateStamp

	Persistent Fields
		Name 			is Alpha size 255
			translatable
		Owner is like Actor // LMRK-97977 Was "is Actor". Likely meant to be "is an Actor". Changed to "like" so we have the same dict type but without changing existing functionality.
		Actor
		ActorGroup
		BusinessClass
		Dynamic 		is Boolean          
		Search 			is Text
		ListName 		is a BusinessList
		ChartName		is a BusinessChart
		AsOfDate 		is Date
		ContextFields	is DataView
		Temporal        is Boolean
		SourceType		is Numeric size 1   
			States
    			List			value is 0
    			Page			value is 1
    			Menu			value is 2
    			ManualReport	value is 3 
		ExtendedInfo	is Text
		PDFFontOffset
			default label is "PDFFontOffset"
		Role
		RoleGroup
		PDFPageOptions
		PDFOrientation
			default label is "PDFOrientation"
		MRBusinessObject is BusinessObjectReference 
			default label is "ManualReportReference"
		MRPrintAction 	is a BusinessAction 
			default label is "ManualReportPrintAction"
		ReportCategory 	  is Alpha size 60 
			translatable
			protected
			default label is "ReportSubCategory"
		ViewArchiveData	  is Boolean
		FromScratch is Boolean

	Transient Fields
		FromType 		is Numeric size 1
			States
				None	value is 0
				List	value is 1
				Report	value is 2
				Menu	value is 3

		Sharing				is Numeric size 1
			States
				Private		value is 1
				Group		value is 2
				Public		value is 3
				Role		value is 4
				RoleGroup	value is 5
			derive value from SharingOption

		DataSource			is Numeric size 1
			States
				Live		value is 0
				AsOfDate	value is 1
				Audit		value is 2
			derive value from DataSourceOption

		ReportLPL		is LPL
			default label is "LPL"
			derive value from UserListRel.Delta
			
		PageReportLPL		is LPL
			default label is "LPL"
			derive value from UserPageRel.LPL
			
		ReportErrorText is Text
			default label is "ErrorText"
			derive value from UserListRel.ErrorText
		
		AsyncID is UniqueID
		
		TransSnapshotItemReportDefInfo is a ConfigSnapshotItemReportDefInfo
			default label is "SnapshotItemReportDefinitionInformation"
			protected
			translatable

        TNoInvalid is Boolean
            default label is "MustBeValid_LPL"

        TListLPL is LPL
            default label is "ReportLPL"

        TReportDescription is Text
            default label is "Description"
            derive value from ReportDescription

        TClearReportDescription is Boolean
            default label is "ClearReportDescription"
	
	Conditions
		ReportHasErrorText
			default label is "ReportErrorText"
			when (ReportErrorText entered)

		IsListReport
			default label is "ListReport"
			when (IsReport and IsList)

		IsActiveListReport
			default label is "ActiveListReport"
			when (IsListReport and UserListRel.Status.Active)
			
		IsActivePagetReport
			default label is "ActivePageReport"
			when (IsPage and UserPageRel.Status.Active)

    	IsReport
    		default label is "Report"
   			when (Dynamic)

   		IsFolder
   			default label is "Folder"
   			when (not IsReport)

   		IsSingleBusClassFolder
   			default label is "SingleBusinessClassFolder"
   			when (IsFolder
   			and   IsBusClassFolder)

   		IsAnyBusClassFolder
   			default label is "AnyBusinessClassFolder"
   			when (IsFolder
   			and   !IsBusClassFolder)

   		IsBusClassFolder
   			default label is "BusinessClassFolder"
   			when (BusinessClass entered
   			and   IsList)

   		IsList
   			default label is "List"
   			when (ListName entered)

   		IsPage
   			default label is "Page"
   			when (UserPageRel exists
   			or    SourceType.Page)
   			
   		IsListOrPage
   		    default label is "ListOrPage"
   			when (IsList or IsPage)
   			
   		IsListReportOrPage
   		    default label is "ListOrPageReport"
   			when (IsListReport or IsPage)

		IsMenu
			default label is "Menu"
   			when (UserMenuRel exists
   			or    SourceType.Menu)
   			
   		IsManualReport
   			default label is "ManualReport"
   			when (SourceType.ManualReport) 

        IsActorGroupValidForActor
        	default label is untranslatable
            when (ActorGroup entered
            and  (ActorCreatedReport
            or    ActorGroupShadowRel exists))
            
        CanTagReport
        	default label is "ReportTaggingAllowed"
        	when ((SourceType.List and HasUserListRecord) or (SourceType.Page and UserPageRel exists) or (SourceType.Menu and UserMenuRel exists))

		IsValidForPDF
			default label is "ValidForPDF"
			when (not SourceType.Menu and not SourceType.ManualReport)
			
		IsValidForPDFExport
			default label is "ValidForPDFExport"
			when (IsValidForPDF and IsValidForExport)

		IsValidForCSV
			default label is "ValidForCSV"
			when (SourceType.List)
			
		IsValidForCSVExport
			default label is "ValidForCSVExport"
			when (IsValidForCSV and IsValidForExport)
			
		IsValidForExport
			default label is "ValidForExport" 
			when ((Actor = blank and ActorGroup = blank and Role = blank and RoleGroup = blank) or (ActorCreatedReport) or (IsActorGroupValidForActor) or (IsValidForRole) or (IsValidForRoleGroup) or create stamp.actor = actor)

		ReportsCreatedByActor
			default label is "MyReports"
			when (IsReport
			and (IsList
			or IsPage
			or IsManualReport)
			and ActorCreatedReport)

		AllReportsViewableForActor
			default label is "AllReportsForActor"
			when (IsReport
			and (IsList
			or IsPage
			or IsManualReport)
			and ((Actor not entered
			and ActorGroup not entered
			and Role not entered
			and RoleGroup not entered)
			or ActorCreatedReport
			or IsActorGroupValidForActor
			or IsValidForRole
			or IsValidForRoleGroup))

   		HasUserReportRecords
   			default label is "ExistingPrintFiles"
   			when (UserFolder entered and UserReportRel exists) 

   		ActorCreatedReport
   			when (Owner = actor
   			or Actor = actor)

        UpdatedLastSevenDays
            when ((current timestamp - update stamp <= 604800))  
            
        IsListTreeOnly
        	restricted
        	when ((IsList and UserBusinessListRel.DisplayAsTree and not UserBusinessListRel.HasAnyDetails)
        	or    (IsPage and UserBusinessPageRel.PDFDefaultDisplayAsTree and not UserBusinessPageRel.PDFDefaultHasAnyDetails)) 
       
       	IsListTreeAndDetails
       		restricted
        	when ((IsList and UserBusinessListRel.DisplayAsTree and UserBusinessListRel.HasAnyDetails)
        	or   (IsPage and UserBusinessPageRel.PDFDefaultDisplayAsTree and UserBusinessPageRel.PDFDefaultHasAnyDetails))
        
        IsListDetailsOnly
       		restricted
        	when ((IsList and not UserBusinessListRel.DisplayAsTree and UserBusinessListRel.HasAnyDetails)
        	or   (IsPage and not UserBusinessPageRel.PDFDefaultDisplayAsTree and UserBusinessPageRel.PDFDefaultHasAnyDetails))
        	
        IsCSVListDetailsOnly
       		restricted
        	when (IsList and not UserBusinessListRel.DisplayAsTree and UserBusinessListRel.HasCSVDetails)
        	
        IsCSVListAuditChanges
       		restricted
        	when (IsList and not UserBusinessListRel.DisplayAsTree and UserBusinessListRel.ShowChangedValuesOnRight)
        	
        IsCSVListDetailsAndAuditChanges
        	restricted
        	when (IsCSVListDetailsOnly and IsCSVListAuditChanges)
        	
    	DisplayDetailsOnlyOptions
       		restricted
        	when (IsCSVListDetailsOnly or IsCSVListAuditChanges)
        	
        IsCSVListDetailsOrBoth
        	restricted
        	when (IsCSVListDetailsOnly or IsCSVListDetailsAndAuditChanges)
        	
        IsCSVListTreeOnly
        	restricted
        	when (IsList and UserBusinessListRel.DisplayAsTree and not UserBusinessListRel.HasCSVDetails)
       
       	IsCSVListTreeAndDetails
       		restricted
        	when (IsList and UserBusinessListRel.DisplayAsTree and UserBusinessListRel.HasCSVDetails)
        	
		ShowCSVExportLevels
			restricted
			when (IsList and (UserBusinessListRel.ImplementsTreeView or UserBusinessListRel.ImplementsCubeView))

        HasUserListRecord
        	default label is "UserListRecordExists"
            when (UserListRel exists)
          
        DisplayMissingUserList
        	when (IsList and not HasUserListRecord)
            
        HasUserPageRecord
        	default label is "UserPageRecordExists"
            when (UserPageRel exists)
            
        HasUserListOrPageRecord
        	default label is "UserListOrPageRecordExists"
            when (HasUserListRecord or HasUserPageRecord)

        HasConfigEntityRecord
            restricted
            when (ConfigEntityRel exists)

        IsInvalidListReport
            restricted
            when (UserListRel.Status.Invalid and IsListReport)
            
        IsInvalidPageReport
            restricted
            when (UserPageRel.Status.Invalid and IsPage)
            
        IsInvalidListOrPageReport 
        	restricted
        	when (IsInvalidListReport or IsInvalidPageReport)
        	
        IsInactiveListReport
            restricted
            when (UserListRel.Status.Inactive and IsListReport)
            
        IsInactivePageReport
            restricted
            when (UserPageRel.Status.Inactive and IsPage)
            
        IsInactiveListOrPageReport 
        	restricted
        	when (IsInactiveListReport or IsInactivePageReport)
          
		IsValidForRole
			restricted
			when (Role entered and ActorRoleRel exists or AuthenticatedActorRoleRel exists)
		
		IsValidForRoleGroup
			restricted
			when (RoleGroup entered and ActorRoleGroupRel exists)
		
		CanConvertToIlineSearchForm
			restricted
			when (IsListReport and UserListRel.CanConvertToIlineSearchForm)
			
		DisplayUserListViewSelect
			restricted
			when (IsList and UserListViewRel exists)
			
		HasMultipleListDetailSections
			restricted
			when (instance count of UserBusListDetailListSectionRel > 1)
			
		SharingIsValid
			default label is "EditSharingAllowed"
			when (not Sharing.Public or not IsListReportOrPage or not ListName.IsReport) 
	
		DisplayGalleryOptions
			restricted
			when (UserBusinessListRel.ImplementsGalleryView and UserBusinessListRel.HasNonHiddenDisplayFields)
			
		MultiThreadingIsValid
			default label is untranslatable
			when (IsList and not UserBusinessListRel.DisplayAsTree and not UserBusinessListRel.ImplementsTreeView and not UserBusinessListRel.ImplementsCubeView and not UserBusinessListRel.IsAuditList and not UserBusinessListRel.IsTotalList)
	
	Derived Fields
		SharingOption is a DerivedField
			type is Numeric size 1
			if (ActorGroup entered)
				return 2
			if (Role entered)
				return 4
			if (RoleGroup entered)
				return 5
			if (Actor not entered)
				return 3
			return  1

		SharingTypeString is a MessageField
			"<Sharing>"

		DataSourceOption is a DerivedField
			type is Numeric size 1
			if (Temporal)
				return 2
			if (AsOfDate entered)
				return 1
			return 0

		Count is a ComputeField
			type is Numeric size 3
			(instance count of UserFolderItem set)
			
		Type is a DerivedField
			type is Alpha size 6

			if (IsReport)
				if (SourceType.Page
			    or  UserPageRel exists)
					return "Page"
				else
				if (SourceType.Menu
			    or  UserMenuRel exists)
					return "Menu"
				else
				if (SourceType.ManualReport)
					return "ManualReport" 
				else
					return "List"
			else
				return "Folder"

		TargetName is a DerivedField
			type is LPLName

			if (UserPageRel exists)
				return UserPageRel.UserPageName
			else
			if (SourceType.Page)
				return "Undefined Page"
			else
			if (UserMenuRel exists)
				return UserMenuRel.UserMenuName
			else
			if (SourceType.Menu)
				return "Undefined Menu"
			else
			if (ListName entered)
				return ListName
			else
			if (SourceType.ManualReport)
				return  MRPrintAction 
			else
				return "Folder"

		PageName is a DerivedField
			type is LPLName
			return UserPageRel.UserPageName

		MenuName is a DerivedField
			type is LPLName
			return UserMenuRel.UserMenuName

        SharedWith is a DerivedField
        	type is Alpha 100

        	if (Actor entered)
        		return Actor
        	else
        	if (ActorGroup entered)
        		return ActorGroup
        	else
        	if (Role entered)
        		return Role
        	else
        	if (RoleGroup entered)
        		return RoleGroup
        	else
        		return PublicMF

        PublicMF is a MessageField
			"PUBLIC"
			
		ActiveMF  is a MessageField
			restricted
			"Active"
			
		InactiveMF  is a MessageField
			restricted
			"Inactive"
			
		InvalidMF  is a MessageField
			restricted
			"Invalid"
			
		UnknownMF  is a MessageField
			restricted
			"Unknown"

        UserListStatus is a DerivedField
            type is Alpha 20
            if (UserListRel.Status.Active
            or  UserListRel not exist)
                return ActiveMF
            else
            if (UserListRel.Status.Inactive)
                return InactiveMF
            else
            if (UserListRel.Status.Invalid)
                return InvalidMF
            else
                return UnknownMF
                
        UserPageStatus is a DerivedField
            type is Alpha 20
            if (UserPageRel.Status.Active)
                return ActiveMF
            else
            if (UserPageRel.Status.Inactive)
                return InactiveMF
            else
            if (UserPageRel.Status.Invalid)
                return InvalidMF
            else
                return UnknownMF
                
        UserListOrPageStatus is a DerivedField
            type is Alpha 20
            
            if (IsPage)
            	return UserPageStatus
            	
            return UserListStatus
                
		DerSessionKeys is a NativeField 
			type is Text
			restricted 
			default label is untranslatable
			
		DefaultOrientation is a DerivedField
			type is Numeric size 1
			restricted
			if (IsPage)
				return 1
			return 0

		MissingUserList is a MessageField
			"MissingUserListRecord!"
			
		MissingUserPage is a MessageField
			"MissingUserPageRecord!"
			
		InitialOrientation is a DerivedField
			type is Numeric size 1
			if (IsPage and UserBusinessPageRel.HasDefaultPDFOrientation)
				return UserBusinessPageRel.DefaultPDFOrientation
			
			if (IsList and UserBusinessListRel.HasDefaultPDFOrientation)
				return UserBusinessListRel.DefaultPDFOrientation
				
			return DefaultOrientation
			
		InitialFontOffset is a DerivedField
			type is Numeric size 2
			if (IsPage and UserBusinessPageRel.HasDefaultPDFFontOffset)
				return UserBusinessPageRel.DefaultPDFFontOffset
			
			if (IsList and UserBusinessListRel.HasDefaultPDFOrientation)
				return UserBusinessListRel.DefaultPDFOrientation
				
			return 0
		
		ViewReportMessage is a LabelField
			restricted
			"ViewReport"
			
		RunReportMessage is a LabelField
			restricted
			"RunReport"
			
		ViewMessage is a DerivedField
			type is MessageField
			
			if (IsManualReport)
				return RunReportMessage
				
			return ViewReportMessage 	
			
		DoubleQuote        is a StringField
            type is Alpha size 1
            restricted
            "\""

		DerPageLPL is a NativeField
			type is Text
			restricted
			default label is untranslatable
			
		DerActorDefaultValue is a DerivedField
            type is Actor
			default label is "DefaultActor"
            if (Sharing.Private)
                return actor
            return blank
            
		ContextArchiveEnabled is a NativeField
			type is Boolean
			restricted
			default label is untranslatable


        ReportDescription is a DerivedField
            type is Text
            return ReportDefinitionRel.Description
            
        ExcelMaxRows is a NativeField 
			type is Numeric size 10
			restricted 
			default label is untranslatable
		
	Field Rules
		Owner
			default to actor
			
		BusinessClass
			if (SourceType.Page
			or  SourceType.Menu)
				constraint (BusinessClass not entered)
					"CannotEnterBusinessClassWhenLinkedToAPageOrMenu"




		Search
			if (SourceType.Page
			or  SourceType.Menu
			or  SourceType.ManualReport)
				constraint (Search not entered)
					"CannotEnterSearchWhenLinkedToAPage,MenuOrManualReport"

			if (not Dynamic)
				cannot be entered
					"ASearchDefinitionIsValidOnlyOnReports"

		AsOfDate
			if (not Dynamic or SourceType.ManualReport)
				cannot be entered
					"AnAsOfDateIsValidOnlyOnDynamicReports"
					
		SourceType
			cannot be changed
				"SourceTypeCannotBeChanged.DeleteAndCreateANewReportToChangeType."
				
		ListName
			constraint (SourceType.List)
				"CannotEnterListNameWhenNotLinkedToAList"

		ChartName
			constraint (SourceType.List)
				"CannotEnterChartNameWhenNotLinkedToAList"

			if (ChartName entered)
				constraint (Dynamic)
					"ChartNameOnlyValidForDynamicFolders"
		ActorGroup
			if (Actor entered or Role entered or RoleGroup entered)
				cannot be entered
					"CannotEnterActorGroupForPrivate,RoleOrRoleGroupBasedUserFolder"
		
		Role
			if (Actor entered or ActorGroup entered or RoleGroup entered)
				cannot be entered
					"CannotEnterRoleForPrivate,ActorGroupOrRoleGroupBasedUserFolder"
		
		RoleGroup
			if (Actor entered or ActorGroup entered or Role entered)
				cannot be entered
					"CannotEnterRoleGroupForPrivate,ActorGroupOrRoleBasedUserFolder"

		PDFFontOffset
			initial value is InitialFontOffset
			if (IsValidForPDF)
				constraint (PDFFontOffset >= -5 and PDFFontOffset <= 20)
					"ValidRangeIsFrom_\-\5To20"
			else
				cannot be entered
					"PDFFontOffsetIsNotValidForThisTypeOfFolder"
					
		PDFOrientation
			initial value is InitialOrientation
			
		MRBusinessObject
			if (SourceType.ManualReport)
				required
				Dynamic = true
				
			constraint (MRBusinessObject.BusinessClassName entered and MRBusinessObject.BusinessObjectKey entered)
				"<MRBusinessObject label>MustBeFullyEntered"
				
			if (not BusinessClass entered)
				BusinessClass = MRBusinessObject.BusinessClassName
			else
				constraint (BusinessClass = MRBusinessObject.BusinessClassName)
					"<BusinessClass label>MustMatchTheBusinessClassNameIn<MRBusinessObject label>"
					
			constraint (not OtherMRBusinessObject exists)
				"ReportDefinitionAlreadyExistsFor<BusinessClass>.<MRPrintAction>"
			
		MRPrintAction
			if (SourceType.ManualReport)
				required
				
				constraint (MRPrintAction.ActionType.Function)
			    		"ActionMustBeAnInstanceAction"

		ReportCategory 
			cannot be changed
				"EditTheReportLPLToChangeApplicationReportCriteria"
				
		ViewArchiveData
			initial value is ContextArchiveEnabled

	Local Fields
		OrigName 			 is Alpha size 255
		OrigBusinessClass 	 is LPLName
		OrigListName	     is LPLName
		LocComma		     is Alpha size 1
		LocalPageName 		 is like BusinessPage      
		LocalPagePanelName 	 is like BusinessPagePanel 
		LocRecordsProcessed  is Numeric size 8	       

	Sets
		AllByName
			indexed
			duplicates
			Sort Order
				Name

		FolderByName
			indexed
			duplicates
			Sort Order
				Name
			Instance Selection
				where (IsFolder)

		ReportByName
			indexed
			duplicates
			Sort Order
				Name
			Instance Selection
				where (IsReport)
		
		ByMRBusinessObject
    		indexed
    		duplicates
    		Sort Order
        		MRBusinessObject
        		MRPrintAction
        ByDynamicReportCategory
        	indexed
        	Sort Order
        	    Dynamic
        		ReportCategory
        		UserFolder

	Relations
		UserListRel
			one-to-one relation to UserList
			Field Mapping uses FolderSet
				related.UserFolder			= UserFolder
				related.BusinessView		= BusinessClass
        		related.BusinessList		= ListName

        UserListNameChangeRel
			one-to-one relation to UserList
			Field Mapping uses FolderNameSet
				related.FolderName			= OrigName
				related.BusinessView		= OrigBusinessClass
        		related.BusinessList		= OrigListName
        		
		UserListNameChangeFolderRel
			one-to-one relation to UserList
			Field Mapping uses FolderSet
				related.UserFolder			= UserFolder
				related.BusinessView		= OrigBusinessClass
        		related.BusinessList		= OrigListName

		FromListNameRel
			one-to-one relation to UserList
			Field Mapping uses ActorSet
				related.Actor				= actor
				related.BusinessView		= BusinessClass
        		related.BusinessList		= ListName

		ListConfigurationRel
			one-to-one relation to UserList
			Field Mapping uses GlobalListSet
				related.BusinessView		= BusinessClass
        		related.BusinessList		= ListName

		BusinessListRel
			one-to-one relation to BusinessList
			Field Mapping uses symbolic key
				related.BusinessView = BusinessClass
				related.BusinessList = ListName
		
		UserBusinessListRel
			one-to-one relation to UserBusinessList
			Field Mapping uses part of key
				related.BusinessView 						= BusinessClass
				related.BusinessList 						= ListName
				related.UserBusinessConfigOwner.Actor   	= blank
				related.UserBusinessConfigOwner.UserFolder	= UserFolder
				
		BusinessPageRel
			one-to-one relation to BusinessPage
			Field Mapping uses symbolic key 
				related.BusinessPage = UserPageRel.UserPageName
		
		UserBusinessPageRel
			one-to-one relation to UserBusinessPage
			Field Mapping uses part of key
				related.BusinessPage						= UserPageRel.UserPageName
				related.UserBusinessConfigOwner.Actor   	= blank
				related.UserBusinessConfigOwner.UserFolder	= UserFolder

        UserPageRel
        	one-to-one relation to UserPage
        	Field Mapping uses FolderSet
        		related.UserFolder = UserFolder
        		
       	UserPagePersRel
        	one-to-one relation to UserPage
        	Field Mapping uses ActorSet
        		related.Actor = actor
    			related.UserPageName = UserPageRel.UserPageName
    			
    	UserPageConfigurationRel
			one-to-one relation to UserPage
			Field Mapping uses GlobalSet
        		related.UserPageName = UserPageRel.UserPageName

		UserMenuRel
        	one-to-one relation to UserMenu
        	Field Mapping uses FolderSet
        		related.UserFolder = UserFolder

        UserReportRel
        	one-to-many relation to UserReport
        	Field Mapping uses ByActorSource
        		related.Actor = actor
        		related.UserFolder = UserFolder

        ActorGroupShadowRel
            one-to-many relation to ActorGroupMemberShadow
            Field Mapping uses ByActorActorGroupResidenceGroup
                related.Actor = actor
                related.ActorGroup = ActorGroup

        ConfigEntityRel
            one-to-one relation to ConfigEntity
            Field Mapping uses ByFolder
                related.UserFolder.BusinessObjectKey = UserFolder

        BusinessViewRel
            one-to-one relation to BusinessView
            Field Mapping uses symbolic key
                related.BusinessView = BusinessClass

        MissingUserListRel
            one-to-many relation to UserFolder
            Field Mapping uses symbolic key
			Instance Selection
				where (related.IsListReport
				and    !related.HasUserListRecord)
				
		MissingUserPageRel
            one-to-many relation to UserFolder
            Field Mapping uses symbolic key
			Instance Selection
				where (related.SourceType.Page
				and    !related.HasUserPageRecord)
				
        InvalidReportsListRel
            one-to-many relation to UserFolder
            Field Mapping uses symbolic key
			Instance Selection
				where (related.IsInvalidListReport
				or     related.IsInvalidPageReport)
				
		ActorRoleRel
            one-to-one relation to ActorRole
            Field Mapping uses symbolic key
            	related.Actor = actor
            	related.ActorRole.Role = Role
						
		AuthenticatedActorRoleRel
            one-to-one relation to ActorRole
            Field Mapping uses symbolic key
            	related.Actor = authenticated actor
            	related.ActorRole.Role = Role
            	
		ActorRoleGroupRel
            one-to-many relation to RoleGroupMember
            Field Mapping uses part of key
            	related.RoleGroup = RoleGroup
            Instance Selection
            	where (related.ActorRoleCurrentActorRel.ActorRole exists or related.ActorRoleCurrentAuthenticatedActorRel exists)       
         
		OtherMRBusinessObject
        	one-to-many relation to UserFolder
         	Field Mapping uses ByMRBusinessObject
         		related.MRBusinessObject = MRBusinessObject
         		related.MRPrintAction = MRPrintAction
         	Instance Selection
         		where (related.UserFolder != UserFolder)
         		
		UserListViewRel
			one-to-many relation to UserListView
			Field Mapping uses ByViewListActor
				related.BusinessView = BusinessClass
        		related.BusinessList = ListName
				related.Actor		 = blank
				related.UserFolder	 = UserFolder
				
		UserBusListDetailListSectionRel
			one-to-many relation to UserBusinessListDetailSection
			Field Mapping uses symbolic key
				related.BusinessView 														= BusinessClass
				related.BusinessList 														= ListName



			Instance Selection
				where (related.UserBusinessListDetailSection.UserBusinessConfigOwner.Actor 	= blank
                and	   related.UserBusinessListDetailSection.UserBusinessConfigOwner.UserFolder	= UserFolder
                and    related.IsList = true)
                
        UserBusListDetailListSectionSeqRel
			one-to-many relation to UserBusinessListDetailSection
			Field Mapping uses BySequence
				related.BusinessView 														= BusinessClass
				related.BusinessList 														= ListName
				related.UserBusinessListDetailSection.UserBusinessConfigOwner.Actor   		= blank
				related.UserBusinessListDetailSection.UserBusinessConfigOwner.UserFolder	= UserFolder
			Instance Selection
				where (related.IsList = true)
				
				
		ReportDefinitionRel
			one-to-one relation to ReportDefinition
			Field Mapping uses ByUserFolder 
				related.ReportDefinition.UserFolder = UserFolder
				
	Rule Blocks
		DynamicActorCheck
			if (ReportDistributionGroup.DynamicActorsOnlyProfiles)
				required
                	"AtLeastOneActorValueIsRequired"
			else
			if (DynamicDistributionActors entered and not ReportDistributionGroup.HasDynamicActorReportDistribution)
				confirmation required
					"NoMemberReportDistributionGroupProfilesThatAcceptDynamicActorsFound.Continue?"
				initialize DynamicDistributionActors
	                
	    AssignSnapshotReportDefInfoInvoke
	    	invoked.ReportName = TransSnapshotItemReportDefInfo.ReportName translations
			invoked.DeliveredReportName = TransSnapshotItemReportDefInfo.DeliveredReportName
			invoked.Description = TransSnapshotItemReportDefInfo.Description translations
			invoked.DeliveredDescription = TransSnapshotItemReportDefInfo.DeliveredDescription
			invoked.Category = TransSnapshotItemReportDefInfo.Category
			invoked.SubCategory = TransSnapshotItemReportDefInfo.SubCategory
			invoked.ReportWebAppOccurs = TransSnapshotItemReportDefInfo.ReportWebAppOccurs
			invoked.Inactive = TransSnapshotItemReportDefInfo.Inactive
			invoked.Validity = TransSnapshotItemReportDefInfo.Validity
			invoked.Validity = TransSnapshotItemReportDefInfo.Visibility
	    				
	    UpdateSnapshotReportDefinition
	    	if (TransSnapshotItemReportDefInfo entered)
	    		if (ReportDefinitionRel exists)
	    			invoke UpdateFromSnapshot ReportDefinitionRel
	    				include AssignSnapshotReportDefInfoInvoke
	    		else
	    			invoke CreateFromSnapshot ReportDefinition
	    				invoked.ReportDefinition.Type = SourceType
	    				
	    				if (SourceType.List)
		    				invoked.ReportDefinition.BusinessView 	= BusinessClass
		    				invoked.ReportDefinition.Name 			= ListName
		    			else
		    			if (SourceType.Page)
		    				invoked.ReportDefinition.BusinessView 	= PageName
		    				invoked.ReportDefinition.Name 			= blank
		    			else
		    			if (SourceType.ManualReport)
		    				invoked.ReportDefinition.BusinessView 	= BusinessClass
		    				invoked.ReportDefinition.Name 			= MRPrintAction
		    				
	    				invoked.ReportDefinition.UserFolder 		= UserFolder
	    				
	    				include AssignSnapshotReportDefInfoInvoke
	    	else
	    	    invoke ProcessFolderReports

	Actions
		Create is a Create Action
			Exit Rules
				if (SourceType.List and (FromType.List or FromType.None))
					if (FromListNameRel exists and FromListNameRel.Status.Active)
						invoke Create UserList
							invoked.BusinessView = FromListNameRel.BusinessView
	        				invoked.BusinessList = FromListNameRel.BusinessList
	        				invoked.UserFolder 	 = UserFolder
	        				invoked.TNoInvalid   = TNoInvalid

	        				if (TListLPL = blank)
	        				    invoked.Delta = FromListNameRel.Delta
                            else
	        				    invoked.Delta = TListLPL
	        		else
	        		if (ListConfigurationRel exists and ListConfigurationRel.Status.Active)
						invoke Create UserList
							invoked.BusinessView = ListConfigurationRel.BusinessView
	        				invoked.BusinessList = ListConfigurationRel.BusinessList
	        				invoked.UserFolder 	 = UserFolder
	        				invoked.TNoInvalid   = TNoInvalid

                            if (TListLPL = blank)
                                invoked.Delta = ListConfigurationRel.Delta
                            else
                                invoked.Delta = TListLPL
	        		else
					if (BusinessListRel exists)
						invoke Create UserList
							invoked.BusinessView = BusinessListRel.BusinessView
	        				invoked.BusinessList = BusinessListRel.BusinessList
	        				invoked.UserFolder 	 = UserFolder
	        				invoked.TNoInvalid   = TNoInvalid

                            if (TListLPL = blank)
                                invoked.Delta = BusinessListRel.LPL
                            else
                                invoked.Delta = TListLPL
	        		else
	        			invoke UpdateApplicationReport 
        		else	
        		if (SourceType.Page)	
	        		invoke UpdateApplicationReport
       
        CreateFromSnapshot is a Create Action
    		restricted
    		bypass field rules
    		
    		Local Fields
    			WebAppsSize is Numeric size 4
    			WebAppsSelect is Alpha size 1000
    			ElementCount is Numeric size 4

        	Exit Rules
        		include UpdateSnapshotReportDefinition

		Update is an Update Action
			Local Fields
				LocForceAppUpdate is Numeric size 1


			Entrance Rules
				OrigName 		  = old Name           
				OrigBusinessClass = old BusinessClass
				OrigListName	  = old ListName

			Exit Rules
				if (BusinessClass changed
				or  ListName changed)
					if (UserListNameChangeRel exists)
						invoke Delete UserListNameChangeRel  
					else
					if (UserListNameChangeFolderRel exists)
						invoke Delete UserListNameChangeFolderRel  
						
					if (SourceType.List)
						if (FromListNameRel exists and FromListNameRel.Status.Active)
							LocForceAppUpdate = 1
							invoke Create UserList
								invoked.BusinessView = FromListNameRel.BusinessView
		        				invoked.BusinessList = FromListNameRel.BusinessList
		        				invoked.UserFolder 	 = UserFolder
		        				invoked.Delta		 = FromListNameRel.Delta
		        		else
		        		if (ListConfigurationRel exists and ListConfigurationRel.Status.Active)
		        			LocForceAppUpdate = 1
							invoke Create UserList
								invoked.BusinessView = ListConfigurationRel.BusinessView
		        				invoked.BusinessList = ListConfigurationRel.BusinessList
		        				invoked.UserFolder 	 = UserFolder
		        				invoked.Delta		 = ListConfigurationRel.Delta
		        		else
						if (BusinessListRel exists)
							LocForceAppUpdate = 1
							invoke Create UserList
								invoked.BusinessView = BusinessListRel.BusinessView
		        				invoked.BusinessList = BusinessListRel.BusinessList
		        				invoked.UserFolder 	 = UserFolder
		        				invoked.Delta	     = BusinessListRel.LPL
		        		else	
		        			LocForceAppUpdate = 2	
				else
				if (Name changed)
					if (UserListNameChangeRel exists)
						invoke Update UserListNameChangeRel
							invoked.FolderName = Name
					if (UserListNameChangeFolderRel exists)
						invoke Update UserListNameChangeFolderRel
							invoked.FolderName = Name
					else
					if (UserPageRel exists)
						invoke ChangePageName UserPageRel
							invoked.NewName = Name
					else
					if (UserMenuRel exists)
						invoke ChangeMenuName UserMenuRel
							invoked.NewName = Name
							
                    if (ReportDefinitionRel exists)
						invoke Update ReportDefinitionRel
							invoked.ReportName = Name translations
							invoked.TransFromUserFolder = true

			    if (TListLPL != blank and UserListRel exists)
                    invoke Update UserListRel
                        invoked.TNoInvalid = TNoInvalid
                        invoked.Delta = TListLPL

                if (TClearReportDescription or TReportDescription != ReportDescription)
                    LocForceAppUpdate = 2

				if (LocForceAppUpdate = 2 
				or (IsList and Dynamic changed)
				or IsListReportOrPage)
					invoke UpdateApplicationReport
		
		UpdateFromSnapshot is an Update Action
			restricted
			bypass field rules
			
			Local Fields
    			WebAppsSize is Numeric size 4
    			WebAppsSelect is Alpha size 1000
    			ElementCount is Numeric size 4

			Exit Rules
				include UpdateSnapshotReportDefinition
				
		UpdateNoRules is an Update Action 
    		restricted
    		bypass field rules
    		
    	ChangeSubCategory is an Instance Action 
    		restricted
    		
    		Parameters
    			ParamCategory is Alpha size 60 
    			
    		Local Fields
    			LocalChangeLpl  is LPL

    		
    		Action Rules
    			if (LocalChangeLpl entered)
	    			if (SourceType.List)
	    				if (UserListRel exists)
							invoke Update UserListRel 
								invoked.Delta = LocalChangeLpl
	    			else
	    			if (SourceType.Page)
	    				if (UserPageRel exists)
	    					invoke Update UserPageRel  
								invoked.LPL = LocalChangeLpl







		View is an Update Action 

		Delete is a Delete Action
			Action Rules
				invoke Delete UserListRel 
				invoke Delete UserPageRel
				invoke Delete UserMenuRel
				invoke Delete ReportDefinitionRel
				
		DeleteFromSnapshot is a Delete Action
			restricted
			bypass relational integrity rules
			
			Entrance Rules
				invoke DeleteTranslations 
				invoke Delete ReportDefinitionRel
				
		DeleteTranslations is an Instance Action
			restricted

		
		TagItem is an Instance Action
			valid when (CanTagReport)
			Parameters
				ParamConfigurationTag is a ConfigurationTag
					default label is "Tag"
			
			Action Rules
				if (SourceType.List)
					invoke TagItem UserListRel
						invoked.ParamConfigurationTag = ParamConfigurationTag
				else
				if (SourceType.Page)
					invoke TagItem UserPageRel
						invoked.ParamConfigurationTag = ParamConfigurationTag
				else
				if (SourceType.Menu)
					invoke TagItem UserMenuRel
						invoked.ParamConfigurationTag = ParamConfigurationTag

		Purge is a Delete Action 
			restricted
			Parameters
				Purge                    is Boolean
				DeleteRelatedUserReports is Boolean

			Parameter Rules
				Purge
					initial value is true

			Action Rules
				if (Purge)
					invoke Purge UserListRel 
					invoke Purge UserPageRel
					invoke Purge UserMenuRel
					invoke Purge ReportDefinitionRel
				else
					invoke Delete UserListRel 
					invoke Delete UserPageRel
					invoke Delete UserMenuRel
					invoke Delete ReportDefinitionRel

				if (DeleteRelatedUserReports)
					invoke PurgeFolderReports UserReport
						invoked.FolderUID = UserFolder
						invoked.ThruDate = system current date

		PurgeFoldersForActor is a Set Action
			Parameters
				ParamActor is an Actor
					default label is "Actor"
				ParamDeletedActor is like Actor
					default label is "DeletedActor"
				PhysicallyDeleteRelatedData is Boolean
				DeleteRelatedUserReports    is Boolean

			Parameter Rules
				ParamActor
					if (ParamActor entered)
						ParamDeletedActor = ParamActor
					else
					if (ParamDeletedActor not entered)
						required
							"MustEnterActorOrDeletedActor"

			Instance Selection
				where (Actor = ParamDeletedActor)

			Sort Order
				Actor
    			Name

			Action Rules
				Instance Rules
					invoke Purge
						invoked.Purge 					 = PhysicallyDeleteRelatedData
						invoked.DeleteRelatedUserReports = DeleteRelatedUserReports

		ReassignReportOwner is an Instance Action
			valid when (IsReport)
			Parameters
				PrmNewOwner is an Actor
					default label is "NewOwner"
			Parameter Rules
				PrmNewOwner
					required
						"MustEnterNewOwner"
			Action Rules
				Owner = PrmNewOwner

			Exit Rules
				if (SourceType.List and UserListRel exists)
					invoke ReassignOwner UserListRel
						invoked.PrmNewOwner = Owner
				else
				if (SourceType.Page and UserPageRel exists)
					invoke ReassignOwner UserPageRel
						invoked.PrmNewOwner = Owner
				else
				if (SourceType.Menu and UserMenuRel exists)
					invoke ReassignOwner UserMenuRel
						invoked.PrmNewOwner = Owner


		MassReassignReportOwners is a Set Action
			Parameters
				PrmCurrentOwner is an Actor
					default label is "CurrentOwner"
				PrmNewOwner is an Actor
					default label is "NewOwner"

			Parameter Rules
				PrmCurrentOwner
					required
						"MustEnterCurrentOwner"
				PrmNewOwner
					required
						"MustEnterNewOwner"

			Instance Selection
				where (Owner = PrmCurrentOwner
				and    IsReport)

			Sort Order
				Owner

			Action Rules
				Instance Rules
					invoke ReassignReportOwner
						invoked.PrmNewOwner = PrmNewOwner

		EditReport is an Update Action
			valid when (IsListReportOrPage)
			completion message is "Changes_Saved"

			Local Fields
				LocalUserList is a UserList view
				LocalListLpl  is LPL

			Field Rules
				ActorGroup
					if (Sharing.Group)
						required
					else
						cannot be entered
							"ActorGroupIsOnlyValidWhenSharingWithAGroup"
				Role
					if (Sharing.Role)
						required
					else
						cannot be entered
							"RoleIsOnlyValidWhenSharingWithARole"
				RoleGroup
					if (Sharing.RoleGroup)
						required
					else
						cannot be entered
							"RoleGroupIsOnlyValidWhenSharingWithARoleGroup"
				AsOfDate
					if (DataSource.AsOfDate)
						required
				
				DataSource
					constraint (not IsPage or not DataSource.Audit)
						"AuditDataSourceIsNotValidForAPage"

			Action Rules
				if (Sharing.Public)
					ActorGroup 	= blank
					Actor 		= blank
					Role		= blank
					RoleGroup   = blank
				else
				if (Sharing.Group)
					ActorGroup 	= ActorGroup
					Actor 		= blank
					Role        = blank
					RoleGroup   = blank
				else
				if (Sharing.Role)
					ActorGroup  = blank
					Actor       = blank
					RoleGroup   = blank
				else
				if (Sharing.RoleGroup)
					ActorGroup  = blank
					Actor       = blank
					Role		= blank
				else
				if (Sharing.Private or Sharing not entered)
					Actor 		= actor
					ActorGroup 	= blank
					Role        = blank
					RoleGroup   = blank
					
				if (DataSource.Audit)
					Temporal	= true
					AsOfDate	= blank
				else
				if (DataSource.AsOfDate)
					AsOfDate 	= AsOfDate
					Temporal	= blank
				if (DataSource.Live or DataSource not entered)
					AsOfDate 	= blank	
					Temporal	= blank

			Exit Rules
				if (not IsPage)
					if (not UserListRel exists)
						if (FromListNameRel exists and FromListNameRel.Status.Active)
							LocalListLpl = FromListNameRel.Delta
						else
						if (ListConfigurationRel exists and ListConfigurationRel.Status.Active)
							LocalListLpl = ListConfigurationRel.Delta
						else
						if (BusinessListRel exists)
							LocalListLpl =  BusinessListRel.LPL
						
						invoke Create UserList
							assign result to LocalUserList
							invoked.BusinessView = BusinessClass
							invoked.BusinessList = ListName
							invoked.UserFolder 	 = UserFolder
							invoked.Delta		 = LocalListLpl
							
						ReportErrorText = LocalUserList.ErrorText

				if (Name changed)
                    invoke ProcessFolderReports

		CreateListReport is a Create Action
			completion message is "ReportCreated"
			Parameters
				ReportName			is Alpha size 255
				Sharing				is Numeric size 1
					States
						Private		value is 1
						Group		value is 2
						Public		value is 3
						Role		value is 4
						RoleGroup	value is 5
				ActorGroup
				Role
				RoleGroup
				DataSource			is Numeric size 1
					States
						Live		value is 0
						AsOfDate	value is 1
						Audit		value is 2
				AsOfDate 		is Date
				BusinessView
					default label is "BusinessClass"
				List is a BusinessList
				PageOptions is a PDFPageOptions
				Orientation is a PDFOrientation
				FontOffset is Numeric size 2
					default label is "FontPointSizeOffset"
				ViewArchiveData		is Boolean
				
			Parameter Rules
				ReportName
					required
				BusinessView
					required
			    List
			        required
				ActorGroup
					if (Sharing.Group)
						required
					else
						cannot be entered
							"ActorGroupIsOnlyValidWhenSharingWithAGroup"
				Role
					if (Sharing.Role)
						required
					else
						cannot be entered
							"RoleIsOnlyValidWhenSharingWithARole"
				RoleGroup
					if (Sharing.RoleGroup)
						required
					else
						cannot be entered
							"RoleGroupIsOnlyValidWhenSharingWithARoleGroup"
				AsOfDate
					if (DataSource.AsOfDate)
						required
				
				PageOptions
					required
				
				Orientation
					initial value is List.DefaultPDFOrientation
				
				FontOffset
					initial value is List.DefaultPDFFontOffset
					
			Action Rules
				invoke Create this instance
					invoked.Name			= ReportName
					invoked.SourceType		= 0				
					invoked.Dynamic			= true
					invoked.BusinessClass 	= BusinessView
					invoked.ListName 		= List
					invoked.PDFPageOptions  = PageOptions
					invoked.PDFOrientation 	= Orientation
					invoked.PDFFontOffset 	= FontOffset
					invoked.ViewArchiveData	= ViewArchiveData

					if (Sharing.Private)
						invoked.Actor = actor
					else
					if (Sharing.Group)
						invoked.ActorGroup = ActorGroup
					else
					if (Sharing.Role)
						invoked.Role = Role
					else
					if (Sharing.RoleGroup)
						invoked.RoleGroup = RoleGroup

					if (DataSource.Audit)
						invoked.Temporal	= true
					else
					if (DataSource.AsOfDate)
						invoked.AsOfDate 	= AsOfDate

				
		CreatePageReport is a Create Action
			completion message is "ReportCreated"
			Parameters
				ReportName			is Alpha size 255
				Sharing				is Numeric size 1
					States
						Private		value is 1
						Group		value is 2
						Public		value is 3
						Role		value is 4
						RoleGroup	value is 5
				ActorGroup
				Role
				RoleGroup
				DataSource			is Numeric size 1
					States
						Live		value is 0
						AsOfDate	value is 1
						Audit		value is 2
				AsOfDate 		is Date
				BusinessPage
					default label is "Page"
				BusinessPagePanel
					default label is "Panel"
				PageOptions is a PDFPageOptions
				Orientation is a PDFOrientation
				FontOffset is Numeric size 2
					default label is "FontPointSizeOffset"
				ViewArchiveData		is Boolean
				
			Parameter Rules
				ReportName
					required
				BusinessPage
					required
			    BusinessPagePanel
			        required
				ActorGroup
					if (Sharing.Group)
						required
					else
						cannot be entered
							"ActorGroupIsOnlyValidWhenSharingWithAGroup"
				Role
					if (Sharing.Role)
						required
					else
						cannot be entered
							"RoleIsOnlyValidWhenSharingWithARole"
				RoleGroup
					if (Sharing.RoleGroup)
						required
					else
						cannot be entered
							"RoleGroupIsOnlyValidWhenSharingWithARoleGroup"
				AsOfDate
					if (DataSource.AsOfDate)
						required
				
				PageOptions
					required
					
				Orientation
					initial value is BusinessPage.DefaultPDFOrientation
				
				FontOffset
					initial value is BusinessPage.DefaultPDFFontOffset
					
			Local Fields
				LocalUserFolder is a UserFolder

			Action Rules
				invoke Create this instance
					LocalUserFolder = this instance.UserFolder
					
					invoked.Name			= ReportName
					invoked.SourceType		= 1				
					invoked.Dynamic			= true
					invoked.PDFPageOptions  = PageOptions
					invoked.PDFOrientation 	= Orientation
					invoked.PDFFontOffset 	= FontOffset
					invoked.ViewArchiveData	= ViewArchiveData

					if (Sharing.Private)
						invoked.Actor = actor
					else
					if (Sharing.Group)
						invoked.ActorGroup = ActorGroup
					else
					if (Sharing.Role)
						invoked.Role = Role
					else
					if (Sharing.RoleGroup)
						invoked.RoleGroup = RoleGroup

					if (DataSource.AsOfDate)
						invoked.AsOfDate 	= AsOfDate
				
			Exit Rules
				LocalPageName = BusinessPage
				LocalPagePanelName = BusinessPagePanel
				
				invoke Create UserPage
					invoked.UserPageName = BusinessPage
					invoked.Actor = blank
					invoked.UserFolder = LocalUserFolder
					invoked.Status = 0 
					invoked.LPL = this instance.DerPageLPL

		ResetListReport is an Instance Action
		    default label is "Reset_LPL"
			valid when (IsListReport)
			Local Fields
				LocalListLpl 	is LPL

			Action Rules
			    if (FromScratch)
			        constraint (HasUserListRecord and UserListRel.Status.Active and UserListRel.InitialConfiguration != blank)
			            "ReportInitial_L\P\LNotFound."
                    LocalListLpl = UserListRel.InitialConfiguration
                else
				if (FromListNameRel exists and FromListNameRel.Status.Active)
					LocalListLpl = FromListNameRel.Delta
				else
				if (ListConfigurationRel exists and ListConfigurationRel.Status.Active)
					LocalListLpl = ListConfigurationRel.Delta
				else
				if (BusinessListRel exists)
					LocalListLpl =  BusinessListRel.LPL

				if (LocalListLpl entered)
					if (UserListRel exists)
						invoke Update UserListRel
							invoked.Delta		 = LocalListLpl
					else
						invoke Create UserList
							invoked.BusinessView = BusinessClass
							invoked.BusinessList = ListName
							invoked.UserFolder 	 = UserFolder
							invoked.Delta		 = LocalListLpl
							
		ResetPageReport is an Instance Action
		    default label is "Reset_LPL"
			valid when (IsPage)
			Local Fields
				LocalPageLpl 	is LPL

			Action Rules
				if (UserPagePersRel exists and UserPagePersRel.Status.Active)
					LocalPageLpl = UserPagePersRel.LPL
				else
				if (UserPageConfigurationRel exists and UserPageConfigurationRel.Status.Active)
					LocalPageLpl = UserPageConfigurationRel.LPL
				else
				if (BusinessPageRel exists)
					LocalPageName = BusinessPageRel.BusinessPage 
					LocalPagePanelName = blank 
					LocalPageLpl = DerPageLPL

				if (LocalPageLpl entered)
					invoke Update UserPageRel
						invoked.LPL = LocalPageLpl

		ActivateListReport is an Instance Action
			restricted
			valid when (not IsActiveListReport)
			default label is "Activate"

			Action Rules
				invoke Activate UserListRel
				
		ActivatePageReport is an Instance Action
			restricted
			valid when (not IsActivePagetReport)
			default label is "Activate"

			Action Rules
				invoke Activate UserPageRel

		DeactivateListReport is an Instance Action
			restricted
			valid when (IsActiveListReport)	
			default label is "Deactivate"

			Action Rules
				invoke Deactivate UserListRel
				
		DeactivatePageReport is an Instance Action
			restricted
			valid when (IsActivePagetReport)	
			default label is "Deactivate"

			Action Rules
				invoke Deactivate UserPageRel
				
        UpdateApplicationReport is an Instance Action
        	restricted
        	default label is "UpdateReportSettingsFromSource"
        	valid when (IsListReportOrPage)

			Exit Rules 
				invoke ProcessFolderReports

		SchedulePrintToPDF is an Instance Action
			valid when (IsValidForPDFExport)
			run in background
			
			Parameters
				PageOptions is a PDFPageOptions
				Orientation is Numeric size 1
					States
						Landscape value is 0
						Portrait value is 1
				FontOffset is Numeric size 2
					default label is "FontPointSizeOffset"
				IncludeSearchForm is Boolean
				ReportName is Alpha size 255
					default label is "Name"
				RecordLimit is Numeric size 6
				AllRecords is Boolean
				TreeOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintRootLevelOnly 							value is 0
						PrintAllLevels								value is 4
						SpecifiedMaximumLevelsToPrint				value is 6
						
				DetailsOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintCurrentListOnly 						value is 1
						PrintCurrentListWithDetails 				value is 3
					
				TreeAndDetailsOptions is Numeric size 1
					default label is "Options"	
					States
						PrintRootLevelOnly 							value is 0
						PrintRootLevelOnlyWithDetails				value is 2
						PrintAllLevels								value is 4
						PrintAllLevelsWithDetails					value is 5
						SpecifiedMaximumLevelsToPrintWithDetails 	value is 7
				
				MaxLevels is Numeric size 4
					default label is "MaximumLevels"
					
				ReportDistributionGroup
				SessionKeys is Text
				DynamicDistributionActors
				ParamUserListView is a UserListView
					default label is "SavedSearchCriteria"
				GalleryViewOption is Numeric size 1
					States
						PrintGalleryView 	value is 0
						PrintListView		value is 1
										
			Parameter Rules
				PageOptions
					required
					initial value is PDFPageOptions
					
				Orientation
					initial value is PDFOrientation
					
				ReportName
					initial value is Name translation
					default to Name translation
					required
					
				RecordLimit
					initial value is 5000
					
					if (RecordLimit < 0)
						RecordLimit = 0
						
				FontOffset
					initial value is PDFFontOffset
					
					constraint (FontOffset >= -5 and FontOffset <= 20)
						"ValidRangeIsFrom_\-\5To20"
				DetailsOnlyOptions
					initial value is 1 		
				MaxLevels
					initial value is 3
					
					if ((IsListTreeAndDetails and TreeAndDetailsOptions.SpecifiedMaximumLevelsToPrintWithDetails) 
					or (IsListTreeOnly and TreeOnlyOptions.SpecifiedMaximumLevelsToPrint))
						constraint (MaxLevels > 0)
							"MaximumLevelsCannotBeLessThanOne"			
				SessionKeys


					initial value is DerSessionKeys
					
				DynamicDistributionActors	
					include DynamicActorCheck
					
				ParamUserListView
					constraint (not ParamUserListView entered or ParamUserListView.Status.Active)
						"SavedSearchCriteriaIsNotActive"
					 
			Action Rules
				invoke PrintToPDF
					invoked.PageOptions = PageOptions
					invoked.Orientation = Orientation
					invoked.FontOffset = FontOffset
					invoked.IncludeSearchForm = IncludeSearchForm
					invoked.ReportName = ReportName
					invoked.RecordLimit = RecordLimit
					invoked.AllRecords = AllRecords
					invoked.TreeOnlyOptions = TreeOnlyOptions
					invoked.DetailsOnlyOptions = DetailsOnlyOptions
					invoked.TreeAndDetailsOptions = TreeAndDetailsOptions
					invoked.MaxLevels = MaxLevels
					invoked.ReportDistributionGroup = ReportDistributionGroup
					invoked.SessionKeys = SessionKeys
					invoked.DynamicDistributionActors = DynamicDistributionActors
					invoked.GalleryViewOption = GalleryViewOption
				
		PrintToPDF is an Instance Action
			valid when (IsValidForPDFExport)
			completion message is "PDFGenerationSubmitted"
			
			Parameters
				PageOptions is a PDFPageOptions
				Orientation is a PDFOrientation
				FontOffset is Numeric size 2
					default label is "FontPointSizeOffset"
				IncludeSearchForm is Boolean
				ReportName is Alpha size 255
					default label is "Name"
				RecordLimit is Numeric size 6
				AllRecords is Boolean
				TreeOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintRootLevelOnly 							value is 0
						PrintAllLevels								value is 4
						SpecifiedMaximumLevelsToPrint				value is 6
						
				DetailsOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintCurrentListOnly 						value is 1
						PrintCurrentListWithDetails 				value is 3
					
				TreeAndDetailsOptions is Numeric size 1
					default label is "Options"	
					States
						PrintRootLevelOnly 							value is 0
						PrintRootLevelOnlyWithDetails				value is 2
						PrintAllLevels								value is 4
						PrintAllLevelsWithDetails					value is 5
						SpecifiedMaximumLevelsToPrintWithDetails 	value is 7
				
				MaxLevels is Numeric size 4
					default label is "MaximumLevels"
				
				ReportDistributionGroup
				SessionKeys is Text
				DynamicDistributionActors
				ParamUserListView is a UserListView
					default label is "SavedSearchCriteria"
				GalleryViewOption is Numeric size 1
					States
						PrintGalleryView 	value is 0
						PrintListView		value is 1
			
			Parameter Rules
				PageOptions
					required
					initial value is PDFPageOptions
					
				Orientation
					initial value is PDFOrientation
					
				ReportName
					initial value is Name translation
					default to Name translation
				
				RecordLimit
					initial value is 5000
					
					if (RecordLimit < 0)
						RecordLimit = 0
						
				FontOffset
					initial value is PDFFontOffset
					
					constraint (FontOffset >= -5 and FontOffset <= 20)
						"ValidRangeIsFrom_\-\5To20"
				DetailsOnlyOptions
					initial value is 1 
				MaxLevels
					initial value is 3
					
					if ((IsListTreeAndDetails and TreeAndDetailsOptions.SpecifiedMaximumLevelsToPrintWithDetails) 
					or (IsListTreeOnly and TreeOnlyOptions.SpecifiedMaximumLevelsToPrint))
						constraint (MaxLevels > 0)
							"MaximumLevelsCannotBeLessThanOne"
				
				DynamicDistributionActors	
					include DynamicActorCheck
					
				ParamUserListView
					constraint (not ParamUserListView entered or ParamUserListView.Status.Active)
						"SavedSearchCriteriaIsNotActive"
				


		
		ScheduleExportToCSV is an Instance Action
			valid when (IsValidForCSVExport)	
			run in background
						
			Parameters
				Format is Numeric size 1
					States
						CSV value is 0
						Excel value is 1
				Headers is Numeric size 1
					States
						FieldID value is 0
						FieldLabel value is 1
				ReportName is Alpha size 255
				RecordLimit is Numeric size 6
				AllRecords is Boolean
				TreeOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintAllLevels								value is 4
						SpecifiedMaximumLevelsToPrint				value is 6
				DetailsOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintCurrentListOnly 						value is 1
						PrintCurrentListWithDetails 				value is 3
				TreeAndDetailsOptions is Numeric size 1
					default label is "Options"	
					States
						PrintAllLevels								value is 4
						PrintAllLevelsWithDetails					value is 5
						SpecifiedMaximumLevelsToPrint				value is 6
						SpecifiedMaximumLevelsToPrintWithDetails 	value is 7
				MaxLevels is Numeric size 4
					default label is "MaximumLevels"
				ReportDistributionGroup
				SessionKeys is Text
				DynamicDistributionActors
				ParamUserListView is a UserListView
					default label is "SavedSearchCriteria"
				IncludeKeyContext is Boolean
				BusinessView 
				BusinessList 
				DetailPanelOptions is Numeric size 1
					default label is "DetailOptions"
					States
						AllFieldsFromHeaderAndDetail 				value is 1
						KeyContextAndDetailFields					value is 2
						DetailFieldsOnly							value is 3
				UserBusinessListDetailSection
					default label is "DetailSection"
				DetailType is Numeric size 1
					States
						DetailSection 								value is 0
						AuditChanges								value is 1
				MultiThreadField is a BusinessField
				NumberOfThreads is Numeric size 2
					
			Parameter Rules
				ReportName
					initial value is Name translation
					default to Name translation
					
				RecordLimit
					initial value is 5000
					
					if (RecordLimit < 0)
						RecordLimit = 0
					else
						constraint(not Format.Excel or RecordLimit <= ExcelMaxRows)
							"MaximumNumberOfRowsAllowedFor_ExcelIs<ExcelMaxRows>"
				
				TreeOnlyOptions
					initial value is 4
					
				DetailsOnlyOptions
					initial value is 1 
					
				TreeAndDetailsOptions
					initial value is 4
					
				MaxLevels
					initial value is 3
					
					if ((ShowCSVExportLevels and TreeOnlyOptions.SpecifiedMaximumLevelsToPrint))
						constraint (MaxLevels > 0)
							"MaximumLevelsCannotBeLessThanOne"
							
				SessionKeys


					initial value is DerSessionKeys	
				
				DynamicDistributionActors	
					include DynamicActorCheck
					
				ParamUserListView
					constraint (not ParamUserListView entered or ParamUserListView.Status.Active)
						"SavedSearchCriteriaIsNotActive"
				
				BusinessView
					initial value is BusinessClass
					force default to BusinessClass
				
				BusinessList
					initial value is ListName
					force default to ListName
					
				DetailPanelOptions
					initial value is 1 
					
					if ((IsCSVListDetailsOnly and DetailsOnlyOptions.PrintCurrentListWithDetails and (not UserBusinessListRel.ShowChangedValuesOnRight or DetailType.DetailSection))
					or  (ShowCSVExportLevels and IsCSVListTreeAndDetails and (TreeAndDetailsOptions.PrintAllLevelsWithDetails or TreeAndDetailsOptions.SpecifiedMaximumLevelsToPrintWithDetails)))
						required
					
				UserBusinessListDetailSection
					initial value is first UserBusListDetailListSectionSeqRel.UserBusinessListDetailSection
					
					if ((IsCSVListDetailsOnly and DetailsOnlyOptions.PrintCurrentListWithDetails and (not UserBusinessListRel.ShowChangedValuesOnRight or DetailType.DetailSection))
					or  (ShowCSVExportLevels and IsCSVListTreeAndDetails and (TreeAndDetailsOptions.PrintAllLevelsWithDetails or TreeAndDetailsOptions.SpecifiedMaximumLevelsToPrintWithDetails)))
						default to first UserBusListDetailListSectionSeqRel.UserBusinessListDetailSection
					else
						initialize UserBusinessListDetailSection
						
				MultiThreadField
					if (Format.CSV and MultiThreadingIsValid)
						if (NumberOfThreads > 1)
							required
							constraint(MultiThreadField.RepresentationType.Numeric
				                    or MultiThreadField.RepresentationType.Integer
				                    or MultiThreadField.RepresentationType.Date
				                    or MultiThreadField.RepresentationType.TimeStamp
				                    or MultiThreadField.RepresentationType.UniqueID)
								"MultiThreadFieldMustBeA_Numeric,_Integer,_Date,_TimestampTypeOr_Unique\I\D"
					else
						initialize MultiThreadField
						
				NumberOfThreads
					if (Format.CSV and MultiThreadingIsValid)
						if (MultiThreadField entered)
							required
						
						constraint (NumberOfThreads >= 0)
							"NumberOfThreadsCannotBeNegative"
					else
						initialize NumberOfThreads
					
			Action Rules
				invoke ExportToCSV
					invoked.Format = Format
					invoked.Headers = Headers
					invoked.ReportName = ReportName
					invoked.RecordLimit = RecordLimit
					invoked.AllRecords = AllRecords
					invoked.TreeOnlyOptions = TreeOnlyOptions
					invoked.DetailsOnlyOptions = DetailsOnlyOptions
					invoked.TreeAndDetailsOptions = TreeAndDetailsOptions
					invoked.MaxLevels = MaxLevels
					invoked.ReportDistributionGroup = ReportDistributionGroup
					invoked.SessionKeys = SessionKeys
					invoked.DynamicDistributionActors = DynamicDistributionActors
					invoked.IncludeKeyContext = IncludeKeyContext
					invoked.DetailPanelOptions = DetailPanelOptions
					invoked.UserBusinessListDetailSection = UserBusinessListDetailSection
					invoked.DetailType = DetailType
					invoked.MultiThreadField = MultiThreadField
					invoked.NumberOfThreads = NumberOfThreads
		
		ExportToCSV is an Instance Action
			valid when (IsValidForCSVExport)	
			completion message is "CSVGenerationSubmitted"
			
			Parameters
				Format is Numeric size 1
					States
						CSV value is 0
						Excel value is 1
				Headers is Numeric size 1
					States
						FieldID value is 0
						FieldLabel value is 1
				ReportName is Alpha size 255
				RecordLimit is Numeric size 6
				AllRecords is Boolean
				TreeOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintAllLevels								value is 4
						SpecifiedMaximumLevelsToPrint				value is 6
				DetailsOnlyOptions is Numeric size 1
					default label is "Options"
					States
						PrintCurrentListOnly 						value is 1
						PrintCurrentListWithDetails 				value is 3
				TreeAndDetailsOptions is Numeric size 1
					default label is "Options"	
					States
						PrintAllLevels								value is 4
						PrintAllLevelsWithDetails					value is 5
						SpecifiedMaximumLevelsToPrint				value is 6
						SpecifiedMaximumLevelsToPrintWithDetails 	value is 7
				MaxLevels is Numeric size 4
					default label is "MaximumLevels"
				ReportDistributionGroup
				SessionKeys is Text
				DynamicDistributionActors
				ParamUserListView is a UserListView
					default label is "SavedSearchCriteria"
				IncludeKeyContext is Boolean
				BusinessView 
				BusinessList 
				DetailPanelOptions is Numeric size 1
					default label is "DetailOptions"
					States
						AllFieldsFromHeaderAndDetail 				value is 1
						KeyContextAndDetailFields					value is 2
						DetailFieldsOnly							value is 3
				UserBusinessListDetailSection
					default label is "DetailSection"
				DetailType is Numeric size 1
					States
						DetailSection 								value is 0
						AuditChanges								value is 1
				MultiThreadField is a BusinessField
				NumberOfThreads is Numeric size 2
					
			Parameter Rules
				ReportName
					initial value is Name translation
					default to Name translation
					
				RecordLimit
					initial value is 5000
					
					if (RecordLimit < 0)
						RecordLimit = 0
					else
						constraint(not Format.Excel or RecordLimit <= ExcelMaxRows)
							"MaximumNumberOfRowsAllowedFor_ExcelIs<ExcelMaxRows>"
				
				TreeOnlyOptions
					initial value is 4
					
				DetailsOnlyOptions
					initial value is 1 
					
				TreeAndDetailsOptions
					initial value is 4
					
				MaxLevels
					initial value is 3
					
					if ((ShowCSVExportLevels and TreeOnlyOptions.SpecifiedMaximumLevelsToPrint))
						constraint (MaxLevels > 0)
							"MaximumLevelsCannotBeLessThanOne"		
				
				DynamicDistributionActors	
					include DynamicActorCheck
				
				ParamUserListView
					constraint (not ParamUserListView entered or ParamUserListView.Status.Active)
						"SavedSearchCriteriaIsNotActive"
				
				BusinessView
					initial value is BusinessClass
					force default to BusinessClass
				
				BusinessList
					initial value is ListName
					force default to ListName
					
				DetailPanelOptions
					initial value is 1 
					
					if ((IsCSVListDetailsOnly and DetailsOnlyOptions.PrintCurrentListWithDetails and (not UserBusinessListRel.ShowChangedValuesOnRight or DetailType.DetailSection))
					or  (ShowCSVExportLevels and IsCSVListTreeAndDetails and (TreeAndDetailsOptions.PrintAllLevelsWithDetails or TreeAndDetailsOptions.SpecifiedMaximumLevelsToPrintWithDetails)))
						required
					
				UserBusinessListDetailSection
					initial value is first UserBusListDetailListSectionSeqRel.UserBusinessListDetailSection
					
					if ((IsCSVListDetailsOnly and DetailsOnlyOptions.PrintCurrentListWithDetails and (not UserBusinessListRel.ShowChangedValuesOnRight or DetailType.DetailSection))
					or  (ShowCSVExportLevels and IsCSVListTreeAndDetails and (TreeAndDetailsOptions.PrintAllLevelsWithDetails or TreeAndDetailsOptions.SpecifiedMaximumLevelsToPrintWithDetails)))
						default to first UserBusListDetailListSectionSeqRel.UserBusinessListDetailSection
					else
						initialize UserBusinessListDetailSection
					
				MultiThreadField
					if (Format.CSV and MultiThreadingIsValid)
						if (NumberOfThreads > 1)
							required
							constraint(MultiThreadField.RepresentationType.Numeric
				                    or MultiThreadField.RepresentationType.Integer
				                    or MultiThreadField.RepresentationType.Date
				                    or MultiThreadField.RepresentationType.TimeStamp
				                    or MultiThreadField.RepresentationType.UniqueID)
								"MultiThreadFieldMustBeA_Numeric,_Integer,_Date,_TimestampTypeOr_Unique\I\D"
					else
						initialize MultiThreadField
						
				NumberOfThreads
					if (Format.CSV and MultiThreadingIsValid)
						if (MultiThreadField entered)
							required
					else
						initialize NumberOfThreads
					



	    UpdateDescription is an Instance Action
	    	valid when (HasUserListRecord)
			disable multiple instance selection
            completion message is "DescriptionUpdated"

			Parameters
				ParamDescription is a ConfigDescription
					default label is "Description"

			Parameter Rules
				ParamDescription
					initial value is UserListRel.Description

            Action Rules
                invoke Update UserListRel
                        invoked.Description = ParamDescription
                        
        UpdatePageDescription is an Instance Action
	    	valid when (HasUserPageRecord)
	    	default label is "UpdateDescription"
			disable multiple instance selection
            completion message is "DescriptionUpdated"

			Parameters
				ParamDescription is a ConfigDescription
					default label is "Description"

			Parameter Rules
				ParamDescription
					initial value is UserPageRel.Description

            Action Rules
                invoke Update UserPageRel
                        invoked.Description = ParamDescription
                        
		CreateManualReport is a Create Action
			completion message is "ReportCreated"
			restricted
			Parameters
				ReportName			is Alpha size 255
				Sharing				is Numeric size 1
					States
						Private		value is 1
						Group		value is 2
						Public		value is 3
						Role		value is 4
						RoleGroup	value is 5
				ActorGroup
				Role
				RoleGroup
				ParamBusinessClass is a BusinessClass
					default label is "BusinessClass"
				ParamBusObj is BusinessObjectReference
					default label is "ManualReportReference"
				ParamAction is a BusinessAction 
					default label is "ManualReportReference"
				
			Parameter Rules
				ReportName
					required
				ParamBusinessClass
					required
					if (ParamBusObj.BusinessClassName entered)
						constraint (ParamBusinessClass = ParamBusObj.BusinessClassName)
							"ReferenceBusinessClass<ParamBusObj.BusinessClassName>MustMatchBusinessClass<ParamBusinessClass>"
					ParamBusObj.BusinessClassName = ParamBusinessClass
			    ParamBusObj
			        required
			        constraint (ParamBusObj.BusinessClassName entered and ParamBusObj.BusinessObjectKey entered)
						"<ParamBusObj label>MustBeFullyEntered"
			    ParamAction
			    	required
			    	constraint (ParamAction.ActionType.Function)
			    		"ActionMustBeAnInstanceAction"
				ActorGroup
					if (Sharing.Group)
						required
					else
						cannot be entered
							"ActorGroupIsOnlyValidWhenSharingWithAGroup"
				Role
					if (Sharing.Role)
						required
					else
						cannot be entered
							"RoleIsOnlyValidWhenSharingWithARole"
				RoleGroup
					if (Sharing.RoleGroup)
						required
					else
						cannot be entered
							"RoleGroupIsOnlyValidWhenSharingWithARoleGroup"

			Action Rules
				invoke Create this instance
					invoked.Name				= ReportName
					invoked.SourceType			= 3			
					invoked.Dynamic				= true
					invoked.BusinessClass 		= ParamBusinessClass
					invoked.MRBusinessObject 	= ParamBusObj 
					invoked.MRPrintAction 		= ParamAction 

					if (Sharing.Private)
						invoked.Actor = actor
					else
					if (Sharing.Group)
						invoked.ActorGroup = ActorGroup
					else
					if (Sharing.Role)
						invoked.Role = Role
					else
					if (Sharing.RoleGroup)
						invoked.RoleGroup = RoleGroup
						
		ConvertToInlineSearchForm is an Instance Action 
			valid when (CanConvertToIlineSearchForm)
			completion message is "<UserListRel.SearchFormConvertedMessage>"
			
			Action Rules
				invoke ConvertToInlineSearchForm UserListRel	
				
		ProcessFolderReports is an Instance Action
			restricted

				
		BuildReportDefinitions is a Set Action
			restricted	
			
			Parameters
				ForceUpdate is Boolean
			
			Local Fields
				LocalRecordsCtr	is Numeric size 6
				
			Instance Selection 
			    where (Dynamic and (ForceUpdate or not ReportDefinitionRel exists))

			Sort Order is ByDynamicReportCategory
			
			Action Rules
				Instance Rules
					LocalRecordsCtr += 1 
					LocRecordsProcessed = 0
					
					invoke ProcessFolderReports
						
					LocalRecordsCtr += LocRecordsProcessed
					
					if (LocalRecordsCtr >= 100) 
						commit transaction	
						LocalRecordsCtr = 0
					
