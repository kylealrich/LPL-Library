RequisitionInterfaceInput is a BusinessClass
    owned by rq
    prefix is CSV

    Ontology
    	symbolic key is RequisitionInterfaceInput
    
    Patterns
		implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields

        RunGroup
        RecordType              is Alpha size 1
        	States
				Header			value is "H"
				Line            value is "L"
				Distribution    value is "D"
				Comment         value is "C"
				AddOnCharge     value is "A"
        Company                 is an InventoryCompany   
        RequisitionImport       is like Requisition
        RequisitionImportLine   is like RequisitionLine
        AddOnCharge             is like AddOnCharge
        RequisitionNumber       is like Requisition

        Requester               is like Requester
        RequestingLocation      is like RequestingLocation
        CreationDate            is TimeStamp
        ReferenceNumber
        RequisitionDescription  is Alpha size 30
        FromCompanyLocation     is like FromCompanyLocation    
        VendorName              is Alpha size 30
        PrintRequisition        is Boolean
		OperatorID              is like Operator 
			holds pii
        QuoteRequired           is Boolean
        Dropship                is Boolean
        DropshipName            is a Name	 
        	holds pii
        DropshipAddress         is a PostalAddressV2	
        	holds pii
		DropshipPhoneNumber     is an TelephoneNumber
		DropshipContact         is an AddressContact
		PurchaseOrderBillToName is a Name	 
			holds pii
        PurchaseOrderBillToAddress is a PostalAddressV2	
        	holds pii
        PurchaseOrderBillToContact is an AddressContact
        PurchaseOrderBillToPhoneNumber is an TelephoneNumber
        PurchaseOrderBillToEmailAddress is an EmailAddressMulti 
        	holds pii
        DefaultProcedureInformation 
        DefaultBillCode         is like ConsignCode 
        GlobalDocumentType      is like GlobalDocumentType
        CurrencyTable           is like CurrencyTable
        OneSourceDocumentToOnePO is Numeric size 1

		AssetTemplate           is like AssetTemplate
        Asset                   is like Asset
        PurchaseTaxCode         is like TaxCode
        PurchaseTaxable         is Boolean
        DefaultDistributionAccount is like FinanceCodeBlock          
		Vendor                  is like Vendor
		PurchaseFromLocation    is like PurchaseFromLocation
		Buyer                   is like Buyer
		RequestedDeliveryDate   is Date
		AllocationPriority      is like AllocationPriority
		DeliverTo 
		TransactionCurrencyCode is like Currency
		LocationRule            is like WarehouseLocationRule
		PatientProcedure 

        FromCompanyLocationBin
        Item                    is like Item
        ItemType
        ServiceCode
        Quantity 
        CostOption              is Alpha size 1
        CreatePurchaseOrder     is Boolean
        VendorItem				is like VendorItem
        EnteredUOM				is like UnitOfMeasure             
        MajorPurchasingClass    is like MajorPurchasingClass
        MinorPurchasingClass    is like MinorPurchasingClass
        LateDeliveryDate        is Date
        DistributionBy          is Numeric size 1
        DistributionCode        is like PayablesDistributionCode
        Description				
        POCode                  is like POCode               
        CertificationRequired   is Boolean
        InspectionRequired      is Boolean
        Manufacturer            is like Manufacturer
        ManufacturerNumber
        NewRequisition          is Boolean
        FillOrKill
        RequisitionInterfaceInformation
        TransactionUnitCost     is an UnsignedUnitCost
        OverrideCost            is Boolean
        Contract				is like Contract
        ContractLine            is like ContractLine
        SourcingEventRequired   is Boolean
        CommodityCode           is like CommCodes
        ItemGTIN                is like ItemGTIN
        ConsignCode             is like ConsignCode
        GlobalLineType          is like GlobalLineType

		DistributionAccount		is like TransactionCodeBlock
		AssetInformation        is like AssetInformation
        DistributionPercent		is Percent size 6.3
        DistributionQuantity	is a Quantity
        DistributionAmount		is an InternationalAmount

		SpreadAddOnCharge       is Boolean
		AddOnChargePercent
		TotalAddOnChargeAmount  is an InternationalAmount
		EnteredUnitCost         is an UnsignedUnitCost
		TaxCode                 is like TaxCode
        Taxable                 is Boolean
        PrintOnRequisition      is Boolean
        Summarize               is Boolean
        ZeroCost                is Boolean
        Currency                is like Currency

        CommentType             is AlphaUpper size 1
        CommentTitle            is a CommentName
        CommentText             

		EAMWorkOrder
		EAMWorkOrderActivity
		EAMProjectID
		EAMProjectType
		EAMProjectTaxCode
		EAMManufacturing
    	EAMTrade
    	EAMCommodityCode
    	EAMLineType
    	Chemical					is Boolean
    	RebuildRepair				is Boolean
    	TestRequired				is Boolean
    	EAMTool						is a EAMToolField  
        

    Local Fields
    	
    	LocalRunGroup           				is a RunGroup
		LocalErrorOccurred                   	is Boolean
    	LocalErrorMessage       				is Alpha size 150
    	LocalCompany                    		is like InventoryCompany
		LocalRequisitionImport			        is like RequisitionImport
		LocalRequisitionImportLine			    is like RequisitionImportLine
		InterfacedRequisitionNumber			   	is a Requisition view

		LocalRequisitionNumber					is like Requisition

		LocalInterfaceResult            		is a RequisitionInterfaceResult
		LocalErrorLevel							is Numeric 2
        	States
        		Header          value is 1
        		Line            value is 2
        		Distribution    value is 3
        		Comment         value is 4
        		AddOnCharge     value is 5
        LocalRecordType              is Alpha size 1
        	States
				Header			value is "H"
				Line            value is "L"
				Distribution    value is "D"
				Comment         value is "C"
				AddOnCharge     value is "A"
    
    Derived Fields
    
    	NoImportLinesMessage is a MessageField
    		restricted
    		"CannotAddNewRequisition;MustHaveLineRecordsToProcessRequisition"
    		
    	DerivedRequisition is a DerivedField
			type is Numeric size 12
			restricted
    		return first HeaderRequisitionInterfaceInputRel.RequisitionNumber
    
    Conditions
    
    	InputLinesExist
			restricted
			when (AllLinesRequisitionInterfaceInputRel exists)
    
    	OtherDetailsForLineExist
    		restricted
    		when (OtherDetailsForLineRel exists)
    		
    	RunGroupExistsInImport
    		restricted
    		when (ImportRunGroupRel exists)
    		
    	LineDistributionExists
			restricted
			when (DistributionsRequisitionInterfaceInputRel exists)

		RequisitionImportHasError
			restricted
			when (RequisitionImportRel exists)
    
        DetailsExist
            restricted
            when (LineDetailsRequisitionInterfaceInputRel exists)
            
		RequisitionLineCreated
            restricted
            when (RequisitionLineRel exists)
			
	Relations

		LineDetailsRequisitionInterfaceInputRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroupForLine
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.RequisitionImport 			= LocalRequisitionImport
				related.RequisitionImportLine   	= LocalRequisitionImportLine
			Instance Selection
				where (related.RecordType			= RecordType.Distribution
				or     related.RecordType			= RecordType.Comment
				or	   related.RecordType			= RecordType.AddOnCharge)
		
    	RequisitionInputByRunGroupRel
        	one-to-many relation to RequisitionInterfaceInput
        	Field Mapping uses ByRunGroup
        		related.RunGroup                    = LocalRunGroup
        	Instance Selection
				where (related.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
        		
    	RequisitionInputAddOnChargesRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= LocalRunGroup
                related.RecordType                  = "A"
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
            Instance Selection
        		where (related.SpreadAddOnCharge = false)
                
        RequisitionInputSpreadAddOnChargesRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= LocalRunGroup
                related.RecordType                  = "A"
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
        	Instance Selection
        		where (related.SpreadAddOnCharge = true)
        
        RequisitionInputLineDistributionsRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= LocalRunGroup
                related.RecordType                  = "D"
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
                
        RequisitionInputCommentsRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= LocalRunGroup
                related.RecordType                  = "C"
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
                related.RequisitionImportLine       = 0
    	
    	RequisitionInputLineCommentsRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= LocalRunGroup
                related.RecordType                  = "C"
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
            Instance Selection
            	where (related.RequisitionImportLine > 0)
    	
    	RequisitionsForCleanupRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
                related.RecordType                  = "H"
                related.Company           			= LocalCompany
                related.RequisitionImport 			= LocalRequisitionImport
    
    	RequisitionLinesForCleanupRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= RunGroup
                related.RecordType					= RecordType.Line
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
				
		RequisitionLineDistributionsForCleanupRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
                related.RecordType                  = "D"
                related.Company           			= LocalCompany
                related.RequisitionImport 			= LocalRequisitionImport
		
		RequisitionCommentsForCleanupRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
                related.RecordType                  = "C"
                related.Company           			= LocalCompany
                related.RequisitionImport 			= LocalRequisitionImport
		
		RequisitionAddOnChargesForCleanupRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
                related.RecordType                  = "A"
                related.Company           			= LocalCompany
                related.RequisitionImport 			= LocalRequisitionImport
		
		OtherDetailsForLineRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroupForLine
				related.RunGroup					= LocalRunGroup
				related.Company           			= LocalCompany
				related.RequisitionImport 			= LocalRequisitionImport
				related.RequisitionImportLine   	= LocalRequisitionImportLine
			Instance Selection
				where (related.RequisitionInterfaceInput      != RequisitionInterfaceInput)
				
		LocalInterfaceResultsRel
			one-to-one relation to RequisitionInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= actor.context.FinanceEnterpriseGroup
				related.RequisitionInterfaceResult		= LocalInterfaceResult 
				
		RequisitionRel
			one-to-one relation to Requisition
			Field Mapping uses symbolic key
				related.Company           			= Company
                related.Requisition		        	= RequisitionNumber

		RequisitionLineRel
			one-to-one relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company           			= Company
                related.Requisition		        	= LocalRequisitionNumber
                related.RequisitionLine		        = RequisitionImportLine
    
    	ImportRunGroupRel
			one-to-many relation to RequisitionImport
			Field Mapping uses ByRunGroup

				related.RunGroup                     			= LocalRunGroup
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup 	= actor.context.FinanceEnterpriseGroup)

    	RequisitionImportRel
			one-to-many relation to RequisitionImport
			Field Mapping uses ByRunGroup
				related.RunGroup                     			= RunGroup
				related.Company									= Company
				related.RequisitionImport						= RequisitionImport
			Instance Selection
				where (related.ResultProcessedBy 				= LocalInterfaceResult)
				
    	HeaderRequisitionInterfaceInputRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= RunGroup
                related.RecordType					= RecordType.Header
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport

        AllLinesRequisitionInterfaceInputRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= RunGroup
                related.RecordType					= RecordType.Line
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
    			
    	LineRequisitionInterfaceInputRel
			one-to-many relation to RequisitionInterfaceInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= RunGroup
                related.RecordType					= RecordType.Line
                related.Company           			= Company
                related.RequisitionImport 			= RequisitionImport
                related.RequisitionImportLine 		= RequisitionImportLine

    	DistributionsRequisitionInterfaceInputRel
            one-to-many relation to RequisitionInterfaceInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= RunGroup
                related.RecordType					= RecordType.Distribution
                related.Company           			= Company
                related.RequisitionImport  			= RequisitionImport
        		related.RequisitionImportLine       = RequisitionImportLine
    
    Sets

        ByRunGroup
			Sort Order
				RunGroup
				RecordType
				Company
				RequisitionImport
				RequisitionImportLine
				
		ByRunGroupForLine
			Sort Order
				RunGroup
				Company
				RequisitionImport
				RequisitionImportLine
				RequisitionInterfaceInput

	Rule Blocks

		ProcessErrorRecord
			if (LocalErrorLevel.Header)
				invoke CreateNoRules RequisitionImport
					fill in fields from this instance
					invoked.RecordInError					= true
					invoked.ErrorLevel                      = LocalErrorLevel
					invoked.ErrorMessage					= LocalErrorMessage
					invoked.ResultProcessedBy               = LocalInterfaceResult
			else 
				invoke CreateNoRules RequisitionImport
					fill in fields from first HeaderRequisitionInterfaceInputRel 
					invoked.RecordInError					= true
					invoked.ErrorLevel                      = LocalErrorLevel
					invoked.ErrorMessage					= LocalErrorMessage
					invoked.ResultProcessedBy               = LocalInterfaceResult

    Actions
	
		Create is a Create Action
    	
    	Update is an Update Action
    	
    	Delete is a Delete Action
    	
    	FastDelete is a Delete Action
			restricted
			bypass relational integrity rules

		InterfaceRequisitions is a Set Action
			Parameters
				PrmRunGroup							is a RunGroup
					default label is "RunGroup"
				ReleaseRequisitions			        is Boolean
				IncludeRequisitionsInError 			is Boolean
				PrmMoveErrorsToNewRunGroup  		is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix				is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				PrmRunGroup
					LocalRunGroup     = PrmRunGroup
					required
						"RunGroupIsRequired"
					constraint (!RunGroupExistsInImport)
						"RunGroupAlreadyExistsInInterfaceRecords;MustFinishProcessingRunGroupBeforeUsingAgain"
				ReleaseRequisitions
					initial value is true
				IncludeRequisitionsInError
					if (PrmRunGroup like "*ERRORS_*")
						default to true	
			Local Fields
				LocalInterfaceResultView				is a RequisitionInterfaceResult view				
			Instance Selection			
				where (RecordType.Header
				and    RunGroup							= PrmRunGroup
				and    Company.FinanceEnterpriseGroup 	= actor.context.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules
					invoke Create RequisitionInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.FinanceEnterpriseGroup			= actor.context.FinanceEnterpriseGroup
						invoked.RunTime							= current timestamp
						invoked.RunGroup						= PrmRunGroup
						invoked.IncludeRequisitionsInError 		= IncludeRequisitionsInError
						invoked.ReleaseRequisitions			 	= ReleaseRequisitions
						invoked.MoveErrorsToNewRunGroup     	= PrmMoveErrorsToNewRunGroup	
						invoked.ErrorRunGroupPrefix         	= PrmErrorRunGroupPrefix
						
					invoke Finish LocalInterfaceResultView.RequisitionInterfaceResult 	

				RunGroup Set Rules
					Entrance Rules
						invoke Create RequisitionInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.FinanceEnterpriseGroup		= actor.context.FinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.IncludeRequisitionsInError 	= IncludeRequisitionsInError
							invoked.ReleaseRequisitions			= ReleaseRequisitions
							invoked.MoveErrorsToNewRunGroup     = PrmMoveErrorsToNewRunGroup	
							invoked.ErrorRunGroupPrefix         = PrmErrorRunGroupPrefix
					Exit Rules
						invoke InterfaceRequisitionLines
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmFinanceEnterpriseGroup	= actor.context.FinanceEnterpriseGroup
							invoked.PrmInterfaceResult			= LocalInterfaceResultView.RequisitionInterfaceResult
						
				Instance Rules
					if (InputLinesExist) 
						invoke Unreleased.Create Requisition
							assign result to InterfacedRequisitionNumber
							resume on error
								LocalErrorOccurred	= true
								LocalErrorMessage	= error message
							fill in fields from this instance
							invoked.RequisitionImport			= RequisitionImport
							invoked.InterfaceInProcess			= true
							invoked.OriginatingInterfaceRun 	= LocalInterfaceResultView.RequisitionInterfaceResult
							invoked.RequisitionSource			= RqSource.RequisitionInterface	
					else
						LocalErrorOccurred	= true
						LocalErrorMessage 	= NoImportLinesMessage
					
					if (LocalErrorOccurred)
						LocalErrorLevel 						= LocalErrorLevel.Header
						LocalInterfaceResult					= LocalInterfaceResultView.RequisitionInterfaceResult
						include ProcessErrorRecord
					else
						RequisitionNumber    					= InterfacedRequisitionNumber.Requisition
    
		InterfaceRequisitionLines is a Set Action
			Parameters
				PrmRunGroup								is a RunGroup
				PrmFinanceEnterpriseGroup				is a FinanceEnterpriseGroup
				PrmInterfaceResult						is a RequisitionInterfaceResult
			Local Fields
				LocalSetRequisitionImportHasError		is Boolean
			Instance Selection			
				where (RecordType.Line
				and    RunGroup							= PrmRunGroup
				and    Company.FinanceEnterpriseGroup 	= actor.context.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules

					invoke InterfaceRequisitionLineDistributions
						invoked.PrmRunGroup						= PrmRunGroup
						invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.PrmInterfaceResult				= PrmInterfaceResult
					
				RunGroup Set Rules			
					Exit Rules
						invoke InterfaceRequisitionLineDistributions
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmInterfaceResult			= PrmInterfaceResult

				RequisitionImport Set Rules
					Entrance Rules
						LocalInterfaceResult					= PrmInterfaceResult
						LocalSetRequisitionImportHasError 		= RequisitionImportHasError
						
				Instance Rules

					LocalInterfaceResult = PrmInterfaceResult
					
					if (not LocalSetRequisitionImportHasError)
						invoke Unreleased.Create RequisitionLine
							resume on error
								LocalErrorOccurred 				= true
								LocalErrorMessage 				= error message
							fill in fields from this instance
							invoked.Requisition					= DerivedRequisition 
							invoked.RequisitionLine     		= RequisitionImportLine
							invoked.FromInterfaceImport 		= true
							if (LineDistributionExists)
								invoked.DistributionFromInterfaceImport = true
						
						if (LocalErrorOccurred)
							LocalSetRequisitionImportHasError	= true
							LocalErrorLevel 					= LocalErrorLevel.Line
							LocalInterfaceResult				= PrmInterfaceResult
							include ProcessErrorRecord
						else
							LocalRunGroup						= RunGroup
							LocalCompany						= Company
							LocalRequisitionImport				= RequisitionImport	
							LocalRequisitionImportLine			= RequisitionImportLine
							if (not DetailsExist) 
								invoke FastDelete


		InterfaceRequisitionLineDistributions is a Set Action
			Parameters
				PrmRunGroup								is a RunGroup
				PrmFinanceEnterpriseGroup				is a FinanceEnterpriseGroup
				PrmInterfaceResult						is a RequisitionInterfaceResult
			Local Fields
		    	LocalSetRunGroup           				is a RunGroup
		    	LocalSetCompany                    		is like InventoryCompany
				LocalSetRequisitionImport			    is like RequisitionImport
				LocalSetRequisitionImportLine			is like RequisitionImportLine
				LocalSetRequisitionImportHasError		is Boolean
			Instance Selection			
				where (RecordType.Distribution
				and    RunGroup							= PrmRunGroup
				and    Company.FinanceEnterpriseGroup 	= actor.context.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules
					invoke InterfaceRequisitionComments
						invoked.PrmRunGroup						= PrmRunGroup
						invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.PrmInterfaceResult				= PrmInterfaceResult
					
				RunGroup Set Rules			
					Exit Rules
						invoke InterfaceRequisitionComments
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmInterfaceResult			= PrmInterfaceResult
			
				RequisitionImport Set Rules
					Entrance Rules
						LocalInterfaceResult					= PrmInterfaceResult
						LocalSetRequisitionImportHasError 		= RequisitionImportHasError
				
				RequisitionImportLine Set Rules
					Entrance Rules
						LocalSetRunGroup						= RunGroup
						LocalSetCompany							= Company
						LocalSetRequisitionImport				= RequisitionImport	
						LocalSetRequisitionImportLine			= RequisitionImportLine						
					Exit Rules
						LocalRunGroup							= LocalSetRunGroup
						LocalCompany							= LocalSetCompany
						LocalRequisitionImport					= LocalSetRequisitionImport	
						LocalRequisitionImportLine				= LocalSetRequisitionImportLine
						if (not LocalSetRequisitionImportHasError
		                and not DetailsExist) 
		                    invoke FastDelete LineRequisitionInterfaceInputRel
						
				Instance Rules
					LocalInterfaceResult = PrmInterfaceResult

					if (not LocalSetRequisitionImportHasError)
						invoke Create RequisitionLineDistribution
							resume on error
								LocalErrorOccurred 				= true
								LocalErrorMessage 				= error message
							fill in fields from this instance
							invoked.Requisition					= DerivedRequisition 
							invoked.RequisitionLine     		= RequisitionImportLine
							invoked.TransientFromMassRQCreateAndRelease = true
						
						if (LocalErrorOccurred)
							LocalSetRequisitionImportHasError	= true
							LocalErrorLevel 					= LocalErrorLevel.Distribution
							LocalInterfaceResult				= PrmInterfaceResult
							include ProcessErrorRecord
						else
							invoke FastDelete


		InterfaceRequisitionComments is a Set Action
			Parameters
				PrmRunGroup								is a RunGroup
				PrmFinanceEnterpriseGroup				is a FinanceEnterpriseGroup
				PrmInterfaceResult						is a RequisitionInterfaceResult
			Local Fields
		    	LocalSetRunGroup           				is a RunGroup
		    	LocalSetCompany                    		is like InventoryCompany
				LocalSetRequisitionImport			    is like RequisitionImport
				LocalSetRequisitionImportLine			is like RequisitionImportLine
				LocalSetRequisitionImportHasError		is Boolean
			Instance Selection			
				where (RecordType.Comment
				and    RunGroup							= PrmRunGroup
				and    Company.FinanceEnterpriseGroup 	= actor.context.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules
					invoke InterfaceRequisitionAddOnCharges
						invoked.PrmRunGroup						= PrmRunGroup
						invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.PrmInterfaceResult				= PrmInterfaceResult
					
				RunGroup Set Rules			
					Exit Rules
						invoke InterfaceRequisitionAddOnCharges
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmInterfaceResult			= PrmInterfaceResult

				RequisitionImport Set Rules
					Entrance Rules
						LocalInterfaceResult					= PrmInterfaceResult
						LocalSetRequisitionImportHasError 		= RequisitionImportHasError
				
				RequisitionImportLine Set Rules
					Entrance Rules
						LocalSetRunGroup						= RunGroup
						LocalSetCompany							= Company
						LocalSetRequisitionImport				= RequisitionImport	
						LocalSetRequisitionImportLine			= RequisitionImportLine						
					Exit Rules
						LocalRunGroup							= LocalSetRunGroup
						LocalCompany							= LocalSetCompany
						LocalRequisitionImport					= LocalSetRequisitionImport	
						LocalRequisitionImportLine				= LocalSetRequisitionImportLine
						if (not LocalSetRequisitionImportHasError
		                and not DetailsExist) 
		                    invoke FastDelete LineRequisitionInterfaceInputRel
						
				Instance Rules
					LocalInterfaceResult = PrmInterfaceResult

					if (not LocalSetRequisitionImportHasError)
						if (RequisitionImportLine entered)
							invoke Create RequisitionLineComment
								resume on error
									LocalErrorOccurred			= true
									LocalErrorMessage			= error message
								fill in fields from this instance
								invoked.Requisition				= DerivedRequisition 
								invoked.RequisitionLine     	= RequisitionImportLine
						else
							invoke Create RequisitionComment
								resume on error
									LocalErrorOccurred			= true
									LocalErrorMessage			= error message
								fill in fields from this instance
								invoked.Requisition				= DerivedRequisition 
	
						if (LocalErrorOccurred)
							LocalSetRequisitionImportHasError	= true
							LocalErrorLevel 					= LocalErrorLevel.Comment
							LocalInterfaceResult				= PrmInterfaceResult
							include ProcessErrorRecord
						else
							invoke FastDelete


		InterfaceRequisitionAddOnCharges is a Set Action
			Parameters
				PrmRunGroup								is a RunGroup
				PrmFinanceEnterpriseGroup				is a FinanceEnterpriseGroup
				PrmInterfaceResult						is a RequisitionInterfaceResult
			Local Fields
		    	LocalSetRunGroup           				is a RunGroup
		    	LocalSetCompany                    		is like InventoryCompany
				LocalSetRequisitionImport			    is like RequisitionImport
				LocalSetRequisitionImportLine			is like RequisitionImportLine
				LocalSetRequisitionImportHasError		is Boolean
			Instance Selection			
				where (RecordType.AddOnCharge
				and    RunGroup							= PrmRunGroup
				and    Company.FinanceEnterpriseGroup 	= actor.context.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules
					invoke CreateRequisitionImportRecords
						invoked.PrmRunGroup						= PrmRunGroup
						invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.PrmInterfaceResult				= PrmInterfaceResult
					
				RunGroup Set Rules			
					Exit Rules
						invoke CreateRequisitionImportRecords
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmInterfaceResult			= PrmInterfaceResult

				RequisitionImport Set Rules
					Entrance Rules
						LocalInterfaceResult					= PrmInterfaceResult
						LocalSetRequisitionImportHasError 		= RequisitionImportHasError
				
				RequisitionImportLine Set Rules
					Entrance Rules
						LocalSetRunGroup						= RunGroup
						LocalSetCompany							= Company
						LocalSetRequisitionImport				= RequisitionImport	
						LocalSetRequisitionImportLine			= RequisitionImportLine						
					Exit Rules
						LocalRunGroup							= LocalSetRunGroup
						LocalCompany							= LocalSetCompany
						LocalRequisitionImport					= LocalSetRequisitionImport	
						LocalRequisitionImportLine				= LocalSetRequisitionImportLine
						if (not LocalSetRequisitionImportHasError
		                and not DetailsExist) 
		                    invoke FastDelete LineRequisitionInterfaceInputRel
						
				Instance Rules
					LocalInterfaceResult = PrmInterfaceResult

					if (not LocalSetRequisitionImportHasError)
						if (SpreadAddOnCharge)
							invoke Create RequisitionSpreadAddOnCharge
								resume on error
									LocalErrorOccurred			= true
									LocalErrorMessage			= error message
								fill in fields from this instance
								invoked.Requisition				= DerivedRequisition 
						else
							invoke Create RequisitionLineAddOnCharge
								resume on error
									LocalErrorOccurred			= true
									LocalErrorMessage			= error message
								fill in fields from this instance
								invoked.Requisition				= DerivedRequisition 
								invoked.RequisitionLine			= RequisitionImportLine
	
						if (LocalErrorOccurred)
							LocalSetRequisitionImportHasError	= true
							LocalErrorLevel 					= LocalErrorLevel.AddOnCharge
							LocalInterfaceResult				= PrmInterfaceResult
							include ProcessErrorRecord
						else
							invoke FastDelete

															
		CreateRequisitionImportRecords is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmRunGroup							is a RunGroup
				PrmInterfaceResult					is a RequisitionInterfaceResult
			Instance Selection
				where (PrmRunGroup					= RunGroup
				and    PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules
					invoke FinishInterfacedRequisitions
						invoked.PrmRunGroup						= PrmRunGroup
						invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.PrmInterfaceResult				= PrmInterfaceResult
				RunGroup Set Rules			
					Exit Rules
						invoke FinishInterfacedRequisitions
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmInterfaceResult			= PrmInterfaceResult
				Instance Rules
					LocalInterfaceResult = PrmInterfaceResult
					if (RecordType.Header) 
						if (RequisitionImportHasError)
							if (RequisitionNumber entered)
								invoke FinishInterface RequisitionRel
							invoke FastDelete
					else
					if (RecordType.Line)
						LocalRequisitionNumber = RequisitionImportRel.RequisitionNumber 
						invoke CreateNoRules RequisitionImportLine
							fill in fields from this instance
							if (RequisitionLineCreated)
								invoked.RequisitionLineExists = true
						invoke FastDelete					
					else
					if (RecordType.Distribution)
						invoke CreateNoRules RequisitionImportLineDistribution
							fill in fields from this instance
						invoke FastDelete						
					else
					if (RecordType.Comment)
						invoke CreateNoRules RequisitionImportComment
							fill in fields from this instance
						invoke FastDelete	
					else
					if (RecordType.AddOnCharge)
						invoke CreateNoRules RequisitionImportAddOnCharge
							fill in fields from this instance
						invoke FastDelete	


		FinishInterfacedRequisitions is a Set Action 
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmRunGroup							is a RunGroup
				PrmInterfaceResult					is a RequisitionInterfaceResult
			Local Fields
				LocalRecordsProcessed				is like Requisition
			Instance Selection
				where (PrmRunGroup					= RunGroup
				and    PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup)
			Sort Order is ByRunGroup
			Action Rules
				Empty Set Rules
					invoke Finish PrmInterfaceResult 
				
				RunGroup Set Rules
					Exit Rules
						invoke Finish PrmInterfaceResult 
							invoked.PrmRecordsProcessed	= LocalRecordsProcessed
						
				Instance Rules

					increment LocalRecordsProcessed
					
					if (PrmInterfaceResult.ReleaseRequisitions)
						invoke FinishAndReleaseInterface RequisitionRel
					else
						invoke FinishInterface RequisitionRel
						
					invoke FastDelete
									
																		
