ShipmentLoadConsolidation is a BusinessClass
    owned by wh
    prefix is SHLC

    Ontology
        symbolic key is ShipmentLoadConsolidation

    Patterns 
        disable AuditIndex
    
    Persistent Fields
        Container
        MaxContainer                    is Numeric size 6
        NumberOfFilledContainerUnits    is Numeric size 6
        Vehicle
        ShipTo                 is a RequestingLocation
        Status                 is Numeric size 1
            protected
			States
            	Unreleased        value is 0
            	ForRelease        value is 1
                Released          value is 2
                Closed            value is 3
        FromShipDate           is Date
        ToShipDate             is Date  
        WarehouseStorageLocation

    Local Fields
        LocalVehicle           is like Vehicle
        LocalNumberOfContainer is Numeric size 6
        LocalVolumeContainer   is Numeric size 6
        LocalWeightContainer   is Numeric size 6
    
    Context Fields
        VehicleTrip

    Conditions
        RecordExists
            restricted
            when (ShipmentLoadConsolidation exists)

        HasVehicle
            restricted
            when (Vehicle entered)

        HasMaxContainer
            restricted
            when (MaxContainer entered)

        IsUnreleased
			restricted
			when (Status.Unreleased)

        IsClosed
			restricted
			when (Status.Closed)

        IsReleased
			restricted
			when (Status.Released)
        
        IsForRelease
            restricted
            when (Status.ForRelease)

        HasEnteredFromShipDateAndUnreleased   
            restricted          
            when (FromShipDate entered and IsUnreleased)

        NoEnteredFromShipDateAndUnreleased   
            restricted          
            when (FromShipDate not entered and IsUnreleased)

        ShipmentLoadLineExists
            restricted
            when (ShipmentLoadLineRel exists)
        
        ShipmentLoadExists
            restricted
            when (ShipmentLoadRel exists)

        AllowManuallyCloseShipmentLoad
            restricted
            when (InventoryLocation.MSCMDelivery and VehicleTripLineByShipmentRel not exists)

        CapacityYellowAlert
            restricted
            when (DerivedComputedVehicleTypeVolumeCapacity >= .9 
            or    DerivedComputedVehicleTypeWeightCapacity >= .9)

        ShipmentLoadInVehicleTrip
            restricted
            when (VehicleTripLineByShipmentRel not exists)
        
        AllowCreateShipmentLoadContainer
            restricted
            when (not IsReleasedOrClosed 
            and    DerivedContainerCount not = NumberOfFilledContainerUnits)
        
        EditableMaxContainer
            restricted
            when ( not HasVehicle
            and    not IsReleasedOrClosed)
        
        DisplayMaxContainer
            restricted
            when ( not HasVehicle
            and    IsReleasedOrClosed)
        
        DisplayDerivedNumberOfContainer
            restricted
            when ( HasVehicle
            and   not IsReleasedOrClosed)
        
        IsReleasedOrClosed
            restricted
            when (IsReleased
            or    IsClosed)

        LoadReadyForDispatch
            restricted
            when (not IsClosed
            and   ShipmentLoadLineExists
            and  (VehicleTripLineByShipmentRel not exists
            or   (VehicleTripLineByShipmentRel exists
            and   VehicleTripLineByShipmentRel.VehicleTrip.Status.Created)))

    Derived Fields
        ReleasedTagMsg                      is a MessageField
			restricted
			"Released"

        UnreleasedTagMsg                    is a MessageField
			restricted
			"Unreleased"

        ForReleaseTagMsg                    is a MessageField
            restricted
			"ForRelease"

        ClosedTagMsg                        is a MessageField
            restricted
            "Closed"

        StatusMsgTagField                   is a DerivedField
			type is MessageField
			default label is "Status"
			if (IsReleased)
				return ReleasedTagMsg
			if (IsUnreleased)
				return UnreleasedTagMsg
            if (IsForRelease)
                return ForReleaseTagMsg
            if (IsClosed)
                return ClosedTagMsg

        DerivedContainerLength              is a DerivedField
            type is like LengthDimension
            restricted
            return ContainerRel.ContainerLength
        
        DerivedContainerrWidth              is a DerivedField
            type is like WidthDimension
            restricted
            return ContainerRel.ContainerWidth
        
        DerivedContainerHeight              is a DerivedField
            type is like HeightDimension
            restricted
            return ContainerRel.ContainerHeight

        DerivedContainerWeightCapacity      is a DerivedField
			type is like DimensionSize
			return ContainerRel.ContainerWeightCapacity
			
		DerivedContainerVolumeCapacity      is a DerivedField
			type is like DimensionSize
			return ContainerRel.ContainerVolumeCapacity

        DerivedVehicleType                  is a DerivedField
            type is like VehicleType
            restricted
            return Vehicle.VehicleType

        DerivedVehicleTypeInteriorLength    is a DerivedField
            type is like LengthDimension
            restricted
            return VehicleTypeRel.InteriorLength
        
        DerivedVehicleTypeInteriorWidth     is a DerivedField
            type is like WidthDimension
            restricted
            return VehicleTypeRel.InteriorWidth
        
        DerivedVehicleTypeInteriorHeight    is a DerivedField
            type is like HeightDimension
            restricted
            return VehicleTypeRel.InteriorHeight
			
		DerivedVehicleTypeWeightCapacity    is a DerivedField
			type is like DimensionSize
            if (HasVehicle)
			    return VehicleTypeRel.VehicleWeightCapacity
            else 
                return DerivedMaxContainerTotalWeight

        DerivedVehicleTypeVolumeCapacity    is a DerivedField
			type is like DimensionSize
            if (HasVehicle)
			    return VehicleTypeRel.VehicleVolumeCapacity
            else 
                return DerivedMaxContainerTotalVolume

        DerivedComputedVehicleTypeWeightCapacity    is a ComputeField 
            type is like DimensionSize
            restricted
            (DerivedOrderLoadWeight / DerivedVehicleTypeWeightCapacity)

        DerivedComputedVehicleTypeVolumeCapacity    is a ComputeField
            type is like DimensionSize
            restricted
            (DerivedOrderLoadVolume / DerivedVehicleTypeVolumeCapacity)

        DerivedMaxContainerTotalWeight      is a ComputeField
            type is like DimensionSize
            (MaxContainer * DerivedContainerWeightCapacity)
        
        DerivedMaxContainerTotalVolume      is a ComputeField
            type is like DimensionSize
            (MaxContainer * DerivedContainerVolumeCapacity)

        DerivedRemainingContainerWeight     is a ComputeField
            type is like DimensionSize
            (DerivedVehicleTypeWeightCapacity - DerivedOrderLoadWeight)

        DerivedRemainingContainerVolume     is a ComputeField
            type is like DimensionSize
            (DerivedVehicleTypeVolumeCapacity - DerivedOrderLoadVolume)

        DerivedNumberOfContainer            is a DerivedField
            type is Numeric 6
            LocalVolumeContainer = (DerivedVehicleTypeWeightCapacity/DerivedContainerWeightCapacity) 
            LocalWeightContainer = (DerivedVehicleTypeVolumeCapacity/DerivedContainerVolumeCapacity)

            if (LocalVolumeContainer < LocalWeightContainer)
                return LocalVolumeContainer
            else    
                return LocalWeightContainer

        DerivedOrderLoadVolume              is a DerivedField
            type is like DimensionSize
            return sum ShipmentLoadLineRel.StockVolume 

        DerivedOrderLoadWeight              is a DerivedField
            type is like DimensionSize
            return sum ShipmentLoadLineRel.StockWeight 

        DerivedLastContainerNumber          is a DerivedField
            type is Numeric size 6
            restricted
            return last ShipmentLoadLineContainerCountRel.ShipmentLoadContainer

        DerivedContainerCount            is a DerivedField
            type is Numeric 6
            if (HasVehicle)
                return DerivedNumberOfContainer
            else    
                return MaxContainer
        
        EmptyShipmentLoadContainerCount                 is a DerivedField
            type is Numeric size 6
            restricted
            return instance count of EmptyShipmentLoadContainerRel

        DerivedVolumeUOM is a MessageField
            default label is "Volume"
            "<Company.ItemGroup.VolumeUOM>"
        
        DerivedWeightUOM is a MessageField
            default label is "Volume"
            "<Company.ItemGroup.WeightUOM>"
                
        DerivedContainerVolumeCapacityAndVolumeUOMLabel is a MessageField
            default label is "Volume"
            "<DerivedContainerVolumeCapacity>_<DerivedVolumeUOM>"
    
        DerivedContainerVolumeCapacityLabel is a MessageField
            default label is "Volume"
            "<DerivedContainerVolumeCapacity>"
        
        DerivedContainerWeightCapacityAndWeightUOMLabel is a MessageField
            default label is "Weight"
            "<DerivedContainerWeightCapacity>_<DerivedWeightUOM>"
        
        DerivedContainerWeightCapacityLabel is a MessageField
            default label is "Weight"
            "<DerivedContainerWeightCapacity>"
        
        DerivedVehicleTypeVolumeCapacityAndVolumeUOMLabel is a MessageField
            default label is "Volume"
            "<DerivedVehicleTypeVolumeCapacity>_<DerivedVolumeUOM>"
        
        DerivedVehicleTypeVolumeCapacityLabel is a MessageField
            default label is "Volume"
            "<DerivedVehicleTypeVolumeCapacity>"
        
        DerivedVehicleTypeWeightCapacityAndWeightUOMLabel is a MessageField
            default label is "Weight"
            "<DerivedVehicleTypeWeightCapacity>_<DerivedWeightUOM>"
        
        DerivedVehicleTypeWeightCapacityLabel is a MessageField
            default label is "Weight"
            "<DerivedVehicleTypeWeightCapacity>"
        
        DerivedContainerVolumeCapacityAndVolumeUOM        is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.WeightUOM.NotSelected)
				return DerivedContainerVolumeCapacityAndVolumeUOMLabel
			else
				return DerivedContainerVolumeCapacityLabel
        
        DerivedContainerWeightCapacityAndWeightUOM        is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.VolumeUOM.NotSelected)
				return DerivedContainerWeightCapacityAndWeightUOMLabel
			else
				return DerivedContainerWeightCapacityLabel
        
        DerivedVehicleTypeVolumeCapacityCapacityAndVolumeUOM        is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.WeightUOM.NotSelected)
				return DerivedVehicleTypeVolumeCapacityAndVolumeUOMLabel
			else
				return DerivedVehicleTypeVolumeCapacityLabel
        
        DerivedVehicleTypeWeightCapacityAndWeightUOM        is a DerivedField
			type is MessageField
			if (not Company.ItemGroup.VolumeUOM.NotSelected)
				return DerivedVehicleTypeWeightCapacityAndWeightUOMLabel
			else
				return DerivedVehicleTypeWeightCapacityLabel 

        DerivedWarehouseStorageLocation     is a DerivedField
            type is like WarehouseStorageLocation
            return  DefaultStagingWarehouseStorageLocationRel.WarehouseStorageLocation

    Relations
        ShipmentLoadRel is a ShipmentLoad set

        VehicleTypeRel 
            one-to-one relation to VehicleType
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.VehicleType               = Vehicle.VehicleType
        
        ContainerRel
            one-to-one relation to Container
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.Container                 = Container

        WarehouseShipmentRel
            one-to-many relation to WarehouseShipment
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.InventoryLocation         = ShipmentLoadConsolidation.InventoryLocation
            Instance Selection  
                where (related.Destination        = ShipTo
                and    related.ShipDate          >= FromShipDate
                and    related.ShipDate          <= ToShipDate
                and    related.Status.Closed)

        AllWarehouseShipmentRel
            one-to-many relation to WarehouseShipment
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.InventoryLocation         = ShipmentLoadConsolidation.InventoryLocation
            Instance Selection  
                where (related.Destination        = ShipTo
                and    related.Status.Closed)

        ShipmentLoadLineRel
            one-to-many relation to ShipmentLoadLine
            Field Mapping uses ByShipmentLoadConsolidation
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
                related.ShipmentLoadConsolidation = ShipmentLoadConsolidation

        ShipmentLoadLineContainerCountRel
            one-to-many relation to ShipmentLoadLine
            Field Mapping uses ByShipmentLoadConsolidation
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
                related.ShipmentLoadConsolidation = ShipmentLoadConsolidation

        VehicleTripLineByShipmentRel
            one-to-one relation to VehicleTripLine
            Field Mapping uses ByShipment
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
                related.ShipmentLoadConsolidation = ShipmentLoadConsolidation
        
        ShipmentLoadContainerRel
            one-to-many relation to ShipmentLoadContainer
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
                related.ShipmentLoadConsolidation = ShipmentLoadConsolidation
        
        EmptyShipmentLoadContainerRel
            one-to-many relation to ShipmentLoadContainer
            Field Mapping uses ByShipmentLoadContainer
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
                related.ShipmentLoadConsolidation = ShipmentLoadConsolidation
            Instance Selection
                where (not related.ShipmentLoadLineExists)
           
        
        StagingWarehouseStorageLocationRel
            one-to-many relation to WarehouseStorageLocation
            Field Mapping uses ByStorageLocationType
                related.StorageLocationType       = 3  
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
                
        DefaultStagingWarehouseStorageLocationRel
            one-to-many relation to WarehouseStorageLocation
            Field Mapping uses ByStorageLocationType
                related.StorageLocationType       = 3  
                related.Company                   = Company
                related.InventoryLocation         = InventoryLocation
            Instance Selection
                where (related.DefaultForShipmentLoadConsolidation)

        AvailableVehicleRel
            one-to-many relation to Vehicle
            Field Mapping uses symbolic key
                related.Company                     = Company
            Instance Selection
                where (related.VehicleIsAvailable
                and   (related.VehicleType.VehicleWeightCapacity >= DerivedOrderLoadWeight
                and    related.VehicleType.VehicleVolumeCapacity >= DerivedOrderLoadVolume))

	Field Rules
        Container 
            required  
            
            if (Container changed
            and ShipmentLoadLineExists)
                cannot be changed      
                    "CannotChange_Container;ShipmentsExist"

        MaxContainer 
            constraint(MaxContainer > 0)
                "CannotEnterNegativeValues"
                
            if (HasVehicle)
                cannot be entered
                    "CannotEnter_NumberOf_ContainerWhen_VehicleIsEntered"
            else 
                required

            if (MaxContainer changed)
                constraint (DerivedLastContainerNumber <= MaxContainer)
                    "NumberOf_ContainersCannotBeLessThanTheNumberOfLoadedContainers"

        Vehicle
            if (Vehicle changed
            and ShipmentLoadLineExists)
                cannot be changed      
                    "CannotChange_Vehicle;ShipmentsExist"

            constraint (DerivedNumberOfContainer entered)
                "CannotCreate_Shipment_Load_Consolidation;VehicleCapacityIsLessThanContainerCapacity"

            constraint (Vehicle.VehicleIsAvailable)
                "VehicleIsNotAvailableForUse"

        ShipTo
            required

            if (ShipTo changed
            and ShipmentLoadExists)
                cannot be changed      
                    "CannotChange_ShipTo_Location;ShipmentsExist"

        FromShipDate
            if (FromShipDate changed
            and ShipmentLoadExists)
                cannot be changed      
                    "CannotChange_From_Ship_Date;ShipmentsExist"

            if (FromShipDate not entered 
            and ToShipDate entered)
                required   
        
        ToShipDate
            constraint (FromShipDate <= ToShipDate)
                "ToShipDate_Should_Be_Greater_Than_Or_Equal_To_FromShipDate"

            if (ToShipDate changed
            and ShipmentLoadExists)
                cannot be changed      
                    "CannotChange_To_Ship_Date;ShipmentsExist"

            if (FromShipDate entered 
            and ToShipDate not entered)
                required        
        
        WarehouseStorageLocation
            constraint (WarehouseStorageLocation.Active)
                "CannotAssignInactiveStorageLocation"

            if (action type.Create
            and WarehouseStorageLocation not entered)
                default to DerivedWarehouseStorageLocation

            if (not WarehouseStorageLocation.StorageLocationType.StagingLocation)
                cannot be entered
                    "StorageLocationTypeMustBeStagingLocation"
            
    Sets
        ByContainer
            indexed
            Instance Selection
                where (not IsReleased)
            Sort Order
                Company
                Container
                ShipmentLoadConsolidation
                InventoryLocation

        ByVehicle
            indexed
            Instance Selection
                where (not IsReleased)
            Sort Order
                Company
                Vehicle
                ShipmentLoadConsolidation
                InventoryLocation

	StateCycles
        ShipmentLoadConsolidation is a StateCycle
            state field is Status
            
            Unreleased is a State
                Create is a Create Action
                Update is an Update Action            
                Delete is a Delete Action

                Finish is an Instance Action
                    valid when (ShipmentLoadLineExists)
                    Action Rules
                        if (not HasVehicle)
                            invoke Update
                                invoked.MaxContainer = NumberOfFilledContainerUnits 

                        if (EmptyShipmentLoadContainerCount > 50)
                            invoke DeleteEmptyContainer EmptyShipmentLoadContainerRel
                                invoked.PrmCompany                   = Company
                                invoked.PrmInventoryLocation         = InventoryLocation
                                invoked.PrmShipmentLoadConsolidation = ShipmentLoadConsolidation
                        else
                            invoke DeleteEmptyContainer EmptyShipmentLoadContainerRel in foreground
                                invoked.PrmCompany                   = Company
                                invoked.PrmInventoryLocation         = InventoryLocation
                                invoked.PrmShipmentLoadConsolidation = ShipmentLoadConsolidation

                        make transition to ForRelease

            ForRelease is a State
                Release is an Instance Action
                    Action Rules
                        make transition to Released
            
                Undo is an Instance Action
                    Action Rules
                        make transition to Unreleased
                    
            Released is a State
                Close is an Instance Action
                    valid when (AllowManuallyCloseShipmentLoad)
                    Action Rules
                        make transition to Closed

                CloseShipmentLoad is an Instance Action
                    restricted
                    Action Rules
                        make transition to Closed    

            Closed is a State

    Actions
        SelectLoadNumber is an Instance Action
            restricted
            Action Rules
                invoke Create VehicleTripLine
                    fill in fields from this instance
                    invoked.VehicleTrip     = VehicleTrip
