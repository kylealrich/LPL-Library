DockLogLine is a BusinessClass
    owned by mscm
    prefix is MSDLL

    Ontology
        symbolic key is DockLogLine

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
    
        TrackingNumber

    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted
            
        Delete is a Delete Action
            restricted

        CreateDeliveryLinesAndEvent is a Set Action
            Parameters 
                PrmMobileSupplyChainCompany        is a MobileSupplyChainCompany
                PrmMobileSupplyChainLocation       is a MobileSupplyChainLocation
                PrmDockLog                         is a DockLog
                PrmMobileSupplyChainDeliveryTicket is a MobileSupplyChainDeliveryTicket

            Instance Selection
                where (Company                     = PrmMobileSupplyChainCompany
                and    MobileSupplyChainLocation   = PrmMobileSupplyChainLocation
                and    DockLog                     = PrmDockLog)

            Sort Order is primary
            
            Local Fields
                LocalMobileSupplyChainDeliveryUnitView      is a MobileSupplyChainDeliveryUnit view
                LocalLineCounter                            is Numeric size 6
                
            Action Rules
                DockLog Set Rules
                    Exit Rules
                        invoke Update PrmMobileSupplyChainDeliveryTicket
                            invoked.NumberOfLines = LocalLineCounter

                        invoke CreateEventForAllLines PrmMobileSupplyChainDeliveryTicket
                            invoked.PrmPerformedAction = PerformedAction.Docklogged
                            invoked.PrmTimeSubmitted   = DockLog.TimeSubmitted
                                
                Instance Rules
                    invoke Create MobileSupplyChainDeliveryUnit
                        assign result to LocalMobileSupplyChainDeliveryUnitView
                        invoked.Company                         = Company
                        invoked.MobileSupplyChainLocation       = MobileSupplyChainLocation
                        invoked.MobileSupplyChainDeliveryTicket = PrmMobileSupplyChainDeliveryTicket
                        invoked.CarrierTrackingNumber           = TrackingNumber
                        invoked.Status                          = MobileSupplyChainDeliveryUnit.Status.OnLoadingDock

                    invoke Create MobileSupplyChainDeliveryLine
                        invoked.Company                         = Company
                        invoked.MobileSupplyChainLocation       = MobileSupplyChainLocation
                        invoked.MobileSupplyChainDeliveryTicket = PrmMobileSupplyChainDeliveryTicket
                        invoked.MobileSupplyChainDeliveryUnit   = LocalMobileSupplyChainDeliveryUnitView.MobileSupplyChainDeliveryUnit
                        invoked.LastPerformedAction             = PerformedAction.Docklogged

                    increment LocalLineCounter
