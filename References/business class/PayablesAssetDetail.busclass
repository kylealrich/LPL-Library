PayablesAssetDetail is a BusinessClass
    owned by ap
    prefix is APS
    classic name is APASTDTL

    Ontology
        symbolic key is PayablesAssetDetail
            classic set name is APSSET0
            classic name is DIST-SEQ-NBR

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields
		Vendor						is a snapshot of PayablesInvoice.Vendor
        AssetDescription            is a Description
            classic name is ASSET-DESC
        TagNumber
            classic name is TAG-NBR
        ItemNumber                  is an AssetItemNumber
            classic name is ITEM-NBR
        InServiceDate               is Date
            classic name is INSRV-DATE
        PurchaseDate                is Date
        ModelNumber
        SerialNumber
        HoldAsset                   is Boolean
            classic name is HOLD-AM
        Combine
        ItemDescription
            classic name is ITEM-DESC
        ItemQuantity
        ItemTaxTransaction          is an InternationalAmount
            classic name is ITEM-TAX-TRAN
        ItemTaxBase                 is an InternationalAmount
        AssetGroup
        AssetTemplate
        Asset
        AssetLocation
            classic name is LOCATION-NAME
        AssetDivision
            classic name is DIVISION
        AssetAccountingUnitGroup	is like AssetAccountingUnitGroup
            classic name is AU-GROUP
        BarCode                     is an AssetBarCode
        LocationDetail              is an AssetLocationDetail
            classic name is ITEM-LOC-DTL
        PayablesDistributionWithTax is AlphaUpper size 1
            classic name is AP-DISTRIB-FL
            States
                Detail  value is "D"
                Summary value is "S"
        LandedCost                  is Boolean
            classic name is LAND-COST-FLG

	Local Fields
		LocalExchangeDate	is an ExchangeDate	
		LocalAssetAccountingUnit			is a FinanceCodeBlockNoAccountFull

    	LocalAMTransactionItemCost			is a InternationalAmount		
    	LocalAMTransactionItemTax			is a InternationalAmount		
		LocalAMITransactionItemCostBase     is a InternationalAmount
		LocalAMITransactionItemTaxBase		is a InternationalAmount

		LocalCurrencyTable					is a CurrencyTable
		LocalCurrencyExchange				is a CurrencyExchange
		LocalFromCurrency					is a FromCurrency
    	LocalCurrencyAmount					is a CurrencyAmount
    	LocalCurrencyRate					is a CurrencyExchangeRate

	Context Fields
		ExchangeDate							
	
	Derived Fields
		AssetProcessingMessage is a DerivedField
			type is MessageField 		
			restricted
			if (Asset changed
			or AssetDescription changed
			or TagNumber changed)
				return AssetRecordChangedMessage					 	
			return blank	
				
		AssetRecordChangedMessage is a MessageField
			restricted
			"AssetRecordUpdated"		

	Conditions
	
		AssetTemplateEntered
			when (AssetTemplate entered)
			
		AssetEntered
			when (Asset entered)
			
		IsUpdatable
    		restricted
    		when (PayablesInvoiceDistribution.Status.Unreleased)
    		
    	CreateAssetInterfaceIsValid			
    		restricted
    		when (PayablesInvoiceDistribution.Status.Released
    		and    AssetInterfaceRel not exists)
    
	
	Field Rules
		Asset
			if (AssetTemplate entered)
				cannot be entered
					"EnterAssetNumberOrAssetTemplate,NotBoth"            
			if (Asset entered)
				if (PayablesInvoiceDistribution.DistributionAccount.ToAccountingEntity not = Asset.Company.AccountingEntity)
					confirmation required
						"DistributionAccountingEntityNotEqualToAssetCompanyAccountingEntity-OKToContinue?"

		AssetTemplate
			if (Asset entered)
				cannot be entered
					"EnterAssetNumberOrAssetTemplate,NotBoth"             
			if (AssetTemplate entered)
				if (PayablesInvoiceDistribution.DistributionAccount.ToAccountingEntity not = AssetTemplate.Company.AccountingEntity)
					confirmation required
						"DistributionAccountingEntityNotEqualToAssetTemplateCompanyAccountingEntity-OKToContinue?"

		AssetDescription
			default to PayablesInvoiceDistribution.PayablesInvoiceDetail.ItemDescription
			default to PayablesInvoiceDistribution.Description

		Combine
			if ((AssetTemplate entered
			or   Asset entered)
			and (PayablesInvoiceDistribution.TaxIndicator.Taxable
			and  LandedCost
			and  AssetCompanyRel.PayablesDistributionWithTax.Detail))
				constraint (Combine entered)
					"MustCombine;AMSystemOptionAPDistrib='D'AndLandedTax"	
		
		InServiceDate
			constraint (InServiceDate >= PayablesInvoiceDistribution.PayablesInvoice.InvoiceDate)
				"In-\ServiceDateMustBeEqualToOrGreaterThanInvoiceDate"             
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterInServiceDateWithoutATemplate"		
			else
				default to PurchaseDate			
				default to PayablesInvoice.InvoiceDate
		
		SerialNumber
			if (AssetTemplate not entered)
				cannot be entered
					"SerialNumberUsedOnlyWithAssetTemplate"		
		
		ModelNumber
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterModelNumberWithoutATemplate"	
		
		TagNumber
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterTagNumberWithoutATemplate"		
		
		ItemNumber
			if (AssetTemplate entered)	
				default to PayablesInvoiceDistribution.PayablesInvoiceDetail.VendorItem
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterAnItemWithoutATemplate"		

		
		PurchaseDate
			constraint (PurchaseDate >= PayablesInvoiceDistribution.PayablesInvoice.InvoiceDate)
				"PurchaseDateMustBeEqualToOrGreaterThanInvoiceDate"             		
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterPurchaseDateWithoutATemplate"	
			if (AssetTemplate entered)
				default to PayablesInvoice.InvoiceDate

		ItemQuantity
			if (AssetTemplate entered)
				required
					"MustEnterQuantityWithATemplate"		

		AssetGroup
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterAnAssetGroupWithoutATemplate"	
		
		AssetLocation						
			default to AssetTemplate.AssetLocation
			if (AssetTemplate not entered)
				cannot be entered
					"Location_\NameUsedOnlyWithAssetTemplate"		

		AssetDivision
			default to AssetTemplate.AssetDivision
			if (AssetTemplate not entered)
				cannot be entered
					"DivisionUsedOnlyWithAssetTemplate"                  
		
		BarCode
			if (AssetTemplate not entered)
				cannot be entered
					"Bar_\CodeUsedOnlyWithAssetTemplate"                     
		
		ItemTaxTransaction
			if (AssetTemplate not entered
			and Asset not entered)
				cannot be entered
					"CannotEnterTaxWithoutATemplateOrAssetNbr"		
				
			if (ItemTaxTransaction entered
			and PayablesInvoiceDistribution.TaxIndicator.Taxable)
				cannot be changed
					"AssetItemHasLandedTax;CannotEnterAdditionalTaxInformation"		
			if (PayablesInvoiceDistribution.IsTaxDistribution)
				default to PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount
				
		ItemTaxBase
			if (PayablesInvoice.InvoiceCurrency = Company.Currency)	
				default to ItemTaxTransaction
		
		ItemDescription
			if (AssetTemplate entered)	
				default to PayablesInvoiceDistribution.PayablesInvoiceDetail.ItemDescription
			if (AssetTemplate not entered)
				cannot be entered
					"CannotEnterItemDescriptionWithoutATemplate"	

		AssetAccountingUnitGroup
			default to AssetTemplate.AssetAccountingUnitGroup	
			
		LocationDetail
			if (LocationDetail entered)
				constraint (AssetTemplate entered)
					"Item_\Location_\DetailUsedOnlyWithAssetTemplate"       
			if (LocationDetail entered)
				constraint (AssetLocation entered)
					"Item_\Location_\DetailMustHave_\LocationPopulated"      
				
    Relations
        AssetCompanyRel
            one-to-one relation to AssetCompany
            Field Mapping uses symbolic key
                related.Company       = PayablesInvoiceDistribution.PayablesInvoice.Company

        AssetInterfaceRel	
            one-to-many relation to AssetInterface
            Field Mapping uses Set2
       			related.Company		= PayablesInvoiceDistribution.PayablesInvoice.Company
				related.AssetTemplate											= AssetTemplate
				related.InvoiceAndPurchaseOrderInformation.Vendor				= PayablesInvoiceDistribution.PayablesInvoice.Vendor
				related.InvoiceAndPurchaseOrderInformation.PayablesInvoice 		= PayablesInvoiceDistribution.PayablesInvoice
				related.InvoiceAndPurchaseOrderInformation.DistributionSequence = PayablesInvoiceDistribution.PayablesInvoiceDistribution

        AssetRepairImportRel	
            one-to-many relation to AssetRepairImport
            Field Mapping uses Set2
       			related.Company													= PayablesInvoiceDistribution.PayablesInvoice.Company
				related.InvoiceAndPurchaseOrderInformation.Vendor				= PayablesInvoiceDistribution.PayablesInvoice.Vendor
				related.InvoiceAndPurchaseOrderInformation.PayablesInvoice 		= PayablesInvoiceDistribution.PayablesInvoice
				related.InvoiceAndPurchaseOrderInformation.DistributionSequence = PayablesInvoiceDistribution.PayablesInvoiceDistribution
    			related.Asset													= Asset

		AssetTemplatesRel      
		  	one-to-many relation to AssetTemplate
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= PayablesInvoiceDistribution.PayablesInvoice.Company.FinanceEnterpriseGroup

		PayablesInvoiceDistributionRel
			one-to-one relation to PayablesInvoiceDistribution
            Field Mapping uses symbolic key
				related.Company   					 	= PayablesInvoiceDistribution.PayablesInvoice.Company
				related.PayablesInvoice 				= PayablesInvoiceDistribution.PayablesInvoice
				related.PayablesInvoiceDistribution    	= PayablesInvoiceDistribution
				
		PayablesInvoiceRel
			one-to-one relation to PayablesInvoice
            Field Mapping uses symbolic key
				related.Company   					 	= PayablesInvoiceDistribution.PayablesInvoice.Company
				related.PayablesInvoice 				= PayablesInvoiceDistribution.PayablesInvoice


	Rule Blocks	
		CreateAssetInterfaceRecord
			if (AssetTemplate entered)

				initialize LocalAMTransactionItemTax				
				initialize LocalAMITransactionItemTaxBase				

				if (PayablesInvoiceDistribution.AssetCompanyRel.PayablesDistributionWithTax.Summary)
					if (PayablesInvoiceDistribution.PayablesInvoiceDetail entered)
						if (invoking action = "PayablesInvoiceDistribution.CancelDistribution")
							LocalAMTransactionItemCost		= ((PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount + PayablesInvoiceDistribution.PayablesInvoiceDetail.SumOfLandedTaxDistributions) * -1)
						else
							LocalAMTransactionItemCost		= PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount	+ PayablesInvoiceDistribution.PayablesInvoiceDetail.SumOfLandedTaxDistributions
					else 
					if (invoking action = "PayablesInvoiceDistribution.CancelDistribution")
						LocalAMTransactionItemCost			= ((PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount + PayablesInvoiceDistribution.ExpenseDistributionTotalTax) * -1)
					else
						LocalAMTransactionItemCost			= PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount	+ PayablesInvoiceDistribution.ExpenseDistributionTotalTax
				else
					if (invoking action = "PayablesInvoiceDistribution.CancelDistribution")																	
						LocalAMTransactionItemCost			= PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount * -1	
					else																																
						LocalAMTransactionItemCost			= PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount	

				if (!PayablesInvoiceDistribution.IsTaxDistribution)	
					if (PayablesInvoiceDistribution.PayablesInvoiceDetail entered)
						if (invoking action = "PayablesInvoiceDistribution.CancelDistribution")
							LocalAMTransactionItemTax		= PayablesInvoiceDistribution.PayablesInvoiceDetail.SumOfLandedTaxDistributions * -1
						else
							LocalAMTransactionItemTax		= PayablesInvoiceDistribution.PayablesInvoiceDetail.SumOfLandedTaxDistributions
					else 
					if  (PayablesInvoiceDistribution.HasLinkedAndLandedTaxDistribution)
						if (invoking action = "PayablesInvoiceDistribution.CancelDistribution")	
							LocalAMTransactionItemTax 		= PayablesInvoiceDistribution.ExpenseDistributionTotalTax * -1
						else
							LocalAMTransactionItemTax 		= PayablesInvoiceDistribution.ExpenseDistributionTotalTax	

				LocalCurrencyTable 				 			= Company.CurrencyTable

				initialize LocalCurrencyExchange
				LocalFromCurrency 				 			= PayablesInvoice.InvoiceCurrency
				LocalCurrencyExchange.ToCurrency 			= Company.Currency
				LocalCurrencyRate							= LocalCurrencyExchange.OutputCurrencyRate

				LocalCurrencyAmount							= LocalAMTransactionItemCost
				LocalExchangeDate 				 			= PayablesInvoice.InvoiceDate
				LocalAMITransactionItemCostBase 			= LocalCurrencyExchange.OutputCurrencyAmount

				if (LocalAMTransactionItemTax != 0)
					initialize LocalCurrencyExchange
					LocalFromCurrency 				 		= PayablesInvoice.InvoiceCurrency
					LocalCurrencyExchange.ToCurrency 		= Company.Currency

					LocalCurrencyRate						= LocalCurrencyExchange.OutputCurrencyRate
					LocalCurrencyAmount						= LocalAMTransactionItemTax
					LocalExchangeDate 				 		= PayablesInvoice.InvoiceDate
					LocalAMITransactionItemTaxBase 			= LocalCurrencyExchange.OutputCurrencyAmount

				invoke Create AssetInterface
					if(AssetTemplate.Company.CopyUserFields)
						fill in user fields from PayablesInvoiceRel
						fill in user fields from PayablesInvoiceDistributionRel
					invoked.InterfaceSystem											= "AP"
					invoked.AssetTemplate											= AssetTemplate
					invoked.Company													= AssetTemplate.Company
					invoked.InvoiceAndPurchaseOrderInformation.InvoiceCompany		= PayablesInvoiceDistribution.PayablesInvoice.Company
					invoked.InvoiceAndPurchaseOrderInformation.Vendor				= PayablesInvoiceDistribution.PayablesInvoice.Vendor
					invoked.InvoiceAndPurchaseOrderInformation.PayablesInvoice 		= PayablesInvoiceDistribution.PayablesInvoice					
					invoked.InvoiceAndPurchaseOrderInformation.DistributionSequence = PayablesInvoiceDistribution.PayablesInvoiceDistribution
					invoked.AssetDescription 										= AssetDescription	
					invoked.TransactionItemCost										= LocalAMTransactionItemCost
					invoked.BaseItemCost.FunctionalAmount.EnteredCurrencyAmount		= LocalAMITransactionItemCostBase

					invoked.ItemNumber 												= ItemNumber
					if (ItemNumber entered)
						invoked.ItemNumber 											= ItemNumber
					else 
						invoked.ItemNumber											= PayablesInvoiceDistribution.PayablesInvoiceDetail.Item
					if (ItemDescription entered)
						invoked.Description											= ItemDescription
					else
						invoked.Description											= PayablesInvoiceDistribution.PayablesInvoiceDetail.ItemDescription						
					invoked.TagNumber 												= TagNumber
					invoked.BarCode													= BarCode
					invoked.AssetLocation											= AssetLocation
					invoked.LocationDetail											= LocationDetail
					invoked.HoldAsset 												= HoldAsset
					if (InServiceDate entered)
						invoked.InServiceDate 										= InServiceDate
					else	
						invoked.InServiceDate 										= PayablesInvoiceDistribution.PayablesInvoice.InvoiceDate
					if (PurchaseDate entered)
						invoked.PurchaseDate 										= PurchaseDate
						invoked.PostingDate											= PurchaseDate
					else	
						invoked.PurchaseDate 										= PayablesInvoiceDistribution.PayablesInvoice.InvoiceDate
						invoked.PostingDate											= PayablesInvoiceDistribution.DistributionDate
					invoked.SerialNumber 											= SerialNumber
					invoked.ModelNumber 											= ModelNumber
					invoked.InvoiceAndPurchaseOrderInformation.PurchaseOrder		= PayablesInvoiceDistribution.PurchaseOrder
					invoked.InvoiceAndPurchaseOrderInformation.PurchaseOrderLine	= PayablesInvoiceDistribution.PurchaseOrderLine
					invoked.ItemQuantity 											= ItemQuantity
					if (!PayablesInvoiceDistribution.IsTaxDistribution)	
						invoked.TransactionItemTax									= LocalAMTransactionItemTax
						invoked.BaseItemTax.FunctionalAmount.EnteredCurrencyAmount	= LocalAMITransactionItemTaxBase
					invoked.AssetGroup 												= AssetGroup
					invoked.Combine		 											= Combine
					invoked.BaseNumberOfDecimals 									= PayablesInvoiceDistribution.BaseNumberOfDecimals
					invoked.TransactionNumberOfDecimals 							= PayablesInvoiceDistribution.NumberOfDecimals	
					invoked.Currency 												= PayablesInvoiceDistribution.InvoiceCurrency
					invoked.AssetAccountingUnitGroup 								= AssetAccountingUnitGroup
					invoked.AssetDivision	 										= AssetDivision
					invoked.AssetLocation 											= AssetLocation

					if (AssetTemplate.Company.DefaultDepreciationExpenseProject)
						invoked.DepreciationExpenseProject.Project					= PayablesInvoiceDistribution.DistributionAccount.Project
					if (AssetTemplate.TransactionDimensionDefault.FromTemplate)		
						if (AssetTemplate.AccountingUnit entered)	
							invoked.AssetAccountingUnit								= AssetTemplate.AccountingUnit
						else 	
							LocalAssetAccountingUnit								= PayablesInvoiceDistribution.DistributionAccount
							invoked.AssetAccountingUnit								= LocalAssetAccountingUnit

					else 
						if (AssetTemplate.TransactionDimensionDefault.FromDistribution)					
							LocalAssetAccountingUnit								= PayablesInvoiceDistribution.DistributionAccount
							invoked.AssetAccountingUnit								= LocalAssetAccountingUnit
						else 
							if (AssetTemplate.TransactionDimensionDefault.LeaveBlank)
								initialize invoked.AssetAccountingUnit							
					invoked.DistributionCodeBlock									= PayablesInvoiceDistribution.DistributionAccount


			else
				LocalExchangeDate = PayablesInvoiceDistribution.PayablesInvoice.InvoiceDate	
				invoke Create AssetRepairImport
					if(Asset.Company.CopyUserFields)
						fill in user fields from PayablesInvoiceRel
						fill in user fields from PayablesInvoiceDistributionRel
					if (PayablesInvoiceDistribution.PayablesInvoiceDetail entered)
						invoked.InterfaceSystem										= "MA"
					else	
						invoked.InterfaceSystem										= "AP"
					invoked.Asset													= Asset
					if (Asset.Company entered
					and PayablesInvoice.MatchProcessType.Expense)
						constraint (Asset.Company = PayablesInvoiceDistribution.PayablesInvoice.Company)	
							"AssetCompany<Asset.Company>MustEqualInvoiceCompany<PayablesInvoiceDistribution.PayablesInvoice.Company>"
					invoked.Company													= PayablesInvoiceDistribution.PayablesInvoice.Company
					invoked.InvoiceAndPurchaseOrderInformation.Vendor				= PayablesInvoiceDistribution.PayablesInvoice.Vendor
					invoked.InvoiceAndPurchaseOrderInformation.PayablesInvoice		= PayablesInvoiceDistribution.PayablesInvoice
					invoked.InvoiceAndPurchaseOrderInformation.DistributionSequence = PayablesInvoiceDistribution.PayablesInvoiceDistribution
					invoked.Description 											= AssetDescription	
					if (invoking action = "PayablesInvoiceDistribution.CancelDistribution")		
						invoked.BaseRepairCost 										= PayablesInvoiceDistribution.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount * -1	
						invoked.TransactionRepairCost								= PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount	* -1							
					else																																							
						invoked.BaseRepairCost 										= PayablesInvoiceDistribution.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount
						invoked.TransactionRepairCost								= PayablesInvoiceDistribution.DistributionAmount.CurrencyAmount	
					invoked.BaseRepairTax											= ItemTaxBase
					invoked.TransactionRepairTax									= ItemTaxTransaction
					invoked.BaseNumberOfDecimals 									= PayablesInvoiceDistribution.BaseNumberOfDecimals
					invoked.TransactionNumberOfDecimals 							= PayablesInvoiceDistribution.NumberOfDecimals	
					invoked.Currency 												= PayablesInvoiceDistribution.InvoiceCurrency
					invoked.RepairDate 												= PayablesInvoiceDistribution.PayablesInvoice.InvoiceDate
					invoked.InvoiceAndPurchaseOrderInformation.PurchaseOrder		= PayablesInvoiceDistribution.PayablesInvoice.first MatchPurchaseOrderInvoicesRel.PurchaseOrder	



	Actions
		Create is a Create Action
			Action Rules

 			
		Update is an Update Action
			completion message is "<AssetProcessingMessage>"
			valid when (IsUpdatable)
			Action Rules


		Delete is a Delete Action
			Action Rules

		CreateAssetInterface is an Instance Action
			valid when (CreateAssetInterfaceIsValid)		
			Action Rules
				include CreateAssetInterfaceRecord

		DeleteAssetInterfaceRecords is an Instance Action  
			restricted
			Action Rules	
				if (AssetInterfaceRel exist)
					invoke Delete AssetInterfaceRel
				if (AssetRepairImportRel exist)
					invoke Delete AssetRepairImportRel 		

		UpdateSnapshotFields is an Instance Action
			restricted
			Action Rules
				Vendor				= PayablesInvoice.Vendor


		UpdateSnapshotFieldsSet is a Set Action			
			default label is untranslatable
			restricted
			Parameters
				PrmCompany		is a PayablesCompany
				PrmInvoice		is a PayablesInvoice
			Parameter Rules
				PrmCompany
				PrmInvoice
				
			Instance Selection
				where (Company 			= PrmCompany
				and    PayablesInvoice	= PrmInvoice)
				
			Action Rules
				Instance Rules
					Vendor				= PrmInvoice.Vendor
					
		Purge is a Purge Action
			restricted
			bypass relational integrity rules
					
								
    Sets
        Set1
            not indexed
            Sort Order
                Company
                PayablesInvoice
                PayablesInvoiceDistribution

		ByCompanyVendorInvoice
			Sort Order
            	Company
                Vendor
                PayablesInvoice
                PayablesInvoiceDistribution
