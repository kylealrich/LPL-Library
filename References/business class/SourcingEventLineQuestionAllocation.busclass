SourcingEventLineQuestionAllocation is a BusinessClass
    owned by ss
    prefix is EVLQA
	sql name is SELQCorrectAnswer
	
    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Ontology
    	symbolic key is SourcingEventLineQuestionAllocation
    	
	Persistent Fields
		DisplayOrder
		ScoreAllocation
		
	Transient Fields
		NewDisplayOrder		is a DisplayOrder

	Local Fields
		LocalDisplayOrder	is a DisplayOrder

	Field Rules
		DisplayOrder
			autosequence using SourcingEventLineQuestion.LastQuestionAnswerDisplayOrder

	Conditions
		EventAndLineInActionableState
			restricted
			when (SourcingEvent.InActionableState
			and	  SourcingEventLine.InLineActionableState)
			
		CreateActionValid
			restricted
			when (SourcingEventLineQuestion.ResponseType.List
			and	  EventAndLineInActionableState)

	Sets
		ByDisplayOrder
			duplicates
			Sort Order
				Company
				SourcingEvent
				SourcingEventLine
				SourcingEventLineQuestion
				DisplayOrder
				
    Actions
		Create is a Create Action
			valid when (CreateActionValid)
			
			Action Rules
    			constraint (SourcingEventLineQuestion.ResponseType.List)
    				"CanOnlyAddAllocationsToListTypeQuestions"
				constraint (SourcingEvent.InActionableState)
					"CannotAddAllocationsWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotAddAllocationsWhileLineIsPendingAward,Cancelled,OrAwarded"

     		Exit Rules
				if (SourcingEventLineQuestion.ResponseType.List)
					invoke Update SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.SourcingEventLineQuestionValue
						invoked.DisplayOrder = DisplayOrder

		InternalCreate is a Create Action
			restricted

     	Update is an Update Action
     		valid when (EventAndLineInActionableState)
     		Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotUpdateAllocationsWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotUpdateAllocationsWhileLineIsPendingAward,Cancelled,OrAwarded"
				LocalDisplayOrder = NewDisplayOrder

			Exit Rules
				if (LocalDisplayOrder entered
				and	SourcingEventLineQuestion.ResponseType.List)
					invoke Update SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.SourcingEventLineQuestionValue
						invoked.NewDisplayOrder = LocalDisplayOrder
						
     	Delete is a Delete Action		
			valid when (EventAndLineInActionableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteAllocationsWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotDeleteAllocationsWhileLineIsPendingAward,Cancelled,OrAwarded"
				invoke Delete SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.SourcingEventLineQuestionValue
