InventoryControlResult is a BusinessClass
	owned by ic
	prefix is Icrpt
	
	Ontology
		symbolic key is InventoryControlResult
		
	Patterns
	
	Persistent Fields
		ReportType		is Numeric size 3
			States
				VendorReorderAdviceReport				value is 1
				LocationReplenishmentReport				value is 2
				ReplenishmentByRequisitionPurchaseOrder	value is 3
				FinishedGoodReplenishmentReport			value is 4
				PhysicalInventoryVarianceUpdateReport	value is 5


				PhysicalInventoryCountSheets			value is 6


				AverageCostCalculation                  value is 7 
				ParLocationUtilization					value is 8
				MassRequisitionFromScheduledProcedure	value is 9
		Description									is a Description4
		PrintParameters								is Boolean
		PrintDate									is TimeStamp
		FinishedTimeStamp							is TimeStamp
		UserId										is Actor

		ParametersPhysicalInventoryCountSheets
		Report1										is BinaryDocument
		Report2										is BinaryDocument
		ItemGroup

		InventoryTransactionLineGroup
		Item
		DifferenceAmount                            is an InternationalAmount                   
		NewUnitCost                                 is an InternationalCost

		ParametersParLocationUtilization

		InventoryCompany
			default label is "Company"
		RequestingLocation
		ReleaseRequisition           				is Boolean
		ForecastDay      							is Numeric size 1
			States
				Today                   value is 1
				1Day                    value is 2
				2Day            	    value is 3
				3Day               		value is 4
				4Day                	value is 5
				5Day               		value is 6
		ProcedureDate                				is Date
		ProcessedCount				 				is Numeric 9
			default label is "RecordsProcessed"
		ErrorCount				     				is Numeric 9
			default label is "RecordsWithErrors"
		Status						 				is Numeric 1
			States
				InProcess       value is 0
				Complete		value is 1
								
	Local Fields
		LocalCurrentInventoryPeriodEndDate			is Date
		LocalGeneralLedgerPostingDate				is Date						

		LocalCurrentDate							is Date
		LocalPreviousMonthDate						is Date
		LocalNumberOfDays							is Numeric size 2



	Derived Fields
		AllTextForParameters is a MessageField
			"All"
		
		DerivedDifferenceInDates is a ComputeField
			type is Numeric size 6
			(LocalCurrentInventoryPeriodEndDate - LocalGeneralLedgerPostingDate)

		DerivedParLocationUtilizationReportTitle is a MessageField
			"Par_\Location_\Utilization"

		ParLocationUtilizationNotificationMessage is a MessageField
			"PDFReportParLocationUtilizationIsAvailableToView"
			
		DerivedUnderRecommendedMessage is a MessageField
			"*=_\No.OfDaysWithTransactionIsNotLessThanReplenishmentMinimumAndIsLessThan30."
			
		DerivedUnderMinimumMessage is a MessageField
			"N=_\No.OfDaysWithTransactionIsLessThanReplenishmentMinimum."
		
		DerivedNoDataToProcessMessage is a MessageField
			"**NoDataToProcess"
			
		DerivedReportGroupImpactTotal is a DerivedField
			type is like InternationalAmount
			return sum ParLocationUtilizationHeader set.ImpactTotal

		DerivedReportGroupNameID is a MessageField
			default label is "ReportGroup"
			"<ParametersParLocationUtilization.ReportGroup>_-_<ParametersParLocationUtilization.ReportGroup.Name>"
		

		DerivedMSCMParUtilizationBeginDate is a DerivedField
			type is Date
			restricted
			LocalCurrentDate			= current corporate date
			LocalPreviousMonthDate		= LocalCurrentDate - 1 month
			LocalNumberOfDays			= LocalCurrentDate day
			return LocalPreviousMonthDate - LocalNumberOfDays as days + 1 day

		DerivedMSCMParUtilizationEndDate is a DerivedField
			type is Date
			restricted
			return DerivedMSCMParUtilizationBeginDate + (DerivedMSCMParUtilizationBeginDate days in month) - 1 day


		DerivedProcessedCount is a DerivedField
            type is Alpha size 9
			default label is "RecordsProcessed"
			return ProcessedCount
			
		DerivedErrorCount is a DerivedField
            type is Alpha size 9
			default label is "RecordsWithErrors"
			return ErrorCount
	
	Field Rules
		ItemGroup
			required
			
		PrintParameters
			initial value is true
		
		PrintDate
			default to current timestamp
		
	Relations
		CountSheetPhysicalInventoryFreezesRel
			one-to-many relation to PhysicalInventoryFreeze
			Field Mapping uses symbolic key
				related.Company								= ParametersPhysicalInventoryCountSheets.InventoryCompany
				related.PhysicalInventorySelect				= ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect
			Instance Selection
				where ((!ParametersPhysicalInventoryCountSheets.PrintOnlyThisPage entered)
				or     (ParametersPhysicalInventoryCountSheets.PrintOnlyThisPage entered
				and     related.PhysicalInventoryFreeze.PhysicalInventoryFreezePage	= ParametersPhysicalInventoryCountSheets.PrintOnlyThisPage))




		CountSheetPhysicalInventoryFreezesLineOneOnlyRel
			one-to-many relation to PhysicalInventoryFreeze
			Field Mapping uses ByLine
				related.PhysicalInventorySelect									= ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect
				related.Company													= ParametersPhysicalInventoryCountSheets.InventoryCompany
				related.PhysicalInventoryFreeze.PhysicalInventoryFreezeLine		= 1

		CountSheetPhysicalInventoryTagsRel
			one-to-many relation to PhysicalInventoryTags
			Field Mapping uses symbolic key
				related.Company								= ParametersPhysicalInventoryCountSheets.InventoryCompany
				related.PhysicalInventorySelect				= ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect
					
		InventoryControlResultErrorsRel			is an InventoryControlResultError set
		
		InventoryControlResultsByTimeRel
			one-to-many relation to InventoryControlResult
			Field Mapping uses ByFinishedTimeStampDescending
				related.ItemGroup					= ItemGroup
				related.ReportType					= ReportType
				

		InventoryTransactionLineRel
			one-to-one relation to InventoryTransactionLine
			Field Mapping uses symbolic key
				related.Company                     				= InventoryTransactionLineGroup.InventoryCompany
				related.InventoryLocation           				= InventoryTransactionLineGroup.Location
				related.InventoryTransaction        				= InventoryTransactionLineGroup.InventoryTransaction
				related.TransactionSystemCode                       = InventoryTransactionLineGroup.SystemCode
				related.InventoryTransactionLine.WarehouseShipment 	= InventoryTransactionLineGroup.WarehouseShipment
				related.InventoryTransactionLine.LineNumber        	= InventoryTransactionLineGroup.LineNumber
				related.InventoryTransactionLine.ComponentSequence 	= InventoryTransactionLineGroup.ComponentSequence

		ApplicationAdministratorRoleRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor			= actor
				related.ActorRole.Role	= "ApplicationAdministrator_ST"

		SupplyManagementProcessorRoleRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor 			= actor
				related.ActorRole.Role	= "SupplyManagementProcessor_ST"

		InventoryManagerRoleRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor 			= actor
				related.ActorRole.Role	= "InventoryManager_ST"

		ParLevelSpecialistRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor 			= actor
				related.ActorRole.Role	= "ParLevelSpecialist_ST"		

		MassRequisitionResultDetailRel is a InventoryControlResultDetail set
			Instance Selection
				where (ReportType.MassRequisitionFromScheduledProcedure)

	Conditions
		InventoryCompanyEntered
			restricted
			when (InventoryTransactionLineGroup.InventoryCompany entered)
			
		IsMostRecentReport
			when (FinishedTimeStamp		= first InventoryControlResultsByTimeRel.FinishedTimeStamp)
			
		IsPhysicalInventoryVarianceUpdateReport
			when (ReportType.PhysicalInventoryVarianceUpdateReport)
			
		IsPhysicalInventoryCountSheets
			when (ReportType.PhysicalInventoryCountSheets)
			
		ParametersForCountSheets
			when (ParametersPhysicalInventoryCountSheets.PrintCountSheets)
			
		ParametersForTags
			when (ParametersPhysicalInventoryCountSheets.PrintTags)
			
		ParametersForBinGroups
			when (ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect.PhysicalInventoryBinGroupArray entered)
						
		Finished
			when (FinishedTimeStamp entered)
			
		ParametersForTagsAndFinished
			restricted
			when (ParametersForTags
			and   Finished)
			
		ParametersForCountSheetsAndFinished
			restricted
			when (ParametersForCountSheets
			and   Finished)

	
		IsParLocationUtilization
			restricted
			when (ReportType.ParLocationUtilization)
		
		ReportGroupEntered
			restricted
			when (ReportType.ParLocationUtilization
			and ParametersParLocationUtilization.ReportGroup entered)
		
		NoDataProcessed
			restricted
			when (ReportType.ParLocationUtilization
			and ParLocationUtilizationHeader set not exist)

		HasRecordsProcessed
			restricted
			when (ProcessedCount entered)

		HasRecordsWithErrors
			restricted
			when (ErrorCount entered)

		HasRecordsCompleted
			restricted
			when (HasRecordsProcessed
			and   not HasRecordsWithErrors)
			
	Sets
		ByFinishedTimeStampDescending
			Sort Order
				ItemGroup
				ReportType
				FinishedTimeStamp descending
				InventoryControlResult

		ByItemLocation
			Instance Selection
				where (InventoryCompanyEntered)
			Sort Order
				InventoryTransactionLineGroup.InventoryCompany
				InventoryTransactionLineGroup.Location
				Item
				InventoryControlResult
				
	Actions
		Create is a Create Action
			restricted
			
		Delete is a Delete Action
 			
		Purge is a Purge Action
			restricted

		CreatePhysicalInventoryCountSheets is a Create Action
			default label is "PhysicalInventoryCountSheets"
			Local Fields
				LocalAsyncId			is an AsyncActionRequest

			Field Rules
				ItemGroup	
					force default to ParametersPhysicalInventoryCountSheets.InventoryCompany.ItemGroup
				
				ParametersPhysicalInventoryCountSheets
					constraint (!ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect.Status.SelectIDNotInUse)
						"CannotUseSelectID<ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect>TheMassFreezeProgramHasNotBeenRun"
						
					constraint (!ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect.Status.SelectIDUpdated)
						"CannotUseSelectID<ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect>TheUpdateVarianceProgramHasAlreadyBeenRun"
						
					constraint (ParametersPhysicalInventoryCountSheets.PrintTags
					or          ParametersPhysicalInventoryCountSheets.PrintCountSheets)
						"MustChooseToPrintEitherTagsOrCountSheets,OrBoth"
						
					if (ParametersPhysicalInventoryCountSheets.PrintTags)
						constraint (!ParametersPhysicalInventoryCountSheets.PrintOnlyThisPage entered)
							"CannotPrintTagsAndSinglePageSimultaneously"
						
			Entrance Rules
				FinanceEnterpriseGroup		= actor.context.FinanceEnterpriseGroup
				ItemGroup					= ParametersPhysicalInventoryCountSheets.InventoryCompany.ItemGroup
				ReportType					= ReportType.PhysicalInventoryCountSheets
				PrintDate					= current timestamp
				UserId						= actor
				
			Exit Rules
				invoke CountSheetTags PhysicalInventoryFreeze in background		
					assign async action request id to LocalAsyncId
					invoked.ParametersPhysicalInventoryCountSheets		= ParametersPhysicalInventoryCountSheets
					invoked.PrmItemGroup								= ItemGroup
					invoked.PrmInventoryControlResult					= InventoryControlResult
										
				invoke FastUpdateWithoutEdits in background 	
					run after LocalAsyncId	
					
	   			invoke SendCountSheetsCompletionNotifications in background 	
	   				run after LocalAsyncId
		
		PurgePhysicalInventoryCountSheets is a Set Action
			Parameters
				PrmItemGroup			is an ItemGroup
				PrmThroughDate			is Date
										
			Parameter Rules
				PrmItemGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					confirmation required
						"Physical_Inventory_Count_SheetsAnd_TagsWillBePurgedThrough<PrmThroughDate>.DoYouWantToProceed?"
					
			Instance Selection
				where (ItemGroup				= PrmItemGroup
				and    ReportType.PhysicalInventoryCountSheets
				and    FinishedTimeStamp date 	<= PrmThroughDate)
				
			Action Rules
				Instance Rules
					invoke Purge
					
		ResubmitPhysicalInventoryCountSheets is an Instance Action
			valid when (ReportType.PhysicalInventoryCountSheets)
			Parameters
	            PrmResubmitInventoryCompany							is an InventoryCompany
	            PrmResubmitPhysicalInventorySelect					is a PhysicalInventorySelect
	            PrmResubmitPrintTags								is Boolean
	            PrmResubmitPrintCountSheets							is Boolean
	            PrmResubmitPrintFreezeQuantityOnDocuments			is Boolean
				PrmResubmitPrintOnlyThisPage						is Numeric size 6
	            PrmDescription										is a Description4
	            PrmPrintParameters									is Boolean
	            
	       	Parameter Rules
	       		PrmResubmitInventoryCompany
	       			initial value is ParametersPhysicalInventoryCountSheets.InventoryCompany
				PrmResubmitPhysicalInventorySelect
					initial value is ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect
				PrmResubmitPrintCountSheets
					initial value is ParametersPhysicalInventoryCountSheets.PrintCountSheets
				PrmResubmitPrintTags
					initial value is ParametersPhysicalInventoryCountSheets.PrintTags
				PrmResubmitPrintFreezeQuantityOnDocuments
					initial value is ParametersPhysicalInventoryCountSheets.PrintFreezeQuantityOnDocuments
				PrmResubmitPrintOnlyThisPage
					initial value is ParametersPhysicalInventoryCountSheets.PrintOnlyThisPage
				PrmDescription
					initial value is Description
				PrmPrintParameters
					initial value is PrintParameters
						
			Local Fields
				LocalParametersPhysicalInventoryCountSheets 	is a ParametersPhysicalInventoryCountSheets
				
			Action Rules
				LocalParametersPhysicalInventoryCountSheets.InventoryCompany				= PrmResubmitInventoryCompany
				LocalParametersPhysicalInventoryCountSheets.PhysicalInventorySelect			= PrmResubmitPhysicalInventorySelect
				LocalParametersPhysicalInventoryCountSheets.PrintCountSheets				= PrmResubmitPrintCountSheets
				LocalParametersPhysicalInventoryCountSheets.PrintTags						= PrmResubmitPrintTags
				LocalParametersPhysicalInventoryCountSheets.PrintFreezeQuantityOnDocuments	= PrmResubmitPrintFreezeQuantityOnDocuments
				
				invoke  CreatePhysicalInventoryCountSheets InventoryControlResult
					invoked.ItemGroup								= ItemGroup
					invoked.ParametersPhysicalInventoryCountSheets 	= LocalParametersPhysicalInventoryCountSheets
					invoked.Description								= PrmDescription	
					invoked.PrintParameters							= PrmPrintParameters

		FastUpdateWithoutEdits is an Instance Action
			restricted
			Action Rules
				FinishedTimeStamp	= current timestamp
				if (ReportType.PhysicalInventoryCountSheets)
					if (ParametersPhysicalInventoryCountSheets.PrintCountSheets)
						Report1			= CountSheets as pdf in landscape font offset is -2					
					if (ParametersPhysicalInventoryCountSheets.PrintTags)
						Report2			= CountSheetTags as pdf in landscape font offset is -3				
					
			Exit Rules
				if (ReportType.PhysicalInventoryCountSheets)
					if (ParametersPhysicalInventoryCountSheets.PrintTags)
						invoke Delete CountSheetPhysicalInventoryTagsRel

		SendCountSheetsCompletionNotifications is an Instance Action
			restricted
		    Local Fields
				LocalActor									is an Actor
        
			Action Rules
				if (ParametersPhysicalInventoryCountSheets.NotificationGroup entered)
					send notification
						to ParametersPhysicalInventoryCountSheets.NotificationGroup
						description is "CountSheetsProducedForSelectId:<ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect>.SeeReport:<Description>"
						priority is high
						detail is "CountSheetsProducedForSelectId:<ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect>.SeeReport:<Description>"
				else	
					LocalActor	= actor
					send notification
						to LocalActor
						description is "CountSheetsProducedForSelectId:<ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect>.SeeReport:<Description>"
						priority is high
						detail is "CountSheetsProducedForSelectId:<ParametersPhysicalInventoryCountSheets.PhysicalInventorySelect>.SeeReport:<Description>"
		
		CreateParUtilizationReport is a Create Action
			default label is "ParLocationUtilization"
			Local Fields
				LocalAsyncId			is an AsyncActionRequest
			
			Entrance Rules
				constraint (ParametersParLocationUtilization.IsValidForProcessing)
					"Either_Report_GroupOr_Par_LocationShouldBeEntered."
					
				FinanceEnterpriseGroup		= actor.context.FinanceEnterpriseGroup
				ItemGroup					= ParametersParLocationUtilization.InventoryCompany.ItemGroup
				ReportType					= ReportType.ParLocationUtilization
				UserId						= actor
			
			Field Rules
				ParametersParLocationUtilization
					if (ParametersParLocationUtilization.TransientPreviousMonthOnly)
						ParametersParLocationUtilization.DateRange.Begin 	= DerivedMSCMParUtilizationBeginDate
						ParametersParLocationUtilization.DateRange.End   	= DerivedMSCMParUtilizationEndDate
			Exit Rules
				invoke ParLocationUtilization ItemLocation
					invoked.PrmInventoryControlResult				= InventoryControlResult
					invoked.PrmCompany								= ParametersParLocationUtilization.InventoryCompany
					invoked.PrmReportGroup							= ParametersParLocationUtilization.ReportGroup
					invoked.PrmParLocation							= ParametersParLocationUtilization.InventoryLocation
					invoked.PrmReplenishDays						= ParametersParLocationUtilization.ReplenishDays
					invoked.PrmDateRange							= ParametersParLocationUtilization.DateRange
					invoked.PrmMinimumSamples						= ParametersParLocationUtilization.MinimumSamples
					invoked.PrmSortOption							= ParametersParLocationUtilization.SortOption
					invoked.PrmPrintOption							= ParametersParLocationUtilization.PrintOption
					invoked.PrmUpdateOption							= ParametersParLocationUtilization.UpdateOption
					invoked.PrmParLocationUtilizationReportGroup	= ParametersParLocationUtilization.ReportDistributionGroup

		GenerateReport is an Instance Action
			restricted
			default label is untranslatable
			Parameters
				PrmParLocationUtilizationReportGroup	is a ReportDistributionGroup
			Action Rules
				if (ReportType.ParLocationUtilization)
					generate document ParLocationUtilization as pdf in landscape font offset is -2
						set using action StoreGeneratedReport
					
			Exit Rules
				FinishedTimeStamp	= current timestamp
				
				invoke SendNotification
					invoked.PrmParLocationUtilizationReportGroup	= PrmParLocationUtilizationReportGroup
						
		StoreGeneratedReport is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				Report is BinaryDocument
			Action Rules
				Report1	= Report

		SendNotification is an Instance Action
			restricted
			Parameters
				PrmParLocationUtilizationReportGroup	is a ReportDistributionGroup
				
			Local Fields
				LocalActor				is an Actor
				
			Action Rules
				LocalActor = actor
				
				if (ReportType.ParLocationUtilization)
					if (PrmParLocationUtilizationReportGroup entered)
						invoke DistributeBinaryDocument PrmParLocationUtilizationReportGroup
							invoked.Report					= Report1
							invoked.ReportName				= "ParLocationUtilizationReport"
							invoked.ReportDistributionGroup	= PrmParLocationUtilizationReportGroup

					if (ApplicationAdministratorRoleRel exists)
						send notification
							to LocalActor
							description is "<ParLocationUtilizationNotificationMessage>"
							priority is medium
							detail is "<ParLocationUtilizationNotificationMessage>"
							linkback (webapp is ApplicationAdministrator navigation is ParLocationUtilizationPDF text is "<ParLocationUtilizationNotificationMessage>")
					else
					if (SupplyManagementProcessorRoleRel exists)
						send notification
							to LocalActor
							description is "<ParLocationUtilizationNotificationMessage>"
							priority is medium
							detail is "<ParLocationUtilizationNotificationMessage>"
							linkback (webapp is SupplyManagementProcessor navigation is ParLocationUtilizationPDF text is "<ParLocationUtilizationNotificationMessage>")
					else
					if (InventoryManagerRoleRel exists)
						send notification
							to LocalActor
							description is "<ParLocationUtilizationNotificationMessage>"
							priority is medium
							detail is "<ParLocationUtilizationNotificationMessage>"
							linkback (webapp is InventoryManager navigation is ParLocationUtilizationPDF text is "<ParLocationUtilizationNotificationMessage>")
					else
					if (ParLevelSpecialistRel exists)
						send notification
							to LocalActor
							description is "<ParLocationUtilizationNotificationMessage>"
							priority is medium
							detail is "<ParLocationUtilizationNotificationMessage>"
							linkback (webapp is ParLevelSpecialist navigation is ParLocationUtilizationPDF text is "<ParLocationUtilizationNotificationMessage>")
					else
						send notification
							to LocalActor
							description is "<ParLocationUtilizationNotificationMessage>"
							priority is medium
							detail is "<ParLocationUtilizationNotificationMessage>"
							linkback (webapp is ApplicationAdministrator navigation is ParLocationUtilizationPDF text is "<ParLocationUtilizationNotificationMessage>")
		
		FinishMassRequisition is an Instance Action
			restricted

			Parameters
				PrmProcessedCount				 				is Numeric 9
				PrmErrorCount				     				is Numeric 9
			Local Fields
				LocalActor 			is an Actor

			Action Rules
				ProcessedCount 		= PrmProcessedCount
				ErrorCount			= PrmErrorCount
				FinishedTimeStamp 	= current timestamp
				Status				= Status.Complete
				LocalActor 			= actor

				if (HasRecordsProcessed)
					send notification
						to LocalActor
						description is "Mass_RequisitionFromScheduledProcedureCompleted.ItemGroup:<ItemGroup>_Company:<InventoryCompany>_ResultID:<InventoryControlResult>"
						priority is high
						detail is "Mass_RequisitionFromScheduledProcedureCompleted.ItemGroup:<ItemGroup>_Company:<InventoryCompany>_ResultID:<InventoryControlResult>"
						linkback (webapp is InventoryManager navigation is MassRequisitionResultNav text is "Mass_RequisitionFromScheduledProcedureCompleted.ItemGroup:<ItemGroup>_Company:<InventoryCompany>_ResultID:<InventoryControlResult>")		
				else
					send notification
						to LocalActor
						description is "NoRecordsToProcessFor_Mass_Requisition.ItemGroup:<ItemGroup>_Company:<InventoryCompany>_ResultID:<InventoryControlResult>"
						priority is high
						detail is "NoRecordsToProcessFor_Mass_Requisition.ItemGroup:<ItemGroup>_Company:<InventoryCompany>_ResultID:<InventoryControlResult>"
						linkback (webapp is InventoryManager navigation is MassRequisitionResultNav text is "NoRecordsToProcessFor_Mass_Requisition.ItemGroup:<ItemGroup>_Company:<InventoryCompany>_ResultID:<InventoryControlResult>")		
					
