PurchaseOrderReceiptComment is a BusinessClass
	owned by po
	prefix is PORCT
	
	Ontology
		part of PurchaseOrderReceipt
			delete cascades
			relative key is SequenceNumber
			
	Patterns
		implements StaticJava
		implements Archivable
			
	Persistent Fields
		CommentTitle					is a CommentName
    	CommentType						is AlphaUpper size 1
    		States
                PrintOnInternalDocuments	value is "I"
                PrintOnReceivingDocument	value is "R" 
                PrintOnPurchaseOrder		value is "P" 
                InvoiceComments				value is "N" 
                PrintOnDeliveryTicket		value is "D"
				DisplayOnlyOnInternalDocuments value is "S"
                DisplayOnly					value is "O"
                PrintOnPurchaseOrderTrailer	value is "T"
    	CommentText
		Attachment						is an AlternateAttachment

	Transient Fields
		TransientPurchaseOrderReceipt is like PurchaseOrderReceipt
			derive value from PurchaseOrderReceipt		
		TransientPrintOnInternalDocuments	is Boolean
			default label is "PrintOnInternalDocuments"
		TransientPrintOnReceivingDocument	is Boolean
			default label is "PrintOnReceivingDocument"
		TransientInvoiceComments			is Boolean
			default label is "InvoiceComments"
		TransientPrintOnDeliveryTicket		is Boolean
			default label is "PrintOnDeliveryTicket"
		TransientDisplayOnlyOnInternalDocuments	is Boolean
			default label is "DisplayOnlyOnInternalDocuments"
		TransientDisplayOnly				is Boolean
			default label is "DisplayOnly"
		TransientPrintOnPurchaseOrderTrailer	is Boolean
			default label is "PrintOnPurchaseOrderTrailer"																	
	
	Field Rules
		SequenceNumber
			autosequence
				minimize contention
			
		CommentType	
			initial value is CommentType.PrintOnReceivingDocument
			default to CommentType.PrintOnReceivingDocument
			
			if (old CommentType.PrintOnPurchaseOrder
				or CommentType.PrintOnPurchaseOrder)
					cannot be changed
						"UpdateIsNotAvailableForPrintOnPurchaseOrderCommentType"

		CommentTitle
			required
			
		TransientPrintOnReceivingDocument
			initial value is true
				

	Derived Fields
		DerivedInitialCommentType	is a DerivedField
			type is like CommentType
			restricted
			if (CommentType entered)
				return CommentType
			else
			if (TransientPrintOnInternalDocuments)
				return CommentType.PrintOnInternalDocuments	
			else
			if (TransientPrintOnReceivingDocument)
				return CommentType.PrintOnReceivingDocument
			else
			if (TransientInvoiceComments)
				return CommentType.InvoiceComments
			else
			if (TransientPrintOnDeliveryTicket)
				return CommentType.PrintOnDeliveryTicket
			else
			if (TransientDisplayOnlyOnInternalDocuments)
				return CommentType.DisplayOnlyOnInternalDocuments
			else
			if (TransientDisplayOnly)
				return CommentType.DisplayOnly
			else
			if (TransientPrintOnPurchaseOrderTrailer)
				return CommentType.PrintOnPurchaseOrderTrailer

	Local Fields
		LocalCommentType		is like CommentType
		LocalAttributeCtr   	is Numeric 2
	
	Rule Blocks
		CreateComment
			invoke SystemCreate PurchaseOrderReceiptComment
				invoked.Company					= Company
				invoked.PurchaseOrderReceipt	= PurchaseOrderReceipt
				invoked.CommentTitle			= CommentTitle
				invoked.CommentType				= LocalCommentType
				invoked.CommentText				= CommentText
				invoked.Attachment.File			= Attachment.File
				invoked.Attachment.MimeType		= Attachment.MimeType
				invoked.Attachment.Title		= Attachment.Title
		
	Conditions
		IsCommentTypeSelected
			restricted
			when (TransientPrintOnInternalDocuments
			or    TransientPrintOnReceivingDocument
			or    TransientInvoiceComments
			or    TransientPrintOnDeliveryTicket
			or	  TransientDisplayOnlyOnInternalDocuments
			or    TransientDisplayOnly
			or    TransientPrintOnPurchaseOrderTrailer)
	
		HasAttachment
			restricted
			when (Attachment entered)
		




		
		DeleteAllowed
			when (not CommentType.PrintOnPurchaseOrder
			and	  PurchaseOrderReceipt.Status.Unreleased)
	
	Relations
	
	Sets
		ByCommentType
			Sort Order
				Company	
				PurchaseOrderReceipt
				CommentType
				SequenceNumber
	
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
				
	Actions

		SystemCreate is a Create Action
			restricted
	
		Create is a Create Action
			valid when (PurchaseOrderReceipt.Status.Unreleased)
			Field Rules
				CommentType
					constraint (not CommentType.PrintOnPurchaseOrder)
						"CreateIsNotAvailableForPrintOnPurchaseOrderCommentType"
			Action Rules
				if (CommentType not entered)
					constraint (TransientPurchaseOrderReceipt not entered 
					or          IsCommentTypeSelected)
						"CommentTypeIsRequired"
				else
					constraint(not IsCommentTypeSelected)
						"CannotEnterBothCommentTypeAndTransientCommentTypes"			
					
				invoke SystemCreate this instance
					invoked.CommentType		= DerivedInitialCommentType
			
			Exit Rules
				initialize LocalCommentType
				if (TransientPrintOnInternalDocuments
				and not CommentType.PrintOnInternalDocuments)
					LocalCommentType = CommentType.PrintOnInternalDocuments
					include CreateComment

				if (TransientPrintOnReceivingDocument
				and not CommentType.PrintOnReceivingDocument)
					LocalCommentType = CommentType.PrintOnReceivingDocument
					include CreateComment
																																																					
				if (TransientInvoiceComments
				and not CommentType.InvoiceComments)
					LocalCommentType = CommentType.InvoiceComments
					include CreateComment
					
				if (TransientPrintOnDeliveryTicket
				and not CommentType.PrintOnDeliveryTicket)
					LocalCommentType = CommentType.PrintOnDeliveryTicket
					include CreateComment
						
				if (TransientDisplayOnlyOnInternalDocuments
				and not CommentType.DisplayOnlyOnInternalDocuments)
					LocalCommentType = CommentType.DisplayOnlyOnInternalDocuments
					include CreateComment

				if (TransientDisplayOnly
				and not CommentType.DisplayOnly)
					LocalCommentType = CommentType.DisplayOnly
					include CreateComment
						
				if (TransientPrintOnPurchaseOrderTrailer
				and not CommentType.PrintOnPurchaseOrderTrailer)
					LocalCommentType = CommentType.PrintOnPurchaseOrderTrailer
					include CreateComment																												

		MobileCreate is a Create Action
			default label is "Create"
			valid when (PurchaseOrderReceipt.Status.Unreleased)
			Field Rules
				CommentType
					constraint (not CommentType.PrintOnPurchaseOrder)
						"CreateIsNotAvailableForPrintOnPurchaseOrderCommentType"
			Action Rules
				if (CommentType not entered)
					constraint (TransientPurchaseOrderReceipt not entered 
					or			IsCommentTypeSelected)
						"CommentTypeIsRequired"
				else
					constraint(not IsCommentTypeSelected)
						"CannotEnterBothCommentTypeAndTransientCommentTypes"

				invoke SystemCreate this instance
					invoked.CommentType 	= DerivedInitialCommentType

			Exit Rules
				initialize LocalCommentType
				if (TransientPrintOnInternalDocuments
				and not CommentType.PrintOnInternalDocuments)
					LocalCommentType = CommentType.PrintOnInternalDocuments
					include CreateComment

				if (TransientPrintOnReceivingDocument
				and not CommentType.PrintOnReceivingDocument)
					LocalCommentType = CommentType.PrintOnReceivingDocument
					include CreateComment

				if (TransientInvoiceComments
				and not CommentType.InvoiceComments)
					LocalCommentType = CommentType.InvoiceComments
					include CreateComment

				if (TransientPrintOnDeliveryTicket
				and not CommentType.PrintOnDeliveryTicket)
					LocalCommentType = CommentType.PrintOnDeliveryTicket
					include CreateComment

				if (TransientDisplayOnly
				and not CommentType.DisplayOnly)
					LocalCommentType = CommentType.DisplayOnly
					include CreateComment

				if (TransientPrintOnPurchaseOrderTrailer
				and not CommentType.PrintOnPurchaseOrderTrailer)
					LocalCommentType = CommentType.PrintOnPurchaseOrderTrailer
					include CreateComment

		Update is an Update Action
			valid when (PurchaseOrderReceipt.Status.Unreleased)

		
		Delete is a Delete Action
			valid when (DeleteAllowed)
	

		Purge is a Purge Action
			restricted
			Action Rules
				Attachment.LocalPurgeTriggered = true
				
		PurgePurchaseOrderReceiptComment is a Set Action
			restricted
			Parameters
				PrmCompany				is a PurchasingCompany
				PrmPurchaseOrderReceipt	is like PurchaseOrderReceipt
			
			Instance Selection
				include deleted records
				where (Company				= PrmCompany
				and    PurchaseOrderReceipt	= PrmPurchaseOrderReceipt)
				
			Action Rules
				Instance Rules
					invoke Purge
		
		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
					
