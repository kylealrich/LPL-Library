GainLossTotal is a BusinessClass
    owned by GeneralLedger
    prefix is GNLST

    Ontology
    	symbolic key is GainLossTotal
        
    Patterns
 		implements DynamicCreation
 		disable Auditing 
 		disable EffectiveDated
		disable AsOfDateProcessing

    Persistent Fields
		FunctionalTotal				is an TotalAmount
		AlternateTotal				is an TotalAmount
		AlternateTotal2				is an TotalAmount
		AlternateTotal3				is an TotalAmount
		ProjectTotal				is an TotalAmount
		ReportTotal1				is an TotalAmount
		ReportTotal2				is an TotalAmount
		ReportTotal3				is an TotalAmount
		ReportTotal4				is an TotalAmount
		ReportTotal5				is an TotalAmount
        Status						is Numeric 1
            States
                UnProcessed			value is 0
                Processed	   		value is 1
        LinkBackSequence			is Numeric 6
		FinanceDimension1			is like FinanceDimension1
		FinanceDimension2			is like FinanceDimension2
		FinanceDimension3			is like FinanceDimension3
		FinanceDimension4			is like FinanceDimension4
		FinanceDimension5			is like FinanceDimension5
		FinanceDimension6			is like FinanceDimension6
		FinanceDimension7			is like FinanceDimension7
		FinanceDimension8			is like FinanceDimension8
		FinanceDimension9			is like FinanceDimension9
		FinanceDimension10			is like FinanceDimension10
		
	Transient Fields
		TransientFunctionalAmount	is an TotalAmount
		TransientAlternateAmount	is an TotalAmount
		TransientAlternateAmount2	is an TotalAmount
		TransientAlternateAmount3	is an TotalAmount
		TransientProjectAmount		is an TotalAmount
		TransientReportAmount1		is an TotalAmount
		TransientReportAmount2		is an TotalAmount
		TransientReportAmount3		is an TotalAmount
		TransientReportAmount4		is an TotalAmount
		TransientReportAmount5		is an TotalAmount
        TransientLinkBackSequence	is Numeric 6

	Local Fields
		LocalCurrencyCode				is a Currency
		LocalClosePeriod				is a GeneralLedgerClosePeriod
		LocalJournalControl				is like GeneralLedgerJournalControl
		LocalAccountingEntity			is a AccountingEntity

	Rule Blocks
		UpdateAccountTotal
			FunctionalTotal			+= TransientFunctionalAmount
			AlternateTotal			+= TransientAlternateAmount
			AlternateTotal2			+= TransientAlternateAmount2
			AlternateTotal3			+= TransientAlternateAmount3

			ReportTotal1			+= TransientReportAmount1
			ReportTotal2			+= TransientReportAmount2
			ReportTotal3			+= TransientReportAmount3
			ReportTotal4			+= TransientReportAmount4
			ReportTotal5			+= TransientReportAmount5
			initialize TransientFunctionalAmount
			initialize TransientAlternateAmount
			initialize TransientAlternateAmount2
			initialize TransientAlternateAmount3
			initialize TransientProjectAmount
			initialize TransientReportAmount1
			initialize TransientReportAmount2
			initialize TransientReportAmount3
			initialize TransientReportAmount4
			initialize TransientReportAmount5

		GainLossLedgerTransactitonFields
			LocalAccountingEntity			= PrmAccountingEntity
			LocalClosePeriod				= PrmClosePeriod
			LocalJournalControl				= PrmJournalControl
			LocalTransactionSequence 		+= 1
			invoked.FinanceEnterpriseGroup						= PrmEnterpriseGroup
			invoked.AccountingEntity							= PrmAccountingEntity
			invoked.GeneralLedgerClosePeriod					= PrmClosePeriod
			invoked.GeneralLedgerJournalControl					= PrmJournalControl
			invoked.GeneralLedgerTransaction					= LocalTransactionSequence
			invoked.FinanceCodeBlock.Ledger						= GeneralLedgerJournalControlRel.Ledger
			invoked.FinanceCodeBlock.ToAccountingEntity			= PrmAccountingEntity
			invoked.FinanceCodeBlock.GeneralLedgerChartAccount	= LocalGainLossAccount
			if (FinanceEnterpriseGroup.AccountingUnitRequired)
				invoked.FinanceCodeBlock.AccountingUnit				= GainLossTotal.ZoneAccountingUnit
			if (FinanceEnterpriseGroup.FinanceDimension1Required)
				invoked.FinanceCodeBlock.FinanceDimension1			= GainLossTotal.ZoneDimension1
			invoked.CurrencyCode								= GainLossTotal.Currency
			invoked.AutoReverse									= PrmAutoReverse
			if (LocalProcess	= 1)
				invoked.GeneralLedgerEvent						= "GR"
			else
				invoked.GeneralLedgerEvent						= "CT"
			invoked.System										= GainLossTotal.System
			initialize invoked.TransactionAmount	
			invoked.GainLossRunSequence							= PrmRunSequence
			invoked.PrimaryLedger								= GainLossTotal.Ledger	
			invoked.TransientJournalizeGroup					= PrmJournalizeGroup
			invoked.DoNotUpdateZoneTotal						= true
			invoked.GainLosslinkSequence						= LinkBackSequence
			invoked.IsGainLossAccountTransaction				= true
			if (LocalFromTransaction	= 1)
				invoked.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount = LocalFunctionalGainLoss
				invoked.ReportCurrencyAmount.AlternateAmount.EnteredCurrencyAmount	= LocalAlternateGainLoss
				invoked.ReportCurrencyAmount.AlternateAmount2.EnteredCurrencyAmount	= LocalAlternateGainLoss2
				invoked.ReportCurrencyAmount.AlternateAmount3.EnteredCurrencyAmount	= LocalAlternateGainLoss3

				invoked.ReportCurrencyAmount.ReportAmount1.EnteredCurrencyAmount	= LocalReport1GainLoss
				invoked.ReportCurrencyAmount.ReportAmount2.EnteredCurrencyAmount	= LocalReport2GainLoss
				invoked.ReportCurrencyAmount.ReportAmount3.EnteredCurrencyAmount	= LocalReport3GainLoss
				invoked.ReportCurrencyAmount.ReportAmount4.EnteredCurrencyAmount	= LocalReport4GainLoss
				invoked.ReportCurrencyAmount.ReportAmount5.EnteredCurrencyAmount	= LocalReport5GainLoss						
			if (LocalFromTransaction	= 2)
				if (LocalFunctionalGainLoss		> 0)
					invoked.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount = LocalFunctionalGainLoss
				if (LocalAlternateGainLoss		> 0)
					invoked.ReportCurrencyAmount.AlternateAmount.EnteredCurrencyAmount	= LocalAlternateGainLoss
				if (LocalAlternateGainLoss2		> 0)
					invoked.ReportCurrencyAmount.AlternateAmount2.EnteredCurrencyAmount	= LocalAlternateGainLoss2
				if (LocalAlternateGainLoss3		> 0)
					invoked.ReportCurrencyAmount.AlternateAmount3.EnteredCurrencyAmount	= LocalAlternateGainLoss3											


				if (LocalReport1GainLoss		> 0)
					invoked.ReportCurrencyAmount.ReportAmount1.EnteredCurrencyAmount	= LocalReport1GainLoss
				if (LocalReport2GainLoss		> 0)
					invoked.ReportCurrencyAmount.ReportAmount2.EnteredCurrencyAmount	= LocalReport2GainLoss
				if (LocalReport3GainLoss		> 0)
					invoked.ReportCurrencyAmount.ReportAmount3.EnteredCurrencyAmount	= LocalReport3GainLoss
				if (LocalReport4GainLoss		> 0)
					invoked.ReportCurrencyAmount.ReportAmount4.EnteredCurrencyAmount	= LocalReport4GainLoss
				if (LocalReport5GainLoss		> 0)
					invoked.ReportCurrencyAmount.ReportAmount5.EnteredCurrencyAmount	= LocalReport5GainLoss						
			if (LocalFromTransaction	= 3)
				if (LocalFunctionalGainLoss		< 0)
					invoked.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount = LocalFunctionalGainLoss
				if (LocalAlternateGainLoss		< 0)
					invoked.ReportCurrencyAmount.AlternateAmount.EnteredCurrencyAmount	= LocalAlternateGainLoss
				if (LocalAlternateGainLoss2		< 0)
					invoked.ReportCurrencyAmount.AlternateAmount2.EnteredCurrencyAmount	= LocalAlternateGainLoss2
				if (LocalAlternateGainLoss3		< 0)
					invoked.ReportCurrencyAmount.AlternateAmount3.EnteredCurrencyAmount	= LocalAlternateGainLoss3											


				if (LocalReport1GainLoss		< 0)
					invoked.ReportCurrencyAmount.ReportAmount1.EnteredCurrencyAmount	= LocalReport1GainLoss
				if (LocalReport2GainLoss		< 0)
					invoked.ReportCurrencyAmount.ReportAmount2.EnteredCurrencyAmount	= LocalReport2GainLoss
				if (LocalReport3GainLoss		< 0)
					invoked.ReportCurrencyAmount.ReportAmount3.EnteredCurrencyAmount	= LocalReport3GainLoss
				if (LocalReport4GainLoss		< 0)
					invoked.ReportCurrencyAmount.ReportAmount4.EnteredCurrencyAmount	= LocalReport4GainLoss
				if (LocalReport5GainLoss		< 0)
					invoked.ReportCurrencyAmount.ReportAmount5.EnteredCurrencyAmount	= LocalReport5GainLoss						

		UpdateJournalTotals
			invoked.PrmFunctionalDrAmount			= FunctionalDrAmount
			invoked.PrmFunctionalCrAmount			= FunctionalCrAmount
			invoked.PrmAlternateDrAmount			= AlternateDrAmount
			invoked.PrmAlternateCrAmount			= AlternateCrAmount
			invoked.PrmAlternateDrAmount2			= AlternateDrAmount2
			invoked.PrmAlternateCrAmount2			= AlternateCrAmount2
			invoked.PrmAlternateDrAmount3			= AlternateDrAmount3
			invoked.PrmAlternateCrAmount3			= AlternateCrAmount3
			invoked.PrmRpt1DrAmount					= Rpt1DrAmount
			invoked.PrmRpt1CrAmount					= Rpt1CrAmount
			invoked.PrmRpt2DrAmount					= Rpt2DrAmount
			invoked.PrmRpt2CrAmount					= Rpt2CrAmount
			invoked.PrmRpt3DrAmount					= Rpt3DrAmount
			invoked.PrmRpt3CrAmount					= Rpt3CrAmount
			invoked.PrmRpt4DrAmount					= Rpt4DrAmount
			invoked.PrmRpt4CrAmount					= Rpt4CrAmount
			invoked.PrmRpt5DrAmount					= Rpt5DrAmount
			invoked.PrmRpt5CrAmount					= Rpt5CrAmount
			if (PrmAutoReverse)
				invoked.PrmAutoReverseDrAmount		= FunctionalDrAmount
				invoked.PrmAutoReverseCrAmount		= FunctionalCrAmount

	Derived Fields

    Conditions
           
    Sets
    	ForGainLossProcess
    		Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				GainLossTotal.AccountingEntity
				GainLossTotal.Ledger
				GainLossTotal.RunSequence
				Status
				GainLossTotal.Currency
				UniqueID

	Relations
		GeneralLedgerJournalControlRel
			one-to-one relation to GeneralLedgerJournalControl
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= LocalAccountingEntity
				related.GeneralLedgerClosePeriod	= LocalClosePeriod
				related.GeneralLedgerJournalControl	= LocalJournalControl

	Field Rules

    Actions
		Create is a Create Action
			restricted
			bypass field rules
			Action Rules
				LinkBackSequence		= TransientLinkBackSequence
				include UpdateAccountTotal

		Update is an Update Action
			restricted
			bypass field rules
			Action Rules
				include UpdateAccountTotal

		Delete is an Action
			restricted

		CreateGainLossTransactions is a Set Action
			restricted
			Parameters
				PrmEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis			is a ReportingBasis
					default label is "ReportingBasis"
				PrmAccountingEntity			is a AccountingEntity
					default label is "AccountingEntity"
				PrmRunSequence 				is Alpha 10
					default label is "RunSequence"
				PrmJournalControl			is a GeneralLedgerJournalControl
					default label is "JournalControl"
				PrmClosePeriod				is a GeneralLedgerClosePeriod
					default label is "GlobalLedgerClosePeriod"
				PrmOption						is Numeric 1
					default label is "Option"
					States
						Revalue					value is 1		
						Translation				value is 2		
				PrmAutoReverse				is Boolean
					default label is "AutoReverse"
				PrmReleased					is Boolean
					default label is "CreateReleased"
				PrmJournalizeGroup			is like JournalizeGroup
					default label is "JournalizeGroup"
				PrmPrimaryLedger			is a Ledger
					default label is "PrimaryLedger"

			Queue Mapping Fields
				PrmAccountingEntity.PostingCategory
				
			Local Fields	
				LocalTransactionSequence			is Numeric 12
				LocalActionCurrencyCode				is a Currency
				LocalFunctionalGainLoss	   		 	is a TotalAmount
				LocalAlternateGainLoss	   		 	is a TotalAmount
				LocalAlternateGainLoss2	   		 	is a TotalAmount
				LocalAlternateGainLoss3	   		 	is a TotalAmount

				LocalReport1GainLoss	    		is a TotalAmount
				LocalReport2GainLoss		    	is a TotalAmount
				LocalReport3GainLoss		    	is a TotalAmount
				LocalReport4GainLoss		    	is a TotalAmount
				LocalReport5GainLoss		    	is a TotalAmount
				LocalFromTransaction				is Numeric 1
				LocalGainLossAccount				is a GeneralLedgerChartAccount
				LocalGainAccount					is a GeneralLedgerChartAccount
				LocalLossAccount					is a GeneralLedgerChartAccount
				BypassStructureRelationEdit
				LocalProcess						is Numeric 1

			Instance Selection
				where (FinanceEnterpriseGroup			= PrmEnterpriseGroup
				and	   ReportingBasis					= PrmReportingBasis
				and	   GainLossTotal.AccountingEntity	= PrmAccountingEntity
				and	   GainLossTotal.Ledger				= PrmPrimaryLedger
				and    GainLossTotal.RunSequence		= PrmRunSequence
				and    Status.UnProcessed)

			Set Is
				PrmEnterpriseGroup
				PrmReportingBasis
				PrmAccountingEntity
				PrmPrimaryLedger
				PrmRunSequence

    		Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				GainLossTotal.AccountingEntity
				GainLossTotal.Ledger
				GainLossTotal.RunSequence
				Status
				GainLossTotal.Currency

			Accumulators
				FunctionalDrAmount
				FunctionalCrAmount
				AlternateDrAmount
				AlternateCrAmount
				AlternateDrAmount2
				AlternateCrAmount2
				AlternateDrAmount3
				AlternateCrAmount3
				Rpt1DrAmount
				Rpt1CrAmount
				Rpt2DrAmount
				Rpt2CrAmount
				Rpt3DrAmount
				Rpt3CrAmount
				Rpt4DrAmount
				Rpt4CrAmount
				Rpt5DrAmount
				Rpt5CrAmount

			Action Rules
				Empty Set Rules

				Set Rules
					Entrance Rules
						LocalAccountingEntity			= PrmAccountingEntity
						LocalClosePeriod				= PrmClosePeriod
						LocalJournalControl				= PrmJournalControl
						LocalTransactionSequence		= GeneralLedgerJournalControlRel.DerivedGLTransaction

				FinanceEnterpriseGroup Set Rules
					Exit Rules
						LocalAccountingEntity			= PrmAccountingEntity
						if (!PrmReleased)




							invoke UpdateJournalTotals GeneralLedgerJournalControlRel
								include UpdateJournalTotals

				GainLossTotal.Currency Set Rules
					Entrance Rules
						LocalActionCurrencyCode			= GainLossTotal.Currency

					Exit Rules

				Instance Rules
					BypassStructureRelationEdit = true
					Status						= 1
					LocalGainAccount			= GainLossTotal.GainAccount
					LocalLossAccount			= GainLossTotal.LossAccount
					LocalFunctionalGainLoss		= FunctionalTotal * -1
					LocalAlternateGainLoss		= AlternateTotal  * -1
					LocalAlternateGainLoss2		= AlternateTotal2 * -1
					LocalAlternateGainLoss3		= AlternateTotal3 * -1

					LocalReport1GainLoss		= ReportTotal1    * -1
					LocalReport2GainLoss		= ReportTotal2    * -1
					LocalReport3GainLoss		= ReportTotal3    * -1
					LocalReport4GainLoss		= ReportTotal4    * -1
					LocalReport5GainLoss		= ReportTotal5    * -1
					initialize LocalFromTransaction
					if  (LocalFunctionalGainLoss entered
					or   LocalAlternateGainLoss entered
					or   LocalAlternateGainLoss2 entered
					or   LocalAlternateGainLoss3 entered

					or   LocalReport1GainLoss entered
					or   LocalReport2GainLoss entered
					or   LocalReport3GainLoss entered
					or   LocalReport4GainLoss entered
					or   LocalReport5GainLoss entered)
						LocalProcess			= PrmOption
						LocalGainLossAccount		= LocalGainAccount
						if (LocalGainAccount		= LocalLossAccount)
							LocalFromTransaction		= 1
							if (PrmReleased)
								invoke CreateReleasedGLSystemTransactionsNoRules Released GeneralLedgerTransaction
									include GainLossLedgerTransactitonFields
							else
								invoke CreatePostedGLSystemTransactionsNoRules Posted GeneralLedgerTransaction
									include GainLossLedgerTransactitonFields
						else
							if (LocalFunctionalGainLoss		> 0
							or  LocalAlternateGainLoss		> 0
							or  LocalAlternateGainLoss2		> 0
							or  LocalAlternateGainLoss3		> 0

							or  LocalReport1GainLoss		> 0
							or  LocalReport2GainLoss		> 0
							or  LocalReport3GainLoss		> 0
							or  LocalReport4GainLoss		> 0
							or  LocalReport5GainLoss		> 0)
								LocalFromTransaction		= 2
								if (PrmReleased)
									invoke CreateReleasedGLSystemTransactionsNoRules Released GeneralLedgerTransaction
										include GainLossLedgerTransactitonFields
								else
									invoke CreatePostedGLSystemTransactionsNoRules Posted GeneralLedgerTransaction
										include GainLossLedgerTransactitonFields
							if (LocalGainAccount		!= LocalLossAccount)
								LocalGainLossAccount	 = LocalLossAccount
							if (LocalFunctionalGainLoss		< 0
							or  LocalAlternateGainLoss		< 0
							or  LocalAlternateGainLoss2		< 0
							or  LocalAlternateGainLoss3		< 0

							or  LocalReport1GainLoss		< 0
							or  LocalReport2GainLoss		< 0
							or  LocalReport3GainLoss		< 0
							or  LocalReport4GainLoss		< 0
							or  LocalReport5GainLoss		< 0)
								LocalFromTransaction		= 3
								if (PrmReleased)
									invoke CreateReleasedGLSystemTransactionsNoRules Released GeneralLedgerTransaction
										include GainLossLedgerTransactitonFields
								else
									invoke CreatePostedGLSystemTransactionsNoRules Posted GeneralLedgerTransaction
										include GainLossLedgerTransactitonFields
						if (FunctionalTotal > 0 ) 
							FunctionalDrAmount			+= FunctionalTotal
							FunctionalCrAmount			-= FunctionalTotal
						else
							FunctionalDrAmount			-= FunctionalTotal
							FunctionalCrAmount			+= FunctionalTotal
						if (AlternateTotal > 0 ) 
							AlternateDrAmount			+= AlternateTotal
							AlternateCrAmount			-= AlternateTotal
						else
							AlternateDrAmount			-= AlternateTotal
							AlternateCrAmount			+= AlternateTotal
						if (AlternateTotal2 > 0 ) 
							AlternateDrAmount2			+= AlternateTotal2
							AlternateCrAmount2			-= AlternateTotal2
						else
							AlternateDrAmount2			-= AlternateTotal2
							AlternateCrAmount2			+= AlternateTotal2
						if (AlternateTotal3 > 0 ) 
							AlternateDrAmount3			+= AlternateTotal3
							AlternateCrAmount3			-= AlternateTotal3
						else
							AlternateDrAmount3			-= AlternateTotal3
							AlternateCrAmount3			+= AlternateTotal3				
						if (ReportTotal1 > 0 ) 
							Rpt1DrAmount				+= ReportTotal1
							Rpt1CrAmount				-= ReportTotal1
						else
							Rpt1DrAmount				-= ReportTotal1
							Rpt1CrAmount				+= ReportTotal1
						if (ReportTotal2 > 0 ) 
							Rpt2DrAmount				+= ReportTotal2
							Rpt2CrAmount				-= ReportTotal2
						else
							Rpt2DrAmount				-= ReportTotal2
							Rpt2CrAmount				+= ReportTotal2
						if (ReportTotal3 > 0 ) 
							Rpt3DrAmount				+= ReportTotal3
							Rpt3CrAmount				-= ReportTotal3
						else
							Rpt3DrAmount				-= ReportTotal3
							Rpt3CrAmount				+= ReportTotal3
						if (ReportTotal4 > 0 ) 
							Rpt4DrAmount				+= ReportTotal4
							Rpt4CrAmount				-= ReportTotal4
						else
							Rpt4DrAmount				-= ReportTotal4
							Rpt4CrAmount				+= ReportTotal4
						if (ReportTotal5 > 0 ) 
							Rpt5DrAmount				+= ReportTotal5
							Rpt5CrAmount				-= ReportTotal5
						else
							Rpt5DrAmount				-= ReportTotal5
							Rpt5CrAmount				+= ReportTotal5

		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis			is like ReportingBasis
					default label is "ReportingBasis"
				PrmAccountingEntity			is like AccountingEntity
					default label is "AccountingEntity"
				PrmCurrencyLedger			is like Ledger
					default label is "Ledger"
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and	   ReportingBasis				= PrmReportingBasis
				and   ((PrmAccountingEntity entered
				and	   GainLossTotal.AccountingEntity = PrmAccountingEntity)				
				or	  (!PrmAccountingEntity entered))
				and   ((PrmCurrencyLedger entered
				and    	GainLossTotal.CurrencyLedger	=  PrmCurrencyLedger)
				or	  (!PrmCurrencyLedger entered)))

			Set Is
				PrmFinanceEnterpriseGroup
				PrmReportingBasis
				PrmAccountingEntity

			Action Rules
				Empty Set Rules
				Instance Rules
					invoke Delete 
