IntercompanyBillingRechargeItem is a BusinessClass
	owned by intercobilling
	prefix is ICBIT
	sql name is IBRechargeItem
	representative text is "<IntercompanyBillingRechargeItem>-<Description>"

    Ontology
        symbolic key is IntercompanyBillingRechargeItem 
            
    Persistent Fields
        Description
        FromReceivableAccount	       	        is a FinanceCodeBlock
		FromReceivablePLAccount					is a FinanceCodeBlock
        ToPayableAccount   	    	   	        is a FinanceCodeBlock
        ToPayablePLAccount  	    	   	    is a FinanceCodeBlock
        DefaultUnitOfMeasure					is a IntercompanyBillingUnitOfMeasure
        TaxCategory								is a ProductTaxCategory 
		TaxUsageCode
        Taxable									is Boolean
		DefaultTaxCode							is a TaxCode 
		AlternatePayablesTaxCode				is a TaxCode 
		Active									is Boolean
        LineType         						is AlphaUpper 1
            States
                Goods    value is "G"
                Services value is "S"
		HSNSACCode
		PayablesDistributionCode				is a IntercompanyBillingDistributionCode
		ReceivablesDistributionCode				is a IntercompanyBillingDistributionCode

	Field Rules
		Description
			default to IntercompanyBillingRechargeItem
			
		DefaultUnitOfMeasure
			required
			
			constraint (DefaultUnitOfMeasure.Active)
				"UnitOfMeasureIsInactive"

		DefaultTaxCode 
			if (DefaultTaxCode entered)
				constraint (Taxable)
					"Default_\Tax_\CodeCannotBeEnteredIf_\Recharge_\ItemIsNot_\Taxable"

		AlternatePayablesTaxCode 
			if (AlternatePayablesTaxCode entered)
				constraint (DefaultTaxCode entered)
					"Alternate_\Payables_\Tax_\CodeCanOnlyBeEnteredIfA_\Default_\Tax_\CodeIsEntered"
				constraint (AlternatePayablesTaxCode != DefaultTaxCode)
					"Alternate_\Payables_\Tax_\CodeCannotBeTheSameAsThe_\Default_\Tax_\Code.Leave_\Alternate_\Payables_\Tax_\CodeBlankToUseTheSame_\Tax_\CodeForReceivablesAndPayables."
				if (action type.Create
				or  DefaultTaxCode changed
				or  AlternatePayablesTaxCode changed)
					confirmation required
						"Alternate_\Payables_\Tax_\CodeCanOnlyBeUsedWithThe_\Infor_\TaxSystem.Alternate_\Payables_\Tax_\CodeMustHaveTheSameRateAsThe_\Default_\Tax_\CodeAtTheTimeOfDocumentCreation.Continue?"

		LineType
			initial value is "G"

		PayablesDistributionCode
			constraint (PayablesDistributionCode.Type.Payables)
				"PayablesDistributionCodeIsNotDefinedAsAPayablesType"

		ReceivablesDistributionCode
			constraint (ReceivablesDistributionCode.Type.Receivables)
				"ReceivablesDistributionCodeIsNotDefinedAsAReceivablesType"

	Conditions
		ReceivablePLAccountHasAccountingEntity
			restricted
			when (FromReceivablePLAccount.ToAccountingEntity entered)
			
		PayablePLAccountHasAccountingEntity
			restricted
			when (ToPayablePLAccount.ToAccountingEntity entered)
		
		IsHSNSACCodeEnabled
			restricted 
			when (ICBAndHSNSACGeneralLedgerCompanyRel exists)
			
	Relations
		
		ICBAndHSNSACGeneralLedgerCompanyRel
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
		  	Instance Selection
		    	where (related.FinanceEnterpriseGroup = IntercompanyBillingGroup
		        and    related.IntercompanyBilling    = true
		        and    related.RequireHSNSACCode      = true)


	Actions
        Create is a Create Action
        	Action Rules
        		constraint (FromReceivablePLAccount.GeneralLedgerChartAccount entered)
        			"AtMinimum,MustEnterAReceivableChartAccount"
        			
        		constraint (ToPayablePLAccount.GeneralLedgerChartAccount entered)
        			"AtMinimum,MustEnterAPayableChartAccount"
        			
        		Active = true

		Update is an Update Action
			Action Rules
        		constraint (FromReceivablePLAccount.GeneralLedgerChartAccount entered)
        			"AtMinimum,MustEnterAReceivableChartAccount"
        			
        		constraint (ToPayablePLAccount.GeneralLedgerChartAccount entered)
        			"AtMinimum,MustEnterAPayableChartAccount"

        Delete is a Delete Action
