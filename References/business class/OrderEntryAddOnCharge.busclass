OrderEntryAddOnCharge is a BusinessClass
    owned by bl
    prefix is OEM
    classic name is OEMISC

    Ontology
        symbolic key is OrderEntryAddOnCharge
            classic set name is OEMSET1
            classic name for OrderEntryAddOnCharge.Currency is CURRENCY-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        OrderDiscount			is Boolean
            classic name is ORD-DISC-FL
        ChargeType
        AddOnCharge
            classic name is AOC-CODE
        UnitPrice               is an InternationalCost
        PricePercent            is a CostPercent
            classic name is PRICE-PCT
        Price                   is an InternationalAmount
        UnitCost                is an InternationalCost
        CostPercent
            classic name is COST-PCT
        Cost                    is an InternationalCost
        UpdateSalesAnalysis		is Boolean
            restricted
            classic name is UPD-SA-FL
        TaxStatus				is AlphaUpper size 1
        	States
            	Taxable   value is "T"
            	Exempt    value is "E"
            classic name is TAX-EXEMPT-CD
        TaxCode
        MajorSalesClass
            classic name is SALES-MAJCL
        MinorSalesClass
            classic name is SALES-MINCL
        ProductTaxCategory
            classic name is PROD-TAX-CAT




        Active                  is Boolean
            classic name is ACTIVE-FLAG
        InUse                   is Boolean
            classic name is USED-FL
        TrackType
        SalesAccount            is a FinanceCodeBlock
            classic name for SalesAccount.AccountingUnit is SLS-ACCT-UNIT
            classic name for SalesAccount.GeneralLedgerChartAccount is SLS-ACCOUNT
        OffsetAccount           is a FinanceCodeBlock
            classic name for OffsetAccount.AccountingUnit is OFF-ACCT-UNIT
            classic name for OffsetAccount.GeneralLedgerChartAccount is OFF-ACCOUNT
        CostOfGoodsSoldAccount  is a FinanceCodeBlock
            classic name for CostOfGoodsSoldAccount.AccountingUnit is CGS-ACCT-UNIT
            classic name for CostOfGoodsSoldAccount.GeneralLedgerChartAccount is CGS-ACCOUNT
		UNSPSCCode
		HSNSACCode

    Conditions

        IsCurrencyEntered
        	restricted
            when (OrderEntryAddOnCharge.Currency entered)

        IsFlatAmount
        	restricted
            when (ChargeType.FlatAmount)
        
        IsDefaultChargeType
        	restricted
        	when (ChargeType.FlatAmount
        	or    ChargeType not entered)

        IsGeneralLedgerMasterCostOfGoods
        	restricted
            when (CostOfGoodsSoldAccount.AccountingUnit entered
            or    CostOfGoodsSoldAccount.GeneralLedgerChartAccount entered)


        IsGeneralLedgerMasterOffset
        	restricted
            when (OffsetAccount.AccountingUnit entered
            or    OffsetAccount.GeneralLedgerChartAccount entered)


        IsGeneralLedgerMasterSales
        	restricted
            when (SalesAccount.AccountingUnit entered
            or    SalesAccount.GeneralLedgerChartAccount entered)


        IsInventoryCompany
        	restricted
            when (MajorSalesClass entered
            or    MinorSalesClass entered)

        IsMajorclass
        	restricted
            when (MajorSalesClass entered
            or    MinorSalesClass entered)

        IsMinorclass
        	restricted
            when (MinorSalesClass entered)

        IsPerUnit
        	restricted
            when (ChargeType.UnitPrice)

        IsPercentage
        	restricted
            when (ChargeType.Percentage)

        IsAddOnCharge
        	restricted
            when (AddOnCharge entered)

        IsPriceByCurrency
        	restricted
            when (not Company.CurrencyBasedPricing)

        IsPriceByCompanyCurrency
        	restricted
            when (Company.CurrencyBasedPricing)

        IsTaxCodeEntered
        	restricted
            when (TaxCode entered)



    Relations
		
		
		BillingCompanyRel
			one-to-one relation to BillingCompany
			Field Mapping uses symbolic key
				related.Company = Company	
		

       	TaxEntityRel				 
            one-to-one relation to TaxEntity
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
                related.TaxEntity 				= Company.AccountingEntity
                       
        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = Company

       	MajorClassRel
            one-to-one relation to MajorClass
            Field Mapping uses symbolic key
                related.ItemGroup  = InventoryCompanyRel.ItemGroup
                related.ClassType  = "S"
                related.MajorClass = MajorSalesClass

        MinorClassRel
            one-to-one relation to MinorClass
            Field Mapping uses symbolic key
                related.ItemGroup  = InventoryCompanyRel.ItemGroup
                related.ClassType  = "S"
                related.MajorClass = MajorSalesClass
                related.MinorClass = MinorSalesClass

    Field Rules

        ChargeType
            initial value is ChargeType.FlatAmount
            default to ChargeType.FlatAmount
	    	cannot be changed
				"CannotChangeChargeType"
        Description
            required

        Active
            default to true


        TaxStatus
            default to TaxStatus.Taxable

			
		Company
			constraint (TaxEntityRel exists)
				"TaxEntityDoesNotExist"
		
		OrderEntryAddOnCharge	
			if (!BillingCompanyRel.CurrencyBasedPricing)
				if (OrderEntryAddOnCharge.Currency not entered)
				
					OrderEntryAddOnCharge.Currency = BillingCompanyRel.Currency
				else
					constraint (OrderEntryAddOnCharge.Currency = BillingCompanyRel.Currency)
						"OnlyBaseCurrencyCanBeEntered"
			else
				constraint (OrderEntryAddOnCharge.Currency entered)
					"CurrencyMustBeEnteredForMultipleCurrencyCompany"		
			

		TrackType
			cannot be changed
				"CannotChangeTrackType" 				
					
		Price
			if (ChargeType.FlatAmount)
				if (Cost not entered)
					required
						"MustEnterAmountOrCostForFlatAmountType"
			else 	
				cannot be entered
					"EnterOnlyOneOfUnitPrice,PercentageOrAmount"			
		
		PricePercent			
			if (ChargeType.Percentage)
				if (CostPercent not entered)
					required
						"MustEnterPricePercentageOrCostPercentageForPercentageType"
			else 	
				cannot be entered
					"EnterOnlyOneOfUnitPrice,PercentageOrAmount"						
		
		UnitPrice
			if (ChargeType.UnitPrice)
				if (UnitCost  not entered)
					required		
						"MustEnterUnitPriceOrCostForUnitPriceType"
			else 	
				cannot be entered
					"EnterOnlyOneOfUnitPrice,PercentageOrAmount"			
		
		Cost
			if (not ChargeType.FlatAmount)	
				cannot be entered
					"EnterOnlyOneOfUnitPrice,PercentageOrAmount"
		UnitCost
			if (not ChargeType.UnitPrice)	
				cannot be entered
					"EnterOnlyOneOfUnitPrice,PercentageOrAmount"
		CostPercent
			if (not ChargeType.Percentage)	
				cannot be entered
					"EnterOnlyOneOfUnitPrice,PercentageOrAmount"
		
					
		SalesAccount
			required
		
		OffsetAccount
			required				
		
		CostOfGoodsSoldAccount		
			required	
	
	Attach Rules
		constraint (Active)
			"AddOnChargeIsInactive"
	
	Actions
		Create is a Create Action
		
		Delete is a Delete Action
		
		Update is an Update Action			
				
	
							
