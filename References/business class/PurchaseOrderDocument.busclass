PurchaseOrderDocument is a BusinessClass
	owned by po
	prefix is PODT
	
	Ontology
		symbolic key is PurchaseOrderDocument
		
	Persistent Fields
		VendorGroup
		Vendor
			default label is "PurchasingVendor"
		PurchaseFromLocation
		PurchaseOrderDocumentTemplate
		Active							is Boolean
	
	Attach Rules
		constraint (Active)
			"PurchaseOrderDocumentTemplateFileIsNotActive"
	
	Conditions
		RecordExists
			restricted
			when (PurchaseOrderDocument entered)
			
		DocumentByCompany
			when (Company entered)
		
		DocumentByVendor
			when (VendorGroup entered
			and	  Vendor entered)
			
		DocumentForPurchasingVendor
			when (DocumentByVendor
			and	  PurchaseFromLocation not entered
			and	  Active)
			
	Relations
		SampleTemplateRel
			one-to-many relation to SampleDocumentTemplate
			Field Mapping uses ByType
				related.Type            = 1 
				
		PurchasingVendorRel
			one-to-many relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup				= VendorGroup
			Instance Selection
				where (related.PurchasingVendorExists)
	
	Field Rules
		PurchaseOrderDocumentTemplate
			required
			
		Active
			default to true
			
	Actions
		CreateDocumentByCompany is a Create Action
			Entrance Rules
				constraint (Company entered)
					"CompanyIsRequired"
					
				constraint (VendorGroup not entered
				and			Vendor not entered)
					"CannotEnterVendorGroupOrPurchasingVendorWhenCreatingDocumentForCompany"
					
		CreateDocumentByPurchasingVendor is a Create Action
			Entrance Rules
				constraint (VendorGroup entered
				and			Vendor entered)
					"VendorGroupAndVendorAreRequired"
					
				constraint (Company not entered)
					"CannotEnterPurchasingCompanyWhenCreatingDocumentForVendor"
					
			Action Rules
				if (PurchaseFromLocation entered)
					constraint (PurchaseFromLocation exists)
						"PurchaseFromLocationDoesNotExist"
					
		Update is an Update Action
		
		Delete is a Delete Action
