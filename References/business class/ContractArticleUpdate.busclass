ContractArticleUpdate is a BusinessClass
    owned by po
    prefix is CTAU

    Ontology
    	symbolic key is ContractArticleUpdate

    Persistent Fields
		OriginalTitle				is a Description
		RevisedTitle				is a Description
		OriginalHeaderText			is Alpha 100
		RevisedHeaderText			is Alpha 100
		ActionTaken					is Numeric 2
			States
				Rejected	value is 1
				Approved	value is 2
				Withdrawn	value is 3
				Disapproved	value is 4
		Status						is Numeric 2
			States
				Draft		value is 1
				Pending		value is 2
				Complete	value is 3
		RequestType					is Numeric 2
			default label is "Action"
			States
				Create		value is 1
				Update		value is 2
				Delete		value is 3
				Restore		value is 4
		SpecificRequestType			is Numeric 2
			default label is "RequestType"
			States
				Create					value is 1
				UpdateArticle			value is 2
				AddAttachment			value is 3
				DeleteAttachment		value is 4
				DeleteArticle			value is 5
				Restore					value is 6
		RequestingActor				is an Actor
			default label is "RequestedBy"
		RejectionComment			is Text
		ApprovalComment				is Text
		RequestDateTime				is TimeStamp
			default label is "RequestDate"
		ApprovalStage				is Numeric 2
			States
				BeforeSupplierNegotiation			value is 1
				SupplierNegotiation					value is 2
				AfterSupplierNegotiation			value is 3
				BeforeSupplierAddendumNegotiation	value is 4
				SupplierAddendumNegotiation			value is 5
				AfterSupplierAddendumNegotiation	value is 6
		TermNegotiationVersion		is a SequenceNumber
			default label is "Version"
		AdhocArticle				is Boolean
		AddedDuringNegotiation		is Boolean
		NoChangesMade				is Boolean
		SupplierRequest				is Boolean

	Transient Fields

	Derived Fields
		DerivedComparedTitle is a NativeField
			type is RichText

		DerivedComparedHeaderText is a NativeField
			type is RichText

		DerivedCompareVersionFirstLastHeaderText is a NativeField
			type is RichText
			restricted

		DerivedRequesterEmailAddress is a DerivedField
			type is Alpha 100
			restricted
			return RequestingActor.ContactInfo.EmailAddress
		
		DerivedRequesterPhoneNumber is a DerivedField
			type is Alpha 100
			return RequestingActor.ContactInfo.TelephoneNumber.TelephoneDisplay

		DerivedRequesterName is a DerivedField
			type is Alpha 100
			return RequestingActor.PersonName.PreferredFirstAndLastName

	Conditions
		DisplayRejectionComment
			restricted
			when (RejectionComment entered)

		RequesterEmailAddressExists
			restricted
			when (DerivedRequesterEmailAddress entered)

		ChangesMade
			restricted
			when (!NoChangesMade
			and	 (((ContractArticle.AdhocArticle
			or	    AddedDuringNegotiation)
			and	  RequestType.Create)
			or	  RequestType.Update
			or	  RequestType.Delete))

		ChangesApproved
			restricted
			when (ChangesMade
			and	  ActionTaken.Approved)

		MoreRecentUpdateInThisVersionExists
			restricted
			when (MoreRecentUpdateInThisVersionRel exists)

		UpdateOrDelete
			restricted
			when (RequestType.Update
			or	  RequestType.Delete)

	Relations
		MoreRecentUpdateInThisVersionRel
			one-to-many relation to ContractArticleUpdate
			Field Mapping uses symbolic key
				related.ContractGroup				= ContractGroup
				related.Contract					= Contract
				related.ContractArticle				= ContractArticle
			Instance Selection
				where (related.TermNegotiationVersion = TermNegotiationVersion
				and	   related.RequestDateTime > RequestDateTime)

		UpdatesInThisVersionRel
			one-to-many relation to ContractArticleUpdate
			Field Mapping uses symbolic key
				related.ContractGroup				= ContractGroup
				related.Contract					= Contract
				related.ContractArticle				= ContractArticle
			Instance Selection
				where (related.TermNegotiationVersion = TermNegotiationVersion)

    Field Rules
		RevisedTitle
			if (SpecificRequestType.UpdateArticle)
				required

 	Sets
 		ByRequestDateTime
 			indexed
 			Sort Order
 				RequestDateTime
 				ContractArticleUpdate
 				ContractArticle
 				Contract
 				ContractGroup

	Actions
		AutoCreate is a Create Action
			restricted
			Action Rules
				RevisedTitle				= ContractArticle.Title
				RevisedHeaderText			= ContractArticle.HeaderText
				RequestingActor				= actor
				RequestType					= 1		
				SpecificRequestType			= 1		
				RequestDateTime				= current timestamp
				OriginalTitle				= ContractArticle.Title
				OriginalHeaderText			= ContractArticle.HeaderText
				Status						= 3		
				ActionTaken					= 2		
				TermNegotiationVersion		= 1

		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
