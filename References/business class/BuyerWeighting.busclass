BuyerWeighting is a BusinessClass
    owned by ss
    prefix is BYW

    Ontology
    	symbolic key is BuyerWeighting

    Persistent Fields
    	Criteria 						is Text
		BestPossibleScore				is Numeric 3
		Active							is Boolean

	Local Fields
		LocalCompany			is like Company 
		LocalSourcingEvent		is Numeric size 8
				
	Conditions
			
	Relations
		EventBuyerWeightingRel
			one-to-one relation to SourcingEventBuyerWeighting
			Field Mapping uses ByBuyerWeighting
				related.Company				    = LocalCompany
				related.SourcingEvent			= LocalSourcingEvent
				related.BuyerWeighting			= BuyerWeighting
			
	Sets
			
	Field Rules
		Criteria
			required
		BestPossibleScore
			constraint (BestPossibleScore >= 0)
				"BestPossibleScoreCannotBeLessThanZero"
			constraint (BestPossibleScore <= 100)
		 		"BestPossibleScoreCannotBeGreaterThan100"
		Active
			default to true

	Attach Rules
		constraint (Active)
			"BuyerWeightingCriteriaIsInactive"
				
	Actions
		Create is a Create Action 
							
		Update is an Update Action
						
		Delete is a Delete Action
					

		AttachToEvent is an Instance Action
			Parameters
				Company				is a SourcingCompany
				SourcingEvent
				
			Parameter Rules
				Company
					required
				SourcingEvent
					required
					
			Action Rules
				LocalCompany	   = Company
				LocalSourcingEvent = SourcingEvent
				
				constraint (!EventBuyerWeightingRel exists)
					"BuyerWeightingCriteriaIsAlreadyAttachedToThisSourcingEvent"

				invoke Create SourcingEventBuyerWeighting
					invoked.Company						= Company
					invoked.SourcingEvent				= SourcingEvent
					invoked.BuyerWeighting				= BuyerWeighting
					fill in fields from BuyerWeighting
