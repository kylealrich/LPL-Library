PurchaseOrderTransactionDetail is a BusinessClass
    owned by po
    prefix is POT
    classic name is POTRANSDTL

    Ontology
        symbolic key is PurchaseOrderTransactionDetail
            classic set name is POTSET1
            classic name for PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType is DOC-TYPE
            classic name for PurchaseOrderTransactionDetail.DocumentNumberNumeric is DOC-NBR-NUM
            classic name for PurchaseOrderTransactionDetail.LineNumber is LINE-NBR
            classic name for PurchaseOrderTransactionDetail.TransactionSequence is TRANS-SEQ

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        SourceDocumentAlpha
            classic name is SOURCE-DOC-A
        PurchaseOrderReceipt
            classic name is REC-NUMBER
        Lot					 is an ItemLot
        Sublot
        Serial               is an ItemSerialNumber
        	delete ignored
        Bin                  
        Item
        Quantity
        	precision is DerivedNumberOfDecimalsQuantity
        SecondaryQuantity    is a Quantity
            classic name is SECONDARY-QTY
            precision is DerivedNumberOfDecimalsQuantity
        LotOnHold            is Boolean
            classic name is HOLD-FLAG
		ShelfLife
		ManufacturingDate
        LotExpirationDate    is Date
            classic name is EXPIRE-DATE
		ReceivingLocation     is an InventoryLocation
		ExpirationDateSource		

	Context Fields
    	ContextPurchaseOrderReceipt      is a PurchaseOrderReceipt
        ContextPurchaseOrderReceiptLine  is a PurchaseOrderReceiptLine
	
	
	Local Fields 
		LocalLotExistedAtReceipt		is Boolean
		LocalDoNotUpdateBins        	is Boolean
		LocalUpdateFromPOUDIDtl        	is Boolean
		LocalOldLot						is an ItemLot
		LocalOldSerial					is an ItemSerialNumber		
		UOMCalculation
	
    Conditions
        IsUnreleased
        	restricted
	   		when (IsReceivingPutaway 
			or    PurchaseOrderReceiptLineRel2.PurchaseOrderReceipt.Status.Unreleased
			or   (PurchaseOrderAdjLineRel exists and PurchaseOrderAdjLineRel.POReceiptAdjustmentAndInspection.Status.Unreleased)
			or   (PurchaseOrderInspectionLineRel exists and PurchaseOrderInspectionLineRel.POReceiptAdjustmentAndInspection.Status.Unreleased))

		IsReceivingPutaway 
			restricted
			when (ContextPurchaseOrderReceipt.PutawayStatus.ForReceivingPutaway)
			
        HasLotOrSerial
            classic name is LOT-OR-SERIAL
            restricted
            when (Lot entered
            or    Serial entered)

        HasLot
            classic name is LOT-TRACKED
            restricted
            when (Lot entered)

        HasPurchaseOrderReceipt
            classic name is RECEIPT-EXISTS
            restricted
            when (PurchaseOrderReceipt entered)

		IsReceiptOrAdjustment
            classic name is RECEIVE-OR-ADJ
            restricted
            when (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving
            or    PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment)
        
        IsReceipt
            classic name is RECEIVE-OR-ADJ
            restricted
            when (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving)
            
		HasLineToInspect
			restricted

            when (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection 
            and PurchaseOrderReceipt entered)

		IsValidForAdjustment
			restricted
            when (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment 
            and PurchaseOrderReceipt entered)

		IsReceiving
			restricted
			when (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving
			and   PurchaseOrderReceipt entered)
			
		IsNegativeAdjustment
			restricted
			when (IsValidForAdjustment
			and   PurchaseOrderAdjLineRel.Quantity < 0)
		
		IsPositiveAdjustment
			restricted
			when (IsValidForAdjustment
			and	  PurchaseOrderAdjLineRel.Quantity > 0)

        HasSerial
            classic name is SERIAL-TRACKED
            restricted
            when (Serial entered)
            
        HasBin
            classic name is BIN-TRACKED
            restricted
            when (Bin entered)
		
		HasDifferentLotExpirationDate
			restricted
			when (POTransactionDetailsDifferentLotExpirationRel exists)

        IsVendorReturn
        	restricted
            when (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.VendorReturn)

        IsLotRequiredAtReceipt
        	restricted  
			when (ItemLocationRel.LotTracked.LotRequiredAtReceipt or ItemLocationAdjRel.LotTracked.LotRequiredAtReceipt or ItemLocationInspRel.LotTracked.LotRequiredAtReceipt) 

			
		IsSerialRequiredAtReceipt
			restricted
			when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt or ItemLocationAdjRel.SerialTracked.SerialRequiredAtReceipt or ItemLocationInspRel.SerialTracked.SerialRequiredAtReceipt)


        IsUDIRequiredAtReceipt
        	restricted  
			when (ItemLocationRel.UDITracked.UDIRequiredAtReceipt or ItemLocationAdjRel.UDITracked.UDIRequiredAtReceipt or ItemLocationInspRel.UDITracked.UDIRequiredAtReceipt) 

		IsBinTracked 
			restricted	
			when (ItemLocationRel.BinTracked or ItemLocationAdjRel.BinTracked or ItemLocationAdjRel.BinTracked)
		
		IsBinLotSerialTracked
			restricted
			when (IsLotRequiredAtReceipt or IsSerialRequiredAtReceipt or IsBinTracked)	
			       
        IsCatchWeightItem
        	restricted
        	when (Item.CatchWeightCode.AllTransactionsRequireBoth or Item.CatchWeightCode.ReceiptRequiresBoth)
        
        ItemLotExists
        	restricted
        	when (ItemLotRel exists)
        
        IsLotExists
        	restricted
        	when (StockOnHandDetailRel exists and StockOnHandDetailRel.StockOnHandQuantity != 0 and ItemLotRel.LotExpirationDate != LotExpirationDate and ItemLotRel.LotExpirationDate != 0)	   
        
   
    	IsBinQuantityOutOfBalance
    		restricted
    		when (DerivedAllowedAdjustmentQuantity < 0)
    		
    	IsCatchWeightAndNotIsSerialRequiredAtReceipt
    		when (PurchaseOrderReceiptLineRel.IsCatchWeightItem
    		and	  not PurchaseOrderReceiptLineRel.IsSerialRequiredAtReceipt)
    		
    	IsNotCatchWeightAndNotIsSerialRequiredAtReceipt
    		when (not PurchaseOrderReceiptLineRel.IsCatchWeightItem
    		and	  not PurchaseOrderReceiptLineRel.IsSerialRequiredAtReceipt)
    		
    	IsCatchWeightAndNotIsSerialRequiredAtReceiptForInspection
    		when (PurchaseOrderInspectionLineRel.IsCatchWeightItem
    		and	  not PurchaseOrderInspectionLineRel.IsSerialRequiredAtReceipt)
    		
    	IsNotCatchWeightAndNotIsSerialRequiredAtReceiptForInspection
    		when (not PurchaseOrderInspectionLineRel.IsCatchWeightItem
    		and	  not PurchaseOrderInspectionLineRel.IsSerialRequiredAtReceipt)
    		
    	IsCatchWeightAndNotIsSerialRequiredAtReceiptForAdjustment
    		when (PurchaseOrderAdjLineRel.IsCatchWeightItem
    		and	  not PurchaseOrderAdjLineRel.IsSerialRequiredAtReceipt)
    		
    	IsNotCatchWeightAndNotIsSerialRequiredAtReceiptForAdjustment
    		when (not PurchaseOrderAdjLineRel.IsCatchWeightItem
    		and	  not PurchaseOrderAdjLineRel.IsSerialRequiredAtReceipt)

		HasItemLocationStockUOM
			when (ItemLocationRel.HasItemLocationStockUOM)

		IsSerialNumberInUse
			restricted
			when (OtherPurchaseOrderTransactionDetailWithSameSerialRel exists and Serial exists)

		HasItemStorageLocation
			restricted
			when (ItemStorageLocationRel exists)

		IsTrackingDetailBalanced
			restricted
			when (DerivedSumDetailTrackingQuantity = Quantity
			or (IsCatchWeightItem and DerivedSumDetailTrackingQuantity = SecondaryQuantity))
		
		IsValidForDelivery
			restricted
			when (not IsUDIRequiredAtReceipt and PurchaseOrderReceipt.IsMSCMEnabled)

		IsTrackingPanelEnabled
			restricted
			when (IsValidForDelivery
			and  (MSCMPurchaseOrderReceiptTrackingRel exists
			or   PurchaseOrderReceiptLineRel.IsTrackingPanelEnabled))
			
    Derived Fields
    	DerivedQuantityInPurchaseOrderUOM	is a DerivedField
    		type is like Quantity
    			precision is PurchaseOrderReceiptLineRel.PurchaseOrderLine.DerivedNumberOfDecimalsQuantity
    		if (PurchaseOrderReceiptLineRel.ReceivedUOM not = PurchaseOrderReceiptLineRel.PurchaseOrderLine.EnteredBuyUOM)
    			initialize UOMCalculation
    			UOMCalculation.InputQuantity			= Quantity
				UOMCalculation.InputUOM					= PurchaseOrderReceiptLineRel.ReceivedUOM
				UOMCalculation.InputToUOM				= PurchaseOrderReceiptLineRel.PurchaseOrderLine.EnteredBuyUOM
				UOMCalculation.Method					= UOMCalculation.Method.ConvertToAlternate
				return UOMCalculation.OutputQuantity
			else
				return Quantity
    	
    	DerivedNumberOfDecimalsQuantity is a ConditionalField
			type is Numeric size 1
			restricted
			if (Item exists)
				Item.NumberOfDecimalsQuantity
			else
			if (InventoryCompanyRel exists)
				InventoryCompanyRel.NumberOfDecimalsQuantity
			else
				4
				
    	DerivedLotSubLot is a StringField
			type is AlphaUpper 17
			Lot
			" - "
			Sublot

		DerivedQuantityUOMDisplay is a StringField
			type is Alpha 20
			default label is "Quantity"
			Quantity " " PurchaseOrderReceiptLineRel.ReceivedUOM
		
		DerivedLotSubLotForDisplay is a DerivedField
			type is AlphaUpper 17
			if (Lot entered)
				if (Sublot entered)
					return DerivedLotSubLot
				else
					return Lot
			else
				return blank
		
		DifferentLotExpirationDate is a MessageField
			"CannotEnterSameLotWithDifferentExpiryDate"

    	QuantityOutOfBalanceLess is a DerivedField 
			type is Boolean
			restricted

			if (PurchaseOrderInspectionLineRel.POReceiptAdjustmentAndInspectionLine.Quantity not = sum PurchaseOrderTransactionDetailsRels.Quantity)
				return true
			else
				if (PurchaseOrderAdjLineRel.POReceiptAdjustmentAndInspectionLine.Quantity not = sum PurchaseOrderTransactionDetailsAdjRels.Quantity)
					return true
		
					
		QuantityOutOfBalanceMore is a DerivedField 
			type is Boolean
			restricted

			if (PurchaseOrderInspectionLineRel.POReceiptAdjustmentAndInspectionLine.Quantity  = sum PurchaseOrderTransactionDetailsRels.Quantity)
				return true
			else
			if (PurchaseOrderAdjLineRel.POReceiptAdjustmentAndInspectionLine.Quantity  = sum PurchaseOrderTransactionDetailsAdjRels.Quantity)
					return true
			else
				return false 
		
		DerivedTotalInspectedQuantityPerBin is a DerivedField
			type is like Quantity
			restricted
			return sum PurchaseOrderTransactionDetailsInspectionBinRels.Quantity
			
		DerivedTotalAdjustedQuantityPerBin is a DerivedField
			type is like Quantity
			restricted
			return sum PurchaseOrderTransactionDetailsAdjustmentBinRels.Quantity

		DerivedTotalReceivedQuantityPerBin is a DerivedField
			type is like Quantity
			restricted
			return sum PurchaseOrderTransactionDetailsReceiptBinRels.Quantity

		DerivedAllowedAdjustmentQuantity is a DerivedField
			type is like Quantity
			restricted		
			return (DerivedTotalReceivedQuantityPerBin + DerivedTotalAdjustedQuantityPerBin) 


    	DerivedTotalReceivedDetailQuantity is a DerivedField
    		type is like Quantity
    		restricted
   			return sum DetailsReceivedRel.Quantity
   			
   		DerivedTotalAdjustedDetailQuantity is a DerivedField
   			type is like Quantity
   			restricted
   			return sum DetailsAdjustedRel.Quantity
   			
   		DerivedTotalDetailQuantity is a DerivedField
   			type is like Quantity
   			restricted
   			return (DerivedTotalReceivedDetailQuantity + DerivedTotalAdjustedDetailQuantity)
    		
    	DerivedTotalInspectionDetailQuantity is a DerivedField
    		type is like Quantity
    		restricted
   			return (Quantity + sum SimilarInspectionDetailsRel.Quantity)
   			
   		DerivedQuantityIsRequiredMessage is a DerivedField
   			type is MessageField
   			if (not IsCatchWeightItem)
				return "Quantity is required"

		DerivedLocationStockUOM										is a DerivedField 
			type is like UnitOfMeasure
			return ItemLocationRel.ItemLocationStockUOM.UnitOfMeasure

		DerivedLocationStockUOMMultiplier							is a DerivedField  
			type is like UOMMultiplier
			return ItemLocationRel.ItemLocationStockUOM.ItemUOM.UOMConversion

    	DerivedQuantity												is a DerivedField
    		type is like Quantity
			if (PurchaseOrderReceiptLineRel2.ReceivedUOM not = DerivedLocationStockUOM)
    			initialize UOMCalculation
    			UOMCalculation.InputQuantity			= Quantity
				UOMCalculation.InputUOM					= PurchaseOrderReceiptLineRel2.ReceivedUOM
				UOMCalculation.InputToUOM				= DerivedLocationStockUOM
				UOMCalculation.Method					= UOMCalculation.Method.ConvertToAlternate
				return UOMCalculation.OutputQuantity
			else
				return Quantity

		DerivedSumDetailTrackingQuantity is a ComputeField
            type is like Quantity
			restricted
            (sum MSCMPurchaseOrderReceiptTrackingRel.Quantity)

		DerivedWarehouseStorageLocation	is a DerivedField
			type is like WarehouseStorageLocation
			default label is "WarehouseStorageLocation"
			return Bin.WarehouseStorageLocation

		DerivedRemainingShelfLife is a ComputeField
			type is Numeric 6
			default label is "Remaining_Shelf_Life"
			(LotExpirationDate - current corporate date)

		DerivedWarehouseLotExpirationDate is a ComputeField
			type is Date
			restricted
			(ManufacturingDate + ShelfLife)

    Relations

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            required
            Field Mapping uses symbolic key
                related.Company = Company
        
        PurchaseOrderReceiptLineRel
            one-to-one relation to PurchaseOrderReceiptLine
            valid when (HasPurchaseOrderReceipt)
            Field Mapping uses symbolic key
                related.Company                  = Company
                related.PurchaseOrderReceipt     = PurchaseOrderTransactionDetail.DocumentNumberNumeric                
                related.PurchaseOrderReceiptLine = PurchaseOrderTransactionDetail.LineNumber 

        PurchaseOrderReceiptLineRel2
            one-to-one relation to PurchaseOrderReceiptLine
            valid when (HasPurchaseOrderReceipt)
            Field Mapping uses symbolic key
                related.Company                  = Company

                related.PurchaseOrderReceipt     = PurchaseOrderReceipt 
                related.PurchaseOrderReceiptLine = PurchaseOrderTransactionDetail.LineNumber 
                               
       	PurchaseOrderInspectionLineRel
            one-to-one relation to POReceiptAdjustmentAndInspectionLine

            Field Mapping uses symbolic key
                related.Company                  			 = Company
                related.AdjustmentInspectionDocumentType 	 = AdjustmentInspectionDocumentType.ReceiptInspection
                related.POReceiptAdjustmentAndInspection     = PurchaseOrderTransactionDetail.DocumentNumberNumeric                
                related.POReceiptAdjustmentAndInspectionLine = PurchaseOrderTransactionDetail.LineNumber          
			
		PurchaseOrderAdjLineRel
            one-to-one relation to POReceiptAdjustmentAndInspectionLine

            Field Mapping uses symbolic key
                related.Company                  			 = Company
                related.AdjustmentInspectionDocumentType 	 = AdjustmentInspectionDocumentType.ReceiptAdjustment
                related.POReceiptAdjustmentAndInspection     = PurchaseOrderTransactionDetail.DocumentNumberNumeric                
                related.POReceiptAdjustmentAndInspectionLine = PurchaseOrderTransactionDetail.LineNumber
                          
		
        PoreturnlnRel
            one-to-one relation to VendorReturnLine
            valid when (IsVendorReturn)
            Field Mapping uses symbolic key
                related.Company          = Company
                related.VendorReturn     = PurchaseOrderTransactionDetail.DocumentNumberNumeric
                related.VendorReturnLine = PurchaseOrderTransactionDetail.LineNumber
                
		

		PurchaseOrderTransactionDetailRel 
			one-to-many relation to PurchaseOrderTransactionDetail
			Field Mapping uses BySerial
        	    related.Serial					= Serial
        
        
        PurchaseOrderTransactionDetailLotRel 
			one-to-many relation to PurchaseOrderTransactionDetail
			Field Mapping uses ByLot
        	    related.Lot						= Lot
        
        OtherPurchaseOrderTransactionDetailWithSameSerialRel
        	one-to-many relation to PurchaseOrderTransactionDetail
        	Field Mapping uses BySerialAndItem
        		related.Item					= Item
        		related.Serial					= Serial
        	Instance Selection
        		where (related.UniqueID != UniqueID)
        	    
        StockOnHandDetailRel
			one-to-many relation to StockOnHandDetail
			Field Mapping uses symbolic key
				related.Company												= Company
				related.InventoryLocation 									= PurchaseOrderReceiptLineRel2.ShipToLocation
				related.Item												= PurchaseOrderReceiptLineRel2.Item
			Instance Selection
				where (related.StockOnHandDetail.UnitOfMeasure	 			= PurchaseOrderReceiptLineRel2.ReceivedUOM 
				and    related.StockOnHandDetail.Lot 						= Lot			
				and    related.StockOnHandDetail.Sublot 					= Sublot
				and    related.StockOnHandDetail.Bin 						= Bin)
		
		StockOnHandDetailForReleaseRel
			one-to-one relation to StockOnHandDetail
			Field Mapping uses symbolic key
				related.Company										= Company
				related.InventoryLocation 							= PurchaseOrderReceiptLineRel2.ShipToLocation
				related.Item										= PurchaseOrderReceiptLineRel2.Item
				related.StockOnHandDetail.UnitOfMeasure	 			= PurchaseOrderReceiptLineRel2.ReceivedUOM	
				related.StockOnHandDetail.Lot 						= Lot			
				related.StockOnHandDetail.Sublot 					= Sublot
				related.StockOnHandDetail.Bin 						= Bin		
							        
    	ItemLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = PurchaseOrderReceiptLineRel2.ShipToLocation
                related.Item              = PurchaseOrderReceiptLineRel2.Item
                
       	ItemLocationAdjRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = PurchaseOrderAdjLineRel.PurchaseOrderReceiptLine.ShipToLocation
                related.Item              = PurchaseOrderAdjLineRel.PurchaseOrderReceiptLine.Item
        
        ItemLocationInspRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = PurchaseOrderInspectionLineRel.PurchaseOrderReceiptLine.ShipToLocation
                related.Item              = PurchaseOrderInspectionLineRel.PurchaseOrderReceiptLine.Item
        
        ItemLotRel
            one-to-one relation to ItemLot
            Field Mapping uses symbolic key
                related.Company           							= Company
                related.InventoryLocation				 			= PurchaseOrderReceiptLineRel2.ShipToLocation
                related.Item              							= Item
                related.ItemLot           							= Lot
        	    
		ItemSerialRel	
            one-to-one relation to ItemSerialNumber
            Field Mapping uses symbolic key			
				related.Company         	 						= Company
				related.Item              							= Item
				related.ItemSerialNumber	 						= Serial
                
        PurchaseOrderTransactionDetailsRels  
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection
                and   related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderTransactionDetail.DocumentNumberNumeric
                and   related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber) 
                
		PurchaseOrderTransactionDetailsRel
			one-to-many relation to PurchaseOrderTransactionDetail
			Field Mapping uses BySerialAndItem
				related.Item										= Item
				related.Serial										= Serial
				related.Company										= Company
				related.PurchaseOrderReceipt 						= PurchaseOrderReceipt
				related.PurchaseOrderTransactionDetail.LineNumber	= PurchaseOrderTransactionDetail.LineNumber
			Instance Selection
				where (related.IsReceiptOrAdjustment)
        
        PurchaseOrderTransactionDetailsAdjRels  
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment
                and   related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderTransactionDetail.DocumentNumberNumeric
                and   related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber) 
                
        PurchaseOrderTransactionDetailsLotRels  
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                and   related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderTransactionDetail.DocumentNumberNumeric
                and   related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber
                and   related.Lot = Lot
                and   related.Sublot = Sublot
                and   (related.Bin not entered
                or    related.Bin = Bin))
		
		POTransactionDetailsDifferentLotExpirationRel
			one-to-many relation to PurchaseOrderTransactionDetail
			Field Mapping uses ByLot
				related.Lot                                                                 = Lot
				related.Company                                                             = Company
				related.PurchaseOrderReceipt                                                = PurchaseOrderReceipt
				related.PurchaseOrderTransactionDetail.LineNumber                           = PurchaseOrderTransactionDetail.LineNumber
				related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType      = PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
				related.PurchaseOrderTransactionDetail.DocumentNumberNumeric                = PurchaseOrderTransactionDetail.DocumentNumberNumeric
			Instance Selection
				where (related.PurchaseOrderTransactionDetail.TransactionSequence           not = PurchaseOrderTransactionDetail.TransactionSequence
				and   related.LotExpirationDate                                             not = LotExpirationDate)
                
        PurchaseOrderTransactionDetailsSerialRels  
	        one-to-many relation to PurchaseOrderTransactionDetail
	        Field Mapping uses symbolic key
	            related.Company = Company
	        Instance Selection
	            where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
	            and   related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderTransactionDetail.DocumentNumberNumeric
	            and   related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber
	            and   related.Serial = Serial)  

        PurchaseOrderTransactionDetailsReceiptBinRels  
	        one-to-many relation to PurchaseOrderTransactionDetail
	        Field Mapping uses symbolic key
	            related.Company = Company
	        Instance Selection
	            where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving
	            and    related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderReceipt
	            and    related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber
	            and    related.Bin = Bin)

        ReceivedDetailsRel  
	        one-to-many relation to PurchaseOrderTransactionDetail
	        Field Mapping uses Set2
	        	related.Company															= Company
                related.PurchaseOrderReceipt											= PurchaseOrderReceipt
                related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderTransactionDetail.LineNumber
                related.Lot																= Lot
                related.Sublot															= Sublot
                related.Serial															= Serial
                related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving
                related.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= PurchaseOrderReceipt

        PurchaseOrderTransactionDetailsInspectionBinRels  
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection
                and    related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderTransactionDetail.DocumentNumberNumeric
                and    related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber
                and    related.Bin = Bin) 

        PurchaseOrderTransactionDetailsAdjustmentBinRels  
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment
                and    related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderTransactionDetail.DocumentNumberNumeric
                and    related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber
                and    related.Bin = Bin) 


		POTransactionDetailAllowedBinsForReceiptRel	
			one-to-many relation to Bin
			Field Mapping uses Set4 
				related.Company = Company
				related.InventoryLocation = PurchaseOrderReceiptLineRel2.ShipToLocation
		

		DetailsReceivedRel
			one-to-many relation to PurchaseOrderTransactionDetail
	        Field Mapping uses ByLine
	        	related.Company															= Company
      			related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseTransactionDocumentType.Receiving
      			related.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= PurchaseOrderReceipt
      			related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderTransactionDetail.LineNumber
      		Instance Selection
      			where (related.Bin		= Bin
      			and    related.Lot		= Lot
      			and	   related.Serial	= Serial)
      			
		DetailsAdjustedRel
			one-to-many relation to PurchaseOrderTransactionDetail
	        Field Mapping uses ByReceiptAdjustment
	        	related.Company															= Company
      			related.PurchaseOrderReceipt											= PurchaseOrderReceipt
      			related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderTransactionDetail.LineNumber
      		Instance Selection
      			where (related.Bin 		= Bin
      			and	   related.Lot 		= Lot
      			and	   related.Serial 	= Serial)
      			
      	SimilarInspectionDetailsRel
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses ByLine
            	related.Company															= Company
      			related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseTransactionDocumentType.ReceivingInspectionRejection
      			related.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= PurchaseOrderTransactionDetail.DocumentNumberNumeric
      			related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderTransactionDetail.LineNumber
      		Instance Selection
      			where (related.PurchaseOrderTransactionDetail.TransactionSequence	   != PurchaseOrderTransactionDetail.TransactionSequence
      			and    related.Bin		= Bin
      			and    related.Lot		= Lot
      			and	   related.Serial	= Serial)

        PurchaseOrderUDIDetailRel  
            one-to-many relation to PurchaseOrderUDIDetail
            Field Mapping uses symbolic key
                related.Company 														= Company
      		Instance Selection
                where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType	= PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                and    related.PurchaseOrderUDIDetail.DocumentNumberNumeric 			= PurchaseOrderTransactionDetail.DocumentNumberNumeric
                and	   related.PurchaseOrderUDIDetail.LineNumber 						= PurchaseOrderTransactionDetail.LineNumber 
      			and	   related.Lot														= LocalOldLot
      			and	   related.SerialNumber												= LocalOldSerial)
      			
		ItemStorageLocationRel
			one-to-one relation to ItemStorageLocation
			Field Mapping uses symbolic key
				related.Company						= PurchaseOrderReceiptLineRel2.Company
				related.InventoryLocation			= PurchaseOrderReceiptLineRel2.ShipToLocation
				related.Item						= PurchaseOrderReceiptLineRel2.Item
				related.WarehouseStorageLocation	= Bin.WarehouseStorageLocation

		MSCMPurchaseOrderReceiptTrackingRel
			one-to-many relation to MobileSupplyChainPurchaseOrderReceiptTracking
			Field Mapping uses symbolic key
				related.Company															= Company
				related.PurchaseOrderReceipt											= PurchaseOrderReceipt
				related.PurchaseOrderReceiptLine										= PurchaseOrderTransactionDetail.LineNumber
				related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
				related.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= PurchaseOrderTransactionDetail.DocumentNumberNumeric
				related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderTransactionDetail.LineNumber
				related.PurchaseOrderTransactionDetail.TransactionSequence				= PurchaseOrderTransactionDetail.TransactionSequence
				related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType			= blank
				related.PurchaseOrderUDIDetail.DocumentNumberNumeric					= blank
				related.PurchaseOrderUDIDetail.LineNumber								= blank
				related.PurchaseOrderUDIDetail.TransactionSequence						= blank

		BinHasItemStorageLocationRel
			one-to-many relation to Bin
			Field Mapping uses symbolic key
				related.Company						= ContextPurchaseOrderReceiptLine.Company
				related.InventoryLocation			= ContextPurchaseOrderReceiptLine.ShipToLocation
			Instance Selection
				where (related.HasItemStorageLocation)

    Sets

        Set2
            indexed
            Instance Selection
                where (HasLotOrSerial)
            Sort Order
                Company
                PurchaseOrderReceipt
                PurchaseOrderTransactionDetail.LineNumber
                Lot
                Sublot
                Serial
                PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                PurchaseOrderTransactionDetail.DocumentNumberNumeric
				PurchaseOrderTransactionDetail.TransactionSequence
				
		BySerial
			indexed
			Sort Order
				Serial
				Company
                PurchaseOrderReceipt
                PurchaseOrderTransactionDetail.LineNumber
                PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                PurchaseOrderTransactionDetail.DocumentNumberNumeric
                PurchaseOrderTransactionDetail.TransactionSequence
                Bin
                Lot
                Sublot
                
        BySerialAndItem
			indexed
			Sort Order
				Item
				Serial
				Company
                PurchaseOrderReceipt
                PurchaseOrderTransactionDetail.LineNumber
                PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                PurchaseOrderTransactionDetail.DocumentNumberNumeric
                PurchaseOrderTransactionDetail.TransactionSequence
                Bin
                Lot
                Sublot
                
       	ByLot
			indexed
			Sort Order
				Lot
				Company
                PurchaseOrderReceipt
                PurchaseOrderTransactionDetail.LineNumber
                PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                PurchaseOrderTransactionDetail.DocumentNumberNumeric
                PurchaseOrderTransactionDetail.TransactionSequence
                Bin
                Serial
                Sublot
      
      	ByLine
      		indexed
      		Sort Order
      			Company
      			PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
      			PurchaseOrderTransactionDetail.DocumentNumberNumeric
      			PurchaseOrderTransactionDetail.LineNumber
                PurchaseOrderTransactionDetail.TransactionSequence
                
		ByReceiptAdjustment
			Instance Selection
				where (IsValidForAdjustment)
			Sort Order
				Company
                PurchaseOrderReceipt
                PurchaseOrderTransactionDetail.LineNumber
				PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType
                PurchaseOrderTransactionDetail.DocumentNumberNumeric
                PurchaseOrderTransactionDetail.TransactionSequence
                








                


    Rule Blocks
    	UpdateDetailsInBalance
    		if (PurchaseOrderReceiptLineRel2 exists)









    			invoke Update PurchaseOrderReceiptLineRel2
    				invoked.TransientDisallowTxnDetailCallback = true

    		
    		if (PurchaseOrderInspectionLineRel exist)
    			invoke SetDetailsInBalance PurchaseOrderInspectionLineRel
    		else
    		if (PurchaseOrderAdjLineRel exists)
    			invoke SetDetailsInBalance PurchaseOrderAdjLineRel

		UpdateBinAndMultipleBins
			if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving)
				invoke UpdateBinAndMultipleBins PurchaseOrderReceiptLineRel

		UpdatePurchaseOrderUDIDetail
			invoke Update PurchaseOrderUDIDetailRel
				invoked.Lot								= PurchaseOrderTransactionDetail.Lot
				invoked.Quantity						= PurchaseOrderTransactionDetail.Quantity
				invoked.ExpirationDate					= PurchaseOrderTransactionDetail.LotExpirationDate
				invoked.LocalUpdateFromPOTransactionDtl	= true
		
	Field Rules

		PurchaseOrderReceipt
			initial value is ContextPurchaseOrderReceipt
	
        Quantity   	
			if (not IsCatchWeightItem
			or  not PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment)
				required
					"<DerivedQuantityIsRequiredMessage>"
      				
			include UpdateDetailsInBalance
					
			if (HasSerial)
				if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment 
				and PurchaseOrderAdjLineRel.Quantity < 0)
					force default to -1
				else
					if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection
					or  (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment and not IsCatchWeightItem)
					or  PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving)
						force default to 1

			if (HasLineToInspect)
				constraint (DerivedTotalInspectionDetailQuantity <= DerivedTotalDetailQuantity)
					"TotalTransactionQuantityIsGreaterThanActualReceivedQuantity"
					



			if (IsValidForAdjustment)
				display "IsBinQuantityOutOfBalance\:_<IsBinQuantityOutOfBalance>"
				constraint (not IsBinQuantityOutOfBalance)
					"CannotAdjustReceivedQuantityBelowZero" 
			
		Lot
			if (IsLotRequiredAtReceipt)
				required
					"MustEnterLotForLotTrackedItem"
		
		Serial 
			if (IsSerialRequiredAtReceipt)
				required
					"MustEnterSerialNumberForSerialTrackedItem"
			if (HasLineToInspect
			or IsNegativeAdjustment)
				constraint (Serial exists)
					"SerialNumber<Serial>DoesNotExist"
				constraint (PurchaseOrderTransactionDetailsRel exists)
					"SerialNumber<Serial>IsNotReferencedOnOriginalReceipt"
			else
			if (IsReceiving
			or IsPositiveAdjustment)
				constraint (Serial not exists)
					"SerialNumber<Serial>AlreadyExists"        								//"@e.ICED.35"
				constraint (not IsSerialNumberInUse)
					"SerialNumber<Serial>AlreadyInUseForItem"
					
		ReceivingLocation
			initial value is PurchaseOrderReceiptLineRel.ShipToLocation 
		
		Bin 
			if (IsBinTracked)						    
				if (Bin !entered)
					if (ItemLocationRel.IsAWarehouseLocationItem
					and ItemStorageLocationRel.Bin entered)
						default to ItemStorageLocationRel.Bin
					else
						Bin = PurchaseOrderReceiptLineRel2.Bin
				
				if (HasLineToInspect)	
					constraint (PurchaseOrderTransactionDetailsReceiptBinRels exist)	
						"BinsToInspectMustBeReceivedBins"
				
				if (IsValidForAdjustment)
					constraint (POTransactionDetailAllowedBinsForReceiptRel exist)
						"BinNotFound"
				
				if (Bin.BinType entered)
					for each InventoryCompanyRel.BinTypeArray.BinTypeGroup
						if (each.BinType = Bin.BinType)
							constraint (each.AllowsReceipt)
								"<Bin>ForCompany<Company>BinDoesNotAllowReceipt"
							end for each

				if (ItemLocationRel.IsAWarehouseLocationItem
				and Bin.WarehouseStorageLocation entered)
					constraint (HasItemStorageLocation)
						"BinIsNotValidFor_Item<Item>"

		ShelfLife
			if (not ItemLocationRel.IsBinTrackedAndLotTrackedWarehouseItem)
				cannot be entered
					"Shelf_LifeMustBeBlankWhenItemIsNot_Bin_TrackedAnd_Lot_Tracked"
			else
			if (Lot.LotExpirationDate entered)
				force default to Lot.ShelfLife
			else
			if (ManufacturingDate entered 
			and ItemLocationRel.IsBinTrackedAndLotTrackedWarehouseItem) 
				required

		ManufacturingDate
			if (not ItemLocationRel.IsBinTrackedAndLotTrackedWarehouseItem)
				cannot be entered
					"Manufacturing_DateMustBeBlankWhenItemIsNot_Bin_TrackedAnd_Lot_Tracked"
			else
			if (Lot.LotExpirationDate entered)
				force default to Lot.ManufacturingDate
			else
			if (ShelfLife entered 
			and ItemLocationRel.IsBinTrackedAndLotTrackedWarehouseItem) 
				required

		LotExpirationDate
			if (HasLot)
				if (ItemLocationRel.IsAWarehouseLocationItem
				and	LotExpirationDate entered)
					ExpirationDateSource = ExpirationDateSource.Entered

				if (ShelfLife entered)
					force default to DerivedWarehouseLotExpirationDate
					ExpirationDateSource = ExpirationDateSource.CalculatedFromShelfLife

				if (LotExpirationDate entered)
					constraint (LotExpirationDate >= current corporate date)
						"Lot_<Lot>HasExpired"
			
					constraint (!IsLotExists) 
						"LotExists,CannotChangeExpirationDate"		


		SecondaryQuantity

			if (IsCatchWeightItem)
				if (not PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment)
					required
						"QuantityIsRequiredForCatchWeightItem"
					
				if (HasSerial)
					if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment 
					and PurchaseOrderAdjLineRel.Quantity < 0)
						force default to -1
					else
						if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection

						or  PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving)
							force default to 1
			else
				cannot be entered
	   	
		Item
			initial value is ContextPurchaseOrderReceiptLine.Item
			
	Actions
		Create is a Create Action
			valid when (IsUnreleased)								
			
			Entrance Rules



            	if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving)
	   				PurchaseOrderReceipt = PurchaseOrderTransactionDetail.DocumentNumberNumeric

				if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving and PurchaseOrderReceiptLineRel2 exists)
					increment PurchaseOrderReceiptLineRel2.LastPurchaseOrderTransactionDetail by 1
					PurchaseOrderTransactionDetail.TransactionSequence = PurchaseOrderReceiptLineRel2.LastPurchaseOrderTransactionDetail
					PurchaseOrderTransactionDetail.Item = PurchaseOrderReceiptLineRel2.Item
					PurchaseOrderReceipt = PurchaseOrderReceiptLineRel2.PurchaseOrderReceipt	
					ReceivingLocation = PurchaseOrderReceiptLineRel2.ShipToLocation
				else
				if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingAdjustment and PurchaseOrderAdjLineRel exists)
					increment PurchaseOrderAdjLineRel.LastPurchaseOrderTransactionDetail by 1
					PurchaseOrderTransactionDetail.TransactionSequence = PurchaseOrderAdjLineRel.LastPurchaseOrderTransactionDetail
					PurchaseOrderTransactionDetail.Item = PurchaseOrderAdjLineRel.PurchaseOrderReceiptLine.Item
					PurchaseOrderReceipt = PurchaseOrderAdjLineRel.PurchaseOrderReceipt	
					ReceivingLocation = PurchaseOrderAdjLineRel.PurchaseOrderReceiptLine.ShipToLocation  
				else
				if (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection and PurchaseOrderInspectionLineRel exists)
					increment PurchaseOrderInspectionLineRel.LastPurchaseOrderTransactionDetail by 1
					PurchaseOrderTransactionDetail.TransactionSequence = PurchaseOrderInspectionLineRel.LastPurchaseOrderTransactionDetail
					PurchaseOrderTransactionDetail.Item = PurchaseOrderInspectionLineRel.PurchaseOrderReceiptLine.Item
					PurchaseOrderReceipt = PurchaseOrderInspectionLineRel.PurchaseOrderReceipt	
					ReceivingLocation = PurchaseOrderInspectionLineRel.PurchaseOrderReceiptLine.ShipToLocation  
				
										
			Action Rules 
				
			



				



				




						



				



						



				if(HasLot)
					constraint (PurchaseOrderTransactionDetailsLotRels not exists) 
						"LotNumberIsAlreadyOnThisReceiver"
				
				if(HasSerial)
           			constraint (PurchaseOrderTransactionDetailsSerialRels not exists) 
						"SerialNumberIsAlreadyOnThisReceiver"
						



		
				if (HasLot and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0))
					constraint (PurchaseOrderTransactionDetailLotRel	exists)
						"LotNumberDoesNotExistAtReceiptLevelForThisItem"
				
				
						
				if (HasLot and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0))
					for each PurchaseOrderTransactionDetailLotRel
						if (each.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "PO"
						and each.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderTransactionDetail.LineNumber)
							LocalLotExistedAtReceipt = true
							end for each

					constraint(LocalLotExistedAtReceipt)
						"LotNumberDoesNotExistAtReceiptLevelForThisItem"
						
				if (HasLot and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0))
				    constraint (Quantity <= ReceivedDetailsRel.Quantity)
				    	"LotQuantityShouldBeLessThanOrEqualToActualLotQuantityInPoReceipt"
				
				
				if((HasBin or HasLotOrSerial) and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0))

				  	if (Quantity > 0)
				  		Quantity =   Quantity-(2*Quantity)

				   	
			Exit Rules
			
				
						



				



				include UpdateBinAndMultipleBins
				include UpdateDetailsInBalance		
			
		Update is an Action
			valid when (IsUnreleased)
			Entrance Rules
				ReceivingLocation = PurchaseOrderReceiptLineRel2.ShipToLocation
				if(PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN")
					ReceivingLocation = PurchaseOrderInspectionLineRel.PurchaseOrderReceiptLine.ShipToLocation
				else
				if(PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA")
					ReceivingLocation = PurchaseOrderAdjLineRel.PurchaseOrderReceiptLine.ShipToLocation
														
			Action Rules 
				if (HasSerial and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "PO"))
				    constraint (Quantity = 1)
				    	"SerialQuantityShouldBeEqualToOneForThisItem"
				
				if (HasLot and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0))
					constraint (PurchaseOrderTransactionDetailLotRel	exists)
						"LotNumberNotExistedAtReceiptLevelForThisItem"
						
				if (HasLot and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "IN" or PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0))
				    constraint (Quantity <= ReceivedDetailsRel.Quantity)
				    	"LotQuantityShouldBeLessThanOrEqualToActualLotQuantityInPoReceipt"
				
				if((HasBin or HasLotOrSerial) and (PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = "RA" and PurchaseOrderAdjLineRel.Quantity<0 and Quantity>0))

				  	Quantity =   Quantity-(2*Quantity)





				



						



						



				



						




			Field Rules

				Lot
				    if (Lot changed)
				        LocalOldLot		= old Lot
				        						
				Serial
				    if (Serial changed)
				        LocalOldSerial	= old Serial

				LotExpirationDate
					if  (LotExpirationDate changed
					and  LocalOldLot	= blank
					and  LocalOldSerial	= blank)
					    LocalOldLot		= Lot
					    LocalOldSerial	= Serial

				Quantity
					if  (Quantity changed
					and  LocalOldLot	= blank
					and  LocalOldSerial	= blank)
						LocalOldLot		= Lot
						LocalOldSerial	= Serial
						
			Exit Rules
				if (not LocalDoNotUpdateBins)
					include UpdateBinAndMultipleBins

				include UpdateDetailsInBalance

				if (IsLotRequiredAtReceipt
				or  IsSerialRequiredAtReceipt)
					if (PurchaseOrderUDIDetailRel exists
					and !LocalUpdateFromPOUDIDtl)
						include UpdatePurchaseOrderUDIDetail		

		FastUpdate is an Update Action
			restricted
			bypass field rules
			Exit Rules
				include UpdateDetailsInBalance
					
		Delete is an Action
			valid when (IsUnreleased)
			Action Rules
				include UpdateDetailsInBalance
				include UpdateBinAndMultipleBins
		
		DeleteFromHeader is a Delete Action
			restricted
	
		Purge is a Purge Action
			restricted
		
		UpdateSerialStatus is an Instance Action
			restricted
			Action Rules
				invoke UpdateStatusBinLocation Serial
					invoked.PrmStatus	= ItemSerialNumber.Status.OnHand
					
