POReceiptAdjustmentAndInspection is a BusinessClass
    owned by po
    prefix is PJH
    sql name is PORAdjustmentAndInspection
    classic name is PORECADJHD

    Ontology
        symbolic key is POReceiptAdjustmentAndInspection
            classic set name is PJHSET0
            sql name is PORAdjustmentAndInspection
            classic name is DOC-NBR-NUM
            sql name for AdjustmentInspectionDocumentType is AInspectionDocumentType
            classic name for AdjustmentInspectionDocumentType is DOC-TYPE

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        ReferenceNumber
            classic name is REFERENCE-NO
        PurchaseOrderReceipt
            classic name is REC-NUMBER
        OperatorID           is an Operator 
        	holds pii
            classic name is OPER-ID
        DocumentDate         is TimeStamp
            classic name is DATE
        UpdateDate           is TimeStamp
        VendorClaimType
            classic name is VEN-CLAIM-TYPE
        CreatedBy                     is an Operator 
        	holds pii

        LastUpdateBy                  is an Operator 
        	holds pii
        
        ReplaceGoods         is Boolean
        ShipOrHold
        Status               is Numeric size 1
            States
                Unreleased value is 0
                Released   value is 1
        Logged               is Boolean
            classic name is LOGGED-FLAG
        GlobalDocumentType
            classic name is GLBL-DOC-TYPE
		InterfaceInProcess				is Boolean
		OriginatingInterfaceRun			is a PurchaseOrderReceiptInterfaceResult
			delete ignored     
		
	Local Fields
		LocalCurrentLocation			is like InventoryLocation
		LocalInventoryTransaction		is like InventoryTransaction
            
	Transient Fields

		LocalInventoryLocation 									is an InventoryLocation
    	NewVendorReturns   										is an VendorReturn view
    	NewInventoryTransactionForAdjustmentItem    	        is an InventoryTransaction view
		LocalInventoryTransactionForAdjustmentItem		        is an InventoryTransaction 
    	NewInventoryTransactionForAcceptedInspectionItem    	is an InventoryTransaction view
		LocalInventoryTransactionForAcceptedInspectionItem		is an InventoryTransaction
		NewInventoryTransactionForRejectedInspectionItem    	is an InventoryTransaction view
		LocalInventoryTransactionForRejectedInspectionItem		is an InventoryTransaction
		BypassCreateLines										is Boolean	
		TimeStampInGMT
		
    Derived Fields
		ContextMessageAdjustmentEntityType is a StringField
			type is Alpha 30
			restricted
			"InforReceiptAdjustment"

		ContextMessageInspectionEntityType is a StringField
			type is Alpha 30
			restricted
			"InforInspectDelivery"

		ContextMessageAdjustmentText is a MessageField
			restricted
			"Adjustment<POReceiptAdjustmentAndInspection>"

		ContextMessageInspectionText is a MessageField
			restricted
			"Inspection<POReceiptAdjustmentAndInspection>"

		POReceiptAdjustmentAndInspectionDisplay is a DerivedField
			type is MessageField
			if (AdjustmentInspectionDocumentType.ReceiptAdjustment)
				return ContextMessageAdjustmentText
			else 
			if (AdjustmentInspectionDocumentType.ReceiptInspection)
				return ContextMessageInspectionText

		BuyerMessages is a MessageField
			"UnreleasedBuyerMessages"	
			
		DerivedDocumentDate is a DerivedField
			type is Date
			restricted
			TimeStampInGMT.Value = DocumentDate
			return TimeStampInGMT.CorporateDate
		
		CommentCount is a DerivedField
            type is Alpha size up to 20
            if (POReceiptAdjustmentAndInspectionCommentRel exists)
                return "(" + instance count of POReceiptAdjustmentAndInspectionCommentRel + ")"
            else
                return ""
		
		TotalExtendedAmt is a DerivedField                               
			type is like InternationalAmount
			restricted
			return (sum POReceiptAdjLineRels.ExtendedAmount)
		
		TotalVendorPriceUOMQuantity is a DerivedField                               
			type is like Quantity
			restricted
			return (sum POReceiptAdjLineRels.VendorPriceUOMQuantity)

		TotalMatchedQuantity is a DerivedField                               
			type is like Quantity
			restricted
			return (sum POReceiptAdjLineRels.MatchedQuantity)
			
		TotalOpenToMatchQuantity is a DerivedField                               
			type is like Quantity
			restricted
			return (sum POReceiptAdjLineRels.OpenToMatchQuantity)			
			
		TotalNewMatchedAmt is a DerivedField                               
			type is like InternationalAmount
			restricted
			return ((sum POReceiptAdjLineRels.OpenToMatchQuantity)*POReceiptAdjLineRels.MatchUnitCost)			
			
		ReleaseCompleteMessage is a MessageField
			restricted
			"ReleaseCompleted"
		
		ReleasedWithVendorReturnMessage is a MessageField
			restricted
			"ReleaseComplete,VendorReturn<NewVendorReturns.VendorReturn>Created"	 

		DisplayReferenceNumber is a DerivedField
			type is Alpha 25
			default label is "ReferenceNumber"
			return com.lawson.apps.procurement.base.StringUtils.trimLeadingSpaces(ReferenceNumber)		 			 			
			           	

		MobilePurchaseOrderDisplay is a LabelField
			"PurchaseOrder:<PurchaseOrderReceipt.PurchaseOrder>"
		
		MobileCompanyDisplay is a LabelField
			"<Company>_-_<Company.Name>"
		
		MobileReferenceNumber is a DerivedField
			type is Alpha 25
			default label is "ReferenceNumber"
			return com.lawson.apps.procurement.base.StringUtils.trimLeadingSpaces(ReferenceNumber)
		


    Conditions

        IsNotLogged
        	restricted
            when (Status.Released
            and   not Logged
            and   AdjustmentInspectionDocumentType.ReceiptAdjustment)

        IsAdjustmentDocument
        	restricted
            classic name is REC-ADJUSTMENT
            when (AdjustmentInspectionDocumentType.ReceiptAdjustment)

        IsInspectionDocument
        	restricted
            classic name is REC-INSPECTION
            when (AdjustmentInspectionDocumentType.ReceiptInspection)

        DocumentExists
        	restricted
        	when (POReceiptAdjustmentAndInspection entered)
        
        HasReferenceNumber
        	restricted
            classic name is REFERENCE-NO
            when (ReferenceNumber entered)

		HasGlobalDocumentType
			restricted
			when (GlobalDocumentType entered)

        IsReleased
        	restricted
            when (Status.Released)

        IsReleasedAdjustment
        	restricted
            classic name is RELEASED-ADJ
            when (AdjustmentInspectionDocumentType.ReceiptAdjustment
            and   Status.Released)

        IsUnreleased
        	restricted
            when (Status.Unreleased)
            
        HasRejectedItemAndWriteOffYes
        	restricted
        	when (POReceiptInspectionLineRelsForItemRejected exists)
        
        HasAcceptedItem
        	restricted
        	when (POReciptInspectionLineRelsForAllItemAccepted exists)
        
        IsAllItemRejected
        	restricted
        	when (POReciptInspectionLineRelsForAllItemRejected exists)    
        	
    	IsInventoryTransactionUpdateToNonGeneralLedger
    		restricted
    		when (POInspectionRelsForInventoryTransactionUpdateToNonGeneralLedger exists)
    		
    	HasCatchWeightLines
    		restricted
    		when (POReceiptLinesCatchWeightRel exists)
        
		InterfaceReleaseError
			when (OriginatingInterfaceRun entered
			and   OriginatingInterfaceRun.ReleaseReceipts
			and   Status.Unreleased)

    	HasUnreleasedBuyerMessages
    		restricted
    		when (PurchaseOrderUnreleasedBuyerMessagesRel exists)
    		
    	UnreleasedAndHasCatchWeightLines
    		when (Status.Unreleased
    		and   HasCatchWeightLines)
    		
    	UnreleasedAndNoCatchWeightLines
    		when (Status.Unreleased
    		and   not HasCatchWeightLines)
    		
    	ReleasedAndHasCatchWeightLines
    		when (Status.Released
    		and   HasCatchWeightLines)
    		
    	ReleasedAndNoCatchWeightLines
    		when (Status.Released
    		and   not HasCatchWeightLines)
		
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)		
    Sets

        Set2
            indexed
            Instance Selection
                where (HasReferenceNumber)
            Sort Order
                Company
                AdjustmentInspectionDocumentType
                ReferenceNumber
                POReceiptAdjustmentAndInspection

        Set3
            indexed
            Instance Selection
                where (IsUnreleased)
            Sort Order
                Company
                PurchaseOrderReceipt
                AdjustmentInspectionDocumentType
                POReceiptAdjustmentAndInspection

        Set4							
            indexed
            Instance Selection
                where (IsReleasedAdjustment)
            Sort Order
                Company
                DocumentDate
                POReceiptAdjustmentAndInspection

        Set1
            not indexed
            Sort Order
                Company
                AdjustmentInspectionDocumentType
                POReceiptAdjustmentAndInspection

        Set5
        	not indexed
            Instance Selection
                where (IsUnreleased)
            Sort Order
                Company
                AdjustmentInspectionDocumentType
                PurchaseOrderReceipt
        Set6
            not indexed
            Instance Selection
                where (HasReferenceNumber)
            Sort Order
                Company
                ReferenceNumber
                AdjustmentInspectionDocumentType
                POReceiptAdjustmentAndInspection                        
    
        ByInterfaceRun
            Sort Order
            	OriginatingInterfaceRun
                Company
                AdjustmentInspectionDocumentType
                POReceiptAdjustmentAndInspection

		ByPurchaseOrder
			Sort Order
				Company
				PurchaseOrderReceipt.PurchaseOrder
				AdjustmentInspectionDocumentType
                POReceiptAdjustmentAndInspection

	Field Rules
		
		DocumentDate															     

			default to current timestamp													 
			constraint (DerivedDocumentDate >= PurchaseOrderReceipt.DerivedReceivedDate)
				"AdjustmentDateOrTimeCannotBePriorToReceivingDateOrTime"					 
			constraint (DerivedDocumentDate <= current corporate date)
				"DocumentDateCannotBeGreaterThanCurrentDate"						
				
		PurchaseOrderReceipt
			required
				"Field_ReceiptIsRequired"
			constraint(PurchaseOrderReceipt.PurchaseOrder.Released)
				"PurchaseOrderIsNotReleased"
			
			cannot be changed
				
		UpdateDate													
			force default to current timestamp 

		
		CreatedBy
			default to actor
			 
		ReferenceNumber												
			constraint (DuplicateReferenceRel not exists)
				"DuplicateAdjustmentReferenceNumber"	 
        
	Relations
		NonInventoriedLinesRel is a POReceiptAdjustmentAndInspectionLine set
			Instance Selection
				where (not related.IsInventoried
				and	   related.Status.Unreleased)
		
		LinesWithSameLocationRel is a POReceiptAdjustmentAndInspectionLine set
			Instance Selection
				where (related.IsInventoried
				and    related.ReceivingLocation = LocalCurrentLocation
				and    related.Status.Unreleased)
				
		InventoryTransactionForDistinctHeaderAndLocationRel
			one-to-many relation to InventoryTransaction
			Field Mapping uses symbolic key
				related.Company						= Company
				related.InventoryLocation			= LocalCurrentLocation
			Instance Selection
				where (related.InventoryDocumentType									= InventoryDocumentType.PurchaseOrderReceiptAdjustment
				and    related.POReceiptAdjustmentRel.POReceiptAdjustmentAndInspection	= POReceiptAdjustmentAndInspection)
				
				
		InventoryTransactionForReleaseRel
			one-to-one relation to InventoryTransaction
			Field Mapping uses symbolic key
				related.Company						= Company
				related.InventoryLocation			= LocalCurrentLocation
				related.InventoryTransaction		= LocalInventoryTransaction
	           
		InventoriedAjustmentOrInspectionLinesRel is a POReceiptAdjustmentAndInspectionLine set
			Instance Selection
				where (related.IsInventoried)
	
		AdjustmentAndInspectionLinesWithBuyerMessageRel is a POReceiptAdjustmentAndInspectionLine set
			Instance Selection
				where (related.HasBuyerMessage)
	
		InspectionLinesForVendorReturnRel
			one-to-many relation to POReceiptAdjustmentAndInspectionLine
			delete cascades
			Field Mapping uses symbolic key
				related.Company								= Company
				related.AdjustmentInspectionDocumentType	= AdjustmentInspectionDocumentType
				related.POReceiptAdjustmentAndInspection	= POReceiptAdjustmentAndInspection
			Instance Selection
				where (related.WriteOff.CreateReturn
				and    related.Quantity > 0)
	
        POReceiptAdjustmentAndInspectionCommentRel
        	one-to-many relation to POReceiptAdjustmentAndInspectionComment
        	Field Mapping uses part of key
        		related.Company     = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection

		POReceiptLineRequireInspectionRels
			one-to-many relation to PurchaseOrderReceiptLine
		    Field Mapping uses symbolic key
		    	related.Company                           = Company
		        related.PurchaseOrderReceipt    	      = PurchaseOrderReceipt
		    Instance Selection
           		where (related.InspectionRequired
           		and    related.Status.ReleasedRequiresInspection)

		POReceiptLineRels
			one-to-many relation to PurchaseOrderReceiptLine
		    Field Mapping uses symbolic key
		    	related.Company                           = Company
		        related.PurchaseOrderReceipt    	      = PurchaseOrderReceipt
		    Instance Selection
           		where (related.InspectionRequired)

		POReceiptLinesForInspectionRel
			one-to-many relation to PurchaseOrderReceiptLine
			Field Mapping uses symbolic key
				related.Company							= Company
				related.PurchaseOrderReceipt			= PurchaseOrderReceipt
			Instance Selection
				where (related.InspectionRequired
				and    not related.Status.ReleasedPendingTwoStepReceiving)

		POReceiptAdjLineRels
			one-to-many relation to PurchaseOrderReceiptLine

		    Field Mapping uses symbolic key
		    	related.Company                           = Company
		        related.PurchaseOrderReceipt    	      = PurchaseOrderReceipt

		POReceiptLinesCatchWeightRel
			one-to-many relation to PurchaseOrderReceiptLine
		    Field Mapping uses symbolic key
		    	related.Company                           = Company
		        related.PurchaseOrderReceipt    	      = PurchaseOrderReceipt
		    Instance Selection
           		where (related.CatchWeightQuantity > 0)
			
		PurchaseOrderLineRels
			one-to-many relation to PurchaseOrderLine

		    Field Mapping uses symbolic key
		    	related.Company                           = Company
		        related.PurchaseOrder    	      = PurchaseOrderReceipt.PurchaseOrder		        
		        
        PORecInspectionLineRels
            one-to-many relation to POReceiptAdjustmentAndInspectionLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company              			 = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType 
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection
                
        PORecAdjustmentLineRels
            one-to-many relation to POReceiptAdjustmentAndInspectionLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company              			 = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType 
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection 
                

        IcCompanyRel
            one-to-one relation to InventoryCompany
            required
            Field Mapping uses symbolic key
                related.Company = Company

        PORecInspectionRels
            one-to-many relation to POReceiptAdjustmentAndInspection
            Field Mapping uses Set5
                related.Company              = Company
                related.AdjustmentInspectionDocumentType = "IN"
                related.PurchaseOrderReceipt = POReceiptAdjustmentAndInspection.PurchaseOrderReceipt

        PORecAdjustmentRels
            one-to-many relation to POReceiptAdjustmentAndInspection
            Field Mapping uses Set5
                related.Company              = Company
                related.AdjustmentInspectionDocumentType = "RA"
                related.PurchaseOrderReceipt = POReceiptAdjustmentAndInspection.PurchaseOrderReceipt

        MatchCompanyRel
            one-to-one relation to MatchCompany
            Field Mapping uses symbolic key
                related.Company = Company                               
                                   
		VendorLocationRel
       		one-to-one relation to VendorLocation
       		Field Mapping uses symbolic key
       			related.VendorGroup = PurchaseOrderReceipt.Vendor.VendorGroup
       			related.Vendor = PurchaseOrderReceipt.Vendor
       			related.VendorLocation = PurchaseOrderReceipt.ShipToLocation
       			      
        DuplicateReferenceRel
			one-to-many relation to POReceiptAdjustmentAndInspection
			Field Mapping uses Set6
                related.Company					= Company
                related.ReferenceNumber			= ReferenceNumber
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType
            Instance Selection
                where (related.POReceiptAdjustmentAndInspection	!= POReceiptAdjustmentAndInspection)

        PotransdtlRels
            one-to-many relation to PurchaseOrderTransactionDetail
            delete cascades
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType = AdjustmentInspectionDocumentType
                and   related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = POReceiptAdjustmentAndInspection)

        RecaocdtlRel
            one-to-many relation to PurchaseOrderReceiptLineAOC
            Field Mapping uses symbolic key
                related.Company                  = Company
                related.PurchaseOrderReceipt     = PurchaseOrderReceipt
                related.PurchaseOrderReceiptLine = blank 
                

             					   
        POReceiptInspectionLineRelsForItemRejected
            one-to-many relation to POReceiptAdjustmentAndInspectionLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company              			 = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType 
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection
            Instance Selection
            	where (related.HasItemRejectedAndWriteOffYes
            	and    related.IsInventoried)
            	
    	POReciptInspectionLineRelsForAllItemAccepted
            one-to-many relation to POReceiptAdjustmentAndInspectionLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company              			 = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType 
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection
            Instance Selection
            	where (related.HasAllItemAccepted
            	and    related.IsInventoried)
            	
        	
    	POReciptInspectionLineRelsForAllItemRejected
            one-to-many relation to POReceiptAdjustmentAndInspectionLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company              			 = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType 
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection
            Instance Selection
            	where (related.HasAllItemRejected)
            	
            	
    	POInspectionRelsForInventoryTransactionUpdateToNonGeneralLedger
            one-to-many relation to POReceiptAdjustmentAndInspectionLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company              			 = Company
                related.AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType 
                related.POReceiptAdjustmentAndInspection = POReceiptAdjustmentAndInspection
            Instance Selection
            	where (related.HasInventoryTransactionUpdateToNonGeneralLedger)

		PurchaseOrderUnreleasedBuyerMessagesRel
			one-to-many relation to PurchaseOrderBuyerMessage
			Field Mapping uses AllByReceiver
				related.Company         		= Company
				related.PurchaseOrderReceipt	= PurchaseOrderReceipt
			Instance Selection
				where (related.Status.Unreleased)
				
		PurgePOReceiptAdjustmentAndInspectionLineRel
			one-to-many relation to POReceiptAdjustmentAndInspectionLine
			Field Mapping uses symbolic key
				related.Company								= Company
                related.AdjustmentInspectionDocumentType	= AdjustmentInspectionDocumentType
				related.POReceiptAdjustmentAndInspection	= POReceiptAdjustmentAndInspection
			Instance Selection
				include deleted records
		
		GeneralLedgerCompanyRel
            one-to-one relation to GeneralLedgerCompany
            Field Mapping uses symbolic key
                related.Company = Company
	Rule Blocks
					
		CreateInventoryTransactionForAdjustmentItem
			invoke Unreleased.Create InventoryTransaction
				assign result to NewInventoryTransactionForAdjustmentItem
				fill in fields from this instance
					except invoked.OriginatingInterfaceRun
				invoked.Company							= Company
				invoked.InventoryLocation			 	= first POReceiptAdjLineRels.ShipToLocation
				invoked.InventoryDocumentType 			= InventoryDocumentType.PurchaseOrderReceiptAdjustment
				invoked.OriginatingTransaction			= reference to this instance
				invoked.GeneralLedgerDate				= DocumentDate
			
			LocalInventoryTransactionForAdjustmentItem = NewInventoryTransactionForAdjustmentItem.InventoryTransaction
								    	
								    	
    	CreateInventoryTransactionForAcceptedInspectionItem
    	
    		invoke Unreleased.Create InventoryTransaction
				assign result to NewInventoryTransactionForAcceptedInspectionItem
				fill in fields from this instance
					except invoked.OriginatingInterfaceRun
				invoked.Company							= Company
				invoked.InventoryLocation			 	= first POReceiptAdjLineRels.ShipToLocation
				invoked.InventoryDocumentType 			= InventoryDocumentType.AcceptedInspectionItem 
				invoked.OriginatingTransaction			= reference to this instance
				invoked.GeneralLedgerDate				= DocumentDate
				
			LocalInventoryTransactionForAcceptedInspectionItem = NewInventoryTransactionForAcceptedInspectionItem.InventoryTransaction
    	
    	CreateInventoryTransactionForRejectedInspectionItem

    		invoke Unreleased.Create InventoryTransaction
				assign result to NewInventoryTransactionForRejectedInspectionItem
				fill in fields from this instance
					except invoked.OriginatingInterfaceRun
				invoked.Company							= Company
				invoked.InventoryLocation			 	= first POReceiptAdjLineRels.ShipToLocation
				invoked.InventoryDocumentType 			= InventoryDocumentType.RejectedInspectionItem 
				invoked.OriginatingTransaction			= reference to this instance
				invoked.GeneralLedgerDate				= DocumentDate
				
			LocalInventoryTransactionForRejectedInspectionItem = NewInventoryTransactionForRejectedInspectionItem.InventoryTransaction
			
	Create Rules
		if (PurchaseOrderReceipt entered)
			constraint(PORecInspectionRels not exists)							
				"Receipt<PurchaseOrderReceipt>AlreadyHasUnreleasedInspection"
			constraint(PORecAdjustmentRels not exists)
				"Receipt<PurchaseOrderReceipt>AlreadyHasUnreleasedAdjustment"


	StateCycles
		POReceiptInspectionLifeCycle is a StateCycle
			state field is Status
			Unreleased is a State
	
				CreateInspection is a Create Action
					default label is "Create"
					Field Rules
						PurchaseOrderReceipt
							constraint(POReceiptLineRequireInspectionRels exists)
								"NoInspectionLineExistsFor_Receipt<PurchaseOrderReceipt>"
						
						VendorClaimType
							default to VendorLocationRel.VendorClaimType
							default to PurchaseOrderReceipt.Vendor.VendorClaimType
							default to MatchCompanyRel.VendorClaimType
							
						ShipOrHold	
							default to VendorLocationRel.ShipOrHold
							default to PurchaseOrderReceipt.Vendor.ShipOrHold
							default to MatchCompanyRel.ShipOrHold
							
						ReplaceGoods
							if(VendorLocationRel.ReplaceGoods != "")								
								ReplaceGoods  = VendorLocationRel.ReplaceGoods
							else	
								if(PurchaseOrderReceipt.Vendor.ReplaceGoods != "")
									ReplaceGoods = PurchaseOrderReceipt.Vendor.ReplaceGoods
								else
									if(MatchCompanyRel.ReplaceGoods != "")
										ReplaceGoods = MatchCompanyRel.ReplaceGoods
						
					Action Rules
						AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType.ReceiptInspection					
					Exit Rules
						
				
				CreateAdjustment is a Create Action
					default label is "Create"
					Field Rules
						PurchaseOrderReceipt
							constraint(not PurchaseOrderReceipt.Status.ArchivedClosed)
								"Receipt<PurchaseOrderReceipt>Archived.CannotCreateAnotherAdjustmentRecord"	
							constraint(PurchaseOrderReceipt.Status.Released)
								"Receipt<PurchaseOrderReceipt>IsNotReleased"
							if (PurchaseOrderReceipt.PurchaseOrder.SubcontractorPurchaseOrder)
								constraint (PurchaseOrderReceipt.PurchaseOrder.Approved)
									"CannotAddOrModifyAnAdjustmentAsThePONeedsApproval"			  
					Action Rules
						AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType.ReceiptAdjustment
						constraint(not IsReleasedAdjustment)							
							"ReleasedAdjustmentsDocumentExist"
					Exit Rules

									
				AddAcceptAll is a Create Action
					default label is "CreateAcceptAll"
					Field Rules
						PurchaseOrderReceipt
							constraint(POReceiptLineRequireInspectionRels exists)
								"NoInspectionLineExistsFor_Receipt<PurchaseOrderReceipt>"
								
						VendorClaimType
							default to VendorLocationRel.VendorClaimType
							default to PurchaseOrderReceipt.Vendor.VendorClaimType
							default to MatchCompanyRel.VendorClaimType
							
						ShipOrHold	
							default to VendorLocationRel.ShipOrHold
							default to PurchaseOrderReceipt.Vendor.ShipOrHold
							default to MatchCompanyRel.ShipOrHold
							
						ReplaceGoods
							if (VendorLocationRel.ReplaceGoods != "")								
								ReplaceGoods  = VendorLocationRel.ReplaceGoods
							else	
								if (PurchaseOrderReceipt.Vendor.ReplaceGoods != "")
									ReplaceGoods = PurchaseOrderReceipt.Vendor.ReplaceGoods
								else
									if (MatchCompanyRel.ReplaceGoods != "")
										ReplaceGoods = MatchCompanyRel.ReplaceGoods
								
					Action Rules
						AdjustmentInspectionDocumentType = AdjustmentInspectionDocumentType.ReceiptInspection
				    			
					Exit Rules
						
						for each POReceiptLineRequireInspectionRels
							invoke CreateInspection Unreleased POReceiptAdjustmentAndInspectionLine

								invoked.Company										= Company
								invoked.PurchaseOrderReceipt 						= PurchaseOrderReceipt
								invoked.PurchaseOrderReceiptLine					= each.PurchaseOrderReceiptLine
								invoked.PurchaseOrder								= each.PurchaseOrder
								invoked.PurchaseOrderLine							= each.PurchaseOrderReceiptLine
								invoked.POReceiptAdjustmentAndInspectionLine		= each.PurchaseOrderReceiptLine
								invoked.AdjustmentInspectionDocumentType 			= AdjustmentInspectionDocumentType
								invoked.POReceiptAdjustmentAndInspection 			= POReceiptAdjustmentAndInspection
								invoked.Inspected 									= true

						invoke ReleaseInspection
						make transition to Released

				Update is an Update Action
					Field Rules
						
					Action Rules
		    			 		
					Exit Rules
                            
				FastUpdate is an Update Action
					restricted
					bypass field rules                    	    

				Delete is a Delete Action
					Entrance Rules
						if (IsInspectionDocument)
							invoke DeleteInspection PORecInspectionLineRels
						else
						if (IsAdjustmentDocument)
							invoke DeleteAdjustmentFromHeader PORecAdjustmentLineRels
				
				ReleaseInspection is an Instance Action
					valid when (IsInspectionDocument)
					completion message is "<LocalCompletionMessage>"
					default label is "Release"
					Local Fields
						LocalCompletionMessage		is Text
					
					Action Rules
						constraint (POReceiptAdjustmentAndInspection.PORecInspectionLineRels exists)
							"CannotRelease,NoLinesExist"
							
						LocalCompletionMessage	= ReleaseCompleteMessage //"ReleaseComplete-Continue"
						
						invoke UpdateFromMatch PurchaseOrderReceipt.PurchaseOrder 
							invoked.PrmLastActivityDate			= current corporate date
						

						
						if (Company.PackageTrackingLevel.HeaderLevel or Company.PackageTrackingLevel.EnableMscmRadTracking)
							invoke CreateArrival PurchaseOrderReceipt.PurchaseOrderReceiptTrackingRel
						
						if (InventoriedAjustmentOrInspectionLinesRel exists)
							if (HasAcceptedItem)
								include CreateInventoryTransactionForAcceptedInspectionItem
							if (HasRejectedItemAndWriteOffYes)
								include CreateInventoryTransactionForRejectedInspectionItem
						
						invoke ReleaseInspection Unreleased PORecInspectionLineRels
							invoked.PrmInventoryTransactionForAcceptedInspectionItem  = NewInventoryTransactionForAcceptedInspectionItem.InventoryTransaction
							invoked.PrmInventoryTransactionForRejectedInspectionItem  = NewInventoryTransactionForRejectedInspectionItem.InventoryTransaction
						
						invoke TransitionToReleased Unreleased NewInventoryTransactionForAcceptedInspectionItem.InventoryTransaction
						invoke TransitionToReleased Unreleased NewInventoryTransactionForRejectedInspectionItem.InventoryTransaction
						
						if (IsInventoryTransactionUpdateToNonGeneralLedger)
							invoke Released.UpdateToNonGeneralLedgerFromPOInspection NewInventoryTransactionForAcceptedInspectionItem.InventoryTransaction
							invoke Released.UpdateToNonGeneralLedgerFromPOInspection NewInventoryTransactionForRejectedInspectionItem.InventoryTransaction
						
						if (InspectionLinesForVendorReturnRel exists)		
							if (PurchaseOrderReceipt.PurchaseOrder entered)									
								invoke CreateFromPurchaseOrder VendorReturn
									assign result to NewVendorReturns
									invoked.Company 							= Company
									invoked.Vendor 								= PurchaseOrderReceipt.Vendor
									invoked.CreatedFromPurchaseOrder			= PurchaseOrderReceipt.PurchaseOrder
									invoked.VendorClaimType 					= VendorClaimType
									if (ReplaceGoods)
										invoked.TransientReplaceGoods			= VendorReturn.TransientReplaceGoods.Yes
									else
										invoked.TransientReplaceGoods			= VendorReturn.TransientReplaceGoods.No
									invoked.ShipOrHold							= ShipOrHold
							else									
								invoke Create VendorReturn
									assign result to NewVendorReturns
									invoked.Company 							= Company

									invoked.Vendor 								= PurchaseOrderReceipt.Vendor
									invoked.VendorClaimType 					= VendorClaimType
									if (ReplaceGoods)
										invoked.TransientReplaceGoods			= VendorReturn.TransientReplaceGoods.Yes
									else
										invoked.TransientReplaceGoods			= VendorReturn.TransientReplaceGoods.No
									invoked.ShipOrHold							= ShipOrHold
							
							LocalCompletionMessage = ReleasedWithVendorReturnMessage
						
							for each InspectionLinesForVendorReturnRel
		    			 		invoke CreateVendorReturns each
		    			 			invoked.PrmVendorReturn = NewVendorReturns.VendorReturn
							
							LocalCompletionMessage = ReleasedWithVendorReturnMessage
						
						invoke UpdateHeaderToArchivedClose PurchaseOrderReceipt
						make transition to Released	
						
						
				ReleaseAdjustment is an Instance Action
					valid when (IsAdjustmentDocument)
					default label is "Release"
					Local Fields
						LocalOriginalMinorAmount	is an InternationalAmount    
						LocalMacMatchAmt			is an InternationalAmount    
						LocalWriteOffAmt			is an InternationalAmount    
						LocalHasQuantity			is a Quantity                
						
					Action Rules
						constraint (POReceiptAdjustmentAndInspection.PORecInspectionLineRels exists)
							"CannotRelease,NoLinesExist"
						
						invoke UpdateFromMatch PurchaseOrderReceipt.PurchaseOrder 
							invoked.PrmLastActivityDate			= current corporate date
						
						if (Company.PackageTrackingLevel.HeaderLevel or Company.PackageTrackingLevel.EnableMscmRadTracking)
							invoke CreateArrival PurchaseOrderReceipt.PurchaseOrderReceiptTrackingRel
					Exit Rules
                        
                        for each distinct HeaderAndLocation in InventoriedAjustmentOrInspectionLinesRel
							LocalCurrentLocation = each.ReceivingLocation
							
                        	if (InventoryTransactionForDistinctHeaderAndLocationRel exists)
                        		LocalInventoryTransaction = last InventoryTransactionForDistinctHeaderAndLocationRel.InventoryTransaction
                        	else
								invoke Unreleased.Create InventoryTransaction
									assign result to NewInventoryTransactionForAdjustmentItem
									fill in fields from this instance
										except invoked.OriginatingInterfaceRun
									invoked.Company                         = Company
									invoked.InventoryLocation               = LocalCurrentLocation
									invoked.InventoryDocumentType           = InventoryDocumentType.PurchaseOrderReceiptAdjustment
									invoked.OriginatingTransaction			= reference to this instance
									invoked.GeneralLedgerDate				= DocumentDate
									
								LocalInventoryTransaction = NewInventoryTransactionForAdjustmentItem.InventoryTransaction
							
							invoke ReleaseAdjustment Unreleased LinesWithSameLocationRel
								invoked.PrmInventoryTransactionForAdjustmentItem	= LocalInventoryTransaction
							
							if (LinesWithSameLocationRel not exists)
								invoke TransitionToReleased Unreleased InventoryTransactionForReleaseRel
							
						invoke ReleaseAdjustment Unreleased NonInventoriedLinesRel

						invoke UpdateReceiptAmountFromPORAdjInsp PurchaseOrderReceipt
							invoked.PrmReceiptAmount = TotalExtendedAmt
							invoked.PrmHashQuantity  = TotalVendorPriceUOMQuantity

						invoke UpdateHeaderToArchivedClose PurchaseOrderReceipt
						make transition to Released 

			Released is a State
				 
					 		
	Actions
	
		Purge is a Purge Action
			restricted
			
			Entrance Rules
				invoke Purge PurgePOReceiptAdjustmentAndInspectionLineRel
				
						
						
