ExpenseManagementInterfaceDetail is a BusinessClass
    owned by ap
    prefix is XPD
    sql name is XMInterfaceDetail
    classic name is EEEXPDTL

    Ontology
        symbolic key is ExpenseManagementInterfaceDetail
		    sql name is XMInterfaceDetail
            classic set name is XPDSET1
            classic name for ExpenseManagementInterfaceDetail is SEQ

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

		PayablesCompany
			disable surrogates
        Vendor
		HROrganizationAndEmployee
        ExpenseManagementInterfaceExpenseType
        ExpenseDate                      is Date
        Comment1                         is a Data
            classic name is EE-COMMENT1
        Comment2                         is a Data
            classic name is EE-COMMENT2
        Comment3                         is a Data
            classic name is EE-COMMENT3
        Status                           is Numeric size 1 
			protected
            States
                NotProcessed         value is 1
                InError              value is 2
                SentForReimbursement value is 9
        SentDate                         is TimeStamp
			protected
        PaymentMethod                    is Numeric size 4
            classic name is PAYMETHOD
            States
                CompanyPaid              		value is 10
                OutOfPocket              		value is 20
                ReimburseToCreditCardCompany	value is 30
                CompanyPaidCreditCard    		value is 40
        TransactionCurrency              is a Currency
            classic name is TRAN-CURRENCY
        TransactionAmount                is an InternationalAmount
            classic name is TRAN-AMT
        ReimbursementCurrency            is a Currency
            classic name is REIM-CURRENCY
        ReimbursementAmount              is an InternationalAmount
            classic name is REIM-AMT
			default label is "Amount"
        ReimbursementRate                is a Baserate
            classic name is REIMRATE
        BaseCurrency                     is a Currency
        BaseAmount                       is an InternationalAmount
            classic name is BASE-AMT
        BaseRate                         is a Baserate
            classic name is BASERATE
        TransactionPersonalExpenseAmount is an InternationalAmount
            sql name is TPersonalExpenseAmount
            classic name is TRAN-PERS-AMT
        TaxCode1                         is a TaxCode
            classic name is TAX-CODE1
        TaxAmount1                       is an InternationalAmount
            classic name is TAX-AMT1
			default label is "TaxAmount"
        ReimbursementTaxAmount1          is an InternationalAmount
            classic name is REIM-TAX-AMT1
        TaxCode2                         is a TaxCode
            classic name is TAX-CODE2
        TaxAmount2                       is an InternationalAmount
            classic name is TAX-AMT2
        ReimbursementTaxAmount2          is an InternationalAmount
            classic name is REIM-TAX-AMT2
        Receipt                          is Numeric size 4
            States
                HasReceipt value is 10
                NoReceipt  value is 20
        PayCode                          is AlphaUpper size 4
        	restricted
        ErrorText                        is a Text
        GltObjId                         is an ObjId
        	default label is untranslatable
        AtnObjId                         is an ObjId
        	default label is untranslatable
        Billable                         is Boolean
        AccountingEntity
        FinanceStructure				 is a FinanceCodeBlock
            classic name for FinanceStructure.ToAccountingEntity is POST-COMPANY
            classic name for FinanceStructure.AccountingUnit is ACCT-UNIT
            classic name for FinanceStructure.GeneralLedgerChartAccount is ACCOUNT
            classic name for FinanceStructure.Project is ACTIVITY
        BillingFinanceStructure			 is a FinanceCodeBlock
            classic name for BillingFinanceStructure.GeneralLedgerChartAccount is BILL-ACCOUNT
		ExpenseManagementInterfaceResult
			protected
			delete ignored
		ErrorMessage					 is Alpha 150
			protected

	Field Rules
		FinanceStructure
			required
		BillingFinanceStructure
			if (Billable)
				BillingFinanceStructure = FinanceStructure
        PayablesCompany
			if (PaymentMethod.CompanyPaid
			or  PaymentMethod.CompanyPaidCreditCard)
				default to ExpenseManagementInterfaceHeader.PayablesCompany
		Vendor
			required
		PaymentMethod
			required
		ExpenseDate
			required
		AccountingEntity
			required
		ExpenseManagementInterfaceExpenseType
			required
		ReimbursementAmount
			required
		ReimbursementCurrency
			required

	Local Fields
		BypassUnitAndAmountEdit
		LocalFinanceEnterpriseGroup						is like FinanceEnterpriseGroup
		LocalCalculateExpenseManagementFinanceStructure	is a CalculateExpenseManagementFinanceStructure
		LocalAnswer				 						is Alpha size 1
		LocalFinanceCodeBlock							is a FinanceCodeBlock

    Derived Fields
		ThisProgramNameText is a MessageField 
			restricted
			"ExpenseManagementInterfaceDetail"

		ExpenseManagementInterfacePostingMessage is a MessageField 
			restricted
			"ExpenseManagementInterfacePosting"

		NotProcessedMessage is a MessageField
			"Not_Processed"

		InErrorMessage is a MessageField
			"In_Error"

		SentForReimbursementMessage is a MessageField
			"Sent_For_Reimbursement"

		PostedMessage is a MessageField
			"Posted"

		DerivedStatus is a DerivedField
			type is Alpha size 30
			default label is "Status"
			if  (Status.SentForReimbursement)
				if (PaymentMethod.CompanyPaid
				or  PaymentMethod.CompanyPaidCreditCard)
					return PostedMessage
				else
					return SentForReimbursementMessage
			else
				if  (Status.NotProcessed)
					return NotProcessedMessage
				else
					return InErrorMessage

	Conditions
		CanPurge
			when (ExpenseManagementInterfaceResult not entered)

		AllocationsExist
			when (ExpenseManagementInterfaceAllocation set exists)

    	SecurityGroupAllowsAccess
    		when (PayablesCompany !entered
    		or   (PayablesCompany entered
    		and   PayablesCompany.SecurityGroupAllowsAccess))
    
    Sets

        Set2
            indexed
            Sort Order
                Status
                PayablesCompany
				Vendor
				ExpenseManagementInterfaceHeader.Document
                ExpenseManagementInterfaceDetail

        Set3
            indexed
            Sort Order
                PaymentMethod
                PayablesCompany
				ExpenseManagementInterfaceHeader.Document
                ExpenseManagementInterfaceDetail

		ByExpenseManagementInterfaceResult
			indexed
			duplicates
			Sort Order
				VendorGroup
				ExpenseManagementInterfaceResult

		ByCompanyTaxCode1ExpenseDate
			indexed
			duplicates
			Sort Order
				VendorGroup
				ExpenseManagementInterfaceHeader
				PayablesCompany
				TaxCode1
				ExpenseDate

	Relations
		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= LocalFinanceEnterpriseGroup
				related.GeneralLedgerSystemCode		= "AP"

		ExpenseManagementTravelPlanDetailRel
			one-to-one relation to ExpenseManagementTravelPlanDetail
			Field Mapping uses ByInterfaceExpenseType
				related.VendorGroup								= VendorGroup
				related.ExpenseManagementTravelPlan				= ExpenseManagementInterfaceHeader.ExpenseManagementTravelPlan
				related.ExpenseManagementInterfaceExpenseType	= ExpenseManagementInterfaceExpenseType
	
	Rule Blocks
		ValidateData
			if (VendorGroup.FinanceEnterpriseGroup.AccountingUnitOrder not entered)
				FinanceStructure.AccountingUnit		= blank
			if (VendorGroup.FinanceEnterpriseGroup.ProjectOrder not entered)
				FinanceStructure.Project 			= blank
		
	Actions
		Create is a Create Action
			Field Rules
				FinanceStructure
					if (Vendor entered)
						default to Vendor.ExpenseDefault
							default individual fields
					if (ExpenseManagementInterfaceExpenseType entered)
						default to ExpenseManagementInterfaceExpenseType.FinanceStructure
							default individual fields
			Exit Rules
				include ValidateData
				
		Update is an Update Action
			Action Rules
				include ValidateData
						
		SetStatus is an Instance Action
			restricted
			Parameters
		        PrmStatus							is Numeric size 1
		            States
		                NotProcessed         	value is 1
		                InError              	value is 2
		                SentForReimbursement 	value is 9
				PrmErrorMessage						is Alpha 150
				PrmExpenseManagementInterfaceResult	is a ExpenseManagementInterfaceResult
			Action Rules
				Status			= PrmStatus
				ErrorMessage	= PrmErrorMessage
				if (PrmStatus.SentForReimbursement)
					SentDate = current timestamp
					ExpenseManagementInterfaceResult = PrmExpenseManagementInterfaceResult
				else
					SentDate = blank
					ExpenseManagementInterfaceResult = blank
				
		UpdateExpenseManagementTravelPlanDetail is an Instance Action
			restricted
			Action Rules
				invoke Update ExpenseManagementTravelPlanDetailRel
					invoked.ExpensedAmount	+= ReimbursementAmount

		CompanyExpensePosting is a Set Action
			completion message is "CompanyExpensePostingComplete"
			Parameters
				PrmVendorGroup					is a VendorGroup
					default label is "VendorGroup"
				PrmPayablesCompany				is a PayablesCompany
					default label is "PayablesCompany"
				PostingDate						is Date
				OffsetFinanceStructure			is a FinanceCodeBlock
				OffsetGeneralLedgerChartAccount	is a GeneralLedgerChartAccount
					context of PrmVendorGroup
			
			Parameter Rules
				PrmVendorGroup
					required
				PrmPayablesCompany
					required
				PostingDate
					initial value is current corporate date
					default to current corporate date
				OffsetFinanceStructure
					if (!PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
						required
							"Offset_Finance_StructureIsRequired"
				OffsetGeneralLedgerChartAccount
					if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
						required
							"Offset_General_Ledger_Chart_AccountIsRequired"
								
			Instance Selection
				where (VendorGroup = PrmVendorGroup
				and    PayablesCompany = PrmPayablesCompany
				and   !Status.SentForReimbursement
				and   (PaymentMethod.CompanyPaid
				or     PaymentMethod.CompanyPaidCreditCard))

			Local Fields				
				GLTJournalizeGroup						is like JournalizeGroup
				LocalExpenseManagementInterfaceResult 	is a ExpenseManagementInterfaceResult view
				LocalGLTransactionDetail				is a GLTransactionDetail view
				ParameterErrorOccurred					is Boolean
				LocalErrorMessage						is Alpha 150
				LocalTotalTransactionAmount				is an InternationalAmount
				TransactionErrorOccurred				is Boolean
				
			Action Rules
				Empty Set Rules
					invoke Create ExpenseManagementInterfaceResult
						assign result to LocalExpenseManagementInterfaceResult
						invoked.VendorGroup 	= PrmVendorGroup
						invoked.RunType			= 2
						invoked.RunTime			= current timestamp
						invoked.JournalizeGroup	= blank
						invoked.Status			= 1

				Set Rules
					Entrance Rules
						if (!PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
							invoke Create GLTransactionInterfaceEdit
								resume on error
									ParameterErrorOccurred		= true
									LocalErrorMessage			= error message
								invoked.FinanceEnterpriseGroup	= actor.context.FinanceEnterpriseGroup
								invoked.FinanceCodeBlock		= OffsetFinanceStructure
								invoked.System					= "AP"
								invoked.GeneralLedgerEvent		= "AP"
								invoked.TransactionAmount		= 1
								invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
								invoked.PostingDate				= current date
								invoked.TransactionDate			= current date

						if  (!ParameterErrorOccurred)
							LocalFinanceEnterpriseGroup = PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
							invoke IncrementLastJournalizeGroup GeneralLedgerSystemCodeRel
							GLTJournalizeGroup = GeneralLedgerSystemCodeRel.DerivedJournalizeGroup
						else
							LocalErrorMessage			= "Interface Company Expense Posting Parameter Error:" + LocalErrorMessage
						
						invoke Create ExpenseManagementInterfaceResult
							assign result to LocalExpenseManagementInterfaceResult
							invoked.VendorGroup 	= VendorGroup
							invoked.RunType			= 2
							invoked.RunTime			= current timestamp
							invoked.JournalizeGroup	= GLTJournalizeGroup
							invoked.Status			= 2

						if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
							initialize OffsetFinanceStructure
							OffsetFinanceStructure.GeneralLedgerChartAccount = OffsetGeneralLedgerChartAccount

					Exit Rules
						if  (!ParameterErrorOccurred)
							invoke InitiateJournalizeForRunGroup PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup in background
								invoked.PrmJournalizeGroup				= GLTJournalizeGroup
								invoked.PrmJournalizeGroupDescription	= ExpenseManagementInterfacePostingMessage
								if (PrmPayablesCompany.PostingOption.Detail)
									invoked.PrmInterfaceInDetail		= true
								else
									invoked.PrmInterfaceInDetail 		= false

						invoke Update LocalExpenseManagementInterfaceResult.ExpenseManagementInterfaceResult
							invoked.Status = 1

				Instance Rules
					if  (ParameterErrorOccurred)
						ErrorMessage = LocalErrorMessage
						Status = Status.InError
					else
						BypassUnitAndAmountEdit		= false
						TransactionErrorOccurred	= false
						LocalErrorMessage			= blank

						if (ExpenseManagementInterfaceAllocation set not exists)
							invoke Create GLTransactionInterfaceEdit
								resume on error
									TransactionErrorOccurred	= true
									LocalErrorMessage			= error message
								invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
								invoked.FinanceCodeBlock		= FinanceStructure
								invoked.System					= "AP"
								invoked.GeneralLedgerEvent		= "ED"
								invoked.TransactionAmount		= TransactionAmount
								invoked.CurrencyCode			= TransactionCurrency					 
								invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
								invoked.PostingDate				= PostingDate
								invoked.TransactionDate			= ExpenseDate

							if  (!TransactionErrorOccurred)
								LocalFinanceCodeBlock = OffsetFinanceStructure
								if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
									initialize LocalCalculateExpenseManagementFinanceStructure
									LocalCalculateExpenseManagementFinanceStructure.InputVendorGroup						= PrmVendorGroup
									LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceHeader	= ExpenseManagementInterfaceHeader
									LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceDetail	= ExpenseManagementInterfaceDetail
									LocalCalculateExpenseManagementFinanceStructure.InputFinanceCodeBlock					= OffsetFinanceStructure

									LocalAnswer				= LocalCalculateExpenseManagementFinanceStructure.CalculateExpenseManagementFinanceStructure
									LocalFinanceCodeBlock	= LocalCalculateExpenseManagementFinanceStructure.OutputFinanceCodeBlock

								invoke Create GLTransactionInterfaceEdit
									resume on error
										TransactionErrorOccurred	= true
										LocalErrorMessage			= error message
									invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
									invoked.FinanceCodeBlock		= LocalFinanceCodeBlock
									invoked.System					= "AP"
									invoked.GeneralLedgerEvent		= "AC"
									invoked.TransactionAmount		= TransactionAmount * -1
									invoked.CurrencyCode			= TransactionCurrency					 
									invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
									invoked.PostingDate				= PostingDate
									invoked.TransactionDate			= ExpenseDate

							if  (TransactionErrorOccurred)
								ErrorMessage = LocalErrorMessage
								Status = Status.InError
							else
								invoke Released.Create GLTransactionDetail
									assign result to LocalGLTransactionDetail
									invoked.OriginatingTransaction 	= reference to this instance
									invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
									invoked.System					= "AP"
									invoked.Reference				= ThisProgramNameText
									invoked.Description				= ExpenseManagementInterfaceExpenseType.Description
									invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
									invoked.Status					= 1
									invoked.TransactionAmount		= TransactionAmount
									invoked.CurrencyCode			= TransactionCurrency					 
									invoked.TransactionDate			= ExpenseDate		 
									invoked.PostingDate				= PostingDate	 
									invoked.FinanceCodeBlock		= FinanceStructure
									invoked.DocumentNumber		  	= ExpenseManagementInterfaceHeader.Document
									invoked.GeneralLedgerEvent 		= "ED"
									invoked.AutoReverse 			= false
									invoked.JournalizeGroup			= GLTJournalizeGroup

								LocalFinanceCodeBlock = OffsetFinanceStructure
								if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
									initialize LocalCalculateExpenseManagementFinanceStructure
									LocalCalculateExpenseManagementFinanceStructure.InputVendorGroup						= PrmVendorGroup
									LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceHeader	= ExpenseManagementInterfaceHeader
									LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceDetail	= ExpenseManagementInterfaceDetail
									LocalCalculateExpenseManagementFinanceStructure.InputFinanceCodeBlock					= OffsetFinanceStructure

									LocalAnswer				= LocalCalculateExpenseManagementFinanceStructure.CalculateExpenseManagementFinanceStructure
									LocalFinanceCodeBlock	= LocalCalculateExpenseManagementFinanceStructure.OutputFinanceCodeBlock

								invoke Released.Create GLTransactionDetail
									invoked.OriginatingTransaction 	= reference to LocalGLTransactionDetail.GLTransactionDetail
									invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
									invoked.System					= "AP"
									invoked.Reference				= ThisProgramNameText
									invoked.Description				= ExpenseManagementInterfaceExpenseType.Description
									invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
									invoked.Status					= 1
									invoked.TransactionAmount		= TransactionAmount * -1
									invoked.CurrencyCode			= TransactionCurrency					 
									invoked.TransactionDate			= ExpenseDate		 
									invoked.PostingDate				= PostingDate	 
									invoked.FinanceCodeBlock		= LocalFinanceCodeBlock
									invoked.DocumentNumber		  	= ExpenseManagementInterfaceHeader.Document
									invoked.GeneralLedgerEvent 		= "AC"
									invoked.AutoReverse 			= false
									invoked.JournalizeGroup			= GLTJournalizeGroup
						else
							LocalTotalTransactionAmount		= TransactionAmount
							for each ExpenseManagementInterfaceAllocation set

								invoke Create GLTransactionInterfaceEdit
									resume on error
										TransactionErrorOccurred	= true
										LocalErrorMessage			= error message
									invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
									invoked.FinanceCodeBlock		= each.FinanceStructure
									invoked.System					= "AP"
									invoked.GeneralLedgerEvent		= "ED"
									invoked.TransactionAmount		= TransactionAmount * each.Percentage
									invoked.CurrencyCode			= TransactionCurrency					 
									invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
									invoked.PostingDate				= PostingDate
									invoked.TransactionDate			= ExpenseDate

								if  (!TransactionErrorOccurred)
									FinanceStructure		= each.FinanceStructure
									LocalFinanceCodeBlock	= OffsetFinanceStructure
									if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
										initialize LocalCalculateExpenseManagementFinanceStructure
										LocalCalculateExpenseManagementFinanceStructure.InputVendorGroup						= PrmVendorGroup
										LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceHeader	= ExpenseManagementInterfaceHeader
										LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceDetail	= ExpenseManagementInterfaceDetail
										LocalCalculateExpenseManagementFinanceStructure.InputFinanceCodeBlock					= OffsetFinanceStructure

										LocalAnswer				= LocalCalculateExpenseManagementFinanceStructure.CalculateExpenseManagementFinanceStructure
										LocalFinanceCodeBlock	= LocalCalculateExpenseManagementFinanceStructure.OutputFinanceCodeBlock
									invoke Create GLTransactionInterfaceEdit
										resume on error
											TransactionErrorOccurred	= true
											LocalErrorMessage			= error message
										invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
										invoked.FinanceCodeBlock		= LocalFinanceCodeBlock
										invoked.System					= "AP"
										invoked.GeneralLedgerEvent		= "AC"
										invoked.TransactionAmount		= TransactionAmount * each.Percentage * -1
										invoked.CurrencyCode			= TransactionCurrency					 
										invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
										invoked.PostingDate				= PostingDate
										invoked.TransactionDate			= ExpenseDate

								if (TransactionErrorOccurred)
									ErrorMessage = LocalErrorMessage
									Status = Status.InError
									end for each

							if (!TransactionErrorOccurred)
								for each ExpenseManagementInterfaceAllocation set
									if (each.ExpenseManagementInterfaceAllocation != last ExpenseManagementInterfaceAllocation set.ExpenseManagementInterfaceAllocation)
										invoke Released.Create GLTransactionDetail
											assign result to LocalGLTransactionDetail
											invoked.OriginatingTransaction 	= reference to this instance
											invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
											invoked.System					= "AP"
											invoked.Reference				= ThisProgramNameText
											invoked.Description				= ExpenseManagementInterfaceExpenseType.Description
											invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
											invoked.Status					= 1
											invoked.TransactionAmount		= TransactionAmount * each.Percentage
											invoked.CurrencyCode			= TransactionCurrency					 
											invoked.TransactionDate			= ExpenseDate		 
											invoked.PostingDate				= PostingDate	 
											invoked.FinanceCodeBlock		= each.FinanceStructure
											invoked.DocumentNumber		  	= ExpenseManagementInterfaceHeader.Document
											invoked.GeneralLedgerEvent 		= "ED"
											invoked.AutoReverse 			= false
											invoked.JournalizeGroup			= GLTJournalizeGroup

										LocalTotalTransactionAmount	   -= LocalGLTransactionDetail.TransactionAmount

										FinanceStructure		= each.FinanceStructure
										LocalFinanceCodeBlock	= OffsetFinanceStructure
										if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
											initialize LocalCalculateExpenseManagementFinanceStructure
											LocalCalculateExpenseManagementFinanceStructure.InputVendorGroup						= PrmVendorGroup
											LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceHeader	= ExpenseManagementInterfaceHeader
											LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceDetail	= ExpenseManagementInterfaceDetail
											LocalCalculateExpenseManagementFinanceStructure.InputFinanceCodeBlock					= OffsetFinanceStructure

											LocalAnswer				= LocalCalculateExpenseManagementFinanceStructure.CalculateExpenseManagementFinanceStructure
											LocalFinanceCodeBlock	= LocalCalculateExpenseManagementFinanceStructure.OutputFinanceCodeBlock
										invoke Released.Create GLTransactionDetail
											invoked.OriginatingTransaction 	= reference to LocalGLTransactionDetail.GLTransactionDetail
											invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
											invoked.System					= "AP"
											invoked.Reference				= ThisProgramNameText
											invoked.Description				= ExpenseManagementInterfaceExpenseType.Description
											invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
											invoked.Status					= 1
											invoked.TransactionAmount		= TransactionAmount * each.Percentage * -1
											invoked.CurrencyCode			= TransactionCurrency					 
											invoked.TransactionDate			= ExpenseDate		 
											invoked.PostingDate				= PostingDate	 
											invoked.FinanceCodeBlock		= LocalFinanceCodeBlock
											invoked.DocumentNumber		  	= ExpenseManagementInterfaceHeader.Document
											invoked.GeneralLedgerEvent 		= "AC"
											invoked.AutoReverse 			= false
											invoked.JournalizeGroup			= GLTJournalizeGroup
							
									else
										invoke Released.Create GLTransactionDetail
											assign result to LocalGLTransactionDetail
											invoked.OriginatingTransaction 	= reference to this instance
											invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
											invoked.System					= "AP"
											invoked.Reference				= ThisProgramNameText
											invoked.Description				= ExpenseManagementInterfaceExpenseType.Description
											invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
											invoked.Status					= 1
											invoked.TransactionAmount		= LocalTotalTransactionAmount
											invoked.CurrencyCode			= TransactionCurrency					 
											invoked.TransactionDate			= ExpenseDate		 
											invoked.PostingDate				= PostingDate	 
											invoked.FinanceCodeBlock		= each.FinanceStructure
											invoked.DocumentNumber		  	= ExpenseManagementInterfaceHeader.Document
											invoked.GeneralLedgerEvent 		= "ED"
											invoked.AutoReverse 			= false
											invoked.JournalizeGroup			= GLTJournalizeGroup

										FinanceStructure		= each.FinanceStructure
										LocalFinanceCodeBlock	= OffsetFinanceStructure
										if (PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)
											initialize LocalCalculateExpenseManagementFinanceStructure
											LocalCalculateExpenseManagementFinanceStructure.InputVendorGroup						= PrmVendorGroup
											LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceHeader	= ExpenseManagementInterfaceHeader
											LocalCalculateExpenseManagementFinanceStructure.InputExpenseManagementInterfaceDetail	= ExpenseManagementInterfaceDetail
											LocalCalculateExpenseManagementFinanceStructure.InputFinanceCodeBlock					= OffsetFinanceStructure

											LocalAnswer				= LocalCalculateExpenseManagementFinanceStructure.CalculateExpenseManagementFinanceStructure
											LocalFinanceCodeBlock	= LocalCalculateExpenseManagementFinanceStructure.OutputFinanceCodeBlock
										invoke Released.Create GLTransactionDetail
											invoked.OriginatingTransaction 	= reference to LocalGLTransactionDetail.GLTransactionDetail
											invoked.FinanceEnterpriseGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
											invoked.System					= "AP"
											invoked.Reference				= ThisProgramNameText
											invoked.Description				= ExpenseManagementInterfaceExpenseType.Description
											invoked.AccountingEntity		= PrmPayablesCompany.GeneralLedgerCompany.AccountingEntity
											invoked.Status					= 1
											invoked.TransactionAmount		= LocalTotalTransactionAmount * -1
											invoked.CurrencyCode			= TransactionCurrency					 
											invoked.TransactionDate			= ExpenseDate		 
											invoked.PostingDate				= PostingDate	 
											invoked.FinanceCodeBlock		= LocalFinanceCodeBlock
											invoked.DocumentNumber		  	= ExpenseManagementInterfaceHeader.Document
											invoked.GeneralLedgerEvent 		= "AC"
											invoked.AutoReverse 			= false
											invoked.JournalizeGroup			= GLTJournalizeGroup

						if (!TransactionErrorOccurred)
							Status			= Status.SentForReimbursement
							ErrorMessage	= blank
							SentDate		= current timestamp
							ExpenseManagementInterfaceResult = LocalExpenseManagementInterfaceResult.ExpenseManagementInterfaceResult
							if (ExpenseManagementInterfaceHeader.ExpenseManagementTravelPlan entered)
								invoke UpdateExpenseManagementTravelPlanDetail
								
						if (all ExpenseManagementInterfaceHeader.ExpenseManagementInterfaceDetail set.Status.SentForReimbursement
						and all ExpenseManagementInterfaceHeader.ExpenseManagementInterfaceDetail set.PaymentMethod.CompanyPaid)


							invoke Update ExpenseManagementInterfaceHeader
								invoked.Status 								= Status.SentForReimbursement
								invoked.ErrorMessage						= blank
								invoked.SentDate							= current timestamp
								invoked.ExpenseManagementInterfaceResult 	= LocalExpenseManagementInterfaceResult.ExpenseManagementInterfaceResult
							if (ExpenseManagementInterfaceHeader.ExpenseManagementTravelPlan entered
							and ExpenseManagementInterfaceHeader.LastExpenseForTravelPlan)
								invoke CloseTravelPlan ExpenseManagementInterfaceHeader.ExpenseManagementTravelPlan

		ResetErrorMessage is an Instance Action
			valid when (Status.InError)
			Action Rules
				initialize ErrorMessage
				Status = Status.NotProcessed

		Purge is a Purge Action
			valid when (CanPurge)

		FastPurge is a Purge Action
			restricted
			bypass relational integrity rules

		PurgeInBackground is a Set Action
			restricted
			completion message is "PurgeInBackgroundComplete"
			
			Sort Order is ByExpenseManagementInterfaceResult
			
			Parameters
				PrmVendorGroup		is a VendorGroup
					default label is "VendorGroup"
				PrmExpenseManagementInterfaceResult is like ExpenseManagementInterfaceResult
					default label is "ExpenseManagementInterfaceResult"
			Parameter Rules
				PrmVendorGroup
					required
				PrmExpenseManagementInterfaceResult
					required
			
			Instance Selection
				where (VendorGroup						= PrmVendorGroup
				and    ExpenseManagementInterfaceResult = PrmExpenseManagementInterfaceResult)
			
			Action Rules
				Instance Rules
					invoke FastPurge

		ChangeKey is an Instance Action
			restricted
			default label is untranslatable
			Parameters
				PrmDocument is an Invoice
            Action Rules
				ExpenseManagementInterfaceHeader.Document = PrmDocument
