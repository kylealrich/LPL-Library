CustomerMajorClass is a BusinessClass
    owned by ar
    prefix is MJC
    classic name is MAJCUSTCL

    Ontology
        symbolic key is CustomerMajorClass
            classic set name is MJCSET1
            classic name is MAJOR-CLASS

    Patterns
        implements StaticJava
        disable AuditIndex
		implements BODId
		
    Persistent Fields

        Description
            classic name is DESC
        Active      is Boolean
            classic name is ACTIVE-STATUS
        UsedFlag    is Boolean
            classic name is USED-FL
	
	Local Fields 
		ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		LocalNativeLPLBODTrigger		is Boolean
		NewBODTracker  				is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker	is Numeric 15
		Error            			is Boolean
	    ErrorMessage     			is Alpha 300
		LocalBODTrigger				is Boolean
		LocalConfigurationParameter	is Alpha size up to 200

	Context Fields
		FSMInboundBODTracker
    
    Derived Fields

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
	
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or ActionCode.Create	)
				return "Add"
			else
			if(action type.Update or action type.Delete)
				return "Replace"
			else
				return ""	
		
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if(action type.Delete or ActionCode.Delete)
				return "Closed"
			else
			if(Active = "true")
				return "Open"
			else
				return "Closed"
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
					
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return 	"infor-nid:" + DerivedTenantID + ":" + CustomerMajorClass+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
		
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
		
		CustomerMajorClassXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	      	CustomerMajorClassXMLBOD = template.IONSyncCodeDefination_CustomerMajorClass_ST document for this instance	

    Conditions
    	IsACustomerClass
    		restricted
    		when (CustomerMinorClassRel exists)
    
    Relations

        CustomerMinorClassRel
            one-to-many relation to CustomerMinorClass
            delete restricted		
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
                related.CustomerMajorClass			= CustomerMajorClass

		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1
            
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker


    Field Rules

        Description
            required

        Active
            default to true
            
    Rule Blocks				
    	TriggerCustomerMajorClassServiceRules
    		trigger "CustomerMajorClassService" PA service
				resume on error
				title is "EG:<FinanceEnterpriseGroup>CMC:<CustomerMajorClass>"
				Criteria
					FinanceEnterpriseGroup

				Variables
					ActionCode
					FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					include persistent fields from CustomerMajorClass
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp							

    Actions
        Create is a Create Action
    	UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
        
        	

        Update is an Update Action

        Delete is a Delete Action
        	Entrance Rules
        		if (FinanceEnterpriseGroup.BODTrigger)
					ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerCustomerMajorClassServiceRules

							
		BODDataInitialLoadCustomerMajorClassCriteria1 is a Set Action
			restricted
			Parameters
				PrmFEG					is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFromCustomerType		is a CustomerMajorClass
					default label is "PrmFromCustomerType"
				PrmToCustomerType			is a CustomerMajorClass
					default label is "PrmToCustomerType"
			Parameter Rules
			Instance Selection			
				where  (((PrmFEG entered
				and		  PrmFEG = FinanceEnterpriseGroup)
				or		  PrmFEG not entered)
				and	   ((PrmFromCustomerType entered
				and		 PrmFromCustomerType <= CustomerMajorClass)
				or		 PrmFromCustomerType not entered)				
				and    ((PrmToCustomerType entered
				and		 PrmToCustomerType >= CustomerMajorClass)
				or		 PrmToCustomerType not entered)
				and		(Active = true))
		
			Sort Order
				FinanceEnterpriseGroup
				CustomerMajorClass
			
			Action Rules											
				Instance Rules	
					if (FinanceEnterpriseGroup.BODTrigger)
						ActionCode	= ActionCode.Create		
						increment bod id.VariationID
						include TriggerCustomerMajorClassServiceRules


		SendCustomerMajorClassBODNativeLPL is an Instance Action			
			restricted
			Action Rules
				send ion bod
					bod is CustomerMajorClassXMLBOD
					bod type is "Sync.CodeDefinition"
					document id is CustomerMajorClass
					variation id is	BODVariationID

		TriggerCustomerMajorClassNativeLPLBOD is an Instance Action			
			restricted
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "CustomerMajorClass"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_CustomerMajorClass_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= CustomerMajorClass
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= "CustomerMajorClass"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= CustomerMajorClass
							invoked.BODVariationID			= BODVariationID
							invoked.BODID					= DerivedBODFormattedId
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= "CustomerMajorClass"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendCustomerMajorClassBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= CustomerMajorClassXMLBOD
							invoked.BODID								= DerivedBODFormattedId
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODXML									= CustomerMajorClassXMLBOD
							invoked.BODID									= DerivedBODFormattedId

	Action Exit Rules
		if (FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode	= ActionCode.Update
					if (action type.Create)
						ActionCode	= ActionCode.Create				
					else
						if (action type.Delete)
							ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerCustomerMajorClassServiceRules

