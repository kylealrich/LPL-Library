CashLedgerStatement is a BusinessClass	
    owned by cb
    prefix is CSM
    classic name is CBSTATEMNT

    Ontology
        symbolic key is CashLedgerStatement
            classic set name is CSMSET1
            classic name is RECON-STMT-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        ReconciliationStatementDate             is an ExchangeDate
            classic name is RECON-STMT-DT
        BeginningReconciliationStatementBalance is an InternationalAmount
            sql name is BRStatementBalance
            classic name is BEGIN-BAL
            	precision is CurrencyDecimalsNumber
        EndingReconciliationStatementBalance    is an InternationalAmount
            sql name is ERStatementBalance
            classic name is END-BAL
            	precision is CurrencyDecimalsNumber
        ReconciliationJournalBook               is a JournalBook
            classic name is RECON-JRNL-BK
            context of Company.OrigCompany
            restricted
        StatementStatus                         is a StmtStatus
            classic name is STMT-STATUS
        Company                                 is a CompanyIsolated 
        ReceivableProcessLevel
            classic name is PROC-LEVEL
            context of Company.OrigCompany  
        Currency
            classic name is CURRENCY-CODE
        CurrencyRate
            classic name is ORIG-RATE
        CurrencyMultiplicationOrDivision
            sql name is CMultiplicationOrDivision
            classic name is CURR-MUDV
        CurrencyDecimalsNumber                  is an AcctNd
            classic name is ORIG-ND
        StatementBalanceEntered                 is an InternationalAmount
            classic name is STMT-BAL-ENT
            	precision is CurrencyDecimalsNumber
        StatementEnteredDebit                   is an InternationalAmount
            classic name is STMT-ENT-DR
            	precision is CurrencyDecimalsNumber
        StatementEnteredCredit                  is an InternationalAmount
            classic name is STMT-ENT-CR
            	precision is CurrencyDecimalsNumber
        CustomerBankTransactionCode             is a BankTransactionCode
            classic name is DEP-INST-CODE
        VendorBankTransactionCode               is a BankTransactionCode
            classic name is CHK-INST-CODE
        BankTransactionCode
            classic name is BANK-INST-CODE
        ReconciliationBankTransactionCode       is a BankTransactionCode
            sql name is RBankTransactionCode
            classic name is RECN-INST-CODE
        ReceivablePaymentHeader
            classic name is BATCH-NBR
            context of Company.OrigCompany  
        NumberOfLines
            classic name is NBR-LINES
        CompanyCashCode							is like CompanyCashCode

	Local Fields
	
		LocalCurrencyExchange	is a CurrencyExchange
			
    Conditions

        IsOpenStatement
        	restricted
            classic name is OPEN-STMT
            when (StatementStatus.Open)

    Relations

        CbstmtdtlRel
            one-to-many relation to CashLedgerStatementDetail
            delete restricted
            Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashCode.CashManagementGroup	
                related.CashCode            	= CashCode
                related.CashLedgerStatement 	= CashLedgerStatement

        CashLedgerStatementForCashCodeRel
            one-to-one relation to CashLedgerStatement
            Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashCode.CashManagementGroup	
                related.CashCode 				= CashCode
                related.CashLedgerStatement 	= CashCode.CashLedgerStatement

		ReceivableCompanyRel
            one-to-one relation to ReceivableCompany
            Field Mapping uses symbolic key
                related.Company   = Company

		ReceivableProcessLevelRel
            one-to-one relation to ReceivableProcessLevel
            Field Mapping uses symbolic key
                related.Company  					= Company
                related.ReceivableProcessLevel      = ReceivableProcessLevel

        ReceivablePaymentsRel
        	one-to-many relation to ReceivablePayment
        	Field Mapping uses symbolic key
        		related.Company	= Company
        		related.ReceivablePaymentHeader = ReceivablePaymentHeader	

        CashLedgerTransactionsOpenRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set6
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode				= CashCode
            Instance Selection
                where (related.Status.Open)                
    
    Sets

 
    Field Rules

		CashCode
			constraint (CashCode.ReconciliationStatement)
				"CashCodeMustIndicateStatement_#IsRequired"                

		CashLedgerStatement
			if (CashLedgerStatement not entered)
				constraint (!CashCode.SingleOpenStatement)
					"Bank_\Stmt_\NbrRequiredForNon-singleStmtUser"            

		Company
			required		
			cannot be changed

		CompanyCashCode
			required
				"Company-cashCodeRelationshipDoesNotExist"			              

		ReconciliationStatementDate
			required
			initial value is current corporate date
			default to current corporate date





		CustomerBankTransactionCode


			required
			constraint (CustomerBankTransactionCode.BankTransactionType.BankService)
				"MustBeABankServiceTypePayCode"                            
			constraint (CustomerBankTransactionCode.AutomaticReconciliation)
				"InvalidPayCode;MustBeSetForAutoReconcile"                 
			

		VendorBankTransactionCode


			required
			constraint (VendorBankTransactionCode.BankTransactionType.CashPayment)
				"MustBeACashTypePayCode"                                   
			constraint(VendorBankTransactionCode.AutomaticReconciliation)
				"InvalidPayCode;MustBeSetForAutoReconcile"                 
			
		BankTransactionCode


			required
			constraint (BankTransactionCode.BankTransactionType.BankService)
				"MustBeABankServiceTypePayCode"                            
			constraint (BankTransactionCode.AutomaticReconciliation)
				"InvalidPayCode;MustBeSetForAutoReconcile"                 
									
		ReconciliationBankTransactionCode


			required
			constraint (!ReconciliationBankTransactionCode.AutomaticReconciliation)
				"InvalidPayCode;CannotBeSetForAutoReconcile"               

		ReceivableProcessLevel
			constraint (ReceivableProcessLevelRel.ActiveStatus = "A")
				"ProcessLevelIsInactive"                                   
			if (old ReceivableProcessLevel entered)	
				cannot be changed
					"AllPaymentsForBatchMustHaveSameProcessLevel"             
				
		
		StatementStatus
			default to StatementStatus.Open
			
		Currency
			default to CashCode.Currency
			cannot be changed
			constraint (Currency = Company.OrigCompany.Currency or ReceivableCompanyRel.MultiCurrencyProcessing)  
				"CashCodeAndCompanyCurrencyMustMatch;CompanyNotMultiCurrency"	
 
 		CurrencyRate
 			if (Currency = Company.OrigCompany.Currency)  
 				default to 1
 				constraint (CurrencyRate = 1)
 					"Currency_\exchange_\rate_\must_\be_\1.0"		
 			else
 				initialize LocalCurrencyExchange
 				LocalCurrencyExchange.ToCurrency = Company.OrigCompany.Currency  
 				default to LocalCurrencyExchange.OutputCurrencyRate
 				if (CurrencyRate != LocalCurrencyExchange.OutputCurrencyRate)
 					constraint(ReceivableCompanyRel.CurrencyRateOverride.Yes)
 						"CannotOverrideCurrencyExchangeRate;OverrideFlag=FALSE"	

					initialize LocalCurrencyExchange
 					LocalCurrencyExchange.ToCurrency = Company.OrigCompany.Currency   
 					LocalCurrencyExchange.EnteredCurrencyRate = CurrencyRate
 					constraint (LocalCurrencyExchange.WithinRateTolerance)
 						"RateIsNotWithinToleranceRange"	
 				
		CurrencyDecimalsNumber
			default to CashCode.BankAccountNumber.Currency.NumberOfDecimals					
 				
 			
 	Actions
 		Create is a Create Action
			Entrance Rules
				if (CashCode.SingleOpenStatement)			
					constraint (!CashLedgerStatement entered)			
						"Bank_\Stmt_\NbrMustBeBlankForSingleStmtUser"	           
					constraint (BeginningReconciliationStatementBalance not entered)			
						"Begin_\BalanceMustBeBlankForSingleStmtUser"             
					constraint (CashLedgerStatementForCashCodeRel not exists or CashLedgerStatementForCashCodeRel.StatementStatus.Closed)					
						"CannotAddStatement_-PrevStatementStillOpen"             
			Action Rules
				if (CashCode.SingleOpenStatement)	
					CashLedgerStatement = CashCode.CashLedgerStatement + 1
					BeginningReconciliationStatementBalance = CashCode.LastEndingBalance
					invoke Update CashCode
						invoked.CashLedgerStatement = CashLedgerStatement
						invoked.LastUpdateDate = current timestamp

				
		Update is an Update Action
			Entrance Rules
				constraint (StatementStatus.Open)
					"BankStatementClosed_-InquiryOnly"	                       
				if (CashCode.SingleOpenStatement) 	
					constraint (BeginningReconciliationStatementBalance not changed)
						"SingleStatementUser_-CannotChangeBeginBal"              
			Action Rules
				if (ReceivablePaymentHeader entered)
					if (ReconciliationStatementDate != ReceivablePaymentHeader.DepositDate
					or CurrencyRate != ReceivablePaymentHeader.CurrencyRate)
						if (ReceivablePaymentHeader.Currency != Company.OrigCompany.Currency)  
							invoke CashLedgerCurrencyRateUpdate ReceivablePaymentsRel
								invoked.PrmCurrencyRate = CurrencyRate
					if (ReconciliationStatementDate != ReceivablePaymentHeader.GeneralLedgerDate)
						invoke Update ReceivablePaymentsRel
							invoked.GeneralLedgerDate = ReconciliationStatementDate
					
								
		Delete is a Delete Action
			Entrance Rules
				constraint (StatementStatus.Open)
					"BankStatementClosed_-InquiryOnly"	                       
				constraint (!CashCode.SingleOpenStatement)
					"SingleStatementUser_-CannotDeleteStatement"	             
				constraint(ReceivablePaymentsRel not exist)
					"CannotDelete;_\A\RPaymentRecordsExist"                   
			Action Rules
				if (ReceivablePaymentHeader entered)
					invoke Delete ReceivablePaymentHeader
		
		UpdateTotals is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmStatementBalanceEntered 	is an InternationalAmount
				PrmStatementEnteredDebit 	is an InternationalAmount
				PrmStatementEnteredCredit	is an InternationalAmount
			Action Rules
				StatementBalanceEntered = PrmStatementBalanceEntered	
				StatementEnteredDebit 	= PrmStatementEnteredDebit
				StatementEnteredCredit	= PrmStatementEnteredCredit
		
		UpdateReceipt is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmReceivableCompany	   is a ReceivableCompany
				PrmReceivableProcessLevel  is a ReceivableProcessLevel
				PrmReceivablePaymentHeader is a ReceivablePaymentHeader
			Action Rules
				ReceivableProcessLevel  = PrmReceivableProcessLevel
				ReceivablePaymentHeader = PrmReceivablePaymentHeader
							
	StateCycles
		CashLedgerStatementLifeCycle is a StateCycle
			state field is StatementStatus
			Open is a State
			Closed is a State
			
