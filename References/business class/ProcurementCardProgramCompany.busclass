ProcurementCardProgramCompany is a BusinessClass
    owned by po
    prefix is PGC
    classic name is PDPROGCOMP

    Ontology
        symbolic key is ProcurementCardProgramCompany
            classic set name is PGCSET1
            classic name for ProcurementCardProgram is PCARD-PROGRAM

    Patterns
		implements ContextualParent
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        AuthorityCode	   is a PayablesAuthorityCode
            classic name is AUTH-CODE
        ClearingAccount    is a FinanceCodeBlockFull
            classic name for ClearingAccount.AccountingUnit is CLG-ACCT-UNIT
            classic name for ClearingAccount.GeneralLedgerChartAccount is CLG-ACCOUNT
        TaxAccount         is a FinanceCodeBlockFull
            classic name for TaxAccount.AccountingUnit is TAX-ACCT-UNIT
            classic name for TaxAccount.GeneralLedgerChartAccount is TAX-ACCOUNT

    Transient Fields
    	TransientAccountingEntity    is an AccountingEntity
    		derive value from Company.AccountingEntity 
    
    Relations
        ProcurementCardRequestsRel 
            one-to-many relation to ProcurementCardRequest
            Field Mapping uses symbolic key
            	related.ProcurementGroup		= ProcurementGroup
                related.ProcurementCardProgram 	= ProcurementCardProgram

        ProcurementCardCompanyRel
            one-to-one relation to ProcurementCardCompany
            Field Mapping uses symbolic key
            	related.ProcurementGroup				= ProcurementGroup
                related.ProcurementCardProgram			= ProcurementCardProgram
                related.ProcurementCardRequest			= blank
	            related.Company							= Company
			
        ProcurementCardCompaniesRel
        	one-to-many relation to ProcurementCardCompany
        	Field Mapping uses Set2
        		related.ProcurementGroup				= ProcurementGroup
        		related.ProcurementCardProgram			= ProcurementCardProgram
        		related.Company							= Company 
        		
		ProcurementCardStatementTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses Set3
				related.ProcurementGroup							= ProcurementGroup
				related.ProcurementCardProgram                      = ProcurementCardProgram
				related.ProcurementCardStatementTransaction.Company	= Company
			Instance Selection
				where (!related.Status.Closed)	
				
		PayablesCompanyRel
			one-to-one relation to PayablesCompany
			Field Mapping uses symbolic key
				related.Company     		= Company
		
	Field Rules		
		Company
			constraint (Company.ProcurementGroup = ProcurementGroup)
				"ProcurementGroupAssociatedWithCompanyIsNotSameAsProcurementGroupAssociatedWithTheProcurementCardProgram;ProcurementGroupOfCompany<Company.ProcurementGroup>;ProcurementGroupOfProgram<ProcurementGroup>"                                                                               //"@e.PD01.209"

		ClearingAccount
			required
				"ClearingAccountIsRequired"

		TaxAccount
			required
				"TaxAccountIsRequired"

	Conditions	
		IsPdcardcompany
			restricted
			when (ProcurementCardCompaniesRel exists)
		
		HasDefaultProcessLevel
			restricted
			when (PayablesCompanyRel.HasDefaultProcessLevel)
		
	Actions	
        Create is a Create Action
        
		Update is an Update Action
		
		Delete is a Delete Action					
			Entrance Rules										
				constraint (ProcurementCardStatementTransactionsRel not exist)
					"CannotBeDeleted;CompanyAndProcurementCardProgramDoesNotExistInProcurementCardStatementTransactionInClosedState"                //"@e.PD01.208"						
													
				constraint (ProcurementCardCompaniesRel not exist)
				 	"CannotBeDeleted;ProcurementCardProgramAndCompanyExistsInProcurementCardCompany"                                                //"@e.PD01.204"				
