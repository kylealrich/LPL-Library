CustomerOrderReturn is a BusinessClass
    owned by oe
    prefix is RTN
    classic name is RETURNHDR

    Ontology
        symbolic key is CustomerOrderReturn
            classic set name is RTNSET1
            classic name is AUTH-NO

    Patterns
        implements StaticJava

        disable AuditIndex
        implements BODId

    Persistent Fields

        Date                    	is Date
        UserId                  	is a User
        Customer
        CustomerBillTo
            classic name is BILL-TO
        CustomerShipTo
            classic name is SHIP-TO
        Dropship					is Boolean
            classic name is DROPSHIP-FL
        InventoryLocation
            classic name is LOCATION
        PostingDate             	is Date
        CustomerReturnReference
            classic name is CUST-RET-REF
		CustomerRequestedAction 	is AlphaUpper size 1
	        States
	            Replacement value is "R"
	            CreditMemo  value is "C"	
        ReasonCode					is a OrderCancelCreditReason
        OriginalInvoice				is a BillingInvoice
        	classic name for OriginalInvoice.InvoicePrefix is ORIG-INVC-PRE
        	classic name for OriginalInvoice.InvoiceNumber is ORIG-INVC-NBR
		InvoicePrefix
			classic name is INVC-PREFIX
        InvoiceNumber
            classic name is INVC-NUMBER
		OrderNumber					is a CustomerOrder 
			classic name is ORDER-NBR					
        WarehouseShipment
            classic name is SHIPMENT-NBR
        BillingProcessLevel
            classic name is PROCESS-LEVEL
        BillingInvoiceType
            classic name is INVC-TYPE
        Currency
            classic name is CURRENCY-CODE
        Contact
        PhoneNumber      			is a TelephoneNumber 
        	holds pii
            classic name for PhoneNumber.InternationalPrefix is INT-PREFIX
            classic name for PhoneNumber.SubscriberNumber is PHONE-NMBR
            classic name for PhoneNumber.Extension is PHONE-EXT
        Status                  	is Numeric size 1
            States
                Entered  value is 0
                Released value is 1
                Closed   value is 9
			protected
		LastSequence				is a Sequence3
			classic name is LAST-SEQ
		LastCommentSequence			is a Sequence3
			classic name is LAST-COMM-SEQ
		LastLineNumber          	is a LineNumber
			classic name is LAST-LINE-NBR
        UndecidedItemCount      	is Numeric size 3
        	classic name is UND-ITEM-CNT
        	protected
		UndecidedCommentCount   	is Numeric size 4
			classic name is UND-CM-CNT
			protected
		CommentCount            	is Numeric size 3
			classic name is CM-CNT
			protected
		AutomaticCashApplication	is Boolean
			classic name is AUTO-APP-FL 
		CreateCreditMemo			is Boolean
			classic name is CREATE-CM-CD
		BillingElectronicPaymentType
			classic name for BillingElectronicPaymentType is EP-TYPE
		BillingCustomerCreditCard
			classic name for BillingCustomerCreditCard is EP-ACCOUNT
		ExpirationDate				is Date
			classic name is EP-EXP-DATE
		FreightCode					is a BillingFreightCode
			classic name is FREIGHT-CDE
		FreightCharge 				is an InternationalAmount
			classic name is FRT-CHARGE
		LastAddOnChargeSequence		is a Sequence3	 
			classic name is LAST-MISC-SEQ			
        LineTotal               	is an InternationalAmount
			protected
        GlobalDocumentType
            classic name is GLBL-DOC-TYPE
        FreightTotal					 is an InternationalAmount
            classic name is FREIGHT-TOT
            protected
        InsuranceTotal					 is an InternationalAmount
            classic name is INSURANCE-TOT
            protected
        CustomsTotal					 is an InternationalAmount
            classic name is CSTMS-TOT
            protected
        OtherAddOnChargesTotal			 is an InternationalAmount
            classic name is OTHER-AOC-TOT            
            protected
        CompanyCustomer 
        TemporaryHold					 is AlphaUpper size 1
			classic name is TEMP-HOLD
            States
                ApprovedByEngine value is "A"
                    default label is "Approved by Engine"
                SentForApproval  value is "S"
                    default label is "Sent for Approval"
				RejectedByEngine value is "R"
					default label is "Rejected by Engine"
	
	Local Fields
		LocalInventoryTransaction   is an InventoryTransaction
		NewInventoryTransaction		is an InventoryTransaction view
		NewBillingInvoice			is a BillingInvoice view	
		LocalBillingInvoice			is a BillingInvoice
		LocalOrderReturn			is a CustomerOrderReturn
		LocalInvoiceLine			is like BillingInvoiceLine
		LocalActionCode				is an ActionCode
		LocalBODCurrentTimeStamp	is a BODCurrentTimeStamp

		LocalDiscountsCalculated	is Boolean
		LocalDiscountableAmount		is like InternationalAmount
		LoopCounter					is Numeric 2
		LocalCalculatedDiscount		is like InternationalAmount
		LocalDollarLimit			is Numeric 11
		LocalLineTotal				is like InternationalAmount


		LocalInvoiceNumber 			is Alpha size 30
		LocalNativeLPLBODTrigger	is Boolean

		LocalOrderEntryAddOnCharge	is like OrderEntryAddOnCharge
		NewBODTracker  				is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker	is Numeric 15
		Error            			is Boolean
	    ErrorMessage     			is Alpha 300
		LocalConfigurationParameter	is Alpha size up to 200
		
	Context Fields
		FSMInboundBODTracker
	
	Derived Fields
		SentToEngineCompletionMessage 	is a DerivedField
			type is Alpha 50
			if (TaxEntityRel.ThirdParty.TaxEngine
			and TemporaryHold.SentForApproval)
				return "Order return sent to tax engine for approval"

		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforCustomerReturn"
			
		ContextMessageText is a MessageField
			restricted
			"ReturnFromCustomer<Customer>WithOrderNumber<OrderNumber>"
		
		DerivedNationalCompany is a DerivedField
			type is like Company
			restricted
			if (CompanyCustomer.NationalAccountIndicator.No)
				return Company
			else
			if (CompanyCustomer.NationalAccountIndicator.Child)
				return CompanyCustomer.NationalCompany
			else
				return CompanyCustomer.Company

		DerivedNationalCustomer is a DerivedField
			type is like Customer
			restricted
			if (CompanyCustomer.NationalAccountIndicator.No)
				return Customer
			else
			if (CompanyCustomer.NationalAccountIndicator.Child)
				return NationalAccountInvoiceRel.NationalAccount.NationalAccountGroup.NatCustomer
			else
				return CompanyCustomer.Customer

		DerivedTenantID is a DerivedField 
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
				
		CustomerOrderReturnDescription is a DerivedField
			type is Alpha 100
			restricted
			return "Return authorization "+ InvoiceNumber + " has been created"
					
		DerivedBODID is a DerivedField
			type is Alpha 200
			restricted 
			return	"infor-nid:" + DerivedTenantID +":"+ Company.FinanceEnterpriseGroup +":"+ CustomerOrderReturn +":"+ "?CustomerOrderReturn&verb=Process"
			
		CustomerOrderReturnAlertXML is a DerivedField
			type is XMLDocument
			restricted
			CustomerOrderReturnAlertXML = template.IONCustomerOrderReturn_CustomerOrderReturn_ST document for this instance



		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedAppProdline is a DerivedField 
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value



		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODCurrentTimeStamp = current timestamp
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"			
    
    	DerivedBODVariationID is a DerivedField
			type is Alpha size 60
			restricted
			return bod id.VariationID
			
    	DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha size 4
			restricted
			return Company.FinanceEnterpriseGroup
    	
    	DerivedCompany is a DerivedField
			type is Alpha size 10
			restricted
			return Company using "%d"
			
    	DerivedDocumentID is a DerivedField
			type is Alpha 100
			restricted
			return DerivedCompany + DerivedDelimiter + CustomerOrderReturn
    	
    	DerivedAccountingEntity is a DerivedField
			type is Alpha size 200
			restricted
			return DerivedFinanceEnterpriseGroup + DerivedDelimiter +  Company.AccountingEntity
    	
    	DerivedBODId is a DerivedField
			type is Alpha 200
			restricted 
			return "infor-nid:IEFIN" + DerivedAppProdline + ":" + DerivedAccountingEntity + ":" + DerivedDocumentID  + ":" + "?CustomerReturn&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
    	
    	DerivedCustOrderReturnStatus is a DerivedField
			type is Alpha size 20
			restricted
			if (Status.Released)
				return "Open" 
			else
			if (Status.Closed)
				return "Received"
    
    	DerivedActionCode is a DerivedField
			type is Alpha size 10
			restricted
			if (LocalActionCode = "C" or LocalActionCode.Create)
				return "Add"
			if (LocalActionCode = "U" or LocalActionCode.Update)
				return "Replace"
			if (LocalActionCode = "D" or LocalActionCode.Delete)
				return "Delete"
    	
    	DerivedLocaion is a DerivedField
    		type is Alpha size 100
    		restricted
    		return DerivedCompany + DerivedDelimiter + InventoryLocation
    		
    	DerivedLastMDDate is a DerivedField
			type is Alpha size 100
			restricted
			DerivedLastMDDate = update stamp.timestamp
			return DerivedLastMDDate[1:4] + "-" + DerivedLastMDDate[5:6] + "-" + DerivedLastMDDate[7:8] + "T" + DerivedLastMDDate[9:10] + ":" + DerivedLastMDDate[11:12] + ":" + DerivedLastMDDate[13:14] + "Z"
    	
    	DerivedUpdateActor is a DerivedField
			type is Alpha size 30
			restricted
			return update stamp.actor
    	
    	DerivedCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted	
			return LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
			
		DerivedDocID is a DerivedField
			type is Alpha size 100
			restricted
			return DerivedCompany + DerivedDelimiter + OrderNumber
		
		DerivedCustomerPartyID is a DerivedField
			type is Alpha size 100
			restricted
			return DerivedCompany + DerivedDelimiter + Customer
		
		DerivedCustReturnExtendedAmount is a DerivedField
			type is Numeric size 20
			restricted
			DerivedCustReturnExtendedAmount = 0
			for each CustomerOrderReturnLinesRel
				DerivedCustReturnExtendedAmount = DerivedCustReturnExtendedAmount + each.DerivedExtendedAmount
			return DerivedCustReturnExtendedAmount
		
		DerivedOriginalInvoiceNumber is a DerivedField
			type is Alpha size 20
			restricted
			return OriginalInvoice.InvoiceNumber 
		
		DerivedOriginalInvNumber is a DerivedField
			type is Alpha size 30
			restricted
			if (DerivedOriginalInvoiceNumber size = 8)
				LocalInvoiceNumber = DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 7)
				LocalInvoiceNumber = "0" + DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 6)
				LocalInvoiceNumber = "00" + DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 5)
				LocalInvoiceNumber = "000" + DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 4)
				LocalInvoiceNumber = "0000" + DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 3)
				LocalInvoiceNumber = "00000" + DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 2)
				LocalInvoiceNumber = "000000" + DerivedOriginalInvoiceNumber
			else
			if (DerivedOriginalInvoiceNumber size = 1)
				LocalInvoiceNumber = "0000000" + DerivedOriginalInvoiceNumber
			else
				LocalInvoiceNumber = DerivedOriginalInvoiceNumber
			DerivedOriginalInvNumber = DerivedCompany + DerivedDelimiter + OriginalInvoice.InvoicePrefix + LocalInvoiceNumber
			return DerivedOriginalInvNumber
			
		DerivedInv is a DerivedField
			type is Alpha size 25
			restricted
			return InvoiceNumber

		DerivedInvoice is a DerivedField
			type is Alpha size 100
			restricted
			if (DerivedInv size = 8)
				LocalInvoiceNumber = DerivedInv
			else
			if (DerivedInv size = 7)
				LocalInvoiceNumber = "0" + DerivedInv
			else
			if (DerivedInv size = 6)
				LocalInvoiceNumber = "00" + DerivedInv
			else
			if (DerivedInv size = 5)
				LocalInvoiceNumber = "000" + DerivedInv
			else
			if (DerivedInv size = 4)
				LocalInvoiceNumber = "0000" + DerivedInv
			else
			if (DerivedInv size = 3)
				LocalInvoiceNumber = "00000" + DerivedInv
			else
			if (DerivedInv size = 2)
				LocalInvoiceNumber = "000000" + DerivedInv
			else
			if (DerivedInv size = 1)
				LocalInvoiceNumber = "0000000" + DerivedInv
			else
				LocalInvoiceNumber = DerivedInv	
			if(InvoiceNumber entered)
				DerivedInvoice = DerivedCompany + DerivedDelimiter + InvoicePrefix + LocalInvoiceNumber
				return DerivedInvoice
				
		CustomerReturnLineXML is a DerivedField
			type is XMLDocument
			restricted
			if (CustomerOrderReturnLinesRel exist)
				for each CustomerOrderReturnLinesRel
					each.LocalCurrency 			= Currency
					each.LocalAccountingEntity 	= DerivedAccountingEntity
					each.LocalDelimiter			= DerivedDelimiter
					each.LocalDocumentID 		= DerivedDocumentID
					each.LocalDocID				= DerivedDocID
					each.LocalLogicalID			= DerivedLogicalID
					CustomerReturnLineXML += template.IONSyncCustomerReturn_CustomerOrderReturnLine_ST document for each
				return CustomerReturnLineXML
			else
				return ""
		
		SyncCustomerReturnXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			SyncCustomerReturnXMLBOD = template.IONSyncCustomerReturn_CustomerOrderReturn_ST document for this instance
			return SyncCustomerReturnXMLBOD		
		
		CustomerOrderReturnFormTitle is a DerivedField
			type is Alpha size up to 80
			if (RecordExists)
				return CustomerOrderReturnTitle
			else
				return CustomerOrderReturnTitleNoRecord
		
		CustomerOrderReturnTitleNoRecord is a LabelField
			restricted
			"CreateOrderReturn"
		
		CustomerOrderReturnTitle is a LabelField
			restricted
			"OrderReturn_<CustomerOrderReturn>"
		
		DerivedCreditMemoNumber	is a DerivedField		
			type is AlphaUpper 22		
			if (Company.ManualInvoiceNumbering
			and IsInvoiceCreated)
				return BillingInvoiceRel.GlobalInvoiceNumber
			else
				return InvoiceNumber
						
	Transient Fields
		CompletionMessage	is Alpha 150
			derive value from SentToEngineCompletionMessage

    Conditions

        IsBilltoEntered
        	restricted
            when (CustomerBillTo entered)

        IsClosed
        	restricted
            when (Status.Closed)

        IsCreditMemo
        	restricted
            when (InvoicePrefix entered
            or    InvoiceNumber entered)

        IsCurrencyCode
        	restricted
            when (Currency entered)

        IsEntered
        	restricted
            when (Status.Entered)

        IsInvoiceCreated
        	restricted
            when (InvoicePrefix entered
            or    InvoiceNumber entered)

        IsReasonEntered
        	restricted
            when (ReasonCode entered)

        IsReleased
        	restricted
            when (Status.Released)

        IsShiptoEntered
        	restricted
            when (CustomerShipTo entered)
        
        IsInvoicePrinted
        	restricted
        	when (OriginalInvoice.Status.Printed
        	or    OriginalInvoice.Status.ARGLUpdated)
        	
    	IsReleasedAndInvoiceCreated
    		restricted
    		when (IsReleased
    		and   IsInvoiceCreated)

		DisplayOnlyForLoggedInUser
			restricted

			when  ((actor.agent(CustomerRepresentativeContact).IsActiveNationalAccountParent	
			and		DerivedNationalCompany = actor.agent(CustomerRepresentativeContact).CustomerRepresentative.ReceivableCompany
			and		DerivedNationalCustomer = actor.agent(CustomerRepresentativeContact).CustomerRepresentative.Customer)

			or		(actor.agent(CustomerRepresentativeContact).IsActiveInvoiceCustomer	
			and		Company = actor.agent(CustomerRepresentativeContact).CustomerRepresentative.ReceivableCompany
			and		Customer = actor.agent(CustomerRepresentativeContact).CustomerRepresentative.Customer))

		IsValidElectronicPaymentAccount
			restricted 
			when ((BillingCustomerCreditCardListRel exists
			and    BillingCustomerCreditCard exists)
			or     BillingCustomerCreditCardListRel not exists)
		
		RecordExists
			restricted
			when (CustomerOrderReturn exists)

		IsOnHold
			restricted
			when (TemporaryHold.SentForApproval)
	
	Rule Blocks
		CalculateDiscounts							
			if (!LocalDiscountsCalculated)
				invoke CalculateDiscounts CustomerOrderReturnLinesRel
				invoke CalculateDiscounts CustomerOrderReturnLineAddOnChargesRel
					invoked.PrmLineTotal = LocalLineTotal
				
				initialize LoopCounter
				while (LoopCounter < 6)
					LoopCounter +=1
					if (OrderNumber.OrderDiscountCode.DollarLimits.DollarLimit[LoopCounter] entered)
						if (LocalDiscountableAmount <= OrderNumber.OrderDiscountCode.DollarLimits.DollarLimit[LoopCounter])							
							end while

				LocalCalculatedDiscount	= LocalDiscountableAmount * OrderNumber.OrderDiscountCode.Percents.Pct[LoopCounter]
				LocalDiscountsCalculated = true

    Relations
		TaxEngineTaxDetailRel
            one-to-many relation to TaxEngineTaxDetail
            Field Mapping uses ByCompanyOrder
				related.Company					= Company
                related.OrderNbr 				= OrderNumber
			Instance Selection
				where (related.PONumber			= CustomerOrderReturn)		

    	InventoryCompanyRel
    		one-to-one relation to InventoryCompany
    		Field Mapping uses symbolic key
    			related.Company 					 = Company

		
		CustomerOrderReturnLinesRel is a CustomerOrderReturnLine set
			Instance Selection
				where (related.CustomerOrderReturn	= CustomerOrderReturn) 

		CustomerOrderReturnAttachmentRel is a CustomerOrderReturnAttachment set
			Instance Selection	
				where (related.CustomerOrderReturn  	= CustomerOrderReturn)
        
		CustomerOrderReturnLineAddOnChargesRel
            one-to-many relation to CustomerOrderReturnLineAddOnCharges
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.CustomerOrderReturn     = CustomerOrderReturn
                related.CustomerOrderReturnLine = blank
		
		BillingInvoiceRel
            one-to-one relation to BillingInvoice
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingInvoice.InvoicePrefix = InvoicePrefix
                related.BillingInvoice.InvoiceNumber = InvoiceNumber
        
        NewBillingInvoiceLinesRel
            one-to-many relation to BillingInvoiceLine
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingInvoice.InvoicePrefix = InvoicePrefix
                related.BillingInvoice.InvoiceNumber = InvoiceNumber        

		BillingInvoicePurgeRel
        	one-to-many relation to BillingInvoice
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingInvoice.InvoicePrefix = InvoicePrefix
                related.BillingInvoice.InvoiceNumber = InvoiceNumber
            Instance Selection
				where (related.Status = 9)	
				
		InventoryTransactionLinesRel
			one-to-many relation to InventoryTransactionLine
			Field Mapping uses symbolic key
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.InventoryTransaction	= LocalInventoryTransaction
				related.TransactionSystemCode	= "OE"
			Instance Selection
				where (related.InventoryTransaction.InventoryDocumentType.CustomerReturn)
		
		InventoryDispositionReturnLinesRel is a CustomerOrderReturnLine set
			Instance Selection
				where (related.InventoryDisposition.ReturnToStock
	            or     related.InventoryDisposition.Scrap)
		
		BillingInvoiceLinesRel
			one-to-many relation to BillingInvoiceLine
			Field Mapping uses symbolic key
				related.Company 				= Company
				related.BillingInvoice			= LocalBillingInvoice

		BillingInvoiceLineDetailsRel
			one-to-many relation to BillingInvoiceLineLotSerialBin
			Field Mapping uses symbolic key
				related.Company 				= Company
				related.BillingInvoice			= LocalBillingInvoice		
				related.BillingInvoiceLine      = LocalInvoiceLine		
		
		CustomerOrderLinesRel
			one-to-many relation to CustomerOrderLine
			Field Mapping uses symbolic key
				related.Company					= Company
				related.CustomerOrder			= OrderNumber
		
		CustomerOrderAddOnChargesRel				
			one-to-many relation to CustomerOrderAddOnCharge
			Field Mapping uses symbolic key
				related.Company 				= Company
				related.CustomerOrder			= OrderNumber
			Instance Selection
            	where (related.CustomerOrderLine not entered) 
		
		CustomerOrderLineAddOnChargesRel				
			one-to-many relation to CustomerOrderAddOnCharge
			Field Mapping uses symbolic key
				related.Company 				= Company
				related.CustomerOrder			= OrderNumber	
				related.CustomerOrderLine		= LocalInvoiceLine	
		
		NationalAccountInvoiceRel
		    one-to-many relation to NationalAccount
		    Field Mapping uses Set2
		        related.NationalAccount.InvoiceAccountGroup.Company      = Company
		        related.NationalAccount.InvoiceAccountGroup.Customer     = Customer

		BillingCustomerCreditCardListRel
			one-to-many relation to BillingCustomerCreditCard
			Field Mapping uses symbolic key
				related.Company							= Company
				related.Customer						= Customer
				related.BillingElectronicPaymentType 	= BillingElectronicPaymentType				
    
    	OtherInvoicedReturnsRel
    		one-to-many relation to CustomerOrderReturn
    		Field Mapping uses symbolic key
    			related.Company = Company
    		Instance Selection
    			where (not related.CustomerOrderReturn = CustomerOrderReturn
    			and related.OrderNumber = OrderNumber
    			and related.IsInvoiceCreated)

		BillingInvoiceAddOnChargesRel			
			one-to-many relation to BillingInvoiceAddOnCharge
			Field Mapping uses symbolic key
				related.Company					= Company
				related.BillingInvoice			= OriginalInvoice
			Instance Selection
				where (related.BillingInvoiceLine not entered)

		CustomerOrderReturnAddOnChargesRel
			one-to-many relation to CustomerOrderReturnLineAddOnCharges
			Field Mapping uses symbolic key
				related.Company                 					= Company
			Instance Selection
				where	(not related.CustomerOrderReturn			= CustomerOrderReturn
				and		related.CustomerOrderReturnLine not entered
				and		related.CustomerOrderReturn.OrderNumber		= CustomerOrderReturn.OrderNumber
				and		related.Miscellaneous						= LocalOrderEntryAddOnCharge)

		TaxEntityRel
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.TaxEntity 				= Company.AccountingEntity
        
		CustomerOrderReturnDistributionsRel
			one-to-many relation to CustomerOrderReturnDistribution
			Field Mapping uses symbolic key
				related.Company					= Company
				related.CustomerOrderReturn		= CustomerOrderReturn


		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationPARel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "PulseAlert"
            	related.FSMBODConfiguration.Direction 	= 1
        
        FSMBODConfigurationDetailPARel
        	one-to-many relation to FSMBODConfigurationDetail
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 			= 2	
            	related.FSMBODConfiguration.Noun 			= "PulseAlert"
            	related.FSMBODConfiguration.Direction 		= 1
            Instance Selection
				where (related.Alert						= "FSM_ION_CustomerOrderReturnAlert"
				and	   related.Enable)		
		
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
		


		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CustomerReturn"
            	related.FSMBODConfiguration.Direction 	= 1

		        
    Sets

        Set2
            indexed
            Instance Selection
                where (IsInvoiceCreated)
            Sort Order
                Company
                InvoicePrefix
                InvoiceNumber

        Set3
            indexed
            Sort Order
                Company
                InventoryLocation
                Status
                CustomerOrderReturn

		ByCompanyCustomer
			indexed
            Sort Order
                Company
                Customer
                CustomerOrderReturn
                
    Field Rules
    	Company 
    		constraint (InventoryCompanyRel exists)
    			"InventoryCompanyDoesNotExist"    //OERTN.101"
    	
    	UserId
    		default to actor
    		
    	Date
    		default to current corporate date

        Customer
        	if (OriginalInvoice entered)
        		default to OriginalInvoice.Customer
        		
        		constraint (Customer = OriginalInvoice.Customer)
        			"CustomerForReturnMustEqualInvoiceCustomer" 			//"OERTN.125"
            required
            
            if (OrderNumber entered)

            	constraint (Customer = OrderNumber.Customer)
            		"ReturnCustomerMustBeSameAsOrderCustomer"				//"OERTN.234" 
			constraint (CompanyCustomer exists)
				"CompanyCustomerDoesNotExist"

        BillingInvoiceType
            required
       		
       		constraint (BillingInvoiceType.Returns)
       			"InvoiceTypeDoesNotAllowReturnProcessing"			//"OERTN.127"
       			
       		constraint (BillingInvoiceType.Credit)
       			"InvoiceTypeDoesNotAllowCreditProcessing"           
        
        InventoryLocation
			if (OriginalInvoice entered)
				default to OriginalInvoice.Location
            required

        BillingProcessLevel
        	if (OriginalInvoice entered)
        		default to OriginalInvoice.BillingProcessLevel        
            required
        
        OriginalInvoice
        	constraint (OriginalInvoice.BillingInvoiceSource.Shipment)
        		"CannotReturn;InvoiceNotCreatedByOrderEntry"  //"OERTN.135"
        	
        	constraint (IsInvoicePrinted)
        		"InvoiceIsNotPrinted" 						// OERTN.134"
        	
        	if (OrderNumber entered
        	and WarehouseShipment entered)
        		OriginalInvoice.InvoicePrefix = WarehouseShipment.InvoicePrefix
        		OriginalInvoice.InvoiceNumber = WarehouseShipment.InvoiceNumber
        	
       	FreightCode
       		if (action type.Create and OriginalInvoice entered)
    			default to OriginalInvoice.FreightCode
	           
       		if (FreightCharge entered)
       			required 
       				"FreightChargeNotAllowedWhenFreightCodeIsBlank"
					
       	FreightCharge 
       		if (action type.Create and OriginalInvoice entered )
           		default to OriginalInvoice.FreightCharge.TransactionAmount
           			
        	if (FreightCode entered)
        		required
        			"FreightChargeIsRequiredWhenFreightCodeIsEntered"
					
        OrderNumber
        	if (OriginalInvoice entered)
        		default to OriginalInvoice.CustomerOrder
        		
        		constraint (OrderNumber = OriginalInvoice.CustomerOrder)
        			"OrderDoesNotBelongToInvoiceEntered"			//"OERTN.137"
        
        WarehouseShipment	
        	if (OriginalInvoice entered)
        		default to OriginalInvoice.WarehouseShipment
        		
        		constraint (WarehouseShipment = OriginalInvoice.WarehouseShipment)
	        		"ShipmentDoesNotBelongToInvoiceEntered" 		//OERTN.137"

			if (OrderNumber entered
			and WarehouseShipment entered)
				if (OriginalInvoice not entered)
					constraint (WarehouseShipment.InvoicePrefix entered or WarehouseShipment.InvoiceNumber entered)
		        		"ShipmentMustBeInvoicedBeforeReturning"		//OERTN.136"

        CustomerShipTo
        	default to OriginalInvoice.CustomerShipTo
       		
       		if (OriginalInvoice entered)
       			constraint (CustomerBillTo = OriginalInvoice.BillTo)
       				"ShipToForReturnMustBeSameAsInvoiceShipTo" 	//OERTN.129"
       				         	
       	CustomerBillTo
       		default to OriginalInvoice.BillTo
       		
       		if (OriginalInvoice entered)
       			constraint (CustomerBillTo = OriginalInvoice.BillTo)
       				"BillToForReturnMustBeSameAsInvoiceBillTo"    

       	Currency
       		if (OriginalInvoice entered)

       			default to OriginalInvoice.Currency	
       			
       			constraint (Currency = OriginalInvoice.Currency)
       				"InvalidCurrencyForCustomer<Customer>"//"OE30.234"
       		
       		constraint (Currency = CompanyCustomer.Currency)
       			"InvalidCurrencyForCustomer<Customer>"//"OE30.234"
      			 	
       	ReasonCode
       		if (CreateCreditMemo)
       			required    
    			constraint (ReasonCode.Billing)
    				"ReasonCodeIsNotValidForBilling"				
				
		BillingElectronicPaymentType
			if (BillingCustomerCreditCard entered
			or  ExpirationDate entered)
				required						
					"ElectronicPaymentTypeIsRequired"		
			else
				cannot be entered       		

		BillingCustomerCreditCard
			if (BillingElectronicPaymentType entered)
				constraint (IsValidElectronicPaymentAccount)
					"AccountDoesNotExist"
				required

		ExpirationDate
			constraint (ExpirationDate >= current corporate date)
				"ElectronicPaymentHasExpired" 	//OERTN.163"
    
       	
	Actions
		CreateFromBatch is a Create Action
			restricted	
			
		ReleaseFromBatch is an Instance Action
			restricted
			Action Rules
				if (CustomerOrderReturnLinesRel exists)						
				    Status = Status.Released

		CalculateDiscountableAmount is an Instance Action				
			restricted
			Parameters
				PrmDiscountableAmount		is an InternationalAmount
				PrmLineTotal				is an InternationalAmount
			Action Rules
				LocalDiscountableAmount		+= PrmDiscountableAmount
				LocalLineTotal				+= PrmLineTotal

		UpdateOrderReturn is an Instance Action
			restricted
			Parameters
				PrmUndecidedItemCount 		is Numeric 3
				PrmUndecidedCommentCount	is Numeric 3
				PrmCommentCount				is Numeric 3
				PrmLineTotal				is an InternationalAmount
			Parameter Rules
				PrmUndecidedCommentCount
					default to PrmCommentCount * -1
			
				
			Action Rules
				UndecidedItemCount			+= PrmUndecidedItemCount
				if (UndecidedItemCount < 0)
					UndecidedItemCount		= 0
				
				UndecidedCommentCount		+= PrmUndecidedCommentCount
				if (UndecidedCommentCount < 0)
					UndecidedCommentCount	= 0
				
				CommentCount		+= PrmCommentCount
				if (CommentCount < 0)
					CommentCount	= 0
				LineTotal 				+= PrmLineTotal

		TriggerCustomerOrderReturn is an Instance Action
			restricted
			Parameters
			Action Rules
				if (Company.FinanceEnterpriseGroup.BODTrigger and FSMBODConfigurationPARel.Enable)
      				if(FSMBODConfigurationDetailPARel.Enable)	
						invoke TriggerPulseAlert FSMBODConfigurationPARel
							invoked.PrmActorGroup 	= "ORDERPROCESSINGMANAGER" 
							invoked.PrmMainXML 		= CustomerOrderReturnAlertXML	
							invoked.PrmDescription	= CustomerOrderReturnDescription
							invoked.PrmBODID		= DerivedBODID		


		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation		  is Alpha size 22
					default label is "Location"
				PrmDocumentID		is Alpha size 100
					default label is "DocumentID"
			Action Rules
				if (bod id.AccountingEntity	 != PrmAccountingEntity)
					bod id.AccountingEntity	 = PrmAccountingEntity
				if (bod id.Location			 != PrmLocation)
					bod id.Location			 = PrmLocation
				if (bod id.DocumentID		 != PrmDocumentID)
					bod id.DocumentID		 = PrmDocumentID


					
		TriggerCustomerReturn is an Instance Action
			restricted
			default label is untranslatable
			Action Rules
				if (Company.FinanceEnterpriseGroup.BODTrigger)
					if(action != "UpdateBODIdFields") 
						LocalActionCode = LocalActionCode.Create
						increment bod id.VariationID
						trigger "CustomerReturnService" PA service
							resume on error
							title is "EG:<Company.FinanceEnterpriseGroup>CO:<Company>"
							Criteria
								Company.FinanceEnterpriseGroup
							    Company	
							Variables
								include persistent fields from CustomerOrderReturn
								bod id.VariationID
									variable name is BODVariationId
								Company.AccountingEntity
									variable name is AccountingEntity
								LocalActionCode
									variable name is ActionCode
								Company.FinanceEnterpriseGroup
									variable name is FinanceEnterpriseGroup	
								LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
									variable name is CurrentTimeStamp


		SendCustomerReturnBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is SyncCustomerReturnXMLBOD
					bod type is "Sync.CustomerReturn"
					document id is  DerivedDocumentID
					variation id is DerivedBODVariationID
					accounting entity is DerivedAccountingEntity
					location is InventoryLocation	
			
		
		TriggerCustomerReturnNativeLPLBOD is an Instance Action
			restricted
			default label is untranslatable
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CustomerReturn"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "CustomerOrderReturn"
					invoked.PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
					invoked.PrmCompany					= Company
					invoked.PrmMainUserTemplate 		= "IONSyncCustomerReturn_CustomerOrderReturn_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				if(Company.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CustomerReturn"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Direction				= 1
							invoked.Reference1				= Company
							invoked.Reference2				= CustomerOrderReturn
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.Reference1				= Company
							invoked.Reference2				= CustomerOrderReturn
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendCustomerReturnBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODID
							invoked.BODXML								= SyncCustomerReturnXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODID
							invoked.BODXML									= SyncCustomerReturnXMLBOD
					



		TriggerBRCustomerReturn is an Instance Action
			restricted
			default label is untranslatable
			Action Rules
				if (Company.FinanceEnterpriseGroup.BODTrigger)
					if (action != "UpdateBODIdFields") 
						LocalActionCode = LocalActionCode.Update
						increment bod id.VariationID
						trigger "BRCustomerOrderReturnService" PA service
							resume on error
							title is "EG:<Company.FinanceEnterpriseGroup>CO:<Company>COR:<CustomerOrderReturn>"
							Criteria
								Company.FinanceEnterpriseGroup
								Company
							Variables
								include persistent fields from CustomerOrderReturn 
								LocalActionCode
									variable name is ActionCode
								bod id.VariationID
									variable name is BODVariationId
								LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
									variable name is CurrentTimeStamp										
								Company
									variable name is LawsonCompany
								Company.GeneralLedgerCompany.AccountingEntity										
									variable name is Accountingentity
									

		TaxEngineApprove is an Instance Action
			restricted
			Action Rules
				TemporaryHold = TemporaryHold.ApprovedByEngine
				for each CustomerOrderReturnLinesRel	
					invoke TaxEngineApprove each

		TaxEngineReject is an Instance Action
			restricted
			Action Rules
				TemporaryHold = TemporaryHold.RejectedByEngine
				for each CustomerOrderReturnLinesRel	
					invoke TaxEngineReject each


		OrderEntryReturnsPurge	is a Set Action

			restricted
			Parameters
		    	PrmCompany                 is a BillingCompany
		    		default label is "Company"
        		PrmCutoffDate              is Date
					default label is "CutoffDate"
					
		    Parameter Rules
		    	PrmCompany
		    		required
					constraint (PrmCompany.FinanceEnterpriseGroup.PurgeCutOffDate entered)
		            	"PurgeCutOffDateMustBeSetFor_Finance_Enterprise_Group<PrmCompany.FinanceEnterpriseGroup>"
		    		
		    	PrmCutoffDate
		    		required
					initial value is PrmCompany.FinanceEnterpriseGroup.PurgeCutOffDate
					default to PrmCompany.FinanceEnterpriseGroup.PurgeCutOffDate
		    		
		   	Local Fields 
				RecordCount				is Numeric 10
				LocalActor              is an Actor
		       	
			Instance Selection
				include deleted records
				where (PrmCompany     = Company
				and    Status.Closed
				and    PrmCutoffDate  >= Date)
			
			
			Action Rules
				
				Empty Set Rules 
					LocalActor = actor
					
					send notification
						to LocalActor
						description is "NoCustomerOrderReturnRecordsFoundToPurge"
						priority is high
						detail is "OrderEntryReturnsPurgeHasBeenCompletedfor<RecordCount>records"
				Set Rules 
					Entrance Rules 
						initialize RecordCount
					Exit Rules 
						LocalActor = actor
					
						send notification
							to LocalActor
							description is "OrderEntryReturnsPurgeHasBeenCompleted"
							priority is high	
							detail is "OrderEntryReturnsPurgeHasBeenCompletedFor<RecordCount>records"
				
				Instance Rules
					if (InvoicePrefix  entered) 
						invoke Purge BillingInvoicePurgeRel
					invoke Purge CustomerOrderReturnDistributionsRel
					invoke Purge CustomerOrderReturnLineAddOnChargesRel
					invoke Purge CustomerOrderReturnLinesRel
					invoke Purge CustomerOrderReturnAttachmentRel
					increment RecordCount
					invoke Purge
					
		Purge is a Purge Action
			restricted
						
		PutOnHold is an Instance Action
			restricted
			Action Rules
				TemporaryHold = TemporaryHold.SentForApproval
				invoke PutOnHold CustomerOrderReturnLinesRel

	StateCycles
		CustomerReturnProcessing is a StateCycle
			state field is Status
			Entered is a State	
				Create is a Create Action
					Entrance Rules
					Exit Rules
						if (BillingInvoiceAddOnChargesRel exists)
							for each BillingInvoiceAddOnChargesRel
								LocalOrderEntryAddOnCharge = each.Miscellaneous
								if (CustomerOrderReturnAddOnChargesRel not exists)
									invoke Create CustomerOrderReturnLineAddOnCharges
									
										invoked.Company								= Company
										invoked.CustomerOrderReturn					= CustomerOrderReturn
										invoked.Miscellaneous						= each.Miscellaneous
										invoked.ChargeType 							= each.ChargeType
								        invoked.OrderDiscount 						= each.OrderDiscount
								        invoked.TrackType 							= each.TrackType
								        invoked.SalesAccount 						= each.SalesAccount
								        invoked.OffsetAccount 						= each.OffsetAccount
								        invoked.CostOfGoodsSoldAccount 				= each.COGSAccount				
								        if (each.ChargeType.FlatAmount)
								        	invoked.EnteredPrice 	 = each.EnteredPrice.TransactionAmount
							        		invoked.Cost	   	     = each.UnitCost	
								        if (each.ChargeType.UnitPrice)
								        	invoked.EnteredUnitPrice = each.EnteredUnitPrice			
								        	invoked.UnitCost		 = each.UnitCost
								        if (each.ChargeType.Percentage)
								        	invoked.PricePercent 	 = each.PricePercent
								        	invoked.CostPercent 	 = each.CostPercent
										
				CopyReturn is a Create Action
					Local Fields
						LocalQuantity				is like Quantity
					Parameters
						PrmBillingCompany 			is a BillingCompany
						PrmCustomerOrderReturn		is a CustomerOrderReturn
						PrmInventoryLocation		is an InventoryLocation
						PrmBillingInvoice			is a BillingInvoice
						PrmCustomerOrder			is a CustomerOrder
						PrmShipment					is a WarehouseShipment
						PrmInvoiceType				is a BillingInvoiceType
						PrmCreateCreditMemo			is Boolean
						PrmReasonCode				is a OrderCancelCreditReason
						PrmCopyAddOnCharge			is Boolean
						PrmCopyFreightCharge		is Boolean
						PrmInventoryDisposition     is AlphaUpper size 1
							States
								ReturnToStock value is "R"
					            Scrap         value is "S"
					            Ignore        value is "I"
					            Undecided     value is blank
					
					Parameter Rules
						PrmBillingCompany
							default to PrmBillingInvoice.Company
							default to PrmCustomerOrder.Company
							default to PrmShipment.Company
						
						PrmBillingInvoice
							if (PrmCustomerOrder not entered
							and PrmShipment not entered)
								required
							else
								if (PrmShipment entered)
									if (PrmBillingInvoice not entered)
										PrmBillingInvoice.InvoicePrefix = PrmShipment.InvoicePrefix
										PrmBillingInvoice.InvoiceNumber = PrmShipment.InvoiceNumber
										 
									constraint (PrmBillingInvoice.InvoicePrefix = PrmShipment.InvoicePrefix
									and  		PrmBillingInvoice.InvoiceNumber = PrmShipment.InvoiceNumber)
										"InvoiceDoesNotExistForShipmentOrOrder" 		//OE30.908"
							
							constraint (!PrmBillingInvoice.BillingInvoiceSource.Manual)
								"CannotReturn;InvoiceNotCreatedFromOrderEntry"			//OE30.912"
						
						PrmCustomerOrder
							if (PrmBillingInvoice not entered)
								required
							else
								default to PrmBillingInvoice.CustomerOrder
								constraint (PrmCustomerOrder = PrmBillingInvoice.CustomerOrder)
									"OrderDoesNotBelongToInvoice"			//OE30.904"
						
						PrmInventoryLocation
							default to PrmCustomerOrder.InventoryLocation
							
						PrmShipment
							if (PrmBillingInvoice not entered)
								required
							else
								default to PrmBillingInvoice.WarehouseShipment
								constraint (PrmShipment = PrmBillingInvoice.WarehouseShipment)
									"ShipmentDoesNotBelongToInvoice"		//OE30.905"
						
						PrmInvoiceType
							required
						
					Action Rules
						invoke Entered.Create this instance
							fill in fields from PrmBillingInvoice
							if (PrmBillingCompany.ManualReturnNumbering)
								invoked.CustomerOrderReturn		= PrmCustomerOrderReturn
							invoked.Company 			= PrmBillingCompany
							invoked.InventoryLocation   = PrmInventoryLocation
							invoked.OriginalInvoice		= PrmBillingInvoice
							invoked.WarehouseShipment	= PrmShipment
							invoked.OrderNumber			= PrmCustomerOrder
							invoked.BillingInvoiceType  = PrmInvoiceType
							invoked.CreateCreditMemo    = PrmCreateCreditMemo
							invoked.ReasonCode          = PrmReasonCode
							invoked.FreightCharge		= PrmCopyFreightCharge
							
							if (PrmCopyFreightCharge)
								invoked.FreightCharge   = PrmBillingInvoice.FreightCharge.TransactionAmount
							else
								initialize invoked.FreightCharge

							if (TaxEntityRel.ThirdParty.TaxEngine)
								initialize invoked.TemporaryHold
						
						LocalOrderReturn				= this instance.CustomerOrderReturn
						LocalBillingInvoice 			= PrmBillingInvoice
						
						for each BillingInvoiceLinesRel
							LocalQuantity = each.Quantity - each.WarehouseShipmentLineRel.DerivedCustomerOrderLineReturnedQuantity
							invoke Entered.Create CustomerOrderReturnLine
								fill in fields from each
								invoked.CustomerOrderReturn 	= LocalOrderReturn
								invoked.CustomerOrderReturnLine = each.BillingInvoiceLine
								invoked.ReasonCode          	= PrmReasonCode
								invoked.CreateCreditMemo    	= PrmCreateCreditMemo
								invoked.Quantity				= LocalQuantity
								invoked.InventoryDisposition	= PrmInventoryDisposition
								initialize invoked.LastAddOnChargeSequence
							
							LocalInvoiceLine = each.BillingInvoiceLine
							for each BillingInvoiceLineDetailsRel
								invoke Create CustomerOrderReturnLineDetail
									fill in fields from each
									invoked.CustomerOrderReturn 	= LocalOrderReturn
									invoked.CustomerOrderReturnLine	= LocalInvoiceLine
							
							if (PrmCopyAddOnCharge)
								for each CustomerOrderLineAddOnChargesRel
									invoke Create CustomerOrderReturnLineAddOnCharges
										fill in fields from each
										invoked.CustomerOrderReturn 	= LocalOrderReturn
										invoked.CustomerOrderReturnLine	= LocalInvoiceLine										
										
										
						if (PrmCopyAddOnCharge)	
							for each CustomerOrderAddOnChargesRel
								invoke Create CustomerOrderReturnLineAddOnCharges
									fill in fields from each
									invoked.CustomerOrderReturn 	= LocalOrderReturn

								








































































																
				Update is an Update Action
					Action Rules
						if (TaxEntityRel.ThirdParty.TaxEngine)
							constraint (!IsOnHold)
								"CannotUpdate;SentForApproval"
				
				Delete is a Delete Action
					Entrance Rules
						if (TaxEntityRel.ThirdParty.TaxEngine)
							constraint (!IsOnHold)
								"CannotDelete;SentForApproval"
					
				Release is an Instance Action
					completion message is "<CompletionMessage>"
					valid when (!IsOnHold)

					Action Rules
						if (TaxEntityRel.ThirdParty.TaxEngine)
							for each CustomerOrderReturnLinesRel		
								constraint (!each.InventoryDisposition.Undecided)
									"CannotRelease;InventoryDispositionIsRequiredForLine<each.CustomerOrderReturnLine>"

							constraint (!TemporaryHold.SentForApproval)
								"ShipmentAlreadySentForApproval"

						constraint (Currency entered)
							"CannotRelease;CurrencyIsRequired" //"OERTN.108"
						
						constraint (CustomerOrderReturnLinesRel exists)
							"CannotReleaseReturn;NoLinesExists"		

						if (OriginalInvoice.OrderDiscount entered)		
							include CalculateDiscounts

						if (TaxEntityRel.ThirdParty.TaxEngine
						and TemporaryHold != TemporaryHold.ApprovedByEngine)
							invoke PutOnHold
							invoke TriggerBRCustomerReturn 
						else
							if (CommentCount > 0)
								invoke Unreleased.Create BillingInvoice
									assign result to NewBillingInvoice
									fill in fields from this instance
									invoked.BillingInvoiceSource		= "R"
									invoked.AutoApply					= AutomaticCashApplication
									invoked.GeneralLedgerDate			= PostingDate
									invoked.Location					= InventoryLocation
									invoked.AuthorizationNumber			= CustomerOrderReturn
									invoked.CustomerOrder				= OrderNumber
									invoked.FreightCharge.TransactionAmount = FreightCharge * (-1)
									if (OriginalInvoice.IsTaxEngine)
										if (any TaxEngineTaxDetailRel.TaxCode entered)
											invoked.TaxExemptCode				= "T"
											invoked.TaxCode						= InventoryLocation.TaxCode
										else
											invoked.TaxExemptCode				= "E"
										invoked.GlobalInvoiceNumber			= first TaxEngineTaxDetailRel.Invoice
									else
										invoked.TaxExemptCode				= OrderNumber.TaxStatus
										invoked.TaxCode						= OrderNumber.TaxCode
										invoked.GlobalInvoiceNumber			= OriginalInvoice.GlobalInvoiceNumber
									invoked.OriginalBillingInvoice		= OriginalInvoice
									invoked.EnteredDiscount				= OriginalInvoice.EnteredDiscount
									invoked.OrderDiscount				= LocalCalculatedDiscount * -1
									initialize invoked.LastAddOnChargeSequence
									initialize invoked.LastLineNumber                     
									initialize invoked.LastTopCommentLine                    
									initialize invoked.LastBottomCommentLine     
								
								LocalBillingInvoice	= NewBillingInvoice.BillingInvoice
								InvoicePrefix       = LocalBillingInvoice.InvoicePrefix
								InvoiceNumber       = LocalBillingInvoice.InvoiceNumber
							invoke Unreleased.Create InventoryTransaction
								assign result to NewInventoryTransaction
								fill in fields from this instance
								invoked.InventoryDocumentType 	= "CR"
								invoked.OriginatingTransaction	= reference to this instance

							LocalInventoryTransaction = NewInventoryTransaction.InventoryTransaction
							
							invoke Entered.Release CustomerOrderReturnLinesRel
								invoked.PrmInventoryTransaction = LocalInventoryTransaction
								invoked.PrmBillingInvoice		= LocalBillingInvoice
							
							if (NewBillingInvoiceLinesRel not exists)
								invoke Unreleased.Delete LocalBillingInvoice
								
							if (InventoryTransactionLinesRel exists)
								invoke FinalRelease LocalInventoryTransaction
							else
								invoke SystemDelete LocalInventoryTransaction
								
							if (PostingDate not entered)
								PostingDate = current corporate date
							
							if (InventoryDispositionReturnLinesRel exists)
								make transition to Released
							else
								make transition to Closed
								
							if (Company.FinanceEnterpriseGroup.BODTrigger)
								trigger "CustomerOrderReturnService" PA service
									resume on error
									title is "EG:<Company.FinanceEnterpriseGroup>CO:<Company>"
									Criteria
										Company.FinanceEnterpriseGroup
										Company
									Variables
										Company
											variable name is Company	
										BillingProcessLevel
											variable name is BillingProcessLevel
										InvoiceNumber
											variable name is InvoiceNumber
										Customer
											variable name is Customer
										InventoryLocation
											variable name is InventoryLocation
										CustomerOrderReturn
											variable name is CustomerOrderReturn
											
							invoke TriggerCustomerOrderReturn
							
							invoke Release CustomerOrderReturnLineAddOnChargesRel
								invoked.PrmBillingInvoice = LocalBillingInvoice	
							

							invoke TriggerCustomerReturn






			Released is a State
			
			Closed is a State
