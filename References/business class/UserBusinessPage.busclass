UserBusinessPage is a BusinessClass
    owned by repository
    store using com.lawson.rdtech.framework.repository.UserPageStorage

    Ontology
        part of BusinessPage
    		relative key is UserBusinessConfigOwner
    			default label is "UserPageOwner"

    Patterns
    	implements ReadOnly
    	disable UniqueID

	Persistent Fields
		Type					 is Numeric size 1
			States
				Unconfigured 	value is 0
				Configuration	value is 1
				Personalization	value is 2
				Folder			value is 3
		LPL 					 is LPL
		HasDefaultPDFOrientation	is Boolean
		    default label is "DefaultPDFOrientationDefined"
		DefaultPDFOrientation		is a PDFOrientation
		HasDefaultPDFFontOffset		is Boolean
		    default label is "DefaultPDFFontOffsetDefined"
		DefaultPDFFontOffset		is a PDFFontOffset
		PDFDefaultBusinessView		is a BusinessView
		PDFDefaultBusinessList		is a BusinessList
		PDFDefaultHasDetailPanels	is Boolean
			default label is "PDFDefaultDetailPanelsExist"
	
	Conditions
		HasDefaultBusinessList
		    default label is "DefaultBusinessListDefined"
			when (PDFDefaultBusinessList exists)
		
		HasPersonalizedDefaultUserBusinessList
		    default label is "PersonalizedDefaultUserBusinessListDefined"
			when (PersonalizedPDFDefaultUserBusinessList exists and PersonalizedPDFDefaultUserBusinessList.Type.Personalization)
		
		HasConfiguredDefaultUserBusinessList
		    default label is "ConfiguredDefaultUserBusinessListDefined"
			when (GlobalPDFDefaultUserBusinessList exists) 
		
		PDFDefaultHasAnyDetails
			default label is "AnyDetailsExist"
			when (PDFDefaultHasDetailPanels
			or   (HasPersonalizedDefaultUserBusinessList and (PersonalizedPDFDefaultUserBusinessList.HasDetailFields or PersonalizedPDFDefaultUserBusinessList.HasDetailSections or PersonalizedPDFDefaultUserBusinessList.ShowChangedValuesOnRight))
			or   (HasConfiguredDefaultUserBusinessList and (GlobalPDFDefaultUserBusinessList.HasDetailFields or GlobalPDFDefaultUserBusinessList.HasDetailSections or GlobalPDFDefaultUserBusinessList.ShowChangedValuesOnRight)))
		
		PDFDefaultDisplayAsTree
			when (HasPersonalizedDefaultUserBusinessList and (PersonalizedPDFDefaultUserBusinessList.ImplementsTreeView or PersonalizedPDFDefaultUserBusinessList.ImplementsCubeView)
			or    HasConfiguredDefaultUserBusinessList and (GlobalPDFDefaultUserBusinessList.ImplementsTreeView or GlobalPDFDefaultUserBusinessList.ImplementsCubeView))
		
	Relations		
		GlobalPDFDefaultUserBusinessList
			one-to-one relation to UserBusinessList
			Field Mapping uses part of key
				related.BusinessView 						= PDFDefaultBusinessView
				related.BusinessList 						= PDFDefaultBusinessList
				related.UserBusinessConfigOwner.Actor   	= blank
				related.UserBusinessConfigOwner.UserFolder	= blank
				
		PersonalizedPDFDefaultUserBusinessList
			one-to-one relation to UserBusinessList
			Field Mapping uses part of key
				related.BusinessView 						= PDFDefaultBusinessView
				related.BusinessList 						= PDFDefaultBusinessList
				related.UserBusinessConfigOwner.Actor   	= actor
				related.UserBusinessConfigOwner.UserFolder	= blank
