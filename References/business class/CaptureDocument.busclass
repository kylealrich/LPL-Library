CaptureDocument is a BusinessClass
	owned by idm
	prefix is CADOC
	
	Ontology
		symbolic key is CaptureDocument
	
	Patterns
        implements StaticJava
        disable AuditIndex
	
	Persistent Fields
	
	Field Rules
			

	Local Fields
		RowCount				is Numeric size	9
		MaximumRowCount         is Numeric size	9
		ColumnCount				is Numeric size	2
		MaximumColumnCount		is Numeric size	2
		FieldCount              is Numeric size	9
		MaximumFieldCount       is Numeric size	9
		I1						is Numeric size	9
		I2						is Numeric size	2
		I3						is Numeric size	9
		LocalFinanceEnterpriseGroup is a FinanceEnterpriseGroup
		DocumentFieldName		is Alpha size 100
			States
				BillToName                             value is "BillToName"
				BillToAddress                          value is "BillToAddress"
				BillToCity                             value is "BillToCity"
				BillToState                            value is "BillToState"
				BillToZip                              value is "BillToZip"
				BillToCountry                          value is "BillToCountry"
				BillFromName                           value is "BillFromName"
				BillFromAddress                        value is "BillFromAddress"
				BillFromCity                           value is "BillFromCity"
				BillFromState                          value is "BillFromState"
				BillFromZip                            value is "BillFromZip"
				BillFromCountry                        value is "BillFromCountry"
				Company                                value is "Company"
				CorrelationId                          value is "CorrelationId"
                CreateInvoiceDetails                   value is "CreateInvoiceDetails"
				Currency                               value is "Currency"
				ExternalVendorId                       value is "ExternalVendorId"
				FinanceEnterpriseGroup                 value is "FinanceEnterpriseGroup"
				TotalInvoice                           value is "TotalInvoice"
				InvoiceDate                            value is "InvoiceDate"
				InvoiceDueDate                         value is "InvoiceDueDate"
				InvoiceNumber                          value is "InvoiceNumber"
				InvoiceType                            value is "InvoiceType"
				PaymentTerms                           value is "PaymentTerms"
				PurchaseOrder                          value is "PurchaseOrder"
				RemitToCode                            value is "RemitToCode"
				RemitToName                            value is "RemitToName"
				RemitToAddress                         value is "RemitToAddress"
				RemitToCity                            value is "RemitToCity"
				RemitToState                           value is "RemitToState"
				RemitToZip                             value is "RemitToZip"
				RemitToCountry                         value is "RemitToCountry"
				ShipToName                             value is "ShipToName"
				ShipToAddress                          value is "ShipToAddress"
				ShipToCity                             value is "ShipToCity"
				ShipToState                            value is "ShipToState"
				ShipToZip                              value is "ShipToZip"
				ShipToCountry                          value is "ShipToCountry"
				TaxAmount                              value is "TaxAmount"
				Vendor                                 value is "Vendor"
				VendorGroup                            value is "VendorGroup"
				MatchReferenceNumber                   value is "MatchReferenceNumber"
				ExternalPurchaseOrder                  value is "InterfacedPurchaseOrder"

		InvoiceNumber			is AlphaUpper size 22
		TotalInvoiceAmount		is an InternationalAmount
		PurchaseOrderNumber		is like PurchaseOrder
		PoLineNumber            is like PurchaseOrderLine
		InvoiceDateString			is Alpha 10
		InvoiceDueDateString		is Alpha 10
		TaxAmount				is an InternationalAmount
		CorrelationId			is Alpha size 100
		InvoiceType             is a PayablesInvoiceType
        DocumentType			is Alpha size 100
        CreateInvoiceDetails    is Numeric size	1

        ExternalCompany         is like Company
        ExternalVendorGroup		is like VendorGroup
        ExternalVendor			is like Vendor
		ExternalCurrency		is Alpha size up to 40	
		ExternalTerms			is Alpha size up to 40
		ExternalVendorId		is Alpha size up to 40

		ShipToName				is Alpha size 120
		ShipToAddress			is Alpha size 40
		ShipToCity              is a MunicipalityLarge
		ShipToState             is like StateProvince
		ShipToZip               is a PostalCode
		ShipToCountry           is Alpha size 100	

		BillToName				is Alpha size 120
		BillToAddress			is Alpha size 40
		BillToCity				is a MunicipalityLarge
		BillToState				is like StateProvince
		BillToZip				is a PostalCode
		BillToCountry			is Alpha size 100	

		BillFromName			is Alpha size 120
		BillFromAddress			is Alpha size 40
		BillFromCity			is a MunicipalityLarge
		BillFromState			is like StateProvince
		BillFromZip				is a PostalCode
		BillFromCountry			is Alpha size 100	

		RemitToCode  			is like VendorLocation           
		RemitToName				is Alpha size 120
		RemitToAddress			is Alpha size 40
		RemitToCity				is a MunicipalityLarge
		RemitToState            is like StateProvince
		RemitToZip              is a PostalCode
		RemitToCountry			is Alpha size 100	
		MatchReferenceNumber	is like MatchReferenceNumber
		ExternalPurchaseOrder	is like PurchaseOrderImport

		ColumnName				is Alpha size 100
			States
				ItemNumber               value is "ItemNumber"
				PurchaseOrderLine        value is "PurchaseOrderLine"
				Description              value is "Description"
				Quantity                 value is "Quantity"
				Unit                     value is "Unit"
				UnitPrice                value is "UnitPrice"
				DiscountPercent          value is "DiscountPercent"
				Discount                 value is "Discount"
				LineTotal                value is "LineTotal"
				PurchaseOrderNumberForLine  	 value is "PurchaseOrder"
				LineExternalPurchaseOrder		 value is "InterfacedPurchaseOrder"
		Quantity
		ItemNumber				is AlphaUpper 32
		UnitPrice				is an InternationalAmount
		LineTotal				is an InternationalAmount
		Description				is Alpha size 100
		Unit                    is like UnitOfMeasure
		DiscountPercent
		Discount				is an InternationalAmount
		PurchaseOrderNumberForLine	is like PurchaseOrder
		LineExternalPurchaseOrder	is like PurchaseOrderImport

		
	Derived Fields
		DerivedInvoiceDate is a DerivedField
			type is Date
			DerivedInvoiceDate =  InvoiceDateString[7:10] + InvoiceDateString[1:2] + InvoiceDateString[4:5]

		DerivedInvoiceDueDate is a DerivedField
			type is Date
			DerivedInvoiceDueDate =  InvoiceDueDateString[7:10] + InvoiceDueDateString[1:2] + InvoiceDueDateString[4:5]

	Conditions

	Relations

	Sets

	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action				
			restricted

		ImportCaptureDocumentBOD is an Import Action
			restricted
			bod type is Sync.CaptureDocument
			
			Parameters
				CaptureDocumentBOD is XMLDocument

			Local Fields
				LocalDocumentInterfaceInvoice	is a DocumentInterfaceInvoice view
				LocalPOXpathNotExist	is Boolean

			Field Rules

			Action Rules

				DocumentType					= CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentType"
				MaximumFieldCount		 		= CaptureDocumentBOD select "count (//DataArea/CaptureDocument/DocumentField/Name)"
				MaximumRowCount	 				= CaptureDocumentBOD select "count (//DataArea/CaptureDocument/DocumentTable/Row/ID)"

				FieldCount = 1
				while (MaximumFieldCount >= FieldCount)
					DocumentFieldName = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Name"
					
					if (DocumentFieldName.InvoiceNumber)
						InvoiceNumber = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.TotalInvoice)
						TotalInvoiceAmount = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.PurchaseOrder)
						PurchaseOrderNumber = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.InvoiceDate)
						InvoiceDateString = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.InvoiceDueDate)
						InvoiceDueDateString = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.Currency)
						ExternalCurrency = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.PaymentTerms)
						ExternalTerms = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.TaxAmount)
						TaxAmount = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.InvoiceType)
						InvoiceType = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.CorrelationId)
						CorrelationId = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.ShipToName)
						ShipToName = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.ShipToAddress)
						ShipToAddress = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.ShipToCity)
						ShipToCity = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.ShipToState)
						ShipToState = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.ShipToZip)
						ShipToZip = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.ShipToCountry)
						ShipToCountry = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.BillToName)
						BillToName = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillToAddress)
						BillToAddress = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillToCity)
						BillToCity = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillToState)
						BillToState = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillToZip)
						BillToZip = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillToCountry)
						BillToCountry = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.BillFromName)
						BillFromName = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillFromAddress)
						BillFromAddress = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillFromCity)
						BillFromCity = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillFromState)
						BillFromState = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillFromZip)
						BillFromZip = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.BillFromCountry)
						BillFromCountry = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.RemitToCode)
						RemitToCode = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.RemitToName)
						RemitToName = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.RemitToAddress)
						RemitToAddress = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.RemitToCity)
						RemitToCity = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.RemitToState)
						RemitToState = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.RemitToZip)
						RemitToZip = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					if (DocumentFieldName.RemitToCountry)
						RemitToCountry = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.FinanceEnterpriseGroup)
						LocalFinanceEnterpriseGroup = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.Company)
						ExternalCompany = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.VendorGroup)
						ExternalVendorGroup = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.Vendor)
						ExternalVendor = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.ExternalVendorId)
						ExternalVendorId = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.CreateInvoiceDetails)
						CreateInvoiceDetails = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					
					if (DocumentFieldName.MatchReferenceNumber)
						MatchReferenceNumber = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"

					if (DocumentFieldName.ExternalPurchaseOrder)
						ExternalPurchaseOrder = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentField[<FieldCount>]/Value"
					
					FieldCount += 1

				if (!LocalFinanceEnterpriseGroup entered)
					LocalFinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup

				if (DocumentType = "FSM_PayablesInvoice")
					invoke Create DocumentInterfaceInvoice
						assign result to LocalDocumentInterfaceInvoice
						invoked.FinanceEnterpriseGroup   = LocalFinanceEnterpriseGroup
						invoked.DocumentInterfaceInvoice = CorrelationId
						invoked.Company                  = ExternalCompany
						invoked.VendorGroup              = ExternalVendorGroup
						invoked.Vendor                   = ExternalVendor
						invoked.Invoice                  = InvoiceNumber
						invoked.InvoiceDate              = DerivedInvoiceDate
						invoked.DueDate                  = DerivedInvoiceDueDate
						invoked.PurchaseOrder            = PurchaseOrderNumber
						invoked.InvoiceAmount            = TotalInvoiceAmount
						invoked.ExternalCurrency         = ExternalCurrency
						invoked.ExternalTerms            = ExternalTerms
						invoked.TaxAmount                = TaxAmount
						invoked.CreationSource			 = 1	
						invoked.ExternalVendorID         = ExternalVendorId
						invoked.RemitToCode              = RemitToCode
						invoked.CreateDetails            = CreateInvoiceDetails
						invoked.MatchReferenceNumber     = MatchReferenceNumber
						invoked.InterfacedPurchaseOrder  = ExternalPurchaseOrder
					
					RowCount = 1
					while (MaximumRowCount >= RowCount)
	
                        PoLineNumber = RowCount
						ColumnCount = 1
						MaximumColumnCount = CaptureDocumentBOD select "count (//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column/Name)"
						LocalPOXpathNotExist = false
						while (MaximumColumnCount >= ColumnCount)
							ColumnName = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Name"
							
							
							if (ColumnName.ItemNumber)
								ItemNumber = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
	
							if (ColumnName.PurchaseOrderLine)
								PoLineNumber = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"

							if (ColumnName.Description)
								Description = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
	
							if (ColumnName.Quantity)
								Quantity = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
	
							if (ColumnName.Unit)
								Unit = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
		
							if (ColumnName.UnitPrice)
								UnitPrice = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
	
							if (ColumnName.DiscountPercent)
								DiscountPercent = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
	
							if (ColumnName.Discount)
								Discount = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
	
							if (ColumnName.LineTotal)
								LineTotal = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
							
							if (ColumnName.PurchaseOrderNumberForLine)
								LocalPOXpathNotExist = true
								PurchaseOrderNumberForLine = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"
								if(PurchaseOrderNumberForLine not entered )
									PurchaseOrderNumberForLine = PurchaseOrderNumber
									
							if(!LocalPOXpathNotExist)
								PurchaseOrderNumberForLine = PurchaseOrderNumber

							if (ColumnName.LineExternalPurchaseOrder)
								LineExternalPurchaseOrder = CaptureDocumentBOD select "//DataArea/CaptureDocument/DocumentTable/Row[<RowCount>]/Column[<ColumnCount>]/Value"

																
							ColumnCount += 1
						
						if (PurchaseOrderNumber entered 
						or  PurchaseOrderNumberForLine entered
						or  ExternalPurchaseOrder entered
						or  LineExternalPurchaseOrder entered)
							invoke Create DocumentInterfaceDetail
								invoked.FinanceEnterpriseGroup   = LocalDocumentInterfaceInvoice.FinanceEnterpriseGroup
								invoked.DocumentInterfaceInvoice = LocalDocumentInterfaceInvoice.DocumentInterfaceInvoice
								invoked.PurchaseOrderLine       = PoLineNumber
								invoked.Item                    = ItemNumber
								invoked.ItemDescription         = Description
								invoked.InvoiceQuantity         = Quantity
								invoked.InvoiceUnitCost         = UnitPrice
								invoked.InvoiceUOM              = Unit
								invoked.PurchaseOrder       	= PurchaseOrderNumberForLine
								invoked.InterfacedPurchaseOrder = LineExternalPurchaseOrder
								
							invoke Create DocumentInterfacePurchaseOrder
								invoked.FinanceEnterpriseGroup   = LocalDocumentInterfaceInvoice.FinanceEnterpriseGroup
								invoked.DocumentInterfaceInvoice = LocalDocumentInterfaceInvoice.DocumentInterfaceInvoice
								invoked.PurchaseOrder   = PurchaseOrderNumberForLine
								invoked.CreateDetails = 2
								invoked.InterfacedPurchaseOrder = LineExternalPurchaseOrder

						RowCount += 1

