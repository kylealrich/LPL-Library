ReceivableInvoiceDetailImport is a BusinessClass
    owned by ar
    prefix is ARI
    classic name is ARIITEM

    Ontology
        symbolic key is ReceivableInvoiceDetailImport
            classic set name is ARISET1

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
		RunGroup
        Company					is a ReceivableCompany

        InvoiceType				is a ReceivableInvoiceDetailType	
            classic name is TRANS-TYPE
        Invoice
            classic name is INVOICE
        Sequence
        Description
        ItemReference         is AlphaUpper size 32
            classic name is ITEM
        TransactionAmount     is an InternationalAmount
            classic name is AMOUNT
        ItemUser1
            classic name is ITEM-USER1
        Project				is a FinanceCodeBlockProjectOnly
            classic name is ACTIVITY
        LineNumber
            classic name is LINE-NBR
        LineType
        LineSeq               is a Sequence3
        InvoiceLineObjID
            classic name is INVL-OBJ-ID
        Sort1
        Sort2
        Sort3
        Sort4
        AdjFl                 is a Flag1


        TransactionUserField1 is a TransUser1
            classic name is TRANS-USER1
        InvoiceFinanceCodeBlock	is a FinanceCodeBlock
        ReasonCode
        GlobalLineType
            classic name is GLBL-LINE-TYPE
        ProjectContract			is a FinanceCodeBlockProjectOnly
		HSNSACCode

	Field Rules
		RunGroup
			required
			default to ReceivableInvoiceImportRel.RunGroup
		Company
			required
		InvoiceType
			required
			if (!ReceivableInvoiceImportRel.InvoiceType.MixedSignLines)
				default to ReceivableInvoiceImportRel.InvoiceType 
		Invoice
			required
		Sequence
			autosequence using ByRunGroup
	
	Derived Fields
		DerivedTransactionAmount	is a DerivedField
			type is like InternationalAmount
			restricted
			if  (InvoiceType.CreditMemo)
				return TransactionAmount
			else
				return TransactionAmount * -1
				
	Relations






	
        ReceivableInvoiceImportRel
            one-to-many relation to ReceivableInvoiceImport
            Field Mapping uses ByInvoice
                related.Company			 				= Company
				related.Invoice							= Invoice
	
    Sets

        Set2
            indexed
            Sort Order
                Company
                Invoice
                InvoiceType
                Sequence

        ByRunGroup
            Sort Order
                RunGroup
                Company
				Invoice
                InvoiceType
				Sequence


	Create Rules
		constraint (ReceivableInvoiceImportRel exists)
			"InterfaceInvoiceDoesNotExist"
			
	Actions
		Create is a Create Action
		
		Update is an Update Action
			Action Rules
		
		FastUpdate is an Update Action
			restricted
			bypass field rules

		Delete is a Delete Action
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
