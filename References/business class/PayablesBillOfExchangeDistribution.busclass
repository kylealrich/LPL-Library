PayablesBillOfExchangeDistribution is a BusinessClass
    owned by ap
    prefix is DDT
    sql name is PBillOfExchangeDistribution
    classic name is APDRFTDIST

    Ontology
        symbolic key is PayablesBillOfExchangeDistribution
            classic set name is DDTSET1
            sql name is PBillOfExchangeDistribution
            classic name is SEQ-NBR
            classic name for BankTransactionCode is BANK-INST-CODE
            classic name for PayablesBillOfExchange is TRANS-IDENT

    Patterns
		implements ContextualParent
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Company                   is a PayablesCompany
			disable surrogates
        PayGroup
        DistributionType          is AlphaUpper size 1
            classic name is DIST-TYPE
            States
                Accepted    value is "A"
                Cashed      value is "C"
                Dishonered  value is "D"
                Canceled    value is "X"
                TaxReversal value is "R"
        Vendor
        TransactionNumber
            classic name is TRANS-NBR
        Suffix
        Status                    is Numeric size 1
            classic name is REC-STATUS
            States
                Unposted value is 1
                Posted   value is 9
        InvVendor                 is a Vendor
        Invoice
        InvSuffix                 is a Suffix
        InvCancelSeq              is a SeqNbr
        InvSeqNbr                 is Numeric size 4
        InvVoidSeq                is a SeqNbr





        NumberOfDecimals          is Numeric size 1
            classic name is TRAN-ND
        BaseNumberOfDecimals
            classic name is BASE-ND
        Currency				  is a FromCurrency
            classic name is CURRENCY-CODE
        AccrualCode               is a PayablesAccrualCode
            classic name is ACCR-CODE
        OriginalCompany			  is a GeneralLedgerCompany
			disable surrogates






        BillOfExchangeAccrualCode is a PayablesBillOfExchangeAccrualCode
            classic name is DRAFT-CODE
        PostDate                  is Date
        GltObjId                  is an ObjId
        JournalBook
            classic name is JRNL-BOOK-NBR
            restricted

	Transient Fields
		TransientCurrencyTable				is a CurrencyTable
			derive value from Company.DerivedCurrencyTable
		TransientAccountingEntity		    is an AccountingEntity
			derive value from Company.AccountingEntity  			  				        	        	            		
		GLFinanceCodeBlock		 			is a  TransactionCodeBlock				 		
			derive value from GLTransactionDetailRel.FinanceCodeBlock 
		GLTransactionAmount		 			is a  CurrencyAmount			
			derive value from GLTransactionDetailRel.TransactionAmount 
		GLBaseAmount						is a FinanceCurrencyAmount		
			derive value from GLTransactionDetailRel.ReportCurrencyAmount 
		TransientExchangeDate				is an ExchangeDate
			derive value from PostDate
		TransientReferenceToThisInstance	is BusinessObjectReference
			derive value from reference to this instance

	Local Fields


	Derived Fields
		BillOfExchangeDistributionPostingMessage is a MessageField 
			restricted
			"BillOfExchangeDistributionPosting"

		DerivedCurrencyTable is a DerivedField
			type is AlphaUpper 12
			return Company.DerivedCurrencyTable

    Conditions
        IsInvoiceDist
        	restricted
            when (Invoice entered)
 

	Relations
		CompanySystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "AP"
				related.Company						= Company

		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 

				related.OriginatingTransaction	= reference to this instance			

		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup          = CashManagementGroup 
				related.GeneralLedgerSystemCode         = "AP" 

	Field Rules
        GLBaseAmount
        	required

	Rule Blocks
		CreateGLTransactionDetail
			invoke CreateUnreleasedGLTransDetailNoRules GLTransactionDetail		 
				fill in fields from this instance
				invoked.OriginatingTransaction 	= reference to this instance
				invoked.FinanceEnterpriseGroup	= CashManagementGroup  
				invoked.CurrencyCode			= Currency
				invoked.System					= "AP"
				invoked.TransactionAmount		= GLTransactionAmount
				invoked.FinanceCodeBlock		= GLFinanceCodeBlock
				invoked.ReportCurrencyAmount 	= GLBaseAmount
				invoked.Reference				= TransactionNumber

            	invoked.GeneralLedgerEvent 	    = "BE"
				invoked.AccountingEntity		= Company.AccountingEntity

				invoked.TransactionDate			= PostDate
				invoked.PostingDate				= PostDate
				invoked.ControlDocumentNumber 	= TransactionNumber
				invoked.DocumentNumber		  	= TransactionNumber
				invoked.AutoReverse 			= false

			invoke Release GLTransactionDetailRel


	Actions
		Create is a Create Action
			Exit Rules	
				include CreateGLTransactionDetail

		Update is an Update Action

		Delete is a Delete Action
			Action Rules
				if (GLTransactionDetailRel exist) 
					invoke Released.Delete GLTransactionDetailRel		
		
		Purge is a Purge Action
			restricted			
		
		CreateOffset is an Instance Action
			restricted
			Parameters
				PrmActionDate	is Date
			Action Rules
				invoke Create PayablesBillOfExchangeDistribution
					fill in fields from this instance
						except invoked.GltObjId	
					invoked.GLTransactionAmount										= GLTransactionAmount * -1
					invoked.GLBaseAmount.FunctionalAmount.EnteredCurrencyAmount		= GLBaseAmount.FunctionalAmount.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.AlternateAmount.EnteredCurrencyAmount		= GLBaseAmount.AlternateAmount.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.AlternateAmount2.EnteredCurrencyAmount		= GLBaseAmount.AlternateAmount2.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.AlternateAmount3.EnteredCurrencyAmount		= GLBaseAmount.AlternateAmount3.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ToFunctionalAmount.EnteredCurrencyAmount	= GLBaseAmount.ToFunctionalAmount.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ToAlternateAmount.EnteredCurrencyAmount	= GLBaseAmount.ToAlternateAmount.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ToAlternateAmount2.EnteredCurrencyAmount	= GLBaseAmount.ToAlternateAmount2.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ToAlternateAmount3.EnteredCurrencyAmount	= GLBaseAmount.ToAlternateAmount3.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ProjectAmount.EnteredCurrencyAmount		= GLBaseAmount.ProjectAmount.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ReportAmount1.EnteredCurrencyAmount		= GLBaseAmount.ReportAmount1.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ReportAmount2.EnteredCurrencyAmount		= GLBaseAmount.ReportAmount2.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ReportAmount3.EnteredCurrencyAmount		= GLBaseAmount.ReportAmount3.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ReportAmount4.EnteredCurrencyAmount		= GLBaseAmount.ReportAmount4.EnteredCurrencyAmount * -1
					invoked.GLBaseAmount.ReportAmount5.EnteredCurrencyAmount		= GLBaseAmount.ReportAmount5.EnteredCurrencyAmount * -1
					invoked.Status				= 1
					invoked.PostDate			= PrmActionDate



		JournalizeDistributions is a Set Action		

			completion message is "<CompletionMessage>"


			Parameters
				PrmVendorGroup		is a VendorGroup
					default label is "VendorGroup"
				PrmPayGroup			is a PayGroup
					default label is "PayGroup"					
				PrmPostThruDate	is Date
					default label is "PostThruDate"	
				PrmDescription				is a Description
					default label is "Description"

			Parameter Rules
				PrmVendorGroup
					required
				PrmPayGroup 	 
					required
				PrmPostThruDate		 
					required
				PrmDescription
					initial value is BillOfExchangeDistributionPostingMessage

			Local Fields
				CompletionMessage			is Alpha 150
				RecordCount					is Numeric 10
				MyJournalizeGroup			is a JournalizeGroup
				
			Instance Selection
				where (CashManagementGroup	= PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup
				and    Status.Unposted
				and    PayGroup				= PrmPayGroup
				and    PostDate				<= PrmPostThruDate)

			Sort Order  
				CashManagementGroup
				Status
				PayGroup
				PostDate

			Accumulators

				
			Action Rules								
				Empty Set Rules
					CompletionMessage = "BillOfExchangePostingCompleteForJournalizeGroup...:NoRecordsFoundToJournalize<MyJournalizeGroup>"
					
				Set Rules
					Entrance Rules
						initialize RecordCount


			            increment GeneralLedgerSystemCodeRel.LastJournalizeGroup
						MyJournalizeGroup 		 = GeneralLedgerSystemCodeRel.DerivedJournalizeGroup						 

					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmVendorGroup.BusinessGroup.FinanceEnterpriseGroup in background
							invoked.PrmJournalizeGroup	= MyJournalizeGroup
							invoked.PrmJournalizeGroupDescription	= PrmDescription
						
						CompletionMessage = "BillOfExchangePostingCompleteForJournalizeGroup...:<MyJournalizeGroup>;<RecordCount>...RecordsProcessed"
				
				Instance Rules
					increment RecordCount
					if (GLTransactionDetailRel exist)
						invoke UpdateJournalizeGroup GLTransactionDetailRel	 
							invoked.PrmJournalizeGroup = MyJournalizeGroup  
						Status = Status.Posted
						


