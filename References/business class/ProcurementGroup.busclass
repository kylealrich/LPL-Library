ProcurementGroup is a BusinessClass
    owned by po
    prefix is PCG
    classic name is PROCUREGRP

    Ontology
        symbolic key is ProcurementGroup
            classic set name is PCGSET1
            classic name is PROCURE-GROUP

    Patterns
        implements StaticJava
        disable AuditIndex
		implements BODId
    Persistent Fields

        Description                                 is Alpha size 30
            classic name is PROC-GRP-DESC


        Currency
            classic name is CURRENCY-CODE
        BlanketPurchaseTypePOCode                   is a POCode
            classic name is BL-PO-CODE
        StandingPurchaseTypePOCode                  is a POCode
            classic name is ST-PO-CODE
        AllowSpecialItemsOnTemplates                is Boolean
            classic name is TEMPL-SPECIAL




        IncludeAOCForLifetimeMaximumEditsOnCatalog  is Boolean
            sql name is IAOCFLMaximumEditsOnCatalog
            classic name is INCL-AOC-CT
        IncludeAOCForLifetimeMaximumEditsOnBlanket  is Boolean
            sql name is IAOCFLMaximumEditsOnBlanket
            classic name is INCL-AOC-BL
        IncludeAOCForLifetimeMaximumEditsOnContract is Boolean
            sql name is IAOCFLMaximumEditsOnContract
            classic name is INCL-AOC-CN
        IncludeAOCForLifetimeMaximumEditsOnService  is Boolean
            sql name is IAOCFLMaximumEditsOnService
            classic name is INCL-AOC-SV
        IncludeAOCForLifetimeMaximumEditsOnStanding is Boolean
            sql name is IAOCFLMaximumEditsOnStanding
            classic name is INCL-AOC-ST
        IncludeTaxForLifetimeMaximumEditsOnCatalog  is Boolean
            sql name is ITFLMaximumEditsOnCatalog
            classic name is INCL-TAX-CT
        IncludeTaxForLifetimeMaximumEditsOnBlanket  is Boolean
            sql name is ITFLMaximumEditsOnBlanket
            classic name is INCL-TAX-BL
        IncludeTaxForLifetimeMaximumEditsOnContract is Boolean
            sql name is ITFLMaximumEditsOnContract
            classic name is INCL-TAX-CN
        IncludeTaxForLifetimeMaximumEditsOnService  is Boolean
            sql name is ITFLMaximumEditsOnService
            classic name is INCL-TAX-SV
        IncludeTaxForLifetimeMaximumEditsOnStanding is Boolean
            sql name is ITFLMaximumEditsOnStanding
            classic name is INCL-TAX-ST
		PurchaseOrderEncumbranceDate                is Numeric size 1
			States
            	PurchaseOrderDate          value is 1
            	LineEarlyDeliveryDate      value is 2
		RequisitionCommitmentDate                   is Numeric size 1
			States
            	CreationDate               value is 1
            	LineEarlyDeliveryDate      value is 2
		LastArticleDisplayOrder                     is a SequenceNumber
			disable Auditing
		LastQuestionDisplayOrder                    is a SequenceNumber
			disable Auditing
        MaskProcurementCardNumberDisplay     	    is Boolean
        MaskProcurementCardNumberEntry		    	is Boolean
        AribaEnabled								is Boolean
        AribaNetworkId								is Alpha size 20

    Local Fields
 		SpendDimension
 		LocalCounter				 is Numeric size 2
    	ActionCode
		LocalBODCurrentTimeStamp	 is a BODCurrentTimeStamp
		LocalNativeLPLBODTrigger	 is Boolean				
		LocalFSMInboundBODTracker		is Numeric 15
		Error            				is Boolean
		ErrorMessage     				is Alpha 300
		LocalBODTrigger					is Boolean
		NewBODTracker  				is a FSMInboundBODTracker view
		LocalConfigurationParameter	is Alpha size up to 200
		
    Derived Fields
    	RepresentativeText is a StringField
			type is Text
			default label is "ProcurementGroup"
			ProcurementGroup " - " Description
			
    	TotalSpendAnalysisFromDate is a DerivedField
 			type is Date
 			restricted
 			return first TotalSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.Begin
 			
 		TotalSpendAnalysisThruDate is a DerivedField
 			type is Date
 			restricted
 			return first TotalSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.End
 			
 		BuyerSpendAnalysisFromDate is a DerivedField
 			type is Date
 			restricted
 			return first BuyerSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.Begin
 			
 		BuyerSpendAnalysisThruDate is a DerivedField
 			type is Date
 			restricted
 			return first BuyerSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.End
 			
 		DiversitySpendAnalysisFromDate is a DerivedField
 			type is Date
 			restricted
 			return first DiversitySpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.Begin
 			
 		DiversitySpendAnalysisThruDate is a DerivedField
 			type is Date
 			restricted
 			return first DiversitySpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.End
 			
 		CommodityCodeSpendAnalysisFromDate is a DerivedField
 			type is Date
 			restricted
 			return first CommodityCodeSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.Begin
 			
 		CommodityCodeSpendAnalysisThruDate is a DerivedField
 			type is Date
 			restricted
 			return first CommodityCodeSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.End
 			
 		UNSPSCSpendAnalysisFromDate is a DerivedField
 			type is Date
 			return first UNSPSCSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.Begin
 			
 		UNSPSCSpendAnalysisThruDate is a DerivedField
 			type is Date
 			return first UNSPSCSpendInvoiceLineFactConsolidateControlsRel.ConsolidationDateRange.End
    
 		GrandTotalSpend is a DerivedField
 			type is like InternationalAmount
			restricted
 			return sum ConsolidationLevelOneConsolidatesRel.TotalExtendedAmount
 			
 		ActorBusinessGroup is a DerivedField
			type is like BusinessGroup
			return actor.context.BusinessGroup

		ProcurementGroupDescription is a MessageField
			"<Description>_(<ProcurementGroup>)"
		
		DerivedProcurementGroupName is a MessageField
			default label is "ProcurementGroup"
			"<ProcurementGroup>_-_<Description>"


		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or ActionCode.Create)
				return "Add"
			else
			if(action type.Update or action type.Delete)
				return "Replace"
			else
				return ""	
				
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create)
				return "Open"
			else
			if(action type.Delete or ActionCode.Delete)
				return "Deleted"
			else
			if(action type.Update)			
				return "Open"
			else
				return ""

		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha 4
			restricted
			return ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup
			
		DerivedAccountingEntity is a DerivedField
			type is Alpha 20
			restricted
			return DerivedFinanceEnterpriseGroup + DerivedDelimiter + ProcurementGroup
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
			
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return 	"infor-nid:" + DerivedTenantID + ":" + ":"  + ":" + ProcurementGroup+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
		
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
		
		ProcurementGroupXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	        ProcurementGroupXMLBOD = template.IONSyncCodeDefination_ProcurementGroup_ST document for this instance		

	Context Fields
		FSMInboundBODTracker

    Conditions

        CMInUse
        	restricted
            when (ContractGroupRel exists)
            
        HasRunDates
        	restricted
   			when (ProcurementGroupSetActionsRel exists)
   			
   		BusinessGroupAllowsAccess
			when ((actor.context.BusinessGroup != ""
			and	   actor.context.BusinessGroup = ProcurementGroup)
			or	  (actor.context.BusinessGroup = ""))

		RecordExists
			restricted
			when (ProcurementGroup exists)
			
    Relations

		BuyerRel
			one-to-many relation to Buyer
			Field Mapping uses symbolic key
				related.HROrganization	= ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.HROrganization

      	TemplateSpecialItemsRel is a ProcurementTemplateLine set
			Instance Selection
				where (related.SpecialItemDescription entered)
				
		ContractGroupRel 
    		one-to-one relation to ContractGroup
    		Field Mapping uses symbolic key
    			related.ContractGroup = ProcurementGroup
    	
    	RecallGroupRel
    		one-to-one relation to RecallGroup
    		Field Mapping uses symbolic key
    			related.RecallGroup = ProcurementGroup
    					
		ItemGroupRel
			one-to-one relation to ItemGroup
			Field Mapping uses symbolic key
				related.ItemGroup = ProcurementGroup
				
 		ProcurementGroupSetActionLoadPoInvLineRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 4

 		ProcurementGroupSetActionLoadRebateDueInvoiceLineRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 5
 
		ProcurementGroupSetActionLoadPolineRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 1
				
		ProcurementGroupSetActionUpdatePolineRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 2
		
		ProcurementGroupSetActionDeletePolineRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 3
				
 		ProcurementGroupSetActionLoadSupplierRebateInvoiceRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 9

 		ProcurementGroupSetActionLoadRebateDueInvoiceLineAllRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 10
				
		ProcurementGroupSetActionInvoiceSpendExportAndConsolidateRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 11
				
		ProcurementGroupSetActionInvoiceSpendExportOnlyRel
			one-to-one relation to ProcurementGroupSetAction
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementGroupSetAction						= 12
				
		ProcurementGroupSetActionsRel						is a ProcurementGroupSetAction set
 
		InvoiceLineFactConsolidateControlsRel		is a InvoiceLineFactConsolidateControl set
 
		TotalSpendInvoiceLineFactConsolidateControlsRel
			one-to-many relation to InvoiceLineFactConsolidateControl
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.ConsolidationOptions.ConsolidateTotalSpend
				and    related.HasTotalSpend)
				 
		BuyerSpendInvoiceLineFactConsolidateControlsRel
			one-to-many relation to InvoiceLineFactConsolidateControl
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.ConsolidationOptions.ConsolidateBuyerSpend
				and    related.HasBuyerSpend)
 
		DiversitySpendInvoiceLineFactConsolidateControlsRel
			one-to-many relation to InvoiceLineFactConsolidateControl
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.ConsolidationOptions.ConsolidateDiversitySpend
				and    related.HasDiversitySpend)
 
		CommodityCodeSpendInvoiceLineFactConsolidateControlsRel
			one-to-many relation to InvoiceLineFactConsolidateControl
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.ConsolidationOptions.ConsolidateCommodityCodeSpend
				and    related.HasCommodityCodeSpend)
 
		UNSPSCSpendInvoiceLineFactConsolidateControlsRel
			one-to-many relation to InvoiceLineFactConsolidateControl
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.ConsolidationOptions.ConsolidateUNSPSCSpend
				and    related.HasUnspscSpend)
 
 		ConsolidationLevelOneConsolidatesRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel						= 1
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.InvoiceLineFactConsolidate.Level1.Type.ProcurementGroup)

 		TotalSpendConsolidatesRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel						= 1
				related.ProcurementGroup						= ProcurementGroup
				related.InvoiceLineFactConsolidateControl		= first TotalSpendInvoiceLineFactConsolidateControlsRel.InvoiceLineFactConsolidateControl


								
		BuyerSpendConsolidatesRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel						= 1
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.InvoiceLineFactConsolidate.Level1.Type	= SpendDimension.Type.Buyer)
				
		DiversitySpendConsolidatesRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel						= 1
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.InvoiceLineFactConsolidate.Level1.Type	= SpendDimension.Type.DiversityCode)
				
		CommodityCodeSpendConsolidatesRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel						= 1
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.InvoiceLineFactConsolidate.Level1.Type	= SpendDimension.Type.CommodityCodeSegment1)
				
		UNSPSCSpendConsolidatesRel 
			one-to-many relation to InvoiceLineFactConsolidate
			Field Mapping uses ByConsolidationLevel
				related.ConsolidationLevel						= 1
				related.ProcurementGroup						= ProcurementGroup
			Instance Selection
				where (related.InvoiceLineFactConsolidate.Level1.Type	= SpendDimension.Type.UNSPSCSegment)
				
		PurchasingOverdueReceiptsCountRel
			one-to-one relation to PurchasingOverdueReceiptsCount
			Field Mapping uses symbolic key
				related.ProcurementGroup               = ProcurementGroup
				related.PurchasingOverdueReceiptsCount = LocalCounter
				
		ReceivedNotInvoicedCountRel
			one-to-one relation to ReceivedNotInvoicedCount
			Field Mapping uses symbolic key
				related.ProcurementGroup               = ProcurementGroup
				related.ReceivedNotInvoicedCount 		= LocalCounter

		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker


    Sets











    Field Rules

        Currency
            required




        Description
            required




		AllowSpecialItemsOnTemplates
			if (AllowSpecialItemsOnTemplates changed)
				if (old AllowSpecialItemsOnTemplates
				and !AllowSpecialItemsOnTemplates)
					constraint (TemplateSpecialItemsRel not exists)
						"SpecialItemsExistOnTemplate;CannotChange"
						
		PurchaseOrderEncumbranceDate
			default to PurchaseOrderEncumbranceDate.PurchaseOrderDate
		
		RequisitionCommitmentDate
			default to RequisitionCommitmentDate.CreationDate
			
	Rule Blocks			
		
					
		TriggerProcurementGroupServiceRules
			trigger "ProcurementGroupService" PA service
				resume on error
				title is "EG:<ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup>PG:<ProcurementGroup>"
				Criteria
					ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup							
				Variables
					ActionCode
					include persistent fields from ProcurementGroup
					ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup													
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
			
			

	Actions
		Create is a Create Action
		
			Exit Rules
			
				LocalCounter = 1
				while (LocalCounter < 6)	
					if (!PurchasingOverdueReceiptsCountRel exists)
						invoke Create PurchasingOverdueReceiptsCount
							invoked.ProcurementGroup 				= ProcurementGroup
							invoked.PurchasingOverdueReceiptsCount 	= LocalCounter
					LocalCounter +=1
				LocalCounter = 1
				while (LocalCounter < 6)	
					if (!ReceivedNotInvoicedCountRel exists)
						invoke Create ReceivedNotInvoicedCount
							invoked.ProcurementGroup 			= ProcurementGroup
							invoked.ReceivedNotInvoicedCount 	= LocalCounter
					LocalCounter +=1

		Update is an Update Action
		
			Exit Rules
			
				LocalCounter = 1
				while (LocalCounter < 6)	
					if (!PurchasingOverdueReceiptsCountRel exists)
						invoke Create PurchasingOverdueReceiptsCount
							invoked.ProcurementGroup 				= ProcurementGroup
							invoked.PurchasingOverdueReceiptsCount 	= LocalCounter
					LocalCounter +=1
				LocalCounter = 1
				while (LocalCounter < 6)	
					if (!ReceivedNotInvoicedCountRel exists)
						invoke Create ReceivedNotInvoicedCount
							invoked.ProcurementGroup 			= ProcurementGroup
							invoked.ReceivedNotInvoicedCount 	= LocalCounter
					LocalCounter +=1

		Delete is a Delete Action
			Entrance Rules
				if (ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger)
					ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerProcurementGroupServiceRules


								
		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
		
		SendProcurementGroupBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ProcurementGroupXMLBOD
					bod type is "Sync.CodeDefinition"
					accounting entity is DerivedAccountingEntity
					document id is ProcurementGroup
					variation id is	BODVariationID

		TriggerProcurementGroupNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "ProcurementGroup"
					invoked.PrmFinanceEnterpriseGroup	= ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_ProcurementGroup_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)	
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= ProcurementGroup
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= ProcurementGroup
							invoked.Reference2				= "ProcurementGroup"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= ProcurementGroup
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= ProcurementGroup
							invoked.Reference2				= "ProcurementGroup"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendProcurementGroupBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODFormattedId
							invoked.BODXML								= ProcurementGroupXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODFormattedId
							invoked.BODXML									= ProcurementGroupXMLBOD
					
		CreateTemplateAndTemplateLineSearchKeywords is a Set Action 
			restricted
			Parameters
				ParmProcurementGroup	is an ProcurementGroup
					default label is "ProcurementGroup"
			Parameter Rules
				ProcurementGroup
					required
						"ProcurementGroupIsRequired"
				 										
			Instance Selection
				where 	(ProcurementGroup	= ParmProcurementGroup)
				
			Action Rules
				Instance Rules
					invoke BuildTextIndexesForTemplateLines ProcurementTemplateLine
						invoked.ParmProcurementGroup	= ParmProcurementGroup
						
					invoke RecreateTemplateSearchKeywords ProcurementTemplate
						invoked.ParmProcurementGroup	= ParmProcurementGroup
						
	Action Exit Rules
		if (ProcurementGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode	= ActionCode.Update
					if (action type.Create)
						ActionCode	= ActionCode.Create				
					else
						if (action type.Delete)
							ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerProcurementGroupServiceRules



								
								
