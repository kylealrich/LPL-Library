FinanceResourceInvApproval is a BusinessClass
    owned by apautomation
    prefix is APFIA

    Ontology
    	symbolic key is FinanceResourceInvApproval

    Persistent Fields
    	VendorGroup
		InvoiceRoutingCategory
		InvoiceApprovalAuthCode
			default label is "InvoiceApprovalRole"

	Derived Fields

	Context Fields

	Conditions
		InactiveResource
			restricted
			when (!FinanceResource.Active)
		ActiveResource		
			restricted
			when (FinanceResource.Active)
		VendorGroupEntered
			restricted
			when (VendorGroup entered)

	Relations
		FinanceResourceInvApprovalRel
			one-to-many relation to FinanceResourceInvApproval
			Field Mapping uses ByResource
				related.FinanceResource			= FinanceResource
				related.InvoiceApprovalAuthCode	= InvoiceApprovalAuthCode
				related.InvoiceRoutingCategory	= InvoiceRoutingCategory
				related.VendorGroup				= VendorGroup

	Field Rules
		VendorGroup
			initial value is InvoiceRoutingCategory.VendorGroup
			required
			cannot be changed

		InvoiceRoutingCategory
			required
			constraint (InvoiceRoutingCategory.VendorGroup = InvoiceApprovalAuthCode.VendorGroup)
				"InvoiceApprovalRoleVendorGroup<InvoiceApprovalAuthCode.VendorGroup>MustBeTheSameAsInvoiceRoutingCategoryVendorGroup<InvoiceRoutingCategory.VendorGroup>"

		InvoiceApprovalAuthCode
			required

	Attach Rules

	Sets
		ByResource
			Sort Order
				FinanceResource
				InvoiceApprovalAuthCode
				InvoiceRoutingCategory
				VendorGroup
				FinanceResourceInvApproval

		ByRoutingCategory
			Sort Order
				InvoiceRoutingCategory
				VendorGroup
				FinanceResourceInvApproval
				FinanceResource

		ByAuthorityCode
			Sort Order
				InvoiceApprovalAuthCode.Priority
				FinanceResourceInvApproval
				FinanceResource
				VendorGroup

		ByRoleAndCategory
			Sort Order
				InvoiceApprovalAuthCode
				InvoiceRoutingCategory
				VendorGroup
				FinanceResourceInvApproval
				FinanceResource

	Actions
		Create is a Create Action
			completion message is "InvoiceApprovalAssignmentCreated"
			Action Rules
				constraint (FinanceResourceInvApprovalRel not exists)
					"InvoiceApprovalAssignmentAlreadyExistsForResource<FinanceResource.PreferredSimplePresentationName>Role<InvoiceApprovalAuthCode>Category<InvoiceRoutingCategory>"

		Update is an Update Action
			completion message is "InvoiceApprovalAssignmentUpdated"
			Action Rules
				constraint (FinanceResourceInvApprovalRel not exists)
					"InvoiceApprovalAssignmentAlreadyExistsForResource<FinanceResource.PreferredSimplePresentationName>Role<InvoiceApprovalAuthCode>Category<InvoiceRoutingCategory>"

		Delete is a Purge Action
			default label is "Delete"
			completion message is "InvoiceApprovalAssignmentDeleted"

		Purge is a Purge Action
			restricted

		DeleteInvoiceApprovalAssignments is a Set Action
			restricted
			
			Instance Selection
				where (true)
			
			Action Rules
				Instance Rules
					invoke Purge
