MatchReconQueueSet is a BusinessClass
    owned by ma
    prefix is MRQs

    Ontology
        symbolic key is MatchReconQueueSet

    Patterns
        implements StaticJava
        disable AuditIndex
        disable Auditing

    Persistent Fields
		PurchaseOrder
		Type				is Alpha 1
			States
				Automatic	value is 1
				Cost		value is 2
				Quantity	value is 3
				Manual 		value is 4 
				
	Derived Fields
		AutomaticMessage is a MessageField
			restricted
			"Automatic"
			
		CostMessage is a MessageField
			restricted
			"Cost"

		QuantityMessage is a MessageField
			restricted
			"Quantity"

		TypeMessage is a DerivedField
			type is Alpha 20
			restricted
			if (Type.Automatic)
				return AutomaticMessage
			else
			if (Type.Cost)
				return CostMessage
			else
			if (Type.Quantity)
				return QuantityMessage
				

		InvoiceLineQuantity is a DerivedField
			type is like Quantity
			return sum ReconQueueInvoiceDetailRel.MatchedQuantity

		InvoiceLineExtendedCost is a DerivedField
			type is like InternationalCost
			return sum ReconQueueInvoiceDetailRel.DerivedExtendedMatchAmount

		ReceiptLineQuantity is a DerivedField
			type is like Quantity
			return sum ReconQueueReceiptLineRel.DerivedReceiptLineQuantity

		ReceiptLineExtendedCost is a DerivedField
			type is like InternationalCost
			return sum ReconQueueReceiptLineRel.DerivedReceiptExtendedCost

		ContractLineExtendedCost is a DerivedField
			type is like InternationalCost
			return sum ReconQueueInvoiceDetailRel.DerivedContractLineDiscrepancy

		Discrepancy is a DerivedField
			type is like InternationalAmount
			if (PayablesInvoice.MatchProcessType.ServiceContract)
				return InvoiceLineExtendedCost - ContractLineExtendedCost
			else			
				return InvoiceLineExtendedCost - ReceiptLineExtendedCost

		OverbillDiscrepancy is a DerivedField
			type is like InternationalAmount
				
			return sum ReconQueueInvoiceDetailRel.OverbillAmount				


		InvoiceQuantity is a DerivedField
			type is like Quantity
			return sum InvoicesRel.DerivedQuantity

		InvoiceExtendedAmount is a DerivedField
			type is like InternationalCost
			return sum InvoicesRel.DerivedExtendedAmount

		ReceiptQuantity is a DerivedField
			type is like Quantity
			return sum ReceiptsRel.DerivedQuantity

		ReceiptExtendedAmount is a DerivedField
			type is like InternationalCost
			return sum ReceiptsRel.DerivedExtendedAmount

		DiscrepancyIM is a DerivedField
			type is like InternationalAmount
			return InvoiceExtendedAmount - ReceiptExtendedAmount

		OverbillDiscrepancyIM is a DerivedField
			type is like InternationalAmount
			if  (InvoiceExtendedAmount > ReceiptExtendedAmount)
				return InvoiceExtendedAmount - ReceiptExtendedAmount


		InvoiceAOCQuantity is a DerivedField
			type is like Quantity
			return sum InvoiceAOCsRel.MatchedQuantity

		InvoiceAOCExtendedAmount is a DerivedField
			type is like InternationalCost
			return sum InvoiceAOCsRel.DerivedExtendedMatchAmount

		ReceiptAOCQuantity is a DerivedField
			type is like Quantity
			return sum ReceiptAOCsRel.MatchedQuantity

		ReceiptAOCExtendedAmount is a DerivedField
			type is like InternationalCost
			return sum ReceiptAOCsRel.DerivedExtendedMatchAmount

		DiscrepancyAOC is a DerivedField
			type is like InternationalAmount
			return InvoiceAOCExtendedAmount - ReceiptAOCExtendedAmount

		OverbillDiscrepancyAOC is a DerivedField
			type is like InternationalAmount
			if  (InvoiceAOCExtendedAmount > ReceiptAOCExtendedAmount)
				return InvoiceAOCExtendedAmount - ReceiptAOCExtendedAmount

	Relations
        MatchReconQueueRel
            one-to-one relation to MatchReconQueue
            Field Mapping uses symbolic key
                related.Company                 						= Company
                related.PayablesInvoice            						= PayablesInvoice

        ReconQueueInvoiceDetailRel
            one-to-many relation to MatchWorkInvoiceDetail
            Field Mapping uses symbolic key
                related.Company                 						= Company
            	related.MatchWork										= MatchReconQueueRel.MatchWork
                related.PayablesInvoice            						= PayablesInvoice
            Instance Selection
				where (related.MatchReconQueueSet 						= MatchReconQueueSet)

        ReconQueueReceiptLineRel
            one-to-many relation to MatchWorkReceiptLine
            Field Mapping uses symbolic key
                related.Company                 						= Company
            	related.MatchWork										= MatchReconQueueRel.MatchWork
			Instance Selection
				where (related.PayablesInvoice							= PayablesInvoice
				and    related.MatchReconQueueSet 						= MatchReconQueueSet)

        InvoicesRel
            one-to-many relation to MatchWorkInvoice
            Field Mapping uses symbolic key
                related.Company                 						= Company
            	related.MatchWork										= MatchReconQueueRel.MatchWork
            Instance Selection
				where (related.MatchReconQueueSet 						= MatchReconQueueSet
				and    related.IncludeInView)

        ReceiptsRel
            one-to-many relation to MatchWorkReceipt
            Field Mapping uses symbolic key
                related.Company                 						= Company
            	related.MatchWork										= MatchReconQueueRel.MatchWork
			Instance Selection
				where (related.MatchReconQueueSet 						= MatchReconQueueSet
				and    related.Vendor									= MatchReconQueueRel.Vendor
				and    related.IncludeInView)


        InvoiceAOCsRel
            one-to-many relation to MatchWorkInvoiceAddOnCharge
            Field Mapping uses symbolic key
                related.Company                 						= Company
            	related.MatchWork										= MatchReconQueueRel.MatchWork
            Instance Selection
				where (related.PurchaseOrder							= PurchaseOrder
				and    related.MatchReconQueueSet 						= MatchReconQueueSet)

        ReceiptAOCsRel
            one-to-many relation to MatchWorkReceiptLineAOC
            Field Mapping uses symbolic key
                related.Company                 						= Company
            	related.MatchWork										= MatchReconQueueRel.MatchWork
			Instance Selection
				where (related.PurchaseOrder							= PurchaseOrder
				and    related.MatchReconQueueSet 						= MatchReconQueueSet)

	Sets
		ByDescendingSet
			indexed
			Sort Order
				MatchReconQueueSet descending
				Company
				PayablesInvoice
	
	Actions
		Create is a Create Action
			restricted
			Action Rules 
				if (Type not entered)
					Type = Type.Manual
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
			default label is "Delete"
			Entrance Rules
				invoke UnselectRecordForRecon InvoicesRel
				invoke UnselectRecordForRecon ReceiptsRel
				invoke UnselectRecordForRecon ReconQueueInvoiceDetailRel
				invoke UnselectRecordForRecon ReconQueueReceiptLineRel
		        invoke UnselectRecordForRecon InvoiceAOCsRel
       			invoke UnselectRecordForRecon ReceiptAOCsRel
								
