ReportDistributionProfile is a BusinessClass
    owned by report
    prefix is DGrpP
    default label is "DistributionProfile"

    Ontology
    	symbolic key is ReportDistributionProfile

	Patterns
        disable AsOfDateProcessing
        disable EffectiveDated
        disable Auditing

    Persistent Fields
    	Description is Alpha 50
    	ReportOptions
		ActorGroup
		DistributionPfiService is a PfiServiceDefinition
			default label is "ProcessAutomationServiceName"
		SendToEmailAddress				  is an EmailAddressMulti
			default label is "To"
		SendToEmailFrom					  is an EmailAddress
			default label is "From"
		SendToEmailCc					  is an EmailAddressMulti
			default label is "Cc"
		SendToEmailBcc					  is an EmailAddressMulti
			default label is "Bcc"
		SendToEmailSubjectLine			  is Alpha size up to 250
			default label is "Subject"
		SendToEmailContent				  is Text
			default label is "Content"
 
		IncludeDescendentActorGroups	  is  Boolean
		SuppressBlankReportToActorGroup   is  Boolean    	
		    default label is "SuppressDistributionOfBlankReportToActorGroup\(StaticJavaReportsOnly)"
		SuppressBlankReportToEmail        is  Boolean
		    default label is "SuppressDistributionOfBlankReportToEmail\(StaticJavaReportsOnly)"
		SuppressBlankReportToFlow         is  Boolean
		    default label is "SuppressDistributionOfBlankReportToProcessAutomationService\(StaticJavaReportsOnly)"
		Role
		RoleGroup
		SuppressBlankReportToRole		  is Boolean
			default label is "SuppressDistributionOfBlankReportToRole\(StaticJavaReportsOnly)"
		BurstByActor 					  is Boolean
		AllowDynamicActorSet 			  is Boolean
		ReportEmailOption								
		ReportEmailTemplate
		SuppressUserNotification		  is Boolean
			default label is "SuppressUserNotification"
	
	Transient Fields
    	TransSubmittingActor 				is an Actor 
    		default label is "SubmittingActor"
    		derive value from DerSubmittingActor
    	TransAsyncActionRequest	is an AsyncActionRequest 
    		default label is "AsyncActionRequest"	
    		derive value from DerAsyncActionRequest
    	
	Local Fields
		DistributedEmail 	is Boolean 
		DistributedFlow 	is Boolean 
		LocalUserReport		is a UserReport
		
	Context Fields
		ReportDistributionGroup 

	Derived Fields
		HasReportOptions is a DerivedField
    		type is Boolean
    		default label is "ReportOptionsExist"
			if (ReportOptionsRel exists)
				return true
			else
				return false

		UsedInMultipleDistributionGroups is a DerivedField
    		type is Boolean
			if (instance count of ReportDistributionGroupMemberShipRel > 1)
				return true
			else
				return false

		UsedInNoDistributionGroups is a DerivedField
    		type is Boolean
			if (instance count of ReportDistributionGroupMemberShipRel < 1)
				return true
			else
				return false

		UsedInOneDistributionGroup is a DerivedField
    		type is Boolean
			if (instance count of ReportDistributionGroupMemberShipRel = 1)
				return true
			else
				return false

        UsedInDistributionGroupCount is a DerivedField
            type is Numeric size 5
            return instance count of ReportDistributionGroupMemberShipRel

        DescendentsTargetedText is a DerivedField
            type is Alpha size 10
            restricted
            
            if (IncludeDescendentActorGroups)
            	return "(Targeted)"
        	else
        		return ""

        GroupOnlyTargetedText is a DerivedField
            type is Alpha size 10
            if (!IncludeDescendentActorGroups)
            	return "(Targeted)"
        	else
        		return ""
        		
		DerSubmittingActor is a DerivedField	
			type is Actor
			default label is "SubmittingActor"
			
			if (TransAsyncActionRequest exists)
    			return TransAsyncActionRequest.Actor
    		
    		return actor
			
		DerSubmittingActorEmail is a DerivedField
			type is EmailAddressField
			default label is "SubmittingActorEmail"
			

			 
			if (TransSubmittingActor.ContactInfo.EmailAddress entered)
				return TransSubmittingActor.ContactInfo.EmailAddress
			
			return TransSubmittingActor.ContactInfo.AlternateEmail
			
    	DerTargetActorEmail is a DerivedField
			type is EmailAddressField
			default label is "TargetActorEmail"
			
			if (LocalUserReport.Actor.ContactInfo.EmailAddress entered)
				return LocalUserReport.Actor.ContactInfo.EmailAddress
			
			return LocalUserReport.Actor.ContactInfo.AlternateEmail
		
		RenderedReportEmailTemplateFrom	is a DerivedField
			type is EmailAddressField
			restricted
			return ReportEmailTemplate.From text
						
		RenderedReportEmailTemplateSubject is a DerivedField
			type is Text
			restricted
			return ReportEmailTemplate.Subject text
									
		RenderedReportEmailTemplateBody	is a DerivedField
			type is RichText
			restricted
			return ReportEmailTemplate.Body text
			
		DerAsyncActionRequest is a NativeField
			type is UniqueID
			restricted
			
		DerTimeToExec is a DerivedField
			type is TimeStamp
			default label is "NextScheduledTime"
			restricted
			
			
			if (TransAsyncActionRequest.PendingScheduling)
				return TransAsyncActionRequest.TimeToExec
			
			return blank

	Relations
		ReportDistributionGroupMemberRel
			one-to-one relation to ReportDistributionGroupMember
			Field Mapping uses symbolic key
				related.ReportDistributionGroup 	= ReportDistributionGroup
				related.ReportDistributionProfile 	= ReportDistributionProfile
				
		ReportDistributionGroupMemberShipRel
			one-to-many relation to ReportDistributionGroupMember
			Field Mapping uses ByDistributionProfileDistributionGroup
				related.ReportDistributionProfile 		= ReportDistributionProfile
				
		ReportOptionsRel
			one-to-one relation to ReportOptions
			Field Mapping uses symbolic key
				related.ReportOptions 		= ReportOptions

        ActorGroupMemberShadowRel	
        	one-to-many relation to ActorGroupMemberShadow
        	Field Mapping uses ByActorGroupResidenceGroupActor
        		related.ActorGroup		= ActorGroup
        		related.ResidenceGroup	= ActorGroup

		NonParentReportDistributionGroupsRel
			one-to-many relation to ReportDistributionGroup
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ReportDistributionGroupMemberRel not exist)

		PfiServiceDefinitionRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses symbolic key
				related.PfiServiceDefinition	= DistributionPfiService

		RoleGroupActorsRel
			one-to-many relation to ActorRole
			Field Mapping uses ByRoleAndActor
				related.ActorRole.Role = RoleGroup.RoleGroupMembers.Role 

	Conditions
		MemberOfDistributionGroup
			when (ReportDistributionGroupMemberRel exists)

		NotMemberOfDistributionGroup
			when (!ReportDistributionGroupMemberRel exists)

		InMultipleDistributionGroups
			when (UsedInMultipleDistributionGroups)
			
		InNoDistributionGroups
			when (UsedInNoDistributionGroups)
			
		InOneDistributionGroup
			when (UsedInOneDistributionGroup)
		
		NoActorsInTargetedGroups
			when ((ActorGroup entered)
				and  ((IncludeDescendentActorGroups and ActorGroup.ActorGroupMemberShadow set not exists)
					or (!IncludeDescendentActorGroups and ActorGroup.GroupMemberRel not exists)))
		
		DynamicActorsOnly
			when (AllowDynamicActorSet and DistributionPfiService not entered and SendToEmailAddress not entered and SendToEmailCc not entered and SendToEmailBcc not entered and ActorGroup not entered and Role not entered and not RoleGroup entered)
		
		ActorBasedProfile
			when (ActorGroup entered or Role entered or RoleGroup entered or AllowDynamicActorSet)
	
		ContainsNonDynamicActorBasedProfile
			when (ActorGroup entered or Role entered or RoleGroup entered)
	
		ShowDescendentActorGroups
			when (ActorGroup entered and IncludeDescendentActorGroups)
			
	Field Rules
		ActorGroup
			if (DistributionPfiService not entered and SendToEmailAddress not entered and SendToEmailCc not entered and SendToEmailBcc not entered and Role not entered and not RoleGroup entered and not AllowDynamicActorSet)
				required
					"MustEnterAtLeastOneOfActorGroup,Role,RoleGroup,_\Process_\AutomationServiceName,OrEmailTo,Cc,BccOrChooseAllowDynamicActorSet"

		IncludeDescendentActorGroups
			default to false
			if ((ActorGroup not entered) and (IncludeDescendentActorGroups))
				cannot be entered
					"IncludeDescendentActorGroupsIsOnlyValidWhenActorGroupIsSpecified"
					
		SendToEmailContent
			if (SendToEmailAddress entered or SendToEmailCc entered or SendToEmailBcc entered)
				required
					"EmailContentIsRequiredWhenEmailTo,Cc,OrBccAreEntered"
					
		SuppressBlankReportToActorGroup
		    initial value is true
		
		SuppressBlankReportToEmail
		    initial value is true
		
		SuppressBlankReportToFlow
		    initial value is true
		    
		SuppressBlankReportToRole
			initial value is true
		    
		Role
			if (Role entered and RoleGroup entered)	
				confirmation required
					"AreYouSureYouWantToUseBothARoleAndRoleGroup?"
		
		BurstByActor
			if (BurstByActor)
				constraint (ActorGroup entered or Role entered or RoleGroup entered or AllowDynamicActorSet)
					"BurstByActorRequiresAnActorBasedOptionToBeUsed"
					
				if (DistributionPfiService entered or SendToEmailAddress entered or SendToEmailCc entered or SendToEmailBcc entered or SendToEmailFrom entered or SendToEmailSubjectLine entered or SendToEmailContent entered)
					confirmation required
						"OnlyActorBasedOptionsWillBeUsedForBursting,OtherOptionsWillBeCleared.Continue?"
					
					initialize DistributionPfiService
					initialize SendToEmailAddress
					initialize SendToEmailCc
					initialize SendToEmailBcc
					initialize SendToEmailFrom
					initialize SendToEmailSubjectLine
					initialize SendToEmailContent
				
				SuppressBlankReportToActorGroup = true
				SuppressBlankReportToEmail = true
				SuppressBlankReportToFlow = true
				SuppressBlankReportToRole = true
				
		ReportEmailTemplate
			if (ReportEmailOption = ReportEmailOption.DefinedMessageOnly)
				required

	Actions
	
        Create is a Create Action
        
        	Action Rules
				if (DistributionPfiService entered)
					constraint (PfiServiceDefinitionRel exists)
						"Process_\AutomationServiceDoesNotExist"

        Update is an Update Action
        
        	Action Rules
				if (DistributionPfiService entered)
					constraint (PfiServiceDefinitionRel exists)
						"Process_\AutomationServiceDoesNotExist"

        Delete is a Delete Action

		AddProfileToDistributionGroup is an Instance Action
		
			Parameters
				ReportDistributionGroup
			
			Parameter Rules
				ReportDistributionGroup
					required
					
					if (ReportDistributionGroup.ReportDistributionGroupMember set exists)
						if (BurstByActor)
							if (not ReportDistributionGroup.BurstOnlyReportDistribution and not ReportDistributionGroup.HasBurstReportDistribution) 
								confirmation required
									"ReportDistributionGroupContainsNonBurstProfiles.AreYouSureYouWantToMixWithABurstProfile?"
						else
							if (ReportDistributionGroup.BurstOnlyReportDistribution) 
								confirmation required
									"ReportDistributionGroupCurrentlyContainsOnlyBurstProfiles.AreYouSureYouWantToMixWithANonBurstProfile?"

			Action Rules
				invoke AddProfileToDistributionGroup ReportDistributionGroupMember
					fill in fields from this instance
					invoked.ReportDistributionGroup = ReportDistributionGroup 

		DistributeReportViaEmail is an Instance Action  
		 	restricted


			Parameters
				PrmName					is Alpha size 80	
					default label is "ReportName"
		    	PrmDocument				is BinaryObject		
		    		default label is "Document"
		    	PrmMimeType				is a MimeType		
		    		default label is "MIME_Type"
		    	
			Action Rules
				if (SendToEmailAddress entered or SendToEmailCc entered or SendToEmailBcc entered)
					send email
						to SendToEmailAddress
						cc SendToEmailCc
						bcc SendToEmailBcc
						from SendToEmailFrom
						subject "<SendToEmailSubjectLine>"
						Attachments
							attachment PrmDocument
								name is PrmName + PrmMimeType.DefaultExtension
								mime type is PrmMimeType
						
						Contents
							"<SendToEmailContent>"
					
					DistributedEmail = true
	
	
        DistributeReportViaFlow is an Instance Action  
            restricted


            Parameters
                PrmReportDocumentID     is UniqueID         
                    default label is "ReportDocumentID"
                PrmName                 is Alpha size 80    
                    default label is "ReportName"
                PrmAssociatedBatchTask  is like BatchTask   
                    default label is "AssociatedJob"
                PrmAssociatedBatchTaskParentStream              is like BatchTask   
                    default label is "AssociatedJobParentJobStream"
                PrmAssociatedBatchTaskSequenceInParentStream    is Numeric size 8   
                    default label is "AssociatedJobSequenceInParentJobStream"
                PrmAssociatedBatchTaskTopLevelStream            is like BatchTask   
                    default label is "AssociatedJobTopLevelJobStream"
                
            Action Rules
                if (DistributionPfiService entered)

					DistributedFlow = true
					
                    trigger DistributionPfiService PA service
                        resume on error
							DistributedFlow = false
                        Variables    
                            PrmReportDocumentID
                                variable name is ReportDocumentID
                            PrmName
                                variable name is ReportName
                            PrmAssociatedBatchTask
                                variable name is AssociatedJob
                            PrmAssociatedBatchTaskParentStream
                                variable name is ParentJobstream
                            PrmAssociatedBatchTaskSequenceInParentStream
                                variable name is ExecutionOrderInParentJobstream
                            PrmAssociatedBatchTaskTopLevelStream
                                variable name is AssociatedTopLevelJobstream
		
		SetLocalFields is an Instance Action 
			restricted
			Parameters
				ParamUserReport 	is an UserReport
				
			Action Rules
				LocalUserReport 	= ParamUserReport
				
