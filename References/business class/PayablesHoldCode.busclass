PayablesHoldCode is a BusinessClass
    owned by ap
    prefix is HLD
    classic name is APHOLDCODE
    default label is "HoldCode"

    Ontology
        symbolic key is PayablesHoldCode
            classic set name is HLDSET1
            name is HoldCode
            classic name is HLD-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    



			
    
    
    Persistent Fields

        Description
        HoldOnVendor  is AlphaUpper size 1
            classic name is HOLD-VEN-FLAG
            States
                TemporaryHold value is "T"
                PermanentHold value is "P"
        HoldOnInvoice is AlphaUpper size 1
            classic name is HOLD-INV-FLAG
            States
                TemporaryHold value is "T"
                PermanentHold value is "P"
        InUse         is Boolean
            classic name is ACTIVITY-FLAG


	Derived Fields
		Count is a ComputeField
			type is Numeric size 10
			
			(instance count of PaymentsOnHoldForCodeRel)

	Conditions
		IsValidForActorContext
			restricted
			when ((actor.context.FinanceEnterpriseGroup != ""
			and   VendorGroup.BusinessGroup.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
			or   (actor.context.FinanceEnterpriseGroup = ""))

		IsHoldOnInvoice
			when (HoldOnInvoice entered)
			
		IsHoldOnVendor
			when (HoldOnVendor entered)

	Field Rules
		HoldOnVendor
			if (HoldOnVendor not entered)
				constraint (HoldOnInvoice entered)
					"MustSelectVendorOrInvoiceHoldFlag"                       


	Relations
        PaymentsOnHoldForCodeSet is a PayablesOnHold set
        	Instance Selection
        		where (related.HoldCode		= HoldCode)
        		
        PaymentsOnHoldForCodeRel
        	one-to-many relation to PayablesOnHold
        	Field Mapping uses symbolic key
        		related.VendorGroup		= VendorGroup
        		related.HoldCode		= HoldCode









	Actions
		Create is a Create Action

		Update is an Update Action
			Action Rules
				if (HoldCode = VendorGroup.ManualPaymentHoldCode
				or  HoldCode = VendorGroup.CreditBalanceHoldCode
				or  HoldCode = VendorGroup.PrepaymentHoldCode
				or  HoldCode = VendorGroup.ReturnOnInvestmentHoldCode
				or  HoldCode = VendorGroup.MinimumPaymentHoldCode
				or  HoldCode = VendorGroup.MaximumPaymentHoldCode)
					if (HoldOnVendor changed)
						constraint (false)
							"CannotChangeVendorGroupHoldCodeInformation"            
					if (HoldOnInvoice changed)
						constraint (false)
							"CannotChangeVendorGroupHoldCodeInformation"            
			

		Delete is a Delete Action
			Action Rules
				constraint (PayablesOnHold set not exists)
					"CannotDelete;HoldCodeIsActive"                           

		SetAsInUse is an Instance Action
			restricted
			Action Rules
				InUse = true
		            
