ReceiptAdjustmentInput is a BusinessClass
    owned by ma
    prefix is W0004

    Ontology
    	symbolic key is ReceiptAdjustmentInput

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
    	RunGroup
        Company                 	is a PurchasingCompany
        DocumentType            	is Alpha size 2
	        States
	            ReceivingAdjustment         value is "RA"
	            LineOnlyAddition            value is "LO"
	            ItemDetail					value is "IC"
        AdjustmentReferenceNumber   is like ReferenceNumber 
        LineNumber                 	is like LineNumber
        ReceiptReferenceNumber      is like PurchaseOrderReceiptImport 
        PurchaseOrder               is like PurchaseOrder    
        InterfacedPurchaseOrder 	is like PurchaseOrderImport
        PoRelease               	is like PORelease
        PoCode                  	is like POCode
        Location                	is like InventoryLocation   		
        Description             	is like Description    
        ItemType                	is like ItemType
        OriginalUnitCost            is like InternationalCost
        AdjustedQuantity          	is like Quantity
        RecComments             	is like ErsComments 
        Bin                     	is like Bin
        Operator                  	is like Operator 
        	holds pii
        Status                  	is Numeric size 1
        TransactionDate             is TimeStamp     
        UpdateDate              	is TimeStamp     
        VendorItem                  is like VendorItem
        VendorPriceUOMQuantity      is like Quantity
        VendorPriceUOM              is like UnitOfMeasure
        VendorPriceUOMMultiplier    is like UOMMultiplier
        VendorBuyUOM                is like UnitOfMeasure
        VendorBuyUOMMultiplier      is like UOMMultiplier
        CatchWeightQuantity         is like Quantity
        ReceivedUOM                	is like UnitOfMeasure
        ReceivedUOMMultiplier       is like UOMMultiplier
        Serial                  	is like ItemSerialNumber
        Lot                     	is like ItemLot
        Sublot                  	is like Sublot
        LotExpirationDate           is Date     
        DetailQuantity              is like Quantity
        HoldFlag                	is Alpha size 1
        TaxCode                 	is like TaxCode
        TaxUsageCode                is like TaxUsageCode		
        AcctUnit                	is like AccountingUnit
        Account                 	is like GeneralLedgerChartAccount	
        SubAcct                 	is like SubAcct
        GlobalDocumentType          is like GlobalDocumentType			
        GlobalLineType              is like GlobalLineType
        AdjustmentAndInspection		is like POReceiptAdjustmentAndInspection
        AdjustmentAndInspectionLine is like POReceiptAdjustmentAndInspectionLine

	Local Fields
    	InterfacedAdjustmentAndInspection		is a POReceiptAdjustmentAndInspection view
    	InterfacedAdjustmentAndInspectionLine	is a POReceiptAdjustmentAndInspectionLine view
    	InterfacedReceiptLine					is a PurchaseOrderReceiptLine view
    	
    	LocalAdjustmentAndInspection			is like POReceiptAdjustmentAndInspection
    	LocalPurchaseOrderReceipt				is like PurchaseOrderReceipt
    	LocalPurchaseOrderReceiptLine			is like PurchaseOrderReceiptLine
    	
		LocalInterfaceResult 					is a PurchaseOrderReceiptInterfaceResult
		LocalRunGroup							is like RunGroup
		LocalCompany							is like Company
						
		ErrorOccurred							is Boolean
		LocalErrorMessage						is Alpha 150


        LocalSequence                  			is like SeqNbr

	Relations
        AdjustmentDetailImportRel
            one-to-many relation to ReceiptAdjustmentInput
            Field Mapping uses ByRunGroup
				related.RunGroup								= RunGroup
                related.Company                    				= Company
                related.DocumentType 							= DocumentType.ItemDetail
                related.AdjustmentReferenceNumber 				= AdjustmentReferenceNumber
                related.ReceiptReferenceNumber 					= ReceiptReferenceNumber
                related.LineNumber 								= LineNumber

    	PurchaseOrderReceiptByReferenceRel
			one-to-many relation to PurchaseOrderReceipt
			Field Mapping uses Set11
				related.Company									= Company
				related.ReferenceNumber							= ReceiptReferenceNumber
				
		PurchaseOrderReceiptRel
			one-to-one relation to PurchaseOrderReceipt
			Field Mapping uses symbolic key
				related.Company									= Company
				related.PurchaseOrderReceipt					= ReceiptReferenceNumber

    	AdjustmentAndInspectionRel
						one-to-many relation to POReceiptAdjustmentAndInspection
			Field Mapping uses symbolic key
				related.Company									= Company
				related.AdjustmentInspectionDocumentType		= DocumentType
			Instance Selection
				where (related.ReferenceNumber					= AdjustmentReferenceNumber)
				
		AdjustmentAndInspectionForReleaseRel
			one-to-one relation to POReceiptAdjustmentAndInspection
			Field Mapping uses symbolic key
				related.Company									= Company
				related.AdjustmentInspectionDocumentType		= AdjustmentInspectionDocumentType.ReceiptAdjustment
				related.POReceiptAdjustmentAndInspection		= LocalAdjustmentAndInspection
				
		PurchaseOrderReceiptLineForReleaseRel
			one-to-one relation to PurchaseOrderReceiptLine
			Field Mapping uses symbolic key
				related.Company									= Company
				related.PurchaseOrderReceipt					= LocalPurchaseOrderReceipt
				related.PurchaseOrderReceiptLine				= LocalPurchaseOrderReceiptLine

    	AdjustmentAndInspectionLineRel
			one-to-one relation to POReceiptAdjustmentAndInspectionLine
			Field Mapping uses symbolic key
				related.Company									= Company
				related.AdjustmentInspectionDocumentType		= DocumentType
				related.POReceiptAdjustmentAndInspection		= AdjustmentAndInspection
				related.POReceiptAdjustmentAndInspectionLine	= AdjustmentAndInspectionLine

    	LocalInterfaceResultsRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= actor.context.FinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult		= LocalInterfaceResult

		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
                related.Company                    			= Company
                related.PurchaseOrderImport 				= InterfacedPurchaseOrder
                related.POCode								= PoCode

        LocalAdjustmentsInRunGroupRel
            one-to-many relation to ReceiptAdjustmentInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= LocalRunGroup

        LocalAdjustmentsInRunGroupCompanyRel
            one-to-many relation to ReceiptAdjustmentInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= LocalRunGroup
                related.Company                     		= LocalCompany

			
    Sets

        ByRunGroup
            Sort Order
            	RunGroup
            	Company
        		DocumentType
        		AdjustmentReferenceNumber
        		ReceiptReferenceNumber
        		LineNumber
        		Bin
        		Serial
        		Lot
        		Sublot

    Field Rules
        		
        RunGroup
        	required
        	
		Company
			required
			
		DocumentType
			required

		AdjustmentReferenceNumber
			if  (DocumentType.ReceivingAdjustment)
				required
				
		ReceiptReferenceNumber
			required
				
		AdjustedQuantity
			if  (DocumentType.LineOnlyAddition)
				required

    Actions

        Create is a Create Action

		Update is an Update Action
		
        Delete is a Delete Action

        FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
			Entrance Rules

        
		DeleteAllTransactionsForRunGroup is a Set Action
			confirmation required
			Parameters
				PrmRunGroup				  is AlphaUpper 30
					default label is "RunGroup"
			Instance Selection
				where (RunGroup	= PrmRunGroup
                and   (Company.FinanceEnterpriseGroup    = actor.context.FinanceEnterpriseGroup
                or     Company not entered))
			Action Rules
				Instance Rules
					invoke FastDelete 

		CreateErrorRecord is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like PurchaseOrderReceiptInterfaceResult
 					
			Action Rules

				LocalInterfaceResult   		= PrmResult
				LocalCompany           		= Company

				invoke Update LocalInterfaceResultsRel
					invoked.Status = 2

				if  (LocalInterfaceResultsRel.MoveErrorsToNewRunGroup)
				
					if  (LocalInterfaceResultsRel.ErrorRunGroup not entered)
						LocalRunGroup					= LocalInterfaceResultsRel.ErrorRunGroupPrefix + "ERRORS_" + RunGroup

						invoke Update LocalInterfaceResultsRel
							invoked.ErrorRunGroup		= LocalRunGroup
					else
						LocalRunGroup					= LocalInterfaceResultsRel.ErrorRunGroup
				else
					LocalRunGroup						= LocalInterfaceResultsRel.RunGroup


				invoke CreateNoRules POReceiptAdjustmentAndInspectionImport
					fill in fields from this instance
						except invoked.RunGroup
					invoked.RunGroup					= LocalRunGroup
					invoked.POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber  = ReceiptReferenceNumber
					invoked.POReceiptAdjustmentAndInspectionImport.DocumentType	= DocumentType
					invoked.POReceiptAdjustmentAndInspectionImport.LineNumber	= LineNumber
					
					invoked.RecordInError				= true
					invoked.ErrorMessage				= PrmErrorMessage
					invoked.InterfaceRun	            = PrmResult

					invoked.AdjustmentAndInspection		= InterfacedAdjustmentAndInspection.POReceiptAdjustmentAndInspection
					invoked.AdjustmentAndInspectionLine	= InterfacedAdjustmentAndInspectionLine.POReceiptAdjustmentAndInspectionLine
					invoked.PurchaseOrderReceiptLine	= InterfacedReceiptLine.PurchaseOrderReceiptLine
						
				initialize LocalSequence

				for each AdjustmentDetailImportRel

					LocalSequence += 1
					invoke CreateNoRules POReceiptInventoryDetailImport
						fill in fields from each
							except invoked.RunGroup
						invoked.RunGroup									= LocalRunGroup
						invoked.PurchaseOrderReceiptImport  				= each.ReceiptReferenceNumber
						invoked.POReceiptInventoryDetailImport.LineNumber	= each.LineNumber
						invoked.POReceiptInventoryDetailImport.Sequence		= LocalSequence


					invoke FastDelete each


				invoke FastDelete 
					
		InterfaceAdjustmentsAndSubstitutions is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup   is an FinanceEnterpriseGroup
					default label is "PrmFinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				ReleaseReceipts				is Boolean


				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PurchaseOrderReceiptInterfaceResult
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"

					if  (PrmCompany entered)
						for each LocalAdjustmentsInRunGroupCompanyRel
							constraint (each.ReceiptReferenceNumber entered)
								"ReceiptReferenceNumberRequiredOnAllRecords"
							constraint (each.DocumentType.ReceivingAdjustment or each.DocumentType.LineOnlyAddition or each.DocumentType.ItemDetail)
								"RunGroupContainsRecordWithInvalidRecordType<DocumentType>(SeeReferenceNumber<each.ReceiptReferenceNumber>)"
					else
						for each LocalAdjustmentsInRunGroupRel
							constraint (each.ReceiptReferenceNumber entered)
								"ReceiptReferenceNumberRequiredOnAllRecords"
							constraint (each.DocumentType.ReceivingAdjustment or each.DocumentType.LineOnlyAddition or each.DocumentType.ItemDetail)
								"RunGroupContainsRecordWithInvalidRecordType<DocumentType>(SeeReferenceNumber<each.ReceiptReferenceNumber>)"

				ReleaseReceipts
					initial value is true

			Local Fields
				LocalInstanceCount						is Numeric 10
				LocalDetailCount						is Numeric 10
				LocalFailedHeaderCount					is Numeric 10
				LocalPOReceiptAdjustmentAndInspection	is like POReceiptAdjustmentAndInspection
				LocalPOReceipt							is like PurchaseOrderReceipt
				LocalPOReceiptLine						is like PurchaseOrderReceiptLine
				LocalSetInterfaceResult					is a PurchaseOrderReceiptInterfaceResult view 		

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = actor.context.FinanceEnterpriseGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany))

			Sort Order
            	RunGroup
            	Company
        		DocumentType
        		AdjustmentReferenceNumber
        		ReceiptReferenceNumber
        		LineNumber
        		Bin
        		Serial
        		Lot
        		Sublot

			Action Rules

				Empty Set Rules
					invoke Create PurchaseOrderReceiptInterfaceResult
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.RunTime						= current timestamp
						invoked.RunGroup					= PrmRunGroup
						invoked.RunType						= 2
						invoked.Company						= PrmCompany
				        invoked.ReleaseReceipts				= ReleaseReceipts
						invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
						invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix
						invoked.Status						= 1 
				
				RunGroup Set Rules
					Entrance Rules
						invoke Create PurchaseOrderReceiptInterfaceResult
							assign result to LocalSetInterfaceResult
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.RunType						= 2
							invoked.Company						= PrmCompany
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix

					Exit Rules
						invoke Update LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
							invoked.InterfaceCounts.PassedHeaderCount			= (LocalInstanceCount - LocalFailedHeaderCount)
							invoked.InterfaceCounts.FailedHeaderCount			= LocalFailedHeaderCount
							invoked.InterfaceCounts.DetailCount					= LocalDetailCount
							if (!LocalSetInterfaceResult.Status = 2)
								invoked.Status        			= 1 
								
				AdjustmentReferenceNumber Set Rules
					Exit Rules
						if (ErrorOccurred not entered)
							if  (DocumentType.ReceivingAdjustment)
								LocalAdjustmentAndInspection = LocalPOReceiptAdjustmentAndInspection
								invoke Unreleased.FastUpdate AdjustmentAndInspectionForReleaseRel.POReceiptAdjustmentAndInspection
									invoked.InterfaceInProcess				= false
		
								if  (ReleaseReceipts)
									invoke ReleaseAdjustment Unreleased AdjustmentAndInspectionForReleaseRel.POReceiptAdjustmentAndInspection
										resume on error
											ErrorOccurred 				= true
											LocalErrorMessage			= error message
									
							else		
							if  (DocumentType.LineOnlyAddition)
								LocalPurchaseOrderReceipt 		= LocalPOReceipt
								LocalPurchaseOrderReceiptLine	= LocalPOReceiptLine
								invoke FastUpdate PurchaseOrderReceiptLineForReleaseRel.PurchaseOrderReceiptLine
									invoked.InterfaceInProcess				= false
	
								if  (ReleaseReceipts)
									invoke Release PurchaseOrderReceiptLineForReleaseRel.PurchaseOrderReceiptLine
										resume on error
											ErrorOccurred 				= true
											LocalErrorMessage			= error message

				Instance Rules
					

					ErrorOccurred									= false
					LocalInterfaceResult 							= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult

					if (DocumentType not entered)
						ErrorOccurred = true
						LocalErrorMessage = "Document Type is required"
						
					else
					if (DocumentType.ReceivingAdjustment)
						LocalInstanceCount += 1
						if (AdjustmentAndInspectionRel exists)
							LocalPOReceiptAdjustmentAndInspection = AdjustmentAndInspectionRel.POReceiptAdjustmentAndInspection
						else
							invoke Unreleased.CreateAdjustment POReceiptAdjustmentAndInspection
								assign result to InterfacedAdjustmentAndInspection
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
									except invoked.Status
									
								invoked.Company								= Company
								invoked.AdjustmentInspectionDocumentType	= DocumentType
								invoked.POReceiptAdjustmentAndInspection	= InterfacedAdjustmentAndInspection.POReceiptAdjustmentAndInspection
								invoked.ReferenceNumber						= AdjustmentReferenceNumber
								if (PurchaseOrderReceiptByReferenceRel exists)
									invoked.PurchaseOrderReceipt			= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
								else
									invoked.PurchaseOrderReceipt			= ReceiptReferenceNumber
								invoked.DocumentDate						= TransactionDate
									
								invoked.InterfaceInProcess					= true
								invoked.OriginatingInterfaceRun				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
								
							LocalPOReceiptAdjustmentAndInspection = InterfacedAdjustmentAndInspection.POReceiptAdjustmentAndInspection


						
						if (ErrorOccurred)
							LocalFailedHeaderCount += 1

						else
							LocalDetailCount += 1
							invoke Unreleased.CreateAdjustment POReceiptAdjustmentAndInspectionLine
								assign result to InterfacedAdjustmentAndInspectionLine
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
									
								invoked.Company									= Company
								invoked.AdjustmentInspectionDocumentType		= DocumentType
								invoked.POReceiptAdjustmentAndInspection		= LocalPOReceiptAdjustmentAndInspection
								invoked.ReferenceNumber							= AdjustmentReferenceNumber
								invoked.DocumentDate							= TransactionDate
								invoked.POReceiptAdjustmentAndInspectionLine	= LineNumber
								invoked.PurchaseOrderReceiptLine				= LineNumber
								invoked.PurchaseOrderLine						= LineNumber
								invoked.UpdateDate								= UpdateDate
								invoked.Bin										= Bin
								invoked.Quantity								= AdjustedQuantity
								invoked.CatchWeightQuantity						= CatchWeightQuantity
								invoked.Status									= Status

							if (!ErrorOccurred)
								AdjustmentAndInspectionLine					= InterfacedAdjustmentAndInspectionLine.POReceiptAdjustmentAndInspectionLine



					else
					if  (DocumentType.LineOnlyAddition)
						if (PurchaseOrder not entered)
							ErrorOccurred = true
							LocalErrorMessage = "Purchase Order is required for Line Only Addition"
						else 
						if (AdjustedQuantity not entered)
							ErrorOccurred = true
							LocalErrorMessage = "Quantity is required for Line Only Addition"
						else
							LocalDetailCount += 1
							invoke CreateFromInterface PurchaseOrderReceiptLine
								assign result to InterfacedReceiptLine
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
									except invoked.Status
									
								invoked.Company								= Company
								invoked.ReferenceNumber						= AdjustmentReferenceNumber
								if (PurchaseOrderReceiptByReferenceRel exists)
									invoked.PurchaseOrderReceipt			= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
									LocalPOReceipt							= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
								else
									invoked.PurchaseOrderReceipt			= ReceiptReferenceNumber
									LocalPOReceipt							= ReceiptReferenceNumber

								invoked.PurchaseOrderReceiptLine			= LineNumber
								invoked.PurchaseOrder						= PurchaseOrder
								invoked.PurchaseOrderLine 					= LineNumber
								invoked.ShipToLocation						= Location
								invoked.EnteredReceivedQuantity				= AdjustedQuantity
								invoked.OriginalReceivedQuantity			= AdjustedQuantity
									
								invoked.InterfaceInProcess					= true
								invoked.OriginatingInterfaceRun				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
								
							LocalPOReceiptLine = InterfacedReceiptLine.PurchaseOrderReceiptLine
								

					
					if (!ErrorOccurred)
						initialize LocalSequence
						for each AdjustmentDetailImportRel
							LocalSequence += 1
							invoke Create PurchaseOrderTransactionDetail
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from each

								if (PurchaseOrderReceiptByReferenceRel exists)
									invoked.PurchaseOrderReceipt			= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
								else
									invoked.PurchaseOrderReceipt			= ReceiptReferenceNumber
								if (LocalPOReceiptAdjustmentAndInspection entered)
									invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= "RA"
									invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= LocalPOReceiptAdjustmentAndInspection
									invoked.SourceDocumentAlpha												= LocalPOReceiptAdjustmentAndInspection
								else
									invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= "PO"
									if (PurchaseOrderReceiptByReferenceRel exists)
										invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric		= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
										invoked.SourceDocumentAlpha											= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
									else
										invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric		= ReceiptReferenceNumber
										invoked.SourceDocumentAlpha											= ReceiptReferenceNumber
								invoked.PurchaseOrderTransactionDetail.LineNumber							= each.LineNumber
								invoked.PurchaseOrderTransactionDetail.TransactionSequence					= LocalSequence
								invoked.Quantity															= AdjustedQuantity
								
							if (ErrorOccurred)
								end for each
								
					if (ErrorOccurred)
						invoke CreateErrorRecord
							invoked.PrmErrorMessage			= LocalErrorMessage
							invoked.PrmResult				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult

					else
					if (!ErrorOccurred)






						invoke FastDelete 
								

									





















































