PfiFlowDefinition is a BusinessClass
    owned by pfi
    prefix is PfiFD
    default label is "ProcessDefinition"
    framework type is ProcessFlow
    disable data area copy
		preserve target data

    Ontology
        symbolic key is PfiFlowDefinition

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements AuditLogEntryActions

    Persistent Fields

        Description              is a PfiDescription
        FlowType                 is Numeric size 1
            default label is "ProcessType"
            States
                SystemDefined      value is 1
                UserDefined        value is 2
                ApplicationDefined value is 3
        NotifyByEmail            is Numeric size 1
            States
                None                     value is 0
                OnCompletion             value is 1
                OnFailure                value is 2
                Always                   value is 3
                OnFailureOrActivityError value is 4
        IsFlowEnabled            is Boolean
            default label is "ProcessIsEnabled"
        CaptureActivityTimes     is Boolean
            default label is "CaptureActivityStartAndEndTimes"
        FlowLogLevel             is Numeric size 2
            States
                None                value is 1
                    default label is "Disabled"
                WorkunitOnly        value is 2
                    default label is "Enabled"
            default label is "WorkUnitLogging"
        CurrentFlowVersion       is Numeric size 5
            default label is "CurrentProcessVersion"
        LogBufferSize is Numeric size 6
        EmailTo                  is Text
        EmailCC                  is Text
        EmailSubject             is Text
        EmailMessage             is Text
        VariableNames            is a PfiVariableNames
        PruneDefinition          is a PfiPruneFields
        ActCntSoftLimitEnabled   is Boolean
            default label is "EnableActivityCountSoftLimit"
        ActCntSoftLimit          is Numeric size 6
            default label is "ActivityCountSoftLimit"
        ActCntHardLimitEnabled   is Boolean
            default label is "EnableActivityCountHardLimit"
        ActCntHardLimit          is Numeric size 6
            default label is "ActivityCountHardLimit"
        ActCntEmailTo            is Alpha size 100
            default label is "ActivityLimiterEmailTo"
        ActCntEmailFrom          is Alpha size 100
            default label is "ActivityLimiterEmailFrom"
        IsSubProcess             is Boolean
            default label is "Sub-process"
        CancelCheckFrequency     is Numeric size 1
            States
                Immediate    value is 0
                Intermittent value is 1
        CancelCheckSeconds       is Numeric size 3
        Configuration is a PfiConfiguration
            default label is "EmailConfiguration"
        Hash                      is Alpha size up to 64
        StagedFlowVersion is Numeric size 5
        IsStagedSubProcess        is Boolean
            default label is "StagedSubProcess"
        ErrorLimit                is Numeric size 6
            default label is "Error Limit Before Termination"
        ClassicAutoRestart        is Numeric size 1
            States
                Disabled            value is 0
                Enabled             value is 1
        ClassicAutoRestartUpdateStatus   is Numeric size 1
            States
                Unprocessed         value is 0
                AutoRestartDisabled value is 1
                AutoRestartEnabled  value is 2
                NotApplicable       value is 3
                
		Checkpoint is Numeric size 1
			States
				Enabled value is 0
				Disabled value is 1
				
		ContinueWithCheckPointError is Numeric size 1
			default label is "ContinueWithCheckpointError"
			States
				Default value is 0
					default label is "Default(Yes)"
				Yes		value is 1
				No		value is 2
				
		
		AutoRestartOverride is Numeric size 1
			default label is "AutoRestart"
			States
				Enabled  value is 0
				Disabled value is 1
				
		RestartCount is Numeric size 1
		PauseTime is Numeric size 3
			default label is "PauseTimeInSeconds"
		KeepWorkUnitActive is Boolean
				
    Derived Fields

		IsAutoRestartValid is a DerivedField
			type is Boolean
			if (AutoRestartEnabled and AutoRestartOverride = AutoRestartOverride.Enabled)
				return true
			return false
			
		IsValidPauseTime is a DerivedField
			type is Boolean
			if (not KeepWorkUnitActive or PauseTime <= 300)
				return true
			return false
			
        IsStagedVersionSubProcess is a DerivedField
            type is Boolean
            default label is "StagedVersionSubProcess"
            if (PfiStagedFlowVersionRel.exists)
                return last PfiStagedFlowVersionRel.PfiFlowVersion.IsStagedSubProcess
            return false

        MaxStagedFlowVersion is a DerivedField
            type is Numeric size 5
            if (PfiStagedFlowVersionRel.exists)
                return last PfiStagedFlowVersionRel.PfiFlowVersion
            return 0

        MaxFlowVersion is a DerivedField
            type is Numeric size 5
            if (PfiFlowVersionRel.exists)
                return last PfiFlowVersionRel.PfiFlowVersion
            return 0

        CurrentFlowXml is a DerivedField
            type is Text
            return PfiCurrentFlowVersionRel.FlowXml

        AutoRestart is a NativeField
            type is Numeric size 1
            default label is "CurrentProcessAutoRestartCode"
      
        AutoRestartText is a DerivedField
            type is Alpha size 20
            default label is "CurrentProcessAutoRestart"
            if (AutoRestartEnabled)
                return "Enabled"
            else
                return "Disabled"
      
        IsNewProcess is a DerivedField
            type is Boolean
            default label is "NewProcess"
            if (VersionsExist)
                return false
            else
                return true
      
        NeedEmailInfo is a DerivedField
            type is Boolean
            if (NotifyByEmail.OnCompletion
                or NotifyByEmail.OnFailure
                or NotifyByEmail.Always
                or NotifyByEmail.OnFailureOrActivityError)
                return true
            else
                return false

        Url is a NativeField
            type is Text

        Urlv2 is a NativeField
            type is Text

        IPDWebLinkbackUrl is a DerivedField
            type is Text
            restricted
            return linkback(webapp is ProcessDesigner navigation is IPDCompositeFormNav) 
        
        CanMoveProcess is a NativeField
            type is Boolean

        AccessType is a NativeField
            type is Numeric size 1

        NotificationWorkUnit is a StringField
            type is Alpha size 15
            "<!WorkUnit>"
        NotificationDescription is a StringField
            type is Alpha size 15
            "<!Description>"
        NotificationStatusText is a StringField
            type is Alpha size 15
            "<!_StatusText>"

        NotificationField is a MessageField
            "NotificationFrom_\Infor_\Process_\Server:_\Work_\Unit:<NotificationWorkUnit>_\Description:<NotificationDescription>_\Status:<NotificationStatusText>"


    Conditions
    
        VersionsExist
            when (PfiFlowVersion set exists)

        CanViewProcess
            when (CanEditProcess or
                (AccessType = 1))

        CanEditProcess
            when (AccessType = 2)

        AutoRestartEnabled
            when (AutoRestart = 1)
            
        AutoRestartInXmlToggle
            when (stackconfig(ipa).AutoRestartInXml = "true" or stackconfig(ipa).AutoRestartInXml = "")
            
		IsPruningChangeDisabled
            default label is untranslatable
            when (stackconfig(ipa).disablePruningChange = true)
                       
    Relations

        PfiCurrentFlowVersionRel
            one-to-one relation to PfiFlowVersion
            Field Mapping uses symbolic key
                related.PfiFlowDefinition = PfiFlowDefinition
                related.PfiFlowVersion    = CurrentFlowVersion

        PfiActiveReceiverRel
            one-to-many relation to PfiReceiver
            Field Mapping uses ByProcessDefinitionReceiver
                related.PfiFlowDefinition = PfiFlowDefinition
            Instance Selection
                where (related.IsActive)

        PfiStagedFlowVersionRel
            one-to-many relation to PfiFlowVersion
            Field Mapping uses ByStagedFlowVersion
                related.IsStaged = true
                related.PfiFlowDefinition = PfiFlowDefinition

        PfiFlowVersionRel
            one-to-many relation to PfiFlowVersion
            Field Mapping uses ByStagedFlowVersion
                related.IsStaged = false
                related.PfiFlowDefinition = PfiFlowDefinition

    Sets

        ByDescription
            sql name is PfiFD01
            not indexed
            Sort Order
                Description
                PfiFlowDefinition

    Field Rules

        LogBufferSize
            initial value is 10000
            default to 10000

        IsFlowEnabled
            initial value is true
            default to true

        CaptureActivityTimes
            initial value is false
            default to false

        FlowLogLevel
            default to FlowLogLevel.WorkunitOnly

        FlowType
            default to FlowType.UserDefined

        EmailTo
            default to "<!_ProcessOriginatorEmailAddress>"

        EmailSubject
            default to NotificationField

        PruneDefinition.PruneWorkunitData
            default to PruneDefinition.PruneWorkunitData.None
            initial value is PruneDefinition.PruneWorkunitData.None

        PruneDefinition.PruneAfterDays
            default to 1
            initial value is 1

        ActCntSoftLimitEnabled
            default to true
            initial value is true

        ActCntSoftLimit
            default to 10000
            initial value is 10000
            if (ActCntSoftLimitEnabled)
                required
                constraint (ActCntSoftLimit > 0)
                    "ActivityCountSoftLimitMustBeGreaterThanZero"
                constraint (ActCntSoftLimit <= 999000)
                    "ActivityCountSoftLimitMustBeLessThanOrEqualTo999000"
                constraint (!ActCntHardLimitEnabled or ActCntSoftLimit < ActCntHardLimit)
                    "ActivityCountSoftLimitMustBeLessThanActivityCountHardLimit"

        ActCntHardLimitEnabled
            default to true
            initial value is true

        ActCntHardLimit
            default to 999000
            initial value is 999000
            if (ActCntHardLimitEnabled)
                required
                constraint (ActCntHardLimit > 0)
                    "ActivityCountHardLimitMustBeGreaterThanZero"
                constraint (ActCntHardLimit <= 999000)
                    "ActivityCountHardLimitMustBeLessThanOrEqualTo999000"
                constraint (!ActCntSoftLimitEnabled or ActCntHardLimit > ActCntSoftLimit)
                    "ActivityCountHardLimitMustBeGreaterThanActivityCountSoftLimit"

        CancelCheckFrequency
            default to CancelCheckFrequency.Intermittent
            initial value is CancelCheckFrequency.Intermittent

        CancelCheckSeconds
            default to 10
            initial value is 10
            if (CancelCheckFrequency.Intermittent)
                required

        ErrorLimit
            default to 5000
            initial value is 5000
            constraint (ErrorLimit > 0)
                        "ValueMustBeGreaterThanZero"

        ClassicAutoRestart
            default to ClassicAutoRestart.Disabled
            initial value is ClassicAutoRestart.Disabled

        ClassicAutoRestartUpdateStatus
            default to ClassicAutoRestartUpdateStatus.Unprocessed
            initial value is ClassicAutoRestartUpdateStatus.Unprocessed

		AutoRestartOverride
			default to AutoRestartOverride.Enabled
			initial value is AutoRestartOverride.Enabled
		
		RestartCount
			default to 3
			initial value is 3
		
		PauseTime
			default to 60
			initial value is 60
			constraint (IsValidPauseTime)
				"PauseTimeMustBeLessThan300SecondsForKeepWorkUnitActiveOption"
				
    Actions

        Create is a Create Action
        
        CreateWithVersion is a Create Action
            restricted

            Parameters
                ParamFlowDefinition is a PfiFlowDefinition
                    default label is "FlowDefinition"
                ParamDescription    is a PfiDescription
                    default label is "Description"
                ParamFlowXml        is Text
                    default label is "FlowXml"
                ParamIsFlowEnabled  is Boolean
                    default label is "FlowEnabled"
                ParamFlowType       is Numeric size 1
                    default label is "FlowType"
                ParamFlowLogLevel   is Numeric size 1
                    default label is "FlowLogLevel"
                ParamHash           is Alpha size up to 64
                    default label is "Hash"
                ParamSubProcess     is  Boolean
                    default label is "SubProcess"

            Action Rules
                invoke Create this instance
                    invoked.PfiFlowDefinition  = ParamFlowDefinition
                    invoked.Description        = ParamDescription
                    invoked.IsFlowEnabled      = ParamIsFlowEnabled
                    invoked.FlowType           = ParamFlowType
                    invoked.FlowLogLevel       = ParamFlowLogLevel
                    invoked.Hash               = ParamHash
                    invoked.IsSubProcess       = ParamSubProcess
                    invoked.CurrentFlowVersion = 1
            Exit Rules
                invoke AddProcessVersion
                    invoked.ParamFlowXml = ParamFlowXml

        UpdateWithVersion is an Instance Action
            restricted

            Parameters
                ParamFlowXml        is Text
                    default label is "FlowXml"
                ParamHash           is Alpha size up to 64
                    default label is "Hash"
                ParamSubProcess     is  Boolean
                    default label is "SubProcess"

            Action Rules
                Hash         = ParamHash
                IsSubProcess = ParamSubProcess

            Exit Rules
                invoke AddProcessVersion
                    invoked.ParamFlowXml = ParamFlowXml

        UpdateVersionNumber is an Update Action
            restricted

            Action Rules
                CurrentFlowVersion = MaxFlowVersion
                StagedFlowVersion  = MaxStagedFlowVersion
                IsStagedSubProcess = IsStagedVersionSubProcess

        Update is an Update Action

        Delete is a Delete Action

        MakeStagedVersionAsCurrentVersion is an Instance Action
            restricted

            Action Rules
                if(StagedFlowVersion > CurrentFlowVersion)
                    CurrentFlowVersion = StagedFlowVersion
                StagedFlowVersion  = 0
                IsSubProcess       = IsStagedSubProcess

            Exit Rules
                for each PfiStagedFlowVersionRel
                    invoke Update each
                        invoked.IsStaged = false

        AddProcessVersion is an Instance Action
            restricted

            Parameters
                ParamFlowXml is Text
                    default label is "FlowXml"

            Action Rules
                invoke Create PfiFlowVersion
                    invoked.PfiFlowDefinition = PfiFlowDefinition
                    invoked.FlowXml           = ParamFlowXml

        MoveToUserDefinedProcess is a Set Action
            restricted
            Instance Selection
                where (CanMoveProcess)

            Action Rules
                Instance Rules
                    FlowType = FlowType.UserDefined

        PruneWorkunits is a Set Action
            default label is "ScheduleWorkUnitDataCleanup"
            run in background

            Instance Selection
                where (PruneDefinition.PruneWorkunitData.Scheduled)

            Action Rules
                Instance Rules
                	if (IsPruningChangeDisabled)
                		invoke PruneWorkunitOld PfiWorkunit
                        	invoked.ProcessName = PfiFlowDefinition
                        	invoked.DaysToRetainBeforePruning = PruneDefinition.PruneAfterDays
                    else
	                    invoke PruneWorkunit PfiWorkunit
	                        invoked.ProcessName = PfiFlowDefinition
	                        invoked.DaysToRetainBeforePruning = PruneDefinition.PruneAfterDays
