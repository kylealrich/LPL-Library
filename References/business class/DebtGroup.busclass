DebtGroup is a BusinessClass
	owned by treasury
	prefix is DMDG	

	Ontology
		symbolic key is DebtGroup
		
	Persistent Fields								
		Description		  is Alpha up to 250
		Active

	Local Fields
		NewParentDebtGroup is a DebtGroup
		
	Derived Fields
		NewParentDebtGroupIsAChildDebtGroup is a DerivedField
			type is Boolean
			restricted
   			for each NewParentDebtGroup and ancestors
				if (each.ParentDebtGroup = DebtGroup)
					return true
					end for each
					
	Field Rules
		Description
			default to DebtGroup
		
		Active
			initial value is true
	
	Conditions
		HasParentDebtGroup
			restricted
    		when (ParentDebtGroup entered)
    		
    	HasChild
    		restricted
    		when (DebtGroup children exist)
    				
  	Attach Rules
		constraint (Active)
			"DebtGroupIsInactive"
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
			
		ReparentDebtGroup is an Instance Action
			Parameters
				PrmParentDebtGroup is a DebtGroup
				
			Parameter Rules
				PrmParentDebtGroup
					required
						"NewDebtGroupIsRequired"
						
					constraint (PrmParentDebtGroup != DebtGroup)
						"CannotSelectDebtGroupToBeItsOwnParent"
						
					constraint (PrmParentDebtGroup != ParentDebtGroup)
						"NewParentIsTheSameAsCurrentParent"
						
			Action Rules
				NewParentDebtGroup = PrmParentDebtGroup
				
				constraint (!NewParentDebtGroupIsAChildDebtGroup)
					"CannotChooseAChildDebtGroupToBeTheNewDebtGroup"
					
				ParentDebtGroup = NewParentDebtGroup
				
		RemoveParentDebtGroup is an Instance Action
			valid when (HasParentDebtGroup)
			Action Rules
				initialize ParentDebtGroup
				
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted	
