IntercompanyBillingDistributionCode is a BusinessClass
	owned by intercobilling
	prefix is ICBDC
	sql name is "ICBDistributionCode"
	representative text is "<IntercompanyBillingDistributionCode>-<Description>"

	Ontology
		symbolic key is IntercompanyBillingDistributionCode

	Patterns
		disable AuditIndex
		implements ContextualParent
    
	Persistent Fields
		Description
		Type						is AlphaUpper 1
			States
				Payables		value is "P"
				Receivables		value is "R"
		TotalPercent				is Percent size 6.3
			protected
		Active
    

	Field Rules
		Description
			default to IntercompanyBillingDistributionCode
		Type
			required
				"TypeIsRequired"
		TotalPercent
			constraint (TotalPercent <= OneHundredPercent)
				"PercentTotalCannotBeGreaterThan_100;\Percent_\Total=<TotalPercent>"


	Attach Rules
		if (parentcontext.name != "IntercompanyBillingDistributionCodeDetail")
			constraint (IntercompanyBillingDistributionCodeDetail set exists)
				"DistributionCodeDoesNotHaveAnyDetails"

		constraint (Active)
			"DistributionCodeIsNotActive"		


	Derived Fields
		OneHundredPercent is a DerivedField
			type is Percent size 6.3
			restricted
			return 100.000%


	Conditions
		DisplayPercentWarning
			restricted
			when (TotalPercent != OneHundredPercent)


	Relations
		IntercompanyBillingDistributionCodeDetailRel
			one-to-many relation to IntercompanyBillingDistributionCodeDetail
			delete cascades
			Field Mapping uses symbolic key
				related.IntercompanyBillingGroup			= IntercompanyBillingGroup
				related.IntercompanyBillingDistributionCode	= IntercompanyBillingDistributionCode

		IntercompanyBillingRechargeItemRel
			one-to-many relation to IntercompanyBillingRechargeItem
			Field Mapping uses symbolic key
				related.IntercompanyBillingGroup			= IntercompanyBillingGroup
			Instance Selection
				where (related.PayablesDistributionCode		= IntercompanyBillingDistributionCode
				or     related.ReceivablesDistributionCode	= IntercompanyBillingDistributionCode)


	Sets
		ByType
			indexed
			Sort Order
				IntercompanyBillingGroup
				Type
				IntercompanyBillingDistributionCode


	Actions
		Create is a Create Action
			Action Rules
				Active = true

		Update is an Update Action
			Action Rules

		Delete is a Delete Action
			Entrance Rules
				constraint (IntercompanyBillingRechargeItemRel not exists)
					"CannotDelete;DistributionCodeIsUsedOnARechargeItem"

		UpdateTotals is an Instance Action
			restricted
			Parameters
				OldDistributionPercent		is Percent size 6.3
				NewDistributionPercent		is Percent size 6.3
			Action Rules
				TotalPercent = TotalPercent - OldDistributionPercent + NewDistributionPercent
				constraint (TotalPercent <= OneHundredPercent)
					"PercentTotalCannotBeGreaterThan_100;_\PercentTotal=<TotalPercent>"

