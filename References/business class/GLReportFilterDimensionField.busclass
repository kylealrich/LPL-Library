GLReportFilterDimensionField is a BusinessClass
	owned by GeneralLedger
	prefix is GLRFF

	Ontology
		symbolic key is GLReportFilterDimensionField

	Patterns

	Persistent Fields
    	FilterDisplayValue	is Alpha 100
    	IsLeafLevel			is Boolean
    	DimensionLevel		is Numeric 2
    	StructureDimension	is Alpha 200
    	StructureField		is Alpha 200
    	StructureFilter		is Alpha 50
		TopNodeDescription  is Alpha 100

	Local Fields
		LocalDimFilterValues			is Alpha 1000
		LocalDimFilterDisplayValues		is Alpha 1000

	Derived Fields
		Dimension is a DerivedField
			type is Alpha 200
			return GLReportFilterDimension.Dimension
	
		DerivedSummaryAccountYesNo is a DerivedField
			type is Alpha 3
			if IsLeafLevel
				return "No"
			else
				return "Yes"
		
		DerivedFilterValue is a DerivedField
			type is Alpha 1000

			if (IsLeafLevel)
				LocalDimFilterValues = GLReportFilterDimension.Dimension + "." + GLReportFilterDimensionField.FieldName + " = \"" + GLReportFilterDimensionField.FilterValue + "\""
			else
				LocalDimFilterValues = GLReportFilterDimension.Dimension + " level " + DimensionLevel + " = \"" + GLReportFilterDimensionField.FilterValue + "\""
				
			return LocalDimFilterValues
			
		DerivedFilterDisplayValue is a DerivedField
			type is Alpha 1000

			LocalDimFilterDisplayValues = " "
			
			if (IsLeafLevel)
				LocalDimFilterDisplayValues = GLReportFilterDimension.DimensionLabel  + "." + GLReportFilterDimensionField.FieldName + " = \"" + FilterDisplayValue + "\""
			else
				LocalDimFilterDisplayValues = GLReportFilterDimension.DimensionLabel + " level " + DimensionLevel + " = \"" + FilterDisplayValue + "\""
				
			return LocalDimFilterDisplayValues

	Actions
 		Create is an Action
 			restricted

    	Update is an Update Action
    		restricted

    	Delete is a Delete Action    
			Entrance Rules
				if (GLReport.Lock)
					constraint(GLReport.create stamp.actor = actor)
						"ReportHasBeenLockedForEditing"
			Exit Rules 
				if (GLReport.IntegrationStatus = 1)
					invoke UpdateGLReport GLReport
						invoked.PrmIntegrationStatus = 2
			
				if(SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2
			
    	Purge is a Purge Action
    		restricted
		
	Relations 

		SubReportRel
			one-to-many relation to GLReportElement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.SubReport		= GLReport)
