CashAllocationCodeDetail is a BusinessClass
    owned by cashmgmt
    prefix is CMACD

    Ontology
    	symbolic key is CashAllocationCodeDetail

    Persistent Fields
		AllocationAccount 	is a FinanceCodeBlockFull
			default label is "AccountInformation"
		AllocationAmount	is an InternationalAmount
			default label is "Amount"
		AllocationPercent	is Percent 10.7
			default label is "Percent"
		Comment				is Text
	
	Derived Fields
		DerivedCommentLinkName is a DerivedField
			type is Alpha 12
			restricted
			if (Comment entered)
				return ViewCommentMessage
			else
			if (!CashAllocationCode.Active)
				return AddCommentMessage

		AddCommentMessage is a MessageField
			restricted
			"AddComment"		

		ViewCommentMessage is a MessageField
			restricted
			"ViewComment"
								
	Field Rules
		AllocationAccount
			required
		
		AllocationAmount
			default to (CashAllocationCode.ControlTotal - CashAllocationCode.TotalAllocationDetailAmount)
			
			constraint (AllocationAmount > 0)
				"AmountMustBeGreaterThanZero"
				
			constraint (AllocationAmount <= CashAllocationCode.ControlTotal)
				"AmountMustBeLessThanControlTotal"
				
			if (CashAllocationCode.AllocationType.Amount)
				required
					"AmountIsRequired"
			
		AllocationPercent
			default to (100% - CashAllocationCode.TotalAllocationDetailPercent)
			
			constraint (AllocationPercent > 0%)
				"PercentMustBeGreaterThan0%"
				
			constraint (AllocationPercent <= 100%)
				"PercentMustBeLessThan100%"
				
			if (CashAllocationCode.AllocationType.Percent)
				required
					"PercentIsRequired"
					
	Actions
		Create is a Create Action
			valid when (!CashAllocationCode.Active)
			
		Update is an Update Action
			valid when (!CashAllocationCode.Active)
			
		Delete is a Delete Action
			valid when (!CashAllocationCode.Active)
