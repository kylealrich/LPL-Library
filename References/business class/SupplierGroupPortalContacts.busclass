SupplierGroupPortalContacts is a BusinessClass 
    owned by procurement
    prefix is SGPCT
    representative text is "<Contact.Employee.FirstLastNameID>"
    representative image is Contact.Employee.ResourcePicture.File
        display as portrait image
        missing image is random background
            foreground text is "<Contact.Employee.Name.Initials>"

    Ontology
		symbolic key is SupplierGroupPortalContacts
		
	Persistent Fields
		Contact                 	is an Employee
			default label is "Employee"
		Type 						is a PurchasingContactType
		DefaultMessageDialogContact is Boolean
		PODefaultDialogContact  	is Boolean
		APDefaultDialogContact      is Boolean
		DoNotDisplayOnPortal        is Boolean
		        
    Transient Fields

	Derived Fields

		DerivedDescription is a DerivedField
			type is like Description
			return Type.Description

		ContactName is a DerivedField
			type is Alpha up to 101
			return Contact.FirstLastName
			
		ContactEmail is a DerivedField
			type is Alpha up to 100
			return Contact.EmployeeWorkEmailAddress
			
		ContactPhone is a DerivedField
			type is Alpha up to 100
			return Contact.EmployeeWorkTelephone
			
		ContactInitials is a DerivedField
			type is Alpha up to 2
			return Contact.Name.Initials
			
		ContactPhoto is a DerivedField
			type is BinaryDocument
			return Contact.ResourcePicture.File

	Conditions
		EmailAddressExists
			restricted
			when (Contact.UseForWorkEmail entered)
			
		PhoneNumberExists
			restricted
			when (Contact.UseForWorkPhone entered)
			
		PortalContactExists
			restricted
			when (SupplierGroupPortalContacts exists)
				
		DisplayOnPortal
			restricted
			when (!DoNotDisplayOnPortal)

	Relations
	
		DefaultPortalContactRel 
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup	= SupplierGroup
			Instance Selection
				where (DefaultMessageDialogContact = true
				and    related.UniqueID          != UniqueID)                    
 
 		POPortalContactRel 
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup	= SupplierGroup
			Instance Selection
				where (PODefaultDialogContact = true
				and    related.UniqueID          != UniqueID) 
				
		APPortalContactRel 
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup	= SupplierGroup
			Instance Selection
				where (APDefaultDialogContact = true
				and    related.UniqueID          != UniqueID) 
    
    Sets

    	ByType
    		Sort Order
    			SupplierGroup
    			Type
    			Contact

		ByEmployee
			Sort Order
				Contact
				SupplierGroup
				SupplierGroupPortalContacts
				
	Field Rules
		Contact
			required
								
		

	Actions
	
		Create is a Create Action
		
			Action Rules
				constraint (EmailAddressExists)
					"ContactMustHaveAnEmailAddress"
				
			Exit Rules
			
				if ((DefaultMessageDialogContact = true
				and  DefaultPortalContactRel exists)
				or  (APDefaultDialogContact = true
				and  APPortalContactRel exists)
				or  (PODefaultDialogContact = true
				and  POPortalContactRel exists))
					invoke UpdateDefaults
						invoked.ParmSupplierGroup	= SupplierGroup
						invoked.MainContact 		= SupplierGroupPortalContacts
						if (DefaultMessageDialogContact = true)
							invoked.Default     		= true
						if (APDefaultDialogContact = true)
							invoked.APDefault 			= true
						if (PODefaultDialogContact = true)
							invoked.PODefault           = true
									
		Update is an Update Action
			Action Rules
				if ((DefaultMessageDialogContact = true
				and  DefaultPortalContactRel exists)
				or  (APDefaultDialogContact = true
				and  APPortalContactRel exists)
				or  (PODefaultDialogContact = true
				and  POPortalContactRel exists))
					invoke UpdateDefaults
						invoked.ParmSupplierGroup	= SupplierGroup
						invoked.MainContact 		= SupplierGroupPortalContacts
						if (DefaultMessageDialogContact = true)
							invoked.Default     		= true
						if (APDefaultDialogContact = true)
							invoked.APDefault 			= true
						if (PODefaultDialogContact = true)
							invoked.PODefault           = true
		
		UpdateDefaults is a Set Action
			restricted
			Parameters
				ParmSupplierGroup 	is a SupplierGroup
				MainContact  		is a SupplierGroupPortalContacts
				PODefault    		is Boolean
				APDefault    		is Boolean
				Default      		is Boolean
				
			Instance Selection
				
				where (SupplierGroupPortalContacts != MainContact)
			
			Action Rules
			
				Instance Rules
					if (PODefault = true)
						PODefaultDialogContact = false
					if (APDefault = true)
						APDefaultDialogContact = false
					if (Default = true)
						DefaultMessageDialogContact = false			

		Delete is a Delete Action
