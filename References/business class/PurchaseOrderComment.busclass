PurchaseOrderComment is a BusinessClass
	owned by po
	prefix is POCMT
	
	Ontology
		part of PurchaseOrder
			delete cascades
			relative key is SequenceNumber
			
	Patterns
		implements StaticJava
		implements Archivable
	
	Persistent Fields
		CommentTitle					is a CommentName
    	CommentType						is AlphaUpper size 1 
    		States
                PrintOnInternalDocuments	value is "I"
                PrintOnReceivingDocument	value is "R"
                PrintOnPurchaseOrder		value is "P"
                InvoiceComments				value is "N"
                PrintOnDeliveryTicket		value is "D"
				DisplayOnlyOnInternalDocuments value is "S"
                DisplayOnly					value is "O"
                PrintOnPurchaseOrderTrailer	value is "T"
                CopyError					value is "E"
				PurchaseOrderIssue			value is "F"
				NamedPlaceIncoterm			value is "M"
                Asset						value is "4"
		IssueType						is Numeric size 1
			States
				Draft						value is 1
				Final						value is 2
		LatestDocument						is Boolean
    	CommentText
		Attachment							is an AlternateAttachment
		SupplierCanView						
		SendToVendor						is Boolean
        AttachmentSent						is Boolean
        	protected
	Transient Fields
		DisplaySupplierCanView			is Boolean
			derive value from IsSupplierCanView
		TransientPrintOnInternalDocuments	is Boolean
			default label is "PrintOnInternalDocuments"
		TransientPrintOnReceivingDocument	is Boolean
			default label is "PrintOnReceivingDocument"
		TransientPrintOnPurchaseOrder		is Boolean
			default label is "PrintOnPurchaseOrder"
		TransientInvoiceComments			is Boolean
			default label is "InvoiceComments"
		TransientPrintOnDeliveryTicket		is Boolean
			default label is "PrintOnDeliveryTicket"
		TransientDisplayOnlyOnInternalDocuments	is Boolean
			default label is "DisplayOnlyOnInternalDocuments"
		TransientDisplayOnly				is Boolean
			default label is "DisplayOnly"
		TransientPrintOnPurchaseOrderTrailer	is Boolean
			default label is "PrintOnPurchaseOrderTrailer"	
		TransientNamedPlaceIncoterm			is Boolean
			default label is "NamedPlaceIncoterm"	
		TransientAssetComment				is Boolean
			default label is "AssetComment"	

	Local Fields
		ReplaceLineBreak
		LocalCommentType		is like CommentType
		LocalSupplierCanView	is Boolean
		LocalSendToVendor		is Boolean
		LocalAttributeCtr       is Numeric 2

	Derived Fields
		DerivedInitialCommentType	is a DerivedField
			type is like CommentType
			restricted
			if (CommentType entered)
				return CommentType
			else
			if (TransientPrintOnInternalDocuments)
				return CommentType.PrintOnInternalDocuments	
			else
			if (TransientPrintOnReceivingDocument)
				return CommentType.PrintOnReceivingDocument
			else
			if (TransientPrintOnPurchaseOrder)
				return CommentType.PrintOnPurchaseOrder
			else
			if (TransientPrintOnDeliveryTicket)
				return CommentType.PrintOnDeliveryTicket
			else
			if (TransientPrintOnPurchaseOrderTrailer)
				return CommentType.PrintOnPurchaseOrderTrailer
			else
			if (TransientInvoiceComments)
				return CommentType.InvoiceComments
			else
			if (TransientDisplayOnlyOnInternalDocuments)
				return CommentType.DisplayOnlyOnInternalDocuments
			else
			if (TransientDisplayOnly)
				return CommentType.DisplayOnly
			else
			if (TransientNamedPlaceIncoterm)	
				return CommentType.NamedPlaceIncoterm
			else	
			if (TransientAssetComment)
				return CommentType.Asset	
				 
		DerivedCommentText is a DerivedField
			type is Text
			initialize ReplaceLineBreak
			ReplaceLineBreak.Value    	= CommentText
			ReplaceLineBreak.ReplaceTo	= "|"
			return ReplaceLineBreak.Execute		

	Rule Blocks
		ProcessPurchaseOrderComments
			if (IsSupplierCanView)
				invoke UpdateFromPOHeaderComments PurchaseOrder

		CreateComment
			invoke SystemCreate PurchaseOrderComment
				invoked.Company			= Company
				invoked.PurchaseOrder	= PurchaseOrder
				invoked.CommentTitle	= CommentTitle
				invoked.CommentText		= CommentText
				invoked.Attachment		= Attachment
				invoked.CommentType		= LocalCommentType
				invoked.SupplierCanView	= LocalSupplierCanView
				invoked.SendToVendor	= LocalSendToVendor


	Create Rules   
		include IDM.CreateRules 
			replace AttachmentField with Attachment	
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
						
	Conditions
		IsCommentTypeSelected
			restricted
			when (TransientPrintOnInternalDocuments
			or    TransientPrintOnReceivingDocument
			or    TransientPrintOnPurchaseOrder
			or    TransientInvoiceComments
			or    TransientPrintOnDeliveryTicket
			or	  TransientDisplayOnlyOnInternalDocuments
			or    TransientDisplayOnly
			or    TransientPrintOnPurchaseOrderTrailer
			or    TransientNamedPlaceIncoterm
			or 	  TransientAssetComment)
			
		IsNotPrintOnPurchaseOrderOrTrailer
			restricted
			when (not (CommentType.PrintOnPurchaseOrder 
			or 		   CommentType.PrintOnPurchaseOrderTrailer)) 
			
		IsLatestDocument
			restricted
			when (LatestDocument)
		HasAttachment
			restricted
			when (Attachment entered)
		
		IsSupplierCanView
			when (CommentType.PrintOnPurchaseOrder
			or	  CommentType.PrintOnPurchaseOrderTrailer) 
		
		IsSystemGeneratedType
			when (CommentType.CopyError
			or	  CommentType.PurchaseOrderIssue)
		
		CommentChangesNotAllowed
			when (not IsSystemGeneratedType
			and	  not PurchaseOrder.RestrictPOHeaderChanges)
			
		IsIncoTermType
			when (CommentType.NamedPlaceIncoterm)	

		
		ValidForIDMUpload	
			restricted
			when (Attachment.ValidForIDMUpload)



	Field Rules
		SequenceNumber
			autosequence
				minimize contention
			
		CommentType
			required

		CommentTitle
			required

		SendToVendor
			if 	(IsNotPrintOnPurchaseOrderOrTrailer
			and  SendToVendor)
				initialize SendToVendor
				
		SupplierCanView
			if (IsNotPrintOnPurchaseOrderOrTrailer
			and SupplierCanView)
				initialize SupplierCanView














		AttachmentSent
			initialize AttachmentSent							
				
	Relations
		HasAnotherIssueCommentRel
			one-to-many relation to PurchaseOrderComment
			Field Mapping uses ByCommentType
				related.Company			= Company
				related.PurchaseOrder	= PurchaseOrder
				related.CommentType		= CommentType.PurchaseOrderIssue
			Instance Selection
				where (related.IsLatestDocument)

							 

	Sets
		ByCommentType
			Sort Order
				Company	
				PurchaseOrder
				CommentType
				SequenceNumber
				
	Actions
		SystemCreate is a Create Action
			restricted
			Action Rules
				if (IsLatestDocument)
					for each HasAnotherIssueCommentRel
						invoke Update each
							if (each.LatestDocument)
								invoked.LatestDocument	= false
		
		Create is a Create Action
			valid when (not PurchaseOrder.RestrictPOHeaderChanges)
			Field Rules
				CommentType	
					constraint (not IsSystemGeneratedType)
						"CreateIsNotAvailableForTheSelectedCommentType"
						
			Action Rules
				include ProcessPurchaseOrderComments
				
				if (CommentType not entered)
					constraint (IsCommentTypeSelected)
						"CommentTypeIsRequired"
				else
					constraint(not IsCommentTypeSelected)	
						"CannotEnterBothCommentTypeAndTransientCommentTypes"
				
				if (CommentType.NamedPlaceIncoterm
				or	TransientNamedPlaceIncoterm)
					constraint (PurchaseOrder.PurchaseOrderIncotermRel not exists)
						"CannotHaveMultipleIncotermsComments"
				
				initialize LocalSupplierCanView
				initialize LocalSendToVendor
				
				LocalSupplierCanView	= SupplierCanView
				LocalSendToVendor		= SendToVendor
				
				invoke SystemCreate this instance
					invoked.CommentType	= DerivedInitialCommentType
						
																						
			Exit Rules
				initialize LocalCommentType
				if (TransientPrintOnInternalDocuments
				and not CommentType.PrintOnInternalDocuments)
					LocalCommentType = CommentType.PrintOnInternalDocuments
					include CreateComment
						
				if (TransientPrintOnReceivingDocument
				and not CommentType.PrintOnReceivingDocument)
					LocalCommentType = CommentType.PrintOnReceivingDocument
					include CreateComment 					
							
				if (TransientPrintOnPurchaseOrder
				and not CommentType.PrintOnPurchaseOrder)
					LocalCommentType = CommentType.PrintOnPurchaseOrder
					include CreateComment
						
				if (TransientPrintOnDeliveryTicket
				and not CommentType.PrintOnDeliveryTicket)
					LocalCommentType = CommentType.PrintOnDeliveryTicket
					include CreateComment
						
				if (TransientPrintOnPurchaseOrderTrailer
				and not CommentType.PrintOnPurchaseOrderTrailer)
					LocalCommentType = CommentType.PrintOnPurchaseOrderTrailer
					include CreateComment
						
				if (TransientInvoiceComments
				and not CommentType.InvoiceComments)
					LocalCommentType = CommentType.InvoiceComments
					include CreateComment
				
				if (TransientDisplayOnlyOnInternalDocuments
				and not CommentType.DisplayOnlyOnInternalDocuments)
					LocalCommentType = CommentType.DisplayOnlyOnInternalDocuments
					include CreateComment

				if (TransientDisplayOnly
				and not CommentType.DisplayOnly)
					LocalCommentType = CommentType.DisplayOnly
					include CreateComment																										 

				if (TransientNamedPlaceIncoterm
				and not CommentType.NamedPlaceIncoterm)
					LocalCommentType = CommentType.NamedPlaceIncoterm
					include CreateComment																										 
						
				if (TransientAssetComment
				and not CommentType.Asset)												
					LocalCommentType = CommentType.Asset
					include CreateComment		


		CreateFromBatch is a Create Action
			restricted
			valid when (not PurchaseOrder.RestrictPOHeaderChanges)
			Field Rules
				CommentType	
					constraint (not IsSystemGeneratedType)
						"CreateIsNotAvailableForTheSelectedCommentType"
		
		Update is an Update Action
			valid when (not PurchaseOrder.RestrictPOHeaderChanges)



			Field Rules
				CommentType
					if (CommentType changed)
						constraint (not IsSystemGeneratedType)
							"UpdateIsNotAvailableForTheSelectedCommentType"
			Action Rules
				include ProcessPurchaseOrderComments
		
		FastUpdate is an Update Action
			restricted
			bypass field rules
		
		Delete is a Delete Action
			valid when (CommentChangesNotAllowed)
			Action Rules
				include ProcessPurchaseOrderComments

 		Purge is a Purge Action
			restricted
			Action Rules
				Attachment.LocalPurgeTriggered = true

			
		UploadToIDM is an Instance Action
			valid when (ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted

			Accumulators
				InstanceCount

			Instance Selection
				where (not PurchaseOrder.ForHeaderDeletion
				and Attachment.IsLocal)
			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
													
