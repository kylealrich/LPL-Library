GLTransactionInterfaceResult is a BusinessClass
	owned by GeneralLedger
	
	prefix is GLTIR
	default label is "GlobalLedgerTransactionInterfaceResult"
	
	Ontology
		symbolic key is GLTransactionInterfaceResult
		
	Patterns

 		disable EffectiveDated
		disable AsOfDateProcessing

	Context Fields
#ifdef module integration
		FSMInboundBODTracker
#endif

	Persistent Fields
		RunGroup			is AlphaUpper 30
		RunTime				is TimeStamp
		Status				is Numeric 1
			disable Auditing
			States
				Complete	value is 1
				Incomplete 	value is 2
		ErrorRunGroup		is AlphaUpper 30
			disable Auditing
		OriginalRunGroup	is AlphaUpper 30
			disable Auditing
		EditOnly			is Boolean
		PartialUpdate		is Boolean
		PassedCount			is Numeric 12
			default label is "RecordsImported"
		FailedCount			is Numeric 12
			default label is "RecordsWithErrors"
	Transient Fields 
		TransientSenderLogicalID    			is a SenderLogicalID
		TransientSenderComponentID				is a SenderComponentID
		TransientSenderCreationDateTime 		is a SenderCreationDateTime
		TransientSenderBODID					is a SenderBODID
		TransientSenderOriginalBOD			    is a SenderOriginalBOD
		TransientAckBodTriggered				is Boolean
		TransientAccountingEntity				is AlphaUpper size 12
		
	Local Fields
		LocalPulseAlertFrom						is Alpha size 100
		LocalNativeLPLBODTrigger				is Boolean
		LocalSuccessACKTrigger					is Boolean
		LocalMainUserTemplate					is Alpha size 250
#ifdef module integration
		NewBODTracker  							is a FSMInboundBODTracker view
#endif
		LocalFSMInboundBODTracker				is Numeric 15
		Error            						is Boolean
		ErrorMessage     						is Alpha 300
		LocalBODTrigger							is Boolean
		LocalTriggerFrom						is Alpha size 100
		LocalConfigurationParameter				is Alpha size up to 200
		
	Derived Fields
		RecordCount is a DerivedField
			type is Numeric 12
			default label is "RecordsProcessed"
			return (PassedCount + FailedCount)
#ifdef module integration		
		SSJESuccessAckBODXML is a DerivedField
			type is XMLDocument
			restricted
			SSJESuccessAckBODXML = template.IONAcknowledgeSourceSystemJournalEntry_GLTransactionInterfaceResult_Acknowledge_ST document for this instance
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value					
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value	
		
		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedSuccessAckBODID is a DerivedField
			type is Alpha 200
			restricted
			return "infor-nid:" + DerivedTenantID + ":" + DerivedAccoutingEntity + ":" + ":" + RunGroup + ":" + "?SourceSystemJournalEntry&verb=ACK&TrackerID="+LocalFSMInboundBODTracker
		
		DerivedAccoutingEntity is a DerivedField
			type is Alpha 40
			restricted
			return FinanceEnterpriseGroup + DerivedDelimiter + TransientAccountingEntity
		
		DerivedSenderLogicalID is a DerivedField
			type is Alpha 200
			restricted	
			return TransientSenderLogicalID	
		
		DerivedSenderCompID is a DerivedField
			type is Alpha 100
			restricted	
			return TransientSenderComponentID
			
		SSJEPulseAlertXML is a DerivedField
			type is XMLDocument
			restricted
			SSJEPulseAlertXML = template.IONSourceSystemJournalEntry_GLTransactionInterfaceResult_PulseAlert_ST document for this instance	
			
		DerivedAlertMessage is a DerivedField
			type is Alpha 12
			restricted
			if(Status = 1)
				return "Success"
			else
				return "Failed"
		
		SSJEAlertDescription is a DerivedField
			type is Alpha 200
			restricted
			return "Interface Transaction" + DerivedAlertMessage + "for RunGroup" + RunGroup + "."
			
		DerivedSSJEAlertBODID is a DerivedField
			type is Alpha 200
			restricted 
			return	"infor-nid:" + DerivedTenantID +":"+ FinanceEnterpriseGroup +":"+ RunGroup +":"+ "?SourceSystemJournalEntryAlert&verb=Ack&TrackerID="+LocalFSMInboundBODTracker		
#endif			
	Field Rules
		OriginalRunGroup
			if (RunGroup like "*ERRORS_*")
				if (ErrorRunGroupRel exists)
					OriginalRunGroup = first ErrorRunGroupRel.OriginalRunGroup
				else
					OriginalRunGroup = RunGroup
			else
				OriginalRunGroup = RunGroup
		Status
			if(Status.Incomplete)
				if(RunGroupTransactionsBODRel exists )
					invoke TriggerGLTransactionInterface last RunGroupTransactionsBODRel
						invoked.PrmRunGroup = RunGroup
						invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup











			if(Status.Complete)










				if(TransientSenderComponentID entered and TransientSenderLogicalID entered and (!TransientAckBodTriggered))				
					if (FinanceEnterpriseGroup.BODTrigger)
							trigger "GLTransactionInterfaceService" PA service
				            	resume on error
				                title is "EG:<FinanceEnterpriseGroup>RunGroup:<RunGroup>"
				                Criteria
				                	FinanceEnterpriseGroup
				                Variables
				                	RunGroup
				                		variable name is RGroup
				                    TransientSenderLogicalID
				                    	variable name is SLogicalID
									TransientSenderComponentID
										variable name is SComponentID
									TransientSenderCreationDateTime
										variable name is SCreationDateTime
									TransientSenderBODID
										variable name is SBODID
									TransientSenderOriginalBOD
										variable name is SOriginalBOD
									Status	
										variable name is Status
					                FinanceEnterpriseGroup
					                	variable name is FEG
									TransientAccountingEntity
										variable name is AE	
							TransientAckBodTriggered = true
						
	Conditions
		IncompleteErrorsNotMoved
			restricted
			when (Status.Incomplete
			and	  ErrorRunGroup not entered)
		IncompleteErrorsMoved
			restricted
			when (Status.Incomplete
			and	  ErrorRunGroup entered)
		BudgetGroupsExist
			when (AllGLInterfaceBudgetGroupsRel exists)
	
	Relations
	
		UserTemplateRel
			one-to-many relation to UserTemplate
			Field Mapping uses symbolic key
			Instance Selection
				where (related.UserTemplate		= LocalMainUserTemplate)
#ifdef module integration
		FSMBODConfigurationProjectMasterRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "SourceSystemJournalEntry"
            	related.FSMBODConfiguration.Direction 	= 1
		
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
            			
		FSMBODConfigurationAlertRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "PulseAlert"
            	related.FSMBODConfiguration.Direction 	= 1
		
		FSMBODConfigurationDetailRel
        	one-to-many relation to FSMBODConfigurationDetail
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 			= 2	
            	related.FSMBODConfiguration.Noun 			= "PulseAlert"
            	related.FSMBODConfiguration.Direction 		= 1
            Instance Selection
				where (related.Alert						= "FSM_ION_AcknowledgeSourceSystemJournalEntryAlert"
				and	   related.Enable)
				
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 5
            	related.FSMBODConfiguration.Noun 		= "SourceSystemJournalEntry"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
#endif		
		RunGroupTransactionsRel
			one-to-many relation to GLTransactionInterface
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
			Instance Selection
				where (related.GLTransactionInterface.RunGroup = RunGroup)
				
		RunGroupTransactionsBODRel
			one-to-many relation to GLTransactionInterface
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
			Instance Selection
				where (related.GLTransactionInterface.RunGroup = RunGroup and related.SenderBODID entered)
				
		ErrorRunGroupTransactionsRel
			one-to-many relation to GLTransactionInterface
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
			Instance Selection
				where (related.GLTransactionInterface.RunGroup = ErrorRunGroup)	
				
		JournalsCreatedRel
			one-to-many relation to GeneralLedgerJournalControl
			Field Mapping uses ByOriginatingInterfaceRun
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.OriginatingInterfaceRun = GLTransactionInterfaceResult
				
		AllGLInterfaceBudgetGroupsRel
			one-to-many relation to GLInterfaceBudgetGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
			Instance Selection
				where (related.GLInterfaceBudgetGroup.RunGroup	= RunGroup)
				
		GLInterfaceBudgetGroupRel
			one-to-many relation to GLInterfaceBudgetGroup
			Field Mapping uses ByOriginatingInterfaceRun
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.OriginatingInterfaceRun			= GLTransactionInterfaceResult

		ErrorRunGroupRel
			one-to-many relation to GLTransactionInterfaceResult
			Field Mapping uses ByErrorRunGroup
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.ErrorRunGroup		   = RunGroup
				
	Sets
		ByRunTime
			Sort Order
				FinanceEnterpriseGroup
				RunTime descending
				GLTransactionInterfaceResult
		ByRunGroup
			Sort Order
				FinanceEnterpriseGroup
				RunGroup
				GLTransactionInterfaceResult
		ByOriginalRunGroup
			Sort Order
				FinanceEnterpriseGroup
				OriginalRunGroup
				RunTime descending
				GLTransactionInterfaceResult
		ByErrorRunGroup
			Sort Order
				FinanceEnterpriseGroup
				ErrorRunGroup
				GLTransactionInterfaceResult
				
	Actions
		Create is an Action
			restricted
		Update is an Action
			restricted
		Delete is an Action
			Entrance Rules
				for each GLInterfaceBudgetGroupRel
					invoke Purge each
		Purge is a Purge Action	
			Entrance Rules
				for each GLInterfaceBudgetGroupRel
					invoke Purge each										
#ifdef module integration		
		TriggerSSJENativePulseAlert is an Instance Action
			restricted
			Parameters
			Action Rules
				if (FinanceEnterpriseGroup.BODTrigger and FSMBODConfigurationAlertRel.Enable)
          			if(FSMBODConfigurationRel.Enable)
						invoke TriggerPulseAlert FSMBODConfigurationRel
							invoked.PrmActorGroup 	= "GL ADMIN" 
							invoked.PrmMainXML 		= SSJEPulseAlertXML	
							invoked.PrmDescription	= SSJEAlertDescription
							invoked.PrmBODID		= DerivedSSJEAlertBODID

		SendInterfaceSuccessNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				LocalMainUserTemplate = "IONAcknowledgeSourceSystemJournalEntry_GLTransactionInterfaceResult_Acknowledge_ST"	
				if(UserTemplateRel exists)
					send ion bod
						bod is SSJESuccessAckBODXML
						bod type is "Acknowledge.SourceSystemJournalEntry"
						accounting entity is DerivedAccoutingEntity	
		TriggerInterfaceSuccessNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
				PrmTriggerFrom 			  is Alpha size 100
			Action Rules
				LocalTriggerFrom   			 = PrmTriggerFrom
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 5
					invoked.PrmNoun						= "SourceSystemJournalEntry"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= LocalTriggerFrom
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONAcknowledgeSourceSystemJournalEntry_GLTransactionInterface_Acknowledge_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				if(FinanceEnterpriseGroup.BODTrigger and  LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 5
							invoked.Noun 					= "SourceSystemJournalEntry"
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.FinanceEnterpriseGroup = FinanceEnterpriseGroup
							invoked.BODAccountingEntity		= DerivedAccoutingEntity
							invoked.RunGroup				= RunGroup
							invoked.Reference1			 	= GLTransactionInterfaceResult
							invoked.Reference2 				= LocalTriggerFrom
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.FinanceEnterpriseGroup = FinanceEnterpriseGroup
							invoked.BODAccountingEntity		= DerivedAccoutingEntity
							invoked.RunGroup				= RunGroup
							invoked.Reference1			 	= GLTransactionInterfaceResult
							invoked.Reference2 				= LocalTriggerFrom
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendInterfaceSuccessNativeLPLBOD
						resume on error
							Error            								= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= SSJESuccessAckBODXML
							invoked.BODID								= DerivedSuccessAckBODID
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status								= 3
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= SSJESuccessAckBODXML
							invoked.BODID								= DerivedSuccessAckBODID					
#endif
