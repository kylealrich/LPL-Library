ParticipantGroup is a BusinessClass
    owned by procurement
    prefix is PG


    Ontology
    	symbolic key is ParticipantGroup    

    Persistent Fields
    	ParticipantGroupTitle		is a Description
    	Description					is a Description4
    	MemberGroup                 is Boolean
    		default label is "UseForManufacturerAndDistributorContracts"
		AddNewInventoryLocations    is Boolean 
			default label is "AutoAddNewInventoryLocationsToAssociatedContractLocations"
		AddNewRequestingLocations   is Boolean 		
			default label is "AutoAddNewRequestingLocationsToAssociatedContractLocations"
    	
	Local Fields
		OriginalParticipantGroup	is a ParticipantGroup
		LocalContract				is Numeric size 15
        LocalCompany     			is an InventoryCompany
        LocalLocation    			is a InventoryLocation
        LocalReqLocation 			is a RequestingLocation 
        LocalPricingGroup           is a PricingGroup
        LocalDeptCompany     		is Numeric size 4
        LocalDepartment     		is AlphaUpper size 15
        
	Derived Fields
		NumberOfParticipantGroupLines is a ComputeField
			type is Numeric 6
			restricted
			(instance count of ActiveParticipantGroupLineRel)	
		
	Conditions
	
		LinesExist
			restricted
			when (ParticipantGroupLineRel exists)
			
		HasNewLines
			restricted
			when (HasNewLinesRel exists)
			
		HasMixedCompanies
			restricted
			when (MixedCompaniesRel exists)
			
		HasDepartments
			restricted
			when (DepartmentMembersRel exists)
		
		HasContracts
			restricted
			when (ParticipantGroupContractsRel exists)
			
		CompanyOnly 
			restricted 
			when (first ParticipantGroupLineRel.LocationCompanyOnly)
		
		CompanyType
			restricted
			when (first ParticipantGroupLineRel.CompanyType)
			
		LocationType
			restricted
			when (first ParticipantGroupLineRel.LocationType)
			
		HasReportGroups 
			restricted 
			when (ParticipantGroupReportGroupRel exists)
	Relations
		ParticipantGroupLineRel is a ParticipantGroupLine set
					
		ActiveParticipantGroupLineRel 
			one-to-many relation to ParticipantGroupLine
			Field Mapping uses symbolic key
				related.ProcurementGroup                = ProcurementGroup
				related.ParticipantGroup                = ParticipantGroup
			Instance Selection
				where (related.Active)
		
		HasNewLinesRel 
			one-to-many relation to ParticipantGroupLine
			Field Mapping uses symbolic key
				related.ProcurementGroup                = ProcurementGroup
				related.ParticipantGroup                = ParticipantGroup
			Instance Selection
				where (related.ActiveNewForContracts)
		
		FactSheetRel
			one-to-one relation to SampleDocumentTemplate
			Field Mapping uses symbolic key
				related.SampleDocumentTemplate = "PRICINGGROUP_ST"
		
		MixedCompaniesRel is a ParticipantGroupLine set
			Instance Selection
				where (related.MixedCompanies)
		
		DepartmentMembersRel is a ParticipantGroupLine set
			Instance Selection
				where (related.HasDept)
		
		ParticipantGroupContractsRel 
			one-to-many relation to ParticipantGroupContract
			delete cascades
			Field Mapping uses symbolic key
				related.ProcurementGroup                = ProcurementGroup
				related.ParticipantGroup                = ParticipantGroup
		
		ParticipantGroupContractRel 
			one-to-many relation to ParticipantGroupContract
			Field Mapping uses symbolic key
				related.ProcurementGroup                = ProcurementGroup
				related.ParticipantGroup                = ParticipantGroup
				related.Contract                        = LocalContract
		
		ParticipantGroupLineNonStocklessReqLocRel is a ParticipantGroupLine set
			Instance Selection
				where (related.ParticipantGroupLine.ParticipantLocation.RequestingLocation entered
				and    !related.ParticipantGroupLine.ParticipantLocation.RequestingLocation.Stockless)
		
		ContractParticipantRel
			one-to-one relation to ContractParticipant
			Field Mapping uses symbolic key
				related.ContractGroup			  	 										= ProcurementGroup
				related.Contract 					   										= LocalContract
				related.ContractParticipant.ParticipantLocation.Company  					= LocalCompany
				related.ContractParticipant.ParticipantLocation.Location 					= LocalLocation
				related.ContractParticipant.ParticipantLocation.RequestingLocation      	= LocalReqLocation
           		related.ContractParticipant.ParticipantLocation.PricingGroup                = LocalPricingGroup
           		related.ContractParticipant.ContractCompanyDepartment.ContractCompany		= LocalDeptCompany
          		related.ContractParticipant.ContractCompanyDepartment.ContractDepartment	= LocalDepartment
          		
        ContractTierMemberRel
          	one-to-one relation to ContractTierMember
          	Field Mapping uses symbolic key
				related.ContractGroup			  	 				= ProcurementGroup
				related.Contract 					   				= LocalContract
				related.ContractTierMember.Company  				= LocalCompany
				related.ContractTierMember.Location 				= LocalLocation
				related.ContractTierMember.RequestingLocation      	= LocalReqLocation
				related.ContractTierMember.PricingGroup             = LocalPricingGroup
				
		ContractPricingMemberRel
          	one-to-one relation to ContractDistributorPricingMember
          	Field Mapping uses symbolic key
				related.ContractGroup			  	 						= ProcurementGroup
				related.Contract 							   				= LocalContract
				related.ContractDistributorPricingMember.Company  			= LocalCompany
				related.ContractDistributorPricingMember.Location 			= LocalLocation
				related.ContractDistributorPricingMember.RequestingLocation = LocalReqLocation
				related.ContractDistributorPricingMember.PricingGroup       = LocalPricingGroup
				related.ContractDistributorPricingMember.ManufacturerContract = blank

		ContractGroupRel 
			one-to-one relation to ContractGroup 
			Field Mapping uses symbolic key 
				related.ContractGroup 						= ProcurementGroup
		
		ParticipantGroupReportGroupRel 
			one-to-many relation to ParticipantGroupReportGroup 
			delete cascades
			Field Mapping uses symbolic key 
				related.ProcurementGroup					= ProcurementGroup
				related.ParticipantGroup					= ParticipantGroup 
		
  	Field Rules
		ParticipantGroupTitle
			required
				"TitleIsRequired"
		Description
			required
  	
  	Actions     	 	
 		Create is a Create Action
 		
 		Update is an Update Action
	            		
 			Field Rules
 				
 				MemberGroup
 					if (MemberGroup changed)
 						constraint (!HasContracts)
 							"CannotChangeParticipantOrMemberIndicatorWhenAssociatedContractsExist"
 							
 						if (MemberGroup = false)
 							for each ParticipantGroupLineRel
 								invoke Update each
 									invoked.PurchaseMethod = blank
 						if (MemberGroup = true)
 							constraint (!HasDepartments)
 								"CannotChangeToUseForManufacturerAndDistributorContracts;DepartmentMembersExist"
 							for each ParticipantGroupLineRel
 								invoke Update each
 									invoked.AccountingUnit = blank
 		
 		Delete is a Delete Action
    					
    	CreateNewMembersOrParticipantsForNewParticipantGroupLines is an Instance Action
			default label is "UpdateContractsForNewLines"
			valid when (HasNewLines)
			Parameters
				PrmContractGroup              is a ContractGroup  
				PrmParticipantGroup           is a ParticipantGroup             
				
			Parameter Rules
			
				PrmContractGroup
					initial value is ProcurementGroup
					required
					
				PrmParticipantGroup
					initial value is ParticipantGroup
				
			Action Rules
				invoke CreateNewMembersOrParticipantsForNewParticipantGroupLines ContractGroup
					invoked.ContractGroup   	= PrmContractGroup
					invoked.PrmParticipantGroup = PrmParticipantGroup
    	
    	ImportParticipantsFromGroup is an Instance Action  
    		restricted
    		Parameters
				ProcurementGroup
				Contract
				PrmTier               is a ContractTier
 		
 		CreateContractLocationsFromDefaultGroup is an Instance Action 
			restricted 
			Parameters 
				ParmContract          is like Contract 
				LineLevel             is Boolean
				CompanyOption         is Numeric 1

			Action Rules 

				for each ActiveParticipantGroupLineRel 
					invoke CreateLocationsForAContractInstance each 
						invoked.ParmContract 	= ParmContract 
						invoked.LineLevel    	= LineLevel
						invoked.CompanyOption	= CompanyOption
		
		Copy is an Instance Action
			Parameters
				NewParticipantGroup 		is AlphaUpper size 4
				NewParticipantGroupTitle 	is a Description
				NewDescription 				is a Description4
				NewMemberGroup              is Boolean
			Parameter Rules
				NewParticipantGroup 
					required
				NewParticipantGroupTitle
					required
							
			Action Rules
				OriginalParticipantGroup 				= ParticipantGroup
				invoke Create ParticipantGroup
					invoked.ProcurementGroup			= ProcurementGroup
					invoked.ParticipantGroup			= NewParticipantGroup
					invoked.ParticipantGroupTitle		= NewParticipantGroupTitle 
					invoked.Description					= NewDescription
					invoked.MemberGroup                 = NewMemberGroup
					
				if (!NewMemberGroup)
					for each OriginalParticipantGroup.ParticipantGroupLine set
						invoke Create ParticipantGroupLine
							invoked.ProcurementGroup													= ProcurementGroup
							invoked.ParticipantGroup													= NewParticipantGroup
							invoked.ParticipantGroupLine.ParticipantLocation.Company					= each.ParticipantGroupLine.ParticipantLocation.Company	
							invoked.ParticipantGroupLine.ParticipantLocation.Location 					= each.ParticipantGroupLine.ParticipantLocation.Location	
							invoked.ParticipantGroupLine.ParticipantLocation.RequestingLocation			= each.ParticipantGroupLine.ParticipantLocation.RequestingLocation	
							invoked.AccountingUnit														= each.AccountingUnit
				           	invoked.ParticipantGroupLine.ContractCompanyDepartment.ContractCompany		= each.ParticipantGroupLine.ContractCompanyDepartment.ContractCompany
	          				invoked.ParticipantGroupLine.ContractCompanyDepartment.ContractDepartment	= each.ParticipantGroupLine.ContractCompanyDepartment.ContractDepartment
				
				if (NewMemberGroup)
					for each OriginalParticipantGroup.ParticipantGroupLine set
						invoke Create ParticipantGroupLine
							invoked.ProcurementGroup													= ProcurementGroup
							invoked.ParticipantGroup													= NewParticipantGroup
							invoked.ParticipantGroupLine.ParticipantLocation.Company					= each.ParticipantGroupLine.ParticipantLocation.Company	
							invoked.ParticipantGroupLine.ParticipantLocation.Location 					= each.ParticipantGroupLine.ParticipantLocation.Location	
							invoked.ParticipantGroupLine.ParticipantLocation.RequestingLocation			= each.ParticipantGroupLine.ParticipantLocation.RequestingLocation	
							invoked.PurchaseMethod														= each.PurchaseMethod
