HIBCCItemUPN is a BusinessClass
    owned by ic
    prefix is IPN
    classic name is ITEMUPN1

    Ontology
        symbolic key is HIBCCItemUPN
            classic set name is IPNSET1
            classic name is UPN-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        ReplacementUPN                 is AlphaUpper size 20
            classic name is REPL-UPN1
        ManufacturerProductDescription
            classic name is MFG-PD-DESC
        SaleByDistributor              is Boolean
            classic name is SALE-BY-DISTR
        SaleByManufacturer             is Boolean
            classic name is SALE-BY-MFG
        TradeName
        UnitOfMeasure                  is a UnitOfMeasureEdit
            classic name for UnitOfMeasure.UnitOfMeasure is UOM
        UnitOfMeasureCost              is an InternationalCost
            classic name is UOM-COST
                precision is Item.NumberOfDecimalsCost
        ManufacturerName
            classic name is MFG-NAME
        DivisionOfManufacturer
            classic name is MFG-DIVISION
        FromWorksheetResult             is an ItemInformationWorksheetResult 

    Derived Fields

        ItemTypeUPN1 is a ConditionalField
            type is Numeric size 1
            classic name is ITEM-TYPE-UPN1
            restricted
            if (HasHIBCCItemUPN)
                3
            else
                blank

    Conditions

        HasHIBCCItemUPN
            classic name is UPN1-ENTERED
            restricted
            when (HIBCCItemUPN entered)
            
		HIBCCItemUPNDuplicated
			restricted
			when (DuplicateHIBCCItemUPNRel exists)
			
		UOMDuplicated
			restricted
			when (DuplicateUOMRel exists)

		ConsolidatedItemMasterExists
			restricted 
			when (ConsolidatedItemMasterRel exists)            
			
	Relations
	
		DuplicateHIBCCItemUPNRel
			one-to-many relation to HIBCCItemUPN
			Field Mapping uses Set6
				related.ItemGroup		= ItemGroup
				related.HIBCCItemUPN	= HIBCCItemUPN
			Instance Selection
				where (related.Item	!= Item)

		DuplicateUOMRel
			one-to-many relation to HIBCCItemUPN
			Field Mapping uses Set5
				related.ItemGroup		= ItemGroup
				related.Item			= Item
				related.UnitOfMeasure	= UnitOfMeasure
			Instance Selection
				where (related.HIBCCItemUPN != HIBCCItemUPN)

        OnContractLineRel 
            one-to-many relation to ContractLine 
            Field Mapping uses ByItemContractLine
                related.ContractGroup       = ItemGroup 
                related.ItemNumber          = Item 
            Instance Selection 
                where (related.HibccUpn             = HIBCCItemUPN
                or     related.LowUOMCodeHibccUPN   = HIBCCItemUPN)

		ContractLineHibccUPNRel
			one-to-many relation to ContractLine
			Field Mapping uses BySpecialItemCreate     
				related.ContractGroup			= ItemGroup
            Instance Selection 
                where (related.HibccUpn                   = HIBCCItemUPN
                and    related.UOM.UnitOfMeasure          = UnitOfMeasure.UnitOfMeasure)
		ContractLineLowUOMHibccUPNRel
			one-to-many relation to ContractLine
			Field Mapping uses BySpecialItemCreate     
				related.ContractGroup			= ItemGroup
            Instance Selection 
                where (related.LowUOMCodeHibccUPN         = HIBCCItemUPN
                and    related.LowUOMCode.UnitOfMeasure   = UnitOfMeasure.UnitOfMeasure)

        ContractLineForItemUOMRel
            one-to-many relation to ContractLine 
            Field Mapping uses ByItemContractLine
                related.ContractGroup       = ItemGroup 
                related.ItemNumber          = Item  
            Instance Selection 
                where (related.UOM.UnitOfMeasure = UnitOfMeasure.UnitOfMeasure)      

        ContractLineForLowUOMRel
            one-to-many relation to ContractLine 
            Field Mapping uses ByItemContractLine
                related.ContractGroup       = ItemGroup 
                related.ItemNumber          = Item  
            Instance Selection 
                where (Item.StockUOM                    = UnitOfMeasure.UnitOfMeasure
                and    related.UOM.UnitOfMeasure       != Item.StockUOM)

        ItemUOMRel 
            one-to-many relation to ItemUOM 
            Field Mapping uses symbolic key 
                related.ItemGroup           = ItemGroup 
                related.Item                = Item         

		ConsolidatedItemMasterRel
			one-to-many relation to ConsolidatedItemMaster
			Field Mapping uses ByInternalItem     
				related.ContractGroup	    = ItemGroup
				related.InternalItem		= Item                                          

    Sets

        Set2
            indexed
            Sort Order
                HIBCCItemUPN
                ItemGroup

        Set5
            indexed
            Sort Order
                ItemGroup
                Item
                UnitOfMeasure
                HIBCCItemUPN

        Set6
            indexed
            Sort Order
                ItemGroup
                HIBCCItemUPN
                
        FromWorksheet
        	Sort Order
        		FromWorksheetResult
        		ItemGroup
        		Item
        		HIBCCItemUPN
        
                
	Field Rules
	
		HIBCCItemUPN
			constraint (!Item.DrugPharmacyOrNarcoticUsage)
				"CannotDefineHIBCC-DIForDrugsOrPharmacyOrNarcoticUsage"
			constraint (!HIBCCItemUPNDuplicated)
				"HIBCC-DIAlreadyExistsForAnotherItem:<first DuplicateHIBCCItemUPNRel.Item>"

		UnitOfMeasure
			required
				"UnitOfMeasureIsRequired"
			constraint (!UOMDuplicated)
				"AnotherHIBCC-DIExistsWithTheSameUnitOfMeasure"

		UnitOfMeasureCost
			constraint (UnitOfMeasureCost decimals <= Item.NumberOfDecimalsCost)
				"TooManyDecimalsEntered:MaxIs<Item.NumberOfDecimalsCost>"	

	Actions
        Create is a Create Action 
            Action Rules 
                constraint (Item exists)
                    "CannotCreateNewHIBCC-DIForAnItemThatDoesNotExist"
            
            Exit Rules 
                if (ContractLineForItemUOMRel exists )
                    for each ContractLineForItemUOMRel 
                        invoke FastUpdateWithoutEdits each 
                            invoked.EnterHIBCCItemUPN        = true
                            invoked.HIBCCItemUPNToEnter      = HIBCCItemUPN
                
                if (ContractLineForLowUOMRel exists)
                    for each ContractLineForLowUOMRel 
                        invoke FastUpdateWithoutEdits each 
                            invoked.EnterLowUOMCodeHIBCCUPN        = true
                            invoked.HIBCCItemUPNToEnter            = HIBCCItemUPN
                
                if (ContractLineHibccUPNRel exists)
                    for each ContractLineHibccUPNRel
                        invoke FastUpdateWithoutEdits each 
                            invoked.DefaultItemFromHibccUPN        = true
                
                if (ContractLineLowUOMHibccUPNRel exists)
                    for each ContractLineLowUOMHibccUPNRel
                        constraint (each.UOM.UnitOfMeasure = any ItemUOMRel.UnitOfMeasure)
                            "CannotAdd;AContractLineExistsWithThisHIBCC-DIAndTheContractLineHasAUnitOfMeasureThatIsNotValidForThisItem"
                        invoke FastUpdateWithoutEdits each 
                            invoked.DefaultItemFromLowUOMCodeHibccUPN   = true

		Update is an Update Action

            Action Rules 
                if (UnitOfMeasure changed)
                    for each OnContractLineRel 
                        invoke FastUpdateWithoutEdits each
                            invoked.RemoveHIBCCItemUPN = true            
            Exit Rules 
                if (UnitOfMeasure changed)
                    if (ContractLineForItemUOMRel exists )
                        for each ContractLineForItemUOMRel 
                            invoke FastUpdateWithoutEdits each 
                                invoked.EnterHIBCCItemUPN        = true
                                invoked.HIBCCItemUPNToEnter      = HIBCCItemUPN
                    
                    if (ContractLineForLowUOMRel exists)
                        for each ContractLineForLowUOMRel 
                            invoke FastUpdateWithoutEdits each 
                                invoked.EnterLowUOMCodeHIBCCUPN        = true
                                invoked.HIBCCItemUPNToEnter            = HIBCCItemUPN
                   
                    if (ContractLineHibccUPNRel exists)
                        for each ContractLineHibccUPNRel
                            invoke FastUpdateWithoutEdits each 
                                invoked.DefaultItemFromHibccUPN        = true
                    
                    if (ContractLineLowUOMHibccUPNRel exists)
                        for each ContractLineLowUOMHibccUPNRel
                            invoke FastUpdateWithoutEdits each 
                                invoked.DefaultItemFromLowUOMCodeHibccUPN   = true                           

		Delete is a Delete Action

            Entrance Rules 
                for each OnContractLineRel 
                    invoke FastUpdateWithoutEdits each
                        invoked.RemoveHIBCCItemUPN = true

				if (ConsolidatedItemMasterExists)
					invoke UpdateUOMDetails ConsolidatedItemMasterRel 
						invoked.ParmHIBCCDI		= HIBCCItemUPN 
						invoked.ParmUOM         = UnitOfMeasure.UnitOfMeasure
						invoked.ParmDeleted     = true  

	Action Exit Rules
		invoke ItemDetailChanged Item 

		if  (ConsolidatedItemMasterExists)
			invoke Update ConsolidatedItemMasterRel         
