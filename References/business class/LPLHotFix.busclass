LPLHotFix is a BusinessClass
    owned by repository
    prefix is PLHF

	Patterns
        disable EffectiveDated
		disable AsOfDateProcessing
		implements UniqueID
		implements Auditing
		implements DynamicCreation
		implements LPLHotFixMetadata	
				
    Persistent Fields
    
        ReposStamp          is SpecReposStamp
        	default label is "ProductLineStamp"
    	FileName            is LPLName
    	Status 				is a UserObjectStatus
		LPL             	is LPL
    	Description			is a ConfigDescription
    	HotFixID            is LPLName
    		default label is "Hot\Fix\I\D"
    	UserEditorType
    	ErrorText			is Text

	Transient Fields
	

		
    Derived Fields
			
    	ParseText is a NativeField
			type is Text		
		Activated is a NativeField
			type is Boolean
		Pending is a NativeField
			type is Boolean 
		HotFixDF is a NativeField
			type is LPLName
			default label is "Hot\Fix\I\D"
		FileNameDF is a NativeField
			type is LPLName
			default label is "FileNameD\F"




	Local Fields

		LocParseText is Text	
		LocParsed is Boolean  
						
    Conditions

		Active
			when (Status.Active)
			
		HasErrorText
			default label is "ErrorTextEntered"
			when (ErrorText entered)   
			
	Field Rules
	
		ReposStamp
			required
			
		FileName
			required
			constraint (HotFixDF entered)
				"FileNameDoesNotContainA_Hot\Fix\I\D."
			FileName = FileNameDF			
					
		LPL
			required  
			
			if (Status.Active)
				LocParseText = ParseText
				
				if (not LocParseText = blank)
					confirmation required
						"<LocParseText>:_L\P\L_Hot_Fix_IsInvalid.__DoYouWantToSaveAnyway?"
					
					Status = Status.Invalid		
					ErrorText = LocParseText

		HotFixID
			default to HotFixDF
			constraint (HotFixID = HotFixDF)							
				"Hot\Fix\I\DDoesNotMatchFile\Name."
								
		UserEditorType
			default to UserEditorType.LPL	
			
		Status
			if (Status.Active)
				initialize ErrorText				
		
	Relations





			
   	Actions
   	
    	Create is a Create Action
    		Exit Rules
    			invoke FireEvent
    			
    	Update is an Update Action
    		Exit Rules
    			invoke FireEvent
	
    	Delete is a Delete Action
    		Exit Rules
    			invoke FireEvent
    	
    	Activate is an Instance Action
    		valid when (not Status.Active)
    		Action Rules
    			LocParseText = ParseText
				
				if (not LocParseText = blank)
					constraint (LocParseText = blank)							
						"<LocParseText>:_L\P\L_Hot_Fix_IsInvalid._Cannot_Activate."				
				
				Status = Status.Active
				initialize ErrorText
				
    		Exit Rules
    			invoke FireEvent
    			
    	Deactivate is an Instance Action
    		valid when (not Status.Inactive)
    		Action Rules
    			Status =  Status.Inactive
    		Exit Rules
    			invoke FireEvent
    			






    	
    	FireEvent is an Instance Action
			restricted    			 

		NormalizeHotFixID is a Set Action
			default label is "NormalizeHot\Fix\I\D"
			run in foreground

			Instance Selection
				where (HotFixDF entered and (HotFixID != HotFixDF))

			Sort Order is primary

			Action Rules
				Instance Rules
					HotFixID = HotFixDF
			
    Sets

        Primary
            primary
            indexed
            Sort Order
                ReposStamp descending
                FileName
                
		ByFileName
            indexed
			Sort Order
				FileName
				ReposStamp descending
				
		ByHotFixID
            indexed
			Sort Order
				ReposStamp descending
				HotFixID
				FileName
