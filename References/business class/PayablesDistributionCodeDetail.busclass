PayablesDistributionCodeDetail is a BusinessClass
    owned by ap
    prefix is DTC
    classic name is APDISTCODE

    Ontology
        symbolic key is PayablesDistributionCodeDetail
            classic set name is DTCSET1
            classic name is SEQ-NBR
            classic name for PayablesDistributionCode is DIST-CODE

    Patterns
        implements StaticJava
        disable AuditIndex
	
	Transient Fields
		NumberOfDecimals												
		NewDistributionAmount				is an InternationalAmount	
		NewDistribTaxableAmount				is an InternationalAmount	
		NewDistribTaxAmount					is an InternationalAmount	
					
    Persistent Fields
        DistributionAmount       is an InternationalAmount
            classic name is TRAN-DIST-AMT
        DistributionPercent      is Percent size 6.3
            classic name is DIST-RATE
        AssetDescription         is a Description
            classic name is ASSET-DESC
        TagNumber
            classic name is TAG-NBR
        ItemNumber               is an AssetItemNumber
            classic name is ITEM-NBR
        ItemDescription
            classic name is ITEM-DESC
        ItemQuantity
        AssetTemplate
        ModelNumber
        SerialNumber
        HoldAsset                is Boolean
            classic name is HOLD-AM
        Asset
        DistributionReference    is a Reference 
            classic name is DST-REFERENCE
        Description
        UnitAmount               is an InternationalAmount
            classic name is UNT-AMOUNT
        Combine
        AssetGroup
        AssetLocation
            classic name is LOCATION-NAME
        AssetDivision
            classic name is DIVISION
		Company is an AssetCompany
			disable surrogates
        AssetAccountingUnitGroup
            classic name is AU-GROUP
        BarCode
        LocationDetail           is an AssetLocationDetail
            classic name is ITEM-LOC-DTL
        DistributionAccount      is a FinanceCodeBlock
            classic name for DistributionAccount.ToAccountingEntity is DIST-COMPANY
            classic name for DistributionAccount.AccountingUnit is DIS-ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is DIS-ACCOUNT
            classic name for DistributionAccount.Project is ACTIVITY
		TaxCode
		ICNCode

	Field Rules
		UnitAmount
			if (PayablesDistributionCode.AccountEdit = "S")
				cannot be entered
					"UnitAmountNotAllowedForServiceInvoiceDistCodes"          
		DistributionAmount
			if (PayablesDistributionCode.AccountEdit = "S")
				cannot be entered
					"AmountNotAllowedForServiceInvoiceDistributionCode"       
			if (DistributionPercent entered)
				cannot be entered
					"DefineEitherAmountOrPercent;NotBoth"                     
		Asset
			constraint (AssetTemplate not entered)
				"EnterAssetTemplateOrAssetNumber;NotBoth"                 
		
		AssetTemplate
			if (AssetLocationDetailRel.Level1 entered
			or  AssetLocationDetailRel.Level2 entered
			or  AssetLocationDetailRel.Level3 entered)
				required
					"AssetTempleRequiredIfLocationLevelEntered"		
			
		LocationDetail
			if (AssetLocationDetailRel.Level1 entered
			or  AssetLocationDetailRel.Level2 entered
			or  AssetLocationDetailRel.Level3 entered)
				required
					"AssetLocationRequiredIfLocationLevelEntered"		
					
		SerialNumber
			if (SerialNumber entered)
				constraint (AssetTemplate entered)
					"SerialNumberNotValidWithoutAssetTemplate"                
		ModelNumber
			if (ModelNumber entered)
				constraint (AssetTemplate entered)
					"ModelNumberNotValidWithoutAssetTemplate"                 
		AssetLocation
			if (AssetLocation entered)
				constraint (AssetTemplate entered)
					"LocationNameNotValidWithoutAssetTemplate"                
		AssetDivision
			if (AssetDivision entered)
				constraint (AssetTemplate entered)
					"DivisionNotValidWithoutAssetTemplate"                    
		TagNumber
			if (TagNumber entered)
				constraint (AssetTemplate entered)
					"TagNumberNotValidWithoutAssetTemplate"                   
		ItemNumber
			if (ItemNumber entered)
				constraint (AssetTemplate entered)
					"ItemNotValidWithoutAssetTemplate"                        
		BarCode
			if (BarCode entered)
				constraint (AssetTemplate entered)
					"Bar_\CodeNotValidWithoutAssetTemplate"                   
		ItemQuantity
			if (AssetTemplate entered)
				required
					"QuantityIsRequiredWithATemplate"                         
		AssetGroup
			if (AssetGroup entered)
				constraint (AssetTemplate entered)
					"AssetGroupNotValidWithoutAssetTemplate"                  
		ItemDescription
			if (ItemDescription entered)
				constraint (AssetTemplate entered)
					"DescriptionNotValidWithoutAssetTemplate"                 

	Relations










		AssetLocationDetailRel
			one-to-one relation to AssetLocationDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= VendorGroup.FinanceEnterpriseGroup
				related.AssetLocation			= AssetLocation
				related.AssetLocationDetail		= LocationDetail

	Derived Fields
		
		DerivedRoundTo is a DerivedField		
    		type is Decimal 5.4
    		restricted
    		if (NumberOfDecimals = 2)
    			return .01
    		else
    		if (NumberOfDecimals = 0)
    			return 1
    		else
    		if (NumberOfDecimals = 3)
    			return .001
    		else
    		if (NumberOfDecimals = 4)
    			return .0001

		RoundedDistributionAmount is a DerivedField		
			type is like InternationalAmount
			restricted
			if (NewDistributionAmount entered)
				RoundedDistributionAmount = NewDistributionAmount
				round RoundedDistributionAmount to nearest DerivedRoundTo
				return RoundedDistributionAmount
				
		RoundedTaxableAmount is a DerivedField			
			type is like InternationalAmount
			restricted
			if (NewDistribTaxableAmount entered)
				RoundedTaxableAmount = NewDistribTaxableAmount
				round RoundedTaxableAmount to nearest DerivedRoundTo
				return RoundedTaxableAmount
				
		RoundedTaxAmount is a DerivedField				
			type is like InternationalAmount
			restricted
			if (NewDistribTaxAmount entered)
				RoundedTaxAmount = NewDistribTaxAmount
				round RoundedTaxAmount to nearest DerivedRoundTo
				return RoundedTaxAmount				
		
				
	Rule Blocks
		EditData			
			if (PayablesDistributionCode.AccountEdit = "S")
				if (AssetTemplate entered
				or  Asset entered)
					constraint (false)
						"AssetEntryNotAllowedForServiceInvoiceDistCodes"         





















	
	Actions
		Create is a Create Action
			Action Rules
				include EditData

				if (DistributionAmount entered)
					invoke UpdateTotals PayablesDistributionCode
						invoked.OldDistributionAmount = blank
						invoked.NewDistributionAmount = DistributionAmount

				if (DistributionPercent entered)
					invoke UpdateTotals PayablesDistributionCode
						invoked.OldDistributionPercent = blank
						invoked.NewDistributionPercent = DistributionPercent
						
				if (UnitAmount entered)
					invoke UpdateTotals PayablesDistributionCode
						invoked.OldDistributionUnitAmount = blank
						invoked.NewDistributionUnitAmount = UnitAmount
			Exit Rules
				invoke SetFlags PayablesDistributionCode

		T2VCreate is a Create Action
			restricted 
			default label is untranslatable 
			Action Rules
				include EditData

		Update is an Update Action
			Action Rules
				include EditData

				if (DistributionAmount changed)
					invoke UpdateTotals PayablesDistributionCode
						invoked.OldDistributionAmount = old DistributionAmount
						invoked.NewDistributionAmount = DistributionAmount

				if (DistributionPercent changed)
					invoke UpdateTotals PayablesDistributionCode
						invoked.OldDistributionPercent = old DistributionPercent
						invoked.NewDistributionPercent = DistributionPercent

				if (UnitAmount changed)
					invoke UpdateTotals PayablesDistributionCode
						invoked.OldDistributionUnitAmount = old UnitAmount
						invoked.NewDistributionUnitAmount = UnitAmount
			Exit Rules
				invoke SetFlags PayablesDistributionCode

		Delete is a Delete Action
			Action Rules
				invoke UpdateTotals PayablesDistributionCode
					invoked.OldDistributionAmount = DistributionAmount
					invoked.NewDistributionAmount = blank

				invoke UpdateTotals PayablesDistributionCode
					invoked.OldDistributionPercent = DistributionPercent
					invoked.NewDistributionPercent = blank
					
				invoke UpdateTotals PayablesDistributionCode
					invoked.OldDistributionUnitAmount = UnitAmount
					invoked.NewDistributionUnitAmount = blank
			Exit Rules
				invoke SetFlags PayablesDistributionCode

				
				

		PerformDistributionCodeProcessing is a Set Action		
			restricted		
			
			Parameters
				PrmPayablesCompany		is a PayablesCompany
				PrmPayablesInvoice		is a PayablesInvoice
				PrmTransientLineTax		is Boolean				
				PrmBalanceOrRelease     is AlphaUpper 1
					States
						Balance	 value is "B"
						Release  value is "R"
				PrmJournalizeGroup			is like JournalizeGroup
				PrmDistCodeAccount			is a FinanceCodeBlock		
				PrmTaxTableInvoicedAmount	is a CurrencyAmount				
				PrmTaxMethod				is a TaxMethod					
				PrmDistributionCode			is a PayablesDistributionCode	
				PrmTaxAmount				is a CurrencyAmount				
				PrmTaxableAmount			is an InternationalAmount		
				PrmUnitsAmount				is an InternationalAmount		
				
			Parameter Rules
				PrmPayablesInvoice
					required
			
			Set Is  
				PrmPayablesCompany
				PrmPayablesInvoice

			Local Fields

				LastPayablesDistributionCodeDetail 	is like PayablesDistributionCodeDetail


				LocalBypassErrorUpdate				is Boolean


				ReleaseError						is Boolean
				
			
			Instance Selection
				where (PayablesDistributionCode = PrmPayablesInvoice.DistributionCode
				and    VendorGroup				= PrmPayablesInvoice.VendorGroup)
			
			
			Action Rules
				Set Rules
					Entrance Rules	


						PayablesDistributionCode = PrmPayablesInvoice.DistributionCode		
						LastPayablesDistributionCodeDetail = PayablesDistributionCode.last PayablesDistributionCodeDetailRel.PayablesDistributionCodeDetail


						invoke ProcessDistributionCode PrmPayablesInvoice							
							invoked.PrmTaxTableInvoicedAmount		= PrmTaxTableInvoicedAmount		
							invoked.PrmTaxMethod					= PrmTaxMethod					
							invoked.PrmDistributionCode				= PrmDistributionCode			
							invoked.PrmTaxAmount					= PrmTaxAmount					
							invoked.PrmTaxableAmount				= PrmTaxableAmount				
							invoked.PrmLinkedTax					= PrmTransientLineTax			
							invoked.PrmUnitsAmount					= PrmUnitsAmount				
							invoked.PrmDistCodeAccount				= PrmDistCodeAccount			

						
					Exit Rules
						invoke AdjustDistributionsForCurrencyRounding PrmPayablesInvoice	
						invoke UpdateErrorRecordsAction PrmPayablesInvoice					
							resume on error

						invoke FastUpdate PrmPayablesInvoice
							initialize invoked.BackgroundProcessing
						
						if  (PrmBalanceOrRelease.Release)
							invoke Unreleased.Release PrmPayablesInvoice
								resume on error
									ReleaseError	= true
							if  (ReleaseError
							and  PrmJournalizeGroup	entered)
								invoke UpdateJournalizeGroup PrmPayablesInvoice
									invoked.PrmJournalizeGroup	= blank
						else
						if (PrmBalanceOrRelease.Balance)
							invoke UpdateErrorRecordsAction PrmPayablesInvoice
								resume on error
				
					
					

				















































































































































































































			




		
