RecallProductTransactionTask is a BusinessClass
    sql name is RecallProdTransUser
    owned by recall
    prefix is RMRPT

    Ontology
    	symbolic key is RecallProductTransactionTask
    
    Patterns
    	disable Auditing		
        disable EffectiveDated	
        implements ContextualParent
					
    Persistent Fields
   		ActivityType	is a snapshot of RecallProductTransaction.ActivityType
   	
   	Conditions
   	
   		ContractHoldAllowed 
   			restricted
   			when (!RecallProductTransaction.ContractLineGroup.Contract.ContractTemplate
	        and   !RecallProductTransaction.ContractLineGroup.Contract.ContractStatus.Closed
	        and   !RecallProductTransaction.ContractLineGroup.Contract.ContractStatus.Inactive
	        and    RecallNoticeTask.Status.Accepted)

		ShipmentRecordsExist
			restricted
			when (WarehouseShipmentLinesRel exists)	

	Relations

		RecallNoticeTaskProductRel
			one-to-many relation to RecallNoticeTaskProduct
			Field Mapping uses symbolic key
				related.RecallGroup 		= RecallGroup
				related.RecallNotice		= RecallNotice
				related.RecallNoticeTask	= RecallNoticeTask
				related.RecallProduct		= RecallProduct

		WarehouseShipmentLinesRel
			one-to-many relation to WarehouseShipmentLine
			Field Mapping uses Set2
				related.Company                             = RecallProductTransaction.RequisitionLineGroup.RequisitionLine.FromCompanyLocationBin.FromCompany
				related.RequestingDocument.DemandSystemCode = "RQ"
				related.RequestingDocument.DocumentNumber   = RecallProductTransaction.RequisitionLineGroup.Requisition
				related.WarehouseShipmentLine               = RecallProductTransaction.RequisitionLineGroup.RequisitionLine
			Instance Selection
				where (related.RequestingDocument.RequestingCompany = RecallProductTransaction.RequisitionLineGroup.RequisitionLine.Company) 				
   	Sets
   		ByTaskProduct
   			indexed
   			Sort Order
   				RecallGroup
   				RecallNotice
   				RecallNoticeTask
   				RecallProduct
   				ActivityType
   				RecallProductTransaction
   				
   		ByTransactionDate
   			Sort Order
   				RecallGroup
   				RecallNotice
   				RecallNoticeTask
   				RecallProduct
   				ActivityType
   				RecallProductTransaction.TransactionDate descending
   	
   	Actions
    	Create is a Create Action
    		restricted
    		
		Update is an Update Action
			restricted
			
		HoldToggleContractLine is an Instance Action
			valid when (ContractHoldAllowed)
			Action Rules
				if (RecallProductTransaction.ContractLineGroup.ContractLine.OnHold)
					invoke RemoveContractLineHold RecallProductTransaction.ContractLineGroup.ContractLine
				else
					invoke HoldContractLine RecallProductTransaction.ContractLineGroup.ContractLine
		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
			restricted
