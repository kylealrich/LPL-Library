CombinedReportableIncomeResult is a BusinessClass		
	owned by ap
	prefix is CRIR
	
	Ontology
		symbolic key is CombinedReportableIncomeResult
	
	Patterns
	
	Persistent Fields
		Company					is a PayablesCompany
		ProcessLevel			is a PayablesProcessLevel
		Vendor
		PayeeName				is a Name	 
			holds pii
		TaxID					is like TaxID 
			holds pii
		Invoice
		InvoiceCurrency			is a Currency
		IncomeCode				is a PayablesIncomeCode		
		PaymentDateRange		is a DateRange4
		PaymentAmount			is an InternationalAmount
		PaymentNumber			is like TransactionNumber
		WithholdingAmount		is an InternationalAmount
		ReportableAmountRange	is an AmountRange
		ReportableIncomeGroup   is a PayablesReportableIncomeGroup		
		FormType				is AlphaUpper size 1					
			default label is "ReportType"
			States
				NonemployeeCompensation		value is "N"
				Miscellaneous				value is "M"
				Interest					value is "I"
				Dividends					value is "D"
				GovernmentPayments			value is "G"
				MerchantCardAndThirdParty	value is "K"
				ForeignPerson				value is "S"

		
	Context Fields
		IncludeAllInvoices		is a BooleanG		



		
	Local Fields
			
	Transient Fields
		TaxIDType				is a TaxIdentificationType	
		
	Field Rules
		VendorGroup
			initial value is Company.VendorGroup
			default to Company.VendorGroup
			required
		
		InvoiceCurrency
			initial value is Company.Currency
			default to Company.Currency
			required
		
		TaxID
			initial value is Vendor.TaxID
			
					
	Relations
		PayablesInvoicePaymentRel
			one-to-many relation to PayablesInvoicePayment
			Field Mapping uses ByCheckDateCompanyVendor
				related.InvoiceCurrency		= InvoiceCurrency
			Instance Selection
				where (related.WithinReportableIncomeCheckDateRange	
				and    related.WithinReportableAmountRange			
				and   (related.PayablesInvoice.VendorGroup	= VendorGroup
				or     VendorGroup not entered)
				and   (related.Company		= Company
				or     Company not entered)
				and   (related.ProcessLevel = ProcessLevel
				or     ProcessLevel not entered)
				and   (related.Vendor	= Vendor
				or     Vendor not entered)

				and  ((TaxID entered								
				and    related.Vendor = any VendorTaxIDRel.Vendor)	
				or     TaxID not entered)
				and   (related.Invoice	= Invoice
				or     Invoice not entered)
				and   (related.InvoiceCurrency	= InvoiceCurrency
				or     InvoiceCurrency not entered)
				and   (related.TransactionNumber	= PaymentNumber
				or     PaymentNumber not entered)
				and   (related.PaymentAmount.CurrencyAmount	= PaymentAmount
				or     PaymentAmount not entered)
				and   (related.WithholdingAmount	= WithholdingAmount
				or     WithholdingAmount not entered)
				and   (IncludeAllInvoices
				or    (!IncludeAllInvoices
				and     related.HasIncomeCode
				and     related.Status.Historical))
				and   (related.IncomeCode	= IncomeCode
				or     IncomeCode not entered)
				and	  (ReportableIncomeGroup not entered					
                or     related.IncludedInReportableIncomeGroup)  		
				and	  ((FormType entered								
				and    related.IncomeCode = any PayablesIncomeCodeRel.PayablesIncomeCode)
				or	   FormType not entered))
		
		
		VendorTaxIDRel
			one-to-many relation to Vendor
			Field Mapping uses ByTaxIdsOnly		
				related.TaxID			= TaxID
				related.VendorGroup		= VendorGroup

		NonPayablesReportableIncomeRel
			one-to-many relation to NonPayablesReportableIncomeAdjustment
			Field Mapping uses Set2
				related.VendorGroup			= VendorGroup
			Instance Selection
				where (related.WithinPaymentDateRange
				and    related.WithinReportableAmountRange
				and   (related.VendorGroup	= VendorGroup
				or     VendorGroup not entered)
				and   (related.Company		= Company
				or     Company not entered)
				and   (related.ProcessLevel = ProcessLevel
				or     ProcessLevel not entered)
				and   (related.Vendor	= Vendor
				or     Vendor not entered)
				and	 ((TaxID entered
				and   (related.Vendor entered
				and	   related.Vendor	= any VendorTaxIDRel.Vendor)
				or	   related.TaxID	= TaxID)
				or	   TaxID not entered)			
				and   (related.TransactionCurrency	= InvoiceCurrency
				or     InvoiceCurrency not entered)
				and   (related.TransactionNumber	= PaymentNumber
				or     PaymentNumber not entered)
				and   (related.PaidAmount	= PaymentAmount
				or     PaymentAmount not entered)
				and   (related.IncomeCode	= IncomeCode
				or     IncomeCode not entered)			    
				and	  (ReportableIncomeGroup not entered
                or     related.IncludedInReportableIncomeGroup)  		
				and	  ((FormType entered					
				and    related.IncomeCode = any PayablesIncomeCodeRel.PayablesIncomeCode)
				or	   FormType not entered))

		PayablesIncomeCodeRel			
			one-to-many relation to PayablesIncomeCode
			Field Mapping uses symbolic key
				related.VendorGroup	=	VendorGroup
			Instance Selection
				where (related.FormType	= FormType)

	Conditions
	
	Derived Fields
		DisplayPayablesCount is a DerivedField
			type is Alpha size up to 20
        	restricted
			if (PayablesInvoicePaymentRel exists)
				return "(" + instance count of PayablesInvoicePaymentRel + ")"
			else
				return ""
			
		DisplayNonPayablesCount is a DerivedField
			type is Alpha size up to 20
        	restricted
			if (NonPayablesReportableIncomeRel exists)
				return "(" + instance count of NonPayablesReportableIncomeRel + ")"
			else
				return ""
	
		PayablesCount is a DerivedField
			type is Numeric 10
        	restricted
			return instance count of PayablesInvoicePaymentRel
		
		NonPayablesCount is a DerivedField
			type is Numeric 10
        	restricted
			return instance count of NonPayablesReportableIncomeRel
			
		TotalCount is a DerivedField
			type is Numeric 10
			restricted
			return (PayablesCount + NonPayablesCount)
		
		DisplayTotalCount is a DerivedField
			type is Alpha size up to 20
        	restricted
			if (PayablesInvoicePaymentRel exists
			or  NonPayablesReportableIncomeRel exists)
				return "(" + TotalCount + ")"
			else
				return ""
			
	Sets
	
	Rule Blocks
		
	Actions
		CreateReportableIncomeJob is a Create Action
			completion message is "ReportableIncomeHeaderCreated"
			Action Rules
			Exit Rules
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		Purge is a Purge Action
				
			
			
			
