InterfaceSelectedPayablesInvoiceRunGroups is a BusinessTask
	owned by ap

	Parameters
		FinanceEnterpriseGroup
		SelectRunGroupsLike				is like RunGroup
		MoveErrorsToNewRunGroup			is Boolean
		ErrorRunGroupPrefix				is AlphaUpper 15
		UpdateTaxSystem					is Boolean
		UpdateVendorBalance				is Boolean
		LastYearEndingDate				is Date
		IncludeInvoicesInError			is Boolean
		PerformBalanceEdits				is Boolean
		JournalizeReleasedDistributions	is Boolean

	Parameter Rules
		FinanceEnterpriseGroup
			default to actor.context.FinanceEnterpriseGroup
		SelectRunGroupsLike
			required
		UpdateTaxSystem
			initial value is true
		UpdateVendorBalance
			initial value is true
		LastYearEndingDate
			if (UpdateVendorBalance)
				required
					"LastYearEndingDateRequiredIfUpdateVendorBalanceSetToTrue"
		PerformBalanceEdits
			initial value is true

	Actions
		ScheduleInterface is an Instance Action
			run in background

			Action Rules
				for each distinct RunGroup in FinanceEnterpriseGroup.AllPayablesPayablesInvoiceImportRel
					if (each.RunGroup like SelectRunGroupsLike)
						invoke InterfaceInvoices PayablesInvoiceImport	
							invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.PrmRunGroup					= each.RunGroup
							invoked.UpdateTaxSystem				= UpdateTaxSystem
							invoked.UpdateVendorBalance			= UpdateVendorBalance
							invoked.LastYearEndingDate			= LastYearEndingDate
							invoked.IncludeInvoicesInError		= IncludeInvoicesInError
							invoked.PrmMoveErrorsToNewRunGroup	= MoveErrorsToNewRunGroup
							invoked.PrmErrorRunGroupPrefix		= ErrorRunGroupPrefix
							invoked.PerformBalanceEdits			= PerformBalanceEdits
							invoked.PrmJournalizeDistributions	= JournalizeReleasedDistributions
