StatementPrintHeader is a BusinessClass
	owned by ar
	prefix is SPH
	
	
	Ontology
		symbolic key is StatementPrintHeader
		
	Patterns
        implements StaticJava
        disable AuditIndex
        	
	Persistent Fields
	
		AsOfDate                	is Date
		AgePeriod1              	is Numeric size 3
        AgePeriod2              	is Numeric size 3
		LanguageCode            	is an IsoLocale
		RSTextCode              	is a ReceivableStatementLanguageText
        NationalAccountSummary  	is AlphaUpper size 1
            States
                NationalAccountRule					value is blank
                DoNotPrintNationalAccountSummary	value is "N"
                PrintNationalAccountSummary			value is "Y"
		Currency
		FileName					is AlphaUpper size 50
	Local Fields
		LocalTitle					is Alpha 50
		LocalDays					is Alpha 3
		WorkNumber					is Numeric 3

		LocalCompany				is like Company
		LocalCustomer				is like Customer
		LocalFileName				is Alpha size 100
		LocalAgingPeriod1Total		is an InternationalAmount
		LocalAgingPeriod2Total		is an InternationalAmount
		LocalAgingPeriod3Total		is an InternationalAmount	
		
		IDMGenerateDocument
		LocalPrintDate				is Date
		
		LocalHeaderCompany			is like Company
		LocalHeaderCustomer			is like Customer
		LocalGenerateIDMReport		is Boolean	
                
        LocalNationalAccountCustomer	 is like Customer
        LocalNationalAccountCustomerName is like VendorName 
        	holds pii
        LocalCurrency					 is like FromCurrency




        LocalCurrentYear				is Numeric 4
        LocalCurrentMonth				is Numeric 2
        LocalCurrentDay					is Numeric 2
		LocalIDMEmailSubject			is Alpha 255
    		Text Variables
				CompanyName				value is Company.Name
				CustomerName			value is Customer.Name
				AsOfDate	    		value is AsOfDate
		AmountToTextTranslation
		IDMXMLDefinition
		AttributeCtr				is Numeric 2
		LocalExecute				is Boolean	

	Transient Fields
		TransientTotalOpenAmountInRight	      is AlphaRight size 18
			derive value from DerivedTotalOpenAmount	
	
	Field Rules
		FileName
			default to "StatementPrintOutput"				
	Derived Fields
				
		AgingPeriod1Title is a DerivedField
			type is Alpha size 15
			LocalDays 	= "1"
			LocalTitle 	= LocalDays + "-"
			LocalDays 	= AgePeriod1
			LocalTitle += LocalDays
			return LocalTitle

		AgingPeriod2Title is a DerivedField
			type is Alpha size 15
			WorkNumber 	= AgePeriod1 + 1
			LocalDays	= WorkNumber
			LocalTitle 	= LocalDays + "-"
			LocalDays 	= AgePeriod2
			LocalTitle += LocalDays
			return LocalTitle

		AgingPeriod3Title is a StringField
			type is Alpha size 15
			"Over "
			AgePeriod2
				
		DerivedFileName is a StringField
    		type is Alpha 100
    		restricted
    		"Company "
			Company
			" Customer "
			Customer
			" in "
			LocalCurrency
			" Receivable Statement as of "
			DerivedReceivablesStatementPrintDate

		DerivedNationalAccountFileName is a StringField
    		type is Alpha 100
    		restricted 
    		"National Account "
    		"Company "
			Company
			" Customer "
			Customer
			" Child Customer "
			LocalCustomer
			" in "
			LocalCurrency
			" Receivable Statement as of "
			DerivedReceivablesStatementPrintDate

		DerivedStatementText is a StringField
			type is Alpha 200	
			RSTextCode.CommentTexts.CommentText[1]
			" "	
			RSTextCode.CommentTexts.CommentText[2]
			" "
			RSTextCode.CommentTexts.CommentText[3]
			" "
			RSTextCode.CommentTexts.CommentText[4]	

		DerivedCurrentAmountTotal	   is a DerivedField
			type is like InternationalAmount
			return sum StatementPrintDetailRel.CurrentAmount

		DerivedAgePeriod1Total is a DerivedField
			type is like InternationalAmount
			
			initialize LocalAgingPeriod1Total
			for each StatementPrintDetailRel
				LocalAgingPeriod1Total += each.Agings.Aging[1]
			return LocalAgingPeriod1Total
			
		DerivedAgePeriod2Total is a DerivedField
			type is like InternationalAmount

			initialize LocalAgingPeriod2Total					
			for each StatementPrintDetailRel				
				LocalAgingPeriod2Total += each.Agings.Aging[2]
			return LocalAgingPeriod2Total
			
		DerivedAgePeriod3Total is a DerivedField
			type is like InternationalAmount				

			initialize LocalAgingPeriod3Total
			for each StatementPrintDetailRel					
				LocalAgingPeriod3Total += each.Agings.Aging[3]
			return LocalAgingPeriod3Total
			
		DerivedCurrentBalance is a DerivedField
			type is like InternationalAmount	







			return sum StatementPrintDetailRel.OpenAmount
			

		DerivedRemitToAddressName is a DerivedField 
			type is like Name
			holds pii
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.Name
			else
				return Company.Name
				
		DerivedRemitToAddressLine1 is a DerivedField
			type is like AddressLine
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.DeliveryAddress.AddressLine1
			else
				return Company.PostalAddress.DeliveryAddress.AddressLine1
				
		DerivedRemitToAddressLine2 is a DerivedField
			type is like AddressLine
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.DeliveryAddress.AddressLine2
			else
				return Company.PostalAddress.DeliveryAddress.AddressLine2
				
		DerivedRemitToAddressLine3 is a DerivedField
			type is like AddressLine
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.DeliveryAddress.AddressLine3
			else
				return Company.PostalAddress.DeliveryAddress.AddressLine3
				
		DerivedRemitToAddressLine4 is a DerivedField
			type is like AddressLine
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.DeliveryAddress.AddressLine4
			else
				return Company.PostalAddress.DeliveryAddress.AddressLine4
				
		DerivedRemitToAddressMunicipality is a DerivedField
			type is like MunicipalityLarge
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.Municipality
			else
				return Company.PostalAddress.Municipality
				
		DerivedRemitToAddressState is a DerivedField
			type is like StateProvince
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.StateProvince
			else
				return Company.PostalAddress.StateProvince
				
		DerivedRemitToAddressPostalCode is a DerivedField
			type is like PostalCode
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.PostalCode
			else
				return Company.PostalAddress.PostalCode
				
		DerivedRemitToAddressCountry is a DerivedField
			type is like Country
			if (HasReceivableLockBox)
				return CompanyCustomerRel.ReceivableLockBox.PostalAddress.Country
			else
				return Company.PostalAddress.Country
				
		DerivedNationalAccountCustomer is a DerivedField
			type is Alpha 9
			if (LocalNationalAccountCustomer entered)
				return LocalNationalAccountCustomer
			else
				return blank
				
		DerivedNationalAccountCustomerName is a DerivedField 
			type is like VendorName
			holds pii
			return LocalNationalAccountCustomerName


		DerivedLastStatementBalance is a DerivedField
			type is like InternationalAmount

			if (Company = LocalCompany and Customer = LocalCustomer)
				return blank
			else 
			if (NationalAccountCustomerRel exists)
				if (NationalAccountCustomerRel.BalanceForwardStatementCycle entered)
					return (NationalAccountCustomerRel.LastStatementBalance - DerivedCurrentBalance)
			else
				if (CompanyCustomerRel.BalanceForwardStatementCycle entered)
					return (CompanyCustomerRel.LastStatementBalance - DerivedCurrentBalance)
				
		DerivedTotalOpenAmount is a DerivedField
			type is like InternationalAmount

			if (Company = LocalCompany and Customer = LocalCustomer)
				return DerivedCurrentBalance
			else 
			if (NationalAccountCustomerRel exists)
				if (NationalAccountCustomerRel.BalanceForwardStatementCycle entered)
					return NationalAccountCustomerRel.LastStatementBalance
				else
					return DerivedCurrentBalance 
			else
				if (CompanyCustomerRel.BalanceForwardStatementCycle entered)
					return CompanyCustomerRel.LastStatementBalance
				else
					return DerivedCurrentBalance 
			
		DerivedReportCurrency is a DerivedField
			type is like FromCurrency
			return LocalCurrency
			
		DerivedReceivablesStatementPrintDate	is a StringField
			type is Alpha 8
			restricted
			LocalCurrentYear
			DerivedCurrentMonth
			DerivedCurrentDay
			
		DerivedCurrentMonth is a DerivedField
			type is Alpha 2
			if (LocalPrintDate month < 10)
				return "0" + LocalPrintDate month
			else
				return LocalPrintDate month
				
		DerivedCurrentDay is a DerivedField
			type is Alpha 2
			if (LocalPrintDate day < 10)
				return "0" + LocalPrintDate day
			else
				return LocalPrintDate day
				
		DerivedEmailSubject is a MessageField
			restricted
			"CustomerStatement<Company.Name>_<Customer>_AsOf<DerivedReceivablesStatementPrintDate>"

    	DerivedCompanyEmailSubject is a DerivedField 
    		type is Alpha size 255
    		restricted
    		LocalIDMEmailSubject = Company.ReceivableStatementIDMEmailSubject
    		return LocalIDMEmailSubject text

		TotalOpenAmountInWords is a DerivedField
			type is Alpha 125
			AmountToTextTranslation.AmountAsArray = TransientTotalOpenAmountInRight
			return AmountToTextTranslation.AmountMessageField2

				
	Conditions
		HasReceivableLockBox
			when CompanyCustomerRel.ReceivableLockBox exists
							
	Relations

		IDMAdditionalAttributesLinesRel
			one-to-many relation to IDMAdditionalAttributesLines
			Field Mapping uses symbolic key
				related.IDMAdditionalAttributesHeader = "FSM_ReceivablesStatement"
			Instance Selection
				where(related.IDMAdditionalAttributesHeader.ActivateAdditionalAttributes
				and	  related.ActivateAdditionalAttributes.Active)
				
		StatementPrintDetailRel
            one-to-many relation to StatementPrintDetail
            Field Mapping uses ByCompanyCustomer
            	related.InvoiceAccountGroup.Company  = Company
            	related.InvoiceAccountGroup.Customer = Customer
            Instance Selection
				where (related.ChildCompany  = LocalCompany
				and    related.ChildCustomer = LocalCustomer
				and    related.OriginalCurrency = LocalCurrency)

		StatementPrintDetailNationalRel
            one-to-many relation to StatementPrintDetail
			Field Mapping uses ByNationalAccountCustomer
            	related.InvoiceAccountGroup.Company  = Company
            	related.InvoiceAccountGroup.Customer = Customer
            Instance Selection
				where (related.ChildNationalAccount = true)
	
		StatementPrintDetailCustomerRel
            one-to-many relation to StatementPrintDetail
            Field Mapping uses ByCompanyCustomer
            	related.InvoiceAccountGroup.Company  = Company
            	related.InvoiceAccountGroup.Customer = Customer
            Instance Selection
				where (related.ChildNationalAccount = false)
				
		StatementPrintAllDetailRel	
            one-to-many relation to StatementPrintDetail
            Field Mapping uses ByCompanyCustomer
            	related.InvoiceAccountGroup.Company  = Company
            	related.InvoiceAccountGroup.Customer = Customer
            	
        CompanyCustomerRel	
            one-to-one relation to CompanyCustomer
            Field Mapping uses symbolic key
            	related.Company  	= Company
            	related.Customer 	= Customer
            	
        NationalAccountCustomerRel
        	one-to-many relation to CompanyCustomer
        	Field Mapping uses symbolic key
        		related.Company		= LocalCompany
        		related.Customer	= LocalCustomer
        		
        NationalAccountCustomerIDMSectionRel
        	one-to-many relation to CompanyCustomer
        	Field Mapping uses symbolic key
        		related.Company		= LocalCompany
        	Instance Selection
        		where (related.Customer	= LocalCustomer)
        		
        CompanyCustomerContactForIDMAutoEmailRel
        	one-to-many relation to CompanyCustomerContact
        	Field Mapping uses symbolic key
        		related.Company = Company
        		related.Customer = Customer
        	Instance Selection
        		where (related.EmailReceivablesStatement
				and    related.EmailAddress entered)
        		

	Rule Blocks
		ProcessCustomerInvoices
       		initialize LocalCurrency
       		for each distinct OriginalCurrency in StatementPrintDetailCustomerRel
       			LocalCurrency 	 = each.OriginalCurrency
       			LocalFileName = DerivedFileName
       			initialize LocalCompany
       			initialize LocalCustomer  
       			invoke UploadARStatementPrintInIDM
       				invoked.PrmIDMPrinter			= PrmIDMPrinter
       			initialize LocalGenerateIDMReport
       				
       				
       	ProcessNationalAccountInvoices
			initialize LocalHeaderCompany
			initialize LocalHeaderCustomer
			initialize LocalCompany
			initialize LocalCustomer
			initialize LocalCurrency
			for each StatementPrintDetailNationalRel
				if (LocalHeaderCompany != each.InvoiceAccountGroup.Company)
					LocalHeaderCompany	= each.InvoiceAccountGroup.Company
					LocalGenerateIDMReport = true
				if (LocalHeaderCustomer != each.InvoiceAccountGroup.Customer)
					LocalHeaderCustomer	= each.InvoiceAccountGroup.Customer
					LocalGenerateIDMReport = true
				if (LocalCompany != each.ChildCompany)
					LocalCompany  		= each.ChildCompany
					LocalGenerateIDMReport = true
				if (LocalCustomer != each.ChildCustomer)
					LocalCustomer 		= each.ChildCustomer
					LocalGenerateIDMReport = true
				if (LocalCurrency	!= each.OriginalCurrency)
					LocalCurrency 		= each.OriginalCurrency
					LocalGenerateIDMReport = true
				
				if (LocalGenerateIDMReport)
					LocalFileName 		= DerivedNationalAccountFileName
					LocalNationalAccountCustomer		= NationalAccountCustomerRel.Customer
					LocalNationalAccountCustomerName	= NationalAccountCustomerRel.Customer.Name
					invoke UploadARStatementPrintInIDM
						invoked.PrmIDMPrinter			= PrmIDMPrinter
					initialize LocalGenerateIDMReport
            								
	Actions
	
        Create is a Create Action
        	restricted
        	
        Update is an Update Action
        	restricted
        	
		Delete is a Delete Action
        	Entrance Rules
        		invoke Delete StatementPrintAllDetailRel    


		ARStatementIDMPrint is an Instance Action
			restricted
			Local Fields


			Parameters
        		NationalAccountSelect        is AlphaUpper size 1
            		States
                		CustomerOnly                   value is "C"
                		NationalAccountOnly            value is "N"
                		BothNationalAccountAndCustomer value is "B"		
                AsOfDate					 is Date
                PrmIDMPrinter				 is an IDMPrinter
        	
        	Parameter Rules
        		NationalAccountSelect
        			required
        		    	        
       		Action Rules	
       			LocalPrintDate = current corporate date
       			
       			LocalCurrentYear = LocalPrintDate year
       			LocalCurrentMonth = DerivedCurrentMonth
       			LocalCurrentDay   = DerivedCurrentDay
       			
       			if (NationalAccountSelect.CustomerOnly)
					include ProcessCustomerInvoices
       			else
       			if (NationalAccountSelect.NationalAccountOnly)
					include ProcessNationalAccountInvoices
       			else
       			if (NationalAccountSelect.BothNationalAccountAndCustomer)
					include ProcessCustomerInvoices
					include ProcessNationalAccountInvoices
   					
		UploadARStatementPrintInIDM is an Instance Action
			restricted
			Parameters
				PrmIDMPrinter				 is an IDMPrinter
			
    		Local Fields

    			IDMAttributes

    			LocalRecipientEmailAddress		is Alpha 4000
					holds pii
	 			
    		Action Rules
    			initialize IDMGenerateDocument
    			
    			IDMGenerateDocument.IDMXMLDefinition.Busclass											= reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName											= "StatementPrintHeaderListforIDMDoc"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName										= "StatementPrintHeader"
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName			= "StatementPrintDetailRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName 			= "StatementPrintDetailsForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1		= 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag				= "Details"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag				= "Detail"
				
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName			= "ReceivableApplicationRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName 			= "ReceivableApplicationsForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1		= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection2		= 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag				= "Applications"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag				= "Application"
				
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].RelationName			= "ReceivableApplicationAdjustmentRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].ListName 			= "ReceivableApplicationAdjustmentForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].LevelSection1		= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].LevelSection2		= 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].LevelSection3		= 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].ListTag				= "Adjustments"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[3].ItemTag				= "Adjustment"
				
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].RelationName			= "NationalAccountCustomerIDMSectionRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].ListName 			= "NationalAccountCustomerForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].LevelSection1		= 2
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].ListTag				= "NationalAccountCustomerDetails"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[4].ItemTag				= "NationalAccountCustomer"
 		
 				initialize IDMAttributes
 				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName  	= "Company"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue  	= Company
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName  	= "Customer"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue  	= Customer
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName  	= "Currency"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue  	= LocalCurrency
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName  	= "AsOfDate"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeDate  	= AsOfDate
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName		= "CompanyName"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue		= Company.Name
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName		= "CustomerName"
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue		= Customer.Name

				if (IDMAdditionalAttributesLinesRel exists)
					AttributeCtr = 7
					include IDM.IDMAdditionalAttributes

				IDMGenerateDocument.IDMAttributes		= IDMAttributes
				IDMGenerateDocument.TemplateUniqueId	= Company.ReceivablesStatementTemplate.IDMUniqueId
				IDMGenerateDocument.DocumentType		= "FSM_ReceivablesStatement"
    			IDMGenerateDocument.FileName			= LocalFileName + Company.ReceivablesStatementTemplate.DerivedOutputFormat
    			
				if (CompanyCustomerRel.EmailReceivablesStatement)
					if (not CompanyCustomerRel.ReceivablesStatementEmailContactsOnly)
						LocalRecipientEmailAddress += CompanyCustomerRel.EmailAddress
					
					for each CompanyCustomerContactForIDMAutoEmailRel
						if (LocalRecipientEmailAddress entered)
							LocalRecipientEmailAddress += ","
						LocalRecipientEmailAddress += each.EmailAddress
				
					if (Company.ReceivableStatementIDMEmailSubject entered)
						IDMGenerateDocument.IDMEmail.Subject		= DerivedCompanyEmailSubject
	    			else
	    				IDMGenerateDocument.IDMEmail.Subject		= DerivedEmailSubject
				
					if (Company.ReceivableStatementIDMEmailTemplate entered)
						IDMGenerateDocument.EmailTemplateUniqueID	= Company.ReceivableStatementIDMEmailTemplate.IDMUniqueId			
					else
	    				IDMGenerateDocument.IDMEmail.Body			= ""
    			
	    			IDMGenerateDocument.IDMEmail.From				= CompanyCustomerRel.ReceivablesStatementFromAndReplyToEmail
	    			IDMGenerateDocument.IDMEmail.To					= LocalRecipientEmailAddress
	    			
	    			
    			if (CompanyCustomerRel.PrintReceivablesStatement)
					if (PrmIDMPrinter entered)    			
						IDMGenerateDocument.IDMPrinter = PrmIDMPrinter 
    			
    			IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
    			
    			invoke CreateFromGenerateDocument IDMJob
    				invoked.Actor				= actor
					invoked.Description			= "Receivables Statement"
					invoked.IDMGenerateDocument = IDMGenerateDocument
