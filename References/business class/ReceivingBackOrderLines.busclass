ReceivingBackOrderLines is a BusinessClass
	owned by po
	prefix is RBOL
	sql name is RecBackOrdLine
	classic name is RBOLNE
	
	Ontology
		symbolic key is ReceivingBackOrderLines
		
	Patterns
		implements StaticJava
		
	Persistent Fields
		SourceDocumentOrigin
		OperatingCompany 			is like InventoryCompany
		SourceDocumentNumeric
		SourceDocumentAlpha
		SourceDocumentLineNumber 	is a LineNumber
		BackOrderedQty				is like Quantity
		RequestingLocation			is like RequestingLocation
	
	Derived Fields
		DerivedSourceDocumentLineNumber is a DerivedField
			type is Alpha 6
			if (SourceDocumentLineNumber entered)
				return SourceDocumentLineNumber
			else
				return blank
				
		DerivedOrderedQuantity is a DerivedField
			type is like Quantity
				precision is PurchaseOrderLine.DerivedNumberOfDecimalsQuantity
			if (PurchaseOrderLineSourceRel exists)
				return PurchaseOrderLineSourceRel.Quantity	
			else
				return PurchaseOrderLine.Quantity
	
	Relations
	
		PurchaseOrderLineSourceRel
			one-to-one relation to PurchaseOrderLineSource
			Field Mapping uses symbolic key
				related.Company												= ReceivingBackOrders.Company
				related.PurchaseOrder										= PurchaseOrderLine.PurchaseOrder
				related.PurchaseOrderLine									= PurchaseOrderLine
				related.PurchaseOrderLineSource.SourceDocumentOrigin		= SourceDocumentOrigin
				related.PurchaseOrderLineSource.OperatingCompany			= OperatingCompany
				related.PurchaseOrderLineSource.SourceDocumentNumeric		= SourceDocumentNumeric
				related.PurchaseOrderLineSource.SourceDocumentAlpha			= SourceDocumentAlpha
				related.PurchaseOrderLineSource.SourceDocumentLineNumber	= SourceDocumentLineNumber
	
			
		PurchaseOrderLineCommentsForIDMRel
			one-to-many relation to PurchaseOrderLineComment
    		Field Mapping uses ByCommentType
    			related.Company		  	  = Company
    			related.PurchaseOrder     = PurchaseOrder
    			related.PurchaseOrderLine = PurchaseOrderLine		
			Instance Selection
    			where (related.CommentType.PrintOnInternalDocuments
    			or     related.CommentType.PrintOnDeliveryTicket) 			
			
	
	Sets
		Set1
			indexed
			Sort Order
			    Company
			    PurchaseOrder
			    RequestingLocation
			    ReceivingBackOrders
			    PurchaseOrderLine
				SourceDocumentNumeric
				SourceDocumentLineNumber
				ReceivingBackOrderLines
	
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted

		DeleteReceivingBackOrderLines is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCompany	  	is like PurchasingCompany
				PrmActor 		is like Actor
				PrmRunBatchID	is like IDMPID
				
			Instance Selection
				where (Company = PrmCompany 
				and	   ReceivingBackOrders.CurrentUser 	= PrmActor
				and	   ReceivingBackOrders.RunBatchID 	= PrmRunBatchID)
				
			Action Rules
				Empty Set Rules
					invoke DeleteReceivingBackOrders ReceivingBackOrders
						invoked.PrmCompany 	  = PrmCompany
						invoked.PrmRunBatchID = PrmRunBatchID
						invoked.PrmActor 	  = PrmActor
						
				Set Rules
					Exit Rules
						invoke DeleteReceivingBackOrders ReceivingBackOrders
							invoked.PrmCompany 	  = PrmCompany
							invoked.PrmRunBatchID = PrmRunBatchID
							invoked.PrmActor 	  = PrmActor	
			
				Instance Rules
					invoke Delete
