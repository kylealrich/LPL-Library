PurchaseOrderBuyerMessage is a BusinessClass
	owned by po
	prefix is POM
	classic name is POMESSAGE

	Ontology
		symbolic key is PurchaseOrderBuyerMessage
			classic set name is POMSET1
			classic name for PurchaseOrderBuyerMessage.MessageDate is MSG-DATE
			classic name for PurchaseOrderBuyerMessage.MessageSequence is MSG-SEQ
			classic name for Buyer is BUYER-CODE

	Patterns
		implements StaticJava
		disable AuditIndex
		implements Archivable

	Persistent Fields
		BuyerMessageType				is AlphaUpper size 1
			classic name is MESSAGE-TYPE
			States
				Manual		value is "M"
				Receiving	value is "R"
		Document
		PurchaseOrderReceipt
			classic name is REC-NUMBER
		DocumentNumberNumeric
			classic name is DOC-NBR-NUM
		PurchaseOrderReceiptLine
			classic name is LINE-NBR
		PurchaseOrder
		ReturnQuantity					is a Quantity
			classic name is RETURN-QTY
		ShipToLocation					is an InventoryLocation
			classic name is LOCATION
		BatchNumber
			classic name is BATCH-NBR
		Vendor
		Invoice
		Suffix
		Status							is Numeric size 1
			States
				Unreleased value is 1
				Released   value is 2
		SubstituteItemMessage			is Boolean
			classic name is SUBST-FLAG
		OverreceivingMessage			is Boolean
			classic name is OVERRECVD-FLAG
		VendorUnitCostMessage			is Boolean
			classic name is UNIT-COST-FLAG
		ReferenceNumber
			classic name is REFERENCE-NO
		PurchaseOrderBuyerMessageCommentArray
		POBuyerMessageLifeCycleState	 is Numeric size 1
			States
				Unreleased	value is 0
				Released	value is 1

	Transient Fields
		TransientUnitCost			is an UnsignedUnitCost
			derive value from DerivedUnitCost
			default label is "UnitCost"






	Local Fields
		LocalVendorReturn		is a VendorReturn view
		LocalVendorReturnLine	is a VendorReturnLine view

		LocalPulseAlertFrom		is Alpha size 100
		LocalConfigurationParameter		is Alpha size up to 200


	Derived Fields

		ReceiptWithLineDisplay		is a LabelField
			"Receipt:<PurchaseOrderReceipt>_Line:<PurchaseOrderReceiptLine>"
		ExcessWithUOM				is a LabelField
			"<ExcessQuantityDisplay>_<PurchaseOrderReceiptLine.ReceivedUOM>"
		ReceiptQuantityWithUOM		is a LabelField
			"<ReceiptQuantity>_<PurchaseOrderReceiptLine.ReceivedUOM>"
		MobileCompanyDisplay		is a LabelField
			"<Company>_-_<Company.Name>"
		MobileDocument 				is a DerivedField
			type is Alpha 25
			default label is "Document"
			return com.lawson.apps.procurement.base.StringUtils.trimLeadingSpaces(Document)


		PurchaseCompanyDisplay				is a LabelField
			"<Company>_-_<Company.Name>"

		POAndReceiptWIthLineDisplay			is a LabelField
			"PO:<PurchaseOrder>|Receipt:<PurchaseOrderReceipt>|Line:<PurchaseOrderReceiptLine>"
		ReceiptAndExcessQtyWithUOMDisplay	is a LabelField
			"Received:<ReceiptQuantityDisplay>_<PurchaseOrderReceiptLine.ReceivedUOM>|Excess:<ExcessQuantityCardViewDisplay>_<PurchaseOrderReceiptLine.ReceivedUOM>"

		DerivedNumberOfDecimalQuantityDisplay is a DerivedField
			type is Numeric size 1
			return 1

		ReceiptQuantityDisplay is a DerivedField
			type is like Quantity
				precision is DerivedNumberOfDecimalQuantityDisplay
			return ReceiptQuantity

		ExcessQuantityCardViewDisplay is a DerivedField
			type is like Quantity
				precision is DerivedNumberOfDecimalQuantityDisplay
			return 	(ExcessQuantityDisplay)

		DerivedUnitCost			is a DerivedField
			type is like InternationalCost
			restricted
			if (action type.Update)
				return 0
			else
				return PurchaseOrderReceiptLine.OriginalUnitCost

		DerivedStatus			is a ConditionalField
			type is Alpha 25
			default label is "Status"
			if (Status.Released)
				ReleasedMessage
			else
				UnreleasedMessage

		UnreleasedMessage is a MessageField
			restricted
			"Unreleased"

		ReleasedMessage	is a MessageField
			restricted
			"Released"

		OverreceivedQuantity	is a MessageField 
			"OverreceivedQuantity"

		ItemSubstitution	is a MessageField 
			"ItemSubstitution"

		NoVendorCost	is a MessageField 
			"NoVendorUnitCost"

		ReleaseWithVendorReturnMessage is a MessageField
			restricted
			"ReleaseComplete,VendorReturn<LocalVendorReturn.VendorReturn>Created"

		ReleaseMessage is a MessageField
			restricted
			"ReleaseComplete"

		LocalCompletionMessage is a DerivedField
			type is MessageField
			restricted
			if (ReturnQuantity entered)
				return ReleaseWithVendorReturnMessage
			else
				return ReleaseMessage

		ReceiptQuantity is a DerivedField
			type is like Quantity
			if (POReceiptAdjustmentAndInspectionLineRel exists)
				return (POReceiptAdjustmentAndInspectionLineRel.CurrentReceivedQuantity)
			else
				return (PurchaseOrderReceiptLine.EnteredReceivedQuantity)

		CatchWeightReceiptQuantity is a DerivedField
			type is like Quantity
			if (POReceiptAdjustmentAndInspectionLineRel exists)
				return (POReceiptAdjustmentAndInspectionLineRel.CurrentReceivedCatchWeightQuantity)
			else
				return (PurchaseOrderReceiptLine.CatchWeightQuantity)

		ExcessQuantity is a ConditionalField
			type is Decimal 13.4
			if	(PurchaseOrderReceiptLine.Status.Entered)
				(PurchaseOrderReceiptLine.EnteredReceivedQuantity + (PurchaseOrderReceiptLine.DerivedPOReceivedQuantityInReceivedUOM - PurchaseOrderReceiptLine.DerivedPOQuantityToProcessInReceivedUOM))
			else
				(POReceiptAdjustmentAndInspectionLineRel.Quantity + (PurchaseOrderReceiptLine.DerivedPOReceivedQuantityInReceivedUOM - ReturnQuantity - PurchaseOrderReceiptLine.DerivedPOQuantityToProcessInReceivedUOM))

		ExcessQuantityDisplay is a DerivedField
			type is like Quantity
			if	(ExcessQuantity > 0)
				return ExcessQuantity
			else
				return 0

		OpenQuantityComputation is a ComputeField
			type is Decimal 13.4
			restricted
			(PurchaseOrderLineRel.QuantityToProcess - PurchaseOrderLineRel.ReceivedQuantity)

		OpenQuantity is a ConditionalField
			type is Decimal 13.4
			if(OpenQuantityComputation<0)
				(0)
			else
				(OpenQuantityComputation)

		ReceivedStockQuantityMultiple is a ConditionalField
			type is Decimal 13.4
			restricted
			if (PurchaseOrderLineRel.ItemType.Inventoried)
				if (POReceiptAdjustmentAndInspectionLineRel exists)
					(POReceiptAdjustmentAndInspectionLineRel.Quantity)
				else
					(PurchaseOrderReceiptLine.EnteredReceivedQuantity+POReceiptAdjustmentAndInspectionLineRel.Quantity)
			else
				(0)

		ReceivedStockQuantity is a ComputeField
			type is Decimal 13.4
			(ReceivedStockQuantityMultiple*PurchaseOrderReceiptLine.ReceivedUOMMultiplier)

		NewStockOnHandQuantity is a ComputeField
			type is Decimal 13.4
			(ItemLocationRel.StockOnHandQuantity+ReceivedStockQuantity)


		DerivedTenantID is a DerivedField
			type is Alpha size 60
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		POOverShipClearedAlertXML is a DerivedField
			type is XMLDocument
			restricted
			POOverShipClearedAlertXML = template.IONPOOverShipClearedAlert_PurchaseOrderBuyerMessage_ST document for this instance
			return POOverShipClearedAlertXML

		POOverShipClearedAlertDescription is a DerivedField
			type is Alpha size 100
			restricted
			return "Overshipment message cleared for PO #" + PurchaseOrder

		DerivedPulseAlertBODID is a DerivedField
			type is Alpha size 100
			restricted
			return 	"infor-nid:" + DerivedTenantID +":"+ Company.FinanceEnterpriseGroup +":"+ PurchaseOrder +":"+ "?POOverShipClearedAlert&verb=Process"

		DerivedPOCode is a DerivedField
			type is Alpha size 4
			restricted
			return PurchaseOrder.POCode

		DerivedPORelease is a DerivedField
			type is Alpha size 4
			restricted
			return PurchaseOrder.PORelease



		DerivedPOOverShipExistsAlertBODID is a DerivedField
			type is Alpha size 100
			restricted
			return "infor-nid:" + DerivedTenantID +":"+ Company.FinanceEnterpriseGroup +":"+ PurchaseOrder +":"+ "?POOverShipExistsAlert&verb=Process"

		POOverShipExistsAlertDescription is a DerivedField
			type is Alpha size 100
			restricted
			return "PO # "+ PurchaseOrder +" has Over Shipment Message"


		POOverShipExistsAlertXML is a DerivedField
			type is XMLDocument
			restricted
			POOverShipExistsAlertXML = template.IONPOOverShipExistsAlert_PurchaseOrderBuyerMessage_ST document for this instance
			return POOverShipExistsAlertXML



 		DerivedLocationStockUOM						is a DerivedField 
			type is like UnitOfMeasure
			return ItemLocationRel.ItemLocationStockUOM.UnitOfMeasure

		DerivedLocationStockUOMMultiplier 			is a DerivedField
			type is like UOMMultiplier
			return ItemLocationRel.ItemLocationStockUOM.ItemUOM.UOMConversion

		DerivedReceivedStockQuantity				is a DerivedField
			type is like Quantity
			if (HasItemLocationStockUOM)
				return ReceivedStockQuantity / DerivedLocationStockUOMMultiplier
            else
                return ReceivedStockQuantity

		DerivedNewStockOnHandQuantity				is a DerivedField
			type is like Quantity
			if (HasItemLocationStockUOM)
				return NewStockOnHandQuantity / DerivedLocationStockUOMMultiplier
            else
                return NewStockOnHandQuantity
	Conditions
		HasBatchNumber
			classic name is APBATCH
			restricted
			when (BatchNumber entered)

		RecordExists
			restricted
			when (PurchaseOrderBuyerMessage exists)

		IsApinvoice
			restricted
			when (BatchNumber entered)

		InventoryOrNonstock
			classic name is ITEM-MASTER
			restricted
			when (PurchaseOrder entered
			and   (PurchaseOrderLineRel.ItemType.Inventoried
			or    PurchaseOrderLineRel.ItemType.NonStock))

		ReleasedReceivingMessage     
			classic name is MSG-RLSD-DO-RL
			restricted
			when (BuyerMessageType.Receiving
			and   Status.Released
			and   PurchaseOrderReceipt = PurchaseOrderReceipt.PurchaseOrderReceipt
			and   PurchaseOrderReceipt.Status.Unreleased)

		AllReleasedReceivingMessage
			classic name is MSG-RLSD-DO-RL
			restricted
			when (Status.Released
			and   PurchaseOrderReceipt = PurchaseOrderReceipt.PurchaseOrderReceipt)

		UnreleasedReceivingMessage
			classic name is OPEN-REC-MSG
			restricted
			when (Status.Unreleased
			and  not PurchaseOrderReceipt.IsCreateInProgress)

		HasPurchaseOrderReceiptLine
			classic name is POLINE
			restricted
			when (PurchaseOrderReceiptLine entered)

		ManualMessage
			classic name is POMSET2
			restricted
			when (BuyerMessageType.Manual)

		ReceivingMessageAndNoDocument
			classic name is POMSET3
			restricted
			when (BuyerMessageType.Receiving
			and   DocumentNumberNumeric not entered)

		ReceivingMessage
			classic name is POMSET5
			restricted
			when (BuyerMessageType.Receiving)

		ReceivingAdjustmentMessage
			restricted
			when (DocumentNumberNumeric entered)

		IsPorecline
			restricted
			when (BuyerMessageType.Receiving)

		HasPurchaseOrder
			classic name is PURCHORDER
			restricted
			when (PurchaseOrder entered)

		HasPurchaseOrderReceipt
			classic name is RECEIVER
			restricted
			when (PurchaseOrderReceipt entered)

		CreateVendorReturn
			restricted
			when (ReturnQuantity entered
			and   Status.Unreleased)
		
		PartiallyInterfacedReceipt
			restricted
			when (PurchaseOrderReceipt.IsCreateInProgress)

		AllowRelease
			restricted
			when (not CreateVendorReturn
			and not PartiallyInterfacedReceipt)
		
		AllowReleaseAndCreateVendorReturn
			restricted
			when (CreateVendorReturn
			and not PartiallyInterfacedReceipt)

		CommentsExist
			restricted
			when (PurchaseOrderBuyerMessageCommentArray entered)

		BudgetErrorsExist
			restricted
			when (BudgetErrorRel exists)

		BuyerEntered
			restricted
			when (Buyer entered)

		PurchaseOrderVendorEntered
			restricted
			when (PurchaseOrder.Vendor entered)

		PurchaseOrderReceiptLineItemEntered
			restricted
			when (PurchaseOrderReceiptLine.Item entered)

		PurchaseOrderPurchaseFromLocationEntered
			restricted
			when (PurchaseOrder.PurchaseFromLocation entered)

		PurchaseOrderReceiptLineShipToLocationEntered
			restricted
			when (PurchaseOrderReceiptLine.ShipToLocation entered)
		
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

		HasItemLocationStockUOM
			when (ItemLocationRel.HasItemLocationStockUOM)

	Relations
		POReceiptAdjustmentAndInspectionLineRel
			one-to-one relation to POReceiptAdjustmentAndInspectionLine
			Field Mapping uses Set1
				related.Company											= Company
				related.AdjustmentInspectionDocumentType				= AdjustmentInspectionDocumentType.ReceiptAdjustment
				related.POReceiptAdjustmentAndInspection				= DocumentNumberNumeric
				related.PurchaseOrderReceiptLine						= PurchaseOrderReceiptLine

		VendorReturnLineRel
			one-to-many relation to VendorReturnLine
			Field Mapping uses ByCompanyAndVendorReturnLine
				related.Company					= Company
				related.VendorReturnLine  		= PurchaseOrderReceiptLine
			Instance Selection
				where (related.PurchaseOrderReceipt  = PurchaseOrderReceiptLine.PurchaseOrderReceipt
				and    related.DocumentNumberNumeric = DocumentNumberNumeric)

		BudgetErrorRel
			one-to-many relation to BudgetEditError
			Field Mapping uses ByBudgetGroup
				related.FinanceEnterpriseGroup              = Company.FinanceEnterpriseGroup
			Instance Selection
				where (related.BudgetEditError.BudgetEditGroup     = any PurchaseOrderLineDistributionRel.UniqueID)

		PurchaseOrderLineDistributionRel
			one-to-many relation to PurchaseOrderLineDistribution
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.PurchaseOrder  			= PurchaseOrder
				related.PurchaseOrderLine       = PurchaseOrderReceiptLine.PurchaseOrderLine

		PurchaseOrderLineRel
			one-to-one relation to PurchaseOrderLine
			valid when (HasPurchaseOrderReceiptLine)

			Field Mapping uses symbolic key
				related.Company                 = Company
				related.PurchaseOrder  			= PurchaseOrder
				related.PurchaseOrderLine       = PurchaseOrderReceiptLine.PurchaseOrderLine

		ItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           = Company
				related.InventoryLocation = PurchaseOrderReceiptLine.ShipToLocation
				related.Item              = PurchaseOrderReceiptLine.Item

		PurchaseOrderReceiptRel
			one-to-one relation to PurchaseOrderReceipt
			Field Mapping uses symbolic key
				related.Company           		= Company
				related.PurchaseOrderReceipt 	= PurchaseOrderReceipt

		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter

		FSMBODConfigurationPulseAlertRel
			one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
				related.FSMBODConfiguration.Verb 		= 2
				related.FSMBODConfiguration.Noun 		= "PulseAlert"
				related.FSMBODConfiguration.Direction 	= 1

		FSMBODConfigurationDetailPulseAlertRel
			one-to-many relation to FSMBODConfigurationDetail
			Field Mapping uses symbolic key
				related.FSMBODConfiguration.Verb 			= 2
				related.FSMBODConfiguration.Noun 			= "PulseAlert"
				related.FSMBODConfiguration.Direction 		= 1
			Instance Selection
				where (related.Alert						= LocalPulseAlertFrom
				and	   related.Enable)


		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company
				
	Sets

		Set3
			indexed
			Instance Selection
				where (ReceivingMessageAndNoDocument)
			Sort Order
				Company
				PurchaseOrderReceipt
				PurchaseOrderReceiptLine

		Set5
			indexed
			Instance Selection
				where (ReceivingMessage)
			Sort Order
				Company
				PurchaseOrderReceipt
				PurchaseOrderReceiptLine
				DocumentNumberNumeric

		AllByReceiver
			Sort Order
				Company
				PurchaseOrderReceipt
				PurchaseOrderReceiptLine
				PurchaseOrderBuyerMessage.MessageDate
				PurchaseOrderBuyerMessage.MessageSequence

		ByPurchaseOrder
			indexed
			Sort Order
				Company
				PurchaseOrder
				PurchaseOrderReceiptLine
				DocumentNumberNumeric
				Buyer
				PurchaseOrderBuyerMessage.MessageDate
				PurchaseOrderBuyerMessage.MessageSequence
				
		ByBuyer
            indexed
            Sort Order
            	Status
                Buyer
				PurchaseOrderBuyerMessage.MessageDate
                Company
                PurchaseOrder
                PurchaseOrderReceiptLine
                PurchaseOrderBuyerMessage.MessageSequence

	Field Rules
		Document
			if (BuyerMessageType.Manual) 
				required

		PurchaseOrderReceiptLine
			required

		PurchaseOrderReceipt
			required

		PurchaseOrder
			if (PurchaseOrder !entered)
				PurchaseOrder = PurchaseOrderReceipt.PurchaseOrder

			constraint (PurchaseOrder = PurchaseOrderReceipt.PurchaseOrder)
				"PurchaseOrderDoesNotMatchReceiver"

		ReturnQuantity
			constraint (ReturnQuantity <= ReceiptQuantity)
				"ReturnQuantityCannotBeGreaterThanReceiptQuantity" 


	StateCycles
		POBuyerMessageLifeCycle is a StateCycle
			state field is POBuyerMessageLifeCycleState
			Unreleased is a State
				UpdateManualMessage is an Update Action
					default label is "Update"
					valid when (ManualMessage)

					Action Rules
   						constraint (CommentsExist)
   							"MustEnterACommentForAManualBuyerMessage"

				Update is an Update Action
					valid when (not ManualMessage)
					Action Rules
						if (TransientUnitCost != PurchaseOrderReceiptLine.OriginalUnitCost) 
							invoke Update PurchaseOrderReceiptLine
								invoked.OriginalUnitCost	= TransientUnitCost
								invoked.ExtendedAmount		= TransientUnitCost * PurchaseOrderReceiptLine.VendorPriceUOMQuantity

				Release is an Instance Action
					default label is "Release"
					valid when (AllowRelease)
					completion message is "<LocalCompletionMessage>"
					Action Rules
						if (BuyerMessageType.Receiving)
							constraint (!BudgetErrorsExist)
								"CannotRelease;BudgetErrorsExist"
							if (VendorUnitCostMessage)
								constraint (PurchaseOrderReceiptLine.OriginalUnitCost entered)
									"CannotRelease;MustEnterUnitCostFromVendor"
							OverreceivingMessage	= false
							SubstituteItemMessage	= false
							VendorUnitCostMessage	= false
							if (POReceiptAdjustmentAndInspectionLineRel exists)
								invoke ClearBuyerMessage POReceiptAdjustmentAndInspectionLineRel
							else
								invoke Update PurchaseOrderReceiptLine
									invoked.BuyerMessage = false
					Exit Rules
						Status = Status.Released
						make transition to Released
						if (BuyerMessageType.Receiving)
							trigger "OverShipClearedService" PA service
								resume on error
								title is "CO:<Company>PO:<PurchaseOrder>"
								Criteria
									Company.FinanceEnterpriseGroup
									Company
								Variables
									Company
										variable name is Company
									PurchaseOrderReceipt
										variable name is PurchaseOrderReceipt
									PurchaseOrder
										variable name is PurchaseOrder
									PurchaseOrder.PORelease
										variable name is PORelease
									PurchaseOrder.POCode
										variable name is POCode
									ShipToLocation
										variable name is Location
									DocumentNumberNumeric
										variable name is DocumentNumberNumeric

							invoke TriggerPOOverShipClearedAlert
								invoked.PrmPulseAlert = "FSM_ION_POOverShipClearedAlert"

				ReleaseAndCreateVendorReturn is an Instance Action
					default label is "Release"
					valid when (AllowReleaseAndCreateVendorReturn)
					completion message is "<LocalCompletionMessage>"
					Parameters
						PrmVendorClaimType	is a VendorClaimType
						PrmShipOrHold		is a ShipOrHold
						PrmReferenceNumber	is a ReferenceNumber
						PrmCatchWeightReturnQuantity is like Quantity
							default label is "ReturnStockQuantity"
					Parameter Rules
						PrmCatchWeightReturnQuantity
							if (PurchaseOrderReceiptLine.IsCatchWeightItem)
								required
									"CatchweightReturnQuantityIsRequired"
						PrmVendorClaimType
							initial value is VendorClaimType.CreditMemo
								when(CreateVendorReturn)
						PrmShipOrHold
							initial value is ShipOrHold.Ship
								when(CreateVendorReturn)
					Entrance Rules
						if (CreateVendorReturn)
							constraint (PrmVendorClaimType entered)
								"MustEnterClaimTypeOfReturningQuantities" 
						else
							constraint (PrmVendorClaimType not entered
							and PrmShipOrHold not entered)
								"NoReturnQuantityEntered;FurtherReturnInfoMustBeBlank" 
					Action Rules
						OverreceivingMessage	= false
						SubstituteItemMessage	= false
						VendorUnitCostMessage	= false
						if (POReceiptAdjustmentAndInspectionLineRel exists)
							invoke ClearBuyerMessage POReceiptAdjustmentAndInspectionLineRel
						else
							invoke Update PurchaseOrderReceiptLine
								invoked.BuyerMessage = false
						if (CreateVendorReturn)
							invoke Create VendorReturn
								assign result to LocalVendorReturn
								invoked.Company								= Company
								invoked.ReturnFromLocation					= ShipToLocation
								invoked.CreationDate						= current corporate date
								invoked.Vendor								= Vendor
								invoked.ReplacementPurchaseOrderBuyer		= Buyer
								invoked.CreatedFromPurchaseOrder			= PurchaseOrder
								invoked.PurchaseFromLocation				= PurchaseOrderLineRel.PurchaseFromLocation
								invoked.Currency							= PurchaseOrder.Currency
								invoked.ReplaceGoods						= false
								invoked.ProcessLevel						= PurchaseOrder.ProcessLevel
								invoked.ReturnMaterialAuthorizationPrinted	= false
								invoked.Dropship							= false
								invoked.ReferenceNumber						= PrmReferenceNumber
								invoked.VendorClaimType						= PrmVendorClaimType
								invoked.ShipOrHold							= PrmShipOrHold

							invoke Create VendorReturnLine
						 		assign result to LocalVendorReturnLine
						 		invoked.Company 					= Company
					 	 		invoked.VendorReturn 				= LocalVendorReturn.VendorReturn
					 	 		invoked.PurchaseOrderReceipt 		= PurchaseOrderReceipt
					 	 		invoked.DocumentNumberNumeric		= DocumentNumberNumeric
					 	 		invoked.ReturnQuantity       		= ReturnQuantity
								invoked.UnitCost					= PurchaseOrderReceiptLine.DerivedOriginalUnitCostInReceivedUOM
					 	 		invoked.VendorItem					= PurchaseOrderReceiptLine.VendorItem
					 	 		invoked.TaxCode						= PurchaseOrderReceiptLine.TaxCode
					 	 		invoked.Item                 		= PurchaseOrderReceiptLine.Item
					 	 		invoked.ItemType             		= PurchaseOrderReceiptLine.ItemType
					 	 		invoked.Description          		= PurchaseOrderReceiptLine.Description
					 	 		invoked.OriginalPurchaseOrder 		= PurchaseOrderReceiptLine.PurchaseOrder
								invoked.OriginalPurchaseOrderLine 	= PurchaseOrderReceiptLine.PurchaseOrderLine
								invoked.ItemGTIN					= PurchaseOrderReceiptLine.ItemGTIN
								invoked.EnteredUOM					= PurchaseOrderReceiptLine.ReceivedUOM
					 	 		invoked.EnteredUOMMultiplier		= PurchaseOrderReceiptLine.ReceivedUOMMultiplier
					 	 		invoked.VendorPriceUOM				= PurchaseOrderReceiptLine.VendorPriceUOM
					 	 		invoked.VendorPriceUOMMultiplier	= PurchaseOrderReceiptLine.VendorPriceUOMMultiplier
								invoked.LastUpdateDate				= current timestamp
								invoked.LastUpdateBy				= actor
								if (PurchaseOrderReceiptLine.IsCatchWeightItem)
									invoked.CatchWeightQuantity		= PrmCatchWeightReturnQuantity
					Exit Rules


						invoke FastUpdate PurchaseOrderReceiptLine
							invoked.VendorReturn 				= LocalVendorReturn.VendorReturn
					 	 	invoked.VendorReturnLine 			= LocalVendorReturnLine.VendorReturnLine
						Status = Status.Released
						make transition to Released
						trigger "OverShipClearedService" PA service
							resume on error
							title is "CO:<Company>PO:<PurchaseOrder>"
							Criteria
								Company.FinanceEnterpriseGroup
								Company
							Variables
								Company
									variable name is Company
								PurchaseOrderReceipt
									variable name is PurchaseOrderReceipt
								PurchaseOrder
									variable name is PurchaseOrder
								PurchaseOrder.PORelease
									variable name is PORelease
								PurchaseOrder.POCode
									variable name is POCode
								ShipToLocation
									variable name is Location
								DocumentNumberNumeric
									variable name is DocumentNumberNumeric

						invoke TriggerPOOverShipClearedAlert
							invoked.PrmPulseAlert = "FSM_ION_POOverShipClearedAlert"

			Released is a State

	Actions



		TriggerOverShipExistsAlert is an Instance Action
			restricted
			Parameters
				PrmReceiverNumber is Numeric size 6
			Action Rules
				trigger "OverShipExistsService" PA service
					resume on error
					title is "CO:<Company>PO:<PurchaseOrder>"
					Criteria
						Company.FinanceEnterpriseGroup
						Company
					Variables
						Company
							variable name is Company
						PurchaseOrderReceipt
							variable name is PurchaseOrderReceipt
						PurchaseOrder
							variable name is PurchaseOrder
						PurchaseOrder.PORelease
							variable name is PORelease
						PurchaseOrder.POCode
							variable name is POCode
						ShipToLocation
							variable name is Location
						DocumentNumberNumeric
							variable name is DocumentNumberNumeric

		Create is a Create Action
			restricted

			Field Rules
				Status = Status.Unreleased
			Exit Rules
				invoke TriggerOverShipExistsAlert

				invoke TriggerPOOverShipExistsAlert
					invoked.PrmPulseAlert = "FSM_ION_POOverShipExistsAlert"
























		CreateManualMessage is a Create Action
			Field Rules
				BuyerMessageType
					BuyerMessageType = "M"

				Vendor
					Vendor = PurchaseOrderReceiptRel.Vendor

				Status
					Status = Status.Unreleased


			Action Rules
   				constraint (CommentsExist)
   					"MustEnterACommentForAManualBuyerMessage"

		UpdateFromPOReceiptLine is an Instance Action
			restricted
			Parameters

				PrmOverreceivingMessage   	is Boolean
				PrmSubstituteItemMessage	is Boolean
				PrmVendorUnitCostMessage	is Boolean

			Action Rules
				if (CreateVendorReturn)
					initialize OverreceivingMessage
				else
					OverreceivingMessage	=	PrmOverreceivingMessage
				SubstituteItemMessage	=	PrmSubstituteItemMessage
				VendorUnitCostMessage	=	PrmVendorUnitCostMessage
				if (Status.Released)
					Status = Status.Unreleased
					PurchaseOrderBuyerMessage.MessageDate		= current timestamp

				make transition to Unreleased

		RecheckBudgetErrors is an Instance Action
			valid when (BudgetErrorsExist)

			Action Rules

				for each PurchaseOrderLineDistributionRel
					if (!each.ContractCommitments)
						invoke EditCommitmentIncrease each
							invoked.FromReceiving 			= true
							invoked.ParmCommitmentIncrease 	= first BudgetErrorRel.DerivedChangeAmount

		Delete is a Delete Action
			restricted

			Action Rules

				if (BudgetErrorsExist)
					invoke Delete BudgetErrorRel

		Purge is a Purge Action
			restricted

		PurgePurchaseOrderBuyerMessage is a Set Action
			restricted
			Parameters
				PrmCompany				is a PurchasingCompany
				PrmPurchaseOrderReceipt	is like PurchaseOrderReceipt

			Instance Selection
				include deleted records
				where (Company				= PrmCompany
				and    PurchaseOrderReceipt	= PrmPurchaseOrderReceipt)

			Action Rules
				Instance Rules
					invoke Purge


		TriggerPOOverShipClearedAlert is an Instance Action
			restricted
			Parameters
				PrmPulseAlert is Alpha size 100
			Action Rules
				LocalPulseAlertFrom = PrmPulseAlert
				if (Company.FinanceEnterpriseGroup.BODTrigger and FSMBODConfigurationPulseAlertRel.Enable)
				  	if(FSMBODConfigurationDetailPulseAlertRel.Enable)
						invoke TriggerPulseAlert FSMBODConfigurationPulseAlertRel
							invoked.PrmActorGroup 	= "BUYER"
							invoked.PrmMainXML 		= POOverShipClearedAlertXML
							invoked.PrmDescription	= POOverShipClearedAlertDescription
							invoked.PrmBODID		= DerivedPulseAlertBODID



		TriggerPOOverShipExistsAlert is an Instance Action
			restricted
			Parameters
				PrmPulseAlert is Alpha size 100
			Action Rules
				LocalPulseAlertFrom = PrmPulseAlert
				if (Company.FinanceEnterpriseGroup.BODTrigger and FSMBODConfigurationPulseAlertRel.Enable)
				  	if(FSMBODConfigurationDetailPulseAlertRel.Enable)
						invoke TriggerPulseAlert FSMBODConfigurationPulseAlertRel
							invoked.PrmActorGroup 	= "BUYER"
							invoked.PrmMainXML 		= POOverShipExistsAlertXML
							invoked.PrmDescription	= POOverShipExistsAlertDescription
							invoked.PrmBODID		= DerivedPOOverShipExistsAlertBODID

