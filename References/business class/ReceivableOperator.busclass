ReceivableOperator is a BusinessClass
	owned by ar
	prefix is AOP
	classic name is AROPTR

	Ontology
		symbolic key is ReceivableOperator
			classic set name is AOPSET1
			classic name is OPR-CODE

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		Name
		ActiveStatus	is AlphaUpper size 1
			States
				Active		value is "A"
				Inactive	value is "I"
		UsedFlag		is Boolean
			classic name is USED-FL


	Field Rules
		Name
			required


	Attach Rules
		if (Company.CashOperatorRequired)
			constraint (ActiveStatus.Active)
				"OperatorIsNotActive"


	Rule Blocks

		SumUnnappliedReceivablePaymentRelAmounts	
			if (!SumOfUnnappliedReceivablePaymentRelDone)	
				initialize SumOfUnnappliedReceivablePaymentRelBaseAmount	
					for each UnnappliedReceivablePaymentRel
						SumOfUnnappliedReceivablePaymentRelBaseAmount		 += each.BaseAmount	
				SumOfUnnappliedReceivablePaymentRelDone = true	


	Local Fields

		SumOfUnnappliedReceivablePaymentRelDone			is Boolean	
		SumOfUnnappliedReceivablePaymentRelBaseAmount	is an InternationalAmount	


	Derived Fields

		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforReceivableOperator"

		ContextMessageText is a MessageField
			restricted
			"ReceivableOperator<ReceivableOperator>ForCompany<Company>"

		UnnappliedReceivablePaymentBalance is a DerivedField	
			type is like InternationalAmount
			include SumUnnappliedReceivablePaymentRelAmounts	

			return (SumOfUnnappliedReceivablePaymentRelBaseAmount)	


	Conditions

		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	   CompanySecurityGroupMemberRel exists)


	Relations

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company
		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company

		UnnappliedReceivablePaymentRel
			one-to-many relation to ReceivablePayment
			Field Mapping uses Set4	
				related.Company		= Company
			Instance Selection
				where (related.ReceivableOperator = ReceivableOperator)	


	Actions

		Create is a Create Action
			Field Rules
				ActiveStatus
					default to ActiveStatus.Active

		Update is an Update Action

		Delete is a Delete Action


		Anonymize is an Instance Action	
			restricted
			Action Rules
				Name	= 999999999

			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup			= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.Status							= 1
					invoked.AffectedBusinessClass			= "AR9"
					invoked.ReceivableCompany				= Company
					invoked.ReceivableOperator				= ReceivableOperator


		PurgeAuditLog is an Instance Action	
			restricted
			Action Rules
				invoke purge audit log entries

			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup			= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.Status							= 2
					invoked.AffectedBusinessClass			= "AR9"
					invoked.ReceivableCompany				= Company
					invoked.ReceivableOperator				= ReceivableOperator


