ClinicalProcedure is a BusinessClass
    owned by ic
    prefix is CP
    
    Ontology
        symbolic key is ClinicalProcedure
    
    Patterns
        implements StaticJava

    Persistent Fields
        ClinicalProcedureName	is Alpha size 100

        Active					is Boolean 


    Derived Fields
        ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforClinicalProcedure"

        ContextMessageText is a MessageField
            restricted
            "ClinicalProcedure<ClinicalProcedure><ClinicalProcedureName>"

        ActiveMessage is a LabelField
            "Active"
        
        InactiveMessage is a LabelField
            "Inactive"

        ActiveDisplay is a DerivedField
            type is MessageField
            default label is "Active"
            if (Active)
                return ActiveMessage
            else
                return InactiveMessage

    Conditions
		RecordExists
			restricted
			when (ClinicalProcedure exists)

    Relations
        PreferenceCardProcedureRel
            one-to-many relation to PreferenceCardProcedure
            Field Mapping uses ByClinicalProcedure
                related.ItemGroup       = ItemGroup
                related.Procedure       = ClinicalProcedure

    Field Rules
        ItemGroup
            constraint (ItemGroup.PreferenceCardsInUse)
                "ItemGroupDoesNotUsePreferenceCard"

        ClinicalProcedureName
            required

        Active
            initial value is true

    Actions
        Create is a Create Action
            Exit Rules
                for each PreferenceCardProcedureRel
                    invoke ReviewErrors each
        
        Update is an Update Action
            Action Rules
                if (Active changed)
                    for each PreferenceCardProcedureRel
                        invoke ReviewErrors each

        Delete is a Delete Action
            Entrance Rules
                constraint (PreferenceCardProcedureRel not exists)
                    "CannotDelete;ClinicalProcedureExistsInAPreferenceCard"
