ConfigurationParameterKey is a BusinessClass
    owned by la
    prefix is CPAK
	store using com.lawson.rdtech.framework.ConfigurationParameterKeyStorage
	
    Ontology
    	symbolic key is ConfigurationParameterKey

    Patterns
        disable Auditing
        disable EffectiveDated
        disable UniqueID
    	implements ReadOnly
    	enable explicit context override
        
    Persistent Fields
    	FormatExample  						is Alpha size up to 150
    	Residence 							is Alpha size 30
    		States
    			EnvironmentDataArea 	  				value is "EnvDataArea"         			
				BaseEnvironmentDataArea   				value is "BaseEnvDataArea"     			// Only in the environment data area named "gen" - No hierarchy lookup
				ApplicationDataArea       				value is "ApplicationDataArea" 			
				EnvironmentOrBaseDataArea 				value is "EnvOrBaseEnvDataArea"			
				ApplicationOrEnvironmentDataArea		value is "ApplicationOrEnvDataArea"		
				ApplicationOrBaseEnvironmentDataArea	value is "ApplicationOrBaseEnvDataArea" 
				AnyDataArea								value is "AnyDataArea"				 	
		
						
		Usage								is Alpha size 12
			States
				Key				value is "Key"
				Suffix			value is "Suffix"
				Prefix			value is "Prefix"
				KeyAndSuffix	value is "KeyAndSuffix"
				KeyAndPrefix	value is "KeyAndPrefix"
		PreferredCategory					is a ConfigurationParameterCategory
			default label is "PreferredComponent"
		HelpText							is Text
		PreferredDataType					is a PrimitiveType
		CanBeKey							is Boolean
			default label is untranslatable:"CanBeUsedAsAKey"
		IncludesLowerDataArea				is Boolean
		IncludesUpperDataArea				is Boolean
		IncludesLowerEnvironmentDataArea	is Boolean
		IncludesUpperEnvironmentDataArea	is Boolean
		IncludesBusinessView				is Boolean
		IncludesBusinessClass				is Boolean
		IncludesBusinessAction				is Boolean
		IncludesBusinessForm				is Boolean
		IncludesBusinessList				is Boolean
		IncludesBusinessCardView			is Boolean
		IncludesBusinessPage				is Boolean
		IncludesBusinessMenu				is Boolean
		IncludesActor  						is Boolean
		IncludesActorGroup					is Boolean
		Deprecated							is Boolean
		IncludesBusinessRelation			is Boolean
		HasStates							is Boolean
			default label is "States"
		StrictStates						is Boolean
		IncludesSequence					is Boolean

	Transient Fields
		DataArea
		EnvironmentDataArea is a DataArea
		BusinessView
		BusinessClass
		BusinessViewAction 	is a BusinessAction
			default label is "Action"
			context of BusinessView
		BusinessClassAction is a BusinessAction
			default label is "Action"
			context of BusinessClass



		BusinessClassForm  	is a BusinessForm
			default label is "Form"
			context of  BusinessClass			
		BusinessList
			default label is "List"
			context of  BusinessClass	
		Actor
		BusinessRelation
			default label is "Relation"
			context of  BusinessClass
				
	Conditions
		CanBeSuffix
			default label is "KeyCanBeSuffixed"
			when (Usage.Suffix or Usage.KeyAndSuffix)
			
		CanBePrefix
			default label is "KeyCanBePrefixed"
			when (Usage.Prefix or Usage.KeyAndPrefix)
			
		CanCreateParameter
			restricted
			when ((not Usage.Key or not ConfigurationParametersRel exists) 
			and   ValidForResidency)
			
		ValidForResidency
			restricted
			when (Residence.ApplicationDataArea
			or    Residence.AnyDataArea 
			or    Residence.ApplicationOrEnvironmentDataArea
			or    Residence.ApplicationOrBaseEnvironmentDataArea
			or   (Residence.EnvironmentDataArea and IsEnvDa) 
			or   (Residence.BaseEnvironmentDataArea and IsBaseEnvDa)
			or   (Residence.EnvironmentOrBaseDataArea and (IsEnvDa or IsBaseEnvDa)))
			
		UsesDataArea
			restricted
			when (IncludesLowerDataArea or IncludesUpperDataArea)	
			
		UsesEnvironmentDataArea
			restricted
			when (IncludesLowerEnvironmentDataArea or IncludesUpperEnvironmentDataArea)		

		HasDefinedParameters
			restricted
			when (ConfigurationParametersRel exists)

	Derived Fields
		ParameterCount is a ComputeField
			type is Numeric size 3
			(instance count of ConfigurationParametersRel) 
			




			
		IsEnvDa is a NativeField
			type is Boolean
			restricted						
			
		IsBaseEnvDa is a NativeField
			type is Boolean
			restricted			
		
	Relations
		ConfigurationParametersRel
    		one-to-many relation to ConfigurationParameter
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ConfigurationParameter.ConfigurationID = ConfigurationParameterCategory
				and    related.ConfigurationParameter.ConfigParamKey = ConfigurationParameterKey)
				







	Form Invokes
		CreateParameter
			invoke Create ConfigurationParameter
				invoked.ConfigurationParameter.ConfigurationID 	= ConfigurationParameterCategory
				invoked.ConfigurationParameter.Name				= ConfigurationParameterKey
				invoked.TConfigurationParameterCategory 		= ConfigurationParameterCategory
				invoked.TConfigurationParameterKey				= ConfigurationParameterKey
				invoked.Type									= PreferredDataType
	
	Actions
		CreateParameter is an Instance Action
			valid when (CanCreateParameter)
			manual update
			
			Parameters
				Name				is Alpha size up to 150
				Type				is a PrimitiveType
    			Length				is Numeric size 6
    			Precision			is Numeric size 1
    			StateValue			is a ConfigurationParameterKeyFieldState
    			Value is Alpha size up to 300 



			
			Parameter Rules
				Name
					initial value is ConfigurationParameterKey
					default to ConfigurationParameterKey
					
				Type
					initial value is PreferredDataType
					default to PreferredDataType
					
			Action Rules
				invoke Create ConfigurationParameter
					invoked.ConfigurationParameter.ConfigurationID 	= ConfigurationParameterCategory
					invoked.ConfigurationParameter.Name				= Name
					invoked.Type									= Type
					invoked.Length									= Length
					invoked.Precision								= Precision
					invoked.Value									= Value
					invoked.TConfigurationParameterKeyFieldState	= StateValue
				
	Sets
		ByPreferredCategory
			not indexed
			no duplicates
			Sort Order
				PreferredCategory
				ConfigurationParameterKey
