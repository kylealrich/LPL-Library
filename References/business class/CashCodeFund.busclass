CashCodeFund is a BusinessClass
    owned by cb
    prefix is CCFU

    Ontology
        symbolic key is CashCodeFund

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		EditLevel			is Numeric 1
			States
				Fund			value is 0
				Group			value is 1
		EditGroup			is a Name	 
			holds pii
		AccountingEntity
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>"
		AccountingUnit
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>"
        FinanceDimension1
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>"
		ClaimOnCash			is an InternationalAmount
		PostedAmount		is an InternationalAmount
        	disable Auditing
		CommittedAmount		is an InternationalAmount
        	disable Auditing
		MinimumFundAmount	is an InternationalAmount
		LineOfCreditAmount	is an InternationalAmount
		SourceCashCodeFund
		ExcludeFromEdit		is Boolean					
		FundUsedForCustodialAccounting is Boolean		
		

    Derived Fields
     	CurrentClaimOnCash is a DerivedField
			type is like InternationalAmount
			return (ClaimOnCash + PostedAmount + CommittedAmount)
 
    	AvailableAmount is a DerivedField
			type is like InternationalAmount
			if (EditLevel.Group)
				return (sum CashCodeFund children.AvailableAmount)
			else
				if (IsSourcCashCodeFund)
					return (sum FundedCashCodeFundRel.AvailableAmount + ClaimOnCash + PostedAmount + CommittedAmount - MinimumFundAmount + LineOfCreditAmount)
				else
					return (ClaimOnCash + PostedAmount + CommittedAmount - MinimumFundAmount + LineOfCreditAmount)

    	EditAvailableAmount is a DerivedField
			type is like InternationalAmount
			if (ParentCashCodeFund entered)
				return ParentCashCodeFund.AvailableAmount
			else
				if (SourceCashCodeFund entered)
					return SourceCashCodeFund.CashCodeFund.AvailableAmount
				else
					return AvailableAmount

		GroupMemberMsg is a LabelField
			"GroupMember"	

		FundMsg is a LabelField
			"Fund"	

		GroupMsg is a LabelField
			"Group"	

		GroupAvailableAmountMsg is a LabelField
			"GroupAvailableAmount"	

		SourceAvailableAmountMsg is a LabelField
			"SourceAvailableAmount"	

		DerivedEditLevel is a DerivedField
			type is Alpha 12
			if (ParentCashCodeFund entered)
				return GroupMemberMsg
			else
			if (EditLevel.Fund)
				return FundMsg
			else
			if (EditLevel.Group)
				return GroupMsg

    	FundValue is a DerivedField
			type is like AccountingUnit
			if (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.AccountingUnit)
				return AccountingUnit
			else
				return FinanceDimension1
				
		EditAvailableAmountLabel is a DerivedField
			type is MessageField
			if (ParentCashCodeFund entered)
				return GroupAvailableAmountMsg
			if (SourceCashCodeFund entered)
				return SourceAvailableAmountMsg

	Transient Fields

    Conditions
    	CanAddToEditGroup
    		restricted
    		when (CashCode.EditAvailableFunds
    		and   EditLevel.Fund
    		and	  ParentCashCodeFund	!entered
    		and   SourceCashCodeFund	!entered)

		CanRemoveFromEditGroup
			restricted
			when (ParentCashCodeFund entered)

		HasChild
    		restricted
    		when (CashCodeFund children exist)
    		
    	CanDeleteCashCodeFund
    		restricted
    		when (PostedAmount 		!entered
    		and   CommittedAmount	!entered
    		and   !HasChild)

		DetailWithBalance
			restricted
			when (EditLevel.Fund
			and   AvailableAmount entered)
			
		HasSourceCashCodeFund
			when (SourceCashCodeFund entered)
			
		IsSourcCashCodeFund
			when (FundedCashCodeFundRel exists)
			
		ViewSourceAvailableAmount
			when (SourceCashCodeFund	entered
			or    ParentCashCodeFund	entered)
						
    Relations
    	DuplicateCashCodeFundRel
			one-to-many relation to CashCodeFund
			Field Mapping uses ByAccountingEntityFund
				related.CashManagementGroup = CashManagementGroup
				related.CashCode			= CashCode
				related.AccountingEntity	= AccountingEntity
				related.AccountingUnit		= AccountingUnit
				related.FinanceDimension1	= FinanceDimension1
			Instance Selection
				where (related.CashCodeFund != CashCodeFund)

    	CashCodeFundWithNoParentRel
			one-to-many relation to CashCodeFund
			Field Mapping uses symbolic key
				related.CashManagementGroup = CashManagementGroup
				related.CashCode			= CashCode
			Instance Selection
				where (related.EditLevel.Fund
				and    related.ParentCashCodeFund !entered
				and    related.SourceCashCodeFund !entered)

    	FundedCashCodeFundRel
			one-to-many relation to CashCodeFund
			Field Mapping uses BySourceCashCodeFund
				related.CashManagementGroup 			= CashManagementGroup
				related.SourceCashCodeFund.CashCode		= CashCode
				related.SourceCashCodeFund.CashCodeFund	= CashCodeFund

               
    Sets
    	ByAccountingEntityFund
			Instance Selection
				where (EditLevel.Fund)
			Sort Order
				CashManagementGroup
				CashCode
				AccountingEntity
				AccountingUnit
				FinanceDimension1

		ByEditLevel
			Sort Order
				CashManagementGroup
				CashCode
				EditLevel
				AccountingEntity
				AccountingUnit
				FinanceDimension1
				CashCodeFund

		BySourceCashCodeFund
			Sort Order
				CashManagementGroup
				SourceCashCodeFund
				CashCode
				CashCodeFund
				
	Field Rules
		EditGroup
			if (EditLevel.Group)
				required
					"EditGroupIsRequired"

		CashCodeFund
			if (EditLevel.Fund)
				constraint (!DuplicateCashCodeFundRel exists)
					"CashCodeFundAlreadyExists"

		AccountingEntity
			if (EditLevel.Fund)
				required

		AccountingUnit
			constraint (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.AccountingUnit)
				"FundDimensionMustBeSetToAccountingUnitWhenAccountingUnitEntered"
			if (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.AccountingUnit)
				if (EditLevel.Fund)
					required

		FinanceDimension1
			constraint (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.Dimension1)
				"FundDimensionMustBeSetToDimension1WhenDimension1Entered"
			if (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.Dimension1)
				if (EditLevel.Fund)
					required

		MinimumFundAmount
			constraint (MinimumFundAmount >= 0)
				"MinimumFundAmountCannotBeNegative"

		LineOfCreditAmount
			constraint (LineOfCreditAmount >= 0)
				"LineOfCreditAmountCannotBeNegative"
				
		SourceCashCodeFund
			constraint (!IsSourcCashCodeFund)
				"CannotAssignSourceCashCodeToACashCodeAlreadyDesignatedAsASource"
				
			constraint (ParentCashCodeFund !entered)
				"CannotAssignSourceCashCodeToCashCodeFundAssignedToAGroup"
		
		ExcludeFromEdit										
			constraint (EditLevel.Fund)
				"ExcludeFromEditOnlyValidForFundLevel"

			constraint (!IsSourcCashCodeFund)
				"ExcludeFromEditCannotBeUsedAsASourceCashCodeFund"
				
			constraint (ParentCashCodeFund !entered)
				"ExcludeFromEditCannotBeUsedWithEditGroups"
		
				
	Actions
		UpdatePostedAmount is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance
			Parameters
				PrmPostedAmount				is an InternationalAmount
				PrmCommittedAmount			is an InternationalAmount

			Action Rules
				PostedAmount		+= PrmPostedAmount
				CommittedAmount		-= PrmCommittedAmount



		UpdateCommittedAmount is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance
			Parameters
				PrmCommittedAmount			is an InternationalAmount
			Action Rules
				CommittedAmount 	+= PrmCommittedAmount


		Create is a Create Action
			Action Rules

		CreateEditGroup is a Create Action	
			valid when (CashCode.EditAvailableFunds)				
			Action Rules
				EditLevel = 1 

		Update is an Update Action
			Action Rules

		Delete is a Delete Action
			valid when (CanDeleteCashCodeFund)
	     			
		AddToEditGroup is an Instance Action
			valid when (CanAddToEditGroup)
			Parameters
				PrmEditGroup is a CashCodeFund
					
			Parameter Rules
				PrmEditGroup
					required
						"EditGroupIsRequired"
						
					constraint (!ParentCashCodeFund entered)
						"CashCodeFundAlreadyAttachedToEditGroup<ParentCashCodeFund.EditGroup>"
						
					constraint (PrmEditGroup != CashCodeFund)
						"CannotAddCashCodeFundToItsOwnEditGroup"

					constraint (!SourceCashCodeFund entered)
						"CashCodeFundIsBeingSourcedByAnotherFund:CannotAddToEditGroup"
			
			Action Rules
				ParentCashCodeFund = PrmEditGroup
									
		RemoveFromEditGroup is an Instance Action
			valid when (CanRemoveFromEditGroup)
			Action Rules
				initialize ParentCashCodeFund		
	     			
