ProjectContractLineModification is a BusinessClass
	owned by Projects
	
	prefix is PCLM
	sql name is ProjectContractLineMod		
	
	Ontology
		symbolic key is ProjectContractLineModification
	
	Patterns

	Persistent Fields
		FinanceDimension2
		Description
    	OriginalPriority			is Numeric size 2
    	OriginalFundingGroup    	is Numeric size 2
    	OriginalBillingGroup		is a ProjectBillingGroup
    	OriginalPercent				is Percent size 8.5
    	OriginalFundedAmount		is a CurrencyAmount
			precision is ProjectContract.Currency.NumberOfDecimals
    	Priority					is Numeric size 2
    	FundingGroup				is Numeric size 2
    	BillingGroup				is a ProjectBillingGroup
    	Percent					    is Percent size 8.5
    	FundedAmount				is a CurrencyAmount
			precision is ProjectContract.Currency.NumberOfDecimals
		Status						is Numeric size 1				
			States	
				Entered					value is 1
				Released				value is 2
		ReleaseDate					is Date		
		ErrorMessage				is Text
			disable Auditing
	
	Local Fields
		ErrorFound							is Boolean
		LocalErrorMessage					is Text	
	    
	Rule Blocks		
				
    Derived Fields
    
		DerivedUpdateContractAmount is a DerivedField
			type is like InternationalAmount
				precision is ProjectContract.Currency.NumberOfDecimals
			if (OriginalFundedAmount != FundedAmount)
				if (OriginalFundedAmount > FundedAmount)				
					return (FundedAmount - OriginalFundedAmount)
				else	
					if (OriginalFundedAmount < FundedAmount)
						return (FundedAmount - OriginalFundedAmount) 	

		DerivedUpdateFinanceDimension2Amount is a DerivedField
			type is like InternationalAmount
				precision is FinanceDimension2.Currency.NumberOfDecimals
			if (OriginalFundedAmount != FundedAmount)
				if (OriginalFundedAmount > FundedAmount)				
					return (FundedAmount - OriginalFundedAmount)
				else	
					if (OriginalFundedAmount < FundedAmount)
						return (FundedAmount - OriginalFundedAmount) 	

		DerivedContractAmount is a DerivedField
			type is like InternationalAmount
				precision is ProjectContract.Currency.NumberOfDecimals
			return (ProjectContract.ContractAmount + DerivedUpdateContractAmount)								

		DerivedFinanceDimension2Amount is a DerivedField
			type is like InternationalAmount
				precision is FinanceDimension2.Currency.NumberOfDecimals
			return (FinanceDimension2.Amount + DerivedUpdateFinanceDimension2Amount)								
				
		ErrorsExistMF is a MessageField
			"<ErrorMessage>"
						
	Field Rules
	
		Description
			required	
			if (!EligibleToRelease)
				cannot be changed
					"CannotChange;LineHasBeenReleased"	
			
		Priority
			if (!EligibleToRelease)
				cannot be changed
					"CannotChange;LineHasBeenReleased"					

		FundingGroup
			if (!EligibleToRelease)
				cannot be changed
					"CannotChange;LineHasBeenReleased"	

		Percent
			if (!EligibleToRelease)
				cannot be changed
					"CannotChange;LineHasBeenReleased"					
			
		FundedAmount
			if (!EligibleToRelease)
				cannot be changed
					"CannotChange;LineHasBeenReleased"					
							
		ReleaseDate
			cannot be changed
				"CannotChangeReleaseDate"					
				
	Conditions
		
		EligibleToDelete
			restricted
			when (Status.Entered)

		EligibleToRelease			
			restricted
			when (Status.Entered)
			
		HasErrors
			restricted	
			when (ErrorMessage entered)			
	
	Relations
	
		ProjectFundingSourceRel
			one-to-one relation to ProjectFundingSource
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectContract			= ProjectContract
				related.FinanceDimension2		= FinanceDimension2

		ContractProjectFundingSourceRel
			one-to-many relation to ProjectFundingSource
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectContract			= ProjectContract

		ProjectContractLineModificationReleasedRel
			one-to-many relation to ProjectContractLineModification
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.ProjectContract			    = ProjectContract
				related.ProjectContractModification = ProjectContractModification
			Instance Selection
				where (related.Status = Status.Released)
				
		DimensionInEnterpriseStructureRel
			one-to-one relation to FinanceDimension2Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	   = FinanceEnterpriseGroup
				related.FinanceDimension2Structure = FinanceEnterpriseGroup.EnterpriseDim2Structure
				related.FinanceDimension2		   = FinanceDimension2				

	Sets
	
    Actions
    
		Create is a Create Action
			restricted
		
		CreateModificationLine is a Create Action
			restricted
		    Parameters
		        PrmFinanceEnterpriseGroup  is a FinanceEnterpriseGroup
		        PrmProjectContract		   is a ProjectContract
				PrmFinanceDimension2	   is a FinanceDimension2
				PrmProjectContractModification is a ProjectContractModification
				PrmDescription             is a Description
    			PrmOriginalPriority		   is Numeric size 2
    			PrmOriginalFundingGroup    is Numeric size 2
				PrmOriginalBillingGroup    is a ProjectBillingGroup
    			PrmOriginalPercent		   is Percent size 8.5
    			PrmOriginalFundedAmount	   is a CurrencyAmount
					precision is PrmProjectContract.Currency.NumberOfDecimals
    			PrmPriority				   is Numeric size 2
    			PrmFundingGroup			   is Numeric size 2
				PrmBillingGroup            is a ProjectBillingGroup
    			PrmPercent				   is Percent size 8.5
    			PrmFundedAmount			   is a CurrencyAmount
					precision is PrmProjectContract.Currency.NumberOfDecimals
				PrmStatus				   is Numeric size 1				
					States	
						Entered				value is 1
						Released            value is 2
		    Parameter Rules
		        PrmFinanceEnterpriseGroup
		            required
		        PrmProjectContract
		        	required 
		        PrmFinanceDimension2
		        	required	   
		    Action Rules
		        invoke Create this instance
		            invoked.FinanceEnterpriseGroup  	= PrmFinanceEnterpriseGroup					
					invoked.ProjectContract				= PrmProjectContract
					invoked.FinanceDimension2			= PrmFinanceDimension2
					invoked.ProjectContractModification = PrmProjectContractModification
					invoked.Description                 = PrmDescription
					invoked.OriginalPriority            = PrmOriginalPriority
					invoked.OriginalFundingGroup		= PrmOriginalFundingGroup
					invoked.OriginalBillingGroup    	= PrmOriginalBillingGroup			
					invoked.OriginalPercent 		    = PrmOriginalPercent
					invoked.OriginalFundedAmount        = PrmOriginalFundedAmount
					invoked.Priority                    = PrmPriority
					invoked.FundingGroup				= PrmFundingGroup
					invoked.BillingGroup                = PrmBillingGroup					
					invoked.Percent 			        = PrmPercent
					invoked.FundedAmount                = PrmFundedAmount
					invoked.Status                      = PrmStatus
		
		Update is an Update Action

		Delete is a Delete Action
			valid when (EligibleToDelete)

		ReleaseModification is an Instance Action
			restricted
			Action Rules
    			initialize ErrorMessage
    			ErrorFound = false
				invoke Update ProjectFundingSourceRel
					resume on error		
						ErrorFound = true
						LocalErrorMessage = error message  
			    	invoked.Priority   			= Priority
			    	invoked.FundingGroup		= FundingGroup
			    	invoked.BillingGroup		= BillingGroup			    	
			    	invoked.Percent     		= Percent					
			    	invoked.FundedAmount 		= FundedAmount	
					invoked.UpdateFromProjectContractLineModification = true
					if (!ProjectContractModification.UpdateFD2Amount)						
						invoked.CheckFundingSourceAmounts = true					
					if (!ProjectContractModification.UpdateProjectContractAmount)						
						invoked.CheckProjectContractAmounts = true
    			if (ErrorFound)
					Status       = 1
    				ErrorMessage = LocalErrorMessage
				else 					
					Status       = 2
					ReleaseDate = current corporate date
					ErrorMessage = blank
			Exit Rules
				if (LocalErrorMessage = blank)
					if (OriginalFundedAmount != FundedAmount)
						if (ProjectContractModification.UpdateProjectContractAmount)					
							invoke Update ProjectContractModification
								invoked.NewContractAmount = DerivedContractAmount
						if (!ProjectContractModification.UpdateProjectContractAmount)
							invoke Update ProjectContractModification														
								invoked.NewContractAmount = ProjectContract.ContractAmount
						if (ProjectContractModification.UpdateProjectContractAmount)
							invoke Update ProjectContract
								invoked.ContractAmount = DerivedContractAmount
						if (ProjectContractModification.UpdateFD2Amount)
							if (FinanceDimension2.Amount entered)	
								invoke Update DimensionInEnterpriseStructureRel.FinanceDimension2
									invoked.BypassUpdateStructure = true
									invoked.Amount = DerivedFinanceDimension2Amount
