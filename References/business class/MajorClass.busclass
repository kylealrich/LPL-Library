MajorClass is a BusinessClass
	owned by ic
	prefix is MAJ
	classic name is MAJORCL

	Ontology
		symbolic key is MajorClass
			classic set name is MAJSET1

	Patterns
		implements StaticJava
		disable AuditIndex
		implements BODId
		
	Persistent Fields
		Description
		Active is Boolean
			classic name is ACTIVE-STATUS

	Local Fields
		LocalContractTierGroup
		LocalContractImportTierGroup
		LocalMinorClass	is a MinorClass
		LocalContractRebate
		ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		LocalNativeLPLBODTrigger			is Boolean
		LocalBodAccountingEntity			is AlphaUpper size 12
		LocalCrossAccountingEntity 			is Alpha 40
		LocalCleanDocIDAccountingentity 	is Alpha 40
		LocalBODFormattedID					is Alpha 150
		MajorClassCleanDocXMLBOD			is XMLDocument
		LocalConfigurationParameter			is Alpha size up to 200
		NewBODTracker  						is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker			is Numeric 15
		Error            					is Boolean
	    ErrorMessage     					is Alpha 300
	    LocalBODTrigger						is Boolean
	    LocalRestartFlag					is Boolean
		LocalSourceValue					is Alpha 30
		
	Attach Rules
		if (not BypassRuleForTheseFiles)
			if (ClassType.InventoryClass)
				constraint (Active)
					"InventoryClass<MajorClass>MustBeActive"
			else
			if (ClassType.PurchasingClass)
				constraint (Active)
					"PurchasingClass<MajorClass>MustBeActive"
			else
			if (ClassType.SalesClass)
				constraint (Active)
					"SalesClass<MajorClass>MustBeActive"
			else
			if (ClassType.FreightClass)
				constraint (Active)
					"FreightClass<MajorClass>MustBeActive"
			else
			if (ClassType.PersonalProtectiveEquipment)
				constraint (Active)
					"PersonalProtectiveEquipmentClass<MajorClass>MustBeActive"

	Conditions

		IsActive
			restricted
			when (Active)

		IsInactive
			restricted
			when (not Active)

		IsPurchasingClass
			classic name is PURCHASE-CLASS
			restricted
			when (ClassType.PurchasingClass)

		IsSalesClass
			restricted
			when (ClassType.SalesClass)

		MajorClassExists
			restricted
			when (MajorClass exists)

		IsInventoryClass
			restricted
			when (ClassType.InventoryClass)

		IsFreightClass
			restricted
			when (ClassType.FreightClass)

		IsPPEClass
			restricted
			when (ClassType.PersonalProtectiveEquipment)

		ItemGroupEntered
			restricted
			when (ItemGroup entered)
		
		BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "PurchaseOrderEDIOutput")

	Context Fields
		FSMInboundBODTracker

	Derived Fields
		ClassNameDescription is a MessageField
			default label is "MajorClass"
			"<Description>_(<MajorClass>)"

		MajorClassDescription is a StringField
			type is Text
			default label is "MajorClass"
			MajorClass " - "Description


		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedMultipleFEG is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "IsMultipleFEG"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedCleanDocumentID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "RequiredCleanDocumentID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedTriggerBODBasedOnCrossReference is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "TriggerBODBasedOnCrossReference"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedMajorClassConfigAE is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "CodeDefinition_MajorClass_AccountingEntity"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID

		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""

		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp

		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"

		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID + ":" + DerivedAccountingEntity + ":"  + ":" + MajorClass+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
		
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp

		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"

	    DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor

		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha size 4
			restricted
			return ItemGroup.BusinessGroup.FinanceEnterpriseGroup

		MajorClassXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	        MajorClassXMLBOD = template.IONSyncCodeDefination_MajorClass_ST document for this instance

		DerivedAccountingEntity	is a DerivedField
			type is Alpha size 100
			restricted
			if(DerivedCleanDocumentID != "Y" and DerivedMajorClassConfigAE not entered)
				return 	DerivedFinanceEnterpriseGroup + DerivedDelimiter + ItemGroup
			else
			if(DerivedCleanDocumentID != "Y" and DerivedMajorClassConfigAE entered)
				return DerivedMajorClassConfigAE
			else
			if(DerivedCleanDocumentID = "Y")
				return LocalCrossAccountingEntity

	    DerivedCode is a DerivedField
			type is Alpha size 20
			restricted
			if(ClassType.PurchasingClass)
				return "Purchasing Type"
			else
			if(ClassType.InventoryClass)
				return "Inventory Type"
			else
			if(ClassType.PersonalProtectiveEquipment)
				return "PPE Type"

	    DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create and ActionCode.Create)
				return "Add"
			else
			if(action type.Update and ActionCode.Update)
				return "Replace"
			else
			if(action type.Delete and ActionCode.Delete)
				return "Delete"
			else
				return ""

		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if(action type.Delete and ActionCode.Delete)
				return "Deleted"
			else
			if(action type.Create or action type.Update)
				if(Active)
					return "Open"
				else
					return "Closed"
			else
				return ""

	Relations
	
		ActiveItemInInventoryMajorClassRel
			one-to-many relation to Item
			Field Mapping uses Set5
				related.ItemGroup				= ItemGroup
				related.MajorInventoryClass		= MajorClass
				
		ActiveItemInPurchasingMajorClassRel
			one-to-many relation to Item
			Field Mapping uses Set4
				related.ItemGroup				= ItemGroup
				related.MajorPurchasingClass	= MajorClass
				
		ActiveItemInMajorSalesClassClassRel
			one-to-many relation to Item
			Field Mapping uses Set6
				related.ItemGroup				= ItemGroup
				related.MajorSalesClass			= MajorClass

		ItemsInMajorPPEClassRel
			one-to-many relation to Item
			Field Mapping uses ByPPEClass
				related.ItemGroup				= ItemGroup
				related.MajorPPEClass			= MajorClass

		MajorMinorTierQualifierCodeRel
			one-to-many relation to ContractTierQualifierCode
			Field Mapping uses  symbolic key
		  		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
		   		related.Contract			  = LocalContractTierGroup.Contract
		   		related.ContractTier		  = LocalContractTierGroup.ContractTier
		   		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
		   	Instance Selection
		   		where (!related.MinorPurchasingClass = blank
		   		and    related.MajorPurchasingClass = MajorClass)
		   		
		ContractTierQualifierCodeRel
			one-to-many relation to ContractTierQualifierCode
			Field Mapping uses  symbolic key
		  		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
		   		related.Contract			  = LocalContractTierGroup.Contract
		   		related.ContractTier		  = LocalContractTierGroup.ContractTier
		   		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
		   	Instance Selection
		   		where (related.MinorPurchasingClass = LocalMinorClass
		   		and    related.MajorPurchasingClass = MajorClass)
		   		
		ContractImportTierQualifierCodeRel
			one-to-many relation to ContractImportTierQualCode
			Field Mapping uses  symbolic key
		  		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
		   		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
		   		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
		   		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
		   	Instance Selection
		   		where (related.MinorPurchasingClass = LocalMinorClass
		   		and    related.MajorPurchasingClass = MajorClass)
		   		
		MajorMinorTierImportQualifierCodeRel
			one-to-many relation to ContractImportTierQualCode
			Field Mapping uses  symbolic key
		  		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
		   		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
		   		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
		   		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
		   	Instance Selection
		   		where (!related.MinorPurchasingClass = blank
		   		and    related.MajorPurchasingClass = MajorClass)
		   		
		ContractRebateQualifierCodeRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses symbolic key
		  		related.ContractGroup		    = LocalContractRebate.ContractGroup
		   		related.Contract			    = LocalContractRebate.Contract
		   		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
		   		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
		   	Instance Selection
		   		where (related.MinorPurchasingClass = LocalMinorClass
		   		and    related.MajorPurchasingClass = MajorClass)
		   		
		MajorMinorRebateQualifierCodeRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses symbolic key
		  		related.ContractGroup		    = LocalContractRebate.ContractGroup
		   		related.Contract			    = LocalContractRebate.Contract
		   		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
		   		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
		   	Instance Selection
		   		where (!related.MinorPurchasingClass = blank
		   		and    related.MajorPurchasingClass = MajorClass)

		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter

		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker

		FSMBODConfigurationRel
			one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
				related.FSMBODConfiguration.Verb 		= 1
				related.FSMBODConfiguration.Noun 		= "CodeDefinition"
				related.FSMBODConfiguration.Direction 	= 1

		FSMCleanDocIDAccountingEntityRel
			one-to-one relation to AccountingEntity
			Field Mapping uses symbolic key
    			related.FinanceEnterpriseGroup 	= ItemGroup.BusinessGroup.FinanceEnterpriseGroup
    			related.AccountingEntity		= LocalCleanDocIDAccountingentity
				
		FSMCleanDocIDCrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
            	related.FSMBODCrossReference.SourceField 		= "CSFGLC"
            	related.FSMBODCrossReference.DestinationField 	= "DESTAE"

		FSMBODAECrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
				related.FSMBODCrossReference.SourceField 		= "CSFGLC"
				related.FSMBODCrossReference.DestinationField 	= "DESTAE"
            Instance Selection
				where (related.SourceValue	= LocalCleanDocIDAccountingentity )

		BODAccountingEntityRel
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.BusinessGroup = ItemGroup)

	Field Rules

		Active
			initial value is true

		Description
			required
	
	Rule Blocks

		TriggerMajorClassServiceRules
    		trigger "MajorClassService" PA service
				resume on error
				title is "EG:<ItemGroup.BusinessGroup.FinanceEnterpriseGroup>MajorClass:<MajorClass>"
				Criteria
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
					MajorClass
				Variables
					ActionCode
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					include persistent fields from MajorClass
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
    			 
	Actions
		CreateFreightClass is a Create Action
			Field Rules
				ClassType
					force default to "F"

		CreateInventoryClass is a Create Action
			Field Rules
				ClassType
					force default to "I"

		CreatePurchasingClass is a Create Action
			Field Rules
				ClassType
					force default to "P"

		CreateSalesClass is a Create Action
			Field Rules
				ClassType
					force default to "S"

		CreatePersonalProtectiveEquipmentClass is a Create Action
			Field Rules
				ClassType
					force default to "E"

		AttachToTierQualifier is an Instance Action
			Parameters
				ContractGroup
				Contract
				ContractTier
				ContractTierQualifier 
			Parameter Rules
				ContractGroup 
					required
				Contract
					required
				ContractTier
					required
				ContractTierQualifier
					required

			Action Rules
				constraint (Contract.NotClosed)
    				"CannotAdd;ContractIsClosed"	
				LocalContractTierGroup.ContractGroup			= ContractGroup
				LocalContractTierGroup.Contract					= Contract
				LocalContractTierGroup.ContractTier				= ContractTier
				LocalContractTierGroup.ContractTierQualifier	= ContractTierQualifier
				constraint (!MajorMinorTierQualifierCodeRel exists)
					"CannotAdd;TierQualifierCodesExistByMinorClass"
				LocalMinorClass = blank
				if (!ContractTierQualifierCodeRel exists)
					invoke Create ContractTierQualifierCode 
						invoked.Contract				= Contract
						invoked.ContractGroup			= ContractGroup
						invoked.ContractTier			= ContractTier
						invoked.ContractTierQualifier	= ContractTierQualifier
						invoked.MajorPurchasingClass	= MajorClass
						invoked.MinorPurchasingClass	= blank
						invoked.ItemCategory			= 3

		SendMajorClassBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				if(DerivedCleanDocumentID = "Y")
					send ion bod
						bod is MajorClassCleanDocXMLBOD
						bod type is "Sync.CodeDefinition"
						accounting entity is LocalCrossAccountingEntity
						document id is MajorClass
						variation id is	BODVariationID	
				else
					send ion bod
						bod is MajorClassXMLBOD
						bod type is "Sync.CodeDefinition"
						accounting entity is DerivedAccountingEntity
						document id is MajorClass
						variation id is	BODVariationID

		TriggerMajorClassNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
				RestartFlag is Boolean
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "MajorClass"
					invoked.PrmFinanceEnterpriseGroup	= ItemGroup.BusinessGroup.FinanceEnterpriseGroup
					invoked.PrmBusinessGroup			= ItemGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_MajorClass_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(DerivedCleanDocumentID = "Y")
						if(RestartFlag)
							if (BODAccountingEntityRel exist)
								initialize LocalBodAccountingEntity
								initialize LocalCleanDocIDAccountingentity
								initialize MajorClassCleanDocXMLBOD
								initialize LocalCrossAccountingEntity
								LocalBodAccountingEntity = FSMInboundBODTracker.Reference3
								if(DerivedMultipleFEG = "Y")
									LocalCleanDocIDAccountingentity = DerivedFinanceEnterpriseGroup + DerivedDelimiter + LocalBodAccountingEntity
								else
									LocalCleanDocIDAccountingentity = LocalBodAccountingEntity
								initialize LocalCrossAccountingEntity
								if(DerivedTriggerBODBasedOnCrossReference = "Y")
									if (FSMBODAECrossReferenceDetailRel.DestinationValue entered)	
										LocalCrossAccountingEntity = FSMBODAECrossReferenceDetailRel.DestinationValue
								else 
									LocalCrossAccountingEntity = LocalCleanDocIDAccountingentity
								MajorClassCleanDocXMLBOD = template.IONSyncCodeDefination_MajorClass_CleanDoc_ST document for this instance
								if(LocalCrossAccountingEntity entered)
									LocalFSMInboundBODTracker		= FSMInboundBODTracker
									invoke Update FSMInboundBODTrackerRel
										invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
										invoked.BODDocumentID			= MajorClass
										invoked.BODVariationID			= BODVariationID
										invoked.StartDate				= system current timestamp
										invoked.Direction				= 1								
										invoked.BODAccountingEntity		= LocalCrossAccountingEntity
										invoked.Reference1				= ItemGroup
										invoked.Reference2              = "MajorClass"
										invoked.Reference3				= LocalBodAccountingEntity
										invoked.Reference4				= ClassType						
										initialize invoked.Error			
										initialize invoked.ErrorMessage
									LocalBODFormattedID = "infor-nid:" + DerivedTenantID + ":" + LocalCrossAccountingEntity + ":"  + ":" + MajorClass+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
									MajorClassCleanDocXMLBOD = template.IONSyncCodeDefination_MajorClass_CleanDoc_ST document for this instance
									invoke SendMajorClassBODNativeLPL
										resume on error
											Error            							= true
											ErrorMessage     							= error message
									if(Error)
										invoke Update FSMInboundBODTrackerRel
											invoked.Error 								= Error
											invoked.ErrorMessage 						= ErrorMessage
											invoked.Status								= 2
											invoked.CloseDate							= system current timestamp
											invoked.BODXML								= MajorClassCleanDocXMLBOD
											invoked.BODID					            = LocalBODFormattedID
									else
										invoke Update FSMInboundBODTrackerRel
											invoked.Status									= 3
											invoked.CloseDate								= system current timestamp
											invoked.BODXML									= MajorClassCleanDocXMLBOD
											invoked.BODID					                = LocalBODFormattedID
						else
						if(DerivedTriggerBODBasedOnCrossReference = "Y")
							if(FSMCleanDocIDCrossReferenceDetailRel exists)
								for each FSMCleanDocIDCrossReferenceDetailRel
									initialize MajorClassCleanDocXMLBOD
									initialize LocalSourceValue
									LocalSourceValue = each.SourceValue
									if (LocalSourceValue entered)
										for each LocalSourceValue split on "<DerivedDelimiter>"
											initialize LocalCleanDocIDAccountingentity
											LocalCleanDocIDAccountingentity = each
									initialize LocalCrossAccountingEntity
									initialize LocalBODFormattedID
									if(FSMCleanDocIDAccountingEntityRel exists)
										LocalCrossAccountingEntity = each.DestinationValue
										if(LocalCrossAccountingEntity entered)
											if(FSMInboundBODTracker not entered)
												invoke Create FSMInboundBODTracker
													assign result to NewBODTracker
													invoked.Verb 					= 1
													invoked.Noun 					= "CodeDefinition"					
													invoked.BODDocumentID			= MajorClass
													invoked.BODVariationID			= BODVariationID
													invoked.Status					= 1
													invoked.StartDate				= system current timestamp
													invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
													invoked.BODAccountingEntity		= LocalCrossAccountingEntity
													invoked.Direction				= 1								
													invoked.Reference1				= ItemGroup							
													invoked.Reference2				= "MajorClass"
													invoked.Reference3				= LocalBodAccountingEntity
													invoked.Reference4				= ClassType
													initialize invoked.Error			
													initialize invoked.ErrorMessage					
												LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
												LocalBODFormattedID = "infor-nid:" + DerivedTenantID + ":" + LocalCrossAccountingEntity + ":"  + ":" + MajorClass+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
												MajorClassCleanDocXMLBOD = template.IONSyncCodeDefination_MajorClass_CleanDoc_ST document for this instance
											invoke SendMajorClassBODNativeLPL
												resume on error
													Error            							= true
													ErrorMessage     							= error message
											if(Error)
												invoke Update FSMInboundBODTrackerRel
													invoked.Error 								= Error
													invoked.ErrorMessage 						= ErrorMessage
													invoked.Status								= 2
													invoked.CloseDate							= system current timestamp
													invoked.BODXML								= MajorClassCleanDocXMLBOD
													invoked.BODID					            = LocalBODFormattedID
											else
												invoke Update FSMInboundBODTrackerRel
													invoked.Status									= 3
													invoked.CloseDate								= system current timestamp
													invoked.BODXML									= MajorClassCleanDocXMLBOD
													invoked.BODID					                = LocalBODFormattedID	
						else
						if(DerivedTriggerBODBasedOnCrossReference = "N")
							if (BODAccountingEntityRel exist)
								for each BODAccountingEntityRel
									initialize LocalBodAccountingEntity
									initialize LocalCrossAccountingEntity
									initialize MajorClassCleanDocXMLBOD
									initialize LocalBODFormattedID
									LocalBodAccountingEntity = each.AccountingEntity
									if(DerivedMultipleFEG = "Y")
										LocalCrossAccountingEntity = DerivedFinanceEnterpriseGroup + DerivedDelimiter + LocalBodAccountingEntity
									else
										LocalCrossAccountingEntity = LocalBodAccountingEntity	
									if(LocalCrossAccountingEntity entered)
										if(FSMInboundBODTracker not entered)
											invoke Create FSMInboundBODTracker
												assign result to NewBODTracker
												invoked.Verb 					= 1
												invoked.Noun 					= "CodeDefinition"					
												invoked.BODDocumentID			= MajorClass
												invoked.BODVariationID			= BODVariationID
												invoked.Status					= 1
												invoked.StartDate				= system current timestamp
												invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
												invoked.BODAccountingEntity		= LocalCrossAccountingEntity
												invoked.Direction				= 1								
												invoked.Reference1				= ItemGroup							
												invoked.Reference2				= "MajorClass"
												invoked.Reference3				= LocalBodAccountingEntity
												invoked.Reference4				= ClassType								
												initialize invoked.Error			
												initialize invoked.ErrorMessage					
											LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
											LocalBODFormattedID = "infor-nid:" + DerivedTenantID + ":" + LocalCrossAccountingEntity + ":"  + ":" + MajorClass+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
											MajorClassCleanDocXMLBOD = template.IONSyncCodeDefination_MajorClass_CleanDoc_ST document for this instance
										invoke SendMajorClassBODNativeLPL
											resume on error
												Error            							= true
												ErrorMessage     							= error message
										if(Error)
											invoke Update FSMInboundBODTrackerRel
												invoked.Error 								= Error
												invoked.ErrorMessage 						= ErrorMessage
												invoked.Status								= 2
												invoked.CloseDate							= system current timestamp
												invoked.BODXML								= MajorClassCleanDocXMLBOD
												invoked.BODID					            = LocalBODFormattedID
										else
											invoke Update FSMInboundBODTrackerRel
												invoked.Status									= 3
												invoked.CloseDate								= system current timestamp
												invoked.BODXML									= MajorClassCleanDocXMLBOD
												invoked.BODID					                = LocalBODFormattedID
					else
						if(FSMInboundBODTracker not entered)
							invoke Create FSMInboundBODTracker
								assign result to NewBODTracker
								invoked.Verb 					= 1
								invoked.Noun 					= "CodeDefinition"					
								invoked.BODDocumentID			= MajorClass
								invoked.BODVariationID			= BODVariationID
								invoked.Status					= 1
								invoked.StartDate				= system current timestamp
								invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
								invoked.BODAccountingEntity		= DerivedAccountingEntity
								invoked.Direction				= 1								
								invoked.Reference1				= ItemGroup							
								invoked.Reference2				= "MajorClass"
								invoked.Reference4				= ClassType								
								initialize invoked.Error			
								initialize invoked.ErrorMessage					
							LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
						else 
							LocalFSMInboundBODTracker		= FSMInboundBODTracker
							invoke Update FSMInboundBODTrackerRel
								invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
								invoked.BODDocumentID			= MajorClass
								invoked.BODVariationID			= BODVariationID
								invoked.BODID					= DerivedBODFormattedId
								invoked.BODAccountingEntity		= DerivedAccountingEntity
								invoked.Status					= 1
								invoked.StartDate				= system current timestamp
								invoked.Direction				= 1								
								invoked.Reference1				= ItemGroup
								invoked.Reference2				= "MajorClass"
								invoked.Reference4				= ClassType								
								initialize invoked.Error			
								initialize invoked.ErrorMessage
						invoke SendMajorClassBODNativeLPL
							resume on error
		                   		Error            							= true
		                        ErrorMessage     							= error message
		                if(Error)
							invoke Update FSMInboundBODTrackerRel
								invoked.Error 								= Error
								invoked.ErrorMessage 						= ErrorMessage
								invoked.Status								= 2
								invoked.CloseDate							= system current timestamp
								invoked.BODXML								= MajorClassXMLBOD
								invoked.BODID					            = DerivedBODFormattedId
						else
							invoke Update FSMInboundBODTrackerRel
								invoked.Status									= 3
								invoked.CloseDate								= system current timestamp
								invoked.BODXML									= MajorClassXMLBOD
								invoked.BODID					                = DerivedBODFormattedId							

		AttachToImportTierQualifier is an Instance Action
			Parameters
				ContractGroup
				ContractImport
				ContractImportTier
				ContractImportTierQualifier 
			Parameter Rules
				ContractGroup 
					required
				ContractImport
					required
				ContractImportTier
					required
				ContractImportTierQualifier
					required

			Action Rules
				LocalContractImportTierGroup.ContractGroup					= ContractGroup
				LocalContractImportTierGroup.ContractImport					= ContractImport
				LocalContractImportTierGroup.ContractImportTier				= ContractImportTier
				LocalContractImportTierGroup.ContractImportTierQualifier	= ContractImportTierQualifier
				constraint (!MajorMinorTierImportQualifierCodeRel exists)
					"CannotAdd;TierQualifierCodesExistByMinorClass"
				LocalMinorClass = blank
				if (!ContractImportTierQualifierCodeRel exists)
					invoke Create ContractImportTierQualCode 
						invoked.ContractImport 			    = ContractImport
						invoked.ContractGroup		        = ContractGroup
						invoked.ContractImportTier		    = ContractImportTier
						invoked.ContractImportTierQualifier = ContractImportTierQualifier
						invoked.MajorPurchasingClass        = MajorClass
						invoked.MinorPurchasingClass        = blank
						invoked.ItemCategory		        = 3
		
		AttachToRebateQualifier is an Instance Action
			Parameters
				ContractGroup
				Contract
   				ContractRebateType			
   				ContractRebate				
   				ContractRebateQualifier
			Parameter Rules
				ContractGroup 
					required
				Contract
					required
				ContractRebateType
					required
				ContractRebate
					required
				ContractRebateQualifier
					required

			Action Rules
				constraint (Contract.CanAmend)
					"ContractMustBeInDraftOrAddendumOrAmendmentStatusToModifyRebateQualifiers"					
				constraint (ContractRebate.Active)
					"ContractRebateMustBeActiveToAddRebateQualifierCodes"
				constraint (!Contract.ServicePurchaseType)
					"RebateQualifierCodesCannotBeEnteredForServiceContracts"    				
				LocalContractRebate.ContractGroup 		    = ContractGroup
				LocalContractRebate.Contract	            = Contract
				LocalContractRebate.ContractRebateType      = ContractRebateType
				LocalContractRebate.ContractRebateQualifier = ContractRebateQualifier			
				constraint (!MajorMinorRebateQualifierCodeRel exists)
					"CannotAdd;RebateQualifierCodesExistByMinorClass"
				LocalMinorClass = blank
				if (!ContractRebateQualifierCodeRel exists)
					invoke Create ContractRebateQualifierCode 
						invoked.ContractGroup		    = ContractGroup
						invoked.Contract			    = Contract
						invoked.ContractRebateType		= ContractRebateType
						invoked.ContractRebateQualifier = ContractRebateQualifier
						invoked.MajorPurchasingClass    = MajorClass
						invoked.MinorPurchasingClass    = blank
						invoked.ItemCategory		    = 3

		Update is an Update Action

		Delete is a Delete Action
			Entrance Rules
				if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and (ClassType.InventoryClass or ClassType.PurchasingClass or ClassType.PersonalProtectiveEquipment))
					ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerMajorClassServiceRules

					
	Action Exit Rules
		if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger and (ClassType.InventoryClass or ClassType.PurchasingClass or ClassType.PersonalProtectiveEquipment))
			if (!action type.Delete)
				if (action type.Create)
					ActionCode	= ActionCode.Create				
				else
					if (action type.Update)
						ActionCode	= ActionCode.Update
				increment bod id.VariationID
				include TriggerMajorClassServiceRules

