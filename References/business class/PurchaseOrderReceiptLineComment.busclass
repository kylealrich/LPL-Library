PurchaseOrderReceiptLineComment is a BusinessClass
	owned by po
	prefix is PRLct
    sql name is PORecLineComments
	
	Ontology
		part of PurchaseOrderReceiptLine
			relative key is SequenceNumber
			
	Patterns
		implements StaticJava
		implements Archivable
			
	Persistent Fields
    	CommentTitle					is a CommentName
    	CommentType						is AlphaUpper size 1
    		States
                PrintOnDeliveryTicket			value is "D"
                PrintOnInternalDocuments		value is "I"
                PrintOnPurchaseOrder			value is "P"
                PrintOnReceivingDocument		value is "R"
				DisplayOnlyOnInternalDocuments 	value is "S"
                PurchaseOrderLineDisplayOnly	value is "O"
                InvoiceComments					value is "N"
				Acknowledgement					value is "A"
    	CommentText
		Attachment						is an AlternateAttachment
	
	
	Conditions
		IsCommentTypeSelected
			restricted
			when (TransientPrintOnInternalDocuments
			or    TransientPrintOnReceivingDocument
			or    TransientPrintOnDeliveryTicket
			or 	  TransientDisplayOnlyOnInternalDocuments
			or    TransientInvoiceComments)
			
		HasAttachment
			restricted
			when (Attachment entered)
	
		IsNotValidType
			restricted
			when (CommentType.PrintOnPurchaseOrder
			or    CommentType.PurchaseOrderLineDisplayOnly)
		
		DeleteAllowed
			restricted
			when (not IsNotValidType
			and   PurchaseOrderReceiptLine.Status.Entered)
	
	Transient Fields
		TransientPurchaseOrderReceipt is like PurchaseOrderReceipt
			derive value from PurchaseOrderReceipt
		TransientPrintOnInternalDocuments	is Boolean
			default label is "PrintOnInternalDocuments"
		TransientPrintOnReceivingDocument	is Boolean
			default label is "PrintOnReceivingDocument"
		TransientPrintOnDeliveryTicket		is Boolean
			default label is "PrintOnDeliveryTicket"
		TransientDisplayOnlyOnInternalDocuments is Boolean
			default label is "DisplayOnlyOnInternalDocuments"
		TransientInvoiceComments			is Boolean
			default label is "InvoiceComments"								
	
	Field Rules
		SequenceNumber
			autosequence
				minimize contention
			
		CommentType
			initial value is CommentType.PrintOnReceivingDocument
			default to CommentType.PrintOnReceivingDocument
			required
			
			if (old CommentType.PrintOnPurchaseOrder 
				or 	old CommentType.PurchaseOrderLineDisplayOnly
				or 	CommentType.PrintOnPurchaseOrder 
				or 	CommentType.PurchaseOrderLineDisplayOnly)
					cannot be changed
						"UpdateIsNotAvailableForTheSelectedCommentType"			

		CommentTitle
			required

		TransientPrintOnReceivingDocument
			initial value is true

	Derived Fields
		DerivedInitialCommentType	is a DerivedField
			type is like CommentType
			restricted
			if (CommentType entered)
				return CommentType
			else
			if (TransientPrintOnInternalDocuments)
				return CommentType.PrintOnInternalDocuments
			else
			if (TransientPrintOnReceivingDocument)
				return CommentType.PrintOnReceivingDocument
			else
			if (TransientPrintOnDeliveryTicket)
				return CommentType.PrintOnDeliveryTicket
			else
			if (TransientDisplayOnlyOnInternalDocuments)
				return CommentType.DisplayOnlyOnInternalDocuments
			else
			if (TransientInvoiceComments)
				return CommentType.InvoiceComments
				
	Local Fields
		LocalCommentType is like CommentType
		LocalAttributeCtr   is Numeric 2

	Rule Blocks
		CreateComment
			invoke SystemCreate PurchaseOrderReceiptLineComment
				invoked.Company						= Company
				invoked.PurchaseOrderReceipt		= PurchaseOrderReceipt
				invoked.PurchaseOrderReceiptLine	= PurchaseOrderReceiptLine
				invoked.CommentTitle				= CommentTitle
				invoked.CommentType					= LocalCommentType
				invoked.CommentText					= CommentText
				invoked.Attachment.File				= Attachment.File	
				invoked.Attachment.MimeType			= Attachment.MimeType
				invoked.Attachment.Title			= Attachment.Title
	

	Sets
  		ByCommentType
  			Sort Order
  				Company	
  				PurchaseOrderReceipt
  				PurchaseOrderReceiptLine
				CommentType
				SequenceNumber		

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
	
	Actions
		SystemCreate is a Create Action
			restricted
			
		Create is a Create Action
			valid when (PurchaseOrderReceiptLine.Status.Entered)
			Field Rules
				CommentType
					constraint (not IsNotValidType)
						"CreateIsNotAvailableForTheSelectedCommentType"
			Action Rules
				
				if (CommentType not entered)
					constraint (TransientPurchaseOrderReceipt not entered 
					or          IsCommentTypeSelected)
						"CommentTypeIsRequired"
				else
					constraint(not IsCommentTypeSelected)
						"CannotEnterBothCommentTypeAndTransientCommentTypes"			
				
				invoke SystemCreate this instance
					invoked.CommentType		= DerivedInitialCommentType
			
			Exit Rules
				initialize LocalCommentType
				if (TransientPrintOnInternalDocuments
				and not CommentType.PrintOnInternalDocuments)
					LocalCommentType = CommentType.PrintOnInternalDocuments
					include CreateComment

				if (TransientPrintOnReceivingDocument
				and not CommentType.PrintOnReceivingDocument)
					LocalCommentType = CommentType.PrintOnReceivingDocument
					include CreateComment
																																																					
				if (TransientPrintOnDeliveryTicket
				and not CommentType.PrintOnDeliveryTicket)
					LocalCommentType = CommentType.PrintOnDeliveryTicket
					include CreateComment			
				
				if (TransientDisplayOnlyOnInternalDocuments
				and not CommentType.DisplayOnlyOnInternalDocuments)
					LocalCommentType = CommentType.DisplayOnlyOnInternalDocuments
					include CreateComment

				if (TransientInvoiceComments
				and not CommentType.InvoiceComments)
					LocalCommentType = CommentType.InvoiceComments
					include CreateComment

		MobileCreate is a Create Action
			default label is "Create"
			valid when (PurchaseOrderReceiptLine.Status.Entered)
			Field Rules
				CommentType
					constraint (not IsNotValidType)
						"CreateIsNotAvailableForTheSelectedCommentType"
			Action Rules
				
				if (CommentType not entered)
					constraint (TransientPurchaseOrderReceipt not entered 
					or          IsCommentTypeSelected)
						"CommentTypeIsRequired"
				else
					constraint(not IsCommentTypeSelected)
						"CannotEnterBothCommentTypeAndTransientCommentTypes"			
				
				invoke SystemCreate this instance
					invoked.CommentType		= DerivedInitialCommentType
			
			Exit Rules
				initialize LocalCommentType
				if (TransientPrintOnInternalDocuments
				and not CommentType.PrintOnInternalDocuments)
					LocalCommentType = CommentType.PrintOnInternalDocuments
					include CreateComment

				if (TransientPrintOnReceivingDocument
				and not CommentType.PrintOnReceivingDocument)
					LocalCommentType = CommentType.PrintOnReceivingDocument
					include CreateComment
																																																					
				if (TransientPrintOnDeliveryTicket
				and not CommentType.PrintOnDeliveryTicket)
					LocalCommentType = CommentType.PrintOnDeliveryTicket
					include CreateComment

				if (TransientInvoiceComments
				and not CommentType.InvoiceComments)
					LocalCommentType = CommentType.InvoiceComments
					include CreateComment
								
		Update is an Update Action
			valid when (PurchaseOrderReceiptLine.Status.Entered)

		Delete is a Delete Action
			valid when (DeleteAllowed)
			
		Purge is a Purge Action
			restricted
			Action Rules
				Attachment.LocalPurgeTriggered = true

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
				
					
				
