LeaseInvoiceDistribution is a BusinessClass
    owned by lm
    prefix is DIS
    classic name is LMDISTRIB

    Ontology
        symbolic key is LeaseInvoiceDistribution
            classic set name is DISNEWPRIMARYINDEX
            classic name is DIST-SEQ-NBR
            classic name for Vendor is PMT-VENDOR
            classic name for LeaseInvoice.PaymentNumber is PMT-NBR
            classic name for LeaseInvoice.CancelSequence is CANCEL-SEQ

    Patterns
        implements StaticJava
        disable AuditIndex
		implements Archivable

    Persistent Fields

        Invoice
        Suffix
        ExecutoryCostCode
            classic name is EXEC-CODE
        Description
        DistributionAmount             is an InternationalAmount
            classic name is ORIG-TRAN-AMT
        PercentTransactionAmount       is an InternationalAmount
            classic name is PCT-TRAN-AMT
        PrincipalAmount                is an InternationalAmount
            classic name is PRINCIPAL-AMT
        InterestAmount                 is an InternationalAmount
            classic name is INTEREST-AMT
        BaseDistributionAmount         is an InternationalAmount
            classic name is ORIG-BASE-AMT
        PercentBaseAmount              is an InternationalAmount
            classic name is PCT-BASE-AMT
        BasePrincipalAmount            is an InternationalAmount
            classic name is BASE-PRINC-AMT
        BaseInterestAmount             is an InternationalAmount
            classic name is BASE-INT-AMT
        ToBaseCurrency                 is a Currency
            classic name is TO-BASE-CURR
        ToBaseCurrencyNumberOfDecimals is a CurrencyNumberOfDecimals
            classic name is TO-BASE-ND
        ToBaseRate                     is a CurrencyRate
        ToBaseAmount                   is an InternationalAmount
            classic name is TO-BASE-AMT
        PrincipalGltransObjID          is an ObjId
            classic name is GLT-PRN-OBJ-ID
        InterestCalculation		       is Boolean  
            classic name is INT-CALC-FL
        InterestCalculationDate        is Date
            classic name is INT-CALC-DATE
        InterestCancel		           is Boolean   
            classic name is INT-CANCEL-FL
        Status                         is Numeric size 1
            States
                Unposted    	value is 0
				NoAPUnposted  	value is 7
					default label is "NoPayablesRecordUnposted"
                LTOUnposted 	value is 8
                	default label is "OperatingLeaseUnposted"
                Posted      	value is 9  
        LeaseAllocationCode
            classic name is ALLOC-CD
        LeaseAllocationCodeDetail
        TaxPoint                       is AlphaUpper size 1
        TaxIndicator
        TaxUsageCode
            classic name is TAX-USAGE-CD
        TaxType                        is AlphaUpper size 1
        TaxCode
        TaxRate                        is a ChrgRate
        TransactionTaxableAmount       is an InternationalAmount
            classic name is TRAN-TAXABLE
        TaxLanded                  	   is Boolean     
            classic name is TAX-LANDED-FL
        InvoiceDistributionAccount     is a TransactionCodeBlock            
            classic name for InvoiceDistributionAccount.ToAccountingEntity is PT-COMPANY
            classic name for InvoiceDistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for InvoiceDistributionAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for InvoiceDistributionAccount.Project is ACTIVITY
		LeaseFund						is like LeaseFund
	
    
    Local Fields
        LocalEnterpriseGroup           	is a EnterpriseGroup
    	LocalCurrencyAmount            	is a CurrencyExchange 
    	LocalAmount					   	is an InternationalAmount
    	LocalBaseAmount					is an InternationalAmount
    	LocalGLTransDetail             	is a GLTransactionDetail view
    	LocalJournalizeGroup		   	is a JournalizeGroup
		LocalFromCurrency				is a FromCurrency
		LocalCurrencyTransactionAmount	is a CurrencyAmount
		LocalExchangeDate				is an ExchangeDate
		LocalPostingDate                is a  PostingDate
		

	Transient Fields	 

		BypassProjectDateEdit
			derive value from DerivedBypassProjectDateEdit		
		BypassDistribAmountEdit 	is Boolean	
		TransientZeroPaymentDistrib is Boolean
		TransientJournalizeGroup 	is like JournalizeGroup
    Conditions

        IsNotCancelled
        	restricted
            when (LeaseInvoice.CancelSequence not entered)

    Relations
                
        LeasePaymentRel
        	one-to-one relation to LeasePayment
        	Field Mapping uses symbolic key
                related.Company                     = Company
                related.Lease                       = Lease
                related.Vendor                      = Vendor
		
		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 
				related.OriginatingTransaction = reference to this instance 
		
		GLTransDetailByJournalizeGroupRel
			one-to-many relation to GLTransactionDetail
				Field Mapping uses ByJournalizeGroup
					related.FinanceEnterpriseGroup				= Company.FinanceEnterpriseGroup
					related.JournalizeGroup						= LocalJournalizeGroup

        LeasePaymentsRel
            one-to-one relation to LeasePayment
            Field Mapping uses symbolic key
            	related.Company		= Company
                related.Lease 		= Lease
                related.Vendor   	= Vendor

    Sets

        Set3
        	indexed
        	Sort Order
        		Company         
				Status          
				Lease           
				Vendor      
				LeaseInvoice.PaymentNumber         
				LeaseInvoice.CancelSequence      
				LeaseInvoiceDistribution
				
        Set2
            indexed
            Instance Selection
                where (IsNotCancelled)
            Sort Order
                Company
                Lease
                Vendor
                LeaseInvoice.PaymentNumber
                InvoiceDistributionAccount.ToAccountingEntity
                InvoiceDistributionAccount.AccountingUnit
                LeaseInvoiceDistribution

        Set1
            Sort Order
                Company
                Vendor
                Lease
                LeaseInvoice
                LeaseInvoiceDistribution


    Derived Fields


		DerivedBypassProjectDateEdit is a DerivedField  
			type is Boolean
			restricted
			if (!LeaseInvoice.PayablesInvoiceRel.MatchProcessType.Expense
			or  LeaseInvoice.PayablesInvoiceRel.IsChargebackSubType
			or  LeaseInvoice.PayablesInvoiceRel.CanceledInvoice)
				return true
			else
				return false

		DerivedSkipInterestCalculation is a DerivedField

			type is Boolean
			restricted
			if (Lease.ShortTermLiabilityAccounting)
				if (InvoiceDistributionAccount.GeneralLedgerChartAccount = Lease.LongTermLiabilityAccount.GeneralLedgerChartAccount)
					return true
			else
				if (InterestCalculation and InvoiceDistributionAccount.GeneralLedgerChartAccount = Lease.InterestAccount.GeneralLedgerChartAccount)
					return true 
				else
					if (InterestCalculation and InvoiceDistributionAccount.GeneralLedgerChartAccount = LeasePaymentsRel.LeaseInvoiceAccrualAccount.GeneralLedgerChartAccount)
						return true 
					else				
						return false

    Field Rules
		
		InvoiceDistributionAccount
			required
			LocalFromCurrency = LeaseInvoice.TransactionCurrency

        DistributionAmount
        	if(!BypassDistribAmountEdit)
	            required
	
				if (LeasePaymentRel.StraightLine
				and ExecutoryCostCode not entered)
					cannot be changed
	            		"StraightLineIsSet;PaymentAmountCannotBeChanged"
	            		
	    		
	    		if (LeaseInvoice.BaseCurrency entered
	    		and LeaseInvoice.BaseCurrency != LeaseInvoice.TransactionCurrency)
		            initialize LocalCurrencyAmount
		            if (LeaseInvoice.OriginalConversionRate entered)
		            	LocalCurrencyAmount.EnteredCurrencyRate         = LeaseInvoice.OriginalConversionRate
		            LocalEnterpriseGroup         		  				= Company.FinanceEnterpriseGroup
		            LocalFromCurrency									= LeaseInvoice.TransactionCurrency		
		            LocalCurrencyAmount.ToCurrency      				= LeaseInvoice.BaseCurrency
		            LocalCurrencyTransactionAmount		 				= DistributionAmount
		            BaseDistributionAmount								= LocalCurrencyAmount.OutputCurrencyAmount
				else
					BaseDistributionAmount								= DistributionAmount
				
				if (LocalExchangeDate not entered)
					LocalExchangeDate = LeaseInvoice.InvoiceDate
	    			
	    		if (DistributionAmount changed
	    		and DistributionAmount != old DistributionAmount)
	    			LocalAmount         = DistributionAmount   - old DistributionAmount
	    			LocalBaseAmount		= BaseDistributionAmount - old BaseDistributionAmount
	    			invoke UpdateTransactionAndBaseAmount LeaseInvoice
	    				invoked.PrmTransactionInvoiceAmount  = LocalAmount
	    				invoked.PrmBaseInvoiceAmount         = LocalBaseAmount
	    				if (TaxRate entered)
	    					invoked.PrmTransactionTaxAmount  = LocalAmount
	    					invoked.PrmBaseTaxAmount         = LocalAmount	        	
	    			
	    			BaseDistributionAmount = DistributionAmount
    					
			
		TaxCode
			if (TaxIndicator not entered)
				cannot be changed
					"CannotChangeTaxCode;DeleteAndRe-add"                     
			
			if (TaxIndicator.Taxable)
				required
					"TaxCodeRequiredWith_\TaxableLineFlag_=\T"               
		
		TaxUsageCode
			if (TaxCode not entered)
				cannot be entered
					"Tax_\Usage_\CodeNotValidIfNo_\Tax_\CodeEntered"		
		
		TransactionTaxableAmount
			if (TaxIndicator.Taxable
			and DistributionAmount entered)
				default to DistributionAmount

			if (TaxCode entered
			and !TaxIndicator.Taxable)
				required
					"TaxableAmountRequiredWithTaxCode"                        

			if (TaxCode entered
			and DistributionAmount not entered
			and TaxIndicator.Taxable)
				required
					"TaxableAmountRequiredWithTaxCode"    
		
		TaxRate
			if (TaxCode.TaxType.CalculatedRate)
				TaxRate = DistributionAmount / TransactionTaxableAmount   
				                 						
	Actions
		Create 			is a Create Action
			restricted
			
		CreateFromBatch is a Create Action
			restricted	
			Parameters
				PrmPostingDate			is an ExchangeDate
			Entrance Rules
				LocalExchangeDate = PrmPostingDate
				LocalPostingDate  = PrmPostingDate
			Action Rules 
				invoke Create this instance
				if(TransientZeroPaymentDistrib)
					invoke Unreleased.Create GLTransactionDetail
						assign result to LocalGLTransDetail
						invoked.OriginatingTransaction 	= reference to this instance
						invoked.FinanceEnterpriseGroup	= LeaseInvoice.Company.FinanceEnterpriseGroup
						invoked.System					= "LM"
						invoked.Reference				= "ZeroPaymentDistributions"
						invoked.AccountingEntity		= LeaseInvoice.Company.AccountingEntity
						invoked.TransactionAmount		= DistributionAmount
						invoked.CurrencyCode			= LeaseInvoice.TransactionCurrency					 
						invoked.TransactionDate			= LocalExchangeDate
						invoked.PostingDate				= LocalPostingDate
						invoked.FinanceCodeBlock		= InvoiceDistributionAccount
			        	invoked.GeneralLedgerEvent 		= "LI"
						invoked.AutoReverse 			= false
						invoked.JournalizeGroup         = TransientJournalizeGroup		
						
					invoke Unreleased.Release LocalGLTransDetail.GLTransactionDetail	

		CreatePayablesInvoiceDistribution is an Instance Action
			restricted
			Parameters
				PrmPayablesInvoice		is a PayablesInvoice
				PrmTaxIndicator			is a TaxIndicator

			Action Rules
				invoke Create PayablesInvoiceDistribution
					fill in fields from this instance
					invoked.Company								= Company
					invoked.PayablesInvoice						= PrmPayablesInvoice
					invoked.GLFinanceCodeBlock					= InvoiceDistributionAccount

					invoked.GLTransactionAmount     			= DistributionAmount
					invoked.DistributionAmount.CurrencyAmount 	= DistributionAmount
					invoked.DistributionAmount.ExchangeDate   	= PrmPayablesInvoice.InvoiceDate
					invoked.UnitAmount 							= 0
					if (LeaseInvoice.VoucherNumber entered)
						invoked.DistributionReference  			= LeaseInvoice.VoucherNumber
					else
						invoked.DistributionReference  			= Invoice
					invoked.TaxableAmount 						= TransactionTaxableAmount
					if (PrmTaxIndicator.Taxable)
						invoked.TaxIndicator 					= 2
									
		Update			is an Update Action
		
		Delete 			is a Delete Action

		Purge is a Purge Action
			restricted
			Entrance Rules
				if (GLTransactionDetailRel exist) 
					invoke PurgeValidSubLedgerTrx GLTransactionDetailRel

		CreateAndReleaseGlTransDetailFromBatch is an Instance Action
			restricted
			Parameters
			    PrmReference            is a Reference
			    PrmCodeBlock            is a FinanceCodeBlock
				PrmTransactionDate		is an ExchangeDate
				PrmPostingDate			is an ExchangeDate
				PrmTransactionAmount	is a CurrencyAmount
				PrmCurrency				is a FromCurrency
				PrmJournalizeGroup      is a JournalizeGroup
				PrmGeneralLedgerEvent	is a GeneralLedgerEvent

			Parameter Rules
				PrmGeneralLedgerEvent
					if (PrmReference = "LeaseInterestCalc") 
						PrmGeneralLedgerEvent	= "LI"
					else  
						PrmGeneralLedgerEvent	= "LA"
					
					if (PrmTransactionDate not entered)
						PrmTransactionDate = LeaseInvoice.DistributionDate
					if (PrmPostingDate not entered)
						PrmPostingDate = LeaseInvoice.DistributionDate

			Action Rules
				invoke Unreleased.Create GLTransactionDetail
					assign result to LocalGLTransDetail
					invoked.OriginatingTransaction 	= reference to this instance
					invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
					invoked.System					= "LM"
					invoked.Reference				= PrmReference
					invoked.AccountingEntity		= Company.AccountingEntity
					invoked.TransactionAmount		= PrmTransactionAmount
					invoked.CurrencyCode			= PrmCurrency					 
					invoked.TransactionDate			= PrmTransactionDate
					invoked.PostingDate				= PrmPostingDate
					invoked.FinanceCodeBlock		= PrmCodeBlock

					invoked.GeneralLedgerEvent 		= PrmGeneralLedgerEvent
					invoked.AutoReverse 			= false
					invoked.JournalizeGroup         = PrmJournalizeGroup		
					
				invoke Unreleased.Release LocalGLTransDetail.GLTransactionDetail
				
		JournalizeDistributionsForBatch is an Instance Action 
			restricted
			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmPayablesCompany	is a PayablesCompany
					default label is "PurchasingCompany"
				PrmDescription			is a Description
				PrmJournalizeGroup       is like JournalizeGroup
			Action Rules
				LocalJournalizeGroup	= PrmJournalizeGroup
				invoke JournalizeDistributions LeaseInvoiceDistribution
					invoked.PrmEnterpriseGroup	 = PrmEnterpriseGroup
					invoked.PrmPayablesCompany   = PrmPayablesCompany
					invoked.PrmDescription		 = PrmDescription
					invoked.PrmJournalizeGroup	 = PrmJournalizeGroup	
			

		JournalizeDistributions is a Set Action		

			restricted
			completion message is "<CompletionMessage>"
			run in foreground	 
			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmPayablesCompany      is a PayablesCompany
				PrmDescription			is a Description
				PrmJournalizeGroup      is like JournalizeGroup
			Parameter Rules
				PrmEnterpriseGroup
					initial value is Company.FinanceEnterpriseGroup
					required
				PrmPayablesCompany 	 
					required
				PrmDescription
					required
			Local Fields
				CompletionMessage		is Alpha 150
				RecordCount				is Numeric 10
				MyJournalizeGroup		is like JournalizeGroup

			Instance Selection
				where (Company				                             = PrmPayablesCompany
				and    GLTransDetailByJournalizeGroupRel.JournalizeGroup = PrmJournalizeGroup)
				
			Sort Order
				Company

			Action Rules								
				Empty Set Rules
					CompletionMessage = "LeaseTransactionPostingCompleteForJournalizeGroup...:NoRecordsFoundToJournalize<MyJournalizeGroup>"
				
				Set Rules
					Entrance Rules
						MyJournalizeGroup = PrmJournalizeGroup
					Exit Rules
						CompletionMessage = "LeaseTransactionPostingCompleteForJournalizeGroup...:<MyJournalizeGroup>;<RecordCount>...RecordsProcessed"
							
				Instance Rules
					MyJournalizeGroup = PrmJournalizeGroup
					invoke InitiateJournalizeForRunGroup PrmEnterpriseGroup in background
						invoked.PrmJournalizeGroup				= MyJournalizeGroup
						invoked.PrmJournalizeGroupDescription	= PrmDescription
						if (Company.PostingOption.Detail)
							invoked.PrmInterfaceInDetail		= true
						else
							invoked.PrmInterfaceInDetail 		= false
						
						CompletionMessage = "LeaseTransactionPostingCompleteForJournalizeGroup...:<MyJournalizeGroup>"
