InvoiceRoutingCodeResource is a BusinessClass
    owned by apautomation
    prefix is APRCR

    Ontology
    	symbolic key is InvoiceRoutingCodeResource

	Patterns
		implements Resequence on ApprovalLevel
			new sequence field is NewApprovalLevel
			set is ByApprovalLevel

    Persistent Fields
    	ApprovalLevel						is a DisplayOrder
		Approver							is a FinanceResource
		MaxApprovalAmount					is a CurrencyAmount
			default label is "MaximumApprovalAmount"
		EscalationDays						is Numeric 6
		EscalateTo							is Numeric 1
			States
				NextApprovalLevel		value is 1
				SpecificApprovalLevel	value is 2
		ApprovalTeam						is a FinanceTeam
		InvoiceApprovalAuthCode
			default label is "InvoiceApprovalRole"
		OverrideNextApprovalLevel			is Boolean
		NextApprovalLevelAfterApprove		is a InvoiceRoutingCodeResource
		NextApprovalLevelAfterEscalation	is a InvoiceRoutingCodeResource
			default label is "EscalationApprovalLevel"
		AmountDefinedAtMemberLevel			is Boolean		

	Transient Fields
		NewApprovalLevel	is a DisplayOrder

	Derived Fields
		DerivedDescription is a DerivedField
			type is Alpha 230
			if (Approver entered)
				return Approver.PreferredSimplePresentationName
			else
			if (ApprovalTeam entered)
				return ApprovalTeam.Description
			else
			if (InvoiceApprovalAuthCode entered)
				return InvoiceApprovalAuthCode
			else
				return ""

		DerivedApprovalLevelPlusOne is a DerivedField
			type is Numeric 8
			return ApprovalLevel + 1

		DerivedNextActiveEscalatedApprovalLevel is a DerivedField		
			type is Alpha 3
			restricted
			default label is "NextApprovalLevel"
			if (NextApprovalLevelAfterEscalation.ActiveResource
			or  NextApprovalLevelAfterEscalation.ActiveResourceRole)
				return NextApprovalLevelAfterEscalation.ApprovalLevel
			return ApprovalLevel

		DerivedNextActiveApprovalLevel is a DerivedField		
			type is Alpha 3
			restricted
			default label is "NextApprovalLevel"
			if (NextApprovalLevelAfterApprove.ActiveResource
			or  NextApprovalLevelAfterApprove.ActiveResourceRole)
				return NextApprovalLevelAfterApprove.ApprovalLevel
			return ApprovalLevel

		DerivedNextApprovalLevel is a DerivedField
			type is Alpha 3
			default label is "NextApprovalLevel"
			if (OverrideNextApprovalLevel)
				return NextApprovalLevelAfterApprove.ApprovalLevel
			else
			if (LastApprovalLevel)
				return "N/A"
			else
				return DerivedApprovalLevelPlusOne

		DerivedNextApprovalLevelApprover is a DerivedField
			type is Alpha size 230
			return first NextApprovalLevelRel.Approver.PreferredSimplePresentationName

		DerivedNextApprovalLevelTeam is a DerivedField
			type is Alpha 100
			return first NextApprovalLevelRel.ApprovalTeam.Description

		InvalidNextLevelEscalationMessage is a MessageField

			"NextLevelDoesNotExist.EscalationValidWhenUsingApprovalCodeGroup"		

		InvalidSpecificLevelEscalationMessage is a MessageField
			"Invalid:EscalatingToSpecificLevelWhereTheLevelIsNotGreaterThanTheCurrentLevel"

		DerivedFirstApproverName is a DerivedField
			type is Alpha 230
			if (Approver entered)
				return Approver.PreferredSimplePresentationName
			else
			if (ApprovalTeam entered)
				return ApprovalTeam.DerivedFirstTeamMemberName
			else
			if (InvoiceApprovalAuthCode entered)
				return first FinanceResourceInvApprovalRel.FinanceResource.PreferredSimplePresentationName
			else
				return ""

		DerivedMaxApprovalAmount is a DerivedField		
			type is like CurrencyAmount
			if (InvoiceRoutingCode.UsesTeamMemberAssignment
			and AmountDefinedAtMemberLevel)
				if (PayablesInvoice.ResponsiblePerson entered)		
					return FinanceTeamMembersCurrentApproverRel.MaxApprovalAmount
				else
					return FinanceTeamMembersCurrentApproverTransientRel.MaxApprovalAmount		
			return MaxApprovalAmount

		DerivedMaxApprovalAmountForApprovalTeam is a DerivedField     
			type is like CurrencyAmount
			return max ApprovalTeam.FinanceTeamMemberRel.MaxApprovalAmount
	Context Fields
		PayablesInvoice
		PayablesCompany

	Conditions
		NextApprovalLevelAfterApproveApproverEntered
			restricted
			when (NextApprovalLevelAfterApprove.Approver entered)
		NextApprovalLevelAfterApproveApprovalTeamEntered
			restricted
			when (NextApprovalLevelAfterApprove.ApprovalTeam entered)
		NextApprovalLevelAfterApproveApprovalRoleEntered
			restricted
			when (NextApprovalLevelAfterApprove.InvoiceApprovalAuthCode entered)
		NextApprovalLevelAfterEscalationApproverEntered
			restricted
			when (NextApprovalLevelAfterEscalation.Approver entered)
		NextApprovalLevelAfterEscalationApprovalTeamEntered
			restricted
			when (NextApprovalLevelAfterEscalation.ApprovalTeam entered)
		NextApprovalLevelAfterEscalationApprovalRoleEntered
			restricted
			when (NextApprovalLevelAfterEscalation.InvoiceApprovalAuthCode entered)
		ShowNextApprovalLevelAfterEscalation
			restricted
			when (EscalationDays > 0
			and	  EscalateTo.SpecificApprovalLevel)
		LastApprovalLevel
			restricted
			when (!ApprovalLevelRel exists
			or	  ApprovalLevel = last ApprovalLevelRel.ApprovalLevel)
		IsTeam
			restricted
			when (ApprovalTeam entered)
		CurrentInvoiceApprovalLevel
			restricted
			when (CurrentInvoiceApprovalLevelRel exists)
		ApproverEntered
			restricted
			when (Approver entered
			or	  ApprovalTeam entered
			or	  (InvoiceApprovalAuthCode entered
			and	  FinanceResourceInvApprovalRel exists))
		InvalidEscalationDays
			restricted
			when (LastApprovalLevel
			and	  EscalationDays > 0
			and	  EscalateTo.NextApprovalLevel)
		ActiveResource
			restricted
			when ((Approver entered
			and	   Approver.Active)
			or	  (ApprovalTeam entered
			and	   ApprovalTeam.ActiveTeamMemberExists))
		ActiveResourceRole		
			restricted
			when (InvoiceApprovalAuthCode entered
			and   InvoiceApprovalAuthCode.Active		
			and	  ActiveFinanceResourceInvApprovalRel exists)
		InactiveResource
			restricted
			when (Approver entered
			and	  !Approver.Active)
		TeamHasNoMembers
			restricted
			when (IsTeam
			and   ApprovalTeam.HasNoMembers)
		TeamHasNoActiveMembers
			restricted
			when (IsTeam
			and	  ApprovalTeam.HasMembers
			and   !ApprovalTeam.ActiveTeamMemberExists)
		NextApprovalLevelApproverEntered
			restricted
			when (first NextApprovalLevelRel.Approver entered)
		NextApprovalLevelApprovalTeamEntered
			restricted
			when (first NextApprovalLevelRel.ApprovalTeam entered)
		NextApprovalLevelExists
			restricted
			when (first NextApprovalLevelRel exists)
		EscalateToNextLevel
			restricted
			when (EscalationDays > 0
			and	  EscalateTo.NextApprovalLevel)
		InvalidNextLevelEscalation
			restricted
			when (EscalationDays > 0
			and	  EscalateTo.NextApprovalLevel
			and   !NextApprovalLevelExists)
		InvalidSpecificLevelEscalation
			restricted
			when (EscalationDays > 0
			and	  EscalateTo.SpecificApprovalLevel
			and   NextApprovalLevelAfterEscalation.ApprovalLevel <= ApprovalLevel)
		InvalidEscalation
			restricted
			when ((EscalationDays > 0)
			and	  (InvalidNextLevelEscalation
			or    InvalidSpecificLevelEscalation))
		MultipleApprovers
			restricted
			when (instance count of FinanceResourceInvApprovalRel > 1)
		SpecificApproverEntered
			restricted
			when (Approver entered)
		RoleApproversExist
			restricted
			when (FinanceResourceInvApprovalRel exists)
		IndividualOrRoleEntered		
			restricted
			when (Approver entered
			or    InvoiceApprovalAuthCode entered)
		RoutingTeamMemberMissingMaxAmount		
			restricted
			when (AmountDefinedAtMemberLevel
			and   ApprovalTeam.FinanceTeamMemberRel.MaxApprovalAmount !entered)
		RoutingFinanceTeamMatch		
			restricted
			when (ApprovalTeam = PayablesInvoice.RoutingFinanceTeam)

	Relations
		DuplicateApproverRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses symbolic key 
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.UniqueID != UniqueID
				and	   related.Approver = Approver)

		DuplicateApprovalTeamRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses symbolic key 
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.UniqueID != UniqueID
				and	   related.ApprovalTeam = ApprovalTeam)

		DuplicateMaxAmountRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses symbolic key 
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.UniqueID != UniqueID
				and	   related.MaxApprovalAmount = MaxApprovalAmount
				and	   MaxApprovalAmount > 0)

		ThisApprovalLevelRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses symbolic key 
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.UniqueID = UniqueID)

		ApprovalLevelRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel 
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode

		CurrentInvoiceApprovalLevelRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel 
				related.VendorGroup			= PayablesInvoice.Company.VendorGroup
				related.InvoiceRoutingCode	= PayablesInvoice.RoutingCode
			Instance Selection
				where (related.ApprovalLevel = PayablesInvoice.RoutingApprovalLevel)

		FinanceResourceInvApprovalRel
			one-to-many relation to FinanceResourceInvApproval
			Field Mapping uses ByRoleAndCategory
				related.InvoiceApprovalAuthCode	= InvoiceApprovalAuthCode
				related.InvoiceRoutingCategory	= PayablesInvoice.RoutingCategory
				related.VendorGroup				= VendorGroup

		ActiveFinanceResourceInvApprovalRel		
			one-to-many relation to FinanceResourceInvApproval
			Field Mapping uses ByRoleAndCategory
				related.InvoiceApprovalAuthCode	= InvoiceApprovalAuthCode
				related.InvoiceRoutingCategory	= PayablesInvoice.RoutingCategory
				related.VendorGroup				= VendorGroup
			Instance Selection
				where (related.ActiveResource)

		NextApprovalLevelRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
				related.ApprovalLevel		= DerivedApprovalLevelPlusOne

		FinanceTeamMembersRel
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ApprovalTeam.FinanceEnterpriseGroup
				related.FinanceTeam				= ApprovalTeam

		FinanceTeamMembersCurrentApproverRel		
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ApprovalTeam.FinanceEnterpriseGroup
				related.FinanceTeam				= PayablesInvoice.ResponsibleTeam
			Instance Selection
				where (related.FinanceTeamMember.TeamMember		= PayablesInvoice.ResponsiblePerson)

		FinanceTeamMembersCurrentApproverTransientRel		
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ApprovalTeam.FinanceEnterpriseGroup
				related.FinanceTeam				= PayablesInvoice.ResponsibleTeam
			Instance Selection
				where (related.FinanceTeamMember.TeamMember	 = PayablesInvoice.AgentRel.BusinessObjectRef(Employee).Employee)

	Field Rules
		Approver
        	constraint (!DuplicateApproverRel exists)
    			"InvoiceApprover<Approver.PreferredSimplePresentationName>AlreadyDefinedForThisRoutingCode"
			constraint (!ApprovalTeam entered)
				"CanEnterEitherApprover,ApprovalTeamOrApprovalRole"
			if (ApprovalTeam not entered
			and InvoiceApprovalAuthCode not entered)
				required
					"MustEnterEitherApprover,ApprovalTeamOrApprovalRole"

		ApprovalTeam
        	constraint (!DuplicateApprovalTeamRel exists)
    			"InvoiceApprovalTeam<ApprovalTeam.Description>AlreadyDefinedForThisRoutingCode"

		MaxApprovalAmount
			if (!InvoiceRoutingCode.UsingFirstAndLastApprovers)
				if (InvoiceRoutingCode.UsesTeamMemberAssignment)		
					if (!AmountDefinedAtMemberLevel)
						required
							"MustEnter_\Maximum_\Approval_\AmountOrFlag_\Amount_\Defined_\At_\Member_\Level"
				else
					required

		EscalationDays
			if (EscalationDays entered)
				constraint (EscalationDays >= 0)
					"CannotEnterNegativeEscalationDays"

		EscalateTo
			initial value is 1
			default to 1	

		ApprovalLevel
			initial value is last ApprovalLevelRel.ApprovalLevel + 1

		OverrideNextApprovalLevel
			if (!OverrideNextApprovalLevel
			and NextApprovalLevelAfterApprove entered)
				initialize NextApprovalLevelAfterApprove

		NextApprovalLevelAfterApprove
			if (OverrideNextApprovalLevel)
				required
					"SkipToLevelIsRequiredWhenSkipNextApprovalLevelIsTrue"
			if (NextApprovalLevelAfterApprove entered)
				constraint (NextApprovalLevelAfterApprove.ApprovalLevel > ApprovalLevel)
					"NextApprovalLevel<NextApprovalLevelAfterApprove.ApprovalLevel>MustBeGreaterThanCurrentApprovalLevel<ApprovalLevel>"

		NextApprovalLevelAfterEscalation
			if (EscalateTo.SpecificApprovalLevel)
				required
					"EscalationApprovalLevelIsRequiredWhenEscalatingToASpecificApprovalLevel"
			if (NextApprovalLevelAfterEscalation entered)
				constraint (NextApprovalLevelAfterEscalation.ApprovalLevel > ApprovalLevel)
					"EscalationApprovalLevel<NextApprovalLevelAfterEscalation.ApprovalLevel>MustBeGreaterThanCurrentApprovalLevel<ApprovalLevel>"

		AmountDefinedAtMemberLevel		
			if (InvoiceRoutingCode.UsesTeamMemberAssignment)
				if (!AmountDefinedAtMemberLevel
				and  MaxApprovalAmount !entered)
					required
						"MustEnter_\Maximum_\Approval_\AmountOrFlag_\Amount_\Defined_\At_\Member_\Level"
			constraint (MaxApprovalAmount !entered)
				"CannotEnter_\Maximum_\Approval_\AmountAndFlag_\Amount_\Defined_\At_\Member_\Level"
			constraint (ApprovalTeam.FinanceTeamMemberRel.MaxApprovalAmount entered)
				"MustEnterMaximumApprovalAmountOnAllTeamMembers"

	Attach Rules

	Sets
		ByAmount
			Sort Order
 				VendorGroup
 				InvoiceRoutingCode
 				MaxApprovalAmount
 				InvoiceRoutingCodeResource

		ByApprover
			Sort Order
 				Approver
 				VendorGroup
 				InvoiceRoutingCode
 				InvoiceRoutingCodeResource

		ByApprovalLevel
			duplicates
			Sort Order
 				VendorGroup
 				InvoiceRoutingCode
 				ApprovalLevel
			
	Actions
		Create is a Create Action
		
		Update is an Update Action

		Delete is a Delete Action
			Action Rules
				if (first ApprovalLevelRel.ApprovalLevel = ApprovalLevel)
					invoke UpdateApprovalLevel last ApprovalLevelRel
						invoked.NewApprovalLevel = last ApprovalLevelRel.ApprovalLevel - 1
				else
					invoke UpdateApprovalLevel first ApprovalLevelRel
						invoked.NewApprovalLevel = 1

		UpdateApprovalLevel is an Update Action
			restricted
