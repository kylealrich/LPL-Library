FranchiseContractComment is a BusinessClass
	owned by fr
	prefix is FRCC	
	
	Ontology
		symbolic key is FranchiseContractComment
		
	Patterns
		implements StaticJava

	Persistent Fields
		Name				is a CommentName
			default label is "Title"
		Type				is AlphaUpper 1
			States
		Comment				is a NewCommentText
		Attachment			is an AlternateAttachment

	Field Rules
		Name
			required

	Derived Fields

		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforFranchiseContractComment"

		ContextMessageText is a MessageField
			restricted
			"FranchiseContractComment<FranchiseContractComment>OfFranchiseContract<Company>ForCustomer<Customer>"

	Local Fields
		LocalAttributeCtr   is Numeric 2
		IDMAttributes 
		IDMItem

	Sets
		ByFranchiseContract
			Sort Order
				FranchiseContract
				FranchiseContractComment		
					
	Conditions
	
		HasAttachment
			restricted
			when (Attachment entered)

	Create Rules
		include IDM.CreateRules 
			replace AttachmentField with Attachment

	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField with Attachment

		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop

		UpdateFranchiseContractCommentIDMAttributes is a Set Action
			default label is untranslatable
			restricted

			Instance Selection
				where (Attachment.FSMAttachmentIDM.IDMUniqueId entered)

			Action Rules
				Instance Rules
					invoke UpdateIDMAttributes
						resume on error

		UpdateIDMAttributes is an Instance Action
			default label is untranslatable
			restricted

			Local Fields
				LocalExecute		is Boolean

			Action Rules
				
				IDMItem.DocumentType = "FSM_FranchiseContractComment"

				IDMItem.IDMUniqueId = Attachment.FSMAttachmentIDM.IDMUniqueId

				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName  	= "Customer"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue  	= Customer

				IDMItem.IDMAttributes = IDMAttributes
				IDMItem.OverrideUser  = true
				
				LocalExecute = IDMItem.Update

					
