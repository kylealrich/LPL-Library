ClosePeriodTaskIssueComments is a BusinessClass
	owned by closemgmt
	prefix is CPTIC
	
	Ontology
		symbolic key is ClosePeriodTaskIssueComments
			
	Patterns
		implements StaticJava

	Persistent Fields	
		Description				is Alpha 60
		Notes	 				is Text
		Attachment				is an AlternateAttachment
		Author	 				is a FinanceResource 	
		CommentDate             is TimeStamp 
			default label is "Date"	
		
	Local Fields
		LocalAttributeCtr   	is Numeric 2
									
	Field Rules
 	    Attachment
			if (!Notes entered)
				required

		Author
			initial value is actor.agent(Employee).Employee	
			force default to actor.agent(Employee).Employee	
			cannot be changed
		Description
			initial value is ClosePeriodTaskIssue.Summary
			default to ClosePeriodTaskIssue.Summary
			cannot be changed
        CommentDate
            initial value is current timestamp
            default to current timestamp	

	Derived Fields

	Conditions
		IAmTheAuthor
			restricted
			when (Author = actor.agent(Employee).Employee)
		HasNotes
			restricted
			when (Notes entered)
		HasAttachment
			restricted
			when (Attachment entered)
		AllowDelete
			restricted
			when (IAmTheAuthor)
					
	Relations

	Sets

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment
	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
			
	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Purge Action
			valid when (AllowDelete)

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
