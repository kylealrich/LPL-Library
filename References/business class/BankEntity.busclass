BankEntity is a BusinessClass
	owned by cb
	prefix is CBE
	classic name is CBBANKENT

	Ontology
		symbolic key is BankEntity
			classic set name is CBESET1

	Patterns
		implements StaticJava
		disable AuditIndex
		implements BODId

	Persistent Fields

		BankName
		BranchName				is a BankName
		PostalAddress			is a PostalAddressV2	
			holds pii
			classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
			classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
			classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
			classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
			classic name for PostalAddress.Municipality is CITY-ADDR5
			classic name for PostalAddress.StateProvince is STATE-PROV
			classic name for PostalAddress.Country is COUNTRY-CODE
		Contact					is a ContactDesc
			classic name is CONTACT-DESC
		PhoneNumber				is a TelephoneNumber 
			holds pii
			classic name for PhoneNumber.InternationalPrefix is PHONE-PREFIX
			classic name for PhoneNumber.SubscriberNumber is PHONE-NUMBER
			classic name for PhoneNumber.Extension is PHONE-EXT
		FaxNumber 
			holds pii
			classic name for FaxNumber.InternationalPrefix is FAX-PREFIX
			classic name for FaxNumber.SubscriberNumber is FAX-NUMBER
			classic name for FaxNumber.Extension is FAX-EXT
		SWIFTCharge				is Numeric size 1
			classic name is CHG-ACCT-CODE
			States
				PrincipalsBaseAccount		value is 1
				PrincipalsForeignAccount	value is 2
				Beneficiary					value is 3
		UsedFlag				is Boolean
		CreatedUser				is an Operator 
			holds pii
			classic name is CREATED-BY
		LastUpdateDate			is TimeStamp
			classic name is LAST-UPDT-DATE
		LastUpdateUser			is an Operator 
			holds pii
			classic name is LAST-UPDATE-BY

		ExcludeFromPaymentFile	is Boolean		
			default label is "ExcludeEntityValueFromPaymentFile"
		SwiftID								

	Local Fields
		ActionCode							is Alpha size 1
			States
				Create	value is "C"
				Update	value is "U"
				Delete	value is "D"
		LocalTitle 							is Alpha size 255

		LocalNativeLPLBODTrigger			is Boolean
		LocalVerb							is Numeric 2 
		LocalNoun							is Alpha size 100
		LocalMainUserTemplate				is Alpha size 250
		NewBODTracker  				is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker	is Numeric 15
		Error            			is Boolean
	    ErrorMessage     			is Alpha 300
		LocalBODTrigger				is Boolean
		LocalConfigurationParameter	is Alpha size up to 200

	Context Fields
		FSMInboundBODTracker

	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforFinancialPartyMaster"

		ContextMessageText is a MessageField
			restricted
			"BankEntity<BankEntity>"



		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedConfigAccountingEntity is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "ProcessFinancialPartyMaster_Out_AccountingEntity"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
				

		DerivedCreatedBy is a DerivedField
			type is Alpha size 60
			restricted
			return create stamp.actor
		
		DerivedUpdatedBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
		  
		DerivedBODRevision is a DerivedField
			type is Alpha size 25
			return ""

		DerivedBODVariationID is a DerivedField
			type is Alpha size 25
			restricted
			return  bod id.VariationID
		
		DerivedDefaultBODLocation is a DerivedField
			type is AlphaUpper size 20
			return ""
			
		DerivedDocumentID is a DerivedField
			type is Alpha size 15
			restricted
			return BankEntity
				
		BODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			BODCurrentTimeStamp = current timestamp
			return BODCurrentTimeStamp
		 		
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return BODCurrentTimeStamp[1:4] + "-" + BODCurrentTimeStamp[5:6] + "-" + BODCurrentTimeStamp[7:8] + "T" + BODCurrentTimeStamp[9:10] + ":" + BODCurrentTimeStamp[11:12] + ":" + BODCurrentTimeStamp[13:14] + "Z"

		DerivedBODID is a DerivedField
			type is Alpha size 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedAccountingEntity + ":" + DerivedDefaultBODLocation + ":" + DerivedDocumentID + ":" +DerivedBODRevision+"?FinancialPartyMaster&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
		
		DerivedAccountingEntity is a DerivedField
			type is Alpha size 20
			restricted
			return CashManagementGroup
		
		DerivedBODActionCode is a DerivedField
			type is Alpha size 10
			restricted
			if (ActionCode.Create or action type.Create)
				return "Add"
			else
				return "Replace" 
				
		DerivedBODStatusCode is a DerivedField
			type is Alpha size 20
			restricted
			if (ActionCode.Create or action type.Create)
				return "Open"			
			if (ActionCode.Delete or action type.Delete)
				return "Deleted"
			if (ActionCode.Update or action type.Update)
				return "Open"
				
		DerivedBankEntityAddLine1 is a DerivedField
			type is Alpha size 100
			restricted
			return PostalAddress.DeliveryAddress.AddressLine1
		
		DerivedBankEntityAddLine2 is a DerivedField
			type is Alpha size 100
			restricted
			return PostalAddress.DeliveryAddress.AddressLine2
		
		DerivedBankEntityAddLine3 is a DerivedField
			type is Alpha size 100
			restricted
			return PostalAddress.DeliveryAddress.AddressLine3
		
		DerivedBankEntityAddLine4 is a DerivedField
			type is Alpha size 100
			restricted
			return PostalAddress.DeliveryAddress.AddressLine4
		
		DerivedBankEntityMunicipality is a DerivedField
			type is Alpha size 100
			restricted
			return PostalAddress.Municipality
			
		DerivedBankEntityStateProvince is a DerivedField
			type is Alpha size 100
			restricted
			return PostalAddress.StateProvince
			
		DerivedBankEntityCountry is a DerivedField	
			type is Alpha size 100	
			restricted
			return PostalAddress.Country
			
		DerivedBankEntityPostalCode is a DerivedField	
			type is Alpha size 100
			restricted
			return PostalAddress.PostalCode
			
		DerivedBankEntityPhonePrefix is a DerivedField
			type is Alpha size 10
			restricted
			return PhoneNumber.InternationalPrefix
			
		DerivedBankEntityPhoneNumber is a DerivedField
			type is Alpha size 20
			restricted
			return PhoneNumber.SubscriberNumber
			
		DerivedBankEntityPhoneExtension is a DerivedField
			type is Alpha size 20
			restricted
			return PhoneNumber.Extension
		
		DerivedBankEntityFaxPrefix is a DerivedField
			type is Alpha size 20
			restricted
			return FaxNumber.InternationalPrefix
		
		DerivedBankEntityFaxNumber is a DerivedField
			type is Alpha size 20
			restricted
			return FaxNumber.SubscriberNumber
		
		DerivedBankEntityExtension is a DerivedField
			type is Alpha size 20
			restricted
			return FaxNumber.Extension    
		
		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha size 20
			restricted
			return CashManagementGroup.FinanceEnterpriseGroup



		SyncFinancialPartyMasterXMLBOD is a DerivedField 
	        type is XMLDocument
	        restricted 
			SyncFinancialPartyMasterXMLBOD = template.IONSyncFinancialPartyMaster_BankEntity_ST document for this instance



		DerivedProcessAccountingEntity is a DerivedField
			type is Alpha size 50
			restricted
			if (DerivedConfigAccountingEntity entered)
				return DerivedConfigAccountingEntity
			else
				return CashManagementGroup
		
		DerivedProcessBODID is a DerivedField
			type is Alpha size 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedProcessAccountingEntity + ":" + DerivedDefaultBODLocation + ":" + DerivedDocumentID + ":" +DerivedBODRevision+"?FinancialPartyMaster&verb=Process&TrackerID=" + LocalFSMInboundBODTracker
		
		ProcessFinancialPartyMasterXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			ProcessFinancialPartyMasterXMLBOD = template.IONProcessFinancialPartyMaster_BankEntity_ST document for this instance
			return ProcessFinancialPartyMasterXMLBOD

			         
    Conditions




          
    Relations












		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "FinancialPartyMaster"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMBODConfigurationProcessRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "FinancialPartyMaster"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
		


    Field Rules

        BankName
            required

		CreatedUser
			initial value is actor
			default to actor




	Rule Blocks
		TriggerProcessFinancialParty 
			trigger "ProcessBankEntityService" PA service
				resume on error
				title is "<LocalTitle>"
				Criteria
					CashManagementGroup.FinanceEnterpriseGroup
					BankEntity
				Variables
					ActionCode
					include persistent fields from BankEntity
					CashManagementGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
				
		TriggerSyncFinancialParty
			trigger PayablesService.BankEntityService PA service
				resume on error
				title is "<LocalTitle>"
				Criteria
					CashManagementGroup.FinanceEnterpriseGroup
					BankEntity
				Variables
					ActionCode
					include persistent fields from BankEntity
					CashManagementGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
	Action Exit Rules
		if (CashManagementGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode = ActionCode.Update
					if (action type.Create)
						ActionCode = ActionCode.Create
					else 
						if(action type.Delete)			
							ActionCode = ActionCode.Delete
					increment bod id.VariationID
					LocalTitle = "EG:" + CashManagementGroup.FinanceEnterpriseGroup +"    Bank:" + BankEntity
					include TriggerSyncFinancialParty
   					include TriggerProcessFinancialParty






    Actions
    	UpdateBODIdFields is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location" 
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"	
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord

		SendFinancialPartyMasterBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is SyncFinancialPartyMasterXMLBOD
					bod type is "Sync.FinancialPartyMaster"
					document id is  DerivedDocumentID
					variation id is DerivedBODVariationID
					accounting entity is DerivedAccountingEntity
		
		TriggerFinancialPartyMasterNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "FinancialPartyMaster"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "BankEntity"
					invoked.PrmFinanceEnterpriseGroup	= CashManagementGroup.FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate 		= "IONSyncFinancialPartyMaster_BankEntity_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(CashManagementGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)					
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "FinancialPartyMaster"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= CashManagementGroup
							invoked.Reference2				= BankEntity
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity	
							invoked.Reference1				= CashManagementGroup					
							invoked.Reference2				= BankEntity
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendFinancialPartyMasterBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= SyncFinancialPartyMasterXMLBOD
							invoked.BODID								= DerivedBODID
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODXML									= SyncFinancialPartyMasterXMLBOD
							invoked.BODID									= DerivedBODID

		SendProcessFinancialPartyMasterBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ProcessFinancialPartyMasterXMLBOD
					bod type is "Process.FinancialPartyMaster"
					document id is  DerivedDocumentID
					variation id is DerivedBODVariationID
					accounting entity is DerivedProcessAccountingEntity	

		TriggerProcessFinancialPartyMasterNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationProcessRel
					invoked.PrmVerb 					= 2
					invoked.PrmNoun						= "FinancialPartyMaster"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "BankEntity"
					invoked.PrmFinanceEnterpriseGroup	= CashManagementGroup.FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate 		= "IONProcessFinancialPartyMaster_BankEntity_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationProcessRel.NativeLPLBODTrigger
				LocalBODTrigger = true 
				if(CashManagementGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)					
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 2
							invoked.Noun 					= "FinancialPartyMaster"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedProcessAccountingEntity
							invoked.Reference1				= CashManagementGroup
							invoked.Reference2				= BankEntity
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedProcessAccountingEntity
							invoked.Reference1				= CashManagementGroup						
							invoked.Reference2				= BankEntity
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendProcessFinancialPartyMasterBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= ProcessFinancialPartyMasterXMLBOD
							invoked.BODID								= DerivedProcessBODID
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODXML									= ProcessFinancialPartyMasterXMLBOD
							invoked.BODID									= DerivedProcessBODID
	

					
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
			Entrance Rules
				if (CashManagementGroup.FinanceEnterpriseGroup.BODTrigger)
					ActionCode = ActionCode.Delete
					increment bod id.VariationID
					LocalTitle = "EG:" + CashManagementGroup.FinanceEnterpriseGroup +"    Bank:" + BankEntity
					include TriggerSyncFinancialParty
 					include TriggerProcessFinancialParty





									
		BODDataInitialLoadBankEntityCriteria is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFEG					is a FinanceEnterpriseGroup
				PrmFromCashManagementGroup			is a CashManagementGroup
				PrmFromBankEntity			is a BankEntity
					context of PrmFromCashManagementGroup
				PrmToBankEntity			is a BankEntity
					context of PrmFromCashManagementGroup
			Parameter Rules				

			Instance Selection
			
				where  (((PrmFromCashManagementGroup entered
				and		  PrmFromCashManagementGroup = CashManagementGroup)
				or		  PrmFromCashManagementGroup not entered)				
				and    ((PrmFromBankEntity entered
				and		 PrmFromBankEntity <= BankEntity)
				or		 PrmFromBankEntity not entered)				
				and    ((PrmToBankEntity entered
				and		 PrmToBankEntity >= BankEntity)
				or		 PrmToBankEntity not entered))
			Sort Order	
				
			Action Rules								
				
				Instance Rules	
					if (CashManagementGroup.FinanceEnterpriseGroup.BODTrigger)
						ActionCode	= ActionCode.Create		
						increment bod id.VariationID
						LocalTitle = "EG:" + CashManagementGroup.FinanceEnterpriseGroup +"Bank:" + BankEntity
						include TriggerSyncFinancialParty 
						include TriggerProcessFinancialParty





