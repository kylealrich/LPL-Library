UserRelation is a BusinessClass
    owned by repository
    prefix is usre
    framework type is LPLConfiguration

	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing
		implements DynamicCreation
		implements ConfigurationMetadata
		implements AuditLogEntryActions
				
    Ontology
        symbolic key is UserRelation
    
    Persistent Fields
    	BusinessClass
    	RelationName		is LPLName
    	BusSet				
    	Status 				is a UserObjectStatus
    	LPL                 is LPL
    	Description         is a ConfigDescription
    	UserEditorType
    	ErrorText			is Text
    	
	Field Rules
		BusinessClass
			required
			cannot be changed
			constraint (!BusinessClass.ReadOnly)
				"ConfigurationNotAllowedIn_Read-\OnlyBusinessClass."

			constraint (!BusinessClass.IsUserClass)
				"ConfigurationNotAllowedInAUserDefinedBusinessClass.UpdateTheConfigurationOnTheUserBusinessClass."
			
		RelationName
    		required
    		cannot be changed
    		
    		constraint (not RelationRel exists
    		or          RelationRel.IsConfiguration) 
    			"Relation<BusinessClass>.<RelationName>AlreadyExists"
    		
		BusSet 
			required
			    		
		LPL    
			required
			
				if (Status.Active)
					LocParseText = ParseText
				
				if (not LocParseText = blank)




					confirmation required
						"<LocParseText>:_User_RelationIsInvalid.__DoYouWantToSaveAnyway?"
				
					Status = Status.Invalid	
					ErrorText = LocParseText
						
		UserEditorType
			default to UserEditorType.LPL	
			
		Status
			if (Status.Active)
				initialize ErrorText			
    	
    Derived Fields
    	ConfigActor is a DerivedField
    		type is Actor
    		default label is "Actor"
			return blank
			
		ParseText is a NativeField
			type is Text			
			
    Conditions
		ConfigGlobal
			default label is "GlobalConfiguration"
			when (true)
			
		HasErrorText
			default label is "ErrorTextEntered"
			when (ErrorText entered)			

	Local Fields    		
		LocParseText is Text
		LocParsed is Boolean 
        LocBusinessView             is LPLName
		LocName                     is LPLName
		
    Relations
    	RelationRel				
   			one-to-one relation to BusinessRelation
   			Field Mapping uses symbolic key
   				related.BusinessClass 		= BusinessClass
   				related.BusinessRelation 	= RelationName
				
        ConfigEntityRel
            one-to-one relation to ConfigEntity
            Field Mapping uses symbolic key
                related.ConfigEntity = UserRelation

		LocalDuplicateRel
    		one-to-many relation to UserRelation
			Field Mapping uses ByClassRelation
				related.BusinessClass   = LocBusinessView
				related.RelationName    = LocName

   	Actions
    	Create is a Create Action
    		Exit Rules
    			invoke FireEvent
    			
    	CreateFromSnapshot is a Create Action
    		restricted
    		bypass field rules

    				
		Update is an Update Action
			Exit Rules
    			invoke FireEvent
    	
    	UpdateFromSnapshot is an Update Action
    		restricted
    		bypass field rules

    				
    	Delete is a Delete Action
    		Exit Rules
    			invoke FireEvent    		
    			
    	DeleteFromSnapshot is a Delete Action
    		restricted
    		bypass relational integrity rules

    	
    	Activate is an Instance Action
    		valid when (not Status.Active)
    		Action Rules
    			LocParseText = ParseText
				
				if (not LocParseText = blank)




					constraint(LocParseText = blank)							
						"<LocParseText>:_User_RelationIsInvalid._Cannot_Activate."

   				Status = Status.Active
   				initialize ErrorText

			Exit Rules
    			invoke FireEvent    			
    			
    	Deactivate is an Instance Action
    		valid when (not Status.Inactive)
    		Action Rules
    			Status = Status.Inactive

			Exit Rules
    			invoke FireEvent    
    			
		Invalidate is an Instance Action
			valid when (not Status.Invalid)
    		restricted
    		Action Rules
    			Status = Status.Invalid
    			
			Exit Rules
    			invoke FireEvent    			    							
    			
		FireEvent is an Instance Action
			restricted      			
	
		TagItem is an Instance Action
			Parameters
				ParamConfigurationTag is a ConfigurationTag
					default label is "Tag"
			
			Entrance Rules
				if (Status.Inactive)
					confirmation required
						"UserRelationIsInactive,TagAnyway?"
							
				if (Status.Invalid)
					confirmation required
						"UserRelationIsInvalid,TagAnyway?"
								
			Action Rules
				invoke Create ConfigurationTagItem
					invoked.ConfigurationTag = ParamConfigurationTag
					invoked.ConfigurationTagItem.Reference = reference to this instance

        CreateRelation is a Create Action
            Parameters
                BusinessClassParam is a BusinessClass
                    default label is "BusinessClass"

                NameParam is LPLName
                    default label is "Name"

                TypeParam is Numeric size 1
                    States
                        OneToOne	value is 0
                        OneToMany	value is 1
                        BusinessSet	value is 2

                    default label is "Type"

                RelationToParam is a BusinessClass
                    default label is "RelationTo"

                BusinessSetParam is a BusinessSet
                    context of RelationToParam
                    default label is "BusinessSet"

                ValidWhenParam is a BusinessField
                    context of BusinessClassParam
                    default label is "ValidWhen"

                ValidWhenNegatedParam is Boolean
                    default label is "Negated"

                DeleteRuleParam is Numeric size 1
                    States
                        Ignored	    value is 0
                        Cascades	value is 1
                        Restricted	value is 2

                    default label is "DeleteRule"

                AsOfParam is Numeric size 1
                    States
                        AsOf	    value is 1
                        AllDates	value is 2
                        Between 	value is 3

                    default label is "AsOf"

                AsOfDateParam is a BusinessField
                    context of BusinessClassParam
                    default label is "AsOfDate"

                BetweenStartDateParam is a BusinessField
                    context of BusinessClassParam
                    default label is "StartDate"

                BetweenEndDateParam is a BusinessField
                    context of BusinessClassParam
                    default label is "EndDate"

			Parameter Rules
				BusinessClassParam
					required

					constraint (!BusinessClassParam.ReadOnly)
						"ConfigurationNotAllowedIn_Read-\OnlyBusinessClass."

					constraint (!BusinessClassParam.IsUserClass)
						"ConfigurationNotAllowedInAUserDefinedBusinessClass.UpdateTheConfigurationOnTheUserBusinessClass."

			    NameParam
			        required

                TypeParam


                RelationToParam
                    required

                BusinessSetParam
                    if (!TypeParam.BusinessSet)
                        required







                AsOfDateParam
                    if (!TypeParam.BusinessSet and AsOfParam.AsOf)
                        required

                BetweenStartDateParam
                    if (!TypeParam.BusinessSet and AsOfParam.Between)
                        required

                BetweenEndDateParam
                    if (!TypeParam.BusinessSet and AsOfParam.Between)
                        required

			Local Fields
				LocalLPLParam is LPL
				LocalBusSetBusinessClassParam is a BusinessClass
				LocalBusSetBusinessSetParam is a BusinessSet
				    context of LocalBusSetBusinessClassParam

            Entrance Rules

            Action Rules
                LocBusinessView = BusinessClassParam
                LocName = NameParam
                constraint (!LocalDuplicateRel exists)
                    "RelationNameInUse_'<LocName>'"

                invoke Create this instance
                    invoked.BusinessClass           = BusinessClassParam
                    invoked.RelationName            = NameParam
                    invoked.BusSet.BusinessClass    = LocalBusSetBusinessClassParam
                    invoked.BusSet.BusinessSet      = LocalBusSetBusinessSetParam
                    invoked.LPL 				    = LocalLPLParam

    Sets
    	ByClassRelation
    		indexed
    		Sort Order
    			BusinessClass
    			RelationName
    	
