OtherForecastTransaction is a BusinessClass
	owned by cashmgmt
	prefix is CMOFT

	Ontology
		symbolic key is OtherForecastTransaction
		
	Persistent Fields
		CashTransactionCategory 
		Description					 is Alpha 250
		ForecastCreationParameter	 is an InterfaceForecastCreationParameter	 
		EnteredByResource			 is a FinanceResource
		Attachment					 is an AlternateAttachment
		CashForecast
			delete ignored
		CashForecastAccount
			delete ignored
		
	Local Fields
		LocalCashForecast			 is AlphaUpper 30
		LocalForecastDate			 is Date
		LocalCashTransactionCategory is a CashTransactionCategory
		LocalCashManagementAccount	 is a CashManagementAccount
		ForecastDateArrayCounter	 is Numeric 1
		LocalAttributeCtr   is Numeric 2
	
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 50
			restricted
			"InforCashForecastInterface"

		ContextMessageText is a MessageField
			restricted
			"CashForecastInterface<OtherForecastTransaction>"

		NumberOfDaysInRange is a DerivedField
  			type is Numeric 4
  			restricted
  			return (instance count of CashForecastPeriodDaysInRangeRel)
  		
  		NumberOfWeekDaysInRange is a DerivedField
  			type is Numeric 4
  			restricted
  			return (instance count of CashForecastPeriodDayOfWeekRel)
  		
  		NumberOfMonthDaysInRange is a DerivedField
  			type is Numeric 4
  			restricted
  			return (instance count of CashForecastPeriodDayOfMonthRel)
  						
	Field Rules
		Description
			required
		
		ForecastCreationParameter
			initial value is 1 
			required
			
		EnteredByResource
			force default to actor.agent(Employee).Employee

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
		
	Conditions
		MyTransactions
			restricted
			when (actor.agent(Employee).Employee = EnteredByResource)
			
		OnCashForecast
			restricted
			when (CashForecast entered)
		
		HasAttachment
			restricted
			when (Attachment entered)
				
	Relations
		CashForecastCategoryRel
			one-to-one relation to CashForecastCategory
			Field Mapping uses symbolic key
				related.CashManagementGroup							 = CashManagementGroup
				related.CashForecast								 = LocalCashForecast
				related.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory
		
		CashForecastPeriodDayOfWeekRel
			one-to-many relation to CashForecastPeriod
			Field Mapping uses symbolic key
				related.CashManagementGroup		  				  = CashManagementGroup
				related.CashForecast		  					  = LocalCashForecast
				related.CashForecastAccount.CashManagementAccount = LocalCashManagementAccount
			Instance Selection
				where (related.CashForecastPeriod within ForecastCreationParameter.ForecastDateRange
				and	   related.NumericDayOfWeek = ForecastCreationParameter.DayOfWeek)
		
		CashForecastPeriodDayOfMonthRel
			one-to-many relation to CashForecastPeriod
			Field Mapping uses symbolic key
				related.CashManagementGroup		  				  = CashManagementGroup
				related.CashForecast		  					  = LocalCashForecast
				related.CashForecastAccount.CashManagementAccount = LocalCashManagementAccount
			Instance Selection
				where (related.CashForecastPeriod within ForecastCreationParameter.ForecastDateRange
				and	   related.CashForecastPeriodDay = ForecastCreationParameter.DayOfMonth)
		
		CashForecastPeriodDaysInRangeRel
			one-to-many relation to CashForecastPeriod
			Field Mapping uses symbolic key
				related.CashManagementGroup		  				  = CashManagementGroup
				related.CashForecast		  					  = LocalCashForecast
				related.CashForecastAccount.CashManagementAccount = LocalCashManagementAccount
			Instance Selection
				where (related.CashForecastPeriod within ForecastCreationParameter.ForecastDateRange)
		
		CashForecastPeriodAmountCubeRel
			one-to-one relation to AnalyticCube
	        Field Mapping uses AnalyticCubeSet
	        	related.BusinessClass = "CashForecastPeriodAmount"
	        							
	Rule Blocks
		AddEnteredTransactionCalculation
  			invoke Create CashForecastDetail
				invoked.CashManagementGroup							 = PrmCashManagementGroup
				invoked.CashForecast								 = PrmCashForecast
				invoked.CashForecastAccount	 						 = PrmCashForecastAccount
				invoked.CashForecastPeriod							 = LocalForecastDate
				invoked.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory
				invoked.Description									 = Description
				invoked.ForecastAmountAction						 = PrmAction
				if (ForecastCreationParameter.ForecastCalculation.Manual)
					invoked.ForecastAmount	= ForecastCreationParameter.ForecastAmount
				else
				if (ForecastCreationParameter.CalculationMethod.Whole)
					invoked.ForecastAmount	= ForecastCreationParameter.ForecastAmount
				else
				if (ForecastCreationParameter.EnteredCalculationOption.Date)
					invoked.ForecastAmount	= (ForecastCreationParameter.ForecastAmount / ForecastCreationParameter.NumberOfDatesEntered)
				else
				if (ForecastCreationParameter.EnteredCalculationOption.DateRange)
					invoked.ForecastAmount	= (ForecastCreationParameter.ForecastAmount / NumberOfDaysInRange)
				else 
				if (ForecastCreationParameter.EnteredCalculationOption.DayOfWeek)
					invoked.ForecastAmount	= (ForecastCreationParameter.ForecastAmount / NumberOfWeekDaysInRange)
				else
				if (ForecastCreationParameter.EnteredCalculationOption.DayOfMonth)
					invoked.ForecastAmount	= (ForecastCreationParameter.ForecastAmount / NumberOfMonthDaysInRange)
				invoked.IsSystemCalculated	= true
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
			valid when(!OnCashForecast)
		
		AddToForecast is an Instance Action
			Parameters
				PrmCashManagementGroup	   is a CashManagementGroup
				PrmCashForecast			   is a CashForecast
				PrmCashTransactionCategory is a CashTransactionCategory
				PrmCashForecastAccount     is a CashForecastAccount
				PrmAction			   	   is Numeric 1
					default label is "Action"
					States	
						OverrideForecastAmount	value is 1
						AppendForecastAmount	value is 2
				
			Parameter Rules
				PrmCashManagementGroup
					required
						"CashManagementGroupIsRequired"
						
				PrmCashForecast
					required
						"CashForecastIsRequired"
						
				PrmCashTransactionCategory
					if (CashTransactionCategory entered
					and PrmCashTransactionCategory != CashTransactionCategory)
						confirmation required
							"CategoryHasBeenChanged;WouldYouLikeToContinue?"
					
				PrmCashForecastAccount
					required
						"AccountIsRequired"
				
				PrmAction	
					required
						"ActionIsRequired"
						
			Entrance Rules
				if (PrmCashTransactionCategory entered)
					if (PrmCashTransactionCategory != CashTransactionCategory)
						CashTransactionCategory = PrmCashTransactionCategory
				else
					constraint (CashTransactionCategory entered)
						"CategoryIsRequired"

				LocalCashForecast = PrmCashForecast
				LocalCashTransactionCategory = CashTransactionCategory
				LocalCashManagementAccount = PrmCashForecastAccount.CashManagementAccount
				
				if (!CashForecastCategoryRel exists)
					invoke Create CashForecastCategory
						invoked.CashManagementGroup							   = PrmCashManagementGroup
						invoked.CashForecast								   = PrmCashForecast
						invoked.CashForecastCategory.CashTransactionCategory   = LocalCashTransactionCategory
						invoked.ParentForecastCategory.CashTransactionCategory = LocalCashTransactionCategory.ParentTransactionCategory
						invoked.ForecastCreationParameter					   = ForecastCreationParameter
						
			Action Rules
				if (ForecastCreationParameter.ForecastCalculation.Manual)
					LocalForecastDate = ForecastCreationParameter.ForecastDateArray.ForecastDate[1]
					include AddEnteredTransactionCalculation
				else
				if (ForecastCreationParameter.EnteredCalculationOption.Date)
					ForecastDateArrayCounter = 1
					while (ForecastDateArrayCounter <= 4)
						if (ForecastCreationParameter.ForecastDateArray.ForecastDate[ForecastDateArrayCounter] entered)
							LocalForecastDate = ForecastCreationParameter.ForecastDateArray.ForecastDate[ForecastDateArrayCounter]
							include AddEnteredTransactionCalculation
						ForecastDateArrayCounter += 1
				else
				if (ForecastCreationParameter.EnteredCalculationOption.DayOfWeek)
					for each CashForecastPeriodDayOfWeekRel
						LocalForecastDate = each.CashForecastPeriod
						include AddEnteredTransactionCalculation
				else
				if (ForecastCreationParameter.EnteredCalculationOption.DayOfMonth)
					for each CashForecastPeriodDayOfMonthRel
						LocalForecastDate = each.CashForecastPeriod
						include AddEnteredTransactionCalculation
				else	
					for each CashForecastPeriodDaysInRangeRel
						LocalForecastDate = each.CashForecastPeriod
						include AddEnteredTransactionCalculation
				
				CashForecast = PrmCashForecast
				CashForecastAccount = PrmCashForecastAccount
		
			Exit Rules
				if (CashForecast.HaveBuiltForecast)
  					invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
					
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
