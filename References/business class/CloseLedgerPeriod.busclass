CloseLedgerPeriod is a BusinessTask
    owned by GeneralLedger

    Parameters
        FinanceEnterpriseGroup
		GeneralLedgerCloseConfiguration
			default label is "GlobalLedgerCloseConfiguration"
        YearPeriodOption		is Numeric 1
        	States
        		Year				value is 1
        		SinglePeriod		value is 2
        		PeriodRange			value is 3
		FiscalYear				is a GeneralLedgerCloseYear
			context of GeneralLedgerCloseConfiguration
		CurrentPeriod			is a GeneralLedgerClosePeriod
			context of GeneralLedgerCloseConfiguration
		AccountingEntityGroup
		LedgerGroup				is a Ledger group 
		AccountingEntity
		Ledger 
		ActionOption			is Numeric 1
	    	States
				LimitedClose		value is 0
				OpenForBackpost		value is 1
				FinalClose			value is 2
		EntityOption			is Numeric 1
	    	States
				Single				value is 1
				Group				value is 2
		LedgerOption			is Numeric 1
	    	States
				Single				value is 1
				Group				value is 2
		PeriodRange				is a ClosePeriodRange
		PrmSkipAttachRule		is a SkipAttachRule			

    Parameter Rules
        FinanceEnterpriseGroup
        	required
        GeneralLedgerCloseConfiguration
        	required
        YearPeriodOption
        	required
        ActionOption
        	if (ActionOption.FinalClose)
				confirmation required
					"FinalClosedPeriodsCannotBeReopened"		
		FiscalYear
			if (YearPeriodOption.Year)
				required
					"FiscalYearIsRequired"
			else
				FiscalYear = 0
		CurrentPeriod
			if (YearPeriodOption.SinglePeriod)
				required
					"PeriodIsRequired"
			else
				CurrentPeriod.GeneralLedgerCalendarPeriod = blank
		PeriodRange
			if (YearPeriodOption.PeriodRange)
				required
					"PeriodRangeIsRequired"
			else
				PeriodRange	 = blank
		AccountingEntity
			if (EntityOption.Single)
				required
					"<FinanceEnterpriseGroup.AccountingEntityLabel>IsRequired"
			else
				AccountingEntity = blank
		AccountingEntityGroup
			if (EntityOption.Group)
				required
					"<FinanceEnterpriseGroup.AccountingEntityLabel>GroupIsRequired"
			else
				AccountingEntityGroup = blank
		Ledger
			if (LedgerOption.Single)
				required
					"LedgerIsRequired"
			else
				Ledger = blank
		LedgerGroup
			if (LedgerOption.Group)
				required
					"LedgerGroupIsRequired"
			else
				LedgerGroup = blank
		PrmSkipAttachRule		
			default to true

    Actions

        SchedulePeriodClose is an Instance Action
			run in background
			
            Action Rules
            
				invoke ProcessEntityLedgerPeriod EntityLedgerPeriod  in background
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmCloseConfiguration		= GeneralLedgerCloseConfiguration
					invoked.PrmAccountingEntityGroup	= AccountingEntityGroup
					invoked.PrmLedgerGroup				= LedgerGroup
					invoked.PrmAccountingEntity			= AccountingEntity
					invoked.PrmLedger					= Ledger
					invoked.PrmYear						= FiscalYear
					invoked.PrmPeriod					= CurrentPeriod
					invoked.PrmActionOption				= ActionOption
					invoked.PrmPeriodRange				= PeriodRange
					invoked.PrmSkipAttachRule			= PrmSkipAttachRule		
					
