PfiProxy is a BusinessClass
    owned by pfi
    prefix is PfiPX
    default label is "Proxy"

    Ontology
        part of PfiUserProfile
            relative key is PfiProxyUserTask

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated

    Persistent Fields

        EffectiveDate  is TimeStamp
        ExpirationDate is TimeStamp
        CreatedBy      is like Actor // LMRK-97977 Was "is Actor". Likely meant to be "is an Actor". Changed to "like" so we have the same dict type but without changing existing functionality.

    Field Rules

        CreatedBy
            default to actor

    Local Fields

        LocalUserProfile is a PfiUserProfile

    Derived Fields

        ProxyUserName is a DerivedField
            type is Alpha size 101
            return PfiUserProfile.FirstNameAndLastNameOrActor

        ProxiedUserName is a DerivedField
            type is Alpha size 101
            LocalUserProfile = PfiProxyUserTask.ProxiedUser
            return LocalUserProfile.FirstNameAndLastNameOrActor

        IsProxyEffective is a DerivedField
            type is Boolean
            default label is "ProxyIsEffective"
            if (CheckEffectiveDate and CheckExpirationDate)
                return true
            return false

        CheckEffectiveDate is a DerivedField
            type is Boolean
            if (EffectiveDate not entered)
                return true
            if (system current timestamp >= EffectiveDate)
                return true
            return false

        CheckExpirationDate is a DerivedField
            type is Boolean
            if (ExpirationDate not entered)
                return true
            if (system current timestamp < ExpirationDate)
                return true
            return false

        CreatedByUserName is a DerivedField
            type is Alpha size 101
            return PfiCreatedByToActorRel.FirstNameAndLastNameOrActor

    Sets

        ByProxiedUserAndTask
            sql name is PfiPX01
            indexed
            Sort Order
                PfiProxyUserTask.ProxiedUser
                PfiProxyUserTask.ProxiedTask
                PfiUserProfile

        ByProxiedTaskAndUser
            sql name is PfiPX02
            indexed
            Sort Order
                PfiProxyUserTask.ProxiedTask
                PfiProxyUserTask.ProxiedUser
                PfiUserProfile

        ByEffectiveDate
            sql name is PfiPX03
            indexed
            Sort Order
                EffectiveDate
                PfiUserProfile
                PfiProxyUserTask.ProxiedTask
                PfiProxyUserTask.ProxiedUser

        ByExpirationDate
            sql name is PfiPX04
            indexed
            Sort Order
                ExpirationDate
                PfiUserProfile
                PfiProxyUserTask.ProxiedTask
                PfiProxyUserTask.ProxiedUser

    Relations

        PfiCreatedByToActorRel
            one-to-one relation to Actor
            Field Mapping uses symbolic key
                related.Actor = CreatedBy

    Rule Blocks

        ValidateDates
            if (EffectiveDate entered and ExpirationDate entered)
                constraint (EffectiveDate < ExpirationDate)
                    "InvalidDateRangeSpecified"

        ValidateProxyUser
            constraint (PfiUserProfile != actor)
                "CannotProxyToSelf"

    Actions

        Create is a Create Action

            Entrance Rules
                include ValidateDates
                include ValidateProxyUser

        Update is an Update Action

            Entrance Rules
                include ValidateDates

        Delete is a Delete Action
