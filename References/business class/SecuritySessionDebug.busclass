SecuritySessionDebug is a BusinessClass
    owned by security
    prefix is SCSD
    stored in environment
    contains environment data // TODO - This likely can be removed since "disable data area copy" has been added, once that pattern is fully implemented.
    disable data area copy
    
    Ontology
    	symbolic key is SecuritySessionDebug
    
    Patterns
    	implements CRUD
		disable Auditing
		disable EffectiveDated
	
	Persistent Fields
		SecurityLogger
		ThreadID			is Numeric 5 
		Start				is TimeStamp
		End					is TimeStamp
		TotalTimeMs			is Numeric size 15
		DataArea 			is Alpha size 20
		Actor 				is Alpha size 50
		RequestType 		is Alpha size 30
		SecObjectType 		is Alpha size 20
		SecObjectName 		is Alpha size 50
		IsAttach			is Boolean
			default label is "Attachable" 
		Action				is Alpha size 30
		Result				is Alpha size 100
		Log 				is Text
		
	Derived Fields
		DebugLog			is a DerivedField
			type is BinaryDocument
			return Log
			
	Sets
		BySecurityLogger
			duplicates
			Sort Order
				SecurityLogger
		
	Actions
		Create is a Create Action
		Delete is a Delete Action
		Purge is a Purge Action
		
		PurgeAll is a Set Action
			Action Rules
				Instance Rules
					invoke Purge

		PurgeSessionDebugLogs is a Set Action
			Parameters
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3

			Local Fields
				LocalThruDate 	is Date
					value is ThruDate		

			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThroughEndDateOrOffset"
					else
						LocalThruDate  = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThroughEndDateAndOffset"

			Instance Selection
				where (Start < LocalThruDate)
									
			Action Rules
				Instance Rules
					invoke Purge
