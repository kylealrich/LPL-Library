RequesterFavoritePOTemplate is a BusinessClass
	owned by rq
	prefix is RQRF
	
	Ontology
		symbolic key is RequesterFavoritePOTemplate
		
	
	Patterns
		disable AuditIndex
	
	Persistent Fields
		UseAsDefault              is Boolean
		
	Conditions
		IsDefaultTemplate
			restricted
			when (UseAsDefault)
	
	Relations
		RequesterFavoritePOTemplateRel
			one-to-many relation to RequesterFavoritePOTemplate
			Field Mapping uses symbolic key
				related.Company			= RequesterFavoritePOTemplate.Company
				related.Requester		= RequesterFavoritePOTemplate.Requester
				
		AnotherDefaultFavoritePOTemplateRel
    		one-to-many relation to RequesterFavoritePOTemplate
    		Field Mapping uses symbolic key
    			related.Company				= RequesterFavoritePOTemplate.Company
				related.Requester			= RequesterFavoritePOTemplate.Requester
    		Instance Selection
    			where (related.UseAsDefault
    			and related.ProcurementTemplate != ProcurementTemplate)
    			
    Sets
    	ByProcurementTemplate
			Sort Order
				ProcurementTemplate
				Company
				Requester
    			
    Rule Blocks
    	CheckUseAsDefault
    		if (AnotherDefaultFavoritePOTemplateRel exists)
    			constraint (!UseAsDefault)
    				"FavoritePOTemplateExistsForThisRequester"	
    					
		
	Field Rules
		Company
	    	constraint (Company.FinanceEnterpriseGroup.HROrganization = actor.context.HROrganization)
				"FinanceEnterpriseGroupAssociatedWithCompanyDoesNotUseHROrganizationForRequester"
    				
		Requester
			required
			
	Actions

		Create is a Create Action
			Field Rules
				ProcurementTemplate
					constraint (ProcurementTemplate.RSSIParticipate
					and ProcurementTemplate.IsValidForActorContext)
						"ProcurementTemplateNotValidForThisRequestor"
						
			Action Rules
				include CheckUseAsDefault
						
		Update is an Update Action
			Action Rules
				include CheckUseAsDefault
		
		Delete is a Delete Action
	
