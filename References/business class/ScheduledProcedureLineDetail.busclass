ScheduledProcedureLineDetail is a BusinessClass
    owned by ic
    prefix is SPLD

    Ontology
        symbolic key is ScheduledProcedureLineDetail

    Patterns
        implements StaticJava

    Persistent Fields
        FromCompanyParLocationBin
        Priority  				            is Numeric size 28
        PickedQuantity                      is like UnsignedQuantity
            precision is DerivedNumberOfDecimalsQuantity
        PickFromLocationGroup               is Boolean
        UniqueParLocation                   is Boolean

    Context Fields

    Transient Fields
    
    Local Fields

    Sets
        ByParLocation
            Sort Order
                Company
                ScheduledProcedure
                FromCompanyParLocationBin.FromCompany
                FromCompanyParLocationBin.ParLocation
                ScheduledProcedureLine
                ScheduledProcedureLineDetail

        ByParLocationAndShelf
            Sort Order
                Company
                ScheduledProcedure
                FromCompanyParLocationBin.FromCompany
                FromCompanyParLocationBin.ParLocation
                FromCompanyParLocationBin.Shelf
                ScheduledProcedureLine
                ScheduledProcedureLineDetail

    Field Rules
        FromCompanyParLocationBin
			initial value is ScheduledProcedureLine.FromCompanyParLocationBin.FromCompany
            default FromCompanyParLocationBin.Shelf to ParItemLocationRel.PreferredBin
            if (FromCompanyParLocationBin.ParLocation changed)
                FromCompanyParLocationBin.Shelf = ParItemLocationRel.PreferredBin

        PickedQuantity
            constraint (IsItemActive)
                "CannotEnterPickedQuantity;ItemIsInactive"
            constraint (IsParLocationActive)
                "CannotEnterPickedQuantity;ParLocationIsInactive"
            constraint (IsParItemLocation)
                "CannotEnterPickedQuantity;NotAParItemInThisLocation"
            constraint (IsParItemLocationActive)
                "CannotEnterPickedQuantity;ParItemIsInactive"
            constraint (not IsExcessPick)
                "CannotPickQuantityMoreThanTheParLevel"

    Derived Fields
        DerivedItem is a DerivedField
            type is MessageField
            if (instance count of ScheduledProcedureLineDetailsRel > 1)
                return ScheduledProcedureLine.Item + " *"
            else
                return ScheduledProcedureLine.Item

        DerivedManufacturerNumber is a DerivedField
            type is like ManufacturerNumber
            return ScheduledProcedureLine.Item.ManufacturerNumber

		DerivedNumberOfDecimalsQuantity is a ConditionalField
			type is Numeric size 1
			if (ScheduledProcedureLine.Item exists)
				ScheduledProcedureLine.Item.NumberOfDecimalsQuantity
			else
			if (InventoryCompanyRel exists)
				InventoryCompanyRel.NumberOfDecimalsQuantity
			else
				4

        DerivedParLevel is a DerivedField
            type is like Quantity
                precision is DerivedNumberOfDecimalsQuantity
            if (ParItemLocationRel exists)
                return ParItemLocationRel.ReorderPoint

        DerivedParLocationErrorMessage is a DerivedField
            type is MessageField
            if (not IsItemActive)
                return MessageItemInactive
            else
            if (not IsParLocationActive)
                return MessageParLocationInactive
            else
            if (not IsParItemLocation)
                return MessageNotParItemInLocation
            else 
            if (not IsParItemLocationActive)    
                return MessageParItemInactive
            else 
                return MessageExcessPick

        DerivedParLocationName      is a DerivedField
            type is MessageField
            return DerivedFromCompany + " - " + FromCompanyParLocationBin.ParLocation.RepresentativeText

        DerivedFromCompany    is a DerivedField
            type is Alpha 10
            return FromCompanyParLocationBin.FromCompany       

        DerivedPickedQuantity is a DerivedField
            type is like Quantity
                precision is DerivedNumberOfDecimalsQuantity
            if (PickedQuantity entered)
                return PickedQuantity

        DerivedPickedQuantityDisplay is a DerivedField
            type is Alpha 15
            if (PickedQuantity entered)
                return PickedQuantity
            else
                return "  _____"

        DerivedUOM is a DerivedField
            type is Alpha 5
            return " " + ScheduledProcedureLine.ComponentUOM + "  "

		MessageItemInactive         is a MessageField
			"ItemIsInactive"

		MessageParLocationInactive  is a MessageField
			"ParLocationIsInactive"

		MessageNotParItemInLocation is a MessageField
			"NotAParItemInThisLocation"

		MessageParItemInactive      is a MessageField
			"ParItemIsInactive"

		MessageExcessPick           is a MessageField
			"PickedQuantityIsMoreThanParLevel"

    Conditions
        HasParLocationError
            restricted
            when (IsPicked
            and   not HasValidParLocation)

        HasValidParLocation
            restricted
            when  (IsItemActive
            and    IsParLocationActive
            and    IsParItemLocation
            and    IsParItemLocationActive
            and    not IsExcessPick)

        IsItemActive
            restricted
            when (ScheduledProcedureLine.Item.Active)

        IsParLocationActive
            restricted
            when (FromCompanyParLocationBin.ParLocation.IsActive)

        IsParItemLocation
            restricted
            when (ParItemLocationRel exists)

        IsParItemLocationActive
            restricted
            when (ParItemLocationRel.Active)

        IsPicked
            restricted
            when (PickedQuantity entered)

        IsOpen
            restricted
            when (ScheduledProcedureLine.IsOpen)

        IsExcessPick
            restricted
            when (PickedQuantity > DerivedParLevel)

        ValidForCreate
            restricted
            when (IsOpen
            and ScheduledProcedureLine.IsForParLocationGroup)

        ValidForDelete
            restricted
            when (IsOpen
            and ScheduledProcedureLine.IsForParLocationGroup
            and not PickFromLocationGroup)

    Relations
		InventoryCompanyRel
			one-to-one relation to InventoryCompany
			Field Mapping uses symbolic key
				related.Company                         = Company

		InventoryLocationRel
			one-to-many relation to InventoryLocation
			Field Mapping uses symbolic key
				related.Company			  			    = ScheduledProcedureLine.FromCompanyParLocationBin.FromCompany

        ParItemLocationRel
            one-to-many relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company                         = FromCompanyParLocationBin.FromCompany
                related.InventoryLocation               = FromCompanyParLocationBin.ParLocation
                related.Item                            = ScheduledProcedureLine.Item

        ScheduledProcedureLineDetailForParLocationRel
            one-to-many relation to ScheduledProcedureLineDetail
            Field Mapping uses ByParLocation
                related.Company                                 = Company
                related.ScheduledProcedure                      = ScheduledProcedure
                related.FromCompanyParLocationBin.FromCompany   = FromCompanyParLocationBin.FromCompany 
                related.FromCompanyParLocationBin.ParLocation   = FromCompanyParLocationBin.ParLocation
            Instance Selection
                where (related.HasValidParLocation)

        ScheduledProcedureLineDetailsRel
            one-to-many relation to ScheduledProcedureLineDetail
            Field Mapping uses symbolic key
                related.Company                                 = Company
                related.ScheduledProcedure                      = ScheduledProcedure
                related.ScheduledProcedureLine                  = ScheduledProcedureLine
            Instance Selection
                where (related.HasValidParLocation)

        UniqueScheduledProcedureLineDetailForParLocationRel
            one-to-many relation to ScheduledProcedureLineDetail
            Field Mapping uses ByParLocation
                related.Company                                 = Company
                related.ScheduledProcedure                      = ScheduledProcedure
                related.FromCompanyParLocationBin.FromCompany   = FromCompanyParLocationBin.FromCompany 
                related.FromCompanyParLocationBin.ParLocation   = FromCompanyParLocationBin.ParLocation
            Instance Selection
                where (related.UniqueParLocation)

	Actions
		Create is a Create Action
			restricted
            Action Rules
            Exit Rules
                if (not HasValidParLocation
                and UniqueParLocation)
                    initialize UniqueParLocation

		CreateNonGroupLocation is a Create Action
            valid when (ValidForCreate)
            default label is "Create"
            Entrance Rules
                FromCompanyParLocationBin.FromCompany   = ScheduledProcedureLine.FromCompanyParLocationBin.FromCompany
                constraint (HasValidParLocation)
                    "<DerivedParLocationErrorMessage>"
                constraint (ScheduledProcedureLineDetailForParLocationRel not exists)
                    "ParLocationAlreadyExists"
                FromCompanyParLocationBin.Shelf         = ParItemLocationRel.PreferredBin
            Action Rules
                if (UniqueScheduledProcedureLineDetailForParLocationRel not exists)
                    UniqueParLocation = true
            Exit Rules
                invoke UpdateQuantities ScheduledProcedureLine
                    invoked.PrmPickedQuantity = PickedQuantity 

		FastUpdate is an Update Action
			restricted
			bypass field rules 

        Update is an Update Action
            valid when (IsOpen)
            Action Rules
                if (PickedQuantity changed)          
                    invoke UpdateQuantities ScheduledProcedureLine
                        invoked.PrmPickedQuantity = PickedQuantity - old PickedQuantity

        Delete is a Delete Action
            valid when (ValidForDelete)
            Action Rules
                if (initiating action not = "ScheduledProcedureLine.Update")
                    invoke UpdateQuantities ScheduledProcedureLine
                        invoked.PrmPickedQuantity = PickedQuantity * -1         
