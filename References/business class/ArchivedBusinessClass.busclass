ArchivedBusinessClass is a BusinessClass
    owned by la
    prefix is ABCL
    
    Ontology
    	symbolic key is ArchivedBusinessClass
    
    Patterns
    	disable AuditIndex
    	disable EffectiveDated
		implements CreateStamp
		implements UpdateStamp

    Persistent Fields
		PurgedCount					is Numeric size 12                 
			disable Auditing 
		PurgedCheckSum 				is a PurgeCheckSumResultsArray     
			default label is "PurgedCountCheckSum"
			disable Auditing 
		ActualInArchiveCount			is Numeric size 12   			
			disable Auditing 
		ActualInArchiveCheckSum 		is a PurgeCheckSumResultsArray 	
			default label is "ActualInArchiveCountCheckSum"
			disable Auditing 
		RestoredFromArchive 		is Numeric size 12 					
			disable Auditing 
		RestoredCheckSum 			is a PurgeCheckSumResultsArray 		
			default label is "RestoredFromArchiveCheckSum"
			disable Auditing 
		UnsentRestoredFromArchive 		is Numeric size 12 			    
			disable Auditing 
		UnsentRestoredCheckSum 			is a PurgeCheckSumResultsArray 	
			default label is "RestoredFromArchiveCheckSum"
			disable Auditing 
		CreatedInArchive 			is Numeric size 12            		
			disable Auditing 
		CreatedCheckSum 			is a PurgeCheckSumResultsArray 		
			default label is "CreatedInArchiveCheckSum"	
			disable Auditing 
		UpdatedInArchive 				is Numeric size 12   			
			disable Auditing 
		UpdatedCheckSum 				is a PurgeCheckSumResultsArray 	
			default label is "UpdatedInArchiveCheckSum"	
			disable Auditing 
		PurgedFromArchive 			is Numeric size 12 					
			disable Auditing 
		PurgedFromArchiveCheckSum 	is a PurgeCheckSumResultsArray 		
			disable Auditing 
		LastRestoredStamp			is TimeStamp  						

	Derived Fields
   		CurrentInArchiveCount is a NativeField
			type is Numeric size 12 

			
		ArchiveCountDiscrepancy is a DerivedField
			type is Numeric size 12 
			return (ActualInArchiveCount - CurrentInArchiveCount)
			
	Conditions
		ActualInArchiveCountConsistent
			default label is untranslatable
			when (ActualInArchiveCount = (CreatedInArchive - (RestoredFromArchive - UnsentRestoredFromArchive) - PurgedFromArchive)) 
			




		CurrentInArchiveCountConsistent
			default label is untranslatable
			when (CurrentInArchiveCount = ActualInArchiveCount)

		UnsentArchiveDataExists
			default label is untranslatable
			when (UnsentArchivedDataRel exists)
			
		IsPurgable
			default label is untranslatable
			when (ArchivedBusinessClass.ImplementsArchivable and ArchivedBusinessClass.ArchivingEnabledForBusinessClass)
		
		ShowRestored
			default label is untranslatable
			when (RestoredFromArchive entered or RestoredCheckSum entered)
		
		ShowUnsentRestored
			default label is untranslatable
			when (UnsentRestoredFromArchive entered or UnsentRestoredCheckSum entered)
			
		CheckSumExists
			default label is untranslatable
			when (ArchivedBusinessClassCheckSum set exists)
			
		AnyInArchiveValues
			default label is untranslatable
			when (ActualInArchiveCount entered or ActualInArchiveCheckSum entered or RestoredFromArchive entered or RestoredCheckSum entered 
			or   CreatedInArchive entered or CreatedCheckSum entered or UpdatedInArchive entered or UpdatedCheckSum entered 
			or   PurgedFromArchive entered or PurgedFromArchiveCheckSum entered)
		
		ResultsExist
			default label is untranslatable
			when (ArchivedBusinessClassValidationResult set exists)
			
	Local Fields
		I is Numeric 2
		J is Numeric 2
		AddCheckSum is Boolean
		
	Rule Blocks
		SetCheckSum

			I = 1
			while I <= sizeofarray {ParamCheckSumField} and I <= sizeofarray {CheckSumField}
				if (not {ParamCheckSumField}.CheckSumField[I].Name entered)
					end while
				
				J = 1
				
				while J <= sizeofarray {CheckSumField} 
					if ({CheckSumField}.CheckSumField[J].Name = blank or {CheckSumField}.CheckSumField[J].Name = {ParamCheckSumField}.CheckSumField[I].Name)
						{CheckSumField}.CheckSumField[J].Name = {ParamCheckSumField}.CheckSumField[I].Name
							
							if (AddCheckSum)
								{CheckSumField}.CheckSumField[J].CheckSumValue += {ParamCheckSumField}.CheckSumField[I].CheckSumValue
							else
								{CheckSumField}.CheckSumField[J].CheckSumValue -= {ParamCheckSumField}.CheckSumField[I].CheckSumValue
								
						end while
					J += 1
	
				I += 1
		
	Actions
		Create is a Create Action
					
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
			
		UpdatePurgeValues is an Instance Action
			restricted
			default label is untranslatable
			

			Parameters
				ParamPurgedCount				is Numeric size 12
					default label is "PurgedCount"
				ParamPurgedCheckSum 			is a PurgeCheckSumResultsArray
					default label is "PurgedCheckSum"
					
			Action Rules
				PurgedCount += ParamPurgedCount
				AddCheckSum = true
				
				include SetCheckSum
					replace ParamCheckSumField with ParamPurgedCheckSum
					replace CheckSumField with PurgedCheckSum
			
		UpdateArchivedValues is an Instance Action
			restricted
			default label is untranslatable
			refresh and lock this instance
			
			Parameters
				ParamCreatedInArchive 		is Numeric size 12
					default label is "CreatedInArchive "
				ParamCreatedCheckSum 		is a PurgeCheckSumResultsArray
					default label is "CreatedInArchiveCheckSum"	
				ParamUpdatedInArchive 		is Numeric size 12 
					default label is "UpdatedInArchive"
				ParamUpdatedCheckSum 		is a PurgeCheckSumResultsArray
					default label is "SkippedArchiveCheckSum"	
					
			Action Rules
				ActualInArchiveCount += ParamCreatedInArchive 
				CreatedInArchive += ParamCreatedInArchive
				UpdatedInArchive += ParamUpdatedInArchive
				AddCheckSum = true
				
				include SetCheckSum
					replace ParamCheckSumField with ParamCreatedCheckSum
					replace CheckSumField with CreatedCheckSum
					
				include SetCheckSum
					replace ParamCheckSumField with ParamUpdatedCheckSum
					replace CheckSumField with UpdatedCheckSum
					

				include SetCheckSum
					replace ParamCheckSumField with ParamCreatedCheckSum
					replace CheckSumField with ActualInArchiveCheckSum
					
				include SetCheckSum
					replace ParamCheckSumField with ParamUpdatedCheckSum
					replace CheckSumField with ActualInArchiveCheckSum
			
		ClearCheckSumValues is an Instance Action
			restricted
			Parameters
				ParamName is a ArchivedBusinessClassCheckSum
				
			Action Rules
				for each PurgedCheckSum.CheckSumField
					if (each.Name = ParamName)
						initialize each
						end for each
				
				for each ActualInArchiveCheckSum.CheckSumField
					if (each.Name = ParamName)
						initialize each
						end for each
						
				for each RestoredCheckSum.CheckSumField
					if (each.Name = ParamName)
						initialize each
						end for each
						
				for each CreatedCheckSum.CheckSumField
					if (each.Name = ParamName)
						initialize each
						end for each
				
				for each UpdatedCheckSum.CheckSumField
					if (each.Name = ParamName)
						initialize each
						end for each
							
				for each PurgedFromArchiveCheckSum.CheckSumField
					if (each.Name = ParamName)
						initialize each
						end for each	
						
		RecomputeFromArchived is an Instance Action
			valid when (IsPurgable)
			confirmation required 
				"AreYouSureYouWantToRecomputeThisCheckSumValue?ThisShouldNotBeRunWhileActivelyPurging."
			run in background
			
			Parameters
				ParamRecomputeAll is Boolean
					default label is "RecomputeAll"
				ParamCheckSum is an ArchivedBusinessClassCheckSum
					default label is "CheckSumName"
					
			Parameter Rules
				ParamRecomputeAll
					initial value is true
					
					if (ParamRecomputeAll)
						initialize ParamCheckSum
				ParamCheckSum
					if (not ParamRecomputeAll)
						required
							"MustSelectACheckSumOrChooseRecomputeAll"
							
			Action Rules
				if (ParamRecomputeAll)
					if (ArchivedBusinessClassCheckSum set exists)
						invoke RecomputeAllFromArchived ArchivedBusinessClassCheckSum in foreground 
							invoked.ParamArchivedBusinessClass = ArchivedBusinessClass
					else
						invoke RecomputeCountFromArchived
				else
					invoke RecomputeFromArchived ParamCheckSum in foreground
					
		RecomputeCountFromArchived is an Instance Action
			restricted
			valid when (IsPurgable)
			default label is untranslatable

			
		ValidateFromArchived is an Instance Action
			valid when (IsPurgable)
			default label is "ValidateArchiveCounts"
			confirmation required 
				"AreYouSureYouWantToValidate?ThisShouldNotBeRunWhileActivelyPurging."
			run in background
			
			Parameters
				ParamValidateAll is Boolean
					default label is "ValidateAll"
				ParamCheckSum is an ArchivedBusinessClassCheckSum
					default label is "CheckSumName"
				ParamSendNotification is Boolean
					default label is "SendNotification"
					
			Parameter Rules
				ParamValidateAll
					initial value is true
					
					if (ParamValidateAll)
						initialize ParamCheckSum
				ParamCheckSum
					if (not ParamValidateAll)
						required
							"MustSelectACheckSumOrChooseValidateAll"
			Local Fields
				Result is a ArchivedBusinessClassValidationResult
							
			Action Rules
				invoke Create ArchivedBusinessClassValidationResult
					assign result to Result
					invoked.ArchivedBusinessClass = ArchivedBusinessClass
					invoked.ArchivedBusinessClassValidationResult = system current timestamp
					invoked.ExpectedInArchiveCount = ActualInArchiveCount
					
					if (ParamValidateAll)
						invoked.ExpectedInArchiveCheckSum = ActualInArchiveCheckSum
					else
						I = 1
						while I <= sizeofarray ActualInArchiveCheckSum
							if (ActualInArchiveCheckSum.CheckSumField[I].Name = ParamCheckSum)
								invoked.ExpectedInArchiveCheckSum.CheckSumField[I] = ActualInArchiveCheckSum.CheckSumField[I]
								end while
							I += 1
					
				if (ParamValidateAll)
					if (ArchivedBusinessClassCheckSum set exists)
						invoke ValidateAllArchivedDataValues ArchivedBusinessClassCheckSum in foreground 
							invoked.ParamArchivedBusinessClass = ArchivedBusinessClass
							invoked.ParamResult = Result
					else
						invoke ValidateCountFromArchived
							invoked.ParamResult = Result
				else
					invoke ValidateArchivedDataValues ParamCheckSum in foreground
						invoked.ParamResult = Result
					
				if (ParamSendNotification)
					invoke SendNotification Result
						if (not ParamValidateAll)
							invoked.ParamCheckSumName = ParamCheckSum
					

		ValidateCountFromArchived is an Instance Action
			restricted
			valid when (IsPurgable)
			default label is untranslatable
			
			Parameters
				ParamResult is a ArchivedBusinessClassValidationResult
					default label is "ValidationResult"
				
			Parameter Rules
				ParamResult
					required
			
			Action Rules
				if (CurrentInArchiveCountConsistent)
					invoke Update ParamResult
						invoked.ValidatedInArchiveCount = CurrentInArchiveCount
					
			
		RestoredFromCommand is an Instance Action
			valid when (IsPurgable)
			restricted
			Parameters
				ParamLastRestoredStamp is TimeStamp
					default label is untranslatable:"<LastRestoredStamp label>"
					
			Parameter Rules
				ParamLastRestoredStamp
					required
			
			Action Rules
				log "<ArchivedBusinessClass>RestoredFromCommandAt<ParamLastRestoredStamp>"
				
				LastRestoredStamp = ParamLastRestoredStamp
				initialize PurgedCount
				initialize PurgedCheckSum 
				initialize ActualInArchiveCount
				initialize ActualInArchiveCheckSum
				initialize RestoredFromArchive
				initialize RestoredCheckSum
				initialize CreatedInArchive
				initialize CreatedCheckSum
				initialize UpdatedInArchive
				initialize UpdatedCheckSum 
				initialize PurgedFromArchive
				initialize PurgedFromArchiveCheckSum
				
				invoke RestoredFromCommandReset ArchivedData in background
					invoked.ParamBusinessClass = ArchivedBusinessClass
					invoked.ParamLastRestoredStamp = ParamLastRestoredStamp
					
		ProcessedCommand is an Import Action 
			restricted
			default label is untranslatable
			
			Parameters
				ParamRecomputeAll is Boolean
					default label is "RecomputeAll"
				ParamLastRestoredStamp is TimeStamp
					default label is untranslatable:"<LastRestoredStamp label>"
					
			Local Fields
				LocArchivedBusinessClass is an ArchivedBusinessClass 
			
			Entrance Rules
				LocArchivedBusinessClass = ArchivedBusinessClass 
				
				if (IsPurgable)
					constraint (LocArchivedBusinessClass entered)
						"ArchivedBusinessClassRequired"
					
					constraint (ParamRecomputeAll entered or ParamLastRestoredStamp entered)
						"AtLeastOneParameterMustBeSpecified"
					
			Action Rules
				if (IsPurgable)
					if (not LocArchivedBusinessClass exists)
						invoke Create ArchivedBusinessClass
							invoked.ArchivedBusinessClass = LocArchivedBusinessClass
							
					if (ParamLastRestoredStamp entered)
						invoke RestoredFromCommand LocArchivedBusinessClass in background
							invoked.ParamLastRestoredStamp = ParamLastRestoredStamp
					else
						invoke RecomputeFromArchived LocArchivedBusinessClass in background
							invoked.ParamRecomputeAll = ParamRecomputeAll
							invoked.ParamCheckSum = blank
							
		UpdateRestoredValues is an Instance Action
			restricted
			default label is untranslatable
			

			Parameters
				ParamRestoredFromArchive		is Numeric size 12
					default label is "RestoredFromArchive"
				ParamRestoredCheckSum 			is a PurgeCheckSumResultsArray
					default label is "CheckSum"
				ParamLastRestoredStamp			is TimeStamp
					default label is "LastRestoredStamp"
				ParamDeletedFromArchive 		is Numeric size 12
					default label is "DeletedFromArchive"
				ParamDeletedCheckSum 			is a PurgeCheckSumResultsArray
					default label is "CheckSum"
				ParamSentToArchive			is Boolean
					default label is "SentToArchive"
					
			Action Rules
				RestoredFromArchive += ParamRestoredFromArchive
				
				if (ParamSentToArchive)
					ActualInArchiveCount -= ParamDeletedFromArchive
				else
					UnsentRestoredFromArchive += ParamRestoredFromArchive
					
				LastRestoredStamp = ParamLastRestoredStamp
				AddCheckSum = true
				
				include SetCheckSum
					replace ParamCheckSumField with ParamRestoredCheckSum
					replace CheckSumField with RestoredCheckSum
					
				if (ParamSentToArchive)
					AddCheckSum = false
					include SetCheckSum
						replace ParamCheckSumField with ParamDeletedCheckSum
						replace CheckSumField with ActualInArchiveCheckSum
				else
					include SetCheckSum
						replace ParamCheckSumField with ParamRestoredCheckSum
						replace CheckSumField with UnsentRestoredCheckSum
						
	Relations
		UnsentArchivedDataRel
			one-to-many relation to ArchivedData
			Field Mapping uses symbolic key
				related.ArchivedData.PurgedBusinessClass = ArchivedBusinessClass
				related.ArchivedData.Sequence > 0
			Instance Selection
				where (related.SentToArchive != 1) 
