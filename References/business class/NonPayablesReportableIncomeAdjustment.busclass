NonPayablesReportableIncomeAdjustment is a BusinessClass
    owned by ap
    prefix is RIC
    sql name is NPReportableIncomeAdjustment
    classic name is APRPTBLINC

    Ontology
        symbolic key is NonPayablesReportableIncomeAdjustment
            classic set name is RICSET1
            classic name for NonPayablesReportableIncomeAdjustment.TransactionSource is SOURCE
            classic name for NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID is RIC-OBJ-ID

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        IncomeCode          is a PayablesIncomeCode
        TaxID
        PaymentDate         is Date
        PaidAmount          is an InternationalAmount
            classic name is PAID-AMT
        ReportableAmount    is an InternationalAmount
            classic name is REPORTABLE-AMT
        TransactionCurrency is a Currency
            classic name is TRAN-CURRENCY
        TransactionNumber
            classic name is TRANS-NBR
        Company             is a PayablesCompany
			disable surrogates
        ProcessLevel        is a PayablesProcessLevel
            classic name is PROC-LEVEL
        Vendor
        PayeeName           is a VendorName 
        	holds pii
        PostalAddress		is a PostalAddressV2	
        	holds pii
			classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
			classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
			classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
			classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
			classic name for PostalAddress.Municipality is CITY-ADDR5
			classic name for PostalAddress.StateProvince is STATE-PROV
        CashCode
        BankTransactionCode
            classic name is BANK-INST-CODE
        TransactionIDNumber
            classic name is TRANS-IDENT
        TaxIDType
            classic name is TIN-TYPE
		WithholdingAmount	is an InternationalAmount	
		IssuerAccountNumber is Alpha 20

	Context Fields
		PaymentDateRange		is a DateRange4		
		ReportableAmountRange	is an AmountRange		
		ReportableIncomeGroup   is a PayablesReportableIncomeGroup		






	Derived Fields
		DefaultProcessLevel		is a DerivedField
			type is like PayablesProcessLevel 
			restricted
			if (DefaultProcessLevelRel exists)
				return DefaultProcessLevelRel.PayablesProcessLevel

	Field Rules					           
		PaymentDateRange
			required
			if (PaymentDateRange entered)
				constraint (PaymentDateRange.Begin <= PaymentDateRange.End)
					"From_\DateCannotBeGreaterThan_\To_\Date"	
				constraint (PaymentDateRange.End year = PaymentDateRange.Begin year)
					"FromAnd_\ToDatesMustBeWithinTheSameYear"		
		
		PaymentDate
			if (PaymentDate changed)
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotChangePaymentDateFor_\S\A\SOr_\C\BTransactions"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotChangePaymentDateFor_\S\A\SOr_\C\BTransactions"		
				constraint (PaymentDate not changed)
					"CannotChange_\Payment_\Date;DeleteAndRe-addRecord."	
		
		Company
			required
			if (Company changed)
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotChangeCompanyFor_\S\A\SOr_\C\BTransactions"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotChangeCompanyFor_\S\A\SOr_\C\BTransactions"		
					
		VendorGroup
			if (Company entered)
				default to Company.VendorGroup
		
		Vendor
			if (Vendor entered)
				if (PayeeName not entered)
					PayeeName = Vendor.LegalName
			if (Vendor changed)
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotChangeVendorFor_\S\A\SOr_\C\BTransactions"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotChangeVendorFor_\S\A\SOr_\C\BTransactions"		

		TransactionNumber
			if (TransactionNumber changed)
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotChangePaymentNumberFor_\S\A\SOr_\C\BTransactions"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotChangePaymentNumberFor_\S\A\SOr_\C\BTransactions"		

		TransactionCurrency
			required
			if (TransactionCurrency changed)
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotChangeCurrencyFor_\S\A\SOr_\C\BTransactions"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotChangeCurrencyFor_\S\A\SOr_\C\BTransactions"		
				
		PaidAmount
			required
			if (PaidAmount changed)
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotChangePaidAmountFor_\S\A\SOr_\C\BTransactions"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotChangePaidAmountFor_\S\A\SOr_\C\BTransactions"		

		WithholdingAmount	
			constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
				"WithholdingAmountNotAllowedFor_\C\BTransactions"

		PayeeName
			if (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
				if (Vendor not entered)
					required
						"PayeeNameRequiredWhenVendorNotEntered" 		
				
		PostalAddress
			if (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
				if (Vendor not entered)
					required
						"PostalAddressRequiredWhenVendorNotEntered"		
				else
					cannot be entered
						"AddressNotAllowedIf_\A\P_\VendorEntered"  		

		ProcessLevel
			required
			default to DefaultProcessLevel
		
		ReportableAmount
			constraint (ReportableAmount <= PaidAmount)
				"ReportableAmountCannotBeGreaterThanPaymentAmount" 		
			
		IncomeCode
			if (ReportableAmount entered)
				required
					"ReportableAmountMustBe_0WhenIncomeCodeNotEntered"   
			
			if (IncomeCode 				entered
			and ReportableAmount 	not entered)
				ReportableAmount = PaidAmount
					
		TaxID
			if (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
				constraint (Vendor not entered)
					"Tax_\IdNotAllowedIf_\A\P_\VendorEntered"      
			
			if (Vendor 		not entered
			and IncomeCode 		entered)
				constraint (TaxID entered)
					"@eAP45.230"			











						















		IssuerAccountNumber	
			constraint (Vendor not entered)
					"Issuer_\Account_\NumberNotAllowedIf_\VendorEntered"


    Relations
		DefaultProcessLevelRel
			one-to-many relation to PayablesProcessLevel
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.DefaultProcessLevel = true)



		NonPayablesReportableIncomeAdjustmentRel
			one-to-many relation to NonPayablesReportableIncomeAdjustment
			Field Mapping uses symbolic key
				related.VendorGroup = VendorGroup
			Instance Selection
				where  (related.UniqueID not = UniqueID
				and		related.NonPayablesReportableIncomeAdjustment.TransactionSource 		= NonPayablesReportableIncomeAdjustment.TransactionSource
				and 	related.NonPayablesReportableIncomeAdjustment.YearPaid 					= NonPayablesReportableIncomeAdjustment.YearPaid)

        CashLedgerPaymentRel
            classic name is CBPAYMENT
            one-to-one relation to CashLedgerPayment
            required
            Field Mapping uses symbolic key
            	related.CashManagementGroup 				  = CashCode.CashManagementGroup 
                related.CashCode                 			  = CashCode
                related.CashLedgerPayment.BankTransactionCode = BankTransactionCode
                related.CashLedgerPayment.TransactionIDNumber = TransactionIDNumber

		ReportableIncomeEntitiesRel				
			one-to-many relation to PayablesReportableIncomeEntity
            Field Mapping uses symbolic key
                related.VendorGroup                     = VendorGroup
                related.PayablesReportableIncomeGroup   = ReportableIncomeGroup
                related.Company                         = Company
            Instance Selection
                where   (related.ProcessLevel       !entered
                or      (related.ProcessLevel       entered
                and     related.ProcessLevel        = ProcessLevel))

		AllReportableIncomeEntitiesRel				
			one-to-many relation to PayablesReportableIncomeEntity
			Field Mapping uses symbolic key
				related.VendorGroup						= ReportableIncomeGroup.VendorGroup
				related.PayablesReportableIncomeGroup	= ReportableIncomeGroup

	Conditions
		WithinPaymentDateRange	
			restricted
			when (PaymentDateRange not entered
			or   (PaymentDateRange entered
			and   PaymentDate within PaymentDateRange))

		WithinReportableAmountRange				
			restricted
			when (ReportableAmountRange not entered
			or   (ReportableAmountRange entered
			and   ReportableAmount within ReportableAmountRange))

		IncludedInReportableIncomeGroup     
            restricted
            when (Company.VendorGroup = ReportableIncomeGroup.VendorGroup
			and		(AllReportableIncomeEntitiesRel !exist
			or		 ReportableIncomeEntitiesRel exists))

    Sets

        Set2
            indexed
            Sort Order
            	VendorGroup  
                Company
                PaymentDate
                ProcessLevel
                Vendor
                PayeeName
                TaxID
                NonPayablesReportableIncomeAdjustment

        ByCompanyProcessLevel
            indexed
            Sort Order
            	VendorGroup  
                Company
                ProcessLevel
                PaymentDate
                Vendor
                PayeeName
                TaxID
                NonPayablesReportableIncomeAdjustment


        Set3
            indexed
            Sort Order
                VendorGroup
                Vendor
                PaymentDate
                PayeeName
                IncomeCode
                NonPayablesReportableIncomeAdjustment



















	Actions
		Create is a Create Action
			Entrance Rules
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotAddRecordsForSourceOfSAS"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotAddRecordsForSourceOfCB"			
				if (NonPayablesReportableIncomeAdjustment.TransactionSource not entered)
					NonPayablesReportableIncomeAdjustment.TransactionSource = "MAN"
				if (PaymentDate entered)
					NonPayablesReportableIncomeAdjustment.YearPaid = PaymentDate year
				else
					NonPayablesReportableIncomeAdjustment.YearPaid = 2999	
					
				VendorGroup = Company.VendorGroup
				if (NonPayablesReportableIncomeAdjustmentRel exists)
					NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID = last NonPayablesReportableIncomeAdjustmentRel.NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID + 1
				else
					NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID = 1

		Update is an Update Action
			Entrance Rules












				if  (NonPayablesReportableIncomeAdjustment.TransactionSource = "CB"		
				and (IncomeCode changed
				or   ReportableAmount changed
				or   TaxID changed
				or   TaxIDType changed
				or   ProcessLevel changed))
					invoke AdjustReportableIncome CashLedgerPaymentRel
						invoked.PrmIncomeCode			= IncomeCode
						invoked.PrmReportableAmount		= ReportableAmount
						invoked.PrmTaxID				= TaxID
						invoked.PrmTaxIDType			= TaxIDType
						invoked.PrmProcessLevel			= ProcessLevel
						invoked.PrmFromNonPayAdustment	= true

		Delete is a Delete Action
			Entrance Rules
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "SAS")
					"CannotDeleteRecordsForSourceOfSAS"		
				constraint (!NonPayablesReportableIncomeAdjustment.TransactionSource = "CB")
					"CannotDeleteRecordsForSourceOfCB"			


		CreateCashLedgerPaymentAdjustment is a Create Action
			restricted
			Action Rules
				VendorGroup = Company.VendorGroup
				if (NonPayablesReportableIncomeAdjustmentRel exists)
					NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID = last NonPayablesReportableIncomeAdjustmentRel.NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID + 1
				else
					NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID = 1
							
		UpdateCashLedgerPaymentAdjustment is an Update Action
			restricted
			
		DeleteCashLedgerPaymentAdjustment is a Delete Action
			restricted

		CreateStudentActivityBankTransactionAdjustment is a Create Action
			restricted
			Action Rules
				if (NonPayablesReportableIncomeAdjustmentRel exists)
					NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID = last NonPayablesReportableIncomeAdjustmentRel.NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID + 1
				else
					NonPayablesReportableIncomeAdjustment.ReportableIncomeObjectID = 1
							
		UpdateStudentActivityBankTransactionAdjustment is an Update Action
			restricted
			
		DeleteStudentActivityBankTransactionAdjustment is a Delete Action
			restricted
			
		Purge is a Purge Action
			restricted
