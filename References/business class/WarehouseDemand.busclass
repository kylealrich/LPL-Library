WarehouseDemand is a BusinessClass
    owned by wh
    prefix is WDH
    classic name is WHDEMNDHDR

    Ontology
        symbolic key is WarehouseDemand
            classic set name is WDHSET1
            classic name for WarehouseDemand.DemandSystemCode is SYSTEM-CD
            classic name for WarehouseDemand.DemandDocument is DOC-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        DocumentNumberNumeric
            classic name is DOC-NBR-NUM
        RequireCompleteShipment       is Boolean
            classic name is SHIP-COMP-FL
        NotReadyForShipmentCompletion is Boolean
            classic name is SHIP-COMP-FAIL
        OnHold                        is Boolean
            classic name is HOLD-FL
        WarehouseProcessType
            classic name is PROCESS-TYPE
        RequirePickingFeedback        is Boolean
            classic name is PICKING-REQ
        RequirePackingFeedback        is Boolean
            classic name is PACKING-REQ
        RequireFreight                is AlphaUpper size 1
            classic name is FREIGHT-REQ
            States
                NotAllowed value is "N"
                Optional   value is "O"
                Required   value is "R"
        FreightCode
        ShippingMethod
            classic name is METHOD
        TransactionId
            classic name is TRANS-ID
        Currency
            classic name is CURRENCY-CODE
        CurrNd                        is an AcctNd
        CashOnDelivery                is Boolean
            classic name is COD-FL
        Customer
        Destination
        DestinationName               is a VendorName 
        	holds pii
            classic name is DEST-NAME
        DestinationAddress            is a PostalAddressV2	
        	holds pii
            classic name for DestinationAddress.DeliveryAddress.AddressLine1 is DEST-ADDR1
            classic name for DestinationAddress.DeliveryAddress.AddressLine2 is DEST-ADDR2
            classic name for DestinationAddress.DeliveryAddress.AddressLine3 is DEST-ADDR3
            classic name for DestinationAddress.DeliveryAddress.AddressLine4 is DEST-ADDR4
            classic name for DestinationAddress.Municipality is DEST-CITY
            classic name for DestinationAddress.StateProvince is DEST-STATE
            classic name for DestinationAddress.PostalCode is DEST-ZIP
            classic name for DestinationAddress.County is DEST-COUNTY
            classic name for DestinationAddress.Country is DEST-CTRY-CD
            classic name for DestinationAddress.Region is DEST-REGION
        BillName                      is a VendorName 
        	holds pii
        BillingAddress                is a PostalAddressV2	
        	holds pii
            classic name for BillingAddress.DeliveryAddress.AddressLine1 is BILL-ADDR1
            classic name for BillingAddress.DeliveryAddress.AddressLine2 is BILL-ADDR2
            classic name for BillingAddress.DeliveryAddress.AddressLine3 is BILL-ADDR3
            classic name for BillingAddress.DeliveryAddress.AddressLine4 is BILL-ADDR4
            classic name for BillingAddress.Municipality is BILL-CITY
            classic name for BillingAddress.StateProvince is BILL-STATE
            classic name for BillingAddress.PostalCode is BILL-ZIP
            classic name for BillingAddress.County is BILL-COUNTY
            classic name for BillingAddress.Country is BILL-CTRY-CD
            classic name for BillingAddress.Region is BILL-REGION
        IntransitDocumentCounter      is Numeric size 3
            classic name is INTRAN-DOC-CTR
        DestinationECCountry          is Numeric size 3
            classic name is DEST-EC-CTRY
        DestinationISOCountry         is Numeric size 3
            classic name is DEST-ISO-CTRY
        BillECCountry
            classic name is BILL-EC-CTRY
        BillISOCountry
            classic name is BILL-ISO-CTRY

    Derived Fields

        WarehouseDemandDisplay is a LabelField
            "System<WarehouseDemand.DemandSystemCode>-DemandDocument<WarehouseDemand.DemandDocument>-DemandCompany<WarehouseDemand.DemandCompany>"

        RequestedShipDate is a ConditionalField
            type is Date
        	restricted
            classic name is RQST-SHIP-DT
            if (FromCustomerOrder)
                CustomerOrderRel.ExpectedShipDate
            else
            if (FromRequisition)
                RequistionRel.RequestedDeliveryDate
            else
                current corporate date
    
    Conditions





        FromCustomerOrder
        	restricted
            classic name is CUSTOMER-ORDER
            when (WarehouseDemand.DemandSystemCode.CustomerOrder)

        HasDocumentNumberNumeric
        	restricted
            classic name is DOC-NUM-ZERO
            when (DocumentNumberNumeric entered)

        FromRequisition
        	restricted
            classic name is REQUISITION
            when (WarehouseDemand.DemandSystemCode.Requisitions)

        FromReturn                                                                             
        	restricted
            classic name is RETURN
            when (WarehouseDemand.DemandSystemCode = "RT")

        ShipmentComplete
        	restricted
            classic name is SHIP-COMPLETE
            when (RequireCompleteShipment
            and   NotReadyForShipmentCompletion)

		IsAllocatableAndUnreleased
			when (all WarehouseDemandLinesRel.IsUnReleased)
		
		IsReleased
			when (all WarehouseDemandLinesRel.IsReleased)            
		
		BackorderedLinesExists
			restricted
			when (DemandLinesWithBackorderRel exists)
				
		LinesRequiringCompleteShipmentExists
			restricted
			when (DemandLinesRequiringCompleteShipmentRel exists)
		
		HasNoLines
			restricted
			when (WarehouseDemandLinesRel not exists)

    Relations

		CustomerOrderRel
            one-to-one relation to CustomerOrder
            valid when (FromCustomerOrder)
            Field Mapping uses symbolic key
                related.Company       = Company
                related.CustomerOrder = WarehouseDemand.DemandDocument

        RequistionRel
            one-to-one relation to Requisition
            valid when (FromRequisition)
            Field Mapping uses symbolic key
                related.Company     = WarehouseDemand.DemandCompany
                related.Requisition = WarehouseDemand.DemandDocument

		CustomerOrderReturnRel
            one-to-one relation to CustomerOrderReturn
            valid when (FromReturn)
            Field Mapping uses symbolic key
                related.Company             = Company
                related.CustomerOrderReturn = DocumentNumberNumeric

        WarehouseDemandLinesRel is a WarehouseDemandLine set
        
        DemandLinesWithBackorderRel is a WarehouseDemandLine set
        	Instance Selection
        		where (related.BackorderedQuantity entered)
		
		DemandLinesRequiringCompleteShipmentRel is a WarehouseDemandLine set
			Instance Selection
				where (related.RequireCompleteShipment)
		
        RequisitionFromWarehouseDemandRel      
        	one-to-one relation to Requisition
            Field Mapping uses symbolic key
                related.Company     = Company
                related.Requisition = WarehouseDemand.DemandDocument
		
		CancellableDemandLinesRel is a WarehouseDemandLine set
			Instance Selection
				where (related.IsAvailableToCancel)

		PostUpgradeWarehouseDemandLineDetailsRel
			one-to-many relation to WarehouseDemandLineDetail
			Field Mapping uses symbolic key 
				related.Company									= Company
				related.WarehouseDemand.DemandSystemCode 		= WarehouseDemand.DemandSystemCode
				related.WarehouseDemand.DemandDocument			= WarehouseDemand.DemandDocument
				related.WarehouseDemand.DemandCompany	 		= blank
                		           
		PostUpgradeWarehouseShipmentCommentOutRel
			one-to-many relation to WarehouseShipmentCommentOut
			Field Mapping uses symbolic key 
				related.Company									= Company
				related.WarehouseDemand.DemandSystemCode 		= WarehouseDemand.DemandSystemCode
				related.WarehouseDemand.DemandDocument			= WarehouseDemand.DemandDocument
				related.WarehouseDemand.DemandCompany	 		= blank
				

		PurgeWarehouseDemandLineRel is a WarehouseDemandLine set 
			Instance Selection
				include deleted records

    Sets

        Set3
            indexed
            Instance Selection
                where (HasDocumentNumberNumeric)
            Sort Order
                Company
                WarehouseDemand.DemandSystemCode
				WarehouseDemand.DemandCompany
                DocumentNumberNumeric
	
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action

        FastUpdate is an Update Action
			restricted
			bypass field rules
		
		UpdateShipmentCompletion is an Instance Action
			restricted
			Parameters
				PrmNotReadyForShipmentCompletion	is Boolean
			Action Rules
				NotReadyForShipmentCompletion = PrmNotReadyForShipmentCompletion
		
		Delete is a Delete Action
			Entrance Rules
				invoke Delete WarehouseDemandLinesRel

		PopulateWarehouseDemandCompany is a Set Action 
			restricted
			Instance Selection
				where (WarehouseDemand.DemandCompany entered)
			Action Rules
				Instance Rules
					for each PostUpgradeWarehouseDemandLineDetailsRel
						invoke QuickCreate each
							fill in fields from each
							invoked.WarehouseDemand.DemandCompany = WarehouseDemand.DemandCompany
						invoke QuickDelete each

					for each PostUpgradeWarehouseShipmentCommentOutRel
						invoke QuickCreate each
							fill in fields from each
							invoked.WarehouseDemand.DemandCompany = WarehouseDemand.DemandCompany
						invoke QuickDelete each

		Purge is a Purge Action
			restricted
            bypass relational integrity rules
			Entrance Rules
				invoke Purge PurgeWarehouseDemandLineRel

        PurgeOldDemandRecords is a Set Action
            restricted
			Parameters
				PrmCompany		  is a Company
					default label is "Company"
				PrmOffsetYear	  is Numeric 2
                    default label is "OffsetYear"
            Parameter Rules
				PrmOffsetYear
                    initial value is 2
					default to 2
					LocalOffsetDate	= current corporate date - PrmOffsetYear as years
			Local Fields
				LocalOffsetDate		is Date
			Instance Selection
				include deleted records
				where ((PrmCompany not entered or Company = PrmCompany)
				and    (create date <=LocalOffsetDate))
			Action Rules
				Instance Rules
					invoke Purge 
		
				
