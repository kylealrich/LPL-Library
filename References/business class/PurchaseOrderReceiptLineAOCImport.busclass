PurchaseOrderReceiptLineAOCImport is a BusinessClass
    owned by ma
    prefix is MRA
    sql name is POrderReceiptLineAOCImport
    classic name is MACRECAOC

    Ontology
        symbolic key is PurchaseOrderReceiptLineAOCImport
            classic set name is MRASET1
            classic name for PurchaseOrderReceiptLineAOCImport.LineNumber is LINE-NBR
            classic name for PurchaseOrderReceiptLineAOCImport.AddOnCharge is AOC-CODE
            classic name for PurchaseOrderReceiptImport is REFERENCE-NO

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields

    	RunGroup
        Vendor
        RecordType                    is a RecType
            classic name is REC-TYPE
		PurchaseOrder
        PORelease
        POCode
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
        Location                      is an InventoryLocation
        AddOnChargePercent
            classic name is AOC-RATE
        AddOnChargeEntryMethod
            classic name is ENTRY
        Quantity
        EnteredUnitCost               is an InternationalCost
            classic name is ENT-UNIT-CST
        TotalAddOnChargeAmount        is an InternationalAmount
            classic name is TOTAL-AOC
        TaxCode
        Taxable                       is Boolean
            classic name is TAXABLE-FLAG
        PrintOnPO                     is Boolean
            classic name is AOC-ON-PO
        Summarize                     is Boolean
            classic name is SUMMARY-FLAG
        ZeroCost                      is Boolean
            classic name is ZERO-COST-FLG
        Closed
            classic name is CLOSED-FL
		MatchDetailKey
        LandedAddOnCharge             is Boolean
            classic name is LANDED-FLAG
        FreightTerm
            classic name is FREIGHT-TERMS
        CrossReferenceVendor
            classic name is XREF-VENDOR
        Currency
            classic name is CURRENCY-CODE
        MatchReferenceNumber
        ReceiptCurrencyConversionRate is an EnteredCurrencyConversionRate
            classic name is REC-CNV-RATE
        VendorGLN                     is like GlobalLocationNumber
        LocationGLN                   is like GlobalLocationNumber
        TrackType
        GlobalLineType
            classic name is GLBL-LINE-TYPE
		RecordInError				  is Boolean

	Local Fields
		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150

		LocalPurchaseOrderLine			is like PurchaseOrderLine
		LocalMatchDetailKey				is like MatchDetailKey

		LocalFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
		LocalInterfaceResult 			is like PurchaseOrderReceiptInterfaceResult

	Derived Fields
		CannotDeterminePurchaseOrderLineMsg is a MessageField
			restricted
			"CannotDeterminePurchaseOrderLine;MultipleLinesExistForValuesEntered"

		CannotFindMatchingPurchaseOrderLineMsg is a MessageField
			restricted
			"CannotFindMatchingPurchaseOrderLine"
	
	Context Fields
		ContextReceiptImport is a PurchaseOrderReceiptImport
		
    Relations

        PurchaseOrderReceiptImportRel
            one-to-one relation to PurchaseOrderReceiptImport
            Field Mapping uses symbolic key
                related.Company                    							= Company
                related.PurchaseOrderReceiptImport 							= PurchaseOrderReceiptImport

        PurchaseOrderReceiptLineImportRel
            one-to-one relation to PurchaseOrderReceiptLineImport
            Field Mapping uses symbolic key
                related.Company                    							= Company
                related.PurchaseOrderReceiptImport 							= PurchaseOrderReceiptImport
            	related.PurchaseOrderReceiptLineImport.LineNumber 			= PurchaseOrderReceiptLineAOCImport.LineNumber
            	related.PurchaseOrderReceiptLineImport.SeqNbr 				= PurchaseOrderReceiptLineAOCImport.SeqNbr

        OtherAddOnChargesForSameLineRel
            one-to-many relation to PurchaseOrderReceiptLineAOCImport
            Field Mapping uses symbolic key
                related.Company                    							= Company
                related.PurchaseOrderReceiptImport 							= PurchaseOrderReceiptImport
            Instance Selection
            	where (related.PurchaseOrderReceiptLineAOCImport.LineNumber 	= PurchaseOrderReceiptLineAOCImport.LineNumber
            	and    related.PurchaseOrderReceiptLineAOCImport.SeqNbr 		= PurchaseOrderReceiptLineAOCImport.SeqNbr
            	and    related.PurchaseOrderReceiptLineAOCImport.AddOnCharge  != PurchaseOrderReceiptLineAOCImport.AddOnCharge)

		LocalInterfaceResultRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= LocalFinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult		= LocalInterfaceResult

        PurchaseOrderLineLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses Set11
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber
				related.MatchDetailKey				= LocalMatchDetailKey
			Instance Selection
				where (related.Closed.No
				and   (Location not entered
				or     related.ShipToLocation		= Location))

        ItemAndVendorItemLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber
			Instance Selection
				where (related.Item					= PurchaseOrderReceiptLineImportRel.Item
				and    related.VendorItem			= PurchaseOrderReceiptLineImportRel.VendorItem)
        
        ItemOnlyLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber
			Instance Selection
				where (related.Item					= PurchaseOrderReceiptLineImportRel.Item)
        
        VendorItemOnlyLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber
			Instance Selection
				where (related.VendorItem			= PurchaseOrderReceiptLineImportRel.VendorItem)
        

	Sets
        ByRunGroup
            Sort Order
				RunGroup
                Company
                PurchaseOrderReceiptImport
                PurchaseOrderReceiptLineAOCImport.LineNumber
                PurchaseOrderReceiptLineAOCImport.AddOnCharge
                PurchaseOrderReceiptLineAOCImport.SeqNbr
		
	Field Rules
		RunGroup
			default to ContextReceiptImport.RunGroup
			initial value is ContextReceiptImport.RunGroup
			required

	Rule Blocks
		GetPurchaseOrderLine

			if  (PurchaseOrderReceiptLineImportRel.Item entered
			and  PurchaseOrderReceiptLineImportRel.VendorItem entered
			and  instance count of ItemAndVendorItemLookupRel = 1)
				LocalPurchaseOrderLine = first ItemAndVendorItemLookupRel.PurchaseOrderLine
			else
			if  (PurchaseOrderReceiptLineImportRel.Item not entered
			and  PurchaseOrderReceiptLineImportRel.VendorItem entered
			and  instance count of VendorItemOnlyLookupRel = 1)
				LocalPurchaseOrderLine = first VendorItemOnlyLookupRel.PurchaseOrderLine
			else
			if  (PurchaseOrderReceiptLineImportRel.Item entered
			and  PurchaseOrderReceiptLineImportRel.VendorItem not entered
			and  instance count of ItemOnlyLookupRel = 1)
				LocalPurchaseOrderLine = first ItemOnlyLookupRel.PurchaseOrderLine
			else
				initialize LocalMatchDetailKey
				initialize LocalPurchaseOrderLine
								
				if  (MatchDetailKey entered)
					LocalMatchDetailKey = PurchaseOrderReceiptLineImportRel.MatchDetailKey
				else
					if  (PurchaseOrderReceiptLineImportRel.ItemType.Inventoried 
					or   PurchaseOrderReceiptLineImportRel.ItemType.NonStock)
						LocalMatchDetailKey 	= PurchaseOrderReceiptLineImportRel.Item
					else
						LocalMatchDetailKey 	= PurchaseOrderReceiptLineImportRel.VendorItem
						if  (LocalMatchDetailKey not entered)
							LocalMatchDetailKey = PurchaseOrderReceiptLineImportRel.Description
	
				if  (LocalMatchDetailKey entered)
					for each PurchaseOrderLineLookupRel
						if  (!PurchaseOrderReceiptLineImportRel.ItemType.Special
						or   PurchaseOrderReceiptLineImportRel.ReceivedUOM not entered
						or   each.EnteredBuyUOM		= PurchaseOrderReceiptLineImportRel.ReceivedUOM)				
	
							if (LocalPurchaseOrderLine entered)
								ErrorOccurred		= true
								LocalErrorMessage	= CannotDeterminePurchaseOrderLineMsg
	
							LocalPurchaseOrderLine = each.PurchaseOrderLine
								
				if (LocalPurchaseOrderLine not entered)
					ErrorOccurred		= true
					LocalErrorMessage	= CannotFindMatchingPurchaseOrderLineMsg

				
	Actions
		Create is a Create Action
			Action Rules
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules
		
		Update is an Update Action
			Action Rules

		FastUpdate is an Update Action
			restricted
			bypass field rules

		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules
				RunGroup = NewRunGroup
					
		Delete is a Delete Action
					
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		AddOnChargeLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				ReleaseReceipts				is Boolean
				ReleaseOption
				AllowDropshipReceivers		is Boolean
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PurchaseOrderReceiptInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup
					
				PrmRunGroup
					required
						"RunGroupIsRequired"

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany))

			Sort Order
				RunGroup
				Company
				PurchaseOrderReceiptImport

			Local Fields
				ReceiptError 			is Boolean
				ReceiptErrorMessage		is Alpha 150
				ReceiptErrorLevel		is Numeric 2
				LocalCount              is Numeric 12
				LocalFailedCount        is Numeric 12
				
			Action Rules

				Empty Set Rules
						

					LocalFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
					LocalInterfaceResult			= PrmInterfaceRun
					if (!LocalInterfaceResultRel.Status = 2)
						invoke Update LocalInterfaceResultRel
							invoked.Status        				= 1

				RunGroup Set Rules
						
					Exit Rules
						LocalFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						LocalInterfaceResult			= PrmInterfaceRun
						if (!LocalInterfaceResultRel.Status = 2)
							invoke Update LocalInterfaceResultRel
								invoked.Status        				= 1
						invoke Update LocalInterfaceResultRel
							invoked.InterfaceCounts.PassedHeaderCount += LocalCount
							invoked.InterfaceCounts.FailedHeaderCount += LocalFailedCount

				PurchaseOrderReceiptImport Set Rules
					Entrance Rules
						ReceiptError = false
						initialize ReceiptErrorMessage
						initialize ReceiptErrorLevel
						
					Exit Rules
						if  (ReceiptError)
							invoke SetError PurchaseOrderReceiptImport
								invoked.PrmErrorMessage			= ReceiptErrorMessage
								invoked.PrmInterfaceResult		= PrmInterfaceRun
								invoked.PrmErrorLevel   		= ReceiptErrorLevel

							LocalCount       -= 1
							LocalFailedCount += 1
						
						if (!PurchaseOrderReceiptImportRel.RecordInError.ErrorsExist)
							if (AllowDropshipReceivers
							and PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder.IsDropship)
								invoke CreateInProgress.TransitionToDropship PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
							else
								invoke CreateInProgress.TransitionToUnreleased PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
							
								invoke Unreleased.FastUpdate PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
									invoked.InterfaceInProcess				= false
								if (ReleaseReceipts)
									if  (ReleaseOption.ReleaseOnly)
										invoke Unreleased.Release PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											resume on error
									else
									if (ReleaseOption.ReleaseAndDeliver)
										invoke Unreleased.ReleaseAndPrintLPL PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											resume on error
									else
									if (ReleaseOption.ReleaseAndDeliverNoBackorders)
										invoke Unreleased.ReleaseAndPrintNoBackordersLPL PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											resume on error
							
							invoke FastDelete PurchaseOrderReceiptImportRel
					
				Instance Rules

					if    (!PurchaseOrderReceiptImportRel.RecordInError.ErrorsExist
					and  !ReceiptError)

					
						RecordInError 						= false
						ErrorOccurred						= false

						if (PurchaseOrderReceiptLineAOCImport.LineNumber entered) 
							if  (PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber not entered
							or  (PurchaseOrderReceiptLineImportRel.Item entered
							and  PurchaseOrderReceiptLineImportRel.LineNumberLookupRel.Item != PurchaseOrderReceiptLineImportRel.Item)
							or  (PurchaseOrderReceiptLineImportRel.VendorItem entered
							and  PurchaseOrderReceiptLineImportRel.LineNumberLookupRel.VendorItem != PurchaseOrderReceiptLineImportRel.VendorItem))
								include GetPurchaseOrderLine
							else
								LocalPurchaseOrderLine = PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber

						if (!ErrorOccurred)
							invoke Create PurchaseOrderReceiptLineAOC
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
									
								invoked.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
								invoked.PurchaseOrderReceipt		= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
								invoked.PurchaseOrderReceiptLine	= LocalPurchaseOrderLine
								invoked.AddOnCharge					= PurchaseOrderReceiptLineAOCImport.AddOnCharge
								invoked.ShipToLocation				= Location
								invoked.OriginalUnitCost			= EnteredUnitCost
								if  (TotalAddOnChargeAmount not entered)
									invoked.TotalAddOnChargeAmount	= Quantity * EnteredUnitCost

						if (ErrorOccurred)

							RecordInError 							= true
							ReceiptError 							= true
							ReceiptErrorMessage = LocalErrorMessage
							LocalFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							LocalInterfaceResult			= PrmInterfaceRun
							if (LocalInterfaceResultRel.Status != 2)
								invoke Update LocalInterfaceResultRel
									invoked.Status        				= 2
							if  (PurchaseOrderReceiptLineAOCImport.LineNumber not entered)
								ReceiptErrorLevel   				= 3
							else
								ReceiptErrorLevel	  				= 4
						else
							if  (PurchaseOrderReceiptLineImportRel exists
							and  !OtherAddOnChargesForSameLineRel exists
							and  !PurchaseOrderReceiptLineImportRel.POReceiptInventoryDetailImportRel exists)

								invoke FastDelete PurchaseOrderReceiptLineImportRel
							

							invoke FastDelete
	
					
