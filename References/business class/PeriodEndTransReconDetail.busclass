PeriodEndTransReconDetail is a BusinessClass
	owned by reconciliation
	prefix is PETR

	Ontology
		symbolic key is PeriodEndTransReconDetail
	
	Patterns
		implements LightweightAuditing
		disable AuditIndex
 		disable EffectiveDated
		disable AsOfDateProcessing
			
	Persistent Fields								
		TemplateMatchField						is Alpha 200	
			Text Variables
				TransactionNumber	value is TransactionNumber
				TransactionDate		value is TransactionDate
				TransactionAmount	value is DerivedTransactionAmount
				Currency			value is DerivedCurrency 
				UnitsAmount			value is UnitsAmount
				SystemCode			value is SystemCode
				SourceCode			value is SourceCode
				Reference			value is Reference
				Description			value is Description
		ReconciliationCodeBlock
		ReconTransactionType
		TransactionReconTemplate
		DetailReconciliationMatchRule
		LineMatchField							is Alpha 200
		DetailLineNumber						is Numeric 6
		TransactionNumber						is Numeric 15
		TransactionDate							is an ExchangeDate
		PostingDate
		CurrencyTable
		Currency								is a FromCurrency
		TransactionAmount						is a FinanceCurrencyAmountGroup
			default label is "Amount"
		AmountType								is Numeric 1
			default label is "Sign"
			States
				Debit				value is 1
				Credit				value is 2
		ReconcilingAmount						is a FinanceCurrencyAmountGroup
		SignedAmount							is a FinanceCurrencyAmountGroup
		UnitsAmount
		SystemCode								is a GeneralLedgerSystemCode
			default label is "SystemCode"
		SourceCode								is a GeneralLedgerEvent
			default label is "Event"
		Reference								is Alpha 10
		Description								is Alpha 100
		Operator								is AlphaUpper 10
		SummaryRecord							is Boolean
		SelectedForMatch						is Boolean
		Reconciled								is Boolean
		ReconciliationInterface
			delete ignored
		OriginatingGLTransaction				is BusinessObjectReference
		MatchedDate								is TimeStamp
		MatchedActor							is an Actor
			default label is "MatchedBy"
		MatchedManually							is Boolean
		NumberOfMatchedInterfaceLines			is Numeric 6
		NumberOfMatchedGeneralLedgerLines		is Numeric 6
			default label is "NumberOfMatchedGlobalLedgerLines"
		DistributionAccount						is a FinanceCodeBlockFull
		CashAllocationCode
		CreateDistribution						is Boolean
		CreateReconcilingItem					is Boolean
		DetailReconciliationCurrency
		DetailReconciliationSign
		ReconcilingDescription					is a Description	
			default label is "Description"
		ReconcilingComment						is Text				
			default label is "Comment"
		NonAccountFunctionalCurrency			is a Currency
		OriginatingTransaction        			is BusinessObjectReference
		
	Local Fields





		LocalReconciliationManagementGroup		is like ReconciliationManagementGroup
		LocalReconciliationAccountStructure		is like ReconciliationAccountStructure
		LocalReconciliationAccount				is like ReconciliationAccount
		LocalGeneralLedgerCloseConfiguration	is like GeneralLedgerCloseConfiguration
		LocalGeneralLedgerClosePeriod			is like GeneralLedgerClosePeriod
	
	Transient Fields
		ReconciliationFromEntity			 	is an AccountingEntity
    		derive value from ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity
						
	Rule Blocks












						
	Derived Fields
		DerivedTransactionAmount is a DerivedField
			type is like InternationalAmount
			if (DetailReconciliationCurrency.Transaction)
				return TransactionAmount.CurrencyAmount
			else
			if (DetailReconciliationCurrency.EntityFunctional)
				return TransactionAmount.FunctionalAmount.EnteredCurrencyAmount




























		DerivedCurrency is a DerivedField
			type is like Currency
			if (DetailReconciliationCurrency.Transaction)
				return Currency
			else
			if (DetailReconciliationCurrency.EntityFunctional)
				return ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity.FunctionalCurrency



























				
		GeneralLedgerDebitAmount is a DerivedField
			type is like InternationalAmount
			default label is "GlobalLedgerDebitAmount"
			if (SummaryRecord)
				if (DetailReconciliationCurrency.Transaction)
					return (sum DrGeneralLedgerPeriodEndTransReconDetailRel.TransactionAmount.CurrencyAmount)
				else
				if (DetailReconciliationCurrency.EntityFunctional)
					return (sum DrGeneralLedgerPeriodEndTransReconDetailRel.TransactionAmount.FunctionalAmount.EnteredCurrencyAmount)



























					
		GeneralLedgerCreditAmount is a DerivedField		
			type is like InternationalAmount
			default label is "GlobalLedgerCreditAmount"
			if (SummaryRecord)
				if (DetailReconciliationCurrency.Transaction)
					return (sum CrGeneralLedgerPeriodEndTransReconDetailRel.TransactionAmount.CurrencyAmount)
				else
				if (DetailReconciliationCurrency.EntityFunctional)
					return (sum CrGeneralLedgerPeriodEndTransReconDetailRel.TransactionAmount.FunctionalAmount.EnteredCurrencyAmount)



























		
		InterfacedTransactionDebitAmount is a DerivedField
			type is like InternationalAmount
			if (SummaryRecord)
				if (DetailReconciliationCurrency.Transaction)
					return (sum DrInterfacedPeriodEndTransReconDetailRel.TransactionAmount.CurrencyAmount)
				else
				if (DetailReconciliationCurrency.EntityFunctional)
					return (sum DrInterfacedPeriodEndTransReconDetailRel.TransactionAmount.FunctionalAmount.EnteredCurrencyAmount)



























					
		InterfacedTransactionCreditAmount is a DerivedField		
			type is like InternationalAmount
			if (SummaryRecord)
				if (DetailReconciliationCurrency.Transaction)
					return (sum CrInterfacedPeriodEndTransReconDetailRel.TransactionAmount.CurrencyAmount)
				else
				if (DetailReconciliationCurrency.EntityFunctional)
					return (sum CrInterfacedPeriodEndTransReconDetailRel.TransactionAmount.FunctionalAmount.EnteredCurrencyAmount)



























					





































		
		NetReconciledDebitAmount is a DerivedField
			type is like InternationalAmount
			default label is "NetDebitAmount"
			if (SummaryRecord)
				if (ReconciliationAccount.LoadingBothSourcesUsingSameSign)
					return (GeneralLedgerDebitAmount - InterfacedTransactionDebitAmount)
				else
					return (GeneralLedgerDebitAmount + InterfacedTransactionDebitAmount)
					
		NetReconciledCreditAmount is a DerivedField
			type is like InternationalAmount
			default label is "NetCreditAmount"
			if (SummaryRecord)
				if (ReconciliationAccount.LoadingBothSourcesUsingSameSign)
					return (GeneralLedgerCreditAmount - InterfacedTransactionCreditAmount)
				else
					return (GeneralLedgerCreditAmount + InterfacedTransactionCreditAmount)
					
		NetReconciledDifference is a DerivedField
			type is like InternationalAmount
			default label is "ReconciledDifference"
			if (SummaryRecord)
				return (NetReconciledDebitAmount - NetReconciledCreditAmount)
		
		MatchedManuallyMessage is a LabelField
			restricted
			"ManuallyMatched"

		AutoMatchedMessage is a LabelField
			restricted
			"AutoMatched"

		DerivedMatchedMessage is a DerivedField
			type is Alpha 20
			if (ReconMatchNumber entered)
				if (MatchedManually)
					return MatchedManuallyMessage
				else
					return AutoMatchedMessage
	
		BlankField is a DerivedField
			type is Alpha 1
			return blank
		
		DerivedFunctionalCurrency is a DerivedField
			type is like Currency
			if (ReconciliationAccountStructure.ReconciliationType.Account)
				return TransactionAmount.AccountingEntity.FunctionalCurrency
			else
				return NonAccountFunctionalCurrency
					
	Relations
		RelatedMatchedTransactionsRel
			one-to-many relation to PeriodEndTransReconDetail
			Field Mapping uses symbolic key
				related.ReconciliationManagementGroup  	= ReconciliationManagementGroup
				related.ReconciliationAccountStructure 	= ReconciliationAccountStructure
				related.ReconciliationAccount	  	   	= ReconciliationAccount
				related.GeneralLedgerCloseConfiguration	= GeneralLedgerCloseConfiguration
				related.GeneralLedgerClosePeriod 		= GeneralLedgerClosePeriod
			Instance Selection
				where (related.ReconMatchNumber	entered
				and    related.ReconMatchNumber = ReconMatchNumber
				and    related.PeriodEndTransReconDetail != PeriodEndTransReconDetail)

		GeneralLedgerPeriodEndTransReconDetailRel
			one-to-many relation to PeriodEndTransReconDetail
			Field Mapping uses symbolic key
				related.ReconciliationManagementGroup  	= ReconciliationManagementGroup
				related.ReconciliationAccountStructure	= ReconciliationAccountStructure
				related.ReconciliationAccount	  	   	= ReconciliationAccount
				related.GeneralLedgerCloseConfiguration	= GeneralLedgerCloseConfiguration
				related.GeneralLedgerClosePeriod 		= GeneralLedgerClosePeriod
				related.PeriodEndTransReconDetail		= PeriodEndTransReconDetail children.PeriodEndTransReconDetail			
			Instance Selection
				where (related.ReconTransactionType.GeneralLedger)
		
		DrGeneralLedgerPeriodEndTransReconDetailRel
			one-to-many relation using GeneralLedgerPeriodEndTransReconDetailRel
			Instance Selection
				where (related.AmountType.Debit)
				
		CrGeneralLedgerPeriodEndTransReconDetailRel
			one-to-many relation using GeneralLedgerPeriodEndTransReconDetailRel
			Instance Selection
				where (related.AmountType.Credit)
				
		InterfacedPeriodEndTransReconDetailRel
			one-to-many relation to PeriodEndTransReconDetail
			Field Mapping uses symbolic key
				related.ReconciliationManagementGroup  	= ReconciliationManagementGroup
				related.ReconciliationAccountStructure 	= ReconciliationAccountStructure
				related.ReconciliationAccount	  	   	= ReconciliationAccount
				related.GeneralLedgerCloseConfiguration	= GeneralLedgerCloseConfiguration
				related.GeneralLedgerClosePeriod 		= GeneralLedgerClosePeriod
				related.PeriodEndTransReconDetail		= PeriodEndTransReconDetail children.PeriodEndTransReconDetail			
			Instance Selection
				where (related.ReconTransactionType.InterfacedTransaction)
		
		DrInterfacedPeriodEndTransReconDetailRel
			one-to-many relation using InterfacedPeriodEndTransReconDetailRel
			Instance Selection
				where (related.AmountType.Debit)
				
		CrInterfacedPeriodEndTransReconDetailRel
			one-to-many relation using InterfacedPeriodEndTransReconDetailRel
			Instance Selection
				where (related.AmountType.Credit)
		
		PeriodEndTransReconDetailRel 
			one-to-many relation to PeriodEndTransReconDetail
			Field Mapping uses symbolic key
				related.ReconciliationManagementGroup  	= LocalReconciliationManagementGroup
				related.ReconciliationAccountStructure 	= LocalReconciliationAccountStructure
				related.ReconciliationAccount	  	   	= LocalReconciliationAccount
				related.GeneralLedgerCloseConfiguration	= LocalGeneralLedgerCloseConfiguration
				related.GeneralLedgerClosePeriod 		= LocalGeneralLedgerClosePeriod
		
		ReconciliationInterfaceRel
			one-to-one relation to ReconciliationInterface
			Field Mapping uses symbolic key
				related.ReconciliationManagementGroup  	= ReconciliationManagementGroup
				related.ReconciliationAccountStructure 	= ReconciliationAccountStructure
				related.ReconciliationAccount	  	   	= ReconciliationAccount
				related.ReconciliationInterface			= ReconciliationInterface
		
		ReconciliationPeriodDetailRel
			one-to-many relation to ReconciliationPeriodDetail
 			Field Mapping uses ByPeriod
 				related.ReconciliationManagementGroup	= LocalReconciliationManagementGroup  
				related.ReconciliationAccountStructure	= LocalReconciliationAccountStructure
	 		Instance Selection
	 			where (related.GeneralLedgerCloseConfiguration = LocalGeneralLedgerCloseConfiguration
				and    related.DetailPeriod.GeneralLedgerCalendarPeriod = LocalGeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				and    related.OriginatingTransaction = OriginatingTransaction)
		
	Sets
		ByDetailLineNumber
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				SelectedForMatch	descending
				DetailLineNumber
				AmountType
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				PeriodEndTransReconDetail

		ByTransactionNumber
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				SelectedForMatch	descending
				TransactionNumber
				AmountType
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				PeriodEndTransReconDetail

		ByTransactionDate
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				SelectedForMatch	descending
				TransactionDate
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				PeriodEndTransReconDetail

		ByTransactionAmount
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				SelectedForMatch	descending
				TransactionAmount.FunctionalAmount.EnteredCurrencyAmount
				AmountType
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				PeriodEndTransReconDetail

 		ByLineMatchField
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				LineMatchField
				AmountType
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				PeriodEndTransReconDetail
 
 		ByDescription
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				SelectedForMatch	descending
				Description
				AmountType
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				PeriodEndTransReconDetail

		BySummaryRecordsFirst
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod
				SummaryRecord	descending
				PeriodEndTransReconDetail
				
    Conditions
		HasChildren
			when (PeriodEndTransReconDetail children exist)
			
		MatchedWithBalance
			when (SummaryRecord
			and   NetReconciledDifference != 0)
		
		MatchedWithNoBalance
			when (SummaryRecord
			and   NetReconciledDifference = 0)

		UnreconciledMatchedAlert
			when (SummaryRecord
			and   !Reconciled)
			
		ReconciledMatchedAlert
			when (SummaryRecord
			and   Reconciled)
			
		CanBeViewed
			when (SummaryRecord
			and   !SelectedForMatch)

		MatchedDetailTransaction
			when (!SummaryRecord
			and   !SelectedForMatch
			and   ReconMatchNumber entered)
		
		UnmatchedDetailTransaction
			when (!SummaryRecord
			and	  !SelectedForMatch
			and   !ReconMatchNumber entered)
			
		IsMatchTypeCompliant
			restricted
			when  (DetailReconciliationMatchRule.MatchType = 0
			or	 ((DetailReconciliationMatchRule.MatchType.OneToOne
			and	   NumberOfMatchedInterfaceLines = 1
			and    NumberOfMatchedGeneralLedgerLines = 1)
			or    (DetailReconciliationMatchRule.MatchType.OneToMany
			and	   NumberOfMatchedInterfaceLines = 1
			and	   NumberOfMatchedGeneralLedgerLines > 1)
			or	  (DetailReconciliationMatchRule.MatchType.ManyToOne
			and	   NumberOfMatchedInterfaceLines > 1
			and	   NumberOfMatchedGeneralLedgerLines = 1)
			or	  (DetailReconciliationMatchRule.MatchType.ManyToMany
			and	   NumberOfMatchedInterfaceLines > 1
			and	   NumberOfMatchedGeneralLedgerLines > 1)))
		
		ShowMatchTypeAlert
			restricted
			when (SummaryRecord
			and   DetailReconciliationMatchRule entered
			and	  !IsMatchTypeCompliant)
		
		DisplayDetailReconciliationMatchRuleLink
			restricted
			when (ReconciliationAccount.DetailReconciliationMethod.UseDetailReconciliationMatchRules
			and	  DetailReconciliationMatchRule entered)
					
  	Actions
  		Create is a Create Action
  			restricted
  					
  		Update is an Update Action
			restricted
		
		FastUpdate is an Update Action
			restricted
			bypass field rules
				
  		Delete is a Delete Action
  			restricted
  		
  		Purge is a Purge Action
  			restricted
  		
  		RestoreDetailTransactionReconciliation is a Set Action
			restricted
  			Parameters
				PrmReconciliationManagementGroup	is a ReconciliationManagementGroup
				PrmReconciliationAccountStructure	is a ReconciliationAccountStructure
				PrmReconciliationAccount			is a ReconciliationAccount
				PrmGeneralLedgerCloseConfiguration	is a GeneralLedgerCloseConfiguration
				PrmGeneralLedgerClosePeriod			is a GeneralLedgerClosePeriod
			
			Local Fields
				LocalReconciliationInterface		is a ReconciliationInterface view
				LocalTransactionReconDetail			is a TransactionReconDetail view
						
			Instance Selection
				where (ReconciliationManagementGroup = PrmReconciliationManagementGroup
				and	   ReconciliationAccountStructure = PrmReconciliationAccountStructure
				and	   ReconciliationAccount = PrmReconciliationAccount
				and    GeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration
				and	   GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = PrmGeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod)
			
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				ReconciliationAccount
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod	
				SummaryRecord	descending
				PeriodEndTransReconDetail
				
			Action Rules
				Set Rules
					Exit Rules
						invoke FastUpdate PrmReconciliationAccount
							invoked.CurrentReconciliationPeriod = PrmGeneralLedgerClosePeriod
						
						LocalReconciliationManagementGroup = PrmReconciliationManagementGroup
						LocalReconciliationAccountStructure = PrmReconciliationAccountStructure
						LocalReconciliationAccount = PrmReconciliationAccount
						LocalGeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration
						LocalGeneralLedgerClosePeriod = PrmGeneralLedgerClosePeriod
						invoke Purge PeriodEndTransReconDetailRel
						
				Instance Rules
					initialize LocalReconciliationInterface
					if (ReconTransactionType.InterfacedTransaction
					and !SummaryRecord)
						if (!ReconciliationInterfaceRel exists)
							invoke Create ReconciliationInterface
								assign result to LocalReconciliationInterface
								fill in fields from this instance
								invoked.LoadedIntoReconciliation = true
						else
							invoke FastUpdate ReconciliationInterface
								invoked.LoadedIntoReconciliation = true
							
					invoke Create TransactionReconDetail
						assign result to LocalTransactionReconDetail
						fill in fields from this instance
						invoked.ReconciliationManagementGroup	= PrmReconciliationManagementGroup
						invoked.ReconciliationAccountStructure	= PrmReconciliationAccountStructure
						invoked.ReconciliationAccount			= PrmReconciliationAccount
						invoked.ReconMatchNumber				= ReconMatchNumber
						invoked.TransactionReconDetail			= PeriodEndTransReconDetail
						invoked.LineMatchField					= LineMatchField
						if (LocalReconciliationInterface.ReconciliationInterface entered)
							invoked.ReconciliationInterface		= LocalReconciliationInterface.ReconciliationInterface
						else
							invoked.ReconciliationInterface		= ReconciliationInterface
						if (SummaryRecord)
							initialize invoked.ReconciliationInterface
						
					if (OriginatingTransaction entered)
						LocalReconciliationManagementGroup = PrmReconciliationManagementGroup
						LocalReconciliationAccountStructure = PrmReconciliationAccountStructure
						LocalGeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration
						LocalGeneralLedgerClosePeriod = PrmGeneralLedgerClosePeriod
						invoke FastUpdate ReconciliationPeriodDetailRel
							invoked.OriginatingTransaction 		= reference to LocalTransactionReconDetail.TransactionReconDetail
