SupplierContactMessage is a BusinessClass
    owned by procurement
    prefix is SCMSG

    Ontology
    	symbolic key is SupplierContactMessage
    	    		
	Persistent Fields
		CreationDateTime	is TimeStamp
		MessageTitle		is Alpha 100
		MessageText			is RichText
		Status				is Numeric 1
			States
				Unread			value is 1
				Read			value is 2
				Deleted			value is 3
				HardDeleted		value is 4
		ReleaseStatus		is Numeric 1
			States
				Draft			value is 1
				Released		value is 2
		Priority			is Numeric 1
			States
				Low				value is 1
				Normal			value is 2
				High			value is 3
		SystemGenerated		is Boolean
		Attachment
		MessageOwner        is an Employee
			default label is "DialogContact"
		Historical          is Boolean
		OriginatingCompany  is like GeneralLedgerCompany
		OriginatingPO       is like PurchaseOrder
		OriginatingInvoice  is like PayablesInvoice
		OriginatingContract is like Contract
		OriginatingEvent    is like SourcingEvent
		OriginatingReceipt  is like PurchaseOrderReceipt
		OriginatingReturn   is like VendorReturn
		OriginatingPayment  is Boolean
		OriginatingRequisition is like Requisition
		FromModificationRequest is Numeric 1
			States
				No 			value is 0
				Requested   value is 1
				Modified    value is 2
				Canceled    value is 3
		ModifyByDate        is TimeStamp

	Local Fields
		LocalNewDialogContact is like Employee

	Derived Fields
		CreationDate is a DerivedField
			type is Date
			return CreationDateTime date
		
		DialogContactName is a StringField
			type is Alpha up to 101
			default label is "DialogContact"
			MessageOwner.FirstLastName
			
		DerivedEnteredInvoice is a DerivedField
			type is Alpha 50
			default label is "Invoice"
			return SupplierContactMessage.InvoiceRel.Invoice			
			
	Conditions
		MessageBySupplierContact
			restricted
 			when (actor.agent(SupplierSourceId).SupplierGroup 		= SupplierGroup
 			and	  actor.agent(SupplierSourceId).Supplier 			= Supplier
 			and	  actor.agent(SupplierSourceId).SupplierSourceId	= SupplierSourceId)

		Deleted
			restricted
			when (Status = 3)
			
		Read
			restricted
			when (Status = 2)
		
		Unread
			restricted
			when (Status = 1)
			
		Released
			restricted
			when (ReleaseStatus.Released)
		
		Active
			restricted
			when ((ReleaseStatus.Released)
			and	 (Status = 1
			or    Status = 2))

		HasAttachment
			restricted
			when (Attachment entered)
		
		SupplierMessageExists
			restricted
			when (SupplierMessage entered)
		
		NeedsSupplierRMA
			restricted
			when (VendorReturnRel.NeedsSupplierRMA)
		
		Editable
			restricted
			when ((!SupplierMessageExists) and (!SystemGenerated) and ReleaseStatus.Draft)

		IsSupplierContact
			restricted
			when (actor.agent(SupplierSourceId).SupplierGroup 		= SupplierGroup
			and	  actor.agent(SupplierSourceId).Supplier 			= Supplier
			and	  actor.agent(SupplierSourceId).SupplierSourceId	= SupplierSourceId)

		DeleteForeverValid
			restricted
			when (Deleted
			and	  IsSupplierContact)

		DialogExists
			restricted
			when (SupplierMessageDialogRel exists)

		MarkAsDeletedValid
			restricted
			when (!Deleted
			and	  IsSupplierContact)

		MarkAsHistoricalValid
			restricted
			when (IsSupplierContact
			and   Historical = false
			and   DialogExists
			and  !Status     = Status.HardDeleted)

		MoveToInboxValid
			restricted
			when (Deleted
			and	  IsSupplierContact)

		MarkAsUnreadValid
			restricted
			when (Read
			and   IsSupplierContact)

		MarkAsReadValid
			restricted
			when (Unread
			and	  IsSupplierContact)
			
		POOrInvoiceOrEventEntered  
			restricted
			when (PurchaseOrderEntered
			or    InvoiceEntered
			or    EventEntered
			or    ReturnEntered)
		
		PurchaseOrderEntered
			restricted
			when (OriginatingPO entered)

		InvoiceEntered
			restricted
			when (OriginatingInvoice entered)

		ContractEntered
			restricted
			when (OriginatingContract entered)
		
		EventEntered
			restricted
			when (OriginatingEvent entered)
		
		ReceiptEntered
			restricted
			when (OriginatingReceipt entered)
		
		ReturnEntered
			restricted
			when (OriginatingReturn entered)

		PurchaseOrderLinkExists
			restricted
			when (PurchaseOrderRel exists)

		InvoiceLinkExists
			restricted
			when (InvoiceRel exists)

		ContractLinkExists
			restricted
			when (ContractRel exists)

		EventLinkExists
			restricted
			when (EventRel exists)
			
		ReceiptLinkExists
			restricted
			when (ReceiptRel exists)

		PaymentLinkExists
			restricted
			when (PaymentRel exists
			and   OriginatingPayment = true)

		ReturnLinkExists
			restricted
			when (VendorReturnRel exists)
	
		RequisitionLinkExists
			restricted
			when (RequisitionRel exists)

		ModificationRequested
			restricted
			when (FromModificationRequest = 1)

		ModificationRequestExists
			restricted
			when (FromModificationRequest > 0)

		ModificationRequestedAndCanUpdate 
			restricted 
			when (ModificationRequested
			and   ModifyByDate > current corporate date)
		
		ValidMessageOwner
			restricted
			when (SupplierGroupPortalContactRel exists)
			
		ValidNewMessageOwner
			restricted
			when (NewSupplierGroupPortalContactRel exists)
	
	Sets
		ByCreationDateTime
			Sort Order
				SupplierGroup
    			CreationDateTime
 				SupplierContactMessage
 				Supplier
 				SupplierSourceId
 				SupplierMessage		

		ByPriority
			Sort Order
				SupplierGroup
    			Priority
 				SupplierContactMessage
 				Supplier
 				SupplierSourceId
 				SupplierMessage

		ByStatus
			Sort Order
				SupplierGroup
    			Status
 				SupplierContactMessage
 				Supplier
 				SupplierSourceId
 				SupplierMessage

		ByPurchaseOrder
			Sort Order
				OriginatingCompany
				OriginatingPO
				SupplierGroup
				Supplier
				SupplierSourceId
				SupplierMessage
				SupplierContactMessage

		BySourcingEvent
			Sort Order
				OriginatingCompany
				OriginatingEvent
				SupplierGroup
				Supplier
				SupplierSourceId
				SupplierMessage
				SupplierContactMessage

		ByVendorReturn
			Sort Order
				OriginatingCompany
				OriginatingReturn
				SupplierGroup
				Supplier
				SupplierSourceId
				SupplierMessage
				SupplierContactMessage

		ByInvoice
			Sort Order
				OriginatingCompany
				OriginatingInvoice
				SupplierGroup
				Supplier
				SupplierSourceId
				SupplierMessage
				SupplierContactMessage

		ByReceipt
			Sort Order
				OriginatingCompany
				OriginatingReceipt
				SupplierGroup
				Supplier
				SupplierSourceId
				SupplierMessage
				SupplierContactMessage

	Relations
	
		SupplierMessageDialogRel
			one-to-many relation to SupplierMessageDialog
			delete cascades
			Field Mapping uses symbolic key
				related.SupplierGroup			= SupplierGroup
				related.Supplier                = Supplier
				related.SupplierSourceId        = SupplierSourceId
				related.SupplierMessage 		= SupplierMessage
				related.SupplierContactMessage	= SupplierContactMessage
	
		RequisitionRel
			one-to-one relation to Requisition
			Field Mapping uses symbolic key
				related.Company					= OriginatingCompany
				related.Requisition            = OriginatingRequisition

		VendorReturnRel
			one-to-one relation to VendorReturn
			Field Mapping uses symbolic key
				related.Company					= OriginatingCompany
				related.VendorReturn            = OriginatingReturn

		PurchaseOrderRel
			one-to-one relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company					= OriginatingCompany
				related.PurchaseOrder           = OriginatingPO
		
		InvoiceRel
			one-to-one relation to PayablesInvoice
			Field Mapping uses symbolic key
				related.Company                 = OriginatingCompany
				related.PayablesInvoice         = OriginatingInvoice

		PaymentRel
			one-to-many relation to CashLedgerPayablesPayment
			Field Mapping uses Set4
				related.CashManagementGroup		   						= InvoiceRel.CashCode.CashManagementGroup
				related.CashCode										= InvoiceRel.CashCode
				related.CashLedgerPayablesPayment.BankTransactionCode	= InvoiceRel.BankTransactionCode
				related.TransactionNumber								= InvoiceRel.TransientPaymentNumber		

		ContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup           = SupplierGroup
				related.Contract                = OriginatingContract

		EventRel
			one-to-one relation to SourcingEvent
			Field Mapping uses symbolic key
				related.Company                 = OriginatingCompany
				related.SourcingEvent           = OriginatingEvent
				
		EventResponseRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses ByEvent
				related.Company                 = OriginatingCompany
				related.SourcingEvent           = OriginatingEvent
			Instance Selection
				where (related.NotifiedSupplier.SupplierGroup		= SupplierGroup
				and    related.NotifiedSupplier.Supplier			= Supplier	
				and    related.NotifiedSupplier.SupplierSourceId 	= SupplierSourceId)		

		ReceiptRel
			one-to-one relation to PurchaseOrderReceipt
			Field Mapping uses symbolic key
				related.Company                 = OriginatingCompany
				related.PurchaseOrderReceipt    = OriginatingReceipt

		SupplierGroupPODefaultPortalContactRel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup           = SupplierGroup
			Instance Selection
				where (related.PODefaultDialogContact = true)
				
		SupplierGroupAPDefaultPortalContactRel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup           = SupplierGroup
			Instance Selection
				where (related.APDefaultDialogContact = true)
				
		SupplierGroupDefaultPortalContactRel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup           = SupplierGroup
			Instance Selection
				where (related.DefaultMessageDialogContact = true)

		SupplierGroupPortalContactRel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses ByEmployee
				related.Contact                 = MessageOwner
				related.SupplierGroup           = SupplierGroup
			
		NewSupplierGroupPortalContactRel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses ByEmployee
				related.Contact                 = LocalNewDialogContact
				related.SupplierGroup           = SupplierGroup
				
	Field Rules
		MessageTitle
			if (!SystemGenerated)
				required
			
		CreationDateTime
			initial value is current timestamp
			default to current timestamp
			
		Status
			initial value is Status.Unread
		
		Priority
			initial value is Priority.Normal

		ReleaseStatus
			initial value is ReleaseStatus.Draft

 	Actions
		Create is a Create Action
			
			Action Rules
				if (SystemGenerated or SupplierMessageExists)
					ReleaseStatus	= ReleaseStatus.Released
				if  (MessageOwner !entered)
					if (OriginatingPO entered
					or  OriginatingContract entered
					or  OriginatingEvent entered
					or  OriginatingReturn entered)
						MessageOwner = first SupplierGroupPODefaultPortalContactRel.Contact
					else
					if (OriginatingInvoice entered)
						MessageOwner = first SupplierGroupAPDefaultPortalContactRel.Contact
					if (MessageOwner !entered)
						if (SupplierGroupDefaultPortalContactRel exists)
							MessageOwner = first SupplierGroupDefaultPortalContactRel.Contact

		Update is an Action
			valid when (Editable)
		
		Delete is an Action
			valid when (Editable)

		Release is an Instance Action
			valid when (ReleaseStatus.Draft)

			Action Rules
				ReleaseStatus	= ReleaseStatus.Released
		
		MarkAsRead is an Instance Action
			valid when (MarkAsReadValid)
			
			Action Rules
				Status	= Status.Read
				
		MarkAsUnread is an Instance Action
			valid when (MarkAsUnreadValid)

			Action Rules
				Status	= Status.Unread

		MoveToInbox is an Instance Action
			valid when (MoveToInboxValid)

			Action Rules
				Status	= Status.Unread
				
		MarkAsDeleted is an Instance Action
			valid when (MarkAsDeletedValid)
			
			Action Rules
				Status	   = Status.Deleted
				Historical = false
				
		MarkAsHistorical is an Instance Action
			valid when (MarkAsHistoricalValid)
			
			Action Rules
				Status 		= Status.Deleted
				Historical 	= true				
		
		DeleteForever is an Instance Action
			valid when (DeleteForeverValid)
			
			Action Rules
				invoke Delete

		UpdateContact is an Instance Action
			restricted
			Parameters
				NewDialogContact is an Employee
				
			Action Rules
				LocalNewDialogContact = NewDialogContact
				constraint (ValidNewMessageOwner)
					"NewDialogContactIsNotValid"
				
				MessageOwner = NewDialogContact	

		EnterReturnRMA is an Instance Action
			valid when (NeedsSupplierRMA)
			default label is "EnterReturnMaterialAuthorizationNumber"
			Parameters
				ParmRMA					is AlphaUpper size 64 
					default label is "ReturnMaterialAuthorizationNumber"
				ParmDialogMessage		is Alpha size 1000
					default label is "Message"
				ParmDialogAttachment    is an Attachment
					default label is "Attachment"

			Action Rules
				invoke Added.Update VendorReturnRel
					invoked.ReturnMaterialAuthorizationNumber = ParmRMA
				invoke Create SupplierMessageDialog
					invoked.SupplierGroup 			= SupplierGroup
					invoked.Supplier                = Supplier
					invoked.SupplierSourceId        = SupplierSourceId
					invoked.SupplierMessage 		= SupplierMessage
					invoked.SupplierContactMessage 	= SupplierContactMessage
					invoked.Message                 = ParmDialogMessage
					invoked.MessageAttachment       = ParmDialogAttachment
				if (Status = Status.Unread)
					Status = Status.Read
				send email
					to MessageOwner.EmployeeWorkEmailAddress
					from SupplierSourceId.EmailAddress
					subject "AReturnMaterialAuthorizationNumberHasBeenEnteredByASupplier"
					Contents
						"VendorReturn<OriginatingReturn>HasHadAnRMANumberEntered"
						"ReturnInformationCanBeViewedInMessagesAndDialog"
		
		StartADialog is an Instance Action
			valid when (Active)
			Parameters
				ParmDialogMessage       is Alpha size 1000
				ParmDialogAttachment	is an Attachment	
				ParmResponseRequested   is Boolean
						
			Action Rules
				invoke Create SupplierMessageDialog
					invoked.SupplierGroup 			= SupplierGroup
					invoked.Supplier                = Supplier
					invoked.SupplierSourceId        = SupplierSourceId
					invoked.SupplierMessage 		= SupplierMessage
					invoked.SupplierContactMessage 	= SupplierContactMessage
					invoked.Message                 = ParmDialogMessage
					invoked.MessageAttachment       = ParmDialogAttachment
					invoked.ResponseRequested       = ParmResponseRequested		
				if (Status = Status.Unread)
					Status = Status.Read
				if (MessageOwner !entered)
					MessageOwner = first SupplierGroupDefaultPortalContactRel.Contact
				if (MessageOwner entered)
					send email
						to MessageOwner.EmployeeWorkEmailAddress
						from SupplierSourceId.EmailAddress
						subject "ADialogHasBeenStartedBySupplier<Supplier.RepresentativeText>;Contact<SupplierSourceId.RepresentativeText>"
						Contents
							"TheDialogCanBeViewedInMessagesAndDialog"				
