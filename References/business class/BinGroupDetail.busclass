BinGroupDetail is a BusinessClass
	default label is "BinSequence"
    owned by ic
    prefix is BGD
    classic name is BINGRPDTL

    Ontology
        symbolic key is BinGroupDetail
            classic set name is BGDSET1
            classic name is BIN-SEQ
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Bin
    
    Context Fields
    	Requisition 
        
    Transient Fields
    	TransientCreateCount					is Numeric size 9
    	TransientBinSequence					is like BinGroupDetail
    		derive value from DerivedBinSequence
    		default label is "BinSequence"
    	TransientBin							is a Bin
    		derive value from DerivedShelfLocation
    		default label is "ShelfLocation"    	
    	TransientItem							is an Item
    		derive value from ItemLocationRel.Item
    		default label is "Item"
    	TransientParLevel                       is an UnsignedQuantity
			derive value from DerivedParLevel
			precision is TransientItem.NumberOfDecimalsQuantity
			default label is "ParLevel"
    	TransientUOM							is a UnitOfMeasureEdit							
    		derive value from ItemLocationRel.DefaultTransactionUOM
    		default label is "UOM"
    	TransientChargeable						is Boolean
    		derive value from DerivedChargeable
    		default label is "Chargeable"
    	TransientInventoryLocationAccountGroup	is a GeneralLedgerCategory
    		derive value from DerivedInventoryLocationAccountGroup
    		default label is "InventoryLocationAccountGroup"
    	TransientNoCharge						is Boolean
    		derive value from DerivedNoCharge
    		default label is "NoCharge"
    	TransientActive							is Boolean
    		derive value from DerivedActive
    		default label is "Active"
    	TransientReplenishFrom					is a ReplenishFrom
			derive value from DerivedReplenishFrom
    		default label is "ReplenishFrom"
    	TransientVendorAndPurchaseFrom			is a VendorAndPurchaseFrom
    		derive value from ItemLocationRel.VendorAndPurchaseFrom
    	TransientVendor							is a Vendor
    		derive value from DerivedVendor
    		default label is "Vendor"
    	TransientPurchaseFromLocation			is a PurchaseFromLocation
    		derive value from DerivedPurchaseFromLocation
    		default label is "PurchaseFromLocation"
    	TransientMinimumOrderQuantity			is an UnsignedQuantity
    		derive value from DerivedMinimumOrderQuantity
    		precision is TransientItem.NumberOfDecimalsQuantity
			default label is "MinimumOrderQuantity"
    	TransientLeadtimeDays					is a LeadtimeDays
    		derive value from DerivedLeadtimeDays
    		default label is "LeadtimeDays"
    	TransientSuggestedReorderPoint			is an UnsignedQuantity
    		derive value from DerivedSuggestedReorderPoint
    		precision is TransientItem.NumberOfDecimalsQuantity
    		default label is "SuggestedParLevel"
    	TransientStandardCost					is an InternationalCost
    		derive value from DerivedStandardCost
    		default label is "StandardCost"					
    	TransientManufacturer					is a Manufacturer
    		derive value from ItemLocationRel.Manufacturer
    		default label is "Manufacturer"
    	TransientManufacturerNumber				is a ManufacturerNumber
    		derive value from DerivedManufacturerNumber
    		default label is "ManufacturerNumber"
    	TransientParLocationCount				is a Quantity
    		derive value from DerivedParLocationCount
    		default label is "ParLocationCount"
    	TransientOnline							is Alpha size 1
    		derive value from DeriveReturnYes	
		TransientOrderMultiple					is an OrderMultiple
			derive value from DerivedOrderMultiple
			default label is "OrderMultiple"

	Field Rules
		Bin
			required
		TransientItem
			if (InventoryLocation.ParOrExchangeCart
			and !FromItemLocationCreate)
				required
		TransientParLevel
			if (InventoryLocation.ParOrExchangeCart
			and !FromItemLocationCreate)
				constraint (TransientParLevel entered)
					"ParLevelCannotBeBlankForParLocation"
		TransientManufacturer
			default to TransientItem.Manufacturer
		TransientManufacturerNumber
			default to TransientItem.ManufacturerNumber
    
	Conditions
		DuplicateBin
			restricted
			when (DuplicateBinRel exists)
			
		ReplenishmentSourceExists
			restricted
			when (ItemReplenishmentSourcesRel exists)

		BinGroupDetailExists
			restricted
			when (BinGroupDetail exists)

		LocationReplenished
			restricted
			when (TransientReplenishFrom entered)

		InvalidOnLocationItemUOM
			when (first ItemLocationRel.DefaultTransactionUOM.UnitOfMeasure entered
			and  ((LocationReplenished
			and   ItemLocationReplenishFromRel.HasItemLocationStockUOM
			and   not first LocationItemUOMForCompanyLocationUOMRel.OKForTransaction)
			or    (not LocationReplenished
			and   ItemLocationForReplenishSourceRel.HasItemLocationStockUOM
			and   not LocationItemUOMForInventoryLocationUOMRel.OKForTransaction)))

		LocationUOMRounded
			when (InvalidOnLocationItemUOM
			and   first ItemLocationRel.DerivedRequisitionLineUOM entered
			and   first ItemLocationRel.DerivedRequisitionLineUOM not = first ItemLocationRel.DefaultTransactionUOM.UnitOfMeasure)

		OrderMultipleRounding
			when (TransientOrderMultiple > 1)
	Local Fields
		LocalBin				is like Bin
		FromItemLocationCreate 	is Boolean
			
	Relations
		BinGroupDetailByDescendingRel
			one-to-many relation to BinGroupDetail
			Field Mapping uses BinSequenceByDescendingOrder
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.BinGroup			= BinGroup
			Instance Selection
				where (related.BinGroupDetail >= BinGroupDetail)
	
		DuplicateBinRel
			one-to-one relation to BinGroupDetail
			Field Mapping uses Set2
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.BinGroup			= BinGroup
				related.Bin					= TransientBin

		SameBinGroupDetailRel
			one-to-one relation to BinGroupDetail
			Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.BinGroup			= BinGroup
				related.BinGroupDetail		= TransientBinSequence
				
		ItemLocationRel
			one-to-many relation to ItemLocation
			Field Mapping uses Set8
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.PreferredBin		= TransientBin
		
		ItemLocationWithItemRel
			one-to-many relation to ItemLocation
			Field Mapping uses Set8
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.PreferredBin		= Bin
				related.Item				= TransientItem
						
		ShelfLocationRel
			one-to-one relation to Bin
			Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.Bin					= LocalBin
				
		ItemReplenishmentSourcesRel
            classic name is ITEMSRC
            one-to-many relation to ItemReplenishmentSource
            Field Mapping uses symbolic key
                related.Company           					= Company
                related.InventoryLocation 					= InventoryLocation
                related.Item             					= TransientItem

	 	ItemLocationReplenishFromRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           					= TransientReplenishFrom.ReplenishFromCompany
				related.InventoryLocation 					= TransientReplenishFrom.ReplenishFromLocation
				related.Item              					= TransientItem

		LocationItemUOMForCompanyLocationUOMRel
			one-to-many relation to LocationItemUOM
			Field Mapping uses symbolic key
				related.Company  							= TransientReplenishFrom.ReplenishFromCompany
				related.InventoryLocation 					= TransientReplenishFrom.ReplenishFromLocation
				related.Item								= TransientItem
				related.UnitOfMeasure						= TransientUOM

		ItemLocationForReplenishSourceRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company 							= InventoryLocation.FromCompany
				related.InventoryLocation					= InventoryLocation.ReplenishFromLocation
				related.Item								= TransientItem

		LocationItemUOMForInventoryLocationUOMRel
			one-to-many relation to LocationItemUOM
			Field Mapping uses symbolic key
				related.Company  							= InventoryLocation.FromCompany
				related.InventoryLocation 					= InventoryLocation.ReplenishFromLocation
				related.Item								= TransientItem
				related.UnitOfMeasure						= TransientUOM

		DefaultGeneralLedgerCategoryRel
            one-to-many relation to GeneralLedgerCategory
            Field Mapping uses symbolic key
                related.Company           					= Company
                related.InventoryLocation 					= InventoryLocation
			Instance Selection
				where (related.Default = true)
	Derived Fields
		DeriveReturnYes is a DerivedField
			type is Alpha size 1
			return "Y"
			
		DerivedBinSequence is a DerivedField
			type is like BinGroupDetail
			return BinGroupDetail
				
		DerivedParLevel is a DerivedField
			type is like UnsignedQuantity
			return ItemLocationRel.ReorderPoint
		
		DerivedParLocationCount is a DerivedField
			type is like Quantity
			if (action type.Update)
				return 0
			else
				return ItemLocationRel.ParLocationCount
		
		DerivedShelfLocation is a DerivedField
			type is like Bin
			return Bin
				
    	DerivedChargeable is a DerivedField						
			type is Boolean
			if (action type.Update)
				return blank
			else
				return ItemLocationRel.PatientChargeable

    	DerivedInventoryLocationAccountGroup is a DerivedField	
			type is like GeneralLedgerCategory
    		if (action type.Create)
				return DefaultGeneralLedgerCategoryRel.GeneralLedgerCategory
			else
				return ItemLocationRel.GeneralLedgerCategory
 
    	DerivedNoCharge is a DerivedField						
			type is Boolean
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.NoCharge
 
    	DerivedActive is a DerivedField							
			type is Boolean
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.Active
 
    	DerivedMinimumOrderQuantity is a DerivedField
			type is like UnsignedQuantity
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.MinimumOrderQuantity
    		
    	DerivedLeadtimeDays is a DerivedField	
			type is like LeadtimeDays
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.LeadtimeDays

    	DerivedSuggestedReorderPoint is a DerivedField
			type is like UnsignedQuantity
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.SuggestedReorderPoint

    	DerivedStandardCost is a DerivedField
			type is like InternationalCost
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.StandardCost
	
    	DerivedManufacturerNumber is a DerivedField
			type is like ManufacturerNumber
    		if (action type.Update)
				return blank
			else
				return ItemLocationRel.ManufacturerNumber
				
		DerivedItemDescription	is a DerivedField
			type is like Description
			default label is "Description"
			return first ItemLocationRel.Item.Description
			
		DerivedVendor	is a DerivedField
			type is like Vendor
			restricted
			if (action type.Update)
				return blank
			else
				return ItemReplenishmentSourcesRel.Vendor
		
		DerivedPurchaseFromLocation is a DerivedField
			type is like VendorLocation
			restricted
			if (action type.Update)
				return blank
			else
				return ItemReplenishmentSourcesRel.PurchaseFromLocation
				
		DerivedReplenishFrom	is a DerivedField
			type is AlphaUpper size 40
			if (action type.Update)
				return blank
			else
				return ItemReplenishmentSourcesRel.ReplenishFrom	

    	RoundingAlertMessage 				is a MessageField
			restricted
			"WillRoundToStockUOM<first ItemLocationRel.ReplenishmentSourceStockUOM>"

    	InvalidUOMMessage 					is a MessageField
			restricted
			"<TransientUOM>UOMDoesNotExistAt_Item_Location"

		RequisitionRoundedMessage 			is a MessageField
			"QuantityOnRequisitionLineHasBeenRoundedTo_<first ItemLocationRel.DerivedRequisitionLineQuantity>_<first ItemLocationRel.DerivedRequisitionLineUOM>"

		DerivedInvalidUOMText				is a DerivedField
			type is Alpha 100
			restricted
			if (ItemLocationRel.ReplenishmentUOMRounding)
				return RoundingAlertMessage
			else
				return InvalidUOMMessage
		
		RoundedToOrderMultiple				is a MessageField
			"HasOrderMultipleOf_<TransientOrderMultiple>"
		
		DerivedOrderMultiple				is a DerivedField
			type is like OrderMultiple
			restricted
			if (action type.Update)
				return 1
			else
				return ItemLocationRel.OrderMultiple
    Sets

        Set2
            indexed
            Sort Order
                Company
                InventoryLocation
                BinGroup
                Bin

		BinSequenceByDescendingOrder
			not indexed
			Sort Order
				Company
				InventoryLocation
				BinGroup
				BinGroupDetail descending
		
	Create Rules
		constraint (!DuplicateBin)
			"BinCannotBeDuplicated"		                          

	Actions
		Create is a Create Action
			Action Rules
				if (InventoryLocation.ParOrExchangeCart)	
					
					if (TransientOnline not entered)
						constraint (Bin not entered)
							"BinCannotBeEntered;PopulateTransientBinInstead"
						
						constraint (BinGroupDetail not entered)
							"BinGroupDetailCannotBeEntered;PopulateTransientBinSequenceInstead"
					
					BinGroup 			= InventoryLocation
					BinGroupDetail		= TransientBinSequence
					Bin					= TransientBin
					TransientActive 	= true
					
					constraint (ItemLocationRel not exists)
						"ShelfLocationAlreadyExists"
					
					if (!FromItemLocationCreate)
						invoke Create ItemLocation
							invoked.Company					= Company
							invoked.InventoryLocation		= InventoryLocation
							invoked.PreferredBin			= TransientBin
							invoked.Item					= TransientItem
							invoked.ReorderPoint			= TransientParLevel
							invoked.DefaultTransactionUOM	= TransientUOM
							invoked.GeneralLedgerCategory	= TransientInventoryLocationAccountGroup
							invoked.PatientChargeable		= TransientChargeable
							invoked.NoCharge				= TransientNoCharge
							invoked.Active					= TransientActive
							invoked.MinimumOrderQuantity	= TransientMinimumOrderQuantity
							invoked.LeadtimeDays			= TransientLeadtimeDays
							invoked.SuggestedReorderPoint	= TransientSuggestedReorderPoint
							invoked.StandardCost			= TransientStandardCost
							invoked.Manufacturer			= TransientManufacturer
							invoked.ManufacturerNumber		= TransientManufacturerNumber
							invoked.VendorAndPurchaseFrom	= TransientVendorAndPurchaseFrom
							invoked.ReplenishFrom			= TransientReplenishFrom
							invoked.OrderMultiple			= TransientOrderMultiple
							invoked.CreatedFromBinGroupDetail		= true							
							invoked.AllowReceipt					= true
							invoked.AllowIssue						= true

		QuickCreate is a Create Action	
			restricted
			bypass field rules
		
		ParLocationUpdate	is an Update Action
			valid when (InventoryLocation.ParOrExchangeCart)
			Exit Rules
				invoke ParLocationUpdate first ItemLocationRel 
					invoked.ParLocationCount	= TransientParLocationCount

		Update is an Update Action
			Action Rules
				if (InventoryLocation.ParOrExchangeCart)
					
					if (TransientOnline not entered)
						constraint (Bin not changed)
							"BinCannotBeChanged;ChangeTransientBinInstead"
						
						constraint (BinGroupDetail not changed)
							"BinGroupDetailCannotBeChanged;ChangeTransientBinSequenceInstead"
					
					if (Bin != TransientBin)
						LocalBin = TransientBin
						if (ShelfLocationRel exists)
							constraint (ItemLocationRel not exists)
								"ShelfLocationAlreadyExists"
						else
							invoke Create Bin
								invoked.Company					= Company
								invoked.InventoryLocation		= InventoryLocation
								invoked.Bin						= TransientBin
								invoked.Active					= true
	
					if (BinGroupDetail != TransientBinSequence)
						constraint (SameBinGroupDetailRel not exists)
							"BinSequenceAlreadyExists" 
	
					invoke Update ItemLocationWithItemRel			
						if (TransientOnline entered
						or  TransientBin entered)
							invoked.PreferredBin			= TransientBin
						if (TransientOnline entered
						or  TransientParLevel entered)
							invoked.ReorderPoint			= TransientParLevel
						if (TransientOnline entered
						or  TransientUOM entered)
							invoked.DefaultTransactionUOM	= TransientUOM
						if (TransientOnline entered
						or  TransientInventoryLocationAccountGroup entered)	
							invoked.GeneralLedgerCategory	= TransientInventoryLocationAccountGroup
						if (TransientOnline entered
						or  TransientChargeable)
							invoked.PatientChargeable		= TransientChargeable
						if (TransientOnline entered
						or  TransientNoCharge)
							invoked.NoCharge				= TransientNoCharge
						if (TransientOnline entered
						or  TransientActive)
							invoked.Active					= TransientActive
						if (TransientOnline entered
						or  TransientMinimumOrderQuantity entered)
							invoked.MinimumOrderQuantity	= TransientMinimumOrderQuantity
						if (TransientOnline entered
						or  TransientLeadtimeDays entered)
							invoked.LeadtimeDays			= TransientLeadtimeDays
						if (TransientOnline entered
						or  TransientSuggestedReorderPoint entered)
							invoked.SuggestedReorderPoint	= TransientSuggestedReorderPoint
						if (TransientOnline entered
						or  TransientStandardCost entered)
							invoked.StandardCost			= TransientStandardCost
						if (TransientOnline entered
						or  TransientManufacturer entered)
							invoked.Manufacturer			= TransientManufacturer
						if (TransientOnline entered
						or  TransientManufacturerNumber entered)
							invoked.ManufacturerNumber		= TransientManufacturerNumber
						if (TransientOnline entered
						or  TransientVendorAndPurchaseFrom entered)
							invoked.VendorAndPurchaseFrom	= TransientVendorAndPurchaseFrom
						if (TransientOnline entered
						or  TransientReplenishFrom entered)
							invoked.ReplenishFrom			= TransientReplenishFrom
						if (TransientOnline entered
						or	TransientOrderMultiple entered)
							invoked.OrderMultiple			= TransientOrderMultiple
						invoked.LocalFromBinGroupDetail		= true
							
					invoke Update ItemReplenishmentSourcesRel
						if (TransientOnline entered
						or TransientVendor entered)	
							invoked.Vendor						= TransientVendor
						if (TransientOnline entered
						or TransientPurchaseFromLocation entered)
							invoked.PurchaseFromLocation		= TransientPurchaseFromLocation
						if (TransientOnline entered
						or TransientReplenishFrom entered)
							invoked.ReplenishFrom				= TransientReplenishFrom
						
	
					Bin 			= TransientBin
					BinGroupDetail 	= TransientBinSequence
			Exit Rules
				if (InventoryLocation.ParOrExchangeCart
				and Bin changed)
					LocalBin	= old Bin
					invoke Delete ShelfLocationRel
				
		FastUpdate is an Update Action
			restricted
			bypass field rules
				
		Delete is a Delete Action
			Exit Rules
				if (not InventoryLocation.LocationType.InventoryLocation)
					invoke DeleteParItemLocation ItemLocationRel

		Purge is a Purge Action

		PurgeRecord is an Instance Action
			Action Rules
				invoke Purge
			Exit Rules
				if (not InventoryLocation.LocationType.InventoryLocation)
					invoke DeleteParItemLocation ItemLocationRel
		
		DeleteBinGroupDetailOnly is a Delete Action
			restricted
			




   		FreezeBinGroups is a Set Action
   			restricted
   			Parameters
   				PrmCompany								is an InventoryCompany
   				PrmSelectId								is a PhysicalInventorySelect
   				PrmBinGroupBusinessClassGroup			is a BinGroup group		
   				
   			Local Fields
   				LocalFreezePageNumber						is Numeric size 4
 				LocalLocationMatchesLocationInReportGroup	is Boolean
				 				 					
   			Instance Selection
   				where (Company = PrmCompany
   				and    BinGroup	within PrmBinGroupBusinessClassGroup)
   				
   			Sort Order
   				Company
   				InventoryLocation
   				BinGroup
   				
			Action Rules
				Company Set Rules
					Entrance Rules
						initialize LocalFreezePageNumber
					
				BinGroup Set Rules
					Entrance Rules
						LocalFreezePageNumber			= 1
						
				Instance Rules


					if (PrmSelectId.ReportGroup entered)
						initialize LocalLocationMatchesLocationInReportGroup
						for each PrmSelectId.InventoryReportGroupLocationSelectRel
							if (InventoryLocation		= each.InventoryLocation)
								LocalLocationMatchesLocationInReportGroup	= true
								end for each
							
					if ((PrmSelectId.AllLocations)
					or  (PrmSelectId.ReportGroup entered				
					and  LocalLocationMatchesLocationInReportGroup))

						initialize TransientCreateCount
						if (PrmSelectId.FreezeEmptyBins)
							invoke FreezeZeroStockOnHandBins ItemLocation
								invoked.PrmCompany				= Company
								invoked.PrmSelectId				= PrmSelectId
								invoked.PrmLocation				= InventoryLocation
								invoked.PrmBinGroup				= BinGroup
								invoked.PrmBin					= Bin
								invoked.PrmFreezePageNumber		= LocalFreezePageNumber
						invoke FreezeStockOnHandDetail StockOnHandDetail		
							invoked.PrmCompany				= Company
							invoked.PrmSelectId				= PrmSelectId
							invoked.PrmLocation				= InventoryLocation
							initialize invoked.PrmItem
							invoked.PrmBinGroup				= BinGroup
							invoked.PrmBin					= Bin
							invoked.PrmFreezePageNumber		= LocalFreezePageNumber
							initialize invoked.PrmSkipConstraints
		
		InsertLinesAbove	is an Instance Action
			Parameters
				PrmInsertLines		is Unsigned Decimal size 2.0
					default label is "NumberOfLinesToInsert"
			
			Parameter Rules
				PrmInsertLines
					initial value is 1
					required
			
			Local Fields
				LocalCompany				is like Company
				LocalInventoryLocation		is like InventoryLocation
				LocalBinGroup				is like BinGroup
				LocalBin					is like Bin
				LocalBinSequence			is like BinGroupDetail
			
			Action Rules
				LocalCompany			= Company
				LocalInventoryLocation	= InventoryLocation
				LocalBinGroup			= BinGroup
				for each BinGroupDetailByDescendingRel
					LocalBin			= each.Bin
					LocalBinSequence	= each.BinGroupDetail
					
					invoke DeleteBinGroupDetailOnly each
					invoke QuickCreate BinGroupDetail
						invoked.Company				= LocalCompany
						invoked.InventoryLocation	= LocalInventoryLocation
						invoked.BinGroup			= LocalBinGroup
						invoked.Bin					= LocalBin
						invoked.BinGroupDetail		= LocalBinSequence + PrmInsertLines
					
							
							
FileCreationLogic.BinGroupDetail is a BusinessClass

	Persistent Fields
	
	Local Fields

		LocalFinanceEnterpriseGroup is like FinanceEnterpriseGroup
		LocalFileCreationSetup		is like FileCreationSetup
		LocalCompany				is like Company
		LocalInventoryLocation		is like InventoryLocation
		LocalBinGroup				is like BinGroup
					
	Relations

		FileCreationRecordNotProcessedRel
			one-to-many relation to FileCreationRecord
			Field Mapping uses ByStatus
				related.FinanceEnterpriseGroup 	= LocalFinanceEnterpriseGroup
				related.FileCreationSetup		= LocalFileCreationSetup
				related.Status					= 1
				related.Key1					= LocalCompany
				related.Key2					= LocalInventoryLocation
				related.Key3					= LocalBinGroup

		FileCreationRecordErrorRel
			one-to-many relation to FileCreationRecord
			Field Mapping uses ByStatus
				related.FinanceEnterpriseGroup 	= LocalFinanceEnterpriseGroup
				related.FileCreationSetup		= LocalFileCreationSetup
				related.Status					= 2
				related.Key1					= LocalCompany
				related.Key2					= LocalInventoryLocation
				related.Key3					= LocalBinGroup
		
	Derived Fields
		BinGroupHash is a NativeField
			type is Numeric 8
			restricted
				
	Actions
	
		GeneratePreviewRecordsForRunGroup is a Set Action
			restricted
			synchronized on untranslatable:"BinGroupDetail_GeneratePreviewRecordsForRunGroup_<FinanceEnterpriseGroupPrm>_<PrmRunGroup>"

			Parameters
				FinanceEnterpriseGroupPrm		is a FinanceEnterpriseGroup
				FileCreationSetupPrm	is a FileCreationSetup
				BeginTimeStamp			is TimeStamp
				EndTimeStamp			is TimeStamp
				PrmNumberOfRunGroups	is Numeric 3
				PrmRunGroup				is Numeric 3

			Local Fields
				InBinGroupGroup					is Boolean
				LocalBinGroupGroupSelection		is a BinGroup group in subject FileCreationBinGroup

			Instance Selection
				where (Company.FinanceEnterpriseGroup = FinanceEnterpriseGroupPrm
				and    (PrmNumberOfRunGroups <= 1
				or      BinGroupHash % PrmNumberOfRunGroups = PrmRunGroup)
				and	   update stamp > BeginTimeStamp
				and	   update stamp <= EndTimeStamp)

			Action Rules
				Instance Rules
					LocalFinanceEnterpriseGroup		= FinanceEnterpriseGroupPrm
					LocalFileCreationSetup	= FileCreationSetupPrm
					LocalCompany			= Company
					LocalInventoryLocation	= InventoryLocation
					LocalBinGroup			= BinGroup
					if (FileCreationRecordNotProcessedRel not exist
					and	FileCreationRecordErrorRel not exist)	
						LocalBinGroupGroupSelection = FileCreationSetupPrm.EligibilityGroup
						if (LocalBinGroupGroupSelection entered)
							InBinGroupGroup = false
							if (BinGroup within LocalBinGroupGroupSelection)
								InBinGroupGroup = true

						if (LocalBinGroupGroupSelection not entered
						or	InBinGroupGroup)											
							invoke Create FileCreationRecord
								invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroupPrm
								invoked.FileCreationSetup	= FileCreationSetupPrm
								invoked.Key1				= Company
								invoked.Key2				= InventoryLocation
								invoked.Key3				= BinGroup
								invoked.SystemTimeStamp		= EndTimeStamp
								invoked.EffectiveDate		= system current corporate date
								invoked.Status				= 1
