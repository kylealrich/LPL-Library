ProcurementCardProgram is a BusinessClass
    owned by po
    prefix is PDP
    classic name is PDPROGRAM

    Ontology
        symbolic key is ProcurementCardProgram
            classic set name is PDPSET1
            classic name is PCARD-PROGRAM

    Patterns
        implements StaticJava
        disable AuditIndex
	
	Local Fields
		
    Persistent Fields

        Description
        Active
            classic name is STATUS
        ProcurementCardProgramEffectiveDate       is Date
            sql name is PCardProgramEffectiveDate
            classic name is EFFECT-DATE
        ProcurementCardProgramAdministrator       is a ProcurementCardUser
            sql name is PCardProgramAdministrator
            classic name is ADMIN-USER-ID
        ProcurementCardProgramAdministratorName   is AlphaUpper size 30
            sql name is PCardProgramAdministratorName
            classic name is ADMIN
        PhoneNumber                               is a TelephoneNumber 
			restricted
        	holds pii
            classic name for PhoneNumber.InternationalPrefix is PREFIX
            classic name for PhoneNumber.SubscriberNumber is PHONE
            classic name for PhoneNumber.Extension is EXTENSION
        EmailAddress							  is an EmailAddressMulti 
			restricted
        	holds pii
            classic name is EMAIL
        Vendor
        MaskProcurementCardNumberDisplay          is Boolean
            classic name is MASK-FL
        MaskProcurementCardNumberEntry		      is Boolean
        InvCreateFl                               is AlphaUpper size 1
        	States
        		Yes									value is "Y"
        		No									value is "N"
        RequireReasonOnDispute                    is Boolean
            classic name is DISP-REASON-FL
        DispExpFl                                 is AlphaUpper size 1
        	States
        		Yes									value is "Y"
        		No									value is "N"
        LastProcurementCardRequest                is like ProcurementCardRequest
            classic name is LAST-REQUEST
            disable Auditing
        AutomaticallyApproveProcurementCardCharge is AlphaUpper size 1
            sql name is AApproveProcurementCardCharge
            classic name is CHARGE-APPR-FL
            States
                No               value is "N"
                POChargesOnly    value is "P"
                AdhocChargesOnly value is "A"
                Both             value is "B"
        AutomaticallyAddMerchant                  is Boolean
            classic name is AUTO-ADD
        AutomaticallyCloseProcurementCardCharge   is AlphaUpper size 1
            sql name is ACloseProcurementCardCharge
            classic name is CHARGE-CLS-FL
            States
                No               value is "N"
                POChargesOnly    value is "P"
                AdhocChargesOnly value is "A"
                Both             value is "B"
        TrackCardsFl                              is AlphaUpper size 1
        	States
        		Yes									value is "Y"
        		No									value is "N"
        AutomaticallyAddProcurementCardUser       is Boolean
            sql name is AAddProcurementCardUser
            classic name is AUTO-ADD-USER
        
            

	Sets
		ByAdministrator
			indexed
			Sort Order
				ProcurementGroup
				ProcurementCardProgramAdministrator
				ProcurementCardProgram

    Relations

        ProcurementCardRequestsRel
            one-to-many relation to ProcurementCardRequest
            delete cascades
            Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
                related.ProcurementCardProgram	= ProcurementCardProgram

        ProcurementCardProgramCompaniesRel
            one-to-many relation to ProcurementCardProgramCompany
            delete cascades
            Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
                related.ProcurementCardProgram 	= ProcurementCardProgram
                
        NewOrUnapprovedProcurementCardRequestsRel
			one-to-many relation to ProcurementCardRequest
        	Field Mapping uses symbolic key  
				related.ProcurementGroup		= ProcurementGroup
             	related.ProcurementCardProgram 	= ProcurementCardProgram
         	Instance Selection
         		where (related.Status.New
         		or     related.Status.Unapproved) 

   		RequesterApproverRel
			one-to-one relation to RoleSecurityClass
			Field Mapping uses symbolic key
				related.Role				= "Requester_ST"
				related.DataArea			= parentcontext.dataarea 
				related.SecurityClass 		= "POProcurementCardApproval_ST"	

		ProcurementCardRequestsForUserRel
			one-to-many relation to ProcurementCardRequest
        	Field Mapping uses symbolic key  
				related.ProcurementGroup		= ProcurementGroup
             	related.ProcurementCardProgram 	= ProcurementCardProgram
			Instance Selection
				where (related.ProcurementCardUser 		= actor.agent(Employee).Employee
				or     related.ProcurementCardProxyUser = actor.agent(Employee).Employee)
				
		ProcurementCardStatementTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses symbolic key
				related.ProcurementGroup		= ProcurementGroup
				related.ProcurementCardProgram 	= ProcurementCardProgram
			Instance Selection
				where (related.Status.Unreleased
				or    related.Status.Released
				or    related.Status.Approved
				or    related.Status.Disputed
				or    related.Status.Rejected)
	
	Derived Fields
		Status is a DerivedField
			type is Alpha 10
			if (Active)
				return ActiveStatus
		
		CancelledStatus	is a DerivedField
			type is Alpha 10
			if (!Active)	
				return Cancelled
				
		Cancelled is a MessageField
			restricted
			"Cancelled"
			
		ActiveStatus is a MessageField
			restricted
			"Active"
		
		DerivedCardProgramName is a MessageField
			default label is "Program"
			"<ProcurementCardProgram>_-_<Description>"
						
	Attach Rules
		constraint (Active)
			"ProcurementCardProgram<ProcurementCardProgram>MustBeActive"			
				
   
    Field Rules
			
		Description
			required
				
	 	ProcurementGroup
            required          
        	
    	ProcurementCardProgramEffectiveDate
			required
				
		ProcurementCardProgramAdministrator
			required
				
			constraint (ProcurementCardProgramAdministrator.Administrator)
				"ProcurementCardProgramAdministratorIsNotSetupAsAdministratorInProcurementCardUser"              //"@e.PD01.116"
		
		Vendor
			required
				
        AutomaticallyApproveProcurementCardCharge
        	initial value is AutomaticallyApproveProcurementCardCharge.No
            default to AutomaticallyApproveProcurementCardCharge.No
        
    	AutomaticallyCloseProcurementCardCharge
        	initial value is AutomaticallyCloseProcurementCardCharge.No
        	default to AutomaticallyCloseProcurementCardCharge.No
        
    Actions
    
        Create is a Create Action
        
			Action Rules
			
				if (RequesterApproverRel !exists)
					invoke Create RoleSecurityClass
						invoked.Role				= "Requester_ST"
						invoked.DataArea			= parentcontext.dataarea
						invoked.SecurityClass		= "POProcurementCardApproval_ST"
				
        	
        Update is an Update Action
        
        	Action Rules
        		constraint (Active)
					"CannotChange,ProgramIsCancelled"                                                //"@e.PD01.110"
				
        Delete is a Delete Action
        
        Cancel is an Instance Action
        	valid when (Active)
        
			Action Rules
				  
				constraint (ProcurementCardStatementTransactionsRel not exists)                           
					"CannotBeCancelled;ProcurementCardProgramDoesNotExistInProcurementCardStatementTransactionInClosedAndCancelledState"		               //"@e.PD01.108"

				for each ProcurementCardProgramCompaniesRel
					constraint (!each.IsPdcardcompany)
						"CannotBeCancelled;ProcurementCardProgramExistsInProcurementCardCompany" 		// "@e.PD01.109"
						
				constraint (NewOrUnapprovedProcurementCardRequestsRel not exists)
					"CannotBeCancelled;ProcurementCardProgramExistsInProcurementCardRequestInNewOrUnapprovedState" 	//"@e.PD01.107"
						
				Active = false
				
					
