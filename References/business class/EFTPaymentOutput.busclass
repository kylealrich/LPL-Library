EFTPaymentOutput is a BusinessClass
	owned by ar
	prefix is EFTP

	Ontology
		symbolic key is EFTPaymentOutput

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		ACHFileName				is AlphaUpper size 50
		BACSFileName			is AlphaUpper size 50
		NonTapeFileName			is AlphaUpper size 50
		SEPAFileName			is AlphaUpper size 50
		CreateDate				is TimeStamp

		ProcessLevel			is a ReceivableProcessLevel

		AchTapeFile				is a PaymentAttachment
		BacsTapeFile			is a PaymentAttachment
		NonTapeFile				is a PaymentAttachment
		SepaFile				is a PaymentAttachment
		ProcessedByThirdParty	is Boolean
		InProcess				is Boolean
		EFTPaymentCreationResult	is like EFTPaymentCreationResult

	Transient Fields

	Local Fields

	Derived Fields
		ACHFileNameOutputFileName is a StringField
			type is Alpha 100
			ACHFileName
			"-"
			"ACHTAPE"

		BACSFileNameOutputFileName is a StringField
			type is Alpha 100
			BACSFileName
			"-"
			"BACSTAPE"

		NonTapeFileNameOutputFileName is a StringField
			type is Alpha 100
			NonTapeFileName
			"-"
			"NONTAPE"

		SEPAFileNameOutputFileName is a StringField
			type is Alpha 100
			SEPAFileName
			"-"
			"SEPATAPE"

	Conditions
		HasACHTapeFileAttachment
			restricted
			when (AchTapeFile entered)
		HasACHTapeData
			restricted
			when (ACHTapeDataRel exists)

		HasBACSTapeFileAttachment
			restricted
			when (BacsTapeFile entered)
		HasBACSTapeData
			restricted
			when (BACSTapeDataRel exists)

		HasNonTapeFileAttachment
			restricted
			when (NonTapeFile entered)
		HasNonTapeData
			restricted
			when (NonTapeDataRel exists)

		HasSEPATapeFileAttachment
			restricted
			when (SepaFile entered)
		HasSEPAData
			restricted
			when (SEPADataRel exists)

	Relations
		ACHTapeDataRel is a EFTPaymentOutputDetail set
			Instance Selection
				where (related.Type.ACHTape)

		BACSTapeDataRel is a EFTPaymentOutputDetail set
			Instance Selection
				where (related.Type.BACSTape)

		NonTapeDataRel is a EFTPaymentOutputDetail set
			Instance Selection
				where (related.Type.NonTape)

		SEPADataRel is a EFTPaymentOutputDetail set
			Instance Selection
				where (related.Type.SEPA)

	Sets
		ByCreateDate
			Sort Order
				CreateDate descending
				Company
				EFTPaymentOutput

		ByProcessedByThirdParty
			Sort Order
				ProcessedByThirdParty descending
				Company
				EFTPaymentOutput

		ByEFTPaymentCreationResult	
			Sort Order
				EFTPaymentCreationResult descending
				Company
				EFTPaymentOutput


	Field Rules
		CreateDate
			default to current timestamp 

		ACHFileName
			default to Company + "-" + ProcessLevel
		BACSFileName
			default to Company + "-" + ProcessLevel
		NonTapeFileName
			default to Company + "-" + ProcessLevel
		SEPAFileName
			default to Company + "-" + ProcessLevel

	Actions
		Create is a Create Action
			restricted
			Action Rules
				InProcess = true

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted

		FinishProcessing is an Instance Action
			restricted
			Action Rules










				InProcess = false

		CreateEFTPaymentFiles is an Instance Action
			Action Rules
				trigger "EFTPaymentFiles" PA service
					resume on error
					title is "CreateEFTPaymentFiles"
					Variables
						Company
						EFTPaymentOutput

		ResetProcessedByThirdParty is an Instance Action								
			valid when (ProcessedByThirdParty)											
			Action Rules																
				ProcessedByThirdParty = false											

		PurgeEFTPaymentOutputRecords is a Set Action
			run in foreground
			completion message is "PurgeOfPaymentOutputRecordsHasCompleted"
			Parameters
				PrmReceivableCompany						is a ReceivableCompany
				PrmCreateDateRange							is a DateRange 

			Parameter Rules
				PrmCreateDateRange
					required
				PrmReceivableCompany
					required
			Instance Selection
				where (CreateDate date within PrmCreateDateRange
				and	   Company = PrmReceivableCompany)
	
			Action Rules
				Instance Rules
					invoke Purge
