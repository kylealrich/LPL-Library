PickShipRoute is a BusinessClass
    owned by oe
    prefix is ORT
    classic name is OEROUTE

    Ontology
        symbolic key is PickShipRoute
            classic set name is ORTSET1
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex
        implements ForceUIRefreshOnStale

    Persistent Fields

        Customer
        CustomerShipTo
            classic name is SHIP-TO
        CompanyCustomer
        RequestingLocation
            default label is "RequesterShipTo"    
        CategoryType                        is Numeric 1
            States
                CustomerOrder               value is 0
                Requisition                 value is 1  
    Conditions

        IsRteStpCompny
        	restricted
            when (OecompanyRel.RouteAndStop)

        IsShiptoEntrd
        	restricted
            when (CustomerShipTo >  blank)

        IsTest
        	restricted
            when (Company = 4)

        RecordExists
			restricted
			when (PickShipRoute exists)

        StartedOrInTransitVehicleTripExists
            restricted
            when (StartedOrIntransitVehicleTripLineRel exists)

    Relations

        ArcompRel
            one-to-one relation to ReceivableCompany
            required
            Field Mapping uses symbolic key
                related.Company = Company

        ArcustomerRel
            one-to-one relation to CompanyCustomer
            required
            Field Mapping uses symbolic key
                related.Company  = Company
                related.Customer = Customer

        OecompanyRel
            one-to-one relation to BillingCompany
            required
            Field Mapping uses symbolic key
                related.Company = Company

        ShiptoRel
            one-to-one relation to CustomerShipTo
            valid when (IsShiptoEntrd)
            required
            Field Mapping uses symbolic key
                related.Company        = Company
                related.Customer       = Customer
                related.CustomerShipTo = CustomerShipTo

        CustdescRel
            one-to-one relation to Customer
            required
            Field Mapping uses symbolic key
                related.CustomerGroup = ArcompRel.CustomerGroupField.CustomerGroup
                related.Customer      = Customer
                
        StartedOrIntransitVehicleTripLineRel
            one-to-many relation to VehicleTripLine
            Field Mapping uses symbolic key
                related.Company              = Company
                related.InventoryLocation    = InventoryLocation
            Instance Selection
                where (related.PickShipRoute = PickShipRoute 
                and   (related.VehicleTrip.Status.Started 
                or     related.VehicleTrip.Status.InTransit))

    Field Rules
        RequestingLocation
            if (StartedOrInTransitVehicleTripExists)
                cannot be changed
                    "CannotChange;_StartedOr_In-_Transit_Vehicle_TripExists"
    Sets

        Set2
            indexed
            Sort Order
                Company
                InventoryLocation
                Customer
                CustomerShipTo
                PickShipRoute

    Actions
        Create is a Create Action

        Update is an Update Action
        
        Delete is a Delete Action
        
        CreateShipmentRoute is a Create Action
            Field Rules
                CategoryType
                    force default to CategoryType.Requisition
