RecipeInstruction is a BusinessClass
	owned by recipe
	
	prefix is RCPI
	
	Ontology
		symbolic key is RecipeInstruction
	
	Patterns
		disable AuditIndex
		
	Persistent Fields
		Sequence
			disable Auditing
		Instruction 			is a Description3
		Attachment				is an AlternateAttachment

	Local Fields
		LocalAttributeCtr   is Numeric 2			

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Field Rules
		Sequence
			if (action type.Create)
				autosequence using Recipe.InstructionCount
			
			constraint (Sequence <= Recipe.InstructionCount)
				"LastSequenceIs<Recipe.InstructionCount>"
				
		Instruction
			required

	Sets
		BySequence
			duplicates
			Sort Order
				ItemGroup
				Item
				Sequence
				RecipeInstruction
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
			Entrance Rules
				invoke SystemUpdate Recipe
					invoked.InstructionCount			-= 1
					
		SystemUpdate is an Update Action
			restricted
			bypass field rules
		
		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
