EFTPaymentCreationResult is a BusinessClass
	owned by ar
	
	prefix is EFTPC
	
	Ontology
		symbolic key is EFTPaymentCreationResult

	Patterns


		disable EffectiveDated
		disable DataTranslations

	Persistent Fields
		EftCompany					is a ReceivableCompany
		PostingDate					is Date
		DepositDate					is Date
		DebitCashCode				is a CashCode
		CreditCashCode				is a CashCode
		ProcessLevel				is a ReceivableProcessLevel
		SelectProcessLevel			is a ReceivableProcessLevel
		BankTransactionCode
		TermsCode
		NotAppldCd					is a ReceivableGeneralLedgerCode
		DiscReasCd					is a ApplicationAdjustmentReason
		OprCode						is like ReceivableOperator
		Reference					is AlphaUpper size 10
		CompanyIdOption				is AlphaUpper size 1
			States
				CompanyAssigned		value is "0"
				DUNS				value is "3"
				IRSID				value is "1"
					default label is "IRS ID"
				UserAssigned		value is "9"
		FillLastBlock				is AlphaUpper size 1
			States
				No					value is "N"
				Yes 				value is "Y"
		BalancedTape				is AlphaUpper size 1
			States
				No					value is "N"
				Yes 				value is "Y"
		IncludeAddenda				is AlphaUpper size 1
			States
				No					value is "N"
				Yes 				value is "Y"
		BankCharges					is Decimal size 16.2
		GLAccount					is a FinanceCodeBlock

		CbUserField1				is AlphaUpper size 30
		CbUserField2				is AlphaUpper size 30
		CbUserField3				is AlphaUpper size 30
		CbUserField4				is AlphaUpper size 30

		ACHFileName					is AlphaUpper size 50
		BACSFileName				is AlphaUpper size 50
		NonTapeFileName				is AlphaUpper size 50
		SEPAFileName				is AlphaUpper size 50

		ReportByCompany				is Boolean
		RunTime						is TimeStamp
		RecordsExists				is Boolean
		Status						is Numeric 1
			States
				InProcess			value is 0
				Complete			value is 1
		BackgroundGroupAsyncId		is an AsyncActionRequest
			delete ignored

		CreditAmount				is an InternationalAmount
			precision is EftCompany.BaseNumberOfDecimals
		DebitAmount					is an InternationalAmount
			precision is EftCompany.BaseNumberOfDecimals
		NetBalance					is an InternationalAmount
			precision is EftCompany.BaseNumberOfDecimals

	Transient Fields
		TransientIncludeSummarizeDetailPanel	is Boolean	
			default label is "IncludeSummarizeDetailPanel"	
	Field Rules
		EftCompany
			required

		PostingDate
			if (CompanySystemClosingControlRel.Control and EftCompany.VerifyGLDateWithinGLDateRange)
				constraint (PostingDate within CompanySystemClosingControlRel.ValidEntryDate)
					"PostDateIsNotWithinValidEntryDatesForCompany;ValidDateRangeIs<CompanySystemClosingControlRel.ValidEntryDate.Begin>-<CompanySystemClosingControlRel.ValidEntryDate.End>"




		DebitCashCode
			if (EftCompany.ReceivableCompany.RequirePaymentCashCodes)
				required
		CreditCashCode
			if (EftCompany.ReceivableCompany.RequirePaymentCashCodes)
				required
		ProcessLevel
			required














		BankTransactionCode
			constraint (BankTransactionCode.BankTransactionType = "C" and BankTransactionCode.TransactionOrigin = "R")

				"Bank_\Transaction_\CodeMustHaveACashPaymentCategoryAnd_\ReceivableOrigin"



		DiscReasCd
			required
				"DiscountReasonCodeRequired"
			constraint (DiscReasCd != "CURR")
				"CURRIsAReservedReasonCode"
			constraint (DiscReasCd.ReceivableAdjustmentReasonType.Discount)
				"TheAssignedReceivableReasonMustHaveAReceivableAdjustmentReasonTypeOfDiscount"








		CompanyIdOption
			initial value is "1"
			default to "1"
		FillLastBlock
			initial value is "N"
			default to "N"
		BalancedTape
			initial value is "N"
			default to "N"

		IncludeAddenda
			initial value is "N"
			default to "N"

		RunTime
			default to current timestamp

	Conditions
		IsSummarizedEFTPaymentOutputCreated
			restricted
			when (EFTPaymentOutputRel exists
			and any EFTransactionRel.ReceivableInvoiceDetail.SummarizeDetail entered)
		IsNonSummarizedEFTPaymentOutputCreated
			restricted
			when (EFTPaymentOutputRel exists
			and any EFTransactionRel.ReceivableInvoiceDetail.SummarizeDetail not entered)
		IsEFTPaymentOutputCreated
			restricted
			when (EFTPaymentOutputRel exists)
		IsSummarize
			restricted
			when (any EFTransactionRel.ReceivableInvoiceDetail.SummarizeDetail entered)		

	Local Fields
		BackgroundGroup							is AlphaUpper up to 60
		WsCreateDate							is TimeStamp
		LocalActor								is Actor

		LocalActualBankDayNumber				is Numeric 8
		LocalBankDate							is Date
		LocalEFTCalendar						is like SystemCalendar
		LocalGlDays								is like DueDays
		LocalDays								is like DueDays
		LocalGlType								is Alpha size 1
		LocalDepositDays						is like DepositDays
		LocalDepositType						is Alpha size 1
		LocalFromCurrency						is like Currency
		LocalToCurrency 						is like Currency
		LocalCurrency							is like Currency
		LocalCashCode							is like CashCode
		LocalCompany							is like ReceivableCompany

	Derived Fields
		ActualBankDayNumber 					is a DerivedField
			type is Date
			restricted
			if (ActualBankDayNumberRel exists)
				LocalActualBankDayNumber 									 = 0
				for each ActualBankDayNumberRel
					if (LocalActualBankDayNumber 							<= LocalDays)
						LocalActualBankDayNumber 							+= 1
						LocalBankDate 										 = each.SystemCalendarDate
					else
						end for each
				return LocalBankDate
			else
				initialize LocalActualBankDayNumber
				return current date + LocalGlDays
		
		DerivedRunDate is a DerivedField	
			type is Date
			restricted
			return RunTime date

	Relations
		ActualBankDayNumberRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup										= FinanceEnterpriseGroup
				related.SystemCalendar 										= LocalEFTCalendar
			Instance Selection
				where (related.IsBankDay
				and	   related.SystemCalendarDate >= current date)

		CompanySystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode	 = "AR"
				related.Company					 = EftCompany

		EFTransactionRel
			one-to-many relation to ElectronicFundsTransferTransaction
			Field Mapping uses ByEFTPaymentCreationResult	
				related.EFTPaymentCreationResult	= EFTPaymentCreationResult	
				related.CustomerGroup				= EftCompany.CustomerGroupField.CustomerGroup
				related.ProcessingCompany 			= EftCompany



		EFTPaymentOutputRel
			one-to-many relation to EFTPaymentOutput
			Field Mapping uses ByEFTPaymentCreationResult	
				related.EFTPaymentCreationResult = EFTPaymentCreationResult	



		ACHEFTNotifyRel
			one-to-many relation to ReceivableElectronicFundsTransferNotify
			Field Mapping uses symbolic key
				related.CustomerGroup			= EftCompany.CustomerGroupField.CustomerGroup
			Instance Selection
				where ((ReportByCompany 
				and		related.Company = EftCompany)
				or		(!ReportByCompany
				and		related.Company not entered)
				and		related.PaymentFormat = "A")
		
		ReceivableCompanyRel
			one-to-many relation to ReceivableCompany
			Field Mapping uses Set2
				related.CustomerGroupField.CustomerGroup       =  EftCompany.CustomerGroupField.CustomerGroup
		
		ReceivableReasonRel
			one-to-one relation to ReceivableReason
			Field Mapping uses symbolic key
				related.Company 				 = LocalCompany
				related.ReceivableReasonType	 =	"AD"
				related.ReceivableReason		 =	DiscReasCd







		ReceivableOperatorRel
			one-to-one relation to ReceivableOperator
			Field Mapping uses symbolic key
				related.Company					 = EftCompany
				related.ReceivableOperator		 = OprCode

		CurrencyRelationshipRel
			one-to-one relation to CurrencyRelationship
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.CurrencyRelationship.FromCurrency	= LocalFromCurrency
				related.CurrencyRelationship.ToCurrency		= LocalToCurrency

		CompanyCashCodeRel
			one-to-one relation to CompanyCashCode
			Field Mapping uses symbolic key
				related.Company			= EftCompany
				related.CashCode		= LocalCashCode

		Work1AR115Rel
			one-to-many relation to Work1AR115
			Field Mapping uses ByEFTPaymentCreationResult
				related.EFTPayCreationResult				= EFTPaymentCreationResult

		Work2AR115Rel
			one-to-many relation to Work2AR115
			Field Mapping uses ByEFTPaymentCreationResult
				related.EFTPayCreationResult				= EFTPaymentCreationResult

	Sets
		ByRunTime
			Sort Order
				RunTime descending
				EFTPaymentCreationResult

		ByCompany
			Sort Order
				EftCompany
				EFTPaymentCreationResult

	Actions
		PerformEFTPaymentCreation is a Create Action
			completion message is "PerformEFTPaymentCreationSubmitted"
			Action Rules
				if (FinanceEnterpriseGroup not entered)
					FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup

				if (EftCompany entered)
					constraint (EftCompany.EFTDebitCashCode entered)
						"Company<EftCompany>IsNotAnEFTProcessingCompany"
					constraint (EftCompany.PaymentBatchNumbering)
						"EFTCompanyMustBeSetUpForAutoBatchNumbering"
				if (EftCompany.CustomerGroupField.CustomerGroup.EFTProcessingLevel = "G")
					ReportByCompany							= false
					LocalEFTCalendar						= EftCompany.CustomerGroupField.CustomerGroup.EFTCalendar
					LocalDepositDays						= EftCompany.CustomerGroupField.CustomerGroup.EFTDepositDays
					LocalDepositType						= EftCompany.CustomerGroupField.CustomerGroup.EFTDepositDaysType
					LocalGlDays								= EftCompany.CustomerGroupField.CustomerGroup.EFTGLDays
					LocalGlType								= EftCompany.CustomerGroupField.CustomerGroup.EFTGLDaysType
					LocalCurrency							= EftCompany.CustomerGroupField.CustomerGroup.Currency
				else
					ReportByCompany							= true
					LocalEFTCalendar						= EftCompany.EFTCalendar
					LocalDepositDays						= EftCompany.EFTDepositDays
					LocalDepositType						= EftCompany.EFTDepositDaysType
					LocalGlDays								= EftCompany.EFTGLDays
					LocalGlType								= EftCompany.EFTGLDaysType
					LocalCurrency							= EftCompany.Currency

				if (LocalCurrency != EftCompany.Currency)
					LocalFromCurrency	= EftCompany.Currency
					LocalToCurrency		= LocalCurrency
					constraint (CurrencyRelationshipRel exists)
						"CurrencyRelationFrom<LocalFromCurrency>To<LocalToCurrency>NotDefined"




























	
				if (EftCompany.CashOperatorRequired)
					constraint (OprCode entered)
						"OperatorCodeRequired"
					constraint (ReceivableOperatorRel exists)
						"OperatorCode<OprCode>DoesNotExist"
					if (ReceivableOperatorRel exists)
						constraint (ReceivableOperatorRel.ActiveStatus = "A")
							"OperatorCode<OprCode>DoesNotHaveActiveStatus"




				if (DebitCashCode entered)
					LocalCashCode	= DebitCashCode
					constraint (CompanyCashCodeRel exists)
						"Company<EftCompany>And_\DebitCashCode<DebitCashCode>RelationshipDoesNotExistInCompanyCashCodes"

				if (CreditCashCode entered)
					LocalCashCode	= CreditCashCode
					constraint (CompanyCashCodeRel exists)
						"Company<EftCompany>And_\CreditCashCode<CreditCashCode>RelationshipDoesNotExistInCompanyCashCodes"

				if (GLAccount.AccountingUnit entered
				or	 GLAccount.GeneralLedgerChartAccount entered)
					constraint (BankCharges entered)
						"BankChargesRequired;GLAccountEntered"

				if (GLAccount.AccountingUnit not entered
				or	 GLAccount.GeneralLedgerChartAccount not entered)
					constraint (BankCharges not entered)
						"GLAccountInformationRequiredWhenBankChargesEntered"

			Exit Rules

















				BackgroundGroup = "EFTPaymentCreationResult_" + EFTPaymentCreationResult

				invoke EFTPaymentCreation ElectronicFundsTransferTransaction in background group(BackgroundGroup)

					assign async action request id to BackgroundGroupAsyncId
						invoked.PrmEftCompany					= EftCompany
						invoked.PrmPostingDate					= PostingDate
						invoked.PrmDepositDate					= DepositDate 
						invoked.PrmDebitCashCode				= DebitCashCode
						invoked.PrmCreditCashCode				= CreditCashCode
						invoked.PrmProcessLevel					= ProcessLevel
						invoked.PrmSelectProcessLevel			= SelectProcessLevel
						invoked.PrmBankTransactionCode			= BankTransactionCode
						invoked.PrmTermsCode					= TermsCode
						invoked.PrmNotAppldCd					= NotAppldCd
						invoked.PrmDiscReasCd					= DiscReasCd
						invoked.PrmOprCode						= OprCode
						invoked.PrmReference					= Reference
						invoked.PrmCompanyIdOption				= CompanyIdOption
						invoked.PrmFillLastBlock				= FillLastBlock
						invoked.PrmBalancedTape					= BalancedTape
						invoked.PrmBankCharges					= BankCharges
						invoked.PrmGLAccount					= GLAccount
						invoked.PrmIncludeAddenda				= IncludeAddenda
						invoked.PrmCbUserField1					= CbUserField1
						invoked.PrmCbUserField2					= CbUserField2					
						invoked.PrmCbUserField3					= CbUserField3
						invoked.PrmCbUserField4					= CbUserField4
						invoked.PrmACHFileName					= ACHFileName
						invoked.PrmBACSFileName					= BACSFileName
						invoked.PrmNonTapeFileName				= NonTapeFileName
						invoked.PrmSEPAFileName					= SEPAFileName
						invoked.PrmReportByCompany				= ReportByCompany	
						invoked.PrmEFTPaymentCreationResult		= EFTPaymentCreationResult
						invoked.PrmCreateDate					= WsCreateDate

		Update is an Update Action
			restricted

		Delete is a Delete Action
			Entrance Rules		
				invoke Delete Work1AR115Rel
				invoke Delete Work2AR115Rel

		Purge is a Purge Action
			bypass relational integrity rules
			Entrance Rules
				invoke Purge Work1AR115Rel
				invoke Purge Work2AR115Rel

		FastUpdate is an Update Action
			restricted
			bypass field rules

		UpdateStatusOnResult is an Instance Action
			restricted
			Local Fields
				DoNothing	is Boolean
			Action Rules










				if (!DoNothing or !RecordsExists)
					Status = 1
					if (RecordsExists)
						LocalActor = actor
						send notification
							to LocalActor
							description is "EFTPaymentCreationHasCompleted"
							priority is high
							detail is "ResultsCanBeSeenInEFTPaymentCreationResults"
					else
						LocalActor = actor
						send notification
							to LocalActor
							description is "EFTPaymentCreationHasCompleted"
							priority is high
							detail is "NoRecordsFoundToProcess"

		DeleteAllResults is a Set Action	
			default label is "DeleteAllResults"
			confirmation required
				"DeletingEFTPaymentCreationResultWillPreventAccessToAnyListViewsAssociatedWithTheResultSet"

			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmCutoffDate			  is Date
					default label is "CutoffDate"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmCutoffDate
					required

			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and	   DerivedRunDate <= PrmCutoffDate)
			
			Action Rules
				Empty Set Rules
					LocalActor = actor
					send notification
						to LocalActor
						description is "NoEFTPaymentCreationResultRecordsFoundToDelete"
						priority is medium
				
				Set Rules
					Exit Rules
						LocalActor = actor
						send notification
							to LocalActor
							description is "EFTPaymentCreationResultsHaveBeenDeletedThrough<PrmCutoffDate>"
							priority is medium
				
				Instance Rules
					invoke Delete

