ResponsibilityMatrix is a BusinessClass
    owned by GeneralLedger
    prefix is REMx

    Ontology
        symbolic key is ResponsibilityMatrix

	Patterns
    
    Persistent Fields
        AccountingEntityColumn				is Boolean
			default label is "<FinanceEnterpriseGroup.AccountingEntityLabel>"
		AccountingUnitColumn			    is Boolean
			default label is "<FinanceEnterpriseGroup.AccountingUnitLabel>"
		AccountColumn					    is Boolean
			default label is "<FinanceEnterpriseGroup.AccountLabel>"
		ProjectColumn					    is Boolean
			default label is "<FinanceEnterpriseGroup.ProjectLabel>"
		FinanceDimension1Column	 			is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension1Label>"
		FinanceDimension2Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension2Label>"
		FinanceDimension3Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension3Label>"
		FinanceDimension4Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension4Label>"
		FinanceDimension5Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension5Label>"
		FinanceDimension6Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension6Label>"
		FinanceDimension7Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension7Label>"
		FinanceDimension8Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension8Label>"
		FinanceDimension9Column		 		is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension9Label>"
		FinanceDimension10Column	 	    is Boolean
			default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>"
        Description
        Priority	   	        			is Numeric 6 
			disable Auditing
		Active                  			is Boolean
		DimensionsCount						is Numeric 2

    Derived Fields
		DerivedResponsibilityMatrix		is a DerivedField
			type is Alpha 200
			DerivedResponsibilityMatrix = ""
			if(AccountingEntityColumn)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "AE"
			if(AccountingUnitColumn)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "AU"
			if(AccountColumn)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "AC"
			if(ProjectColumn)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "PJ"
			if(FinanceDimension1Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D1"
			if(FinanceDimension2Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D2"
			if(FinanceDimension3Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D3"
			if(FinanceDimension4Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D4"
			if(FinanceDimension5Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D5"
			if(FinanceDimension6Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D6"
			if(FinanceDimension7Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D7"
			if(FinanceDimension8Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D8"
			if(FinanceDimension9Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D9"
			if(FinanceDimension10Column)
				DerivedResponsibilityMatrix = DerivedResponsibilityMatrix + "D10"
			return DerivedResponsibilityMatrix

		DerivedEmptySequenceNumber is a StringField
			type is AlphaRight 7
			restricted
			"0000000"
		
		DerivedEntitySequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(AccountingEntityColumn)
				return CodeBlock.ToAccountingEntity.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedUnitSequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(AccountingUnitColumn)
				return CodeBlock.AccountingUnit.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedAccountSequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(AccountColumn)
				return CodeBlock.GeneralLedgerChartAccount.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedProjectSequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(ProjectColumn)
				return CodeBlock.Project.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension1SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension1Column)
				return CodeBlock.FinanceDimension1.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber
		
		DerivedDimension2SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension2Column)
				return CodeBlock.FinanceDimension2.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension3SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension3Column)
				return CodeBlock.FinanceDimension3.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension4SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension4Column)
				return CodeBlock.FinanceDimension4.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension5SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension5Column)
				return CodeBlock.FinanceDimension5.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension6SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension6Column)
				return CodeBlock.FinanceDimension6.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension7SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension7Column)
				return CodeBlock.FinanceDimension7.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension8SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension8Column)
				return CodeBlock.FinanceDimension8.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension9SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension9Column)
				return CodeBlock.FinanceDimension9.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimension10SequenceNumber	is a DerivedField
			type is AlphaRight 7
			restricted
			if(FinanceDimension10Column)
				return CodeBlock.FinanceDimension10.DerivedSequenceNumber
			else
				return DerivedEmptySequenceNumber

		DerivedDimensionCode				is a StringField
            type is like ResponsibilityMatrixApproval
            restricted
            DerivedEntitySequenceNumber
            DerivedUnitSequenceNumber
			DerivedAccountSequenceNumber
			DerivedProjectSequenceNumber
			DerivedDimension1SequenceNumber
			DerivedDimension2SequenceNumber
			DerivedDimension3SequenceNumber
			DerivedDimension4SequenceNumber
			DerivedDimension5SequenceNumber
			DerivedDimension6SequenceNumber
			DerivedDimension7SequenceNumber
			DerivedDimension8SequenceNumber
			DerivedDimension9SequenceNumber
			DerivedDimension10SequenceNumber

		ActiveLabel is a LabelField
			"Active"

		InactiveLabel is a LabelField
			"Inactive"

		StatusDisplayTag is a DerivedField
			type is MessageField
			if (Active)
				return ActiveLabel
			if (!Active)
				return InactiveLabel

		DerivedDimensionsSelected is a DerivedField
			type is Numeric 2
			DimensionsCount = 20
			if(AccountingEntityColumn)
				DimensionsCount -= 1
			if(AccountingUnitColumn)
				DimensionsCount -= 1
			if(AccountColumn)
				DimensionsCount -= 1
			if(ProjectColumn)
				DimensionsCount -= 1
			if(FinanceDimension1Column)
				DimensionsCount -= 1
			if(FinanceDimension2Column)
				DimensionsCount -= 1
			if(FinanceDimension3Column)
				DimensionsCount -= 1
			if(FinanceDimension4Column)
				DimensionsCount -= 1
			if(FinanceDimension5Column)
				DimensionsCount -= 1
			if(FinanceDimension6Column)
				DimensionsCount -= 1
			if(FinanceDimension7Column)
				DimensionsCount -= 1
			if(FinanceDimension8Column)
				DimensionsCount -= 1
			if(FinanceDimension9Column)
				DimensionsCount -= 1
			if(FinanceDimension10Column)
				DimensionsCount -= 1
			return DimensionsCount

		EntityOrder is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.AccountingEntityApprovalOrder
		AUOrder is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.AccountingUnitApprovalOrder
		AccountOrder is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.AccountApprovalOrder
		ProjectOrder is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.ProjectApprovalOrder
		Dim1Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension1ApprovalOrder
		Dim2Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension2ApprovalOrder
		Dim3Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension3ApprovalOrder
		Dim4Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension4ApprovalOrder
		Dim5Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension5ApprovalOrder
		Dim6Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension6ApprovalOrder
		Dim7Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension7ApprovalOrder
		Dim8Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension8ApprovalOrder
		Dim9Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension9ApprovalOrder
		Dim10Order is a DerivedField
			type is Numeric 2
			restricted
			return FinanceEnterpriseGroup.FinanceDimension10ApprovalOrder

		DerivedApprovalOrder is a DerivedField
			type is Numeric 2
			initialize LocalApprovalOrder
			if (EntityOrder entered and AccountingEntityColumn entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > EntityOrder)
					LocalApprovalOrder = EntityOrder
			if (AUOrder entered and AccountingUnitColumn entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > AUOrder)
					LocalApprovalOrder = AUOrder
			if (AccountOrder entered and AccountColumn entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > AccountOrder)
					LocalApprovalOrder = AccountOrder
			if (ProjectOrder entered and ProjectColumn entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > ProjectOrder)
					LocalApprovalOrder = ProjectOrder
			if (Dim1Order entered and FinanceDimension1Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim1Order)
					LocalApprovalOrder = Dim1Order
			if (Dim2Order entered and FinanceDimension2Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim2Order)
					LocalApprovalOrder = Dim2Order
			if (Dim3Order entered and FinanceDimension3Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim3Order)
					LocalApprovalOrder = Dim3Order
			if (Dim4Order entered and FinanceDimension4Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim4Order)
					LocalApprovalOrder = Dim4Order
			if (Dim5Order entered and FinanceDimension5Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim5Order)
					LocalApprovalOrder = Dim5Order
			if (Dim6Order entered and FinanceDimension6Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim6Order)
					LocalApprovalOrder = Dim6Order
			if (Dim7Order entered and FinanceDimension7Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim7Order)
					LocalApprovalOrder = Dim7Order
			if (Dim8Order entered and FinanceDimension8Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim8Order)
					LocalApprovalOrder = Dim8Order
			if (Dim9Order entered and FinanceDimension9Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim9Order)
					LocalApprovalOrder = Dim9Order
			if (Dim10Order entered and FinanceDimension10Column entered)
				if (LocalApprovalOrder not entered or LocalApprovalOrder > Dim10Order)
					LocalApprovalOrder = Dim10Order
			return LocalApprovalOrder

	Conditions

		AutoApprove
			restricted
			when (LocalApprovalTransactionForm.PayablesInvoice
			or    LocalApprovalTransactionForm.ContractManagement
			or    LocalApprovalTransactionForm.PurchaseOrder)

		CreateHistoryForApprovalNotRequired
			restricted
			when (LocalApprovalTransactionForm.PayablesInvoice
			or    LocalApprovalTransactionForm.ContractManagement
			or    LocalApprovalTransactionForm.PurchaseOrder
			or    LocalApprovalTransactionForm.Requisition)

    Transient Fields
		NewPriority	            is Numeric 6
		AdvancedFilter	        is Boolean
		TransientApprovalOrder	is Numeric 2

	Local Fields
		CodeBlock							is a FinanceCodeBlock
		DisableSingleMatrix					is Boolean
		LocalApprovalOrder					is Numeric 2
		LocalFinanceEnterpriseGroup			is like FinanceEnterpriseGroup
		LocalApprovalType					is Alpha 2					
		LocalSystem							is like GeneralLedgerSystemCode	
		LocalApprovalTransactionForm		is like ApprovalTransactionForm
		LocalTransactionHeader1				is Alpha 40
		LocalTransactionHeader2				is Alpha 40
		LocalTransactionHeader3				is Alpha 40
		LocalTransactionHeader4				is Alpha 40
		LocalTransactionLine1				is Alpha 40
		LocalTransactionLine2				is Alpha 40
		LocalTransaction					is Numeric size 18
		LocalApprovalTitle					is Alpha 200
		LocalApprovalAmount					is a CurrencyAmount
		LocalResponsibilityMatrix			is like ResponsibilityMatrix
		LocalMatrixApprovalCode				is like ResponsibilityMatrixApproval
		LocalDocumentType					is like ApprovalTransactionForm
		LocalFoundMatrixApproval			is Boolean
		LocalApprovalCounter				is Numeric size 2
		LocalMaxApprovalOrder				is Numeric size 2
		LocalFinanceCodeBlock				is like FinanceCodeBlock

    Field Rules
		AccountingEntityColumn
			initial value is true
            default to true
            required
            cannot be changed
		AccountingUnitColumn
			cannot be changed
		AccountColumn
			cannot be changed
		ProjectColumn
			cannot be changed
		FinanceDimension1Column
			cannot be changed
		FinanceDimension2Column
			cannot be changed
		FinanceDimension3Column
			cannot be changed
		FinanceDimension4Column
			cannot be changed
		FinanceDimension5Column
			cannot be changed
		FinanceDimension6Column
			cannot be changed
		FinanceDimension7Column
			cannot be changed
		FinanceDimension8Column
			cannot be changed
		FinanceDimension9Column
			cannot be changed
		FinanceDimension10Column
			cannot be changed

	Relations
		ResponsibilityMatrixApprovalGLRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ResponsibilityMatrix				= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= DerivedDimensionCode
			Instance Selection
                where (related.DocumentType.GLJournalEntry
				and		related.Active
				and    related.DerivedApprovalOrder			= TransientApprovalOrder)

		ResponsibilityMatrixApprovalRQRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ResponsibilityMatrix				= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= DerivedDimensionCode
			Instance Selection
                where (related.DocumentType.Requisition
				and		related.Active
				and    related.DerivedApprovalOrder			= TransientApprovalOrder)

		ResponsibilityMatrixApprovalPORel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ResponsibilityMatrix				= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= DerivedDimensionCode
			Instance Selection
                where (related.DocumentType.PurchaseOrder
				and		related.Active
				and    related.DerivedApprovalOrder			= TransientApprovalOrder)

		ResponsibilityMatrixApprovalInvRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ResponsibilityMatrix				= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= DerivedDimensionCode
			Instance Selection
                where (related.DocumentType.PayablesInvoice
				and		related.Active
				and    related.DerivedApprovalOrder			= TransientApprovalOrder)

		ResponsibilityMatrixApprovalCMRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ResponsibilityMatrix				= ResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= DerivedDimensionCode
			Instance Selection
                where (related.DocumentType.ContractManagement
				and		related.Active
				and    related.DerivedApprovalOrder			= TransientApprovalOrder)

		ResponsibilityMatrixRel
			one-to-many relation to ResponsibilityMatrix
			Field Mapping uses ByPriorityAndDimensionCount
				related.FinanceEnterpriseGroup		= LocalFinanceEnterpriseGroup
			Instance Selection
                where (related.Active
				and    related.Priority				= LocalApprovalCounter)

		ResponsibilityMatrixApprovalExistsRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ResponsibilityMatrix				= ResponsibilityMatrix

		ResponsibilityMatrixApprovalProcessorRel
			one-to-one relation to ApprovalProcessor
			delete cascades
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup							= LocalFinanceEnterpriseGroup
				related.ApprovalProcessor.ApprovalType					= LocalApprovalType
				related.ApprovalProcessor.SystemCode					= LocalSystem
				related.ApprovalProcessor.ApprovalTransactionForm		= LocalApprovalTransactionForm
				related.ApprovalProcessor.TransactionHeader1			= LocalTransactionHeader1								
				related.ApprovalProcessor.TransactionHeader2			= LocalTransactionHeader2
				related.ApprovalProcessor.TransactionHeader3			= LocalTransactionHeader3	
				related.ApprovalProcessor.TransactionHeader4			= LocalTransactionHeader4
				related.ApprovalProcessor.TransactionLine1				= LocalTransactionLine1
            	related.ApprovalProcessor.TransactionLine2            	= LocalTransactionLine2
				related.ApprovalProcessor.Transaction					= LocalTransaction

		ResponsibilityMatrixResourceRel
			one-to-many relation to ResponsibilityMatrixResource
			Field Mapping uses symbolic key 
				related.FinanceEnterpriseGroup			= LocalFinanceEnterpriseGroup
				related.ResponsibilityMatrix        	= LocalResponsibilityMatrix
				related.ResponsibilityMatrixApproval	= LocalMatrixApprovalCode
			Instance Selection
				where (related.DocumentType 	 	 = LocalDocumentType
				and		related.ApprovalAmount 		<= LocalApprovalAmount)

		MatrixApprovalGLJournalRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.ResponsibilityMatrix        		= LocalResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= LocalMatrixApprovalCode
			Instance Selection
                where (related.DocumentType.GLJournalEntry
				and		related.Active)	

		MatrixApprovalRequisitionRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.ResponsibilityMatrix        		= LocalResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= LocalMatrixApprovalCode
			Instance Selection
                where (related.DocumentType.Requisition
				and		related.Active)	

		MatrixApprovalPurchaseOrderRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.ResponsibilityMatrix        		= LocalResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= LocalMatrixApprovalCode
			Instance Selection
                where (related.DocumentType.PurchaseOrder
				and		related.Active)	

		MatrixApprovalPayablesInvoiceRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.ResponsibilityMatrix        		= LocalResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= LocalMatrixApprovalCode
			Instance Selection
                where (related.DocumentType.PayablesInvoice
				and		related.Active)	

		MatrixApprovalContractManagementRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.ResponsibilityMatrix        		= LocalResponsibilityMatrix
				related.ResponsibilityMatrixApproval		= LocalMatrixApprovalCode
			Instance Selection
                where (related.DocumentType.ContractManagement
				and		related.Active)	

		MatrixApprovalOrderRel
			one-to-many relation to ResponsibilityMatrixApproval
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
			Instance Selection
				where ((related.Active)
				and   ((related.DocumentType.GLJournalEntry
				and     LocalApprovalTransactionForm 		= ApprovalTransactionForm.GLJournalEntry)
				or	   (related.DocumentType.Requisition
				and     LocalApprovalTransactionForm 		= ApprovalTransactionForm.Requisition)
				or	   (related.DocumentType.PurchaseOrder
				and     LocalApprovalTransactionForm 		= ApprovalTransactionForm.PurchaseOrder)
				or     (related.DocumentType.PayablesInvoice
				and     LocalApprovalTransactionForm 		= ApprovalTransactionForm.PayablesInvoice)
				or     (related.DocumentType.ContractManagement
				and     LocalApprovalTransactionForm 		= ApprovalTransactionForm.ContractManagement)))

#ifdef module rq
		RequisitionRel
			one-to-one relation to Requisition
			Field Mapping uses symbolic key
				related.Company											= LocalTransactionHeader1
				related.Requisition										= LocalTransaction

		RequestingLocationRel
			one-to-one relation to RequestingLocation
			Field Mapping uses symbolic key
				related.Company											= LocalTransactionHeader1
				related.RequestingLocation								= LocalTransactionHeader2
#endif
#ifdef module ap
		PayablesInvoiceAggregationRel
            one-to-one relation to APDistributionAggregation
            Field Mapping uses symbolic key
                related.Company                         				= LocalTransactionHeader1
                related.PayablesInvoice                 				= LocalTransactionHeader2
                related.APDistributionAggregation      					= LocalTransaction
#endif
#ifdef module po

		ContractDistributionAggregationRel
			one-to-one relation to ContractDistributionAggregation
			Field Mapping uses symbolic key
				related.ContractGroup									= LocalTransactionHeader1
				related.Contract										= LocalTransactionHeader2
				related.ContractDistributionAggregation					= LocalTransaction

		PODistributionAggregationRel
			one-to-one relation to PODistributionAggregation
			Field Mapping uses symbolic key
				related.Company											= LocalTransactionHeader1
				related.PurchaseOrder									= LocalTransactionHeader2
				related.PODistributionAggregation						= LocalTransaction
#endif

    Sets
		ByPriority
			Sort Order
				FinanceEnterpriseGroup
				Priority
				ResponsibilityMatrix

		ByPriorityAndDimensionCount
			Sort Order
				FinanceEnterpriseGroup
				Priority
				DimensionsCount
				ResponsibilityMatrix

    Actions
        Create is a Create Action
			Entrance Rules
				Active 					= true
                ResponsibilityMatrix 	= DerivedResponsibilityMatrix
				DimensionsCount			= DerivedDimensionsSelected
        
        Update is an Update Action
            restricted

        Delete is a Delete Action
			Action Rules
				constraint (!ResponsibilityMatrixApprovalExistsRel exists)
					"CannotDelete;MatrixApprovalRecordsExist"


		Activate is an Instance Action
			valid when (!Active)
			Action Rules
				invoke Update
					invoked.Active = true

		Inactivate is an Instance Action
			valid when (Active)
			Action Rules
				invoke Update
					invoked.Active = false

		UpdateApprovalOrder is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmChangeApprovalOrderFrom	is Numeric 2
				PrmChangeApprovalOrderTo	is Numeric 2
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and (Priority = PrmChangeApprovalOrderFrom
				or Priority = PrmChangeApprovalOrderTo))
			Action Rules
				Instance Rules
					Priority = DerivedApprovalOrder

		FixPriorityAndDimensionsCount is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where ((PrmFinanceEnterpriseGroup not entered
				or		FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup)
				and 	DimensionsCount = 0)
			Action Rules
				Instance Rules
					Priority 		= DerivedApprovalOrder
					DimensionsCount = DerivedDimensionsSelected

		SubmitForMatrixApproval is an Instance Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmApprovalType						is Alpha 2		
				PrmSystemCode						is like GeneralLedgerSystemCode	
				PrmApprovalTransactionForm			is like ApprovalTransactionForm
				PrmTransactionHeader1				is Alpha 40
				PrmTransactionHeader2				is Alpha 40
				PrmTransactionHeader3				is Alpha 40
				PrmTransactionHeader4				is Alpha 40
				PrmTransactionLine1					is Alpha 40
				PrmTransactionLine2					is Alpha 40
				PrmTransaction						is Numeric size 18
				PrmFinanceCodeBlock					is a FinanceCodeBlock
				PrmApprovalTitle					is Alpha 200
				PrmApprovalAmount					is a CurrencyAmount
				PrmValidateOnly						is Boolean
				PrmValidateFrom						is Numeric 2
					States
						RequisitionEntry				value 1
						RequestingLocationEntry			value 2
						RequestingLocationSetAction		value 3

			Action Rules
				initialize LocalResponsibilityMatrix
				initialize LocalMatrixApprovalCode
				
				LocalFinanceEnterpriseGroup 		= PrmFinanceEnterpriseGroup
				LocalApprovalType					= PrmApprovalType
				LocalSystem							= PrmSystemCode
				LocalApprovalTransactionForm		= PrmApprovalTransactionForm
				LocalTransactionHeader1				= PrmTransactionHeader1
				LocalTransactionHeader2				= PrmTransactionHeader2
				LocalTransactionHeader3				= PrmTransactionHeader3
				LocalTransactionHeader4				= PrmTransactionHeader4
				LocalTransactionLine1				= PrmTransactionLine1
				LocalTransactionLine2				= PrmTransactionLine2
				LocalTransaction					= PrmTransaction
				LocalApprovalTitle					= PrmApprovalTitle
				LocalApprovalAmount					= PrmApprovalAmount
				LocalFoundMatrixApproval 			= false
				LocalApprovalCounter 				= 1
				LocalMaxApprovalOrder 				= max MatrixApprovalOrderRel.DerivedApprovalOrder
				LocalFinanceCodeBlock				= PrmFinanceCodeBlock

				while (LocalApprovalCounter <= LocalMaxApprovalOrder)
					for each ResponsibilityMatrixRel
						each.CodeBlock = LocalFinanceCodeBlock
						each.TransientApprovalOrder = LocalApprovalCounter
						if (PrmApprovalTransactionForm.GLJournalEntry)
							if (each.ResponsibilityMatrixApprovalGLRel exists)
								LocalDocumentType 			= ApprovalTransactionForm.GLJournalEntry
								LocalResponsibilityMatrix 	= each.ResponsibilityMatrix
								LocalMatrixApprovalCode 	= each.ResponsibilityMatrixApprovalGLRel.ResponsibilityMatrixApproval
								LocalFoundMatrixApproval	= true
								end for each
						else
						if (PrmApprovalTransactionForm.Requisition)
							if (each.ResponsibilityMatrixApprovalRQRel exists)
								LocalDocumentType 			= ApprovalTransactionForm.Requisition
								LocalResponsibilityMatrix 	= each.ResponsibilityMatrix
								LocalMatrixApprovalCode 	= each.ResponsibilityMatrixApprovalRQRel.ResponsibilityMatrixApproval
								LocalFoundMatrixApproval	= true
								end for each
						else
						if (PrmApprovalTransactionForm.PurchaseOrder)
							if (each.ResponsibilityMatrixApprovalPORel exists)
								LocalDocumentType 			= ApprovalTransactionForm.PurchaseOrder
								LocalResponsibilityMatrix 	= each.ResponsibilityMatrix
								LocalMatrixApprovalCode 	= each.ResponsibilityMatrixApprovalPORel.ResponsibilityMatrixApproval
								LocalFoundMatrixApproval	= true
								end for each
						else
						if (PrmApprovalTransactionForm.PayablesInvoice)
							if (each.ResponsibilityMatrixApprovalInvRel exists)
								LocalDocumentType 			= ApprovalTransactionForm.PayablesInvoice
								LocalResponsibilityMatrix 	= each.ResponsibilityMatrix
								LocalMatrixApprovalCode 	= each.ResponsibilityMatrixApprovalInvRel.ResponsibilityMatrixApproval
								LocalFoundMatrixApproval	= true
								end for each
						else
						if (PrmApprovalTransactionForm.ContractManagement)
							if (each.ResponsibilityMatrixApprovalCMRel exists)
								LocalDocumentType 			= ApprovalTransactionForm.ContractManagement
								LocalResponsibilityMatrix 	= each.ResponsibilityMatrix
								LocalMatrixApprovalCode 	= each.ResponsibilityMatrixApprovalCMRel.ResponsibilityMatrixApproval
								LocalFoundMatrixApproval	= true
								end for each
					if (!LocalFoundMatrixApproval)
						LocalApprovalCounter = LocalApprovalCounter + 1
						if (ResponsibilityMatrixRel exists)
							if (FinanceEnterpriseGroup.AccountingEntityApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.ToAccountingEntity entered)
								LocalFinanceCodeBlock.ToAccountingEntity = blank
							if (FinanceEnterpriseGroup.AccountingUnitApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.AccountingUnit entered)
								LocalFinanceCodeBlock.AccountingUnit = blank
							if (FinanceEnterpriseGroup.AccountApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.GeneralLedgerChartAccount entered)
								LocalFinanceCodeBlock.GeneralLedgerChartAccount = blank
							if (FinanceEnterpriseGroup.ProjectApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.Project entered)
								LocalFinanceCodeBlock.Project = blank
							if (FinanceEnterpriseGroup.FinanceDimension1ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension1 entered)
								LocalFinanceCodeBlock.FinanceDimension1 = blank
							if (FinanceEnterpriseGroup.FinanceDimension2ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension2 entered)
								LocalFinanceCodeBlock.FinanceDimension2 = blank
							if (FinanceEnterpriseGroup.FinanceDimension3ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension3 entered)
								LocalFinanceCodeBlock.FinanceDimension3 = blank
							if (FinanceEnterpriseGroup.FinanceDimension4ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension4 entered)
								LocalFinanceCodeBlock.FinanceDimension4 = blank
							if (FinanceEnterpriseGroup.FinanceDimension5ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension5 entered)
								LocalFinanceCodeBlock.FinanceDimension5 = blank
							if (FinanceEnterpriseGroup.FinanceDimension6ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension6 entered)
								LocalFinanceCodeBlock.FinanceDimension6 = blank
							if (FinanceEnterpriseGroup.FinanceDimension7ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension7 entered)
								LocalFinanceCodeBlock.FinanceDimension7 = blank
							if (FinanceEnterpriseGroup.FinanceDimension8ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension8 entered)
								LocalFinanceCodeBlock.FinanceDimension8 = blank
							if (FinanceEnterpriseGroup.FinanceDimension9ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension9 entered)
								LocalFinanceCodeBlock.FinanceDimension9 = blank
							if (FinanceEnterpriseGroup.FinanceDimension10ApprovalOrder = LocalApprovalCounter - 1 and LocalFinanceCodeBlock.FinanceDimension10 entered)
								LocalFinanceCodeBlock.FinanceDimension10 = blank
					if (LocalFoundMatrixApproval)
						end while

				if (!PrmFinanceEnterpriseGroup.EnableResponsibilityMatrix)
					if(ResponsibilityMatrixApprovalProcessorRel exists)
						invoke Delete ResponsibilityMatrixApprovalProcessorRel
				else
					if (!ResponsibilityMatrixResourceRel exists
					and !AutoApprove
					and !PrmApprovalTransactionForm.Requisition)
						if(ResponsibilityMatrixApprovalProcessorRel exists)
							invoke Delete ResponsibilityMatrixApprovalProcessorRel
					else
						if (!PrmValidateOnly)
							if(ResponsibilityMatrixApprovalProcessorRel not exists)
								invoke Create ResponsibilityMatrixApprovalProcessorRel
									invoked.ProcessorTitle				= LocalApprovalTitle
									invoked.Matrix		 				= LocalResponsibilityMatrix
									invoked.ApprovalMatrix				= LocalMatrixApprovalCode
									invoked.ApprovalAmount				= LocalApprovalAmount
							else
								if(!ResponsibilityMatrixApprovalProcessorRel.Status.Approved
								and ResponsibilityMatrixApprovalProcessorRel.ApprovalMatrix != LocalMatrixApprovalCode)
									invoke Update ResponsibilityMatrixApprovalProcessorRel
										invoked.Matrix		 	= LocalResponsibilityMatrix
										invoked.ApprovalMatrix 	= LocalMatrixApprovalCode
										invoked.ApprovalAmount	= LocalApprovalAmount
										invoked.ProcessorTitle	= LocalApprovalTitle
			
#ifdef module rq
				if(PrmValidateOnly)
					if (PrmApprovalTransactionForm.Requisition)
						if (PrmValidateFrom.RequisitionEntry)
							constraint (MatrixApprovalRequisitionRel exists)
								"<RequisitionRel.DerivedMatrixAccountErrorMessage>"
						if (PrmValidateFrom.RequestingLocationEntry)
							constraint (MatrixApprovalRequisitionRel exists)
								"<RequestingLocationRel.DerivedMatrixAccountErrorMessage>"
							if (RequestingLocationRel.IssueAccountInvalidForMatrixApproval)
								invoke FastUpdate RequestingLocationRel
									invoked.IssueAccountInvalidForMatrixApproval = false
						if (PrmValidateFrom.RequestingLocationSetAction)
							if (MatrixApprovalRequisitionRel exists)
								invoke FastUpdate RequestingLocationRel
									invoked.IssueAccountInvalidForMatrixApproval = false
							else
								invoke FastUpdate RequestingLocationRel
									invoked.IssueAccountInvalidForMatrixApproval = true
#endif


			Exit Rules
				if(!PrmValidateOnly)
					if ((PrmApprovalTransactionForm.GLJournalEntry 
					and MatrixApprovalGLJournalRel exists)
					or	(PrmApprovalTransactionForm.Requisition
					and MatrixApprovalRequisitionRel exists)
					or (PrmApprovalTransactionForm.PurchaseOrder
					and MatrixApprovalPurchaseOrderRel exists)
					or (PrmApprovalTransactionForm.PayablesInvoice
					and MatrixApprovalPayablesInvoiceRel exists)
					or (PrmApprovalTransactionForm.ContractManagement
					and MatrixApprovalContractManagementRel exists))
						if(ResponsibilityMatrixResourceRel exists
						and !ResponsibilityMatrixApprovalProcessorRel.Status.Approved)
							invoke StartMatrixApproval ResponsibilityMatrixApprovalProcessorRel
						else
						if (!ResponsibilityMatrixResourceRel exists
						and ResponsibilityMatrixApprovalProcessorRel.Status.ApprovalNotRequired
						and CreateHistoryForApprovalNotRequired)
							invoke CreateApprovalNotRequiredHistory ResponsibilityMatrixApprovalProcessorRel
#ifdef module ap
							if (LocalApprovalTransactionForm.PayablesInvoice
							and !PayablesInvoiceAggregationRel.Status.ApprovalNotRequired)
								invoke Update PayablesInvoiceAggregationRel
									invoked.Status = 2		
								invoke ReleaseTransaction ResponsibilityMatrixApprovalProcessorRel
#endif
#ifdef module po
							if (LocalApprovalTransactionForm.ContractManagement
							and !ContractDistributionAggregationRel.Status.ApprovalNotRequired)
								invoke ApprovalNotRequired ContractDistributionAggregationRel

							if (LocalApprovalTransactionForm.PurchaseOrder
							and !PODistributionAggregationRel.Status.ApprovalNotRequired)
								invoke Update PODistributionAggregationRel
									invoked.Status = 0  
								invoke ReleaseTransaction ResponsibilityMatrixApprovalProcessorRel
#endif
					else
						if (!LocalFoundMatrixApproval
						and  AutoApprove)
							invoke Approve ResponsibilityMatrixApprovalProcessorRel
								invoked.PrmAutoApprove	= true
