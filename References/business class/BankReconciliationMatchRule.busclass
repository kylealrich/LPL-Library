BankReconciliationMatchRule is a BusinessClass
    owned by cashmgmt
    prefix is CRMR
    sql name is ReconMatchRule

    Ontology
    	symbolic key is BankReconciliationMatchRule

    Persistent Fields
		Description						is Alpha up to 250
		StatementRule					is a BankStatementReconciliation group
		SystemRule						is a BankStatementReconciliation group
		MatchType						is a ReconciliationMatchType
		UseDayLag						is Boolean
		DayLag							is Numeric 3
		FutureDay						is Numeric 3
	    ReconciliationTolerancePercent	is Percent 3
			default label is "TolerancePercent"
		ReconciliationToleranceAmount	is an InternationalAmount
			default label is "ToleranceAmount"
	    ReconcileByAmount				is Boolean
		ReconcileByAmountType			is a DebitCreditIndicator
	    LineMatchField					is Alpha 200	
			default label is "StatementLineMatchField"
			Text Variables
				TransactionNumber
				TransactionDate
				TransactionCode
				TransactionAmount
				CashTransactionCategory

				CashLedgerTransactionIdentifier
				TransactionDataOne
				TransactionDataTwo
				TransactionDataThree
				TransactionDataFour

				SystemCode
				Reference
				ParsedData1
				ParsedData2
				ParsedData3
				ParsedData4
				ParsedData5
				ParsedData6
				ParsedData7
				ParsedData8
				ParsedData9
				ParsedData10
		SystemLineMatchField			is Alpha 200	
			Text Variables
				TransactionNumber
				TransactionDate
				TransactionCode
				TransactionAmount
				CashTransactionCategory

				CashLedgerTransactionIdentifier
				TransactionDataOne
				TransactionDataTwo
				TransactionDataThree
				TransactionDataFour

				SystemCode
				Reference
		TransactionNumberSource			is Numeric 2
			default label is "VariableSourceOnStatement"
			States
				RelatedReference value is 0	
				BankReference	 value is 1
				TextReference	 value is 2

		LineMatchFieldArray
		RemoveAlphaCharacters			is Boolean
		RemoveLeadingZeros				is Boolean
		RemoveCharacterString			is Boolean
		RemoveString					is AlphaUpper up to 20
		RemoveCharactersBefore			is Boolean
			default label is "RemoveCharacterStringAndPrecedingCharacters"
		RemoveBefore					is AlphaUpper up to 20
		RemoveCharactersAfter			is Boolean
			default label is "RemoveCharacterStringAndSubsequentCharacters"
		RemoveAfter						is AlphaUpper up to 20

		SystemLineMatchFieldArray		is a LineMatchFieldArray
		SystemRemoveAlphaCharacters		is Boolean
			default label is "RemoveAlphaCharacters"
		SystemRemoveLeadingZeros		is Boolean
			default label is "RemoveLeadingZeros"
		SystemRemoveCharacterString		is Boolean
			default label is "RemoveCharacterString"
		SystemRemoveString				is AlphaUpper up to 20
		SystemRemoveCharactersBefore	is Boolean
			default label is "RemoveCharacterStringAndPrecedingCharacters"
		SystemRemoveBefore				is AlphaUpper up to 20
		SystemRemoveCharactersAfter		is Boolean
			default label is "RemoveCharacterStringAndSubsequentCharacters"
		SystemRemoveAfter				is AlphaUpper up to 20
		Active
	
	Local Fields
		LocalLineMatchField				is Alpha 200 
		LocalSystemLineMatchField		is Alpha 200 
		LocalCounter 					is Numeric 3
		LocalSegment					is Numeric 2
		
	Derived Fields
		LikeTransactionNumberSource is a DerivedField
			type is AlphaUpper 20
			restricted
			return "*" + "TRANSACTIONNUMBER" + "*"
		
		LikeCashLedgerTransactionIdentifierSource is a DerivedField
			type is AlphaUpper 35
			restricted
			return "*" + "CASHLEDGERTRANSACTIONIDENTIFIER" + "*"
			
		LikeTransactionDataOneSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATAONE" + "*"
		
		LikeTransactionDataTwoSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATATWO" + "*"
		
		LikeTransactionDataThreeSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATATHREE" + "*"
			
		LikeTransactionDataFourSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATAFOUR" + "*"

		DerivedFutureDay is a DerivedField		
			type is Numeric 3
			restricted
			if (FutureDay < 0)
				return (FutureDay *-1)
			return FutureDay

		DerivedRemoveString is a StringField
			type is AlphaUpper up to 30
			"\Q"
			RemoveString
			"\E"
		
		DerivedRemoveBefore is a StringField
			type is AlphaUpper up to 30
			"^.*"
			"\Q"
			RemoveBefore
			"\E"
			
		DerivedRemoveAfter is a StringField
			type is AlphaUpper up to 30
			"\Q"
			RemoveAfter
			"\E"
			".*$"
		
		DerivedSystemRemoveString is a StringField
			type is AlphaUpper up to 30
			"\Q"
			SystemRemoveString
			"\E"
			
		DerivedSystemRemoveBefore is a StringField
			type is AlphaUpper up to 30
			"^.*"
			"\Q"
			SystemRemoveBefore
			"\E"
				
		DerivedSystemRemoveAfter is a StringField
			type is AlphaUpper up to 30
			"\Q"
			SystemRemoveAfter
			"\E"
			".*$"
		
		DisableApplyingModificationOptionsToVariablesOnly is a DerivedField		
			type is Boolean
			restricted
			return config.DisableApplyingModificationOptionsToVariablesOnly
			
	Field Rules
		Description
			default to BankReconciliationMatchRule
		
		StatementRule
			if (ReconcileByAmount)
				if (ReconcileByAmountType.Debit)
					constraint (StatementDebitCondition)
						"StatementRuleMustBeSetUpWithDebitCreditIndicatorSetToDebit"
				else
					constraint (StatementCreditCondition)
						"StatementRuleMustBeSetUpWithDebitCreditIndicatorSetToCredit"
			if (SystemRule entered)
				constraint (StatementRule != SystemRule)
					"StatementRuleAndSystemRuleMustBeDifferent"
					
			required
				"StatementRuleIsRequired"
		
		SystemRule
			if (ReconcileByAmount)
				if (ReconcileByAmountType.Debit)
					constraint (SystemDebitCondition)
						"SystemRuleMustBeSetUpWithDebitCreditIndicatorSetToDebit"
				else
					constraint (SystemCreditCondition)
						"SystemRuleMustBeSetUpWithDebitCreditIndicatorSetToCredit"
			if (StatementRule entered)
				constraint (SystemRule != StatementRule)
					"SystemRuleAndStatementRuleMustBeDifferent"
					
			required
				"SystemRuleIsRequired"
				
		MatchType
			initial value is MatchType.ManyToMany
			required
		
		DayLag
			if (UseDayLag)
				constraint (DayLag >= 0)
					"DayLagCannotBeNegative"
			else
				initialize
		
		FutureDay
			if (UseDayLag)
				constraint ((DayLag + FutureDay) >= 0)		
					"FutureDayCannotBeBeforeDayLag"
			else
				initialize
							
		ReconciliationTolerancePercent
			constraint (!ReconciliationToleranceAmount entered)
				"EnterEitherToleranceAmountOrTolerancePercent"
			if (ReconcileByAmount)
				cannot be entered
					"TolerancePercentNotAllowedWithReconcileByAmount"
							
		ReconciliationToleranceAmount
			if (ReconcileByAmount)
				cannot be entered
					"ToleranceAmountNotAllowedWithReconcileByAmount"
		
		ReconcileByAmount
			if (ReconcileByAmount changed)
				invoke UpdateReconcileByAmount BankStatementReconciliation
					invoked.PrmCashManagementGroup				= CashManagementGroup
					invoked.PrmBankReconciliationMatchRule		= BankReconciliationMatchRule
					if (ReconcileByAmount)
						invoked.PrmReconcileByAmountToBeUpdated	= false	
					if (!ReconcileByAmount)
						invoked.PrmReconcileByAmountToBeUpdated	= true	
							
		ReconcileByAmountType
			if (ReconcileByAmount)
				required
					"ReconcileByAmountTypeRequiredWhenReconcileByAmountSelected"

		LineMatchField
			if (ReconcileByAmount)
				default to BankReconciliationMatchRule
			LineMatchField -= " "
			LocalLineMatchField = LineMatchField
			constraint (!MatchRuleMatchesCategoryRel exists)
				"Category<LocalLineMatchField>Exists;StatementLineMatchFieldMustBeDifferent"
			required
				"StatementLineMatchFieldIsRequired"
		
			LocalCounter = 1			
			LocalSegment = 1 	
			initialize LineMatchFieldArray
			while (LocalCounter <= 200)
				if (LineMatchField[LocalCounter] entered)
					if (LineMatchField[LocalCounter] = "{")
						if (LocalCounter != 1)
							LocalSegment += 1
						LineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Segment += LineMatchField[LocalCounter]
						LineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Variable = true
					else
					if (LineMatchField[LocalCounter] = "}")
						LineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Segment += LineMatchField[LocalCounter]
						if (LineMatchField[LocalCounter+1] != "{")
							LocalSegment += 1
					else
						LineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Segment += LineMatchField[LocalCounter]
					LocalCounter += 1
				else
					end while
			
		SystemLineMatchField
			if (ReconcileByAmount)
				default to BankReconciliationMatchRule
			SystemLineMatchField -= " "
			LocalSystemLineMatchField = SystemLineMatchField
			constraint (!SystemMatchRuleMatchesCategoryRel exists)
				"Category<LocalSystemLineMatchField>Exists;SystemLineMatchFieldMustBeDifferent"
			required
				"SystemLineMatchFieldIsRequired"
			
			LocalCounter = 1			
			LocalSegment = 1 	
			initialize SystemLineMatchFieldArray
			while (LocalCounter <= 200)
				if (SystemLineMatchField[LocalCounter] entered)
					if (SystemLineMatchField[LocalCounter] = "{")
						if (LocalCounter != 1)
							LocalSegment += 1
						SystemLineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Segment += SystemLineMatchField[LocalCounter]
						SystemLineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Variable = true
					else
					if (SystemLineMatchField[LocalCounter] = "}")
						SystemLineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Segment += SystemLineMatchField[LocalCounter]
						if (SystemLineMatchField[LocalCounter+1] != "{")
							LocalSegment += 1
					else
						SystemLineMatchFieldArray.LineMatchFieldSegment[LocalSegment].Segment += SystemLineMatchField[LocalCounter]
					LocalCounter += 1
				else
					end while 
		
		RemoveAlphaCharacters
			if (UsesCashLedgerTransactionIdentifierVariable)
				RemoveAlphaCharacters = false
		
			constraint (!RemoveCharacterString)
				"CannotRemoveAlphaCharactersAndRemoveCharacterStringForTheStatementLineMatchField"
			
			constraint (!RemoveCharactersBefore)
				"CannotRemoveAlphaCharactersAndRemoveCharacterStringAndPrecedingCharactersForTheStatementLineMatchField"
			
			constraint (!RemoveCharactersAfter)
				"CannotRemoveAlphaCharactersAndRemoveCharacterStringAndSubsequentCharactersForTheStatementLineMatchField"
						
		RemoveCharacterString
			if (UsesCashLedgerTransactionIdentifierVariable)
				RemoveCharacterString = false
			
			constraint (!RemoveCharactersBefore)
				"CannotRemoveCharacterStringAndRemoveCharacterStringAndPrecedingCharactersForTheStatementLineMatchField"
			
			constraint (!RemoveCharactersAfter)
				"CannotRemoveCharacterStringAndRemoveCharacterStringAndSubsequentCharactersForTheStatementLineMatchField"
							
		RemoveString
			if (RemoveCharacterString)
				required
					"MustEnterACharacterStringToBeRemovedForTheStatementLineMatchField"
			else
				initialize
				
		RemoveCharactersBefore
			if (UsesCashLedgerTransactionIdentifierVariable)
				RemoveCharactersBefore = false
					
		RemoveBefore
			if (RemoveCharactersBefore
			and RemoveCharactersAfter)
				constraint (RemoveBefore != RemoveAfter)
					"RemoveCharacterStringAndPrecedingCharactersMustBeDifferentThanRemoveCharacterStringAndSubsequentCharactersForTheStatementLineMatchField"
				
			if (RemoveCharactersBefore)
				required
					"MustEnterACharacterStringToBeRemovedForTheStatementLineMatchField"
			else
				initialize
				
		RemoveCharactersAfter
			if (UsesCashLedgerTransactionIdentifierVariable)
				RemoveCharactersAfter = false
						
		RemoveAfter
			if (RemoveCharactersAfter
			and RemoveCharactersBefore)
				constraint (RemoveAfter != RemoveBefore)
					"RemoveCharacterStringAndSubsequentCharactersMustBeDifferentThanRemoveCharacterStringAndPrecedingCharactersForTheStatementLineMatchField"
				
			if (RemoveCharactersAfter)
				required
					"MustEnterACharacterStringToBeRemovedForTheStatementLineMatchField"
			else
				initialize
				
		SystemRemoveAlphaCharacters
			if (SystemUsesCashLedgerTransactionIdentifierVariable)
				SystemRemoveAlphaCharacters = false
		
			constraint (!SystemRemoveCharacterString)
				"CannotRemoveAlphaCharactersAndRemoveCharacterStringForTheSystemLineMatchField"
			
			constraint (!SystemRemoveCharactersBefore)
				"CannotRemoveAlphaCharactersAndRemoveCharacterStringAndPrecedingCharactersForTheSystemLineMatchField"
			
			constraint (!SystemRemoveCharactersAfter)
				"CannotRemoveAlphaCharactersAndRemoveCharacterStringAndSubsequentCharactersForTheSystemLineMatchField"
						
		SystemRemoveCharacterString
			if (SystemUsesCashLedgerTransactionIdentifierVariable)
				SystemRemoveCharacterString = false
			
			constraint (!SystemRemoveCharactersBefore)
				"CannotRemoveCharacterStringAndRemoveCharacterStringAndPrecedingCharactersForTheSystemLineMatchField"
			
			constraint (!SystemRemoveCharactersAfter)
				"CannotRemoveCharacterStringAndRemoveCharacterStringAndSubsequentCharactersForTheSystemLineMatchField"
							
		SystemRemoveString
			if (SystemRemoveCharacterString)
				required
					"MustEnterACharacterStringToBeRemovedForTheSystemLineMatchField"
			else
				initialize
				
		SystemRemoveCharactersBefore
			if (SystemUsesCashLedgerTransactionIdentifierVariable)
				SystemRemoveCharactersBefore = false
					
		SystemRemoveBefore
			if (SystemRemoveCharactersBefore
			and SystemRemoveCharactersAfter)
				constraint (SystemRemoveBefore != SystemRemoveAfter)
					"RemoveCharacterStringAndPrecedingCharactersMustBeDifferentThanRemoveCharacterStringAndSubsequentCharactersForTheSystemLineMatchField"
				
			if (SystemRemoveCharactersBefore)
				required
					"MustEnterACharacterStringToBeRemovedForTheSystemLineMatchField"
			else
				initialize
				
		SystemRemoveCharactersAfter
			if (SystemUsesCashLedgerTransactionIdentifierVariable)
				SystemRemoveCharactersAfter = false
						
		SystemRemoveAfter
			if (SystemRemoveCharactersAfter
			and SystemRemoveCharactersBefore)
				constraint (SystemRemoveAfter != SystemRemoveBefore)
					"RemoveCharacterStringAndSubsequentCharactersMustBeDifferentThanRemoveCharacterStringAndPrecedingCharactersForTheSystemLineMatchField"
				
			if (SystemRemoveCharactersAfter)
				required
					"MustEnterACharacterStringToBeRemovedForTheSystemLineMatchField"
			else
				initialize
				
		Active
			initial value is true
	
	Conditions
		DayToleranceMessage
			restricted
			when (UseDayLag
			and   !DayLag entered
			and	  !FutureDay entered)

		NegativeFutureDay		
			restricted
			when (!DayToleranceMessage
			and   FutureDay < 0)

		PositiveFutureDay		
			restricted
			when (!DayToleranceMessage
			and   FutureDay >= 0)

		UsesTransactionNumberVariable 				
			restricted
			when (LineMatchField like LikeTransactionNumberSource)
			
		UsesCashLedgerTransactionIdentifierVariable	
			restricted
			when (LineMatchField like LikeCashLedgerTransactionIdentifierSource)
			
		UsesTransactionDataOneVariable 				
			restricted
			when (LineMatchField like LikeTransactionDataOneSource)
			
		UsesTransactionDataTwoVariable 				
			restricted
			when (LineMatchField like LikeTransactionDataTwoSource)
			
		UsesTransactionDataThreeVariable 			
			restricted
			when (LineMatchField like LikeTransactionDataThreeSource)
			
		UsesTransactionDataFourVariable 			
			restricted
			when (LineMatchField like LikeTransactionDataFourSource)
		
		SystemUsesCashLedgerTransactionIdentifierVariable 
			restricted
			when (SystemLineMatchField like LikeCashLedgerTransactionIdentifierSource)
			
		DisplayVariableSourceOnStatement
			restricted
			when (UsesTransactionNumberVariable
			or	  UsesCashLedgerTransactionIdentifierVariable
			or	  UsesTransactionDataOneVariable
			or	  UsesTransactionDataTwoVariable
			or	  UsesTransactionDataThreeVariable
			or	  UsesTransactionDataFourVariable)
			
		DisplayRemoveAlphaCharacters 
			restricted
			when (!UsesCashLedgerTransactionIdentifierVariable)
		
		DisplaySystemRemoveAlphaCharacters 
			restricted
			when (!SystemUsesCashLedgerTransactionIdentifierVariable)
			
		InMatchRuleGroup
			restricted
			when (BankReconciliationMatchRuleGroupDetailRel exists)
				
		NotInMatchRuleGroup
			restricted
			when (!BankReconciliationMatchRuleGroupDetailRel exists)

		StatementDebitCondition
			restricted
			when (StatementRule.Condition like "*DebitCreditIndicator.Debit*")
	
		StatementCreditCondition
			restricted
			when (StatementRule.Condition like "*DebitCreditIndicator.Credit*")
	
		SystemDebitCondition
			restricted
			when (SystemRule.Condition like "*DebitCreditIndicator.Debit*")
	
		SystemCreditCondition
			restricted
			when (SystemRule.Condition like "*DebitCreditIndicator.Credit*")
	
	Relations
		MatchRuleMatchesCategoryRel
			one-to-one relation to CashTransactionCategory
			Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
				related.CashTransactionCategory = LocalLineMatchField
		
		SystemMatchRuleMatchesCategoryRel
			one-to-one relation to CashTransactionCategory
			Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
				related.CashTransactionCategory = LocalSystemLineMatchField
					 
		BankReconciliationMatchRuleGroupDetailRel
			one-to-many relation to BankReconciliationMatchRuleGroupDetail
			Field Mapping uses ByBankReconciliationMatchRule
				related.CashManagementGroup			= CashManagementGroup
				related.BankReconciliationMatchRule	= BankReconciliationMatchRule
				
	Attach Rules
		constraint (Active)
			"MatchRuleIsInactive"
	
	Actions
		Create is a Create Action
					
		Update is an Update Action
		
		AddMatchRule is an Instance Action
			valid when (Active)
			default label is "AddToMatchRuleGroup"
			Parameters
				PrmCashManagementGroup 		  		is a CashManagementGroup
				PrmBankReconciliationMatchRuleGroup	is a BankReconciliationMatchRuleGroup

			Action Rules
				invoke Create BankReconciliationMatchRuleGroupDetail
					invoked.CashManagementGroup	 				= PrmCashManagementGroup
					invoked.BankReconciliationMatchRuleGroup	= PrmBankReconciliationMatchRuleGroup
					invoked.BankReconciliationMatchRule			= BankReconciliationMatchRule
					
		Delete is a Delete Action
