BirstModelQuerySourceField is a BusinessClass
	owned by la
	prefix is BMQSF

	Ontology
		symbolic key is BirstModelQuerySourceField

	Patterns
		disable AsOfDateProcessing
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Persistent Fields
		ReplicationSet
		RepSetBC
		BusinessClass	is a snapshot of RepSetBC.BusinessClass
		RelatedValue					is Alpha size 300
			is related value for RepSetBC.BusinessClass
		DisplayOrder					is Numeric size 4
			default label is "SortOrder"
		BirstFieldName					is Alpha size 300
		SQLName							is Alpha size 3000
		IsAttribute						is Boolean
			default label is "Attribute"
		IsMeasure						is Boolean
			default label is "Measure"
		FieldType						is Numeric size 1
			States
				BaseField value is 0
				FormulaField value is 1
		FieldDisplayType				is Numeric size 1
			States
				None				value is 0
				Code				value is 1
				Description			value is 2
				SequenceNumber		value is 3               
		IncludeInBirstModel				is Boolean
		DataType						is Alpha size 30
		Width							is Numeric size 4
		IsBooleanField					is Boolean
			default label is "BooleanField"
		IsStateField					is Boolean
			default label is "StateField"
		IsDimensionKey					is Boolean
			default label is "DimensionKey"
		IsForeignKey					is Boolean
		    default label is "ForeignKey"
		ForeignKeyBusClass				is Alpha size 100
		ForeignKeyQuerySource			is a BirstModelQuerySource
		ForeignKeyHierarchyBC			is Alpha size 100
		HierarchyLevelNumber			is Numeric size 2
		HierarchyFieldType				is Numeric size 1
		    States
                None				value is 0
                LevelCode			value is 1
				LevelDescription	value is 2
				DimensionType		value is 3
				SequenceNumber		value is 4
		FilterType						is Numeric size 1
			States
				None			value is 0
				LiteralValue	value is 1
				NotEmpty		value is 2
		FilterValue						is Alpha size 1000
		JoinType						is Numeric size 1
			States
				None			value is 0
				InnerJoin		value is 1
				LeftOuterJoin	value is 2
		JoinField						is Alpha size 100 
			is related value for DriverBusinessClass		
		JoinFieldSQLName				is Alpha size 100

	Field Rules
		ReplicationSet
			required
		RepSetBC
			required
		RelatedValue
			required
		BirstFieldName
			required

	Transient Fields
		NewDisplayOrder is Numeric size 4
			default label is "NewSortOrder"

		DriverBusinessClass	is a BusinessClass
			derive value from DerivedDriverBusinessClass

		OverrideSQLName is Boolean

		TransientSQLName is Alpha 100
			derive value from DerivedJoinFieldSQLName

		TransientSourceType is Numeric size 1
            States
                Dimension value is 0
                Fact value is 1
			derive value from SourceType

	Derived Fields
		DerivedJoinFieldSQLName is a NativeField
			type is Alpha size 100
	
		DerivedDriverBusinessClass is a DerivedField
		 	type is like BusinessClass
		 	return BirstModelQuerySource.BusinessClass

		UseColumnarVariationPattern is a DerivedField
			type is Boolean
			return RepSetBC.UseColumnarVariationPattern

		BirstTableName is a DerivedField
		 	type is Alpha size 60
		 	return BirstModelQuerySource.DisplayName

		MeasurePrefix is a DerivedField
			type is Alpha size 10
			return BirstModelQuerySource.MeasurePrefix

		SourceType is a DerivedField
			type is Numeric size 1
			return BirstModelQuerySource.SourceType

		ForeignKeyBusClassBirstTableName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyBusClassDimKeyRel.BirstTableName

		ForeignKeyBusClassBirstFieldName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyBusClassDimKeyRel.BirstFieldName

		ForeignKeyQuerySourceBirstTableName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyQuerySourceDimKeyRel.BirstTableName

		ForeignKeyQuerySourceBirstFieldName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyQuerySourceDimKeyRel.BirstFieldName

		EnableBirstQuerySourceEdit is a DerivedField
			type is Boolean
			restricted
			return config(config).EnableBirstQuerySourceEdit			

		InternalExecution is a NativeField
		    type is Boolean
		    restricted

	Sets				
		ByBirstFieldName
			indexed
			Sort Order
				BirstModel
				BirstModelQuerySource
				BirstFieldName

		ByDisplayOrder
			indexed
			Sort Order
				BirstModel
				BirstModelQuerySource
				DisplayOrder
				BirstModelQuerySourceField

		ByDimensionKey
			indexed
			Sort Order
				BirstModel
				BirstModelQuerySource
				IncludeInBirstModel
				IsDimensionKey
				BirstModelQuerySourceField

		ByRelatedValue
			indexed
			Sort Order
				BirstModel
				ReplicationSet
				RepSetBC
				RelatedValue
				BirstModelQuerySource
				BirstModelQuerySourceField

		ByForeignKey
			indexed
			Sort Order
				BirstModel
				BirstModelQuerySource
				IncludeInBirstModel
				IsForeignKey
				BirstModelQuerySourceField

		ByBCRelatedValue
			indexed
			Sort Order
				BirstModel
				BusinessClass	
				RelatedValue
				BirstModelQuerySource
				BirstModelQuerySourceField

		ByBCFieldDisplayType
			indexed
			Sort Order
				BirstModel
				BusinessClass
				FieldDisplayType
				RelatedValue
				BirstModelQuerySource
				BirstModelQuerySourceField

		ByBCForeignKeyBC
			indexed
			Sort Order
				BirstModel
				BusinessClass	
				ForeignKeyBusClass			
				RelatedValue
				BirstModelQuerySource
				BirstModelQuerySourceField				

		ByFilterType
			indexed
			Sort Order
				BirstModel
				BirstModelQuerySource
				FilterType
				BirstModelQuerySourceField

		ByJoinType
			indexed
			Sort Order
				BirstModel
				BirstModelQuerySource
				JoinType
				BirstModelQuerySourceField

	Actions
		Create is an Action
			restricted
			valid when (CanEdit)

		Update is an Action
			valid when (CanEdit)
			Exit Rules
				if (!OverrideSQLName)
					JoinFieldSQLName = DerivedJoinFieldSQLName

		Delete is an Action
			valid when (CanEdit)

		Purge is a Purge Action
			valid when (CanEdit)

	Conditions
		HasJoin
			when(!JoinType.None)

		CanEdit
			default label is "FieldsCanBeEdited"
			when (EnableBirstQuerySourceEdit or InternalExecution)

	Relations
		ForeignKeyBusClassDimKeyRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByDimensionKey
				related.BirstModel			= BirstModel
				related.BusinessClass		= ForeignKeyBusClass
				related.IncludeInBirstModel	= true
				related.IsDimensionKey		= true

		ForeignKeyQuerySourceDimKeyRel
			one-to-many relation to BirstModelQuerySourceField
			Field Mapping uses ByDimensionKey
				related.BirstModel				= BirstModel
				related.BirstModelQuerySource	= ForeignKeyQuerySource
				related.IncludeInBirstModel		= true
				related.IsDimensionKey			= true


