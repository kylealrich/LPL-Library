AverageDailyBalanceTransaction is a BusinessClass
    owned by ad
    prefix is ADBDT

    Ontology
    	symbolic key is AverageDailyBalanceTransaction

    Patterns
 		implements DynamicCreation
		implements LightweightAuditing
		disable AuditIndex
 		disable EffectiveDated
		disable AsOfDateProcessing 		

	Context Fields

    Persistent Fields
        ADBDimension1					is like AccountingUnit
        ADBDimension2					is like AccountingUnit
        ADBDimension3					is like AccountingUnit
        Status							is Numeric 1
            States
                Unreleased				value is 0
                Posted					value is 9
 			disable Auditing
		FunctionalAmount				is a InternationalAmount
 			disable Auditing
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
        BasisLastRunDate				is Date
        NonChartAccount					is Boolean
        TransactionLedger				is like Ledger
	        default label is "Ledger"
        Year							is a GeneralLedgerCalendarYear	
        TransactionCurrency				is like Currency
        TransactionAmount				is a InternationalAmount
        	disable Auditing
        	precision is TransactionCurrencyDecimals
        AlternateAmount1				is a InternationalAmount	
        	disable Auditing
			precision is AccountingEntity.AlternateCurrency.NumberOfDecimals
        AlternateAmount2				is a InternationalAmount
        	disable Auditing
			precision is AccountingEntity.AlternateCurrency2.NumberOfDecimals
        AlternateAmount3				is a InternationalAmount
        	disable Auditing
			precision is AccountingEntity.AlternateCurrency3.NumberOfDecimals
		ReportAmount1					is a InternationalAmount	
        	disable Auditing
 			precision is FinanceEnterpriseGroup.ReportCurrencyOne.NumberOfDecimals
		ReportAmount2					is a InternationalAmount
        	disable Auditing
			precision is FinanceEnterpriseGroup.ReportCurrencyTwo.NumberOfDecimals
		ReportAmount3					is a InternationalAmount
        	disable Auditing
			precision is FinanceEnterpriseGroup.ReportCurrencyThree.NumberOfDecimals
		ReportAmount4					is a InternationalAmount
        	disable Auditing
			precision is FinanceEnterpriseGroup.ReportCurrencyFour.NumberOfDecimals
		ReportAmount5					is a InternationalAmount
        	disable Auditing
			precision is FinanceEnterpriseGroup.ReportCurrencyFive.NumberOfDecimals

	Transient Fields
		PrmFunctionalAmount				is a InternationalAmount
			default label is "FunctionalAmount"
        PrmTransactionAmount			is a InternationalAmount
			default label is "TransactionAmount"
        PrmAlternateAmount1				is a InternationalAmount
			default label is "AlternateAmount1"
        PrmAlternateAmount2				is a InternationalAmount
			default label is "AlternateAmount2"
        PrmAlternateAmount3				is a InternationalAmount
			default label is "AlternateAmount3"
		PrmReportAmount1				is a InternationalAmount
			default label is "ReportAmount1"
		PrmReportAmount2				is a InternationalAmount
			default label is "ReportAmount2"
		PrmReportAmount3				is a InternationalAmount
			default label is "ReportAmount3"
		PrmReportAmount4				is a InternationalAmount
			default label is "ReportAmount4"
		PrmReportAmount5				is a InternationalAmount
			default label is "ReportAmount5"

	Derived Fields
		DerivedUndistributedRetainedEarnings is a DerivedField
			type is like GeneralLedgerChartAccount
			restricted
			LocalSystemAccount = SystemAccount.UndistributedRetainedEarnings
			return first SystemAccountRel.GeneralLedgerChartAccount
		DerivedRetainedEarnings is a DerivedField
			type is like GeneralLedgerChartAccount
			restricted
			LocalSystemAccount = SystemAccount.RetainedEarnings
			if (FinanceEnterpriseGroup.SystemSubAccount entered
			and SystemAccountRel exists)
				return first SystemAccountRel.Account + "-" + FinanceEnterpriseGroup.SystemSubAccount
			else
				return first SystemAccountRel.GeneralLedgerChartAccount

		TransactionCurrencyDecimals is a DerivedField
			type is Numeric 1
			restricted
			LocalCurrencyCode = TransactionCurrency
			return LocalCurrencyCode.NumberOfDecimals

	Local Fields
		LocalGeneralLedgerCalendar			is like GeneralLedgerCalendar
		LocalAccountingEntity				is like AccountingEntity
		LocalReportingChart					is like ReportingChart
		LocalReportingBasis					is a ReportingBasis
		LocalDate							is Date
		LocalSummaryPeriodType	 			is Numeric 1
			States
				Year					    value is 1
				Quarter					    value is 2
				Month					    value is 3
				Week					    value is 4
				Day							value is 8					
				EndDate					    value is 5
				TopNode    	   				value is 7
		LocalInstGeneralLedgerChartAccount	is like GeneralLedgerChartAccount
		LocalInstDimensionCode				is Alpha 70
		LocalInstGeneralLedgerCalendarPeriod is like GeneralLedgerCalendarPeriod
		LocalSystemAccount					is a SystemAccount
		LocalCurrencyCode					is a Currency

	Conditions

	Sets				
		ByStatusEntityAccount
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				Status
				AccountingEntity
        	    AverageDailyBalanceTransaction.GeneralLedgerChartAccount
				ADBDimension1
				ADBDimension2
				ADBDimension3
				AverageDailyBalanceTransaction.PostingDate
				AverageDailyBalanceTransaction.TransactionDate
				AverageDailyBalanceTransaction.RunDate
        	    AverageDailyBalanceTransaction.DimensionCode

		ByStatusEntityPostingDate
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				Status
				AccountingEntity
				AverageDailyBalanceTransaction.PostingDate
				AverageDailyBalanceTransaction.TransactionDate
        	    AverageDailyBalanceTransaction.GeneralLedgerChartAccount
				ADBDimension1
				ADBDimension2
				ADBDimension3
				AverageDailyBalanceTransaction.RunDate
        	    AverageDailyBalanceTransaction.DimensionCode

		ByEntityAccountADBDimensions
			Sort Order
				FinanceEnterpriseGroup
				AccountingEntity
        	    AverageDailyBalanceTransaction.GeneralLedgerChartAccount
				ADBDimension1
				ADBDimension2
				ADBDimension3
				AverageDailyBalanceTransaction.PostingDate
				AverageDailyBalanceTransaction.TransactionDate
				AverageDailyBalanceTransaction.RunDate
        	    AverageDailyBalanceTransaction.DimensionCode

	Relations
		GeneralLedgerCalendarDateRel
			one-to-one relation to GeneralLedgerCalendarDate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup						= FinanceEnterpriseGroup
				related.GeneralLedgerCalendar						= LocalGeneralLedgerCalendar
				related.GeneralLedgerCalendarDate					= LocalDate
		GeneralLedgerCalendarPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup						= FinanceEnterpriseGroup
			Instance Selection
				where (related.Date 							    >=  LocalDate)
		GeneralLedgerPeriodShadowRel
			one-to-many relation to GeneralLedgerPeriodShadow
			Field Mapping uses BySummaryPeriodType
				related.FinanceEnterpriseGroup						= FinanceEnterpriseGroup
				related.GeneralLedgerCalendar						= LocalGeneralLedgerCalendar
				related.GeneralLedgerCalendarPeriod					= first GeneralLedgerCalendarPeriodRel.GeneralLedgerCalendarPeriod
				related.SummaryPeriodType							= LocalSummaryPeriodType
		ReportingChartAccountRel
			one-to-one relation to ReportingChartAccount
			Field Mapping uses AccountByChartType
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ReportingChart					= LocalReportingChart
				related.ChartType						= 2 
				related.GeneralLedgerChartAccount		= AverageDailyBalanceTransaction.GeneralLedgerChartAccount
		UpdateAverageDailyBalanceAmountRel
			one-to-one relation to AverageDailyBalanceAmount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup								= FinanceEnterpriseGroup
				related.ReportingBasis										= LocalReportingBasis
				related.AccountingEntity									= LocalAccountingEntity
				related.AverageDailyBalanceAmount.GeneralLedgerChartAccount	= LocalInstGeneralLedgerChartAccount
				related.AverageDailyBalanceAmount.DimensionCode				= LocalInstDimensionCode
				related.AverageDailyBalanceAmount.RunDate 					= LocalDate
		UpdateAverageDailyBalanceAggregateRel
			one-to-one relation to AverageDailyBalanceAggregate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup										= FinanceEnterpriseGroup
				related.ReportingBasis												= LocalReportingBasis
				related.AccountingEntity											= LocalAccountingEntity
				related.AverageDailyBalanceAggregate.GeneralLedgerChartAccount		= LocalInstGeneralLedgerChartAccount
				related.AverageDailyBalanceAggregate.DimensionCode					= LocalInstDimensionCode
				related.AverageDailyBalanceAggregate.GeneralLedgerCalendarPeriod	= LocalInstGeneralLedgerCalendarPeriod
		UpdateAverageDailyBalanceAccountRel
			one-to-one relation to AverageDailyBalanceAccount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
				related.AccountingEntity										= LocalAccountingEntity
				related.AverageDailyBalanceAccount.GeneralLedgerChartAccount	= LocalInstGeneralLedgerChartAccount
				related.AverageDailyBalanceAccount.DimensionCode				= LocalInstDimensionCode
		AverageDailyBalanceAccountRel
			one-to-one relation to AverageDailyBalanceAccount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup									= FinanceEnterpriseGroup
				related.AccountingEntity										= AccountingEntity
				related.AverageDailyBalanceAccount.GeneralLedgerChartAccount	= AverageDailyBalanceTransaction.GeneralLedgerChartAccount
				related.AverageDailyBalanceAccount.DimensionCode				= AverageDailyBalanceTransaction.DimensionCode

		SystemAccountRel
			one-to-many relation to GeneralLedgerChartAccount
			Field Mapping uses BySystemAccount
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.SystemAccount				= LocalSystemAccount

	Field Rules
		AverageDailyBalanceTransaction.GeneralLedgerChartAccount
			required

	Rule Blocks

		UpdateActualTotal
			FunctionalAmount				+= PrmFunctionalAmount
			if (FinanceEnterpriseGroup.CalculateADBOption.AllCurrencyAmounts)
		        TransactionAmount			+= PrmTransactionAmount
		        AlternateAmount1			+= PrmAlternateAmount1
		        AlternateAmount2			+= PrmAlternateAmount2
		        AlternateAmount3			+= PrmAlternateAmount3
				ReportAmount1				+= PrmReportAmount1
				ReportAmount2				+= PrmReportAmount2
				ReportAmount3				+= PrmReportAmount3
				ReportAmount4				+= PrmReportAmount4
				ReportAmount5				+= PrmReportAmount5

		GetCalendarPeriod
			LocalGeneralLedgerCalendar			= PrmReportingBasis.GeneralLedgerCalendar
			if (GeneralLedgerCalendarDateRel exists)
				LocalYTDCalendarPeriod			= GeneralLedgerCalendarDateRel.YearToDateCalendarPeriod
				LocalQTDCalendarPeriod			= GeneralLedgerCalendarDateRel.QuarterToDateCalendarPeriod
				LocalMTDCalendarPeriod			= GeneralLedgerCalendarDateRel.MonthToDateCalendarPeriod
				LocalWTDCalendarPeriod			= GeneralLedgerCalendarDateRel.WeekToDateCalendarPeriod
			else
				LocalSummaryPeriodType			= 1
				if (GeneralLedgerPeriodShadowRel exists)
					LocalYTDCalendarPeriod		= GeneralLedgerPeriodShadowRel.SummaryGeneralLedgerCalendarPeriod
				LocalSummaryPeriodType			= 2
				if (GeneralLedgerPeriodShadowRel exists)
					LocalQTDCalendarPeriod		= GeneralLedgerPeriodShadowRel.SummaryGeneralLedgerCalendarPeriod
				LocalSummaryPeriodType			= 3
				if (GeneralLedgerPeriodShadowRel exists)
					LocalMTDCalendarPeriod		= GeneralLedgerPeriodShadowRel.SummaryGeneralLedgerCalendarPeriod
				LocalSummaryPeriodType			= 4
				if (GeneralLedgerPeriodShadowRel exists)
					LocalWTDCalendarPeriod		= GeneralLedgerPeriodShadowRel.SummaryGeneralLedgerCalendarPeriod

		UpdateAggregateAmounts	
			LocalReportingBasis							= PrmReportingBasis
			LocalAccountingEntity						= PrmAccountingEntity
			LocalInstGeneralLedgerChartAccount			= LocalGeneralLedgerChartAccount
			LocalInstDimensionCode						= LocalDimensionCode
			if (LocalYTDCalendarPeriodOld entered
			and LocalYTDCalendarPeriod		!= LocalYTDCalendarPeriodOld)
				LocalInstGeneralLedgerCalendarPeriod					= LocalYTDCalendarPeriodOld
				invoke Update UpdateAverageDailyBalanceAggregateRel
					invoked.ADBDimension1								= LocalDimension1
					invoked.ADBDimension2								= LocalDimension2
					invoked.ADBDimension3								= LocalDimension3
					invoked.PrmBalanceOnPostDate						= LocalYTDPeriodAmount
					invoked.PrmBalanceOnTransactionDate					= LocalYTDPeriodTranAmount
					invoked.TransactionLedger							= LocalLedger
					invoked.TransactionCurrency							= LocalTransactionCurrency
					invoked.PrmOnPostDateTransactionAmount				= TranAmountYTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount1				= AltAmount1YTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount2				= AltAmount2YTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount3				= AltAmount3YTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount1					= ReportAmt1YTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount2					= ReportAmt2YTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount3					= ReportAmt3YTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount4					= ReportAmt4YTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount5					= ReportAmt5YTDAggregateOnPostDate
				LocalYTDCalendarPeriodOld								= LocalYTDCalendarPeriod
				initialize LocalYTDPeriodAmount
				initialize LocalYTDPeriodTranAmount
				initialize TranAmountYTDAggregateOnPostDate
				initialize AltAmount1YTDAggregateOnPostDate
				initialize AltAmount2YTDAggregateOnPostDate
				initialize AltAmount3YTDAggregateOnPostDate
				initialize ReportAmt1YTDAggregateOnPostDate
				initialize ReportAmt2YTDAggregateOnPostDate
				initialize ReportAmt3YTDAggregateOnPostDate
				initialize ReportAmt4YTDAggregateOnPostDate
				initialize ReportAmt5YTDAggregateOnPostDate
			if (LocalQTDCalendarPeriodOld entered
			and LocalQTDCalendarPeriod		!= LocalQTDCalendarPeriodOld)
				LocalInstGeneralLedgerCalendarPeriod					= LocalQTDCalendarPeriodOld
				invoke Update UpdateAverageDailyBalanceAggregateRel
					invoked.ADBDimension1								= LocalDimension1
					invoked.ADBDimension2								= LocalDimension2
					invoked.ADBDimension3								= LocalDimension3
					invoked.PrmBalanceOnPostDate						= LocalQTDPeriodAmount
					invoked.PrmBalanceOnTransactionDate					= LocalQTDPeriodTranAmount
					invoked.TransactionLedger							= LocalLedger
					invoked.TransactionCurrency							= LocalTransactionCurrency
					invoked.PrmOnPostDateTransactionAmount				= TranAmountQTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount1				= AltAmount1QTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount2				= AltAmount2QTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount3				= AltAmount3QTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount1					= ReportAmt1QTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount2					= ReportAmt2QTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount3					= ReportAmt3QTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount4					= ReportAmt4QTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount5					= ReportAmt5QTDAggregateOnPostDate
				LocalQTDCalendarPeriodOld								= LocalQTDCalendarPeriod
				initialize LocalQTDPeriodAmount
				initialize LocalQTDPeriodTranAmount
				initialize TranAmountQTDAggregateOnPostDate
				initialize AltAmount1QTDAggregateOnPostDate
				initialize AltAmount2QTDAggregateOnPostDate
				initialize AltAmount3QTDAggregateOnPostDate
				initialize ReportAmt1QTDAggregateOnPostDate
				initialize ReportAmt2QTDAggregateOnPostDate
				initialize ReportAmt3QTDAggregateOnPostDate
				initialize ReportAmt4QTDAggregateOnPostDate
				initialize ReportAmt5QTDAggregateOnPostDate
			if (LocalMTDCalendarPeriodOld entered
			and LocalMTDCalendarPeriod		!= LocalMTDCalendarPeriodOld)
				LocalInstGeneralLedgerCalendarPeriod					= LocalMTDCalendarPeriodOld
				invoke Update UpdateAverageDailyBalanceAggregateRel
					invoked.ADBDimension1								= LocalDimension1
					invoked.ADBDimension2								= LocalDimension2
					invoked.ADBDimension3								= LocalDimension3
					invoked.PrmBalanceOnPostDate						= LocalMTDPeriodAmount
					invoked.PrmBalanceOnTransactionDate					= LocalMTDPeriodTranAmount
					invoked.TransactionLedger							= LocalLedger
					invoked.TransactionCurrency							= LocalTransactionCurrency
					invoked.PrmOnPostDateTransactionAmount				= TranAmountMTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount1				= AltAmount1MTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount2				= AltAmount2MTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount3				= AltAmount3MTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount1					= ReportAmt1MTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount2					= ReportAmt2MTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount3					= ReportAmt3MTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount4					= ReportAmt4MTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount5					= ReportAmt5MTDAggregateOnPostDate
				LocalMTDCalendarPeriodOld								= LocalMTDCalendarPeriod
				initialize LocalMTDPeriodAmount
				initialize LocalMTDPeriodTranAmount
				initialize TranAmountMTDAggregateOnPostDate
				initialize AltAmount1MTDAggregateOnPostDate
				initialize AltAmount2MTDAggregateOnPostDate
				initialize AltAmount3MTDAggregateOnPostDate
				initialize ReportAmt1MTDAggregateOnPostDate
				initialize ReportAmt2MTDAggregateOnPostDate
				initialize ReportAmt3MTDAggregateOnPostDate
				initialize ReportAmt4MTDAggregateOnPostDate
				initialize ReportAmt5MTDAggregateOnPostDate
			if (LocalWTDCalendarPeriodOld entered
			and LocalWTDCalendarPeriod		!= LocalWTDCalendarPeriodOld)
				LocalInstGeneralLedgerCalendarPeriod					= LocalWTDCalendarPeriodOld
				invoke Update UpdateAverageDailyBalanceAggregateRel
					invoked.ADBDimension1								= LocalDimension1
					invoked.ADBDimension2								= LocalDimension2
					invoked.ADBDimension3								= LocalDimension3
					invoked.PrmBalanceOnPostDate						= LocalWTDPeriodAmount
					invoked.PrmBalanceOnTransactionDate					= LocalWTDPeriodTranAmount
					invoked.TransactionLedger							= LocalLedger
					invoked.TransactionCurrency							= LocalTransactionCurrency
					invoked.PrmOnPostDateTransactionAmount				= TranAmountWTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount1				= AltAmount1WTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount2				= AltAmount2WTDAggregateOnPostDate
					invoked.PrmOnPostDateAlternateAmount3				= AltAmount3WTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount1					= ReportAmt1WTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount2					= ReportAmt2WTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount3					= ReportAmt3WTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount4					= ReportAmt4WTDAggregateOnPostDate
					invoked.PrmOnPostDateReportAmount5					= ReportAmt5WTDAggregateOnPostDate
				LocalWTDCalendarPeriodOld								= LocalWTDCalendarPeriod
				initialize LocalWTDPeriodAmount
				initialize LocalWTDPeriodTranAmount
				initialize TranAmountWTDAggregateOnPostDate
				initialize AltAmount1WTDAggregateOnPostDate
				initialize AltAmount2WTDAggregateOnPostDate
				initialize AltAmount3WTDAggregateOnPostDate
				initialize ReportAmt1WTDAggregateOnPostDate
				initialize ReportAmt2WTDAggregateOnPostDate
				initialize ReportAmt3WTDAggregateOnPostDate
				initialize ReportAmt4WTDAggregateOnPostDate
				initialize ReportAmt5WTDAggregateOnPostDate

	Actions
		Create is a Create Action
			default label is untranslatable
			restricted
			bypass field rules
			Action Rules
				include UpdateActualTotal

		Update is an Update Action
			default label is untranslatable
			restricted
			bypass field rules
			Action Rules
				include UpdateActualTotal

		Delete is a Delete Action
			default label is untranslatable
			restricted
			Action Rules

		UpdateDailyAggregateAmount is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis				is a ReportingBasis
					default label is "ReportingBasis"
				PrmAccountingEntity				is a AccountingEntity
					default label is "<FinanceEnterpriseGroup.AccountingEntityLabel>"
				PrmLastRunDate					is Date
					default label is "LastRunDate"
				PrmRunDate				is Date
					default label is "RunDate"
				PrmNonChartAccountOnly			is Boolean
				PrmAccount						is like GeneralLedgerChartAccount
				PrmDimension1					is like AccountingUnit
				PrmDimension2					is like AccountingUnit
				PrmDimension3					is like AccountingUnit
				PrmDateRange					is a DateRange
				PrmProcessingType				is Numeric 1
					States
						DailyBalanceProcessing	value is 0
						RecalculatePreviousYear value is 4
						RecalculateCurrentYear	value is 5

			Parameter Rules
				PrmReportingBasis
					LocalReportingBasis			= PrmReportingBasis
					LocalReportingChart			= PrmReportingBasis.ReportingChart
				PrmAccountingEntity
					LocalAccountingEntity		= PrmAccountingEntity

			Queue Mapping Fields
				PrmAccountingEntity.PostingCategory
				
			Set Is
				PrmFinanceEnterpriseGroup
				PrmReportingBasis
				PrmAccountingEntity

			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    AccountingEntity       = PrmAccountingEntity
				and	   Status.Unreleased
				and    (PrmAccount    not entered or PrmAccount    = AverageDailyBalanceTransaction.GeneralLedgerChartAccount)
				and    (PrmDimension1 not entered or PrmDimension1 = ADBDimension1)
				and    (PrmDimension2 not entered or PrmDimension2 = ADBDimension2)
				and    (PrmDimension2 not entered or PrmDimension3 = ADBDimension3)
				and    (NonChartAccount           or (not PrmNonChartAccountOnly and ReportingChartAccountRel exists))
				and    ((PrmProcessingType.DailyBalanceProcessing
				and      AverageDailyBalanceTransaction.PostingDate <= PrmRunDate
				and 	 (PrmFinanceEnterpriseGroup.ADBProcessingDate.PostingDate or (AverageDailyBalanceTransaction.TransactionDate <= PrmRunDate)))
				or		((PrmProcessingType.RecalculatePreviousYear or PrmProcessingType.RecalculateCurrentYear)
				and      AverageDailyBalanceTransaction.PostingDate within PrmDateRange))
				and    (!AverageDailyBalanceTransaction.GeneralLedgerChartAccount.SystemAccount.AverageDailyBalance))

			Sort Order	
				FinanceEnterpriseGroup
				AccountingEntity
        	    AverageDailyBalanceTransaction.GeneralLedgerChartAccount
        	    AverageDailyBalanceTransaction.DimensionCode
				AverageDailyBalanceTransaction.PostingDate
				AverageDailyBalanceTransaction.TransactionDate
				AverageDailyBalanceTransaction.RunDate

			Accumulators
				FunctionalTotal
				TransactionAmountTotal
				AlternateAmount1Total
				AlternateAmount2Total
				AlternateAmount3Total
				ReportAmount1Total
				ReportAmount2Total
				ReportAmount3Total
				ReportAmount4Total
				ReportAmount5Total

			Local Fields
				LocalYTDCalendarPeriod				is like GeneralLedgerCalendarPeriod
				LocalQTDCalendarPeriod				is like GeneralLedgerCalendarPeriod
				LocalMTDCalendarPeriod				is like GeneralLedgerCalendarPeriod
				LocalWTDCalendarPeriod				is like GeneralLedgerCalendarPeriod
				LocalYTDCalendarPeriodOld			is like GeneralLedgerCalendarPeriod
				LocalQTDCalendarPeriodOld			is like GeneralLedgerCalendarPeriod
				LocalMTDCalendarPeriodOld			is like GeneralLedgerCalendarPeriod
				LocalWTDCalendarPeriodOld			is like GeneralLedgerCalendarPeriod
				LocalYTDPeriodAmount				is an InternationalAmount	
				LocalQTDPeriodAmount				is an InternationalAmount
				LocalMTDPeriodAmount				is an InternationalAmount
				LocalWTDPeriodAmount				is an InternationalAmount
				LocalYTDPeriodTranAmount			is an InternationalAmount	
				LocalQTDPeriodTranAmount			is an InternationalAmount
				LocalMTDPeriodTranAmount			is an InternationalAmount
				LocalWTDPeriodTranAmount			is an InternationalAmount
				LocalPostingDate					is Date
				LocalTransactionDate				is Date
				LocalRunDate						is Date
				LocalLastADBDate					is Date
				LocalRecordCount					is Numeric 4
        		LocalGeneralLedgerChartAccount		is like GeneralLedgerChartAccount

				LocalDimensionCode					is Alpha 70
				LocalDimension1						is like AccountingUnit
				LocalDimension2						is like AccountingUnit
				LocalDimension3						is like AccountingUnit
				LocalLedger							is like Ledger
				LocalTransactionCurrency			is like Currency
				LocalAccountChartSection			is a ChartSection

				TranAmountYTDAggregateOnPostDate	is an InternationalAmount	
				TranAmountQTDAggregateOnPostDate	is an InternationalAmount
				TranAmountMTDAggregateOnPostDate	is an InternationalAmount
				TranAmountWTDAggregateOnPostDate	is an InternationalAmount
				AltAmount1YTDAggregateOnPostDate	is an InternationalAmount	
				AltAmount1QTDAggregateOnPostDate	is an InternationalAmount
				AltAmount1MTDAggregateOnPostDate	is an InternationalAmount
				AltAmount1WTDAggregateOnPostDate	is an InternationalAmount
				AltAmount2YTDAggregateOnPostDate	is an InternationalAmount	
				AltAmount2QTDAggregateOnPostDate	is an InternationalAmount
				AltAmount2MTDAggregateOnPostDate	is an InternationalAmount
				AltAmount2WTDAggregateOnPostDate	is an InternationalAmount
				AltAmount3YTDAggregateOnPostDate	is an InternationalAmount	
				AltAmount3QTDAggregateOnPostDate	is an InternationalAmount
				AltAmount3MTDAggregateOnPostDate	is an InternationalAmount
				AltAmount3WTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt1YTDAggregateOnPostDate	is an InternationalAmount	
				ReportAmt1QTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt1MTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt1WTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt2YTDAggregateOnPostDate	is an InternationalAmount	
				ReportAmt2QTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt2MTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt2WTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt3YTDAggregateOnPostDate	is an InternationalAmount	
				ReportAmt3QTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt3MTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt3WTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt4YTDAggregateOnPostDate	is an InternationalAmount	
				ReportAmt4QTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt4MTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt4WTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt5YTDAggregateOnPostDate	is an InternationalAmount	
				ReportAmt5QTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt5MTDAggregateOnPostDate	is an InternationalAmount
				ReportAmt5WTDAggregateOnPostDate	is an InternationalAmount

			Action Rules
				Empty Set Rules

				Set Rules

				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						LocalRunDate				= PrmRunDate

					Exit Rules

				AverageDailyBalanceTransaction.RunDate Set Rules
					Entrance Rules
						LocalPostingDate					= AverageDailyBalanceTransaction.PostingDate
						LocalTransactionDate				= AverageDailyBalanceTransaction.TransactionDate
						LocalGeneralLedgerChartAccount		= AverageDailyBalanceTransaction.GeneralLedgerChartAccount
						LocalAccountChartSection			= AverageDailyBalanceTransaction.GeneralLedgerChartAccount.ChartSection
						LocalDimensionCode					= AverageDailyBalanceTransaction.DimensionCode
						LocalDimension1						= ADBDimension1
						LocalDimension2						= ADBDimension2
						LocalDimension3						= ADBDimension3
						LocalLedger							= TransactionLedger
						LocalTransactionCurrency			= TransactionCurrency
						initialize LocalYTDCalendarPeriodOld
						initialize LocalQTDCalendarPeriodOld
						initialize LocalMTDCalendarPeriodOld
						initialize LocalWTDCalendarPeriodOld

					Exit Rules
						if (FunctionalTotal entered)
							if (PrmFinanceEnterpriseGroup.ADBProcessingDate.PostingDate)
								LocalDate				= LocalPostingDate
							else
								if (LocalPostingDate	< LocalTransactionDate)
									LocalDate				= LocalPostingDate
								else
									LocalDate				= LocalTransactionDate
							initialize LocalYTDCalendarPeriod
							initialize LocalQTDCalendarPeriod
							initialize LocalMTDCalendarPeriod
							initialize LocalWTDCalendarPeriod
							initialize LocalYTDPeriodAmount
							initialize LocalQTDPeriodAmount
							initialize LocalMTDPeriodAmount
							initialize LocalWTDPeriodAmount
							initialize LocalYTDPeriodTranAmount
							initialize LocalQTDPeriodTranAmount
							initialize LocalMTDPeriodTranAmount
							initialize LocalWTDPeriodTranAmount

							initialize TranAmountYTDAggregateOnPostDate
							initialize TranAmountQTDAggregateOnPostDate
							initialize TranAmountMTDAggregateOnPostDate
							initialize TranAmountWTDAggregateOnPostDate
							initialize AltAmount1YTDAggregateOnPostDate
							initialize AltAmount1QTDAggregateOnPostDate
							initialize AltAmount1MTDAggregateOnPostDate
							initialize AltAmount1WTDAggregateOnPostDate
							initialize AltAmount2YTDAggregateOnPostDate
							initialize AltAmount2QTDAggregateOnPostDate
							initialize AltAmount2MTDAggregateOnPostDate
							initialize AltAmount2WTDAggregateOnPostDate
							initialize AltAmount3YTDAggregateOnPostDate
							initialize AltAmount3QTDAggregateOnPostDate
							initialize AltAmount3MTDAggregateOnPostDate
							initialize AltAmount3WTDAggregateOnPostDate
							initialize ReportAmt1YTDAggregateOnPostDate
							initialize ReportAmt1QTDAggregateOnPostDate
							initialize ReportAmt1MTDAggregateOnPostDate
							initialize ReportAmt1WTDAggregateOnPostDate
							initialize ReportAmt2YTDAggregateOnPostDate
							initialize ReportAmt2QTDAggregateOnPostDate
							initialize ReportAmt2MTDAggregateOnPostDate
							initialize ReportAmt2WTDAggregateOnPostDate
							initialize ReportAmt3YTDAggregateOnPostDate
							initialize ReportAmt3QTDAggregateOnPostDate
							initialize ReportAmt3MTDAggregateOnPostDate
							initialize ReportAmt3WTDAggregateOnPostDate
							initialize ReportAmt4YTDAggregateOnPostDate
							initialize ReportAmt4QTDAggregateOnPostDate
							initialize ReportAmt4MTDAggregateOnPostDate
							initialize ReportAmt4WTDAggregateOnPostDate
							initialize ReportAmt5YTDAggregateOnPostDate
							initialize ReportAmt5QTDAggregateOnPostDate
							initialize ReportAmt5MTDAggregateOnPostDate
							initialize ReportAmt5WTDAggregateOnPostDate
							include GetCalendarPeriod
							LocalYTDCalendarPeriodOld		= LocalYTDCalendarPeriod
							LocalQTDCalendarPeriodOld		= LocalQTDCalendarPeriod
							LocalMTDCalendarPeriodOld		= LocalMTDCalendarPeriod
							LocalWTDCalendarPeriodOld		= LocalWTDCalendarPeriod
							while (LocalDate	<= LocalRunDate)
								LocalReportingBasis							= PrmReportingBasis
								LocalAccountingEntity						= PrmAccountingEntity
								LocalInstGeneralLedgerChartAccount			= LocalGeneralLedgerChartAccount
								LocalInstDimensionCode						= LocalDimensionCode
								invoke Update UpdateAverageDailyBalanceAmountRel
									invoked.ADBDimension1			= LocalDimension1
									invoked.ADBDimension2			= LocalDimension2
									invoked.ADBDimension3			= LocalDimension3
									invoked.TransactionLedger		= LocalLedger
									invoked.TransactionCurrency		= LocalTransactionCurrency
									if (LocalDate					>= PrmLastRunDate
									and LocalDate					= LocalPostingDate)
										invoked.PrmAmount						= FunctionalTotal
										invoked.PrmTransactionAmount			= TransactionAmountTotal
									if (LocalDate					< PrmLastRunDate
									and LocalDate					= LocalPostingDate)
										invoked.PrmAdjustmentAmount				= FunctionalTotal
										invoked.PrmTransactionAmount			= TransactionAmountTotal
									if (LocalDate					>= LocalPostingDate)
										invoked.PrmBalanceOnPostDate			= FunctionalTotal
										invoked.PrmOnPostDateTransactionAmount	= TransactionAmountTotal
										invoked.PrmOnPostDateAlternateAmount1	= AlternateAmount1Total
										invoked.PrmOnPostDateAlternateAmount2	= AlternateAmount2Total
										invoked.PrmOnPostDateAlternateAmount3	= AlternateAmount3Total
										invoked.PrmOnPostDateReportAmount1		= ReportAmount1Total
										invoked.PrmOnPostDateReportAmount2		= ReportAmount2Total
										invoked.PrmOnPostDateReportAmount3		= ReportAmount3Total
										invoked.PrmOnPostDateReportAmount4		= ReportAmount4Total
										invoked.PrmOnPostDateReportAmount5		= ReportAmount5Total
									if (LocalDate					>= LocalTransactionDate)
										invoked.PrmBalanceOnTransactionDate	= FunctionalTotal
								include UpdateAggregateAmounts
								if (LocalDate					>= LocalPostingDate)
									LocalYTDPeriodAmount		+= FunctionalTotal
									LocalQTDPeriodAmount		+= FunctionalTotal
									LocalMTDPeriodAmount		+= FunctionalTotal
									LocalWTDPeriodAmount		+= FunctionalTotal
									TranAmountYTDAggregateOnPostDate += TransactionAmountTotal
									TranAmountQTDAggregateOnPostDate += TransactionAmountTotal
									TranAmountMTDAggregateOnPostDate += TransactionAmountTotal
									TranAmountWTDAggregateOnPostDate += TransactionAmountTotal
									AltAmount1YTDAggregateOnPostDate += AlternateAmount1Total
									AltAmount1QTDAggregateOnPostDate += AlternateAmount1Total
									AltAmount1MTDAggregateOnPostDate += AlternateAmount1Total
									AltAmount1WTDAggregateOnPostDate += AlternateAmount1Total
									AltAmount2YTDAggregateOnPostDate += AlternateAmount2Total
									AltAmount2QTDAggregateOnPostDate += AlternateAmount2Total
									AltAmount2MTDAggregateOnPostDate += AlternateAmount2Total
									AltAmount2WTDAggregateOnPostDate += AlternateAmount2Total
									AltAmount3YTDAggregateOnPostDate += AlternateAmount3Total
									AltAmount3QTDAggregateOnPostDate += AlternateAmount3Total
									AltAmount3MTDAggregateOnPostDate += AlternateAmount3Total
									AltAmount3WTDAggregateOnPostDate += AlternateAmount3Total
									ReportAmt1YTDAggregateOnPostDate += ReportAmount1Total
									ReportAmt1QTDAggregateOnPostDate += ReportAmount1Total
									ReportAmt1MTDAggregateOnPostDate += ReportAmount1Total
									ReportAmt1WTDAggregateOnPostDate += ReportAmount1Total
									ReportAmt2YTDAggregateOnPostDate += ReportAmount2Total
									ReportAmt2QTDAggregateOnPostDate += ReportAmount2Total
									ReportAmt2MTDAggregateOnPostDate += ReportAmount2Total
									ReportAmt2WTDAggregateOnPostDate += ReportAmount2Total
									ReportAmt3YTDAggregateOnPostDate += ReportAmount3Total
									ReportAmt3QTDAggregateOnPostDate += ReportAmount3Total
									ReportAmt3MTDAggregateOnPostDate += ReportAmount3Total
									ReportAmt3WTDAggregateOnPostDate += ReportAmount3Total
									ReportAmt4YTDAggregateOnPostDate += ReportAmount4Total
									ReportAmt4QTDAggregateOnPostDate += ReportAmount4Total
									ReportAmt4MTDAggregateOnPostDate += ReportAmount4Total
									ReportAmt4WTDAggregateOnPostDate += ReportAmount4Total
									ReportAmt5YTDAggregateOnPostDate += ReportAmount5Total
									ReportAmt5QTDAggregateOnPostDate += ReportAmount5Total
									ReportAmt5MTDAggregateOnPostDate += ReportAmount5Total
									ReportAmt5WTDAggregateOnPostDate += ReportAmount5Total
								if (LocalDate					>= LocalTransactionDate)
									LocalYTDPeriodTranAmount	+= FunctionalTotal
									LocalQTDPeriodTranAmount	+= FunctionalTotal
									LocalMTDPeriodTranAmount	+= FunctionalTotal
									LocalWTDPeriodTranAmount	+= FunctionalTotal
								LocalLastADBDate	= LocalDate
								LocalDate			+= 1
								initialize LocalYTDCalendarPeriod
								initialize LocalQTDCalendarPeriod
								initialize LocalMTDCalendarPeriod
								initialize LocalWTDCalendarPeriod
								include GetCalendarPeriod
								if ((LocalAccountChartSection.IncomeStatement or  LocalInstGeneralLedgerChartAccount    = DerivedUndistributedRetainedEarnings)
								and (LocalYTDCalendarPeriodOld entered        and LocalYTDCalendarPeriodOld         not = LocalYTDCalendarPeriod)	
								and LocalDate > LocalPostingDate)	
									if (PrmProcessingType.DailyBalanceProcessing) 
										invoke Update UpdateAverageDailyBalanceAccountRel
											invoked.LastADBDate	= LocalLastADBDate
									end while		
								else
								if (LocalDate > LocalRunDate
								and PrmProcessingType.DailyBalanceProcessing) 
									invoke Update UpdateAverageDailyBalanceAccountRel
										invoked.LastADBDate	= LocalLastADBDate

							initialize LocalYTDCalendarPeriod
							initialize LocalQTDCalendarPeriod
							initialize LocalMTDCalendarPeriod
							initialize LocalWTDCalendarPeriod
							include UpdateAggregateAmounts

				Instance Rules
					if (AverageDailyBalanceAccountRel.Active)
						FunctionalTotal				+= FunctionalAmount
						Status						= 9
						BasisLastRunDate			= PrmLastRunDate
						TransactionAmountTotal		+= TransactionAmount
						AlternateAmount1Total		+= AlternateAmount1
						AlternateAmount2Total		+= AlternateAmount2
						AlternateAmount3Total		+= AlternateAmount3
						ReportAmount1Total			+= ReportAmount1
						ReportAmount2Total			+= ReportAmount2
						ReportAmount3Total			+= ReportAmount3
						ReportAmount4Total			+= ReportAmount4
						ReportAmount5Total			+= ReportAmount5

		Purge is a Purge Action
			default label is untranslatable
			restricted

		PurgeData is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmAccount						is like GeneralLedgerChartAccount
				PrmStartDate					is Date
				PrmEndDate						is Date
				PrmAccountingEntity						is like AccountingEntity
				PrmDimension1							is like AccountingUnit
				PrmDimension2							is like AccountingUnit
				PrmDimension3							is like AccountingUnit

			Instance Selection
				where (FinanceEnterpriseGroup                                               = PrmFinanceEnterpriseGroup
				and    (PrmAccount          not entered or PrmAccount                       = AverageDailyBalanceTransaction.GeneralLedgerChartAccount)
				and    (PrmAccountingEntity not entered or PrmAccountingEntity              = AccountingEntity)
				and    (PrmDimension1       not entered or PrmDimension1                    = ADBDimension1)
				and    (PrmDimension2       not entered or PrmDimension2                    = ADBDimension2)
				and    (PrmDimension3       not entered or PrmDimension3                    = ADBDimension3)
				and    (PrmStartDate        not entered or AverageDailyBalanceTransaction.PostingDate >= PrmStartDate)
				and    (PrmEndDate          not entered or AverageDailyBalanceTransaction.PostingDate <= PrmEndDate))

			Action Rules
				Instance Rules
					invoke Purge
