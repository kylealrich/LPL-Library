PaymentProposalRequest is a BusinessClass
    owned by cxml

    prefix is Pprop

    Ontology
        symbolic key is PaymentProposalRequest

    Patterns
        implements CRUD
        disable Auditing
        disable EffectiveDated
        implements Archivable

    Persistent Fields
        DocumentNumber          is AlphaUpper size 22
        PayloadID               is Alpha 100
        ProcessStatus           is AlphaUpper size 10
            States
                New             value is "0"
                Error           value is "1"
                Success         value is "2"
                Warning         value is "3"
        StatusMessage           is Text
        RequestXML              is XMLDocument
        LastUpdatedTimeStamp    is TimeStamp

    Transient Fields

    Derived Fields
        FullHeaderTemplate is a DerivedField
            type is XMLDocument
        	restricted
            FullHeaderTemplate = template.AribaCXMLFullHeader_ST document for this instance

        LightHeaderTemplate is a DerivedField
            type is XMLDocument
        	restricted
            LightHeaderTemplate = template.AribaCXMLLightHeader_ST document for this instance

        PaymentProposalTemplate is a DerivedField
            type is XMLDocument
        	restricted
            PaymentProposalTemplate = template.AribaPaymentProposal_ST document for this instance

        ISODateTimeWithOffSet is a DerivedField       //TimeZone "2007-04-05T12:30-02:00".
            type is Alpha 25
        	restricted
            if (LocalISODateTime = "")
                LocalISODateTime = ISOCurrentTimeStampWithOffset


            if (LocalISODateTime[20] = "Z" )
                LocalISODateTime = LocalISODateTime[1:19] + "+00:00"

                if (LocalISODateTime[12:19] = "00:00:00" )
                    return LocalISODateTime[1:11] + "12:00:00" + LocalISODateTime[20:25]

            return LocalISODateTime[1:22] + ":" + LocalISODateTime[23:24]

        ISOCurrentTimeStampWithOffset is a DerivedField
            type is Alpha 25
            restricted
            if (CurrentTimeStamp = "")
                CurrentTimeStamp = current timestamp
            return (CurrentTimeStamp using"%1$tY-%1$tm-%1$tdT%1$tH:%1$tM:%1$tS%1$tz" )

        ISOInvoiceDate is a DerivedField
            type is Alpha size 10
            restricted
            return PayablesInvoice.InvoiceDate using "%1$tY-%1$tm-%1$td"

        ISOPaymentDate is a DerivedField
            type is Alpha size 10
            restricted
            return PaymentDate using "%1$tY-%1$tm-%1$td"

        PayloadIDString is a StringField
            type is Alpha size 100
        	restricted
            DocumentNumber
            "-"
            ISODateTimeWithOffSet

        PaymentProposalID is a StringField
            type is Alpha size 100
        	restricted
            DocumentNumber
            "-"
            ISODateTimeWithOffSet

        Doctype is a StringField
            type is Alpha size 100
        	restricted
            "<!DOCTYPE cXML SYSTEM \"http://xml.cxml.org/schemas/cXML/"
            CxmlVersion
            "/PaymentRemittance.dtd\">"

        DerivePaymentIdentifier is a StringField
            type is AlphaUpper size 22
        	restricted
            PayablesInvoice.Invoice
            "-"
            DerivedCompany
            "-"
            PayablesInvoice.Vendor

        OrderID is a StringField
            type is Alpha size 10
        	restricted
            DerivedCompany
            "-"
            PayablesInvoice.FirstPurchaseOrder

        DerivedCompany is a StringField
            type is Alpha size 4
            restricted
            PayablesInvoice.Company

        VendorLocationAddressLines is a StringField
            type is Alpha size 160
        	restricted
            VendorLocationRel.VendorAddressLine1
            " "
            VendorLocationRel.VendorAddressLine2
            " "
            VendorLocationRel.VendorAddressLine3
            " "
            VendorLocationRel.VendorAddressLine4

        VendorAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            PayablesInvoice.Vendor.VendorAddressLine1
            " "
            PayablesInvoice.Vendor.VendorAddressLine2
            " "
            PayablesInvoice.Vendor.VendorAddressLine3
            " "
            PayablesInvoice.Vendor.VendorAddressLine4

        PayorAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            PayablesInvoice.ProcessLevel.PostalAddress.DeliveryAddress.AddressLine1
            " "
            PayablesInvoice.ProcessLevel.PostalAddress.DeliveryAddress.AddressLine2
            " "
            PayablesInvoice.ProcessLevel.PostalAddress.DeliveryAddress.AddressLine3
            " "
            PayablesInvoice.ProcessLevel.PostalAddress.DeliveryAddress.AddressLine4

        PayeeAddressLines is a StringField
            type is Alpha size up to 160
        	restricted
            PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine1
            " "
            PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine2
            " "
            PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine3
            " "
            PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.DeliveryAddress.AddressLine4
            
    Local Fields
        UserAgent               is Alpha 10
        FromDomain              is Alpha 20
        FromIdentity            is a NetworkID
        SenderDomain            is Alpha 20
        SenderIdentity          is a NetworkID
        ToDomain                is Alpha 20
        ToIdentity              is a NetworkID
        DeploymentMode          is Alpha size 10
        SharedSecret            is Alpha size 100
        SystemID                is a NetworkID
        CxmlVersion             is Alpha size 11
        WSIEndpoint             is Alpha size 1000

        CurrentTimeStamp            is TimeStamp
        LineCount                   is Numeric size 6
        LocalISODateTime            is Alpha 25

        PayorName                   is a Name	 
        	holds pii
        PayorStreet                 is Alpha size up to 160
        PayorCity                   is Alpha size up to 58
        PayorState                  is AlphaUpper size 3
        PayorPostalCode             is Alpha size up to 12
        PayorCountry                is AlphaUpper size 3
        PayorCountryCode            is AlphaUpper size 2
        PayeeName                   is a Name	 
        	holds pii
        PayeeStreet                 is Alpha size up to 160
        PayeeCity                   is Alpha size up to 58
        PayeeState                  is AlphaUpper size 3
        PayeePostalCode             is Alpha size up to 12
        PayeeCountry                is AlphaUpper size 3
        PayeeCountryCode            is AlphaUpper size 2
        PaymentCurrency             is a  Currency
        PaymentDate                 is Date
        PaymentType                 is Alpha size 10
        PaymentNetAmount            is like CurrencyAmount
        PaymentGrossAmount          is like CurrencyAmount
        PaymentDiscountAmount       is like CurrencyAmount
        InvoiceID                   is like Invoice

        VendorName                  is a Name	 
        	holds pii
        VendorStreet               is Alpha size up to 160
        VendorCity                  is Alpha size up to 58
        VendorState                 is AlphaUpper size 3
        VendorPostal                is Alpha size up to 12
        VendorCountry               is AlphaUpper size 3
        VendorCountryCode           is AlphaUpper size 2
        VendorEmail                 is an EmailAddressMulti 
        	holds pii

        BaseCXML                  is XMLDocument
        PaymentProposalXML        is XMLDocument
        
        LocalActor					is an Actor


    Relations

        VendorLocationRel
            one-to-one relation to VendorLocation
            Field Mapping uses symbolic key
                related.VendorGroup                 = PayablesInvoice.Vendor.VendorGroup
                related.Vendor                      = PayablesInvoice.Vendor
                related.VendorLocation              = PayablesInvoice.PurchaseFromLocation

        DispatchVendorRel
            one-to-one relation to PurchaseOrderDispatchVendor
            Field Mapping uses Set1
                related.Vendor                          = PayablesInvoice.Vendor
                related.PurchaseFromLocation            = blank

        DispatchVendorLocationRel
            one-to-one relation to PurchaseOrderDispatchVendor
            Field Mapping uses Set1
                related.Vendor                          = PayablesInvoice.Vendor
                related.PurchaseFromLocation            = PayablesInvoice.PurchaseFromLocation

        PurchasingVendorRel
            one-to-one relation to PurchasingVendor
            Field Mapping uses symbolic key
                related.VendorGroup                 = PayablesInvoice.VendorGroup
                related.Vendor                      = PayablesInvoice.Vendor

        PurchaseFromLocRel
            one-to-one relation to PurchaseFromLocation
            Field Mapping uses symbolic key
                related.VendorGroup                 = PayablesInvoice.VendorGroup
                related.Vendor                      = PayablesInvoice.Vendor
                related.PurchaseFromLocation        = PayablesInvoice.PurchaseFromLocation

        PaymentMethodRel
            one-to-many relation to PaymentMethodMap
            Field Mapping uses ByBankTransactionCode
                related.BankTransactionCode = PayablesInvoice.BankTransactionCode
                
		CXMLConfigurationRel
			one-to-many relation to CXMLConfiguration
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
			Instance Selection
				where (related.Default)

    Conditions

        IsAribaIntegrated
            restricted
            when (VendorIsAribaEnabled
            or   VendorLocationIsAribaEnabled)

        VendorIsAribaEnabled
            restricted
            when (PayablesInvoice.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel not exists
            and (VendorHasAribaFullAccount
            or VendorHasAribaLightAccount))

        VendorLocationIsAribaEnabled
            restricted
            when (VendorLocationHasAribaFullAccount
            or VendorLocationHasAribaLightAccount)


        VendorHasAribaFullAccount
            restricted
            when ( PayablesInvoice.PurchasingVendorRel.AribaEnabled.Integrated
            or PayablesInvoice.PurchasingVendorRel.AribaEnabled.Flipper)

        VendorHasAribaLightAccount
            restricted
            when ( PayablesInvoice.PurchasingVendorRel.AribaEnabled.Light)

        VendorLocationHasAribaFullAccount
            restricted
            when (PayablesInvoice.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel.AribaEnabled.Integrated
            or PayablesInvoice.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel.AribaEnabled.Flipper)

        VendorLocationHasAribaLightAccount
            restricted
            when ( PayablesInvoice.PurchaseFromLocation.VendorLocation.PurchaseFromLocationRel.AribaEnabled.Light)
            

		NotifyError
		    when ((CXMLConfigurationRel.NotificationLevel.Error
		    or CXMLConfigurationRel.NotificationLevel.Both)
		    and ProcessStatus.Error or ProcessStatus.Warning)
		NotifySuccess
		    when ((CXMLConfigurationRel.NotificationLevel.Success
		    or CXMLConfigurationRel.NotificationLevel.Both)
		    and ProcessStatus.Success)
		EmailNotification
		    when (CXMLConfigurationRel.NotificationType.Email
		    or CXMLConfigurationRel.NotificationType.Both)
		UserNotification
		    when (CXMLConfigurationRel.NotificationType.User
		    or CXMLConfigurationRel.NotificationType.Both)

    Rule Blocks
        ConvertPaymentType
            if (PaymentMethodRel exists)
                PaymentType = PaymentMethodRel.CXMLPaymentType
            else
                PaymentType = "other"

        Declarations
            RequestXML        = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>"
            RequestXML        += Doctype

        SetHeaderValues
            UserAgent = "INFOR"
            FromDomain              = "NetworkID"
            SenderDomain            = "NetworkID"

            if (PayablesInvoice.PurchaseFromLocation entered
            and DispatchVendorLocationRel exists)
                include SetVendorLocationCredentials
            else
                include SetVendorCredentials

            if (VendorHasAribaFullAccount
            or VendorLocationHasAribaFullAccount)
                BaseCXML = FullHeaderTemplate
            else
                BaseCXML = LightHeaderTemplate

        SetVendorLocationCredentials
                FromIdentity            = DispatchVendorLocationRel.FromNetworkID
                SenderIdentity          = DispatchVendorLocationRel.SenderNetworkID
                DeploymentMode          = DispatchVendorLocationRel.DeploymentMode
                SharedSecret            = DispatchVendorLocationRel.SharedSecret
                SystemID                = DispatchVendorLocationRel.SystemID
                CxmlVersion             = DispatchVendorLocationRel.CxmlVersion
                WSIEndpoint             = DispatchVendorLocationRel.PostToServer

                include SetVendorLocationToCredentials

        SetVendorCredentials
                FromIdentity            = DispatchVendorRel.FromNetworkID
                SenderIdentity          = DispatchVendorRel.SenderNetworkID
                DeploymentMode          = DispatchVendorRel.DeploymentMode
                SharedSecret            = DispatchVendorRel.SharedSecret
                SystemID                = DispatchVendorRel.SystemID
                CxmlVersion             = DispatchVendorRel.CxmlVersion
                WSIEndpoint             = DispatchVendorRel.PostToServer

                include SetVendorToCredentials

        SetVendorLocationToCredentials
            if (VendorLocationHasAribaFullAccount)
                ToDomain                = "NetworkID"
                ToIdentity              = DispatchVendorLocationRel.ToNetworkID
            else
                ToIdentity              = PayablesInvoice.Vendor + "-" + VendorLocationRel.VendorLocation
                VendorName              = VendorLocationRel.VendorName
                VendorStreet           = VendorLocationAddressLines
                VendorCity              = VendorLocationRel.VendorCity
                VendorState             = VendorLocationRel.VendorStateProvince
                VendorPostal            = VendorLocationRel.VendorPostalCode
                VendorCountry           = VendorLocationRel.VendorCountry
                VendorCountryCode       = VendorLocationRel.VendorLocation.CurrentAddressRel.PostalAddress.Country.IsoCountryCode
                VendorEmail             = VendorLocationRel.EmailAddress

        SetVendorToCredentials
            if (VendorHasAribaFullAccount)
                ToDomain                = "NetworkID"
                ToIdentity              = DispatchVendorRel.ToNetworkID
            else
                ToIdentity              = PayablesInvoice.Vendor
                VendorName              = PayablesInvoice.Vendor.VendorName
                VendorStreet           = VendorAddressLines
                VendorCity              = PayablesInvoice.Vendor.VendorCity
                VendorState             = PayablesInvoice.Vendor.VendorStateProvince
                VendorPostal            = PayablesInvoice.Vendor.VendorPostalCode
                VendorCountry           = PayablesInvoice.Vendor.VendorCountry
                VendorCountryCode       = PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.Country.IsoCountryCode
                VendorEmail             = PayablesInvoice.Vendor.EmailAddress

        SetPaymentProposalRequest
                PayorName                   = PayablesInvoice.Company.Name
                PayorStreet                = PayorAddressLines
                PayorCity                   = PayablesInvoice.ProcessLevel.PostalAddress.Municipality
                PayorState                  = PayablesInvoice.ProcessLevel.PostalAddress.StateProvince
                PayorPostalCode             = PayablesInvoice.ProcessLevel.PostalAddress.PostalCode
                PayorCountry                = PayablesInvoice.ProcessLevel.PostalAddress.Country
                PayorCountryCode            = PayablesInvoice.ProcessLevel.PostalAddress.Country.IsoCountryCode

                PayeeName                   = PayablesInvoice.Vendor.VendorName
                PayeeStreet                 = PayeeAddressLines
                PayeeCity                   = PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.Municipality
                PayeeState                  = PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.StateProvince
                PayeePostalCode             = PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.PostalCode
                PayeeCountry                = PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.Country
                PayeeCountryCode            = PayablesInvoice.Vendor.CurrentAddressRel.PostalAddress.Country.IsoCountryCode

                PaymentDate                 = PayablesInvoice.DueDate
                PaymentCurrency             = PayablesInvoice.InvoiceCurrency
                PaymentGrossAmount          = PayablesInvoice.InvoiceAmount.CurrencyAmount
                PaymentDiscountAmount       = PayablesInvoice.DiscountAmount.CurrencyAmount
                PaymentNetAmount            = PaymentGrossAmount - PaymentDiscountAmount
                InvoiceID                   = PayablesInvoice.Invoice
                DocumentNumber              = DerivePaymentIdentifier
                PayloadID                   = PayloadIDString

		SendNotification
		    if (EmailNotification)
		        send email
		            to CXMLConfigurationRel.ToNotificationEmail
		            from CXMLConfigurationRel.FromNotificationEmail
		            subject "CXML_Payment_Proposal_<ProcessStatus>_-_Invoice:<InvoiceID>_|_Vendor:<PayablesInvoice.Vendor.VendorName>"
		            Contents
		                "Invoice:<InvoiceID>"
		                "InvoiceDate:<ISOInvoiceDate>"
		                "PaymentDate:<ISOPaymentDate>"
		                "Vendor:<PayablesInvoice.Vendor.VendorName>"
		                "Status:<ProcessStatus>_-_<StatusMessage>"
		    if (UserNotification and ProcessStatus.Success)
		        LocalActor = actor
		        send notification
		            to LocalActor
		            description is "CXML_Payment_Proposal_<ProcessStatus>_-_Invoice:<InvoiceID>_|_Vendor<PayablesInvoice.Vendor.VendorName>"
		            priority is low
		            detail is "Invoice:<InvoiceID>_|_InvoiceDate:<ISOInvoiceDate>_|_PaymentDate:<ISOPaymentDate>_|_Vendor:<PayablesInvoice.Vendor.VendorName>_|_Status:<ProcessStatus>_-_<StatusMessage>"
		    if (UserNotification and (ProcessStatus.Error or ProcessStatus.Warning))
		        LocalActor = actor
		        send notification
		            to LocalActor
		            description is "CXML_Payment_Proposal_<ProcessStatus>_-_Invoice:<InvoiceID>_|_Vendor<PayablesInvoice.Vendor.VendorName>"
		            priority is high
		            detail is "Invoice:<InvoiceID>_|_InvoiceDate:<ISOInvoiceDate>_|_PaymentDate:<ISOPaymentDate>_|_Vendor:<PayablesInvoice.Vendor.VendorName>_|_Status:<ProcessStatus>_-_<StatusMessage>"		


    Field Rules

    Actions
        Update is an Update Action
            Exit Rules
                LastUpdatedTimeStamp = current timestamp

        Delete is a Delete Action

        CreatePaymentProposalRequest is a Create Action

            Action Rules

                CurrentTimeStamp = current timestamp
                LastUpdatedTimeStamp = CurrentTimeStamp

                    if (VendorLocationIsAribaEnabled
                    or (PayablesInvoice.PurchaseFromLocation not entered
                    and VendorIsAribaEnabled))

                        include SetPaymentProposalRequest
                        include ConvertPaymentType
                        include SetHeaderValues
                        include Declarations

                        PaymentProposalXML        = PaymentProposalTemplate
                        BaseCXML select "/cXML"  += PaymentProposalXML
                        RequestXML               += BaseCXML

                        if (!DispatchVendorLocationRel exists
                        and !DispatchVendorRel exists )
                            ProcessStatus = 1
                            StatusMessage = "Not Transmitted PaymentProposalRequest Created Dispatch Vendor Not Found"
                            if (NotifyError)
                            	include SendNotification

                        else
                            ProcessStatus     = 2
                            StatusMessage     = "PaymentProposalRequest Created"

                        if (!ProcessStatus.Error)
                            invoke OutboundCreate CXMLRouter
                                invoked.Data               = RequestXML
                                invoked.Status             = "Payment Proposal Request: " + InvoiceID
                                invoked.RunGroup           = ToIdentity
                                invoked.WSIEndpoint        = WSIEndpoint
                                invoked.SharedSecret       = SharedSecret

                                ProcessStatus              = ProcessStatus.Success
                                StatusMessage              = "Payment Proposal Request Created"
                                if (NotifySuccess)
                                	include SendNotification




