TaxCode is a BusinessClass
    owned by tx
    prefix is TCM
    classic name is TXCODEMAST

    Ontology
        symbolic key is TaxCode
            classic set name is TCMSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Attach Rules
    	if (not BypassRuleForTheseFiles)
			constraint (Status.Active)
    			"TaxCodeIsNotActive"
    
    Persistent Fields

        Description
        TaxType
        Status      is Numeric size 1
            States
                Active   value is 0
                Inactive value is 1

	Local Fields
        LocalTaxEntity     		is a TaxEntity
		LocalTaxTableTaxCode 	is a TaxCode
		LocalTaxPointIsInvoiced	is Alpha 1
		LocalTaxPointGLDate		is Alpha 1
		LocalCompany			is a GeneralLedgerCompany 
		ExistOnThisTable	    is Boolean						
		ThisTable			    is like TaxCode					
		ThisTableStatus         is Numeric size 1				
            States
                Active   value is 0
                Inactive value is 1
		Trigger                 is Alpha 1						
		ListOfTables   		    is Text         				
		AllTableDetail			is RichText						
		LocalTableStatus		is Alpha 10						
		LocalTableCount			is Numeric 3					
		ThisTableEffectiveDate  is Date							
		ThisTaxTableDesc        is a Description				
		LocalSortOption 		is Alpha 1						
			States 
				ByStatus value is blank
				ByEffectiveDate is "E"

	Transient Fields

	Context Fields
		Company					
		AccountingEntity		 
		ToAccountingEntity		 
		TaxEntity
		ProductTaxCategory		
		EffectiveDate

	Derived Fields
		TaxCodeOK is a DerivedField	
			type is Boolean
			restricted
			if (TaxEntityRel.UseTaxCodeAccounts)
				return true
			
			if (TaxCodeAccountsRel exists)
				return true
				
			return false		

		TaxCodeNotSetUpMessage is a MessageField
			restricted
			"TaxCode<TaxCode>NotSetUpInEntityTaxCodeFile"	

		DerivedTaxCodeNameID is a MessageField
			default label is "TaxCode"
			"<TaxCode>_-_<TaxCode.Description>"
						
		DerivedTaxPoint is a DerivedField								
			type is Alpha 1
			restricted
			if (!TaxEntityRel.UseTaxCodeAccounts)
				return TaxEntityRel.TaxPoint
			else
				if (!TaxCodeAccountsRel.TaxCode.TaxType.TaxTableCode)
					return TaxCodeAccountsRel.TaxPoint	
				else
					initialize LocalTaxTableTaxCode				
					if (TaxTableRel.TaxCode1 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode1								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
								
					if (TaxTableRel.TaxCode2 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode2								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
														
					if (TaxTableRel.TaxCode3 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode3								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
														
					if (TaxTableRel.TaxCode4 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode4								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
														 												 					
					if (TaxTableRel.TaxCode5 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode5								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"							
							
					if (TaxTableRel.TaxCode6 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode6								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
														
					if (TaxTableRel.TaxCode7 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode7								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
														
					if (TaxTableRel.TaxCode8 entered)
						LocalTaxTableTaxCode = TaxTableRel.TaxCode8								
					    if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.PaymentDate)
							return "P"
						else
						if (LocalTaxTableTaxCode.TaxCodeAccountsRel.TaxPoint.InvoiceDate)
							LocalTaxPointIsInvoiced = "I"
						else
							LocalTaxPointGLDate		= "G"
							
					if (LocalTaxPointIsInvoiced entered)
						return "I"
					else
						return "G"														 												 			

		DerivedTaxMethodInvOrAcc is a DerivedField		
			type is like TaxMethod
			restricted


			if (TaxEntityRel.ThirdParty.InforTax)
				if (!TaxCode.TaxType.TaxTableCode)

					if ((TaxEntityRel.UseTaxCodeAccounts
					and  TaxCodeAccountsRel.AccruedOrInvoiced.Accrued)
					or (!TaxEntityRel.UseTaxCodeAccounts
					and  TaxEntityRel.AccruedOrInvoiced.Accrued))

						return "A"
					else

						return "I"
				else

					return blank
			else

				return "Z"

		DerivedAccountingEntity  is a DerivedField		
			type is like AccountingEntity				 
			restricted
			if (TaxEntity entered)
				return TaxEntity
			else	
			if (ToAccountingEntity entered)
				return ToAccountingEntity	 
			else	
			if (AccountingEntity entered)
				return AccountingEntity
			else	
			if (Company entered)
				LocalCompany 	= Company
				return LocalCompany.AccountingEntity	 
		
			return blank		

		DerivedUseTaxCodeAccounts  is a DerivedField		
			type is Boolean				 
			restricted
			if (TaxEntity entered)
				LocalTaxEntity = TaxEntity
			else	
			if (ToAccountingEntity entered)
				LocalTaxEntity = ToAccountingEntity	 
			else	
			if (AccountingEntity entered)
				LocalTaxEntity = AccountingEntity
			else	
			if (Company entered)
				LocalCompany 	= Company
				LocalTaxEntity = LocalCompany.AccountingEntity	 
			
			return LocalTaxEntity.UseTaxCodeAccounts		
			
		DerivedTaxCodeTitle is a DerivedField	
			type is MessageField

			if (UseTaxCodeAccounts)
				return EntityTaxCodeTitle
			
			return TaxCodeTitle		

		TaxCodeTitle is a MessageField
			restricted
			"TaxCodes"
		EntityTaxCodeTitle is a MessageField
			restricted
			"EntityTaxCodes"

		CheckTaxTableLabel is a MessageField    	
			"NoTaxTablesFoundWithThisTaxCode"		
		TaxTableStatusLabel is a LabelField			
			"<ThisTableStatus>"
		TotalTablesFoundLabel  is a MessageField    
			"TotalTablesFound:_"


		FormatedEffectiveDate10 is a StringField	  
			type is Alpha 10
			restricted
			default label is untranslatable			
			ConvertDateAlpha8[1:4]
			"-"
			ConvertDateAlpha8[5:6]
			"-"
			ConvertDateAlpha8[7:8]		

		ConvertDateAlpha8 is a DerivedField
			type is Alpha 8
			restricted
			default label is untranslatable			
			return ThisTableEffectiveDate		

		DerivedTimestamp is a DerivedField	       
			type is TimeStamp 
			restricted
			default label is untranslatable
			return current timestamp

		SortedByLabel is a DerivedField			   
			type is MessageField 
			restricted
			default label is untranslatable
			if (LocalSortOption.ByEffectiveDate)
				return SortedByEffectiveDateLabel
			return SortedByStatusLabel	

		SortedByStatusLabel is a MessageField	   
			restricted
			"SortedByStatus"
		SortedByEffectiveDateLabel is a MessageField	   
			restricted
			"SortedByEffectiveDate"

		ReportHeaderLabel is a MessageField		   
			restricted
			"TaxTableReport"
		ReportDateHeaderLabel is a MessageField	   
			restricted
			"Date:_"

		UniqueIDLabel is a MessageField		       
			restricted
			"UniqueID:"	

		TaxTableReportString is a StringField	   
			type is Text
			restricted
			default label is untranslatable
			TableStyle
			TaxTableReportTitle
			TaxTableHeader
			TaxTableColumnHeader	
			AllTableDetail
			TaxTableEnd			

		TableStyle is a StringField	  
			type is RichText
			restricted
			default label is untranslatable			
			"	<style>                                    "
			"	table {                                    "
			"		font-family: arial, sans-serif;        "
			"		border-collapse: collapse;             "
			"		width: 75%;                            "
			"	}                                          "
            "                                              "
			"	td, th {                                   "
			"		border: 1px solid #dddddd;             "
			"		text-align: left;                      "
			"		padding: 5px;                          "
			"	}                                          "
            "                                              "
			"	tr:nth-child(even) {                       "
			"		background-color: #dddddd;             "
			"	}                                          "
			"	</style>                                   "

		TaxTableReportTitle is a StringField	 
			type is RichText
			restricted
			default label is untranslatable					
			" <h1> "
			"  <tr>                                        "
			"	<th colspan=5>Tax Table Report</th>"	 
			"  </tr>                                       "
			" </h1> "
			" <h4> "
			"  <tr>                                        "
			"	<th colspan=5>" + ReportDateHeaderLabel + DerivedTimestamp + "</th>"	 
			"  </tr>                                       "
			" </h4> "			
			" <h4> "
			"  <tr>                                        "
			"	<th colspan=5>"  + SortedByLabel +   "</th>"	 
			"  </tr>                                       "
			" </h4> "				

		TaxTableHeader is a StringField	 
			type is RichText  
			restricted 
			default label is untranslatable
			"==============================================================================================================================<br>"	
			" <h6> "
			"  <tr>                                        "
			"	<th colspan=5>" + TotalTablesFoundLabel + LocalTableCount + "</th>"	 
			"  </tr>                                       "
			" </h6> "			
			"<pre>"	
			"<table>"       

		TaxTableColumnHeader is a StringField	
			type is RichText
			restricted 
			default label is untranslatable			
			"  <tr>                            "	
			"	<th>#</th>                     "						
			"	<th>TaxTable</th>              "	
			"	<th>Status</th>                "	
			"	<th>Description</th>           "	
			"	<th>EffectiveDate</th>         "						 							
			"  </tr>    					   "						
		TaxTableDetail is a StringField	      
			type is RichText  
			restricted   
			default label is untranslatable			               
			"  <tr>                                   "
			"	<td>" LocalTableCount	  		"</td>"				
			"	<td>" ThisTable 		  		"</td>"		 	
			"	<td>" TaxTableStatusLabel  		"</td>"			
			"	<td>" ThisTaxTableDesc 			"</td>"			
			"	<td>" FormatedEffectiveDate10 	"</td>"					
			"  </tr>    							  "	

		TaxTableEnd is a StringField	      
			type is RichText
			restricted
			default label is untranslatable			
			"</table>"

    Conditions

        IsNonMember
        	restricted
            when (not TaxType.TableMember)

        EntityTaxCodeExists
        	restricted
        	when (TaxCodeAccountsRel exists)
        	
        IsValidTaxCode
        	restricted
        	when (not TaxEntityRel.UseTaxCodeAccounts
        	or    EntityTaxCodeExists)

        IsTableSelectable
        	restricted
        	when (TaxType.TableMember
        	or    TaxType.StandAlone)
        
        IsTaxTable
        	restricted
            when (TaxType.TaxTableCode)

       	TaxCodeExistAndNotATable
       		restricted
            when (TaxCodeRel exist
            and  !TaxCodeRel.TaxType.TaxTableCode
            and  !TaxCode.TaxType.CalculatedRate)

       	TaxCodeExistAndIsATable
       		restricted
            when (TaxCodeRel exist
            and  IsTaxTable)
 
        UseTaxCodeAccounts
        	restricted
			when (DerivedUseTaxCodeAccounts)

       	NotUsingTaxCodeAccounts
        	restricted
			when (!DerivedUseTaxCodeAccounts)
        	
       	LooksAtTaxCodeAccountsFlag	 
        	restricted
        	when ((UseTaxCodeAccounts	
			and   TaxCodeAccountsRel exist)
        	or    NotUsingTaxCodeAccounts	 								 
        	and   AllTaxCodeRel exist)

       	UsesCompanyOrEntityNotUTCAFlag	 
        	restricted
        	when ((DerivedAccountingEntity entered
        	and   TaxCodeAccountsRel2 exists)		 
        	or    DerivedAccountingEntity not entered
        	and   TaxCodeRel exist)

		BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "PurchaseOrderEDIOutput")

 		TaxLogHeaderRelExist			
			restricted	
			when (TaxLogHeaderRel exist)	       	        	                       
    Relations
		TaxTableRel
			one-to-many relation to TaxTable
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup    
				related.TaxCode					= TaxCode

        TaxCodeAccountsRel
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup	
                related.TaxEntity 					= DerivedAccountingEntity	
                related.TaxCode 					= TaxCode

        TaxCodeAccountsRel1
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup  
                related.TaxEntity 					= DerivedAccountingEntity	
                related.TaxCode 					= TaxCode
                
        TaxCodeAccountsRel2
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup   
                related.TaxEntity 					= DerivedAccountingEntity	
                related.TaxCode 					= TaxCode

        TaxRateRel
            one-to-many relation to TaxRate
            Field Mapping uses symbolic key
            Instance Selection
                where (related.TaxRate.TaxCode 				= TaxCode
                and		related.TaxRate.ProductTaxCategory 	= ProductTaxCategory		
				and		related.EffectiveDate				>= EffectiveDate)

		TaxEntityRel
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TaxEntity				= DerivedAccountingEntity	 

		TaxEntityTaxCodesRel
			one-to-many relation to EntityTaxCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup  
			Instance Selection
                where (related.TaxEntity				= TaxEntity)
																
        TxtaxcodeRel
            one-to-many relation to EntityTaxCode	 
            Field Mapping uses Set2
                related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup 
                related.TaxCode = TaxCode

        TxtaxrateRel
            one-to-many relation to TaxRate
            Field Mapping uses Set4		
            Instance Selection
                where (related.TaxRate.TaxCode = TaxCode)

		TaxCodeRel
			one-to-one relation to TaxCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup  
				related.TaxCode					= TaxCode

		AllTaxCodeRel
			one-to-many relation to TaxCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup  

		CheckTaxTablesRel									
			one-to-many relation to TaxTable
			Field Mapping uses ByStatus	 
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup

		CheckTaxTablesByEffectoveDateRel					
			one-to-many relation to TaxTable
			Field Mapping uses ByEffectiveDate	
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup

		TaxLogHeaderRel	
			one-to-many relation to TaxLogHeader
			Field Mapping uses ByRelatedObjectUniqueID	 
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.UniqueIdOfRelatedObject = UniqueID				
			Instance Selection	
				where (related.RelatedObjectReference 	= reference to this instance)	

	Rule Blocks
		CheckIfTaxCodeExistOnATable  				
			ExistOnThisTable = false
			for each CheckTaxTablesRel
				if (each.TaxCode1			= TaxCode
				or  each.TaxCode2			= TaxCode
				or  each.TaxCode3			= TaxCode
				or  each.TaxCode4			= TaxCode
				or  each.TaxCode5			= TaxCode
				or  each.TaxCode6			= TaxCode
				or  each.TaxCode7			= TaxCode
				or  each.TaxCode8			= TaxCode)		
					ThisTable = each.TaxTable.TaxCode	
					ExistOnThisTable = true
					end for each	 		

		CreateTaxTableReport						
			if (LocalSortOption.ByEffectiveDate)	
				if (CheckTaxTablesByEffectoveDateRel exist)
					for each CheckTaxTablesByEffectoveDateRel
						if (each.TaxCode1			= TaxCode
						or  each.TaxCode2			= TaxCode
						or  each.TaxCode3			= TaxCode
						or  each.TaxCode4			= TaxCode
						or  each.TaxCode5			= TaxCode
						or  each.TaxCode6			= TaxCode
						or  each.TaxCode7			= TaxCode
						or  each.TaxCode8			= TaxCode)		
							ThisTable = each.TaxTable.TaxCode	
							LocalTableCount +=1	
							ThisTableStatus = each.TaxTable.TaxCode.Status  	
							ThisTableEffectiveDate = each.TaxTable.EffectiveDate	
							ThisTaxTableDesc = each.TaxTable.Description							
							AllTableDetail = AllTableDetail + TaxTableDetail							
			else	
			if (CheckTaxTablesRel exist)
				for each CheckTaxTablesRel
					if (each.TaxCode1			= TaxCode
					or  each.TaxCode2			= TaxCode
					or  each.TaxCode3			= TaxCode
					or  each.TaxCode4			= TaxCode
					or  each.TaxCode5			= TaxCode
					or  each.TaxCode6			= TaxCode
					or  each.TaxCode7			= TaxCode
					or  each.TaxCode8			= TaxCode)		
						ThisTable = each.TaxTable.TaxCode	
						LocalTableCount +=1	
						ThisTableStatus = each.TaxTable.TaxCode.Status  	
						ThisTableEffectiveDate = each.TaxTable.EffectiveDate	
						ThisTaxTableDesc = each.TaxTable.Description							
						AllTableDetail = AllTableDetail + TaxTableDetail					
			
			if (AllTableDetail not entered)
				AllTableDetail = CheckTaxTableLabel

			if (TaxLogHeaderRel not exist)			
				invoke Create TaxLogHeader            
					invoked.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
					invoked.UniqueIdOfRelatedObject     = UniqueID  					
					invoked.TaxLogHeader				= UniqueID	 
					invoked.SystemCode					= "TX"   
					invoked.LogCreatedTimestamp 	 	= current timestamp 	  
					invoked.LogCallingRoutine		 	= "TaxCode" 		  
					invoked.RelatedObjectReference		= reference to this instance		 	 
					invoked.MessageRichText				= TaxTableReportString	 
					
			if (TaxLogHeaderRel exist)	
				invoke Update TaxLogHeaderRel 
					invoked.MessageRichText = TaxTableReportString  

    Field Rules
  			
  		TaxType
  			default to "S"
  			required

			if (TaxType changed
			and old TaxType = "T")
				constraint (!TaxTableRel exists)						 
					"CannotChangeTheType;TaxTableExists" 		

			if (TaxType changed
			and old TaxType = "T")
				constraint (!TxtaxcodeRel exists)		 
					"CannotChangeTheType;CodeSetupForTaxEntity<LocalTaxEntity>"  	

			if (TaxType changed
			and TaxType = "C")
				constraint (!TxtaxrateRel exists)			
					"CannotChangeToType_\C;TaxRateExists" 		
						  			
        Description
            required



		Status													
			if (Status changed
			and Status.Inactive)
				if (CheckTaxTablesRel exists)
					include CheckIfTaxCodeExistOnATable     
					constraint (!ExistOnThisTable)			
						"CannotChangeToInactive;_\ThisTaxCodeExistOnTable<ThisTable>;_\ItMustFirstBeRemovedFromAllTaxTables;_\ThenTryAgain.ToSeeAllTables,RunAction_\Lookup_\Tax_\Tables" 

    Actions
        Create is a Create Action

   		Update is an Update Action
			Action Rules
			    if (TxtaxcodeRel exists)
                    LocalTaxEntity = first TxtaxcodeRel.TaxEntity               		
						
        Delete is a Delete Action
                            
		UpdateAndBypassEdits is an Update Action
			restricted
			default label is untranslatable
			bypass field rules	 

		LookUpTaxTables is an Instance Action


			Parameters
				ReportOptions is Alpha 1
					States
						CheckTaxTables	     value is blank
						DeleteTaxTableReport value is "1"
				SortOption is Alpha 1
					States 
						ByStatus value is blank
						ByEffectiveDate is "E"

			Action Rules
				LocalSortOption = SortOption
				if (ReportOptions.DeleteTaxTableReport)
					invoke Delete TaxLogHeaderRel 
				else
					include CreateTaxTableReport			
