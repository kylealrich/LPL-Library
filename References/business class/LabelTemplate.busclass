LabelTemplate is a BusinessClass
	owned by mscm
	
	prefix is MSLAT
	
	Ontology
		symbolic key is LabelTemplate
			
	Patterns		
	
	Persistent Fields
		Description
		LabelType			      	is Numeric 1
			States
				InventoryLocation 	value is 1
				ParLocation       	value is 2
				StockItem		  	value is 3
				ParItem			  	value is 4
				ParBin				value is 5
				StockBin			value is 6
				KanbanItemBin     	value is 7
				Employee          	value is 8
		Default			          	is Boolean
		PreDefined					is Boolean
			default label is "Pre-Defined"
		UserTemplate
		
	Transient Fields
		OverrideDefault is Boolean 
			default label is "OverrideDefaultIfExisting"
		
		TransientTemplate is XMLDocument
			default label is "Template"
			derive value from DerivedTemplate
			
		TransientItemLocationBinaryDocument is BinaryDocument
			document template for ItemLocation
			derive value from UserTemplate.Template
 
		TransientInventoryLocationBinaryDocument is BinaryDocument
			document template for MobileSupplyChainLocation
			derive value from UserTemplate.Template
		
		TransientBinBinaryDocument is BinaryDocument
			document template for Bin
			derive value from UserTemplate.Template

		TransientKanbanItemBinBinaryDocument is BinaryDocument
			document template for KanbanItemBin
			derive value from UserTemplate.Template

	Derived Fields			
		DerivedTemplate is a DerivedField
			type is XMLDocument
			restricted
			return UserTemplate.Template		
			
		DerivedTemplateFileName is a StringField		
			type is Alpha 200
			restricted
			"MobileSupplyChainLabelTemplate_" LabelTemplate ".xml"
		
		DerivedBusinessClass is a DerivedField
			type is Alpha 30
			restricted
			if (LabelType.StockItem or LabelType.ParItem or LabelType.ParBin)
				return "ItemLocation"
			else
			if (LabelType.InventoryLocation or LabelType.ParLocation)
				return "MobileSupplyChainLocation"
			else
			if (LabelType.StockBin)
				return "Bin"
			else
			if (LabelType.KanbanItemBin)
				return "KanbanItemBin"
			else
			if (LabelType.Employee)
				return "Employee"
												
	Field Rules		
		LabelType
			required
		
		Default
			if (OverrideDefault)
				if (DefaultLabelTemplateRel exists)
					invoke SetDefaultToFalse DefaultLabelTemplateRel
			else
				if (Default)
					constraint (DefaultLabelTemplateRel not exists)
						"ThereIsAlreadyADefaultLabelTemplateFor_<LabelType>"
			if (Default changed)
				constraint (Default)
					"ThereIsNoDefaultForThisLabelType.CannotSetTheDefaultFlagToNo"
											
		TransientTemplate
			if(not PreDefined)
				required
		
	Conditions		
		RecordExists
			restricted
			when (LabelTemplate exists)
			
	Relations
		DefaultLabelTemplateRel
			one-to-many relation to LabelTemplate
			Field Mapping uses symbolic key
				related.MobileSupplyChainConfiguration	= MobileSupplyChainConfiguration
			Instance Selection
				where (related.Default
				and    related.LabelType  	  = LabelType
				and    related.LabelTemplate != LabelTemplate)
		
	Actions
		Create is a Create Action
			Action Rules
				invoke Create UserTemplate
					assign result to UserTemplate
					invoked.UserTemplate 	 = LabelTemplate + "_ST"
					invoked.Template 		 = TransientTemplate
					invoked.TemplateFileName = DerivedTemplateFileName
					invoked.Description      = Description
					invoked.BusinessClass    = DerivedBusinessClass
					invoked.MimeType         = "application/xml"
		
		SystemCreate is a Create Action
			restricted
					
		Update is an Update Action
			Action Rules
				invoke Update UserTemplate
					invoked.Template         = TransientTemplate
					invoked.Description		 = Description
		
		Delete is a Delete Action
			valid when (not PreDefined)			
			Entrance Rules
				constraint (not Default)
					"CannotDeleteADefaultTemplate"
			Exit Rules
				invoke Delete UserTemplate
				
		SetDefaultToFalse is an Instance Action
			restricted
			Action Rules
				Default = false	
