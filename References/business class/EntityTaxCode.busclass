EntityTaxCode is a BusinessClass
    owned by tx
    prefix is TXEcd


    Ontology
        symbolic key is EntityTaxCode


    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        TaxID
        AccruedOrInvoiced         is AlphaUpper size 1
            classic name is ACCRUAL-FL
            States
                Invoiced value is "I"
                Accrued  value is "A"
                	default label is "Accrued"
        AllowAccountChanges       is Boolean
            classic name is ALLOW-CHANGE
        TaxTerms
        TaxPoint                  is AlphaUpper size 1
            States
                InvoiceDate  value is "I"
                ShippingDate value is "S"
                PaymentDate  value is "P"
                GlDate       value is "G"
                    default label is "GlobalLedgerDate"
		VendorGroup
        AccrualCode               is a PayablesAccrualCode
            classic name is ACCR-CODE
        LastTransactionID         is a TransactionSequenceNumber
            classic name is LAST-TRANS-ID
			disable Auditing											
        LandedCost                is Boolean
            classic name is LAND-COST-FLG
        TaxToleranceLimit         is an InternationalAmount
            classic name is TOLERANCE-LMT
        TaxToleranceAmount        is an InternationalAmount
            classic name is TOLERANCE-AMT
        FromRate
        ThruRate                  is Percent size 7.4
        AdjustDiscount            is AlphaUpper size 1
            classic name is ADJUST-DISC
        GoodsAndServicesIndicator is AlphaUpper size 1
            classic name is GDS-SVCS-IND
            States
                Services value is "S"
                Goods    value is "G"
        ValueAddedTaxType         is AlphaUpper size 1
            classic name is VAT-TYPE
            States
                Domestic value is "D"
                IntraEc  value is "E"
                    default label is "Intra-EC"
                Import   value is "I"
                Export   value is "X"
        TriangulationIndicator    is AlphaUpper size 1
            classic name is TRNGL-IND
            States
                Yes value is "Y"
                No  value is "N"
        Status                    is Numeric size 1
            States
                Active   value is 0
                Inactive value is 1
        TaxInputAccount           is a FinanceCodeBlockFull  
            classic name for TaxInputAccount.AccountingUnit is INP-ACCT-UNIT
            classic name for TaxInputAccount.GeneralLedgerChartAccount is INP-ACCOUNT
        TaxInputDueAccount        is a FinanceCodeBlockFull  
            classic name for TaxInputDueAccount.AccountingUnit is INPDU-ACT-UNIT
            classic name for TaxInputDueAccount.GeneralLedgerChartAccount is INPDU-ACCOUNT
        TaxOutputAccount          is a FinanceCodeBlockFull  
            classic name for TaxOutputAccount.AccountingUnit is OUT-ACCT-UNIT
            classic name for TaxOutputAccount.GeneralLedgerChartAccount is OUT-ACCOUNT
        TaxOutputDueAccount       is a FinanceCodeBlockFull  
            classic name for TaxOutputDueAccount.AccountingUnit is OUTDU-ACT-UNIT
            classic name for TaxOutputDueAccount.GeneralLedgerChartAccount is OUTDU-ACCOUNT
		PostOption
		VATReverseIndicator 		is Boolean						
		TaxDistributionCode
			context of TaxEntity
			
	Context Fields

		PayablesInvoice
		
    Derived Fields
    	ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforCodeDefinition"

		ContextMessageText is a MessageField
			restricted
			"TaxEntity<TaxEntity>TaxCode<TaxCode>"

        TaxName is a StringField
            type is Alpha size 30
            TaxCode.Description



		TaxDetailHeaderLabel				is a LabelField
			restricted
    		"Accounts_\Tolerance_\V\A\T"

		EntityTaxCodeHeaderLabel			is a LabelField
			restricted
    		"Tax\Entity/_Tax\Code"
		EntityTaxCodeLabel					is a LabelField
    		"<TaxEntity>,___<TaxCode>"
    	TaxCodeLabel						is a LabelField
    		restricted
    		"TaxCode:<TaxCode>,_<TaxCode.TaxType>"
   		TaxTypeLabel						is a LabelField
   			restricted
    		"TaxType:<TaxCode.TaxType>"    		
    	DescriptonLabel						is a LabelField
    		"TaxCodeDescription:<TaxCode.Description>"
    	TaxCodeStatusLabel					is a LabelField
    		"Status:<TaxCode.Status>"

		AccountsHeaderLabel					is a LabelField
			restricted
    		"Input_\Output_\Accounts"
    	InputAccountLabel					is a LabelField
    		"InputAccount:<TaxInputAccount>"
    	InputDueAccountLabel				is a LabelField
    		"InputDueAccount:<TaxInputDueAccount>"
    	OutputAccountLabel					is a LabelField
    		"OutputAccount:<TaxOutputAccount>"
    	OutputDueAccountLabel				is a LabelField
    		"OutputDueAccount:<TaxOutputDueAccount>"

    	TaxIDLabel							is a LabelField
    		"TaxID:<TaxID>"
    	AccruedOrInvoicedLabel				is a LabelField
    		"AccruedOrInvoiced:<AccruedOrInvoiced>"
    	AllowAccountChangesLabel			is a LabelField
    		"AllowAccountChanges:<AllowAccountChanges>"
    	TaxTermsLabel						is a LabelField
    		"TaxTerms:<TaxTerms>"
    	TaxPointLabel						is a LabelField
    		"TaxPoint:<TaxPoint>"
    	VendorGroupLabel					is a LabelField
    		"VendorGroup:<VendorGroup>"
    	AccrualCodeLabel					is a LabelField
    		"AccrualCode:<AccrualCode>"
    	LandedCostLabel						is a LabelField
    		"LandedCost:<LandedCost>"
    	LastTransactionIDLabel				is a LabelField
    		"LastTransactionID:<LastTransactionID>"   

 		TolernacesHeaderLabel				is a LabelField
 			restricted
    		"Tolerances"
    	TaxToleranceLimitLabel				is a LabelField
    		"TaxToleranceLimit:<TaxToleranceLimit>"
    	TaxToleranceAmountLabel				is a LabelField
    		"TaxToleranceAmount:<TaxToleranceAmount>"
    	FromRateLabel						is a LabelField
    		"FromRate:<FromRate>"
    	ThruRateLabel						is a LabelField
    		"ThruRate:<ThruRate>"    		    		 		

		VATHeaderLabel						is a LabelField
			restricted
    		"V\A\T"
    	GoodsAndServicesIndicatorLabel		is a LabelField
    		"GoodsAndServicesIndicator:<GoodsAndServicesIndicator>"
    	ValueAddedTaxTypeLabel				is a LabelField
    		"V\A\TType:<ValueAddedTaxType>"
    	TriangulationIndicatorLabel			is a LabelField
    		"TriangulationIndicator:<TriangulationIndicator>"



    	EffectDateLabel						is a LabelField
    		"EffectDate:<first TaxTableRel.EffectiveDate>"
    	TaxCode1Label						is a LabelField
    		"1:<first TaxTableRel.TaxCode1>,_<first TaxTableRel.EffectOnTaxable1>,_<first TaxTableRel.TaxCode1Rate>"
    	TaxCode2Label						is a LabelField	
    		"2:<first TaxTableRel.TaxCode2>,_<first TaxTableRel.EffectOnTaxable2>,_<first TaxTableRel.TaxCode2Rate>"
    	TaxCode3Label						is a LabelField
    		"3:<first TaxTableRel.TaxCode3>,_<first TaxTableRel.EffectOnTaxable3>,_<first TaxTableRel.TaxCode3Rate>"
    	TaxCode4Label						is a LabelField
    		"4:<first TaxTableRel.TaxCode4>,_<first TaxTableRel.EffectOnTaxable4>,_<first TaxTableRel.TaxCode4Rate>" 
   		TaxCode5Label						is a LabelField
    		"5:<first TaxTableRel.TaxCode5>,_<first TaxTableRel.EffectOnTaxable5>,_<first TaxTableRel.TaxCode5Rate>"
    	TaxCode6Label						is a LabelField
    		"6:<first TaxTableRel.TaxCode6>,_<first TaxTableRel.EffectOnTaxable6>,_<first TaxTableRel.TaxCode6Rate>"
    	TaxCode7Label						is a LabelField
    		"7:<first TaxTableRel.TaxCode7>,_<first TaxTableRel.EffectOnTaxable7>,_<first TaxTableRel.TaxCode7Rate>"
    	TaxCode8Label						is a LabelField
    		"8:<first TaxTableRel.TaxCode8>,_<first TaxTableRel.EffectOnTaxable8>,_<first TaxTableRel.TaxCode8Rate>"      		   		    		    		
    	TaxTableTotalRate					is a LabelField
    		restricted
    		default label is untranslatable	
    		"TableTotalRate"	
				    			   	
    	DerivedIDMInvoiceTaxTotal is a DerivedField
    		type is like UnitCost
    			precision is 2
			if (HasPayablesInvoiceDetailsInIDM)
				return sum PayablesInvoiceDetailRel.TaxAmount
	    	else
				return sum VendorReturnLineRel.DerivedLineTaxTotalForIDM

		GetCountOfTaxTransations	is a DerivedField			
			type is Numeric 10

			return (instance count of TaxTransactionsRel)
						    			    		
	Local Fields
		LocalTaxCode	is a TaxCode
		LocalTaxEntity	is a TaxEntity

    Conditions

        IsActive
        	restricted
            when (Status.Inactive)

        IsInactive
        	restricted
            when (Status.Active)

        IsInputAccount
        	restricted
            when (TaxInputAccount entered)

        IsInputDueAcct
        	restricted
            when (TaxInputDueAccount entered)

        IsNonMember
        	restricted
            when (not TaxCode.TaxType.TableMember)

        IsNonSPoint
        	restricted
            when (not TaxPoint.ShippingDate)

        IsOutptDueAcct
        	restricted
            when (TaxOutputDueAccount entered)

        IsOutputAccount
        	restricted
            when (TaxOutputAccount entered)

        IsTaxRate
        	restricted
            when (TaxCode.TaxType.StandAlone
            or    TaxCode.TaxType.TableMember)

        IsTaxTable
        	restricted
            when (TaxCode.TaxType.TaxTableCode)

       	IsInforTaxOnly
       		restricted
            when (TaxEntity.ThirdParty.InforTax)

        IsCalculatedRate
        	restricted
            when (TaxCode.TaxType.CalculatedRate)

		TaxDistributionCodeNotValid
			restricted
			when (!TaxEntity.ThirdParty.VertexOSeries
			and   TaxDistributionCode entered)	

		TaxDistributionCodeValid	
			restricted
			when (TaxEntity.ThirdParty.VertexOSeries
			and   TaxDistributionCode entered)	
			
		HasVendorReturnLinesInIDM
			when (PayablesInvoiceRel.VendorReturn entered
			and VendorReturnLineRel exists)
			
		HasPayablesInvoiceDetailsInIDM
			when (PayablesInvoiceDetailRel exists)

		NoTaxTransationsExist	 			
			default label is untranslatable	 
			restricted
			when (GetCountOfTaxTransations = 0)
			
		ResetLastTransactionId	 			
			default label is untranslatable	 
			restricted
			when (NoTaxTransationsExist
			and LastTransactionID > 0)
									                        
    Relations


		TaxTransactionsRel
            one-to-many relation to TaxTransaction

			valid when (IsInforTaxOnly)
			Field Mapping uses symbolic key
      			related.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup   
                related.TaxEntity 						= TaxEntity			
            Instance Selection
                where (related.TaxCode = TaxCode)

        TxtaxrateRel
            one-to-many relation to TaxRate
            valid when (IsTaxRate)
            Field Mapping uses symbolic key
            Instance Selection
                where (related.TaxRate.TaxCode = TaxCode)

      	ExistingEntityTaxCodeRel
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= LocalTaxEntity.FinanceEnterpriseGroup   
                related.TaxEntity 						= LocalTaxEntity
                related.TaxCode							= LocalTaxCode    

		TaxTableRel
			one-to-many relation to TaxTable
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup    
				related.TaxCode							= TaxCode
				
 		TaxDistributionoCodeDetailsRel					
			one-to-many relation to TaxDistributionCodeDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup	
				related.TaxEntity 						= TaxEntity		
				related.TaxDistributionCode	  			= TaxDistributionCode	
				

		PayablesInvoiceDetailRel
            one-to-many relation to PayablesInvoiceDetail
            Field Mapping uses symbolic key
                related.Company 		= TaxEntity
           		related.PayablesInvoice	= PayablesInvoice
           	Instance Selection
           		where (related.TaxCode	= TaxCode)
           		
        PayablesInvoiceRel
        	one-to-one relation to PayablesInvoice
        	Field Mapping uses symbolic key
        		related.Company 		= TaxEntity
        		related.PayablesInvoice = PayablesInvoice
        		
        VendorReturnLineRel
        	one-to-many relation to VendorReturnLine
        	Field Mapping uses symbolic key
        		related.Company 		= TaxEntity
        		related.VendorReturn 	= PayablesInvoiceRel.VendorReturn
        	Instance Selection
        		where (related.TaxCode = TaxCode)
								
    Sets

        Set2
            indexed
            Sort Order
            	FinanceEnterpriseGroup	
                TaxCode

				TaxEntity


  	Field Rules

		TaxCode				
			if (!TaxEntity.UseTaxCodeAccounts)
				cannot be entered		
					"CannotAdd;TaxEntity<TaxEntity>DoesNotUseTaxCodeAccounts"  							
				
			if (TaxEntity.ThirdParty entered)
			 	if (TaxCode.TaxType.TaxTableCode
				and !TaxEntity.ThirdParty.TaxEngine)
					cannot be entered						
						"TaxEntityUsing3rdPartyTaxCannotUseTaxTableCode"								
				if (TaxCode.TaxType.CalculatedRate)
					cannot be entered
						"TaxEntityUsing3rdPartyTax,CannotUseCalculatedRateCode"  						

		AccruedOrInvoiced
			if (!TaxCode.TaxType.TaxTableCode)
				required	
					"AccrualFlagRequiredForStand-alone,CalculateRateAndTableMemberTaxCodes"				
			else
				cannot be entered
					"AccrualFlagCannotBeEnteredForATaxTableTaxCode"			 							

			if (AccruedOrInvoiced.Accrued
			and TaxCode.TaxType.CalculatedRate)
				cannot be entered
					"CalculatedRateTaxCodeCannotBeAccrued"												

		TaxPoint
			if (!TaxCode.TaxType.TaxTableCode)			
				required	
					"TaxPointRequiredForStand-alone,CalculateRateAndTableMemberTaxCodes"				
			else
			if (TaxCode.TaxType.TaxTableCode)
				cannot be entered
					"TaxPointCannotBeEnteredForATaxTableTaxCode"
			if (TaxEntity.ThirdParty entered)							
				constraint (!TaxPoint.PaymentDate)
					"TaxPointCannotBe_\PWhenUsingAThirdPartyTaxSystem"																		
					
		TaxTerms
			required
			if (!TaxCode.TaxType.TaxTableCode)	
				required
					"TaxTermsRequiredForStand-alone,CalculateRateAndTableMemberTaxCodes"				
				if (TaxTerms entered
				and TaxEntity.TaxTerms entered)	
					constraint (TaxTerms = TaxEntity.TaxTerms)
						"TaxCodeTaxTermsDefinitionMustEqualTaxTaxEntityTaxTermsDefinition"				

		TaxID
			if (!TaxCode.TaxType.TaxTableCode)	
				required
					"TaxIDRequiredForStand-alone,CalculateRateAndTableMemberTaxCodes"					
			else
			if (TaxCode.TaxType.TaxTableCode)
				cannot be entered
					"TaxIDNotRequiredForATaxTableTaxCode"	

		LandedCost
			if (TaxCode.TaxType.TaxTableCode)																	
				cannot be entered
					"LandedCostNotValidForATaxTableTaxCode"						

			if (LandedCost) 
				constraint (PostOption.DistributionAccountEntity
				or          PostOption.Blank)
					"PostOptionCannotBeSetToInvoiceCompanyForLandedCostTax"	
					
		AllowAccountChanges
			if (TaxCode.TaxType.TaxTableCode)																	
				cannot be entered
					"AllowAccountChangesOptionNotValidForATaxTableTaxCode"		

		VendorGroup
			if (AccrualCode entered)
				required
				
		AccrualCode
			if (TaxCode.TaxType.TaxTableCode)																					
				cannot be entered
					"AccrualCodeNotValidForATaxTableTaxCode"				
			if (TaxEntity.ThirdParty.VertexOSeries)

				required
					"TaxAccrualCodeRequiredIfUsing_\Vertex"
					
			if (VATReverseIndicator) 												
				required
					"TaxAccrualCodeRequiredIf_\V\A\T\ReverseIndicator=true"
											

		TaxInputAccount
			if (!TaxCode.TaxType.TaxTableCode)		 	
				required
					"InputAccountRequiredForStand-alone,CalculateRateAndTableMemberTaxCodes"			
			else
			if (TaxCode.TaxType.TaxTableCode)
				initialize TaxInputAccount																															
				cannot be entered
					"InputAccountNotValidForATaxTableTaxCode"											

			if (PostOption not entered	
			or  PostOption = "I")
				if (TaxInputAccount entered)	
					constraint (TaxInputAccount.ToAccountingEntity= TaxEntity)
						"CannotPostToADifferentAccountingEntityUsingThisPostOption"
						

		TaxInputDueAccount
			if (!TaxCode.TaxType.TaxTableCode		 	
			and TaxPoint.PaymentDate)	    												
				required
					"AValidInputDueAccountRequiredWhenTaxPointIsP"	 									
			else
			if (TaxCode.TaxType.TaxTableCode)
				initialize TaxInputDueAccount																																				 												
				cannot be entered
					"InputDueAccountNotValidForATaxTableTaxCode"	 									

			if (PostOption not entered	
			or  PostOption = "I")
				if (TaxInputDueAccount entered)	
					constraint (TaxInputDueAccount.ToAccountingEntity= TaxEntity)
						"CannotPostToADifferentAccountingEntityUsingThisPostOption"
					

		TaxOutputAccount
			if (!TaxCode.TaxType.TaxTableCode)		 	
				required
					"OutputAccountingUnitRequiredForStand-alone,CalculateRateAndTableMemberTaxCodes"	
			else
			if (TaxCode.TaxType.TaxTableCode)
				initialize TaxOutputAccount	
				cannot be entered
					"OutputAccountingUnitNotValidForATaxTableTaxCode"	 								
			if (PostOption not entered	
			or  PostOption = "I")
				if (TaxOutputAccount entered)	
					constraint (TaxOutputAccount.ToAccountingEntity= TaxEntity)
						"CannotPostToADifferentAccountingEntityUsingThisPostOption"
					

		TaxOutputDueAccount
			if (!TaxCode.TaxType.TaxTableCode
			and TaxPoint = "P")						            	
				required
					"OutputDueAccountingUnitRequiredWhenTaxPointIsP"									
			else
			if (TaxCode.TaxType.TaxTableCode)
				initialize TaxOutputDueAccount	
				cannot be entered
					"OutputDueAccountingUnitNotValidForATaxTableTaxCode"								

			if (PostOption not entered				
			or  PostOption = "I")
				if (TaxOutputDueAccount entered)	
					constraint (TaxOutputDueAccount.ToAccountingEntity= TaxEntity)
						"CannotPostToADifferentAccountingEntityUsingThisPostOption"
												

		TaxToleranceLimit
			if (TaxCode.TaxType.TaxTableCode)		
				cannot be entered
					"ToleranceLimitNotValidForATaxTableTaxCode"											
			if (TaxCode.TaxType.CalculatedRate)		
				cannot be entered
					"ToleranceLimitNotAllowedForCalculatedRateTaxCode"									
		FromRate
			if (!TaxCode.TaxType.TaxTableCode) 		 	                         			
				if (ThruRate entered)
					required
						"IfDefiningRateRange,DefineBoth_\FromAnd_\Thru"									

					constraint (FromRate < ThruRate)
						"FromRateMustBeLessThan_\ThruRate"												
			else
			if (TaxCode.TaxType.TaxTableCode)																																			 																			            	 						       			
				cannot be entered
					"ToleranceFromRateNotValidForATaxTableTaxCode"										

			if (TaxCode.TaxType.CalculatedRate)		
				cannot be entered
					"RateRangeNotAllowedForCalculatedRateTaxCode"										

		ThruRate
			if (TaxCode.TaxType.TaxTableCode)																																			 																			            	 						       			
				cannot be entered
					"ToleranceThruRateNotValidForATaxTableTaxCode"										

		TaxToleranceAmount	
			if (TaxCode.TaxType.TaxTableCode)																																			 																			            	 						       			
				cannot be entered
					"ToleranceAmountNotValidForATaxTableTaxCode"										

			if (TaxCode.TaxType.CalculatedRate)		
				cannot be entered
					"ToleranceAmountNotAllowedForCalculatedRateTaxCode"									


		ValueAddedTaxType	
			if (TaxCode.TaxType.TaxTableCode)						
				cannot be entered
					"VatTypeNotValidForATaxTableTaxCode"												
		GoodsAndServicesIndicator
			if (TaxCode.TaxType.TaxTableCode)						
				cannot be entered
					"GoodsAndServicesIndicatorNotValidForATaxTableTaxCode"								
		TriangulationIndicator	
			if (TaxCode.TaxType.TaxTableCode)						
				cannot be entered
					"TriangulationIndicatorNotValidForATaxTableTaxCode"									


		PostOption





			if (PostOption not entered
			or  PostOption = "I")
				if (!LandedCost)					
					if (TaxInputAccount entered)	
						constraint (TaxInputAccount.ToAccountingEntity 		= TaxEntity)
							"CannotPostToADifferentAccountingEntityUsingSpecifiedPostOption"
					if (TaxInputDueAccount entered)	
						constraint (TaxInputDueAccount.ToAccountingEntity 	= TaxEntity)
							"CannotPostToADifferentAccountingEntityUsingSpecifiedPostOption"
					if (TaxOutputAccount entered)	
						constraint (TaxOutputAccount.ToAccountingEntity 	= TaxEntity)
							"CannotPostToADifferentAccountingEntityUsingSpecifiedPostOption"
					if (TaxOutputDueAccount entered)	
						constraint (TaxOutputDueAccount.ToAccountingEntity 	= TaxEntity)
							"CannotPostToADifferentAccountingEntityUsingSpecifiedPostOption"


		TaxDistributionCode
			constraint (!TaxCode.TaxType.TaxTableCode)				
				"CannotAddA_\Tax\Distribution\CodeToA_\TaxTable"
				
			if (!action type.Delete	
			and TaxDistributionCode entered)

				invoke Update TaxDistributionCode
					invoked.TransientTaxCode = EntityTaxCode.TaxCode
					invoked.ValidateTaxAccounts = true
					invoked.RequireAllInputOutputAccounts  = true
								

    Actions

        Create is a Create Action
					
    	Update is an Update Action  
    		Entrance Rules
    			if (TaxDistributionCode entered)
		 			constraint (!TaxCode.TaxType.TaxTableCode)				
						"CannotAddA_\Tax\Distribution\CodeToA_\TaxTable"   		
				if (!action type.Delete	
				and TaxDistributionCode entered)

					invoke Update TaxDistributionCode
						invoked.TransientTaxCode = EntityTaxCode.TaxCode
						invoked.ValidateTaxAccounts = true 
						invoked.RequireAllInputOutputAccounts  = true   			                  										 	
																		
        Delete is a Delete Action
			Entrance Rules

				constraint (NoTaxTransationsExist)							
					"CannotDeleteTaxCode;TaxTransactionsHaveBeenPosted"									

       	CreateNoValidate is a Create Action
        	restricted
        	bypass field rules
        	
		CopyFromExisting is an Instance Action	

			Parameters
				PrmTaxEntity			is a TaxEntity
					default label is "TaxEntity"
				ExistingTaxCodeDetail	is a TaxCode
				NewTaxCodeDetail		is a TaxCode
			Parameter Rules
				PrmTaxEntity
					initial value is EntityTaxCode.TaxEntity
					required
				ExistingTaxCodeDetail
					initial value is EntityTaxCode.TaxCode
					required
				NewTaxCodeDetail
					constraint (NewTaxCodeDetail.TaxType = ExistingTaxCodeDetail.TaxType)
						"BothFromAndToTaxCodeTypeMustBeTheSame"	 	 
					required
			Action Rules
				LocalTaxCode	= ExistingTaxCodeDetail
				LocalTaxEntity	= PrmTaxEntity
				if (ExistingEntityTaxCodeRel exist)
					invoke Create EntityTaxCode

						fill in fields from this instance
						invoked.TaxEntity	= ExistingEntityTaxCodeRel.TaxEntity
						invoked.TaxCode		= NewTaxCodeDetail
						invoked.Status 		= 1


		LastTransactionClearOrAddTo is an Instance Action

			Parameters
				LastTransactionId is Alpha 1	 				 
		            States 
		                Clear  value is ""
		                AddTo  value is "A"
			Action Rules
				if (LastTransactionId.AddTo)

					LastTransactionID = last TaxTransactionsRel.TaxTransaction	
				else
					initialize LastTransactionID 				

		ResetTransactionId is an Instance Action	 

        	valid when (ResetLastTransactionId)	
			Action Rules
				if (NoTaxTransationsExist)
					initialize LastTransactionID
