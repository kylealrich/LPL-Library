MatchAging is a BusinessClass
	owned by ma
	
	prefix is MtAg
	
	Ontology
		symbolic key is MatchAging
		
	Patterns
        disable AuditIndex
		disable Auditing   
       	disable EffectiveDated
		implements CreateStamp	
		implements UpdateStamp	
	
	Persistent Fields
		Status is Numeric 1
			States
				Processing value is 0
				Complete value is 1
				Deleting value is 2
		MigrationDate 	is Date 
	Transient Fields

	Local Fields
		LocalFinanceEnterpriseGroup is like FinanceEnterpriseGroup
		LocalPeriod                 is like MatchAgingPeriod 
			
	Derived Fields
	
		AgingInProcessMessage is a MessageField
        	restricted
        	"AgingInProcess"

		DeleteInProcessMessage is a MessageField
        	restricted
        	"DeleteInProcess"

		OpenToViewAgingResultsMsg is a MessageField
			restricted
			"OpenToViewAgingResults"

		AgingDisplayMessage is a DerivedField
			type is Text
			if (Status.Processing)
				return AgingInProcessMessage
			else
			if (Status.Deleting)
				return DeleteInProcessMessage
			else
				return OpenToViewAgingResultsMsg
	Field Rules

	Conditions
		MatchAgingExists
			when (AnyMatchAgingRel exists)


	Relations

		MatchAgingSummaryRel 
			one-to-many relation to MatchAgingSummary
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.MatchAging	= MatchAging
		AnyMatchAgingRel 
			one-to-many relation to MatchAgingSummary
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				
		VendorSummaryRel 
			one-to-many relation to MatchAgingSummary
			Field Mapping uses BySummaryTypeVendor
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.MatchAging	= MatchAging				
				related.SummaryType = 1 
				
		VendorUnmatchedSummaryRel 
			one-to-many relation to MatchAgingSummary
			Field Mapping uses BySummaryTypeVendor
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.MatchAging	= MatchAging
				related.SummaryType = 3 
		YearMonthSummaryRel 
			one-to-many relation to MatchAgingSummary
			Field Mapping uses BySummaryTypeYearMonth
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.MatchAging	= MatchAging
				related.SummaryType = 2 
		AllSummaryRel 
			one-to-many relation to MatchAgingSummary
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.MatchAging	= MatchAging

		AllInvoicesRel
		    one-to-many relation to MatchAgingInvoice
		    Field Mapping uses symbolic key

		UnmatchedInvoicesRel
		    one-to-many relation to MatchAgingInvoice
		    Field Mapping uses symbolic key

		AgingPeriodRel
		    one-to-many relation to MatchAgingPeriod
		    Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup

		LocalPeriodRel
			one-to-one relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= LocalFinanceEnterpriseGroup
				related.MatchAgingPeriod		= LocalPeriod
		LocalMatchAgingRel 
			one-to-one relation to MatchAging
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= LocalFinanceEnterpriseGroup
				related.MatchAging				= 1
	Sets

	Actions
			
		Create is an Action
			restricted
				
		Update is an Update Action
			restricted

		Delete is an Action
			restricted
			Entrance Rules
				invoke Delete AgingPeriodRel

		DeleteAll is an Instance Action
			valid when (Status.Complete)
			default label is "DeleteAging"
			Action Rules
				Status = Status.Deleting
				invoke DeleteSet MatchAgingInvoice
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMatchAging				= MatchAging


		CreateMatchAgingSummary is a Set Action
			valid when (!MatchAgingExists)
			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup
				ThroughNumberOfDaysPeriod1	is Numeric 7 
				ThroughNumberOfDaysPeriod2	is Numeric 7 
				ThroughNumberOfDaysPeriod3	is Numeric 7 
				FromDate 					is Date 
				PrmMigrationDate  			is Date 
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				ThroughNumberOfDaysPeriod1	
					required  
				ThroughNumberOfDaysPeriod2	
					required  
					constraint (ThroughNumberOfDaysPeriod2 > ThroughNumberOfDaysPeriod1)
						"MatchedWithinDaysForPeriod2MustBeGreaterThanMatchedWithinDaysForPeriod1"
				ThroughNumberOfDaysPeriod3	
					required  
					constraint (ThroughNumberOfDaysPeriod3 > ThroughNumberOfDaysPeriod2)
						"MatchedWithinDaysForPeriod3MustBeGreaterThanMatchedWithinDaysForPeriod2"				
			Rule Blocks 
				CreateAging 
					invoke Create MatchAging
						invoked.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.MatchAging = 1
						invoked.MigrationDate = PrmMigrationDate 

					invoke Create MatchAgingPeriod
						invoked.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.MatchAgingPeriod = 1
						invoked.ThroughDays = ThroughNumberOfDaysPeriod1
					invoke Create MatchAgingPeriod
						invoked.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.MatchAgingPeriod = 2
						invoked.ThroughDays = ThroughNumberOfDaysPeriod2
					invoke Create MatchAgingPeriod
						invoked.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.MatchAgingPeriod = 3
						invoked.ThroughDays = ThroughNumberOfDaysPeriod3
					invoke Create MatchAgingPeriod
						invoked.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.MatchAgingPeriod = 4
						invoked.ThroughDays = 9999999   

					invoke CreateMatchAgingInvoice PayablesInvoice
						invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.PrmMatchAging = 1
						invoked.FromDate = FromDate 

			Action Rules
				Empty Set Rules
					include CreateAging 
				Set Rules 
					Exit Rules 
						LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup 
						if (!LocalMatchAgingRel exists)
							include CreateAging 
						
		ReSummarize is an Instance Action
			default label is "Resummarize"
			valid when (Status.Complete)
			run in background
			Action Rules
				constraint (AgingPeriodRel exists)
					"AgingPeriodsHaveNotBeenCreated"
				Status = Status.Processing
			Exit Rules
				invoke Resummarize MatchAgingSummary
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMatchAging				= MatchAging

		RecreateAgingPeriods is an Instance Action
			default label is "ModifyAgingPeriods"
			valid when (Status.Complete)
			run in background
			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup
				ThroughNumberOfDaysPeriod1	is Numeric 7 
				ThroughNumberOfDaysPeriod2	is Numeric 7 
				ThroughNumberOfDaysPeriod3	is Numeric 7 

			Parameter Rules 
				PrmFinanceEnterpriseGroup
					initial value is MatchAging.FinanceEnterpriseGroup 
					default to MatchAging.FinanceEnterpriseGroup
				ThroughNumberOfDaysPeriod1	
					required  
				ThroughNumberOfDaysPeriod2	
					required  
					constraint (ThroughNumberOfDaysPeriod2 > ThroughNumberOfDaysPeriod1)
						"MatchedWithinDaysForPeriod2MustBeGreaterThanMatchedWithinDaysForPeriod1"
				ThroughNumberOfDaysPeriod3	
					required  
					constraint (ThroughNumberOfDaysPeriod3 > ThroughNumberOfDaysPeriod2)
						"MatchedWithinDaysForPeriod3MustBeGreaterThanMatchedWithinDaysForPeriod2"				
			Action Rules
				Status = 0 
				LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup 
				LocalPeriod                 = 1
				invoke Update LocalPeriodRel
					invoked.ThroughDays = ThroughNumberOfDaysPeriod1
				LocalPeriod                 = 2
				invoke Update LocalPeriodRel
					invoked.ThroughDays = ThroughNumberOfDaysPeriod2
				LocalPeriod                 = 3
				invoke Update LocalPeriodRel
					invoked.ThroughDays = ThroughNumberOfDaysPeriod3

				invoke Resummarize MatchAgingSummary
					invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
					invoked.PrmMatchAging				= MatchAging

								
