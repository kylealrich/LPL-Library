ItemStorageLocation is a BusinessClass
    owned by wh
    prefix is IWSL

    Ontology
	    symbolic key is ItemStorageLocation

	Patterns
		implements StaticJava
		disable AuditIndex    

    Persistent Fields
        Active
        Bin
        ExpirationWarning        is Numeric size 4

    Field Rules
        Bin
            if (WarehouseStorageLocationIsABinRel exists)
                force default to WarehouseStorageLocation

        ExpirationWarning
            constraint (ExpirationWarning > 0)
                "ValueMustBeGreaterThanZero"

    Derived Fields

		DerivedFormTitle    is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return ItemStorageLocationCardViewDisplay
			else
				return CreateMessage

        CreateMessage   is a LabelField
            "CreateAn_Item_Storage_Location"

        ItemStorageLocationCardViewDisplay  is a LabelField
            "<Item.ItemAndDescriptionDisplay>|Location<InventoryLocation>|Storage_Location<WarehouseStorageLocation>"

        ExpirationWarningLabel  is a LabelField
            "DaysBeforeTheExpirationDate"

        DerivedStorageCode is a DerivedField
            type is like StorageCode
            if (BinRel exists)
                return BinRel.StorageCode

        DerivedStorageCodeAndDescription is a MessageField
            default label is "Storage_Code"
            "<BinStorageCodeRel.DerivedStorageCodeAndDescription>"

        DerivedStorageCodeMaximumVolume is a DerivedField
            type is like StockVolume
            default label is "Maximum_Volume"
            if (BinStorageCodeRel exists)
                return BinStorageCodeRel.MaximumVolume

        DerivedStorageCodeMaximumWeight is a DerivedField
            type is like StockWeight
            default label is "Maximum_Weight"
            if (BinStorageCodeRel exists)
                return BinStorageCodeRel.MaximumWeight

        DerivedStorageCodeMaximumQuantity is a DerivedField
            type is like Quantity
            default label is "Maximum_Quantity"
            if (BinStorageCodeRel exists)
                return BinStorageCodeRel.MaximumQuantity

        DerivedStorageCodeUOM is a DerivedField
            type is like UnitOfMeasure
            default label is "UnitOf_Measure"
            if (BinStorageCodeRel exists)
                return BinStorageCodeRel.UnitOfMeasure

        DerivedWarehouseZoneRepresentativeText is a MessageField
            default label is "Warehouse_Zone"
            "<WarehouseZoneRel.WarehouseZoneAndDescription>"
        
        DerivedWarehouseZoneComponentRepresentativeText is a MessageField
            default label is "Storage_System_Unit"
            "<WarehouseZoneComponentRel.WarehouseZoneComponentAndDescription>"

        DerivedWarehouseComponentTypeAndDescription is a MessageField
            default label is "Storage_System"
            "<WarehouseComponentTypeRel.WarehouseComponentTypeAndDescription>"

        DerivedAisle is a MessageField
            default label is "Warehouse_Aisle"
            "<WarehouseZoneComponentRel.WarehouseAisle.WarehouseAisleAndDescription>"


    Conditions
        AllowCommingling
            restricted
            when (WarehouseStorageLocation.AllowCommingling
            or   ItemStorageLocationRel not exists)

        RecordExists
            restricted
			when (ItemStorageLocation exists)

        HasBinStorageCode
            restricted
            when (BinRel.StorageCode entered)

		IsLotTracked
			restricted
			when (not ItemLocationRel.LotTracked.NotLotTracked)

        HasExpirationDateWarning
            restricted
            when (IsLotTracked
            and  ExpirationWarning entered)

        HasActivePutawayTransaction
            restricted
            when (ActivePurchaseOrderTransactionDetailsRel exists)

        HasInventoryTransaction
            restricted
            when (HasInventoryTransactionDetailsRel exists)

    Sets
        ByStorageLocation
            Sort Order
                Company
                InventoryLocation
                WarehouseStorageLocation
                Item

    Relations
        WarehouseStorageLocationIsABinRel
            one-to-many relation to Bin
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.Bin                             = WarehouseStorageLocation
            Instance Selection
                where (related.UseAsWarehouseStorageLocation
                and    related.WarehouseStorageLocation = WarehouseStorageLocation)

        InventoryLocationsRel
            one-to-many relation to InventoryLocation
            Field Mapping uses symbolic key
                related.Company             = Company
            Instance Selection
                where (related.UseAsWarehouseLocation)

        ItemLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company             = Company
                related.InventoryLocation   = InventoryLocation
                related.Item                = Item

        BinRel
            one-to-one relation to Bin
            Field Mapping uses symbolic key
                related.Company             = Company
                related.InventoryLocation   = InventoryLocation
                related.Bin                 = Bin

        BinStorageCodeRel
            one-to-one relation to StorageCode
            Field Mapping uses symbolic key
                related.Company             = Company
                related.InventoryLocation   = InventoryLocation
                related.StorageCode         = DerivedStorageCode

        ValidBinsForItemStorageLocationRel
            one-to-many relation to Bin
            Field Mapping uses symbolic key
                related.Company             = Company
                related.InventoryLocation   = InventoryLocation
            Instance Selection
                where (related.WarehouseStorageLocation = WarehouseStorageLocation)

        ItemStorageLocationRel
            one-to-many relation to ItemStorageLocation
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
            Instance Selection
                where (related.WarehouseStorageLocation = WarehouseStorageLocation)

        WarehouseZoneRel
            one-to-one relation to WarehouseZone
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseZone                   = WarehouseStorageLocation.WarehouseZone

        WarehouseZoneComponentRel
            one-to-one relation to WarehouseZoneComponent
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseZone                   = WarehouseStorageLocation.WarehouseZone
                related.WarehouseZoneComponent          = WarehouseStorageLocation.WarehouseZoneComponent

        WarehouseComponentTypeRel
            one-to-one relation to WarehouseComponentType
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseComponentType          = WarehouseZoneComponentRel.WarehouseComponentType

        ActivePurchaseOrderTransactionDetailsRel
            one-to-many relation to PurchaseOrderTransactionDetail
            Field Mapping uses BySerialAndItem
                related.Item                            = Item
            Instance Selection
                where (related.Company                  = Company
                and    not related.PurchaseOrderReceipt.IsArchivedClosed)

        HasInventoryTransactionDetailsRel
            one-to-many relation to InventoryTransactionLineDetail
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
            Instance Selection
                where (related.Item                     = Item)

        
    Create Rules
        constraint (AllowCommingling)
            "WarehouseStorageLocation<WarehouseStorageLocation>DoesNotAllowCommingling"
        constraint (ItemLocationRel.IsAWarehouseLocationItem)
            "ItemMustBe_Inventory_TrackedAnd_Bin_TrackedAt_Location<InventoryLocation>"

    Actions            
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action
            Entrance Rules
                constraint (not HasActivePutawayTransaction)
                    "CannotDelete,ActivityExists"
                
                constraint (not HasInventoryTransaction)
                    "CannotDelete,InventoryTransactionsExists"
