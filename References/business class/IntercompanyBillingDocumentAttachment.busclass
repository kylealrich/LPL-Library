IntercompanyBillingDocumentAttachment is a BusinessClass
    owned by intercobilling
    prefix is ICBDA
    sql name is "ICBDocumentAttachment"

    Ontology
        symbolic key is IntercompanyBillingDocumentAttachment
    
    Persistent Fields
      	AttachmentDate         is TimeStamp
            default label is "Date"
        Attachment  		is an AlternateAttachment

	Field Rules
      	Attachment 
    		required
    			"AttachmentIsRequired"
    	AttachmentDate
    		default to current timestamp

	Local Fields
		LocalAttributeCtr   is Numeric 2

	Conditions
		IsAvailableForAttachment
			restricted
			when (IntercompanyBillingDocumentHeader.Status.Unreleased
			or  IntercompanyBillingDocumentHeader.Status.PendingApproval
			or  IntercompanyBillingDocumentHeader.Status.Approved)

		HasAttachment
			restricted
			when (Attachment entered)

	Sets
		ByAttachmentDate
			Sort Order
				IntercompanyBillingDocumentHeader
				AttachmentDate
				IntercompanyBillingDocumentAttachment

	Create Rules
		include IDM.CreateRules 
			replace AttachmentField with Attachment

	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Actions
		Create is a Create Action
			valid when (IsAvailableForAttachment)

		Update is an Update Action
			valid when (IsAvailableForAttachment)

		Delete is a Delete Action
			valid when (IsAvailableForAttachment)

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField with Attachment

		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField with Attachment
					
					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
