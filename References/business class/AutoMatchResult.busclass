AutoMatchResult is a BusinessClass
	owned by ma
	
	prefix is AuMRs
	
	Ontology
		symbolic key is AutoMatchResult
		
	Patterns
        disable AuditIndex
		disable Auditing   
       	disable EffectiveDated
		implements CreateStamp	
		implements UpdateStamp	
	
	Persistent Fields
		RunTime								is TimeStamp

        PrmCompany                 			is a PayablesCompany
        	default label is "Company"
        PrmCompanyGroup            			is a GeneralLedgerCompanyGroup
        	default label is "CompanyGroup"
        PrmMatchPoint              			is AlphaUpper size 1
            default label is "MatchPoint"
            States
                RuleGroup1 value is "1"
                RuleGroup2 value is "2"
                RuleGroup3 value is "3"
        PrmOverrideInvoiceRuleGroups		is Boolean
		PrmVendorRange						is a VendorNumberRange
        PrmInvoiceProcess          			is Numeric size 1
            States
                ExcludePrepayInvoices     value is 0
                IncludePrepayInvoices     value is 1
                PrepayInvoicesOnly        value is 2
                ExcludeServiceAndDropShip value is 3
        PrmPrepaymentInvoiceMatchRule		is like MatchRule
    	PrmPrepaymentDetailMatchRule		is like MatchRule
        PrmDateType                			is AlphaRight size 1
            default label is "DateType"
            States
                DueDate     value is "D"
                InvoiceDate value is "I"
        PrmRunDate             	    		is Date
        	default label is "RunDate"
        PrmUseSystemDate					is Boolean
        PrmDaysBeforeRunDate           		is Numeric size 3
        PrmDaysAfterRunDate            		is Numeric size 3
        PrmInterfaceRun						is like PayablesInvoiceInterfaceResult
		PrmSummarize						is Numeric 1
			States
				DoNotSummarize				value is 0
				Summarize					value is 1
		PrmSkipDays            	    	is Numeric 3
		PrmSkipDate                		is AlphaRight size 1
			States
				InvoiceDate 			value is "I"
				ReceiptOfInvoiceDate    value is "R"
		PrmSkipOnlyIfNoReceipt			is Boolean

		SelectionComplete					is Boolean
		PerfectMatchCount					is Numeric 9
		PerfectMatchAmount					is an InternationalAmount
		MatchedInToleranceCount				is Numeric 9
		MatchedInToleranceAmount			is an InternationalAmount
		MatchedOutOfToleranceCount			is Numeric 9
			default label is "MatchedOutOfToleranceCount"
		MatchedOutOfToleranceAmount			is an InternationalAmount
		MatchedWithChargebackCount			is Numeric 9
		MatchedWithChargebackAmount			is an InternationalAmount
		TotalChargebackAmount				is an InternationalAmount
		MatchedWithServiceContractCount		is Numeric 9
		MatchedWithServiceContractAmount	is an InternationalAmount
		UnmatchedCount						is Numeric 9
		UnmatchedAmount						is an InternationalAmount
		ErrorCount							is Numeric 9
		ErrorAmount							is an InternationalAmount
		CostVariance						is an InternationalAmount
		
		ProcessingComplete					is Boolean
		SummaryComplete						is Boolean
		MarkedComplete 						is TimeStamp
		
	Transient Fields

	Local Fields
		LocalDecimal is Decimal size 18.8						

	Derived Fields
	
		NoRecordsToProcessMsg is a MessageField
			restricted
			"NoRecordsToProcess"
			 
		InProcessMessage is a MessageField
        	restricted
        	"InProcess"
        	
		CompleteMessage is a MessageField
        	restricted
        	"Complete"
        	
        IncompleteMessage is a MessageField
        	restricted
        	"Incomplete"
        	
        NoRecordsMessage is a MessageField
        	restricted
        	"AllErrorsProcessed"

		NoRecordsFoundForCriteriaMsg is a MessageField
			restricted
			"***NoRecordsFoundForCriteriaEntered***"

		ProcessingErrorsMsg is a MessageField
			restricted
			"SomeInvoicesHaveProcessingErrors.SeeProcessingErrorsPanel"
					        
		DerivedVendorRange is a StringField
			type is Alpha 100
			PrmVendorRange.Begin 
			" - "
			PrmVendorRange.End

		DerivedRunDate is a DerivedField
			type is Date
			default label is "RunDate"
			return RunTime date
			
		DerivedRunTime is a DerivedField
			type is Date
			default label is "RunTime"
			return RunTime time
			
		TotalCountMatchInvoices is a ComputeField
			type is Numeric 10
			(PerfectMatchCount + MatchedInToleranceCount + MatchedOutOfToleranceCount + MatchedWithChargebackCount + MatchedWithServiceContractCount)

		TotalAmountMatchInvoices is a DerivedField
			type is like InternationalAmount
			return (PerfectMatchAmount + MatchedInToleranceAmount + MatchedOutOfToleranceAmount + MatchedWithChargebackAmount + MatchedWithServiceContractAmount)

		DerivedSecondsDifference is a DerivedField
			type is Decimal 10.2
			restricted
			if (RunTime entered and MarkedComplete entered)
				return MarkedComplete - RunTime
				
		DerivedElapsedSeconds is a DerivedField
			type is Decimal 10
			restricted
			LocalDecimal = DerivedSecondsDifference % 60
			round LocalDecimal down to nearest 1
			return LocalDecimal 
				
		DerivedElapsedMinutes is a DerivedField
			type is Numeric size 10
			restricted
            LocalDecimal = DerivedSecondsDifference % 3600 / 60
			round LocalDecimal down to nearest 1
			return LocalDecimal 
				
		DerivedElapsedHours is a DerivedField
			type is Numeric size 10
			restricted
			LocalDecimal = DerivedSecondsDifference % (3600 * 24) / 3600
			round LocalDecimal down to nearest 1
			return LocalDecimal 
				
		DerivedElapsedDays is a DerivedField
			type is Numeric size 3
			restricted
			LocalDecimal = DerivedSecondsDifference / (3600 * 24)
			round LocalDecimal down to nearest 1
			return LocalDecimal 

				
		DerivedElapsedTime is a DerivedField
			type is Alpha 100
			if (DerivedElapsedMinutes not entered
			and DerivedElapsedHours not entered 
			and DerivedElapsedDays not entered)
                return (DerivedElapsedSeconds + "s")
            else
            if (DerivedElapsedHours not entered
			and DerivedElapsedDays not entered)
                return (DerivedElapsedMinutes + "m " + DerivedElapsedSeconds + "s")
            else
            if (DerivedElapsedDays not entered)
                return (DerivedElapsedHours + "h " + DerivedElapsedMinutes + "m " + DerivedElapsedSeconds + "s")
            else
                return (DerivedElapsedDays + "d " + DerivedElapsedHours + "h " + DerivedElapsedMinutes + "m " + DerivedElapsedSeconds + "s")
		

	Field Rules

	Conditions
		MatchingInProcess
			when (SelectionComplete
			and  !ProcessingComplete)


		SummaryNotComplete
			when (SelectionComplete
			and   ProcessingComplete
			and  !SummaryComplete)
		
		ProcessingCompleteHasRecords
			when (ProcessingComplete
			and   InvoicesRel exists)
					
		ProcessingCompleteNoRecords
			when (ProcessingComplete
			and   !InvoicesRel exists)

		InvoicesInProcess
			when (InProcessInvoicesRel exists)

		TerminatedInvoicesExist
			when (UnmatchedAsyncErrorRel exists)

		HasUnmatchedPayablesIssues
			when (UnmatchedPayablesIssuesRel exists)
								
		HasUnmatchedProcurementIssues
			when (UnmatchedProcurementIssuesRel exists)
			
		InterfaceRunEntered
			when (PrmInterfaceRun entered)

		DoneProcessing 
			when (SelectionComplete
			and   !InProcessInvoicesRel exists)								
	Relations
		InvoicesRel 
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup)

		InProcessInvoicesRel 
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				and    related.PayablesInvoice.BackgroundProcessing.MatchInvoice
				and    related.ErrorMessage not entered
				and   !related.AsyncError)

		MatchedInvoicesRel 
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				and    related.InvoiceMatched)

		UnmatchedPayablesIssuesRel 
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				and    related.SystemOwner.Payables

				and    related.InvoiceUnmatched
				and   !related.AsyncError
				and    related.ErrorMessage entered)

		UnmatchedProcurementIssuesRel 
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				and    related.SystemOwner.Procurement
				and    related.PayablesInvoice.BackgroundProcessing not entered
				and    related.InvoiceUnmatched
				and   !related.AsyncError
				and    related.ErrorMessage entered)
				
		UnmatchedAsyncErrorRel
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				and    related.InvoiceUnmatched
				and    related.AsyncError)

		AllInvoicesRel 
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByResult
				related.AutoMatchResult					= AutoMatchResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup = FinanceEnterpriseGroup)

		GeneralLedgerSystemCodeRel		
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode	= "AP"	

		AutoMatchResultSummaryRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
				
		VendorSummaryRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses ByVendor
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.SummaryType.ByVendor
				and    related.IsNotTotal)
				
		VendorSummaryTotalRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses ByVendor
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.SummaryType.ByVendor
				and    related.IsTotal)
				
		BuyerSummaryRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses ByBuyer
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.SummaryType.ByBuyer
				and    related.IsNotTotal)
		
		BuyerSummaryTotalRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses ByBuyer
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.SummaryType.ByBuyer
				and    related.IsTotal)
		
		ErrorNumberSummaryRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses ByErrorNumber
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.SummaryType.ByErrorNumber
				and    related.ErrorNumber entered
				and    related.IsNotTotal)

		ErrorNumberSummaryTotalRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses ByErrorNumber
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult
			Instance Selection
				where (related.SummaryType.ByErrorNumber
				and    related.ErrorNumber entered
				and    related.IsTotal)

		AllSummaryRel 
			one-to-many relation to AutoMatchResultSummary
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AutoMatchResult	= AutoMatchResult

	Sets
		ByRunTime
			Sort Order
				RunTime descending
				AutoMatchResult descending
				FinanceEnterpriseGroup
				UniqueID

	Actions
			
		Create is an Action
			restricted
			Action Rules
				RunTime = current timestamp
				
		Update is an Update Action
			restricted
			Exit Rules
				if (ProcessingComplete
				or  SelectionComplete)
					if (GeneralLedgerSystemCodeRel.EncumbranceOption.Track 
					or  GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit) 
						invoke ProcessInterfaceBatchEdits BudgetEditBatch
							invoked.PrmFinanceEnterpriseGroup 	= FinanceEnterpriseGroup 
							invoked.PrmBusinessClassName 		= "PayablesInvoice" 

		FastUpdate is an Update Action
			refresh and lock this instance
			restricted
			bypass field rules
			Action Rules
				if  (SelectionComplete)
					if (!InvoicesRel exist)
						ProcessingComplete			= true
						MarkedComplete 				= current timestamp

			Exit Rules

				if  (SelectionComplete
				and  InvoicesRel exist)

					invoke UpdateResults first InvoicesRel.AutoMatchResultInvoice

				if  (SelectionComplete)

					if (GeneralLedgerSystemCodeRel.EncumbranceOption.Track
					or  GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit)
						invoke ProcessInterfaceBatchEdits BudgetEditBatch
							invoked.PrmFinanceEnterpriseGroup 	= FinanceEnterpriseGroup 
							invoked.PrmBusinessClassName 		= "PayablesInvoice" 
			
		Delete is an Action
			Entrance Rules
				constraint (ProcessingComplete
				and         SelectionComplete)
					"CannotDelete;JobIsRunning"
					
				invoke FastDelete InvoicesRel
				invoke FastDelete AllSummaryRel

		Purge is a Purge Action
			restricted
			bypass relational integrity rules

		PurgeResults is a Set Action

			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmPurgeThruDate				is Date
					default label is "PurgeThroughDate"
				PrmThruDaysLessThanCurrentDay	is Numeric 3

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmPurgeThruDate
					constraint (PrmPurgeThruDate date < current date)
						"DateEnteredMustBePriorToToday"
				PrmThruDaysLessThanCurrentDay
					if (PrmPurgeThruDate not entered)
						required
							"MustEnterPurgeThroughDateOrPurgeThroughDays"
					else
						cannot be entered
							"CannotEnterBothPurgeThroughDateAndPurgeThroughDays"

			Local Fields
				LocalCompareDate is Date						

			Action Rules
				Instance Rules
					if (PrmPurgeThruDate entered)
						LocalCompareDate = PrmPurgeThruDate
					else
					if  (PrmThruDaysLessThanCurrentDay entered)
						LocalCompareDate = current corporate date - PrmThruDaysLessThanCurrentDay as days
					

					if  (RunTime date <= LocalCompareDate)
						invoke Purge InvoicesRel
						invoke Purge AllSummaryRel
						invoke Purge

		UpdateTotalsAndMarkComplete is an Instance Action
			Action Rules
				invoke UpdateTotalsAndMarkComplete AutoMatchResultInvoice
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
					invoked.PrmAutoMatchResult				= AutoMatchResult

		Summarize is an Instance Action
			default label is "SummarizeErrors"
			Action Rules
				PrmSummarize = 1
				invoke Summarize AutoMatchResultInvoice
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
					invoked.PrmAutoMatchResult				= AutoMatchResult

		RerunWithSameParameters is an Instance Action
			Action Rules
				invoke AutoMatch PayablesInvoice
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
			        invoked.PrmCompany						= PrmCompany
			        invoked.PrmCompanyGroup					= PrmCompanyGroup
			        invoked.PrmMatchPoint					= PrmMatchPoint

					invoked.PrmVendorRange					= PrmVendorRange
			        invoked.PrmInvoiceProcess				= PrmInvoiceProcess
			        invoked.PrmPrepaymentInvoiceMatchRule	= PrmPrepaymentInvoiceMatchRule
        			invoked.PrmPrepaymentDetailMatchRule	= PrmPrepaymentDetailMatchRule
			        invoked.PrmDateType						= PrmDateType
			        invoked.PrmRunDate						= PrmRunDate
			        invoked.PrmUseSystemDate				= PrmUseSystemDate
			        invoked.PrmDaysBeforeRunDate			= PrmDaysBeforeRunDate
			        invoked.PrmDaysAfterRunDate				= PrmDaysAfterRunDate
			        invoked.PrmSummarize					= PrmSummarize
					invoked.PrmSkipDays						= PrmSkipDays
					invoked.PrmSkipDate						= PrmSkipDate
					invoked.PrmSkipOnlyIfNoReceipt			= PrmSkipOnlyIfNoReceipt

		RunAutoMatch is a Set Action

			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmCompany                 		is a PayablesCompany
				PrmCompanyGroup            		is a GeneralLedgerCompanyGroup
				PrmMatchPoint              		is AlphaUpper size 1
					States
						RuleGroup1 value is "1"
						RuleGroup2 value is "2"
						RuleGroup3 value is "3"

				PrmVendorRange					is a VendorNumberRange
				PrmInvoiceProcess          		is Numeric size 1
					States
						ExcludePrepayInvoices     value is 0
						IncludePrepayInvoices     value is 1
						PrepayInvoicesOnly        value is 2
						ExcludeServiceAndDropShip value is 3
				PrmPrepaymentInvoiceMatchRule      is a MatchRule
					context of PrmFinanceEnterpriseGroup
				PrmPrepaymentDetailMatchRule       is a MatchRule
					context of PrmFinanceEnterpriseGroup
				PrmDateType                		is AlphaRight size 1
					States
						DueDate     value is "D"
						InvoiceDate value is "I"
				PrmRunDate             	    	is Date
				PrmUseSystemDate				is Boolean
				PrmDaysBeforeRunDate           	is Numeric size 3
				PrmDaysAfterRunDate            	is Numeric size 3
				PrmExcludeVendors				is a AP146VendorArray
				PrmSkipDays            	    	is Numeric 3
				PrmSkipDate                		is AlphaRight size 1
					States
						InvoiceDate 			value is "I"
						ReceiptOfInvoiceDate    value is "R"
				PrmSummarize					is Numeric 1
					States
						DoNotSummarize			value is 0
						Summarize				value is 1
				PrmSkipOnlyIfNoReceipt			is Boolean			

			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmCompany
					if (PrmCompanyGroup not entered)
						required
							"CompanyOrCompanyGroupRequired"

				PrmCompanyGroup
					if (PrmCompany entered)
						cannot be entered
							"CannotEnterCompanyGroupIfCompanyEntered"

				PrmMatchPoint
					default to PrmMatchPoint.RuleGroup1
					required
				PrmInvoiceProcess
					initial value is 0
					default to 0
				PrmDateType
					initial value is "D"
					default to "D"
				PrmSkipDate
					if (PrmSkipDays entered)
						required
							"SkipDateRequiredIfSkipDaysEntered"
							
			Action Rules
				Empty Set Rules
					invoke AutoMatch PayablesInvoice
						invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.PrmCompany						= PrmCompany
						invoked.PrmCompanyGroup					= PrmCompanyGroup
						invoked.PrmMatchPoint					= PrmMatchPoint

						invoked.PrmVendorRange					= PrmVendorRange
						invoked.PrmInvoiceProcess				= PrmInvoiceProcess
						invoked.PrmPrepaymentInvoiceMatchRule	= PrmPrepaymentInvoiceMatchRule
						invoked.PrmPrepaymentDetailMatchRule	= PrmPrepaymentDetailMatchRule
						invoked.PrmDateType						= PrmDateType
						invoked.PrmRunDate						= PrmRunDate
						invoked.PrmUseSystemDate				= PrmUseSystemDate
						invoked.PrmDaysBeforeRunDate			= PrmDaysBeforeRunDate
						invoked.PrmDaysAfterRunDate				= PrmDaysAfterRunDate
						invoked.PrmExcludeVendors				= PrmExcludeVendors
						invoked.PrmSkipDays						= PrmSkipDays
						invoked.PrmSkipDate						= PrmSkipDate
						invoked.PrmSummarize					= PrmSummarize
						invoked.PrmSkipOnlyIfNoReceipt			= PrmSkipOnlyIfNoReceipt
					
				Set Rules
					Exit Rules

						invoke AutoMatch PayablesInvoice
							invoked.PrmFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.PrmCompany						= PrmCompany
							invoked.PrmCompanyGroup					= PrmCompanyGroup
							invoked.PrmMatchPoint					= PrmMatchPoint

							invoked.PrmVendorRange					= PrmVendorRange
							invoked.PrmInvoiceProcess				= PrmInvoiceProcess
							invoked.PrmPrepaymentInvoiceMatchRule	= PrmPrepaymentInvoiceMatchRule
							invoked.PrmPrepaymentDetailMatchRule	= PrmPrepaymentDetailMatchRule
							invoked.PrmDateType						= PrmDateType
							invoked.PrmRunDate						= PrmRunDate
							invoked.PrmUseSystemDate				= PrmUseSystemDate
							invoked.PrmDaysBeforeRunDate			= PrmDaysBeforeRunDate
							invoked.PrmDaysAfterRunDate				= PrmDaysAfterRunDate
							invoked.PrmExcludeVendors				= PrmExcludeVendors
							invoked.PrmSkipDays						= PrmSkipDays
							invoked.PrmSkipDate						= PrmSkipDate
							invoked.PrmSummarize					= PrmSummarize
							invoked.PrmSkipOnlyIfNoReceipt			= PrmSkipOnlyIfNoReceipt
										
