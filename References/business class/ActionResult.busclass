ActionResult is a BusinessClass
    owned by la
    prefix is ARST
    stored in environment
    
    Ontology
    	symbolic key is ActionResult
    
	Patterns
    	implements ChildHierarchy
    		children are ChildResults
    	disable Auditing
    	disable EffectiveDated
    		
    Persistent Fields
    	Name				is Alpha 80
    	ParentActionResult	is an ActionResult
    		delete cascades
    	BusinessView
    		delete ignored
  		TaskAction			is a BusinessAction
  			delete ignored
  			default label is "Action"
  		BatchTask
  			delete ignored
  		BatchTaskStep
  			delete ignored
  			default label is "StepID"
  		Data				is DataView
  		JavaData			is Text
  		Actor
  		StartTime			is TimeStamp 
  		EndTime				is TimeStamp
  		Result				is Text
  		AsyncActionInvocation 
  			delete ignored    


  		DataArea
  		AdminCommand		is Boolean
  	
  	Conditions
  		HasParent
  			default label is "ParentEntered"
  			when (ParentActionResult entered)

		HasChildren
			default label is "ChildrenExist"
			when (ChildResults exists)  
			
		IsStandAlone
			default label is "Standalone"
			when (not HasParent
			and   not HasChildren)	
									
		HasResult
			default label is "ResultEntered"
			when (Result entered)	
			
		HasBatchTask
			default label is "JobEntered" 
			when (BatchTask entered)								
  	
  		HasTaskStep
  			default label is "JobStepEntered"
  			when (BatchTaskStep entered)

		HasReportData
			default label is "ReportDataExists"
			when (ReportDataRel exists)  	
			
		HasReports
			default label is "ReportsExist"
			when (ReportDocumentRel exists)	
			
		TimePassed
			when (ElapsedTime > 0)	
  			
  	Derived Fields
  		ViewActionName is a StringField
  			type is LPLName
  			BusinessView
  			"."
  			TaskAction.MethodName

		TopLevelResult is a DerivedField
			type is UniqueID
			
			WorkResult = ActionResult
			
			while (WorkResult.HasParent)
				WorkResult = WorkResult.ParentActionResult
			
			return WorkResult.ActionResult
			
		ElapsedTime		is a DerivedField
			type is Numeric 10
            if (EndTime != blank)
            	return EndTime - StartTime
            else
               	return current timestamp - StartTime
                
        DaysInElapsedTime is a DerivedField    
            type is Numeric 6
            DaysInElapsedTime = ((ElapsedTime / 86400) - .5)
            if (DaysInElapsedTime < 1)
                DaysInElapsedTime = 0

        HoursInElapsedTime is a DerivedField    
            type is Numeric 2
            HoursInElapsedTime = (((ElapsedTime - (DaysInElapsedTime * 86400)) / 3600) - .5)
            if (HoursInElapsedTime < 1)
                HoursInElapsedTime = 0

        MinutesInElapsedTime is a DerivedField  
            type is Numeric 2
            MinutesInElapsedTime = ((((ElapsedTime - (DaysInElapsedTime * 86400)) - (HoursInElapsedTime * 3600)) / 60) - .5)
            if (MinutesInElapsedTime < 1)
                MinutesInElapsedTime = 0

        SecondsInElapsedTime is a DerivedField  
            type is Numeric 2
            SecondsInElapsedTime = ((((ElapsedTime - (DaysInElapsedTime * 86400)) - (HoursInElapsedTime * 3600)) - (MinutesInElapsedTime * 60)) - .5)
            if (SecondsInElapsedTime < 1)
                SecondsInElapsedTime = 0     
                
        DaysHoursMinutesSecondsInElapsedTime is a MessageField
            "<DaysInElapsedTime>Days<HoursInElapsedTime>Hrs<MinutesInElapsedTime>Min<SecondsInElapsedTime>Sec"

        HoursMinutesSecondsInElapsedTime is a MessageField
            "<HoursInElapsedTime>Hrs<MinutesInElapsedTime>Min<SecondsInElapsedTime>Sec"

        MinutesSecondsInElapsedTime is a MessageField
            "<MinutesInElapsedTime>Min<SecondsInElapsedTime>Sec"
            
        JustSecondsInElapsedTime is a MessageField
        	"<SecondsInElapsedTime>Sec"
        	
		LessThanOneSecondElapsedTime is a MessageField
        	"LessThan1Second"        	

        ElapsedTimeComponentsText is a DerivedField
            type is MessageField
            if (DaysInElapsedTime !< 1)
                return DaysHoursMinutesSecondsInElapsedTime
            else
            if (HoursInElapsedTime !< 1)
                return HoursMinutesSecondsInElapsedTime
            else
            if (MinutesInElapsedTime !< 1)
                return MinutesSecondsInElapsedTime
            else
            if (SecondsInElapsedTime !< 1)
                return JustSecondsInElapsedTime
            else
                return LessThanOneSecondElapsedTime
                
		SuccessfullyCompleted is a MessageField
			"ActionCompleted"                
			
		ActionFailed is a MessageField
			"NotAllRecordsCouldBePurged."		
			
		PurgeMessage is a DerivedField
    		type is MessageField
    		if (PurgeError)
    			return ActionFailed
    		else
    			return SuccessfullyCompleted
				
	Local Fields
		WorkResult is an ActionResult	
		PurgeError is Boolean	  			
  		
  	Field Rules
  		BusinessView
  			if (HasTaskStep)
  				default to BatchTaskStep.TaskStep.BusinessClass
  			else
  			if (HasBatchTask)
  				default to BatchTask.BusinessClass
  				
  			if (TaskAction entered)
  				required
  		
  		TaskAction
  			if (HasTaskStep)
  				default to BatchTaskStep.TaskStep.TaskAction
			else
  			if (HasBatchTask)
  				default to BatchTask.TaskAction  				
  				
  		BatchTask
  			constraint (BatchTask entered
  			  		or  BusinessView entered
  			  		or  TaskAction entered)
  			 	"MustEnterBusinessViewAndActionOrJob"
  		
  		Actor
  			default to actor
  			required
  			
		AdminCommand
			if (TaskAction.AdminCommandOption.DefaultNonAdmin)
				AdminCommand = false
			else
				AdminCommand = true   			
  		
  		Name
  			if (BatchTaskStep exists)
  				if (BatchTaskStep.Description entered)
  					default to BatchTaskStep.Description
  				else 
  					default to BatchTaskStep.TaskStep
  			else
  			if (BatchTask exists)
  				default to BatchTask
  			else
  				default to ViewActionName

		DataArea
			default to parentcontext.dataarea
  	
  	Relations
  		ChildResults
			one-to-many relation to ActionResult
			delete cascades
			Field Mapping uses ByDataAreaParentResult
				related.DataArea = DataArea	
  				related.ParentActionResult = ActionResult		

		ReportDataRel
			one-to-many relation to ReportData
  			Field Mapping uses ByActionResult
  				related.ActionResult = ActionResult
  				
  		ReportDocumentRel
  			one-to-many relation to ReportDocument
  			Field Mapping uses ByRptData
  				related.ReportData = ReportDataRel.ReportData
  				
  		UserReportRel
  			one-to-one relation to UserReport
  			Field Mapping uses ByReportDocumentActor
  				related.ReportDocument = ReportDocumentRel.ReportDocument
  				related.Actor = Actor
  				







		AsyncActionInvocationRel
			one-to-one relation to AsyncActionInvocation
			Field Mapping uses symbolic key
				related.AsyncActionInvocation = AsyncActionInvocation

		TopLevelResultRel
			one-to-one relation to ActionResult
			Field Mapping uses symbolic key
				related.ActionResult = TopLevelResult
  		
	Sets
		ByName
			no duplicates
			indexed
 			Sort Order
 				Name
 				StartTime
 				ActionResult

		ByDataAreaActorName
			no duplicates
			indexed
 			Sort Order
 				DataArea
 				Actor
 				Name
 				StartTime
 				ActionResult 				
 		
 		ByDataAreaBatchTask
 			no duplicates
			indexed
 			Sort Order
 				DataArea
 				BatchTask
 				BatchTaskStep
 				Actor
 				ActionResult 	
 			Instance Selection
 				where (HasBatchTask)
 				
		ByDataAreaTaskAction
			no duplicates
			indexed
			Sort Order	
 				DataArea
				BusinessView
				TaskAction
				BatchTask
				BatchTaskStep
				ActionResult		 				
 				
		ByDataAreaParentResult
		 	no duplicates
			indexed
			Sort Order	
 				DataArea
				ParentActionResult
				StartTime
				ActionResult		
					
		ByDataAreaStartTimeName
		 	no duplicates
			indexed
			Sort Order	
 				DataArea
				StartTime
				Name
				ActionResult	
				
		ByAdminDATaskAction
			no duplicates
			Sort Order
				DataArea
				BusinessView
				TaskAction
				ActionResult
			Instance Selection
				where (AdminCommand)

		ByAdminDAActor
			no duplicates
			Sort Order
				DataArea
 				Actor
 				Name
 				StartTime
 				ActionResult 	
			Instance Selection
				where (AdminCommand)	
				
		ByAdminDATime
			no duplicates
			Sort Order
				DataArea
 				StartTime
 				Name 	
			Instance Selection
				where (AdminCommand)							
 	
		ByAsyncActionInvocationResult	
			no duplicates
			indexed
 			Sort Order
 				AsyncActionInvocation
 				ActionResult
 
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted
				
		Delete is a Delete Action
			Action Rules
				if (ParentActionResult entered)
					if (HasChildren)
						confirmation required
							"ThisResultIsPartOfASetAndContainsChildren.DeletingWillRemoveThisRecordAndItsChildrenOnly.AreYouSureYouWantToDelete?"
					else
						confirmation required
							"ThisResultIsPartOfASet.DeletingWillRemoveThisRecordOnly.AreYouSureYouWantToDelete?"
				else
				if (HasChildren)
					confirmation required
						"DeletingThisRecordWillAlsoDeleteTheEntireSubsetOfResults.AreYouSureYouWantToDelete?"
						
			Exit Rules
				invoke TriggerInvocationDelete AsyncActionInvocationRel
	
		DeleteInternal is a Delete Action
			restricted
			
        CascadeDelete is an Instance Action
            restricted
            
			Entrance Rules
			
				for each ChildResults
					invoke CascadeDelete each

				for each ReportDataRel 
					invoke Delete each 
							
			Action Rules
					
				if (ActionResult exists) 
					invoke DeleteInternal
						resume on error 

		PurgeAdministratorCommandResults is a Set Action
			completion message is "<PurgeMessage>"
			Parameters
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3
			
			Local Fields
				LocalThruDate 	is Date
					value is ThruDate
			


			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate  = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"

			Instance Selection
				where (DataArea = parentcontext.dataarea
				and    AdminCommand
				and    EndTime < LocalThruDate
				and    EndTime entered)

			Sort Order
				DataArea
				AdminCommand
				EndTime
				ActionResult	
				
			Action Rules
				Set Rules
					Exit Rules
						initialize PurgeError
				Instance Rules
					invoke DeleteInternal
						resume on error
							PurgeError = true
					
		PurgeCommandResults is a Set Action
			completion message is "<PurgeMessage>"
			Parameters
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3
			
			Local Fields
				LocalThruDate 	is Date
					value is ThruDate
			


			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate  = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"

			Instance Selection
				where (DataArea = parentcontext.dataarea
				and    EndTime < LocalThruDate
				and    EndTime entered)

			Sort Order
				DataArea
				EndTime
				ActionResult	
				
			Action Rules
				Set Rules
					Exit Rules
						initialize PurgeError
				Instance Rules
					invoke DeleteInternal
						resume on error
							PurgeError = true
