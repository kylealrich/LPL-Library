SupplierGroupQuestionValue is a BusinessClass
    owned by procurement
    prefix is SGQV

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Ontology
    	symbolic key is SupplierGroupQuestionValue

    Persistent Fields
    	DisplayOrder		is a Sequence
		QuestionToCreate	is like Question

	Transient Fields
		NewDisplayOrder		is a DisplayOrder
		
	Local Fields
		LastDisplayOrder			is a DisplayOrder		
		LocalConditionalQuestion 	is like Question
		LocalSupplier				is like Supplier

	Conditions 
		RepositoryHasConditionalQuestions 
			restricted
			when (RepositoryConditionalQuestionRel exists)

	Relations 

		SameRepositoryQuestionRel 
			one-to-many relation to SupplierGroupQuestion 
			Field Mapping uses ByQuestion
				related.SupplierGroup       = SupplierGroup 
				related.Question            = LocalConditionalQuestion 

		RepositoryConditionalQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses ByListAnswer
				related.ProcurementGroup										= SupplierGroup 
				related.Question                        						= SupplierGroupQuestion.Question 
				related.QuestionAnswer.QuestionAnswerGroup.QuestionListValue	= SupplierGroupQuestionValue 		

  		SupplierRel 
  			one-to-many relation to Supplier
  			Field Mapping uses symbolic key
  				related.SupplierGroup			= SupplierGroup

  		SupplierQuestionsRel 
  			one-to-many relation to SupplierQuestions
  			Field Mapping uses BySupplierGroupQuestion
				related.SupplierGroup			= SupplierGroup
				related.Supplier				= LocalSupplier
				related.SupplierGroupQuestion	= SupplierGroupQuestion

    Sets
    	ByDisplayOrder
    		duplicates
    		Sort Order
    			SupplierGroup
    			SupplierGroupQuestion
    			DisplayOrder
    
    Actions
    	Create is a Create Action
			Field Rules
				DisplayOrder
					autosequence using SupplierGroupQuestion.LastQuestionValueDisplayOrder

    		Action Rules
    			constraint (SupplierGroupQuestion.ResponseType.List)
    				"CanOnlyAddValuesToListTypeQuestions"

    			if (SupplierGroupQuestion.Question entered) 
    				invoke Update SupplierGroupQuestion
    					initialize invoked.Question				
								
			Exit Rules

				if	(SupplierGroup.UseConditionalQuestions)
					for each SupplierRel
						LocalSupplier		= each.Supplier
						if (SupplierQuestionsRel exists)
							invoke Create SupplierQuestionsListValue
								invoked.SupplierGroup  				= SupplierGroup
								invoked.Supplier					= each.Supplier
								invoked.SupplierQuestions			= SupplierQuestionsRel.SupplierQuestions
								invoked.SupplierQuestionsListValue	= SupplierGroupQuestionValue
								fill in fields from SupplierGroupQuestionValue
									except invoked.DisplayOrder

    	CreateDisplayValue is a Create Action
			restricted
    		Action Rules
    			constraint (SupplierGroupQuestion.ResponseType.List)
    				"CanOnlyAddValuesToListTypeQuestions"

			Exit Rules
			
				if (LastDisplayOrder entered)
					if (LastDisplayOrder > SupplierGroupQuestion.LastQuestionValueDisplayOrder)
						invoke Update SupplierGroupQuestion
							invoked.LastQuestionValueDisplayOrder = LastDisplayOrder

    	Update is an Update Action
								
    	Delete is a Delete Action
			Action Rules

    			if (SupplierGroupQuestion.Question entered) 
    				invoke Update SupplierGroupQuestion
    					initialize invoked.Question
