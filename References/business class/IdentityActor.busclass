IdentityActor is a BusinessClass
    owned by security
    prefix is IDACT
    stored in environment
	contains environment data
	disable data area copy
		preserve target data

    Ontology
        symbolic key is IdentityActor

    Patterns
        implements CRUD
        disable AuditIndex

    Transient Fields
    	LastSuccessfulLoginDate is Date
    		default label is "LastSuccessfulLoginOrCreateDate"
    
    Derived Fields
    	ExternalUsersCount is a NativeField
    		type is Numeric size 15
    	
    	ServiceIsRequiredMsg is a MessageField
    		"FieldServiceIsRequired"
    		
    	LastSuccessfulLoginDateIsRequiredMsg is a MessageField
    		"FieldLastSuccessfulLoginOrCreateDateIsRequired"
    		
    	ServiceMsg is a DerivedField
            type is Alpha size 30
            if (Service entered)
                return blank
            else 
            	return ServiceIsRequiredMsg	
        
		LastSuccessfulLoginDateMsg is a DerivedField
            type is Alpha size 60
            if (LastSuccessfulLoginDate entered)
                return blank
            else 
            	return LastSuccessfulLoginDateIsRequiredMsg	
            	        				
    Sets
        BySvcActIdent
            no duplicates
            indexed
            Sort Order
                Service
                Actor
                Identity

        ByActSvcIdent
            no duplicates
            indexed
            Sort Order
                Actor
                Service
                Identity
        
        BySvcIdent
            no duplicates
            Sort Order
                Service
                Identity
                
    Relations
		SvcActorRel 
			one-to-many relation to IdentityActor
			Field Mapping uses ByActSvcIdent
			    related.Actor = Actor 
			    related.Service = Service
				        
	Actions
		Create is a Create Action
			Action Rules
				constraint (SvcActorRel not exist)
					"AnActorCannotHaveMultipleIdentitiesForAService"
												
		Update is an Update Action
		
		Delete is a Delete Action	 
		
		Purge is a Purge Action                
		   		                    		                    		                    	  
		AssignExistingIdentityToActor is a Create Action
			Action Rules
				constraint (SvcActorRel not exist)
					"AnActorCannotHaveMultipleIdentitiesForAService"	
					
		GetExternalUsersCount is an Import Action	
