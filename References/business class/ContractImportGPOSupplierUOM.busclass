ContractImportGPOSupplierUOM is a BusinessClass
	owned by po
	
	prefix is SUUOM
	
	Ontology
		symbolic key is ContractImportGPOSupplierUOM
	
	Patterns
	
	Persistent Fields
		UnitOfMeasure 
			default label is "InternalUnitOfMeasure"
	
	Local Fields 
		LocalContractImportGPOSupplier is a ContractImportGPOSupplier
	
	Derived Fields
	
	Conditions

		InternalUnitOfMeasureEntered 
			when (UnitOfMeasure entered)

		CanUpdateUOMOnContracts 
			when (InternalUnitOfMeasureEntered
			and   ContractImportGPOSupplier.ManufacturerContractsExist)
	
	Relations

		DuplicateInternalUOMRel 
   			one-to-many relation to ContractImportGPOSupplierUOM
			Field Mapping uses symbolic key
				related.SupplierGroup   			= SupplierGroup
				related.ContractImportGPOSupplier   = ContractImportGPOSupplier
			Instance Selection
				where (related.UnitOfMeasure 		 = UnitOfMeasure
				and    related.UniqueID             != UniqueID)		

		NewGPOSupplierUOMRel 
   			one-to-many relation to ContractImportGPOSupplierUOM
			Field Mapping uses symbolic key
				related.SupplierGroup   				= SupplierGroup
				related.ContractImportGPOSupplier   	= LocalContractImportGPOSupplier 
				related.ContractImportGPOSupplierUOM	= ContractImportGPOSupplierUOM 
	
	Sets
	
	Field Rules
	
	Actions
	
		CreateNoEdits is a Create Action 
			restricted 
		
		Create is a Create Action

			Field Rules 

				UnitOfMeasure 
					required
			Action Rules 

				constraint (ContractImportGPOSupplierUOM != UnitOfMeasure)
					"GPOUnitOfMeasureCannotBeTheSameAsInternalUnitOfMeasure"

				if (DuplicateInternalUOMRel exists)
					confirmation required 
						"InternalUnitOfMeasureIsAlreadyMappedFromADifferentGPOUnitOfMeasure;DoYouWantToContinue?"	
			
		Update is an Update Action

			Field Rules 

				UnitOfMeasure 
					required

			Action Rules 

				constraint (ContractImportGPOSupplierUOM != UnitOfMeasure)
					"GPOUnitOfMeasureCannotBeTheSameAsInternalUnitOfMeasure"

				if (DuplicateInternalUOMRel exists)
					confirmation required 
						"InternalUnitOfMeasureIsAlreadyMappedFromADifferentGPOUnitOfMeasure;DoYouWantToContinue?"									
			
		Delete is a Delete Action
			
		UpdateExistingContracts is an Instance Action 
			valid when (CanUpdateUOMOnContracts)
			default label is "UpdateUOMToInternalOnExistingContractsForThisSupplier"
			Action Rules 

				invoke UpdateUOMFromMapping ContractLineImport 
					invoked.NewUOM      		= UnitOfMeasure 
					invoked.CurrentUOM  		= ContractImportGPOSupplierUOM 	 
            		invoked.ParmSupplier        = ContractImportGPOSupplier
					invoked.ParmContractGroup   = SupplierGroup 
		
		CopyToAnotherSupplier is a Set Action
			restricted
			Parameters 
				ParmSupplierGroup   is a SupplierGroup 
				CopyToSupplier		is a ContractImportGPOSupplier 
			 	CopyFromSupplier	is a ContractImportGPOSupplier

			Instance Selection 
				where (ParmSupplierGroup = SupplierGroup
				and    CopyFromSupplier  = ContractImportGPOSupplier)

			Action Rules 

				Instance Rules 

					LocalContractImportGPOSupplier = CopyToSupplier
					if (NewGPOSupplierUOMRel !exists)

						invoke CreateNoEdits ContractImportGPOSupplierUOM
							invoked.SupplierGroup 					= ParmSupplierGroup
							invoked.ContractImportGPOSupplier		= CopyToSupplier
							invoked.ContractImportGPOSupplierUOM	= ContractImportGPOSupplierUOM 
							invoked.UnitOfMeasure                   = UnitOfMeasure   
				



			

			
		
	
