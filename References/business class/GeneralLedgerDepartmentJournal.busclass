GeneralLedgerDepartmentJournal is a BusinessClass
	default label is "GlobalLedgerDepartmentJournal"
    owned by GeneralLedger
	sql name is "GLDepartmentJournal"
    prefix is GLDJR

    Ontology
    	symbolic key is GeneralLedgerDepartmentJournal

    Patterns

    Context Fields

    Persistent Fields
        Status            				is Numeric 1
            States
                Unreleased			value is 0
                Released			value is 1
				Journalized			value is 5
                Deleted				value is 6
    	System							is a GeneralLedgerSystemCode
        TransactionDate
        PostingDate
        Description
        GeneralLedgerEvent
        	default label is "GlobalLedgerEvent"
		Ledger
        Reference
        Currency
		PrimaryLedger					is a Ledger
        JournalAmount					is an InternationalAmount
        	precision is Currency.NumberOfDecimals     
        FinanceCodeBlock
        OriginExpenseCreated			is Boolean
        TransactionTotal				is an InternationalAmount
        	precision is Currency.NumberOfDecimals     

	Transient Fields
        EnteredJournalAmount			is an InternationalAmount
			derive value from JournalAmount		
        
	Local Fields
		AsyncId							is a AsyncActionRequest
        LocalFinanceCodeBlock			is a FinanceCodeBlock
	
	Action Exit Rules

	Rule Blocks
		InitializePrmFields
			initialize PrmEnteredAmount

		UpdateTotals
        	TransactionTotal			+= PrmEnteredAmount
			include InitializePrmFields

		CreateDepartmentTransaction
			if  (FinanceCodeBlock entered
			and !OriginExpenseCreated)
				invoke Create Unreleased GeneralLedgerDepartmentTransaction
					fill in fields from this instance
					invoked.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
					invoked.AccountingEntity				= AccountingEntity
					invoked.GeneralLedgerDepartmentJournal	= GeneralLedgerDepartmentJournal
					invoked.FinanceCodeBlock				= FinanceCodeBlock
					invoked.CurrencyCode					= Currency
					invoked.TransactionAmount				= JournalAmount
					invoked.OriginExpenseTransaction		= true
				OriginExpenseCreated	= true

    Derived Fields
		GlSystemMessage					is a MessageField
			restricted
			"GL"
		DerivedUnapprovedTransactions	is a DerivedField
			type is Numeric 4
			return instance count of GeneralLedgerDepartmentJournal.UnapprovedDepartmentTransactionRel
		BalanceAmount					is a DerivedField
	        type is like InternationalAmount
 	        	precision is Currency.NumberOfDecimals     
        	return (TransactionTotal * -1)
		

  	Field Groups

    Conditions
        AccountingUnitSelected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.AccountingUnit)
        Dimension1Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension1)
        Dimension2Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension2)
        Dimension3Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension3)
        Dimension4Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension4)
        Dimension5Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension5)
        Dimension6Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension6)
        Dimension7Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension7)
        Dimension8Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension8)
        Dimension9Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension9)
        Dimension10Selected
        	restricted
        	when (FinanceEnterpriseGroup.DimensionForDepartmentJournal.Dimension10)
		IsUnreleased
			restricted
			when (Status.Unreleased)
		IsReleased
			restricted
			when (Status.Released)
		IsJournalized
			restricted
			when (Status.Journalized)
		IsDeleted
			restricted
			when (Status.Deleted)
		GLProcessingSystems
			restricted
			when (System 	= "GL")


		CreatorAccess
			restricted
			when (Status.Unreleased
			and   create stamp.actor	= actor)
		ReleaseAccess
			restricted
			when (CreatorAccess
			and   AmountNetToZero)
		UnreleaseAllowed
			when (Status.Released
			and   create stamp.actor	= actor)
		JournalizeAccess
			restricted
			when (Status.Released
			and   create stamp.actor	= actor
			and  !UnprocessedDepartmentTransactionRel exists)
		TransactionsExist
			restricted
			when (GeneralLedgerDepartmentTransaction set exists)
		HasComments
			restricted
			when (GeneralLedgerDepartmentJournalCommentRel exists)
		HasDocuments
			restricted
			when (GeneralLedgerDepartmentJournalDocumentRel exists)
		AmountNetToZero
			restricted
			when (!TransactionTotal entered)
		UnApprovedTransactionsExist
			when (UnapprovedDepartmentTransactionRel exists)

	Relations
		CodeBlockRelationRel
			one-to-one relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.FromField				= 0
				related.ToField					= 1
		CodeBlockRelationDetailRel
			one-to-one relation to CodeBlockRelationDetail
			Field Mapping uses ByKeyValues
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.CodeBlockRelation		= CodeBlockRelationRel.CodeBlockRelation
				related.KeyFromValue			= Ledger
				related.KeyToValue				= AccountingEntity
		GeneralLedgerDepartmentJournalCommentRel 	is a GeneralLedgerDepartmentJournalComment set
		GeneralLedgerDepartmentJournalDocumentRel 	is a GeneralLedgerDepartmentJournalDocument set
		UnprocessedDepartmentTransactionRel 		is a GeneralLedgerDepartmentTransaction set
			Instance Selection
				where (related.Status.Unreleased
				or     related.Status.PendingApproval)
		UnapprovedDepartmentTransactionRel 			is a GeneralLedgerDepartmentTransaction set
			Instance Selection
				where (related.Status.PendingApproval)
		ReleasedDepartmentTransactionRel 			is a GeneralLedgerDepartmentTransaction set
			Instance Selection
				where (related.Status.Released)

    Sets

	Field Rules
		System
			initial value is "GL"
			default to "GL"
			if (!System.RecordType.User)
				constraint (System        = "GL")
					"SystemCanOnlyBe<GlSystemMessage>"
 		AccountingEntity
			if  (AccountingEntity.ValidSystems entered
			and !GLProcessingSystems)
				constraint (System within AccountingEntity.ValidSystems)
					"InvalidSystemFor<FinanceEnterpriseGroup.AccountingEntityLabel>"
		TransactionDate
			initial value is current corporate date
			default to current corporate date
			required
		PostingDate
			initial value is current corporate date
			default to current corporate date
			required
		Description
			if (!Status.Unreleased)
				cannot be changed
			else
				required		
		GeneralLedgerEvent
			initial value is "JE"
			default to "JE"
		Ledger
			initial value is FinanceEnterpriseGroup.CoreLedger
			default to FinanceEnterpriseGroup.CoreLedger
			constraint (CodeBlockRelationDetailRel exists)
				"Invalid<FinanceEnterpriseGroup.LedgerMessage>For<FinanceEnterpriseGroup.AccountingEntityLabel>"
			required
		Currency
			initial value is AccountingEntity.FunctionalCurrency
			default to AccountingEntity.FunctionalCurrency
			if (Currency	!= AccountingEntity.FunctionalCurrency
			or (AccountingEntity.AlternateCurrency entered
			and Currency	!= AccountingEntity.AlternateCurrency)
			or (AccountingEntity.AlternateCurrency2 entered
			and  Currency	!= AccountingEntity.AlternateCurrency2)
			or (AccountingEntity.AlternateCurrency3 entered
			and  Currency	!= AccountingEntity.AlternateCurrency3))
				constraint (FinanceEnterpriseGroup.CurrencyTable entered)
					"CurrencyExchangeTableIsRequiredOnFinanceEnterpriseGroupForCurrencyTransaction"
			required
		PrimaryLedger
			initial value is FinanceEnterpriseGroup.CoreLedger
			force default to Ledger
		FinanceCodeBlock.Ledger
			initial value is FinanceEnterpriseGroup.CoreLedger
			force default to Ledger
		FinanceCodeBlock.ToAccountingEntity
			force default to AccountingEntity
		FinanceCodeBlock
			if  (FinanceCodeBlock.ToAccountingEntity.ValidSystems entered
			and !GLProcessingSystems)
				constraint (System within FinanceCodeBlock.ToAccountingEntity.ValidSystems)
					"InvalidSystemForPosting<FinanceEnterpriseGroup.AccountingEntityLabel>"
			constraint (!FinanceCodeBlock.Ledger.CurrencyLedger)
				"CannotEnterTransactionForCurrencyLedger<FinanceCodeBlock.Ledger.CurrencyLedger>"
			constraint (FinanceCodeBlock.ToAccountingEntity		= AccountingEntity)
				"OriginatingTransactionCannotBeInter<FinanceEnterpriseGroup.AccountingEntityLabel>"
			if (AccountingUnitSelected)
				constraint (FinanceCodeBlock.AccountingUnit entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.AccountingUnitLabel>"
			if (Dimension1Selected)
				constraint (FinanceCodeBlock.FinanceDimension1 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension1Label>"
			if (Dimension2Selected)
				constraint (FinanceCodeBlock.FinanceDimension2 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension2Label>"
			if (Dimension3Selected)
				constraint (FinanceCodeBlock.FinanceDimension3 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension3Label>"
			if (Dimension4Selected)
				constraint (FinanceCodeBlock.FinanceDimension4 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension4Label>"
			if (Dimension5Selected)
				constraint (FinanceCodeBlock.FinanceDimension5 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension5Label>"
			if (Dimension6Selected)
				constraint (FinanceCodeBlock.FinanceDimension6 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension6Label>"
			if (Dimension7Selected)
				constraint (FinanceCodeBlock.FinanceDimension7 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension7Label>"
			if (Dimension8Selected)
				constraint (FinanceCodeBlock.FinanceDimension8 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension8Label>"
			if (Dimension9Selected)
				constraint (FinanceCodeBlock.FinanceDimension9 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension9Label>"
			if (Dimension10Selected)
				constraint (FinanceCodeBlock.FinanceDimension10 entered)
					"Required;FinanceStructure<FinanceEnterpriseGroup.FinanceDimension10Label>"
			required
		EnteredJournalAmount
			JournalAmount			= EnteredJournalAmount
			constraint (JournalAmount	< 0)
				"JournalAmountMustBeACredit"
			required
			
	Actions
		UpdateJournalTotals is an Instance Action
			restricted
			Parameters
				PrmEnteredAmount			is an InternationalAmount

			Action Rules
				include UpdateTotals

	StateCycles
		DepartmentJournalLifeCycle is a StateCycle
			state field is Status
			
			Unreleased		is a State
				Create is a Create Action
					completion message is "DepartmentJournalSucessfullyCreated"
					Action Rules
		 			Exit Rules
						include CreateDepartmentTransaction

				Update is an Update Action
					valid when (CreatorAccess)
					Entrance Rules
						constraint (CreatorAccess)
							"CannotMakeChanges;UnauthorisedUser"

					Action Rules
		 			Exit Rules
						include CreateDepartmentTransaction

				NoRulesUpdate is an Update Action
					restricted
					valid when (CreatorAccess)

				Delete is an Instance Action
					valid when (CreatorAccess)
					default label is "MoveToDeleted"
					Entrance Rules
						constraint (CreatorAccess)
							"CannotMoveToDeleted;UnauthorisedUser"
					Action Rules
						invoke PurgeForDepartmentJournal GeneralLedgerDepartmentTransaction in background
							invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
							invoked.PrmAccountingEntity				= AccountingEntity
							invoked.PrmDepartmentJournal			= GeneralLedgerDepartmentJournal
						make transition to Deleted

				Release is an Instance Action
					completion message is "JournalReleaseComplete"
					valid when (ReleaseAccess)
					Entrance Rules
						constraint (ReleaseAccess)
							"CannotRelease;UnauthorisedUser"
							
					Action Rules
						invoke ReleaseFromJournal Unreleased GeneralLedgerDepartmentTransaction set
						make transition to Released

					Exit Rules

            Released		is a State

				Unrelease is an Instance Action
					valid when (UnreleaseAllowed)

					Entrance Rules
						constraint (UnreleaseAllowed)
							"CannotUnrelease;UnauthorisedUser"

					Action Rules
						invoke Unrelease Released ReleasedDepartmentTransactionRel
						invoke Unrelease PendingApproval UnapprovedDepartmentTransactionRel
						make transition to Unreleased

				Journalize is an Instance Action
					completion message is "JournalizeComplete"
					valid when (JournalizeAccess)
					Entrance Rules
						constraint (AmountNetToZero)
							"CannotJournalize;JournalNotInBalance"

					Action Rules

						if (last GeneralLedgerDepartmentTransaction set.GeneralLedgerDepartmentTransaction > 100)
							invoke CreateSystemJournalTransaction GeneralLedgerDepartmentTransaction in background
								run after AsyncId
								assign async action request id to AsyncId
								invoked.PrmEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity		= AccountingEntity
								invoked.PrmDepartmentJournal	= GeneralLedgerDepartmentJournal
						else
							invoke CreateSystemJournalTransaction GeneralLedgerDepartmentTransaction in foreground
								invoked.PrmEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity		= AccountingEntity
								invoked.PrmDepartmentJournal	= GeneralLedgerDepartmentJournal

						if (last GeneralLedgerDepartmentTransaction set.GeneralLedgerDepartmentTransaction > 100)
							invoke CreateGLTransactionDetail GeneralLedgerDepartmentTransaction in background
								run after AsyncId
								assign async action request id to AsyncId
								invoked.PrmEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity		= AccountingEntity
								invoked.PrmDepartmentJournal	= GeneralLedgerDepartmentJournal
						else
							invoke CreateGLTransactionDetail GeneralLedgerDepartmentTransaction in foreground
								invoked.PrmEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity		= AccountingEntity
								invoked.PrmDepartmentJournal	= GeneralLedgerDepartmentJournal

						if (last GeneralLedgerDepartmentTransaction set.GeneralLedgerDepartmentTransaction > 100)
							invoke JournalizeTransactions  GLTransactionDetail in background
								run after AsyncId
								invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.PrmJournalizeGroup	= "DEPARTMENTJOURNAL:"+AccountingEntity+":"+GeneralLedgerDepartmentJournal
								invoked.PrmAccountingEntity	= AccountingEntity
						else
							invoke JournalizeTransactions  GLTransactionDetail in foreground
								invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.PrmJournalizeGroup	= "DEPARTMENTJOURNAL:"+AccountingEntity+":"+GeneralLedgerDepartmentJournal
								invoked.PrmAccountingEntity	= AccountingEntity

						make transition to Journalized
		
			Deleted 		is a State

            Journalized		is a State
