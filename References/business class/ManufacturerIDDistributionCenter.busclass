ManufacturerIDDistributionCenter is a BusinessClass
    owned by ic
    prefix is MFD
    sql name is MIDDistributionCenter
    classic name is ICMFGDIST

    Ontology
        symbolic key is ManufacturerIDDistributionCenter
            classic set name is MFDSET1
            classic name for ManufacturerIDDistributionCenter.InventoryLocation is LOCATION
            classic name for ManufacturerID is MANUF-ID

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        ManufacturerCrossReference is AlphaUpper size 15
            classic name is MANUF-DIST-ID
        PostalAddress				is a PostalAddressV2	
        	holds pii
            classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
            classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
            classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
            classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
            classic name for PostalAddress.Municipality is CITY-ADDR5
            classic name for PostalAddress.StateProvince is STATE-PROV
            classic name for PostalAddress.Country is COUNTRY-CODE


	Derived Fields
		DefaultManufacturerCrossRef 		is a StringField
			type is AlphaUpper size 15
			restricted
			ManufacturerIDDistributionCenter.Company
			""
			ManufacturerIDDistributionCenter.InventoryLocation
			
	Relations
	
		OtherDistributionCentersForManufacturerIDRel
			one-to-many relation to ManufacturerIDDistributionCenter
			Field Mapping uses symbolic key
				related.ItemGroup											= ItemGroup
				related.ManufacturerID										= ManufacturerID
			Instance Selection
				where (related.ManufacturerIDDistributionCenter				!= ManufacturerIDDistributionCenter)

	Field Rules

		ManufacturerIDDistributionCenter
			constraint (ManufacturerIDDistributionCenter.Company entered)
				"CompanyIsRequired"                                        
			constraint (ManufacturerIDDistributionCenter.InventoryLocation entered)
				"LocationIsRequired"                                       
			constraint (ManufacturerIDDistributionCenter.InventoryLocation.LocationType.InventoryLocation)
				"LocationMustBeOfType:_\Inventory_\Location"
			if (ManufacturerIDDistributionCenter.Name not entered)
				ManufacturerIDDistributionCenter.Name = ManufacturerIDDistributionCenter.InventoryLocation.Name
				
		ManufacturerCrossReference
			default to DefaultManufacturerCrossRef

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action
        	Action Rules
	        	constraint (OtherDistributionCentersForManufacturerIDRel exist)
	        			"CannotDeleteLast_\Manufacturer_\Id_\Distribution_\Center"

		RestrictedDelete is a Delete Action
			restricted
        
