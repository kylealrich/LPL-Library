VatReportingLine is a BusinessClass
    owned by tx
    prefix is TVL
    classic name is TXVATRPTLN

    Ontology
        symbolic key is VatReportingLine
            classic set name is TVLSET1
            classic name is VAT-SEQ-NBR
            classic name for VatReportIDForCountry.VatReportID is VAT-RPT-ID

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        VATBoxNumber              is a VatBoxNbr
            classic name is VAT-BOX-NBR
        Description
        Source			          is AlphaUpper size 1
	        States
	            Input         value is "I"	
	            Output        value is "O"	
            
        TaxUsageIndicator         is AlphaUpper size 1
            classic name is TAX-USG-IND
            States
                Sum       		value is "S"
                	default label is "Summarize"
                TaxCode   		value is "T"
                UsageCode 		value is "U"
                WithholdingCode value is "W"
        InvoicedOrCreditMemo      is AlphaUpper size 1
            classic name is INV-CRD-IND
            States
                Both    value is "B"
                Credit  value is "C"
                Invoice value is "I"
        GoodsAndServicesIndicator is AlphaUpper size 1
            classic name is GDS-SVCS-IND
            States
                Both     value is "B"
                Services value is "S"
                Goods    value is "G"
        ValueAddedTaxType         is AlphaUpper size 1
            classic name is VAT-TYPE
            States
                Domestic value is "D"
                IntraEec value is "E"
                    default label is "Intra EEC"
                Export   value is "X"
                Import   value is "I"
        CumulateTax               is Boolean
            classic name is CALC-TAX-IND
        CumulateBaseTax           is Boolean
            classic name is CALC-BASE-IND
        PrintDetail               is Boolean
            classic name is PRINT-DTL-IND
        Status                    is AlphaUpper size 1
            States
                Active   value is "A"
                Inactive value is "I"
		TotalTaxAmount			is an InternationalAmount		
			restricted
		TotalAdjustmentAmount	is an InternationalAmount		
			restricted
		CalculationLineOnly		is Boolean						
			restricted
		VATReverseIndicator 	is Boolean					



	Transient Fields
		VatYear							is Year
		VatPeriod						is Numeric 2
		
	Local Fields
		LocalVatYear					is Year						
		LocalVatPeriod					is Numeric 2				
		SelectTaxCode					is like TaxCode 			
		LocalThisRptLine				is like VatReportingLine	
		LocalVatUsageCode				is like TaxCode
		LocalVatSeqNbr					is like VatReportingLine 

		LocalVatReportingLine   		is like VatReportingLine
		LocalLineBoxNumber    			is a VatBoxNbr
		LocalTotalTaxAmount	  			is a InternationalAmount	
		LocalCopyFromVatReportingLine	is a VatReportingLine
		LocalNewVatReportingLineKey		is a VatReportingLine
		LocalNewVatReportIDForCountry   is like VatReportIDForCountry
		LocalCompany					is like GeneralLedgerCompany
		
	Derived Fields

		DerivedTotalAdjustmentAmount is a DerivedField
	    	type is Decimal 8.3
	    	return TotalAdjustmentAmount 
	    	
	    DerivedLastBoxNumber is a DerivedField	
			type is like VatBoxNbr
			return VatReportIDForCountry.GetLastVatReportingLine + 1   
			
		DerivedGlobalWithholding is a DerivedField
			type is Boolean
			for each GeneralLedgerCompanyRel
				LocalCompany = each.Company
				if (PayablesCompanyRel.GlobalWithholding)
					return true
			return false
														
    Relations

        VatReportingCodesRel
            classic name is VATRPT-CODES
            one-to-many relation to VatUsageCode
            delete cascades
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup						= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity									= TaxEntity 	
            Instance Selection
                where (related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                and   related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
                and   related.VatUsageCode.VatReportingLine 		= VatReportingLine)

        VatReportingTaxCodesRel
            one-to-many relation to VatUsageCode
            delete cascades
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup						= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity									= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 			= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 			= VatReportIDForCountry.CountryCode
			Instance Selection
                where (related.VatReportingLine 		  			= VatReportingCodesRel.VatReportingLine)
                




                
        VatEuropeanSalesCodesRel
            classic name is VATSALES-CODES
            one-to-many relation to VatEuropeanSalesCode
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup	= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity				= TaxEntity 	            
            Instance Selection
                where (related.VatReportIDForCountry.VatReportID = VatReportIDForCountry.VatReportID
                and   related.VatReportIDForCountry.CountryCode = VatReportIDForCountry.CountryCode)
                                  
        VatReportingDetailRel
            classic name is TXVATRPTDT
            one-to-many relation to VatReportingDetail
            delete cascades
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup			  = TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity						  = TaxEntity 	            
                related.VatReportIDForCountry.VatReportID = VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode = VatReportIDForCountry.CountryCode
                related.VatReportingLine   			  	  = VatReportingLine







       	VatReportingLineTaxCodesRel
            one-to-many relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
            Instance Selection
                where (related.TaxUsageIndicator  			= "T")

        VatReportingLineTaxUsageCodesRel
            one-to-many relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
            Instance Selection
                where (related.TaxUsageIndicator  			= "U")
                
        VatReportingLineWithholdingCodeRel
            one-to-many relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
            Instance Selection
                where (related.TaxUsageIndicator  			= "W")
                
        VatReportingTaxUsageCodesRel
            one-to-many relation to VatUsageCode
            delete cascades
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
			Instance Selection
                where (related.VatReportingLine 		  	= VatReportingLineTaxUsageCodesRel.VatReportingLine)




                








                

      	TaxTransactionsForLineRel	 
            one-to-many relation to TaxTransaction
            valid when (UseTaxCodes)
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup				= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup 
                related.TaxEntity 							= VatReportIDForCountry.TaxEntity
   			Instance Selection
                where (related.TaxCode						= SelectTaxCode)	

      	TaxTransactionsForLineUsageCodesRel	 
            one-to-many relation to TaxTransaction
            valid when (UseTaxUsageCodes)
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup				= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup 
                related.TaxEntity 							= VatReportIDForCountry.TaxEntity
   			Instance Selection
                where (related.TaxUsageCode entered
                and    related.TaxUsageCode			   	 	= VatReportingCodesRel.VatUsageCode.VatCode)

		AdjustVatBoxesRel is a VatLineAdjustment set		











      	TaxTransactionsForLineRel2	 	
            one-to-many relation to TaxTransaction
            valid when (UseTaxCodes)
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup		= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup 
                related.TaxEntity 					= VatReportIDForCountry.TaxEntity
   			Instance Selection

                where ((related.TaxCode				= VatReportingCodesRel.VatUsageCode.VatCode	 				
				and     related.TransactionSource 	= Source
				and   ((InvoicedOrCreditMemo not entered
				or     (InvoicedOrCreditMemo.Both
				or     (InvoicedOrCreditMemo entered
				and     related.InvoicedOrCreditMemo = InvoicedOrCreditMemo)))))) 

      	TaxTransactionsForLineRel3	 	
            one-to-many relation to TaxTransaction
            valid when (UseTaxCodes)
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup		= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup 
                related.TaxEntity 					= VatReportIDForCountry.TaxEntity
   			Instance Selection
   			 	where (VatReportingLine = this instance.VatReportingLine
                and   ((related.TaxCode				= any VatReportingCodesRel.VatUsageCode.VatCode	 				
				and     related.TransactionSource 	= Source
				and   ((InvoicedOrCreditMemo not entered
				or     (InvoicedOrCreditMemo.Both
				or     (InvoicedOrCreditMemo entered
				and     related.InvoicedOrCreditMemo = InvoicedOrCreditMemo))))))) 

		PayablesInvoiceWithholdingRel
			one-to-many relation to PayablesInvoiceWithholding
			valid when (UseWithholdingCodes)
			Field Mapping uses ByReportableRecords
				related.Reportable				= true
			Instance Selection
				where (related.Company					= any GeneralLedgerCompanyRel.Company
				and    related.IncomeWithholdingCode	= any VatReportingCodesRel.VatUsageCode.VatCode)

     	EntityTaxCodeForVatRel		 
            one-to-one relation to EntityTaxCode
          	Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
                related.TaxEntity 							= TaxEntity
                related.TaxCode 							= SelectTaxCode				
      	VatReportingSummaryCodesRel
            one-to-many relation to VatReportingDetail

            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID  
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode   
                related.VatReportingLine 					= VatReportingLine
      		Instance Selection
                where (related.VATBoxNumber					= VATBoxNumber)     


       	VatReportingLineIsSummaryRelxyz
            one-to-many relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
            Instance Selection
                where (related.TaxUsageIndicator  			= "S")

        VatReportingDtlThatIsOnASummaryxyz
            one-to-many relation to VatReportingDetail

            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup			  = TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity						  = TaxEntity 	            
                related.VatReportIDForCountry.VatReportID = VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode = VatReportIDForCountry.CountryCode
                related.VatReportingLine   			  	  = VatReportingLineIsSummaryRelxyz.VatReportingLine


 
        GetVatRptLineTotalRelxyz
            one-to-one relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
                related.VatReportingLine   					= LocalThisRptLine
                               
        VatUsageCodesRelxyz	
            one-to-many relation to VatUsageCode
            delete cascades
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
			Instance Selection
                where (related.VatReportingLine 		  	= VatReportingLineIsSummaryRelxyz.VatReportingLine)                

        VatUsageCodesRel
            one-to-many relation to VatUsageCode
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode


			Instance Selection
                where (related.VatReportingLine	  			= LocalVatSeqNbr)

       	NewVatUsageCodesRel
            one-to-many relation to VatUsageCode
            valid when (SummarizedCodes)
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
                related.VatReportingLine	  				= VatReportingLine
			Instance Selection
                where (related.VatReportingLine	  			= LocalVatUsageCode)                


                                                                                               


      	AdjustByThisVatReportingLineRel
            one-to-many relation to VatReportingLine

            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity							= TaxEntity 	
                related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID  
                related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode   
                related.VatReportingLine 					= LocalVatReportingLine
      		Instance Selection
                where (related.VATBoxNumber					= LocalLineBoxNumber)  

		CopyFromVatReportingLineRel			
            one-to-many relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup	= LocalCopyFromVatReportingLine.TaxEntity.FinanceEnterpriseGroup
    			related.TaxEntity				= LocalCopyFromVatReportingLine.TaxEntity 	

            Instance Selection
                where (related.VatReportIDForCountry.VatReportID = LocalCopyFromVatReportingLine.VatReportIDForCountry.VatReportID
                and   related.VatReportIDForCountry.CountryCode  = LocalCopyFromVatReportingLine.VatReportIDForCountry.CountryCode
                and   related.VatReportingLine					 = LocalCopyFromVatReportingLine)


		NewVatReportingLineRel			
            one-to-many relation to VatReportingLine
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup	= LocalNewVatReportingLineKey.FinanceEnterpriseGroup
    			related.TaxEntity				= LocalNewVatReportingLineKey.TaxEntity 	

            Instance Selection
                where (related.VatReportIDForCountry.VatReportID = LocalNewVatReportingLineKey.VatReportIDForCountry.VatReportID
                and   related.VatReportIDForCountry.CountryCode  = LocalNewVatReportingLineKey.VatReportIDForCountry.CountryCode
                and   related.VatReportingLine					 = LocalNewVatReportingLineKey)
                
        VatUsageCodeRel
            classic name is VATRPT-CODES
            one-to-many relation to VatUsageCode
            delete cascades
            Field Mapping uses symbolic key
		    	related.FinanceEnterpriseGroup						= TaxEntity.FinanceEnterpriseGroup
   				related.TaxEntity									= TaxEntity 	
            Instance Selection
                where (related.VatReportIDForCountry.VatReportID 	= VatReportIDForCountry.VatReportID
                and   related.VatReportIDForCountry.CountryCode 	= VatReportIDForCountry.CountryCode
                and   related.VatUsageCode.VatReportingLine 		= VatReportingLine)
                
		VendorGroupGlobalWithholdingRel
			one-to-many relation to VendorGroup
			Field Mapping uses symbolic key
			Instance Selection
				where (related.VendorGroup.FinanceEnterpriseGroup				= TaxEntity.FinanceEnterpriseGroup
				and    related.GlobalWithholding)	
				
		GeneralLedgerCompanyRel
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses ByAccountingEntity
				related.FinanceEnterpriseGroup	= VatReportIDForCountry.TaxEntity.FinanceEnterpriseGroup
                related.AccountingEntity		= VatReportIDForCountry.TaxEntity
                		
		PayablesCompanyRel
			one-to-one relation to PayablesCompany
			Field Mapping uses symbolic key
				related.Company		= LocalCompany
                				                

    Conditions

		UseTaxCodes
			restricted
			when (TaxUsageIndicator = "T")

		UseTaxUsageCodes
			restricted
			when (TaxUsageIndicator = "U")
	
		UseWithholdingCodes
			restricted
			when (TaxUsageIndicator = "W")

		SummarizedCodes
			restricted
			when (TaxUsageIndicator = "S")
		TotalAdjustmentAmountEntered
			restricted
			when (TotalAdjustmentAmount entered)
		SummarizeHasVatCodesAdded
			restricted
			when (TaxUsageIndicator = "S"
			and   VatReportingCodesRel exist)		

    Field Rules

        VATBoxNumber
            required

        Status
            default to "A"

        TaxUsageIndicator
            default to "T"
            if (TaxUsageIndicator.UsageCode)
				constraint (GoodsAndServicesIndicator not entered)					
					"GoodsAndServicesMustBeBlankForPayablesProcessing"	 				


				constraint (InvoicedOrCreditMemo = blank
				or			InvoicedOrCreditMemo.Both)					
					"Invoice\CreditIndicatorMustBeBlankOrBoth"	 						
			if (TaxUsageIndicator.WithholdingCode)
				constraint (DerivedGlobalWithholding)
					"GlobalWithholdingMustBeEnabledOnPayablesCompaniesRelatedToTaxEntity"

		ValueAddedTaxType					
			if (ValueAddedTaxType.Import)
				constraint (Source.Input)					
					"V\A\T_TypeCannot=ImportForAccountsRecievable(Source=Output)"			
			if (Source.Input)
				if (TaxUsageIndicator.TaxCode)
					constraint (!ValueAddedTaxType.Export)					
						"V\A\T_TypeCannot=ExportForPayablesWhenUsingTaxTableCodes"			

            if (TaxUsageIndicator.UsageCode)
				constraint (ValueAddedTaxType not entered)					
					"V\A\T_TypeMustBeBlankOrBothIfUsingTaxUsage=UsageCodeForPayables"	
					
			constraint (!TaxUsageIndicator.Sum)												
				"ValueAddedTaxTypeMustBeBlankWhenTaxUsage=Summarize"	 							
					
		Source
			if (TaxUsageIndicator.UsageCode												
			or	TaxUsageIndicator.TaxCode)
				required		
					"SourceRequiredWhenTaxUsage=TaxCodeOrUsageCode"	 						

			constraint (!TaxUsageIndicator.Sum)												
				"SourceMustBeBlankWhenTaxUsage=Summarize"	 								
					
		InvoicedOrCreditMemo
			if (TaxUsageIndicator.UsageCode												
			or	TaxUsageIndicator.TaxCode)
				required		
					"InvoiceOrCreditMemoIndicatorRequiredWhenTaxUsage=TaxCodeOrUsageCode"	 						
			constraint (!TaxUsageIndicator.Sum)												
				"InvoiceOrCreditMemoIndicatorMustBeBlankWhenTaxUsage=Summarize"	 								
																							
		PrintDetail
			if (TaxUsageIndicator.UsageCode												
			or	TaxUsageIndicator.TaxCode
			or  TaxUsageIndicator.WithholdingCode)
				required		
					"Print_\DetailRequiredWhenTaxUsage=TaxCodeOrUsageCodeOrWithholdingCode"	 				
			constraint (!TaxUsageIndicator.Sum)												
				"PrintDetailMustBeBlankWhenTaxUsage=Summarize"	 							

		CumulateTax
			if (TaxUsageIndicator.WithholdingCode)
				required		
					"CumulateTaxRequiredWhenTaxUsage=WithholdingCode"	 				
								

		GoodsAndServicesIndicator
			constraint (!TaxUsageIndicator.Sum)												
				"GoodsAndServicesIndicatorMustBeBlankWhenTaxUsage=Summarize"	 			




	Sets	
		ByBoxNumber   
			duplicates
			indexed
			Sort Order
				FinanceEnterpriseGroup
				TaxEntity
				VatReportIDForCountry
				VATBoxNumber
				VatReportingLine

	Rule Blocks
		GetVatTaxTransactions	
			for each TaxTransactionsForLineRel



				if (TaxUsageIndicator 			= "T"
				and each.TaxCode 				= SelectTaxCode
				or (TaxUsageIndicator 			= "U"
				and each.TaxUsageCode 			= SelectTaxCode))


					if (each.TaxPointDate year 		= LocalVatYear
					and each.TaxPointDate month 	= LocalVatPeriod
					and each.TransactionSource  	= Source)

						if (InvoicedOrCreditMemo = "B"

						or  InvoicedOrCreditMemo = each.InvoicedOrCreditMemo)

							TotalTaxAmount 			= TotalTaxAmount + each.TransactionTaxAmount
				else
				if (TaxUsageIndicator 			= "S")



					display "[TLC]GetVatTaxTransactions-Summarize"	
					if (VatReportingSummaryCodesRel exist)
						for each VatReportingSummaryCodesRel 
							TotalTaxAmount = each.SummarizedBaseTaxAmt	

		DoSummeryLines

			for each VatReportingDetailRel
				LocalThisRptLine = each.VatReportingLine
				LocalVatSeqNbr   = each.VatReportingDetail.VATSummarySequense
				if (each.DerivedTaxUsageFlag = "S")

					invoke Delete VatUsageCodesRel	 
					if (VatUsageCodesRel exist											
					and NewVatUsageCodesRel not exist)									
						for each VatUsageCodesRel
							invoke Create VatUsageCode	 
								fill in fields from each								  
									except invoked.VatReportingLine	
								invoked.VatReportingLine		= VatReportingLine		 	
								invoked.VatUsageCode.VatCode	= each.VatUsageCode.VatCode										
																		                 							

	Actions

        Create is a Create Action

   		Update is an Update Action
			Entrance Rules
				if (TaxUsageIndicator changed)												
				    if (old TaxUsageIndicator = "S")
				    	if (VatReportingDetailRel exist) 
							confirmation required
								"Warning!UsageOptionChanged;SummaryCodesWillBeDeleted.Proceed?"

		                    invoke Delete VatReportingDetailRel 
					else
						if (VatReportingCodesRel exist)
							confirmation required
								"Warning!UsageOptionChanged;CodesWillBeDeleted.Proceed?"	
		            		invoke Delete VatReportingCodesRel
	            							                    						
        Delete is a Delete Action
			Entrance Rules
			    if (UseTaxCodes
			    or	UseTaxUsageCodes)
            		invoke Delete VatReportingCodesRel
			    if (SummarizedCodes)
                    invoke Delete VatReportingDetailRel      
                if (AdjustVatBoxesRel exist)				 
                	invoke Delete AdjustVatBoxesRel         







		ClearBoxTotals is an Instance Action		

	 		default label is untranslatable
			restricted		
			Action Rules
				initialize TotalTaxAmount
											                                                             

		AddupBoxTotals is an Instance Action		

	 		default label is untranslatable
			restricted		

			Parameters
				PrmVatYear			is Year
				PrmVatPeriod		is Numeric 2		 
			Local Fields
			Parameter Rules
				PrmVatYear
					initial value is "2015"		
				PrmVatPeriod
					initial value is "01"		
			Action Rules
				VatYear						 = PrmVatYear
				LocalVatYear				 = VatYear
				VatPeriod					 = PrmVatPeriod
				LocalVatPeriod				 = VatPeriod
				if (TaxUsageIndicator = "S")				
					display "LoadTheseTaxCodeToThisLine"
					include	DoSummeryLines					
				initialize TotalTaxAmount
				for each VatReportingCodesRel
					SelectTaxCode 			= each.VatUsageCode.VatCode
					if (EntityTaxCodeForVatRel.GoodsAndServicesIndicator = GoodsAndServicesIndicator
					or  GoodsAndServicesIndicator not entered) 
						include GetVatTaxTransactions

		LoadVatUsageCodesToSummary is an Instance Action		

	 		default label is untranslatable
			restricted		


			valid when (SummarizedCodes)
			Action Rules
				for each VatReportingDetailRel
					LocalThisRptLine = each.VatReportingLine
					LocalVatSeqNbr   = each.VatReportingDetail.VATSummarySequense
					if (each.DerivedTaxUsageFlag = "T"
					or  each.DerivedTaxUsageFlag = "U")

						invoke Delete VatUsageCodesRel	
						if (VatUsageCodesRel exist											
						and NewVatUsageCodesRel not exist)									
							for each VatUsageCodesRel
								invoke Create VatUsageCode	 
									fill in fields from each								  
										except invoked.VatReportingLine	
									invoked.VatReportingLine		= VatReportingLine		 	
									invoked.VatUsageCode.VatCode	= each.VatUsageCode.VatCode												

		AddToSummaryBoxTotal is an Instance Action	 			

	 		default label is untranslatable
			restricted		

			valid when (SummarizedCodes)



			Action Rules
				initialize TotalTaxAmount
				for each VatReportingDetailRel	 							 
		            if (each.EffectOnVatBox.SubtractFromVatBox)
		            	TotalTaxAmount -= each.DerivedLineTaxTotal

		            else

		        		TotalTaxAmount += each.DerivedLineTaxTotal	            				

		AdjustedBoxTotalsAmt is an Instance Action		
	 		default label is untranslatable
			restricted									


			Parameters
				PrmAdjustTotalTax is Boolean			
			Local Fields
				LocalEffectOnVatBox 		is AlphaUpper size 1
		            States
		                AddTo           value is "+"
		                SubtractFrom    value is "-"
		                NoEffectOn   	value is blank	
		        LocalAdjustedTaxTotal is an InternationalAmount      				 
			Action Rules
				initialize TotalAdjustmentAmount
				initialize LocalTotalTaxAmount
				if (AdjustVatBoxesRel exist)
					for each AdjustVatBoxesRel	
						LocalEffectOnVatBox = each.EffectOnVatBox	
			        	LocalTotalTaxAmount = each.DerivedAdjByAmount
			        	if (LocalEffectOnVatBox.AddTo)
			        		TotalAdjustmentAmount += LocalTotalTaxAmount
			            if (LocalEffectOnVatBox.SubtractFrom)
			            	TotalAdjustmentAmount -= LocalTotalTaxAmount								

















				if (PrmAdjustTotalTax)	
					LocalAdjustedTaxTotal = TotalTaxAmount + TotalAdjustmentAmount
					TotalTaxAmount = LocalAdjustedTaxTotal	
				
		UpdateAdjustedBoxTotalsAmt is an Instance Action	
	 		default label is untranslatable
			restricted										
			Parameters

			Local Fields		 
			Action Rules
				if (AdjustVatBoxesRel exist)
					for each AdjustVatBoxesRel	
						initialize each.AdjustByAmount
						for each AdjustVatBoxesRel				
							each.AdjustByAmount = each.DerivedAdjByAmount	


		CopyCurrentRecord is an Instance Action		


	 		Parameters
	 			FromTaxEntity				is a TaxEntity 
	 			FromVatReportIDForCountry   is a VatReportIDForCountry	 		
	 			CopyFromVatReportingLine	is like VatReportingLine	 
	 			NewTaxEntity				is like TaxEntity 
	 			NewVatReportIDForCountry    is a VatReportIDForCountry	
	 			NewVatReportingLineKey		is like VatReportingLine
	 				default label is "NewVatReportingLine#"
	 			NewVatBoxNumber				is like VatBoxNbr	 
				CopyVatUsageCode			is Boolean
					default label is "CopyAllLinesOfCodes"	
				CopyVatReportingDetail		is Boolean					
					default label is "CopyAllLinesOfSummarize"	

	 		Parameter Rules
	 			FromTaxEntity
	 				initial value is TaxEntity
	 			FromVatReportIDForCountry
	 				initial value is VatReportIDForCountry	 		
	 			NewTaxEntity
	 				initial value is TaxEntity
	 			NewVatReportIDForCountry
	 				initial value is VatReportIDForCountry
	 			CopyFromVatReportingLine
	 				initial value is VatReportingLine
				NewVatReportingLineKey
					initial value is VatReportIDForCountry.GetLastVatReportingLine + 1 
					required
				NewVatBoxNumber
					initial value is DerivedLastBoxNumber	
	 		Action Rules
	 			LocalCopyFromVatReportingLine = CopyFromVatReportingLine
	 			LocalNewVatReportIDForCountry = FromVatReportIDForCountry	
	 			if (CopyFromVatReportingLineRel exist)	
	 				invoke Create VatReportingLine
	 					fill in fields from this instance
	 					invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup	
	 					invoked.TaxEntity				= NewTaxEntity 				
	 					invoked.VatReportIDForCountry	= NewVatReportIDForCountry  
						if (NewVatReportingLineKey entered)
							LocalNewVatReportingLineKey	= NewVatReportingLineKey 	
	 						invoked.VatReportingLine 	= NewVatReportingLineKey
	 						invoked.VATBoxNumber		= NewVatBoxNumber

					if (CopyVatUsageCode
				    and CopyFromVatReportingLineRel exist
				    and NewVatReportingLineRel exist)
						for each CopyFromVatReportingLineRel
							for each each.VatUsageCodeRel
								invoke Create VatUsageCode
									fill in fields from each
										except invoked.VatReportingLine	
									invoked.VatReportingLine		= NewVatReportingLineKey		 	
									invoked.VatUsageCode.VatCode	= each.VatUsageCode.VatCode	


					if (CopyVatReportingDetail
				    and CopyFromVatReportingLineRel exist
				    and NewVatReportingLineRel exist)
						for each CopyFromVatReportingLineRel 
							for each each.VatReportingDetailRel
								invoke Create VatReportingDetail
									fill in fields from each					 
										except invoked.VatReportingLine	
									invoked.VatReportingLine		= NewVatReportingLineKey	
									










									

		CopyVatReportingLineDetails is an Instance Action	

	 		default label is untranslatable
	 		restricted		 
	 		Parameters
	 			NewTaxEntity				is like TaxEntity 
	 			NewVatReportIDForCountry    is a VatReportIDForCountry	
				CopyVatUsageCode			is Boolean
				CopyVatReportingDetail		is Boolean					 

	 		Parameter Rules
	 		Action Rules
	 			LocalNewVatReportIDForCountry = NewVatReportIDForCountry	 
				if (CopyVatUsageCode
				and VatUsageCodeRel exist)					 
					for each VatUsageCodeRel
						invoke Create VatUsageCode
							fill in fields from each
		 					invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup	 
		 					invoked.TaxEntity				= NewTaxEntity 				 
		 					invoked.VatReportIDForCountry	= NewVatReportIDForCountry							

				if (CopyVatReportingDetail
				and VatReportingDetailRel exist)
					for each VatReportingDetailRel
						invoke Create VatReportingDetail
							fill in fields from each					 
		 					invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup	 
		 					invoked.TaxEntity				= NewTaxEntity 				 
		 					invoked.VatReportIDForCountry	= NewVatReportIDForCountry						
		 					








