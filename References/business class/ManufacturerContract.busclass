ManufacturerContract is a BusinessClass
    owned by po
    prefix is MFGC
    
    Ontology
  		symbolic key is ManufacturerContract

    Persistent Fields
    	ManufacturerContractNumber		is a Contract
    	
    Local Fields
        DistributorContract 	  	 		is like Contract 
        DistributorVendor					is a Vendor
        MfgVendorItem                		is AlphaUpper size 32
		LocalFromCreateFull          		is Boolean
		LocalFromCreateLines         		is Boolean
		LocalContract                       is like Contract 
		LocalOriginalManufacturerContract 	is a Contract	
		LocalCloseExistingLines             is Boolean 	

	Transient Fields
		FromContractDelete           			is Boolean
		TransientOriginalManufacturerContract 	is a Contract
		TransientCloseExistingLines             is Boolean 

	Derived Fields 
		MemberPivotCount is a ComputeField
			type is Numeric 6
			restricted
			(instance count of MfgContractMemberPivotForDistRel)

		PricingMemberCount is a ComputeField 
			type is Numeric 6
			restricted 
			(instance count of MfgPricingRel) 
	
	Sets
		ByPivotId
			no duplicates
      		Sort Order
           		ContractGroup
           		Contract
            	ManufacturerContractNumber
		ByMfgNbr
      		Sort Order
           		ContractGroup
            	ManufacturerContractNumber
            	Contract
            	ManufacturerContract
            	
    Conditions
					
		MembersToAdd 
			restricted 
			when (MemberPivotCount > PricingMemberCount)
		
		ContractInProgress
			restricted
			when (Contract.InProgress)
			
		ContractHasVendor
			restricted
			when (Contract.Vendor entered)
		
		ContractActive
			restricted
			when (Contract.ContractStatus.Active)
		
		ManufacturerContractClosed
			restricted
			when (ManufacturerContractNumber.ContractStatus.Closed)
		
		CanMaintainManufacturerContract
			restricted
			when (!Contract.ContractStatus.Closed
			and  !ManufacturerContractClosed)
		
		CanAddAllLines
			restricted
			when (CanMaintainManufacturerContract
			and   SomeLinesNotSelected
			and  !HasGPODistributorContract)

		CanAddAllMembers
			restricted
			when (CanMaintainManufacturerContract
			and   MembersToAdd)
	
		NoDateOverlap
			restricted
  			when ((MfgContractRel.ExpirationDate > 0
  			and    DistContractRel.EffectiveDate > 0
  			and    MfgContractRel.ExpirationDate < DistContractRel.EffectiveDate)
  			or    (MfgContractRel.EffectiveDate > 0
  			and    DistContractRel.ExpirationDate > 0 
  			and    MfgContractRel.EffectiveDate  > DistContractRel.ExpirationDate))
  			
  		HasLines
  			restricted
  			when (MfgContractLineRel exists)
  			
		HasOtherManufacturerContract
			restricted
			when (OtherManufacturerContractRel exists)

  		MissingPricingIdentifier
  			restricted
  			when (MfgPricingRel.PricingIdentifier !entered)
  		
  		HasMembers
  			restricted
  			when (MfgPricingRel exists)	
  			
  		CanReleaseMembers
  			restricted
  			when (UnreleasedDistributorPricingRel exists
  			and   HasLines
  			and   !MfgContractRel.HasMembersToRelease
  			and   !Contract.ContractStatus.Closed)
  		
  		NoLinesWithOrders
  			restricted
  			when (MfgContractLineOrderedRel !exists)

		SomeLinesNotSelected 
			restricted
			when (NotOnDistContractRel exists)

		SomeMembersNotSelected
			restricted
			when (MemberNotOnDistContractRel exists)

		HasGPODistributorContract
			restricted
			when (ContractImportDistributorRel exists)

		HasGPODistributorContractMissingLines
			restricted 
			when (ContractImportDistributorMissingLinesRel exists)

		CanMaintainManufacturerContractNoGPOContract
			restricted 
			when (CanMaintainManufacturerContract
			and  !HasGPODistributorContract)
  			
	Relations
		MfgContractLineRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgContractLine
				related.ContractGroup 		 = ContractGroup
				related.Contract      		 = Contract            
            	related.ManufacturerContract = ManufacturerContractNumber
		
		MfgContractLineOrderedRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgContractLine
				related.ContractGroup 		 = ContractGroup
				related.Contract      		 = Contract            
            	related.ManufacturerContract = ManufacturerContractNumber
            Instance Selection
            	where (related.AmountOrdered > 0)	
            	
		MfgPricingRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup 				= ContractGroup
				related.Contract      				= Contract            
			Instance Selection
            	where (related.ContractDistributorPricingMember.ManufacturerContract = ManufacturerContractNumber)
            	
		ContractImportDistributorRel 
			one-to-one relation to ContractImportDistributor 
			Field Mapping uses ByDistributorContractNumber 
				related.ContractGroup				= ContractGroup 
				related.DistributorContractNumber   = Contract 
				related.ContractImport              = ManufacturerContractNumber.DerivedGPOContract 
				related.ContractImportDistributor	= Contract.Supplier 

		ContractImportDistributorMissingLinesRel 
			one-to-many relation to ContractImportDistributor 
			Field Mapping uses ByDistributorContractNumber 
				related.ContractGroup				= ContractGroup 
				related.DistributorContractNumber   = Contract
				related.ContractImport              = ManufacturerContractNumber.DerivedGPOContract 
				related.ContractImportDistributor	= Contract.Supplier 
			Instance Selection 
				where (related.HasMissingDistributorContractLines)

		DistributorContractLinesMissingRel 	
			one-to-many relation to ContractLineImportDistributor 
			Field Mapping uses BySupplier 
				related.ContractGroup 				= ContractGroup 
				related.ContractImport              = ManufacturerContractNumber.DerivedGPOContract 
				related.DistributorSupplier	    	= Contract.Supplier 
			Instance Selection 
				where (related.MissingDistributorContractLine
				and    related.ContractImportDistributorRel.DistributorContractNumber = Contract)

		UnreleasedDistributorPricingRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses ByUnReleased
				related.ContractGroup 				= ContractGroup
				related.Contract      				= Contract            
			Instance Selection
            	where (related.ContractDistributorPricingMember.ManufacturerContract = ManufacturerContractNumber)

		DistributorPricingWithUnreleasedManufMemberRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup 				= ContractGroup
				related.Contract      				= Contract            
			Instance Selection
            	where (related.ContractDistributorPricingMember.ManufacturerContract = ManufacturerContractNumber
				and    related.ManufacturerMemberNotReleased)

		MfgContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup 				= ContractGroup
				related.Contract      				= ManufacturerContractNumber
				
		DistContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup				= ContractGroup
				related.Contract                    = Contract
				
    	MfgContractMemberPivotRel
    		one-to-many relation to MfgContractMemberPivot
            Field Mapping uses symbolic key 
          		related.ContractGroup				= ContractGroup
           		related.ManufacturerContractPivot	= ManufacturerContractNumber
           		
    	MfgContractMemberPivotForDistRel
    		one-to-many relation to MfgContractMemberPivot
            Field Mapping uses symbolic key 
          		related.ContractGroup				= ContractGroup
           		related.ManufacturerContractPivot	= ManufacturerContractNumber
			Instance Selection 
				where (related.DistributorMembers)

		MemberNotOnDistContractRel
    		one-to-many relation to MfgContractMemberPivot
            Field Mapping uses symbolic key 
          		related.ContractGroup				= ContractGroup
           		related.ManufacturerContractPivot	= ManufacturerContractNumber
			Instance Selection
				where (related.ContextContractMemberNotSelected)

    	MfgContractLinePivotRel
    		one-to-many relation to MfgContractLinePivot
            Field Mapping uses symbolic key 
          		related.ContractGroup				= ContractGroup
           		related.ManufacturerContractPivot	= ManufacturerContractNumber
           	Instance Selection 
           		where (related.MfgContractLineRel.CreateDistributorContractLine
				and    related.MfgContractLineRel.LineNotClosed
           		and   (related.MfgContractLineRel.ErrorsExist = false
           		or     related.MfgContractLineRel.InactiveVendorItemOnly))
           						
		NotOnDistContractRel
			one-to-many relation to MfgContractLinePivot
            Field Mapping uses symbolic key 
          		related.ContractGroup				= ContractGroup
           		related.ManufacturerContractPivot	= ManufacturerContractNumber
           	Instance Selection 
           		where (related.MfgContractLineRel.CreateDistributorContractLine
				and    related.MfgContractLineRel.LineNotClosed
           		and   (related.MfgContractLineRel.ErrorsExist = false
           		or     related.MfgContractLineRel.InactiveVendorItemOnly)
				and    related.ContextContractLineNotSelected)

		OtherManufacturerContractRel
			one-to-many relation to ManufacturerContract
			Field Mapping uses symbolic key
				related.ContractGroup                = ContractGroup
				related.Contract                     = Contract
			Instance Selection
				where (related.ManufacturerContract  != ManufacturerContract)
		
		ManufacturerContractExistsRel 
			one-to-many relation to ManufacturerContract 
			Field Mapping uses ByMfgNbr
				related.ContractGroup    			= ContractGroup 
				related.ManufacturerContractNumber  = ManufacturerContractNumber
				related.Contract                    = LocalContract 
		
		NewItemmastFromPoitemvenRel			
        	one-to-many relation to VendorItem
        	Field Mapping uses Set3
        		related.ProcurementGroup	= ContractGroup
        		related.Vendor				= DistributorVendor
        		related.VendorItem			= MfgVendorItem
        		
  	Field Rules
  			
  	
  	Actions     	 	
 		Create is a Create Action
 			restricted
 		    Action Rules
 		    	constraint (!NoDateOverlap)
					"CannotAttachManufacturerContract;TheDateRangeForTheManufacturerContractIsCompletelyOutsideTheDateRangeForTheDistributorContract"
 		
 		CreateFull is a Create Action 
			restricted 

			Action Rules 

			Exit Rules 
			
				LocalFromCreateFull 				= true 
				LocalOriginalManufacturerContract   = TransientOriginalManufacturerContract
				LocalCloseExistingLines             = TransientCloseExistingLines
				invoke AddAllMembersToDistributor
		
		Update is an Update Action
 			restricted
 		    Action Rules
	            		
 		Delete is a Delete Action
 			Entrance Rules

				if (!FromContractDelete)
					invoke DeleteAllDistributorContractLinesForAManufacturer ContractLine
						invoked.PrmContractGroup 			= ContractGroup
						invoked.PrmContract                 = Contract
						invoked.PrmManufacturerContract	    = ManufacturerContractNumber
					
				for each MfgPricingRel
					invoke DeleteFromManufacturerContract each 	 				
 			
		DeleteNoRules is a Delete Action
			restricted
		
		ReleaseMembers is an Instance Action
			valid when (CanReleaseMembers)
			Action Rules
				constraint (!MissingPricingIdentifier)
					"MustEnterAPricingIdentifierForAllMembersForTheManufacturerContract"
				
				constraint (DistributorPricingWithUnreleasedManufMemberRel !exists)
					"CannotRelease;AssociatedManufacturerContractHasUnreleasedMembers"
				
				constraint (Contract.Vendor entered)
					"AVendorMustBeAssociatedWithTheContractSupplierForAContractUsedForPurchasing"					

				invoke BatchReleaseMembersArray ContractLine
					invoked.PrmContractGroup				= ContractGroup
					invoked.PrmContract						= Contract			
					invoked.PrmManufacturerContract         = ManufacturerContractNumber
		
		CheckForErrorsOnContract is an Instance Action 
			restricted
			Action Rules
				invoke CheckForContractLineErrors Contract
		
		
		AddAllLinesToDistributorFromCopy is an Instance Action 
			restricted
			Parameters 
				PrmCloseExistingLines	is Boolean 
			Action Rules 
				LocalFromCreateLines = true 
				TransientCloseExistingLines = PrmCloseExistingLines 
				invoke AddAllLinesToDistributor


		AddAllLinesToDistributor is an Instance Action
			valid when (CanAddAllLines)
			completion message is "AddingLinesToDistributorContract<Contract>HasStarted;Check'MyActions'ForCompletion"
			
			Action Rules
				
				if (ManufacturerContractNumber.ContractLineErrorsExist
				and !LocalFromCreateFull
				and !LocalFromCreateLines)
					confirmation required
						"SomeDistributorContractLinesWillNotBeCreatedBecauseOfErrorsOnManufacturerContractLines.DoYouWantToContinue?"
				
				constraint (MfgPricingRel exists)
					"MustAddMembersToContractBeforeAddingLines;ReleaseMembersWillRunAutomatically"

				if (!LocalFromCreateFull
				and !LocalFromCreateLines)
					constraint (Contract.Vendor entered)
						"AVendorMustBeAssociatedWithTheContractSupplierForAContractUsedForPurchasing"	

					constraint (!MissingPricingIdentifier)
						"MustEnterAPricingIdentifierForAllMembersForTheManufacturerContractBeforeAddingLines;ReleaseMembersWillRunAutomatically"

					constraint (!MfgContractRel.HasMembersToRelease)
						"ManufacturerContractHasMembersThatNeedToBeReleased;ReleaseMembersWillRunAutomaticallyForDistributorContractWhenAddAllLinesIsPerformed"

				DistributorContract = Contract
	        	DistributorVendor	= Contract.Vendor
	        		
				invoke CreateDistributorContractLines ContractLine
	        		invoked.PrmContractGroup		= ContractGroup
	        		invoked.PrmManufacturerContract	= ManufacturerContractNumber
	        		invoked.PrmDistributorContract	= Contract
					if (LocalFromCreateFull
					or  LocalFromCreateLines)
						invoked.PrmFromCopy         = true 
					if (DistributorPricingWithUnreleasedManufMemberRel exists
					or  LocalFromCreateFull
					or  LocalFromCreateLines)
						invoked.PrmReleaseMembers   = false
					else	
						invoked.PrmReleaseMembers   = true
					invoked.PrmCloseExistingLines   = TransientCloseExistingLines
	        		
	    AddMissingLinesToDistributor is an Instance Action 
			valid when (HasGPODistributorContractMissingLines)
			completion message is "AddingLinesToDistributorContract<Contract>HasStarted;Check'MyActions'ForCompletion"			
		
			Action Rules 
			
				invoke AddMissingDistributorContractLines ContractImportDistributorRel

		CopyOrMoveToAnotherDistributorContract is an Instance Action 
			valid when (HasOtherManufacturerContract)
			Parameters 
				CopyOrMoveToContract	is a Contract 
				CopyOrMove              is Numeric 1 
					States 
						Copy            value is 1
						Move            value is 2
			
			Parameter Rules 
				CopyOrMoveToContract
					required 
					constraint (Contract != CopyOrMoveToContract)
						"CannotCopyOrMoveToTheSameContract"
					constraint (CopyOrMoveToContract.IsDistributorContract)
						"MustMoveToADistributorContract"
					constraint (CopyOrMoveToContract.NotClosed)
						"CannotCopyOrMoveToAClosedContract"

			Action Rules 
				LocalContract			= CopyOrMoveToContract
				constraint (ManufacturerContractExistsRel !exists)
					"ManufacturerContractAlreadyExistsOnCopyOrMoveToContract"

				constraint (CopyOrMove > 0)
					"MustSelectEitherCopyOrMove"

				invoke CreateFull 
					invoked.ContractGroup				= ContractGroup 
					invoked.Contract        			= CopyOrMoveToContract
					invoked.ManufacturerContractNumber	= ManufacturerContractNumber

				if (CopyOrMove = 2)
					invoke Delete 

				invoke CreateRelatedContract RelatedContract
					invoked.ContractGroup	  		   = ContractGroup
					invoked.Contract		  		   = CopyOrMoveToContract
					invoked.RelatedContractID 		   = Contract
					invoked.RelatedContractType		   = 6
					invoked.RelatedSupplierName 	   = Contract.Supplier.SupplierName
					invoked.ManufacturerCodeDivision   = Contract.ManufacturerCodeDivision
					invoked.Supplier				   = Contract.Supplier
					invoked.Vendor					   = Contract.Vendor
					invoked.RelatedContractDescription = Contract.Description				

				invoke CreateRelatedContract RelatedContract
					invoked.ContractGroup	  		   = ContractGroup
					invoked.Contract		  		   = Contract
					invoked.RelatedContractID          = CopyOrMoveToContract
					invoked.RelatedContractType		   = 16
					invoked.RelatedSupplierName 	   = CopyOrMoveToContract.Supplier.SupplierName
					invoked.ManufacturerCodeDivision   = CopyOrMoveToContract.ManufacturerCodeDivision
					invoked.Supplier				   = CopyOrMoveToContract.Supplier
					invoked.Vendor					   = CopyOrMoveToContract.Vendor
					invoked.RelatedContractDescription = CopyOrMoveToContract.Description

		AddAllMembersToDistributor is an Instance Action
			valid when (CanAddAllMembers)
			completion message is "AddingMembersToDistributorContract<Contract>HasStarted;Check'MyActions'ForCompletion"
			Action Rules
				
				invoke CreateDistributorPricingMembers MfgContractMemberPivot
	        		invoked.PrmContractGroup				= ContractGroup
	        		invoked.PrmManufacturerContract			= ManufacturerContractNumber
	        		invoked.PrmDistributorContract			= Contract
					invoked.FromCreateFull          		= LocalFromCreateFull
					invoked.OriginalManufacturerContract	= LocalOriginalManufacturerContract
					invoked.PrmCloseExistingLines           = LocalCloseExistingLines
	        		
		Purge is a Purge Action
			restricted
			
		
