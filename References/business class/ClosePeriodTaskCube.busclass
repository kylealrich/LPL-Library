ClosePeriodTaskCube is a BusinessClass
	owned by closemgmt
	prefix is CPTCD

	Ontology
		symbolic key is ClosePeriodTaskCube

    Patterns
		implements AnalyticCube
			disable continuous update
		
	Persistent Fields
		ParentTask							is a ClosePeriodTask
		TaskName							is Alpha 100
		TaskCompany							is a CompanyGroupField
		FinanceTeam
		TaskOwner 							is a FinanceTeamMember
			delete ignored
		Priority							is Numeric 1
			States
				Low							value is 0
				Medium						value is 1
				High						value is 2
		Risk								is Numeric 1
			States	
				Low							value is 0
				Medium						value is 1
				High						value is 2
		RequireSupportingDoco				is Boolean
		RequireJournalEntry                 is Boolean
		RequiresApproval					is Boolean
		TaskStatus							is Numeric 1
			States	
				Scheduled					value is 0
				InProcess					value is 2
				PendingApproval				value is 5
				Closed						value is 7
				Voided						value is 9		
		ScheduleForTask        		 		is a CloseTaskScheduleAndLOE
		ActualForTask						is a CloseTaskScheduleAndLOE
		EstimateForTask						is a CloseTaskScheduleAndLOE
		StartMetric							is Numeric 1
			States
				StartFuture					value is 0		
				StartedEarly				value is 1		
				StartedOnTime				value is 2		
				StartedLate					value is 3		
		DueMetric							is Numeric 1
			States
				DueFuture 					value is 0
				CompletedEarly				value is 1
				CompletedOnTime 			value is 2
				IsLate 						value is 3
				CompletedLate				value is 4
		EffortMetric						is Numeric 1						
			States		
				NotEntered					value is 0
				FutureTask 					value is 1
				LessThanExpected 			value is 2
				AsExpected 					value is 3
				MoreThanExpected 			value is 4
		OneTimeTask							is Boolean
       	PrimaryTaskType						is a CloseTaskType
       	SecondaryTaskType					is a CloseTaskType
       	TertiaryTaskType					is a CloseTaskType
       	DayOfClose							
		BeginTimeSensitive					is Boolean
		DueTimeSensitive					is Boolean
				
	Dimensions

		FinanceTeam	
		TaskOwner

		DayOfClose
		PrimaryTaskType
		SecondaryTaskType
		TertiaryTaskType		
		ReportingPeriod
			is a monthly period dimension with year of ClosePeriod.Year
				current year is CurrentOpenPeriodYear   
				current period is CurrentOpenPeriodPeriod
		TaskStatus






	Measures
		Count



		ClosedCount
		VoidedCount
		OverdueCount
		DueTodayCount
		OpenCount




	Local Fields

	Field Rules
				
	Derived Fields
	
		Count is a DerivedField
			type is Numeric 6
			return 1
	
		ScheduledCount is a DerivedField
			type is Numeric 6
			restricted
			if (TaskStatus.Scheduled)
				return 1
				
		InProcessCount is a DerivedField
			type is Numeric 6
			restricted
			if (TaskStatus.InProcess)
				return 1
				
		PendingApprovalCount is a DerivedField
			type is Numeric 6
			restricted
			if (TaskStatus.PendingApproval)
				return 1
				
		ClosedCount is a DerivedField
			type is Numeric 6
			restricted
			if (TaskStatus.Closed)
				return 1
				
		VoidedCount is a DerivedField
			type is Numeric 6
			restricted
			if (TaskStatus.Voided)
				return 1
		
		OverdueCount is a DerivedField
			type is Numeric 6
			if (Overdue)
				return 1

		DueTodayCount is a DerivedField
			type is Numeric 6
			if (DueToday)
				return 1

		OpenCount is a DerivedField
			type is Numeric 6
			if (Open)
				return 1

		PercentComplete is a ComputeField
			type is Percent 5.2
			((ClosedCount + VoidedCount) / Count)
				
		ReportingPeriod is a DerivedField
			type is Numeric 2
			restricted
			return ClosePeriod.Period
		
		CurrentOpenPeriodPeriod is a DerivedField
			type is Numeric 2
			restricted
			return (first OpenClosePeriodRel.ClosePeriod.Period)
			
		CurrentOpenPeriodYear is a DerivedField
			type is Numeric 4
			restricted
			return (first OpenClosePeriodRel.ClosePeriod.Year)

		DisplayDueOrCloseDate is a ConditionalField
			type is Date
			if (TaskStatus.Closed or TaskStatus.Voided)
				ActualForTask.DueDate
			else
				ScheduleForTask.DueDate
				



			
		DisplayAssignedTo is a ConditionalField
			type is Alpha size 200
			if (!TaskOwner entered)
				FinanceTeam.Description
			else
				TaskOwner.TeamMember.PreferredSimplePresentationName

		DisplayDueOrCloseTime is a ConditionalField
			type is Time
			if (TaskStatus.Closed or TaskStatus.Voided)
				ActualForTask.DueTime
			else
				ScheduleForTask.DueTime
				
	Conditions
		OpenClosePeriod
			restricted
			when (ClosePeriod.CloseManagementStatus.Open)

		Overdue
			restricted
			when ((TaskStatus < TaskStatus.Closed)
			and   ((ScheduleForTask.DueDate < current corporate date)
			or     (DueTimeSensitive
			and    (ScheduleForTask.DueDate = current corporate date)
			and    (ScheduleForTask.DueTime < current corporate time))))

		DueToday
			restricted
			when ((TaskStatus < 7) and (ScheduleForTask.DueDate = current corporate date))

		Open
			restricted
			when (TaskStatus < 7)

		HasTaskTypes
			restricted
			when (CloseManagementGroup.HasTaskTypes)
		
	Relations

		OpenClosePeriodRel
			one-to-many relation to ClosePeriod
			Field Mapping uses ByOpenPeriod
				related.CloseManagementGroup = CloseManagementGroup


	Actions	
		Create is a Create Action
			restricted
		
			Exit Rules
				if (OneTimeTask and (CloseManagementGroup.AutoUpdateAnalyticCubeFile and CloseManagementGroup.AutoRefreshReloadAnalyticCube))
					invoke Refresh CloseManagementGroup.ClosePeriodTaskAnalyticCubeRel in background

		Update is an Update Action
			restricted
			
			Exit Rules
				if (CloseManagementGroup.AutoUpdateAnalyticCubeFile and CloseManagementGroup.AutoRefreshReloadAnalyticCube)
					invoke Refresh CloseManagementGroup.ClosePeriodTaskAnalyticCubeRel in background
				
		Delete is a Delete Action
			restricted

		QuickUpdateNoRefresh is an Update Action
			restricted
			bypass field rules
