ContractSubcontractor is a BusinessClass
	owned by po
	prefix is CSUB

	Ontology
		symbolic key is ContractSubcontractor

	Patterns

	Persistent Fields
		PayablesDiversityCode          
		PaymentPercentage 			is a Percent
		LienReleaseRequired 		is Boolean
		LienReleaseCompleted		is Boolean
		Subcontractor
		SubcontractorCommodityCode
		CommittedAmount				is an InternationalAmount
		EffectiveDate               is a snapshot of Contract.EffectiveDate
		
	Context Fields
		EffectiveDateRange          is a DateRange
	
	Local Fields
		LocalDiversityCode              			is a PayablesDiversityCode
	
	Derived Fields
		DerivedTotalAmountPaid is a DerivedField
			type is like InternationalAmount
			return (sum ContractSubcontractorPayment set.PaymentAmount)
		
		DerivedTotalPercentComplete is a ComputeField
			type is Percent size 6.3
			(DerivedTotalAmountPaid / DisplayCommittedAmount)
			
		RemainingPaymentPercentAlert is a ComputeField
			type is Percent size 6.3
			restricted
			(DerivedCommittedPercent - DerivedTotalPercentComplete)
			
		SubName is a StringField
			type is Alpha 30
			default label is "Name"
			Subcontractor.SubcontractorName
		
		DerivedCommittedPercent is a ComputeField
			type is Percent 6.3
			restricted
			(CommittedAmount / Contract.ProposedTotalContractAmount)
			
		DerivedCommittedAmount is a DerivedField
			type is like InternationalAmount
			restricted
			return (PaymentPercentage * Contract.ProposedTotalContractAmount)
			
		DerivedRemainingAmount is a DerivedField
			type is like InternationalAmount
			if (DerivedTotalAmountPaid < DisplayCommittedAmount)
				return (DisplayCommittedAmount - DerivedTotalAmountPaid)
			else
				return 0
			
		DerivedRemainingPercent is a ConditionalField
			type is Percent 6.3
			if (DerivedRemainingAmount != 0)
				(DerivedRemainingAmount / DisplayCommittedAmount)
			else
				0
			
		DisplayCommittedAmount is a DerivedField
			type is like InternationalAmount
			if (CommittedAmount entered)
				return CommittedAmount
			else
				return DerivedCommittedAmount
				
		DisplayCommittedPercent is a ConditionalField
			type is Percent 6.3
			if (PaymentPercentage entered)
				PaymentPercentage
			else
				DerivedCommittedPercent
				
		DerivedDiversityPctOfContract is a ComputeField
			type is Percent 5.2
			(DisplayCommittedAmount / Contract.DerivedDiversityAmtFromPct)
			
		DerivedIsText is a MessageField
			"_is"
			
		DerivedOfText is a MessageField
			"_of"
			
		DerivedSupplierContactName is a StringField
		    type is Alpha size 101
		    restricted
		    actor.agent(SupplierSourceId).SupplierSourceId.MainContact.FirstAndLastName
		    
		DerivedSupplierName is a StringField
			type is Alpha size 30
			restricted
			actor.agent(SupplierSourceId).Supplier.SupplierName

	Conditions
		ClosedContract
			restricted
			when(Contract.ContractStatus.Closed)
		ValidForDelete
			restricted
			when (!ClosedContract
			and   !ContractSubcontractorPaymentRels exists)
		SubcontractorHasDiversity
			restricted
			when (ContractSubcontractorDiversityRel exists)
		SubcontractorHasDiversities
			restricted
			when (AnyContractSubcontractorDiversityRel exists)
		SubcontractorHasCommittedAmount
			restricted
			when (DisplayCommittedAmount entered)
		ContractAndSubcontractorDiversity
			restricted
			when (Contract.DiversityPctOfProposedTotal !=0
			and SubcontractorHasDiversity)
		PaymentOwedAlert
			restricted
			when (DerivedRemainingAmount !=0)
		ProposedTotalContractAmountEntered
			restricted
			when (Contract.ProposedTotalContractAmount entered)
		SupplierEmailFound
			restricted
			when (actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress entered)
		EffectiveDateWithinRange
    		restricted
    		when (EffectiveDate within EffectiveDateRange)

	Relations
		ContractSubcontractorPaymentRels is a ContractSubcontractorPayment set
		ContractSubcontractorCommentRels is a ContractSubcontractorComment set
		SupplierRels is a Supplier set
		
		SubcontractorsRel
			one-to-many relation to Subcontractor
			Field Mapping uses symbolic key
				related.SupplierGroup	= ContractGroup
 
		SubcontractorRel
			one-to-one relation to Subcontractor
			Field Mapping uses symbolic key
				related.SupplierGroup	= ContractGroup
				related.Subcontractor	= Subcontractor
				
		SupplierGroupExtensionRel
			one-to-one relation to SupplierGroupExtension
			Field Mapping uses symbolic key
				related.SupplierGroup	= ContractGroup
				
		VendorGroupRel
			one-to-one relation to VendorGroup
			Field Mapping uses symbolic key
				related.VendorGroup              = ContractGroup
		
		ContractSubcontractorDiversityRel
			one-to-one relation to ContractSubcontractorDiversity
			Field Mapping uses symbolic key
				related.ContractGroup      		= ContractGroup
				related.Contract           		= Contract
				related.ContractSubcontractor   = ContractSubcontractor
				related.PayablesDiversityCode   = LocalDiversityCode
		
		AnyContractSubcontractorDiversityRel
			one-to-many relation to ContractSubcontractorDiversity
			delete cascades
			Field Mapping uses symbolic key
				related.ContractGroup      		= ContractGroup
				related.Contract           		= Contract
				related.ContractSubcontractor   = ContractSubcontractor
		
		SubcontractorDiversitiesRel
			one-to-many relation to SubcontractorDiversity
			Field Mapping uses BySubcontractor
				related.SupplierGroup			= ContractGroup
				related.Subcontractor			= Subcontractor
		
		SubcontractorDiversityRel
			one-to-one relation to SubcontractorDiversity
			Field Mapping uses BySubcontractor
				related.SupplierGroup			= ContractGroup
				related.Subcontractor			= Subcontractor
				related.PayablesDiversityCode 	= PayablesDiversityCode
				
		SubcontractorDiversitySelectionRel
			one-to-many relation to SubcontractorDiversity
			Field Mapping uses BySubcontractor
				related.SupplierGroup      = ContractGroup
				related.Subcontractor  	   = Subcontractor
			Instance Selection
				where (((related.EffectiveDate  <= Contract.ExpirationDate
				and    related.ExpirationDate >= Contract.EffectiveDate)
				or     VendorGroupRel.DiversityValidWhenWithinDates = false)
				and    related.CanUseForDocument = true)
				
	Sets
		BySubcontractor
			duplicates
			Sort Order
				Subcontractor.SubcontractorName
				Subcontractor
				Contract
				ContractGroup
		
		ByContractOnly
			duplicates
			Sort Order
				Contract
				Subcontractor
				ContractGroup
		
		ByDiversity
			duplicates
			Sort Order
				PayablesDiversityCode
				
	Field Rules
		
		PaymentPercentage
			constraint (Contract.ProposedTotalContractAmount !=0)
				"MustHaveAProposedTotalContractAmountToEnterACommittedPercent"
			constraint (PaymentPercentage >= 0%)
				"CommittedPercentCannotBeNegative"
				
		Subcontractor
			cannot be changed
				"CannotChangeSubcontractor"
				
		CommittedAmount
			constraint (Contract.ProposedTotalContractAmount !=0)
				"MustHaveAProposedTotalContractAmountToEnterACommittedAmount"
			constraint (!PaymentPercentage entered)
				"EnterACommittedAmountOrPercent,NotBoth"
			constraint (CommittedAmount >= 0)
				"CommittedAmountCannotBeNegative"
	
	Actions 
		Create is a Create Action
			restricted
			
			Exit Rules

				if (SubcontractorDiversitySelectionRel exists)
					for each SubcontractorDiversitySelectionRel
						LocalDiversityCode = each.PayablesDiversityCode
						if (ContractSubcontractorDiversityRel !exists)
							invoke Create ContractSubcontractorDiversity
								invoked.ContractGroup         = ContractGroup
								invoked.Contract              = Contract
								invoked.ContractSubcontractor = ContractSubcontractor
								invoked.PayablesDiversityCode = each.PayablesDiversityCode 

		Update is an Update Action
			valid when (!ClosedContract)
			Action Rules
				if (CommittedAmount changed
				or PaymentPercentage changed)
					if (DisplayCommittedAmount > Contract.ProposedTotalContractAmount)
						confirmation required
							"Warning:CommittedAmountToSubcontractorIsGreaterThanTheProposedContractAmount;DoYouWantToContinue?"
			
		SupplierUpdate is an Update Action
			valid when (!ClosedContract)
			default label is "Update"
			
			Action Rules
				if (CommittedAmount changed
				or PaymentPercentage changed)
					if (DisplayCommittedAmount > Contract.ProposedTotalContractAmount)
						confirmation required
							"Warning:CommittedAmountToSubcontractorIsGreaterThanTheProposedContractAmount;DoYouWantToContinue?"
			
			Exit Rules
				if (actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress entered)
					if (ContractGroup.SupplierGroupRel.EmailOnContractSubcontractorUpdate)
						send email
							to Contract.PrimaryContactRel.DerivedEmail
							from Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
							cc actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress
							cc Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
							subject "<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailSubject>"
							Contents 
								"<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailContent>"
								"MaintainedBy<DerivedSupplierContactName>Of<DerivedSupplierName>"
								if (SubcontractorCommodityCode changed
								and old SubcontractorCommodityCode !entered)
									"CommodityCodeOf<SubcontractorCommodityCode>HasBeenAdded"
								else
								if (SubcontractorCommodityCode changed
								and SubcontractorCommodityCode !entered)
									"CommodityCode<old SubcontractorCommodityCode>HasBeenRemoved"
								else
								if (SubcontractorCommodityCode changed)
									"CommodityCodeHasBeenUpdatedFrom<old SubcontractorCommodityCode>To<SubcontractorCommodityCode>"
									
								if (CommittedAmount changed)
									"CommittedAmountHasBeenUpdatedFrom<old CommittedAmount>To<CommittedAmount>"
								if (PaymentPercentage changed)
									"CommittedPercentHasBeenUpdatedFrom<old PaymentPercentage>To<PaymentPercentage>"
								if (LienReleaseRequired changed)
									"LienReleaseRequiredHasBeenUpdatedFrom<old LienReleaseRequired>To<LienReleaseRequired>"
								if (LienReleaseCompleted changed)
									"LienReleaseCompletedHasBeenUpdatedFrom<old LienReleaseCompleted>To<LienReleaseCompleted>"
			
		Delete is a Delete Action
			valid when (ValidForDelete)

		SupplierDelete is a Delete Action
			valid when (ValidForDelete)
			default label is "Delete"
			
			Exit Rules
				if (ContractGroup.SupplierGroupRel.EmailOnContractSubcontractorUpdate)
					send email
						to Contract.PrimaryContactRel.DerivedEmail
						from Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
						cc actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress
						cc Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
						subject "<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailSubject>"
						Contents 
							"<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailContent>"
							"SubcontractorHasBeenDeletedBy<DerivedSupplierContactName>Of<DerivedSupplierName>"
			
