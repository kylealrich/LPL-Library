FinanceDimension4Structure is a BusinessClass
	owned by GeneralLedger
	
	prefix is FD4St
	
	Ontology
		symbolic key is FinanceDimension4Structure
		
	Patterns
	
    Persistent Fields
    	Description
		DimensionNode				is a FinanceDimension4
			delete ignored
		StructureSequence			is Numeric 4
		IsEnterpriseStructure		is Boolean
		UnusedDimensionStructure	is Boolean
			restricted
		Active				

   	Context Fields
		FinanceDimension4
		
    Derived Fields
		TopNodeString	    is a StringField
			type is AlphaUpper 15
			restricted
			StructureSequence
			"_TOP_NODE"

		DerivedOldPrefix	is a StringField
			type is AlphaUpper 15
			restricted
			StructureSequence
			"_"
			
		DerivedNewPrefix    is a StringField
			type is AlphaUpper 15
			restricted
			LocalNewStructureSequence
			"_"
			
		DerivedNewDimension   is a DerivedField
			type is like FinanceDimension4
			restricted
			I1 = DerivedOldPrefix size
			I1 += 1
			DerivedNewDimension = DerivedNewPrefix + LocalCopyDimension[I1:15]

		DerivedSystemDimension is a DerivedField
			type is like FinanceDimension4
			if (SystemFinanceDimension4Rel exists)
				return SystemFinanceDimension4Rel.FinanceDimension4
			else
				return UndefinedMsg
									
		UndefinedMsg is a LabelField
			restricted
			"NotDefined"

		InactiveMF is a MessageField			
			restricted
			"Inactive"

		ActiveMF is a MessageField
			restricted
			"Active"
						
		StatusMessage is a ConditionalField
			type is Alpha size 20
			if (Active)
				ActiveMF
			else
				InactiveMF

		IsEnterpriseStructureMF is a MessageField
			restricted
			"Enterprise_Structure"
						
		IsEnterpriseStructureMessage is a ConditionalField
			type is Alpha size 20
			if (IsEnterpriseStructure)
				IsEnterpriseStructureMF
			else
				blank

	Local Fields
		LocalNewStructure	   		is a FinanceDimension4Structure
		LocalCopyDimension        	is a FinanceDimension4
		LocalNewDimension         	is a FinanceDimension4
		LocalNewStructureSequence   is Numeric 4
		LocalParentDimension	    is a FinanceDimension4
		LocalSubordinateDimension 	is like FinanceDimension4
		I1					    	is Numeric 2		
 		LocalDimensionNode			is like FinanceDimension4
		InvokedByUnusedStructure
		LocalRunGroup				is AlphaUpper 30
		LocalCounter				is Numeric 3	
		 
	Relations
		StructureDimensionRel
			one-to-many relation to FinanceDimension4
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.FinanceDimension4	!= StructureDimensionHierarchyRel.FinanceDimension4)
		StructureDimensionHierarchyRel 
			one-to-many relation to FinanceDimension4Hierarchy
			Field Mapping uses ByParentDimension
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.ParentDimension				= DimensionNode
		DimensionsInStructureRel
			one-to-many relation to FinanceDimension4
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.DimensionStructure			= FinanceDimension4Structure
		EnterpriseDimensionStructureRel
			one-to-many relation to FinanceDimension4Structure
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.IsEnterpriseStructure
			    and    related.UniqueID != UniqueID)
		NotInDimensionStructureRel
			one-to-many relation to FinanceDimension4
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.NotUsedInHierarchy)
		SystemFinanceDimension4Rel
			one-to-one relation to FinanceDimension4Hierarchy
			Field Mapping uses BySystemDimensionInStructure
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension4Structure			= FinanceDimension4Structure
		UploadErrorsRel is a FinanceDimension4Upload set
			Instance Selection
				where (related.ErrorMessage entered)
		RunGroupRel
			one-to-many relation to FinanceDimension4Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension4Structure			= FinanceDimension4Structure
			Instance Selection
				where (related.FinanceDimension4Upload.RunGroup = LocalRunGroup)
		ContextDimensionHierarchyRel
			one-to-many relation to FinanceDimension4Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension4Structure			= FinanceDimension4Structure
				related.FinanceDimension4					= FinanceDimension4

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 6
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
		SecurityGroupMemberRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 6
				related.FinanceDimensionStructure     = FinanceDimension4Structure	
		SecurityGroupsRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 6
				related.FinanceDimensionStructure     = FinanceDimension4Structure
			Instance Selection
				where (related.RequiresRebuild)
		SecurityGroupsNotRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 6
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
			Instance Selection
				where (!related.RequiresRebuild)
															
    Conditions
		UploadRecordsExist
			restricted
			when (FinanceDimension4Upload set exists)
		UploadErrorsExist
			restricted
			when (UploadErrorsRel exists)
		EligibleForExport
			restricted
			when ((!UploadRecordsExist)
			and   (!UnusedDimensionStructure))
		AvailableStructure
			restricted
			when ((!UnusedDimensionStructure)
			and   (!ContextDimensionHierarchyRel exists))
		UsedInASecurityGroup
			when (SecurityGroupMemberRel exists)
		SecurityGroupRequiresRebuild
			when (SecurityGroupsRequiringRebuildRel exists)
												
	Sets
		ByStructureSequence
			Sort Order
				FinanceEnterpriseGroup
				StructureSequence
		





	
		ByEnterpriseStructure
			Sort Order
				FinanceEnterpriseGroup
				IsEnterpriseStructure descending
				FinanceDimension4Structure
		
	Attach Rules
		if (!parentcontext.name = "FinanceDimension4"
		and !parentcontext.name = "FinanceDimension4Upload"
		and !parentcontext.name = "FinanceDimension4Shadow"
		and !parentcontext.name = "FinanceDimension4Hierarchy"
		and !parentcontext.name = "FinanceEnterpriseGroup"
		and !parentcontext.name = "AccountAnalysisSettings"
		and !parentcontext.name = "ReportingBasis")	
			constraint (Active)
				"StructureIsNotActive"
									
    Field Rules
		Active
			if (UnusedDimensionStructure)
				Active = false  
		Description
			required
		StructureSequence
			autosequence using ByStructureSequence
			if (!StructureSequence = old StructureSequence)
				cannot be changed
					"SequenceStructureCannotBeChanged"
		IsEnterpriseStructure
			constraint (Active)
				"EnterpriseDimensionMustBeActive"
			constraint (!UnusedDimensionStructure)
				"TheUnusedDimensionStructureCannotBeTheEnterpriseStructure"				
			constraint (!EnterpriseDimensionStructureRel exists)
				"<first EnterpriseDimensionStructureRel.FinanceDimension4Structure>AlreadyDesignatedAsEnterpriseStructure"

	Rule Blocks
		UpdateFinanceEnterpriseGroupDim4Structure
			if (action type.Create)
				if (IsEnterpriseStructure)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						invoked.PrmEnterpriseDim4Structure = FinanceDimension4Structure
			else								
				if (IsEnterpriseStructure changed)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						if (!IsEnterpriseStructure)
							invoked.PrmClearDim4Structure = true
						else
							invoked.PrmEnterpriseDim4Structure = FinanceDimension4Structure

	Actions
		Create is a Create Action
			Entrance Rules



			Exit Rules
				InvokedByUnusedStructure = true
				
				invoke Create FinanceDimension4
					invoked.FinanceEnterpriseGroup  	= FinanceEnterpriseGroup
					invoked.FinanceDimension4			= TopNodeString
					invoked.DisplayDimension			= "TOP_NODE"
					invoked.DimensionStructure			= FinanceDimension4Structure
					invoked.Description					= Description + " Top Level"

					invoked.DimensionType				= DimensionType.Node
					invoked.SkipHierarchy				= true
					invoked.IsTopNode					= true				
			
				DimensionNode = TopNodeString
				include UpdateFinanceEnterpriseGroupDim4Structure
							
		T2VCreate is a Create Action
			restricted
			default label is untranslatable

		Update is an Update Action
		
			Action Rules
				include UpdateFinanceEnterpriseGroupDim4Structure
				invoke Update DimensionNode
					invoked.Description					= Description + " Top Level"

		Delete is a Delete Action
			confirmation required
				"AreYouSureYouWantToPerformTheFollowingAction:_Delete_Structure?"
			Entrance Rules
				constraint (!IsEnterpriseStructure)
					"CannotDeleteEnterpriseStructure"
				constraint (!UnusedDimensionStructure)
					"CannotDeleteUnusedDimensionStructure"
				constraint (!Active)
					"CannotDeleteActiveStructure"
				

				for each FinanceDimension4Hierarchy set		
					increment LocalCounter
					invoke Delete each
					if (LocalCounter = 50)
						commit transaction
						initialize LocalCounter

			Exit Rules
				invoke DeleteAll DimensionsInStructureRel						

		RestrictedDelete is a Delete Action
			restricted
			
			Entrance Rules

			Exit Rules
				invoke DeleteAll DimensionsInStructureRel
										
		UploadHierarchy is an Instance Action
			valid when (UploadRecordsExist)
			completion message is "UploadInitiated"	
			run in background
			
			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"	
			
			Action Rules
				invoke UploadHierarchy FinanceDimension4Upload in background
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
					invoked.PrmFinanceDimension4Structure	= FinanceDimension4Structure
					invoked.PrmRunGroup						= PrmRunGroup

		ResetUploadErrors is an Instance Action

			completion message is "ResetInitiated"

			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"
									
			Action Rules
				invoke ResetAllErrors FinanceDimension4Upload in background
					invoked.PrmFinanceEnterpriseGroup  		= FinanceEnterpriseGroup
					invoked.PrmFinanceDimension4Structure	= FinanceDimension4Structure
					invoked.PrmRunGroup						= PrmRunGroup
											
		CopyStructure is an Instance Action
			valid when (!UnusedDimensionStructure)
			run in background
			completion message is "CopyInitiated"		
			
			Parameters
				NewStructure		is like FinanceDimension4Structure
				NewDescription		is like Description
				MakeNewStructureActive is Boolean
							
			Parameter Rules
				NewStructure
					required
					LocalNewStructure = NewStructure
					constraint (!LocalNewStructure exists)
						"DimensionStructureAlreadyExists"
				NewDescription
					required

			Local Fields
				AsyncId			is an AsyncActionRequest
															
			Action Rules
				invoke Create FinanceDimension4Structure
					invoked.FinanceEnterpriseGroup	   = FinanceEnterpriseGroup
					invoked.FinanceDimension4Structure = NewStructure
					invoked.Description			   	   = NewDescription
					invoked.IsEnterpriseStructure      = false
					invoked.Active					   = MakeNewStructureActive

			Exit Rules
				
				invoke CopyDimensions FinanceDimension4Hierarchy in background
					assign async action request id to AsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmStructure	 		  = FinanceDimension4Structure
					invoked.PrmNewStructure	 		  = NewStructure
					
				invoke BuildShadowFile LocalNewStructure in background
					run after AsyncId
									
		BuildShadowFile is an Instance Action
			restricted
					
			Local Fields
				LocalAsyncId				is an AsyncActionRequest
						
			Action Rules
				invoke DeleteShadowForStructure FinanceDimension4Shadow in background
					assign async action request id to LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension4Structure				
					
				invoke BuildShadowFile FinanceDimension4Hierarchy in background
					run after LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension4Structure
					
		BuildShadowFileForDimension is an Instance Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmDimension					is a FinanceDimension4
					default label is "Dimension"
		
			Action Rules
				invoke BuildShadowFile FinanceDimension4Hierarchy in foreground
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension4Structure
					invoked.PrmDimension			  = PrmDimension
		
		ExportToUpload is an Instance Action


			completion message is "ExportInitiated"
						
			Parameters
				PrmRunGroup					is AlphaUpper 30
					default label is "RunGroup"
				PrmDimensionGroup   		is a FinanceDimension4 group
					default label is "CustomGroup"
				PrmSummaryDimension		    is a FinanceDimension4
					default label is "Summary<FinanceEnterpriseGroup.FinanceDimension4Label>"	
				PrmPostingDimension			is a FinanceDimension4
					default label is "Posting<FinanceEnterpriseGroup.FinanceDimension4Label>"					

			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (!RunGroupRel exists)
						"UploadRecordsAlreadyExistForThisRunGroup"	
									
				PrmDimensionGroup
					constraint (!PrmSummaryDimension entered
					and			!PrmPostingDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
						
				PrmSummaryDimension
					constraint (!PrmDimensionGroup entered
					and         !PrmPostingDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
										
				PrmPostingDimension
					constraint (!PrmDimensionGroup entered
					and         !PrmSummaryDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"					
			
			Action Rules
			
				if ((!PrmDimensionGroup entered)
				and (!PrmSummaryDimension entered)
				and (!PrmPostingDimension entered))
					invoke ExportToUpload DimensionNode in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension4Structure	  = FinanceDimension4Structure
						invoked.PrmRunGroup						  = PrmRunGroup			
				else
				if (PrmSummaryDimension entered)
					invoke ExportToUpload PrmSummaryDimension in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension4Structure	  = FinanceDimension4Structure
						invoked.PrmRunGroup						  = PrmRunGroup
				else
				if (PrmPostingDimension entered)														
					invoke ExportToUpload PrmPostingDimension in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension4Structure	  = FinanceDimension4Structure
						invoked.PrmRunGroup						  = PrmRunGroup
				else																		
					invoke ExportToUpload FinanceDimension4Hierarchy in background
						invoked.PrmFinanceEnterpriseGroup  		  = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension4Structure     = FinanceDimension4Structure
						invoked.PrmRunGroup				   	      = PrmRunGroup	
						invoked.PrmFinanceDimension4Group	   	  = PrmDimensionGroup

		DeleteUploadRecordsForRunGroup is an Instance Action
			valid when (UploadRecordsExist)
			confirmation required
			completion message is "DeleteSubmitted"
			
			Parameters
				PrmRunGroup			is AlphaUpper 30
					default label is "RunGroup"
					
			Parameter Rules
				PrmRunGroup
					required
					
			Action Rules
				invoke DeleteAll FinanceDimension4Upload in background
					invoked.PrmFinanceEnterpriseGroup  	  = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension4Structure = FinanceDimension4Structure
					invoked.PrmRunGroup				   	  = PrmRunGroup
						
		CleanupUnusedDimensions is an Instance Action
			restricted
			
			Action Rules
				InvokedByUnusedStructure = true
				invoke CleanupUnusedDimensions FinanceDimension4 in background
					invoked.PrmFinanceEnterpriseGroup	  = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension4Structure = FinanceDimension4Structure
					
		AddDimensionToUnusedStructure is an Instance Action
			restricted
			
			Parameters
				FinanceDimension4
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<FinanceDimension4Structure>"
					
				InvokedByUnusedStructure = true
				invoke AddPostingDimensionToStructure FinanceDimension4
					invoked.PrmStructure	   = FinanceDimension4Structure
					invoked.PrmParentDimension = DimensionNode
					
		RemoveDimensionFromUnusedStructure is an Instance Action
			restricted
			
			Parameters
				FinanceDimension4
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<FinanceDimension4Structure>"
					
				InvokedByUnusedStructure = true
				invoke RemoveFromStructure FinanceDimension4				
