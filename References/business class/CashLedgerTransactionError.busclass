CashLedgerTransactionError is a BusinessClass
    owned by cb
    prefix is CLTE

    Ontology
		symbolic key is CashLedgerTransactionError

    Patterns
    	disable AuditIndex
    	disable Auditing
    	disable EffectiveDated

    Persistent Fields
    	ErrorClass						is Numeric 2
    		States
    			MissingInformation		value is 1
    			InvalidInformation		value is 2
    			UnableToProcess			value is 3
    	ErrorType						is a CashLedgerTransactionErrorType
    	ErrorLevel						is Numeric 2
    		default label is "Level"
    		States
    			Error					value is 1
    			Warning					value is 2
		ErrorSource							is Numeric 2
			default label is "SourceOfError"
			States
				StaleDateProcessing				value is 1
				EscheatmentProcessing			value is 2
				EftExtractUpdate 				value is 3	
				MassReleaseTransactions			value is 4
				MassReleasePayments				value is 5
				MassReleaseReturnedPayments		value is 6
				MassReleaseCashReceipts			value is 7
				MassReleaseBankFundTransfers	value is 8
				MassReleaseEFTExtracts			value is 9	
				ApprovalCashReceipt				value is 10	
				WriteOffProcessing				value is 11	
		StaleDateOutputFileHeader
		EscheatmentOutputFileHeader
		CashLedgerMassReleaseResult
		ErrorDescription  				is Alpha size up to 200
			default label is "Error"
		
	Derived Fields
		Msg001 is a MessageField
			restricted
			"VarianceAdjustmentExists"
		Msg002 is a MessageField															
			restricted
	        "StaleDateAccountNotDefinedInCompanyCashCode" 
		Msg003 is a MessageField	
			restricted
			"StaleDateAccountNotDefinedInCompanyCashCodeForProcessLevel"
		Msg004 is a DerivedField	
			type is Alpha size up to 400
			restricted
			return Msg004a + MatchedToBankStatementErrorMessage + Msg004b	
		Msg004a is a MessageField	
			restricted
			"CannotProcess,"
		Msg004b is a MessageField
			restricted
			";MustBeUnmatchedFromTheBankStatementPriorToProcessing"
		Msg005 is a DerivedField
			type is Alpha size up to 200
			restricted
			return Msg005a + StaleDateOutputFileHeaderErrorMessage											
		Msg005a is a MessageField	
			restricted
			"TransactionAlreadyExistsInStaleDateOutputFileHeader"
		Msg006 is a MessageField															
			restricted
	        "EscheatmentAccountNotDefinedInCompanyCashCode" 
		Msg007 is a MessageField	
			restricted
			"EscheatmentAccountNotDefinedInCompanyCashCodeForProcessLevel"
		Msg008 is a DerivedField
			type is Alpha size up to 200
			restricted
			return Msg008a + EscheatmentOutputFileHeaderErrorMessage											
		Msg008a is a MessageField	
			restricted
			"TransactionAlreadyExistsInEscheatmentOutputFileHeader"
		Msg010 is a MessageField	
			restricted
			"TransactionDateIsOutsideOfProjectDateRangeForStaleDateAccount"
		Msg011 is a MessageField	
			restricted					
			"PostingDateIsOutsideOfProjectDateRangeForStaleDateAccount"					
		Msg012 is a MessageField	
			restricted
			"TransactionDateIsOutsideOfProjectDateRangeForEscheatmentAccount"
		Msg013 is a MessageField	
			restricted					
			"PostingDateIsOutsideOfProjectDateRangeForEscheatmentAccount"		
		Msg015 is a MessageField
			restricted
			"PostingDateNotWithinValidEntryDatesForSystemClosingControl"
						
		DerivedStaleDateOutputFileHeader is a DerivedField
			type is Alpha 15
			restricted
			return ": " + CashLedgerTransaction.StaleDateOutputFileHeader
		StaleDateOutputFileHeaderErrorMessage is a DerivedField
			type is Alpha size up to 100
			restricted
			if (CashLedgerTransaction.StaleDateOutputFileHeader entered)
				return DerivedStaleDateOutputFileHeader
			else
				return ""

		DerivedEscheatmentOutputFileHeader is a DerivedField
			type is Alpha 15
			restricted
			return ": " + CashLedgerTransaction.EscheatmentOutputFileHeader
		EscheatmentOutputFileHeaderErrorMessage is a DerivedField
			type is Alpha size up to 100
			restricted
			if (CashLedgerTransaction.EscheatmentOutputFileHeader entered)
				return DerivedEscheatmentOutputFileHeader
			else
				return ""

		DerivedBankStatment is a DerivedField
			type is Alpha 51
			restricted
			return "Transaction Matched To Bank Statement: " + first BankStatementReconciliationsRel.ReconMatchNumber.BankStatement
		DerivedBankStatmentDate is a DerivedField
			type is Alpha 17
			restricted
			return ", Dated: " + first BankStatementReconciliationsRel.ReconMatchNumber.BankStatement.StatementDate
		DerivedBankStatmentLineMatchField is a DerivedField
			type is Alpha 220
			restricted
			return ", Line Match Field: " + first BankStatementReconciliationsRel.ReconMatchNumber.LineMatchField
		MatchedToBankStatementErrorMessage is a DerivedField
			type is Alpha size up to 300
			restricted
			if (TransactionMatchedToBankStatement)
				return DerivedBankStatment + DerivedBankStatmentDate + DerivedBankStatmentLineMatchField 
			else
				return ""

		ErrorMessage is a DerivedField
			type is Alpha size up to 200
			if (ErrorDescription entered)
				ErrorMessage = ErrorDescription 
			else	
			if (ErrorType.VarianceAdjustmentExists)
				return Msg001		
			else
			if (ErrorType.MissingStaleDateAccount)
				return Msg002		
			else
			if (ErrorType.MissingStaleDateAccountForProcessLevel)
				return Msg003		
			else
			if (ErrorType.TransactionMatchedToBankStatement)
				return Msg004
			else
			if (ErrorType.TransactionAlreadyExistsInStaleDateOutputFileHeader)
				return Msg005				
			else
			if (ErrorType.MissingEscheatmentAccount)
				return Msg006		
			else
			if (ErrorType.MissingEscheatmentAccountForProcessLevel)
				return Msg007		
			else
			if (ErrorType.TransactionAlreadyExistsInEscheatmentOutputFileHeader)
				return Msg008				
			else
			if (ErrorType.TransactionDateIsOutsideOfProjectDateRangeForStaleDateAccount)		
				return Msg010
			else
			if (ErrorType.PostingDateIsOutsideOfProjectDateRangeForStaleDateAccount)			
				return Msg011
			else
			if (ErrorType.TransactionDateIsOutsideOfProjectDateRangeForEscheatmentAccount)	
				return Msg012
			else
			if (ErrorType.PostingDateIsOutsideOfProjectDateRangeForEscheatmentAccount)		
				return Msg013		
			else
			if (ErrorType.PostingDateNotWithinValidEntryDatesForSystemClosingControl)
				return Msg015	
			else
				return ""


	Sets
		ByStaleDateErrorSource
			indexed
			Instance Selection
				where (IsStaleDateProcessingErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				StaleDateOutputFileHeader
				CashLedgerTransaction
				CashLedgerTransactionError
				BankTransactionCode
				CashLedgerSourceRecord 							
				CashCode										

		ByEscheatmentErrorSource
			indexed
			Instance Selection
				where (IsEscheatmentProcessingErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				EscheatmentOutputFileHeader
				CashLedgerTransaction
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				CashCode										
				BankTransactionCode								
				
		ByMassReleaseTransactionsErrorSource
			indexed
			Instance Selection
				where (IsMassReleaseTransactionsErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				CashLedgerMassReleaseResult
				CashLedgerTransaction
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				CashCode										
				BankTransactionCode								
				
		ByMassReleasePaymentsErrorSource
			indexed
			Instance Selection
				where (IsMassReleasePaymentsErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				CashLedgerMassReleaseResult
				CashCode								
				CashLedgerPayment.BankTransactionCode
				CashLedgerPayment.TransactionIDNumber
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				BankTransactionCode								
				
		ByMassReleaseReturnedPaymentsErrorSource
			indexed
			Instance Selection
				where (IsMassReleaseReturnedPaymentsErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				CashLedgerMassReleaseResult
				CashCode								
				CashLedgerReturnedPayment.BankTransactionCode
				CashLedgerReturnedPayment.TransactionIDNumber
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				BankTransactionCode								
												
		ByMassReleaseCashReceiptsErrorSource
			indexed
			Instance Selection
				where (IsMassReleaseCashReceiptsErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				CashLedgerMassReleaseResult
				CashCode								
				CashLedgerCashReceiptHeader.BankTransactionCode
				CashLedgerCashReceiptHeader.TransactionNumber
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				BankTransactionCode								
												
		ByMassReleaseBankFundTransfersErrorSource
			indexed
			Instance Selection
				where (IsMassReleaseBankFundTransfersErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				CashLedgerMassReleaseResult
				CashCode								
				CashLedgerFundTransferHeader
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				BankTransactionCode								
											
		ByMassReleaseEFTExtractsErrorSource
			indexed
			Instance Selection
				where (IsMassReleaseEFTExtractsErrorSource)
			Sort Order
				CashManagementGroup
				ErrorSource
				CashLedgerMassReleaseResult
				CashCode								
				CashLedgerElectronicFundsTransferTransaction.BankTransactionCode
				CashLedgerElectronicFundsTransferTransaction.TransactionIDNumber
				CashLedgerTransactionError
				CashLedgerSourceRecord 							
				BankTransactionCode								
											



















																	
	Relations
		CashLedgerTransactionRel
			one-to-one relation to CashLedgerTransaction
			Field Mapping uses symbolic key
				related.CashManagementGroup 							= CashManagementGroup	
				related.BankTransactionCode								= CashLedgerTransaction.BankTransactionCode
				related.CashLedgerSourceRecord							= CashLedgerTransaction.CashLedgerSourceRecord
				related.CashLedgerTransaction   						= CashLedgerTransaction

		CashLedgerPaymentRel
			one-to-one relation to CashLedgerPayment
			Field Mapping uses symbolic key
				related.CashManagementGroup 							= CashManagementGroup	
				related.CashCode										= CashCode
				related.CashLedgerPayment.BankTransactionCode			= CashLedgerPayment.BankTransactionCode
				related.CashLedgerPayment.TransactionIDNumber			= CashLedgerPayment.TransactionIDNumber

		CashLedgerReturnedPaymentRel
			one-to-one relation to CashLedgerReturnedPayment
			Field Mapping uses symbolic key
				related.CashManagementGroup 							= CashManagementGroup	
				related.CashCode										= CashCode
				related.CashLedgerReturnedPayment.BankTransactionCode	= CashLedgerReturnedPayment.BankTransactionCode
				related.CashLedgerReturnedPayment.TransactionIDNumber	= CashLedgerReturnedPayment.TransactionIDNumber

		CashLedgerCashReceiptHeaderRel
			one-to-one relation to CashLedgerCashReceiptHeader
			Field Mapping uses symbolic key
				related.CashManagementGroup 							= CashManagementGroup	
				related.CashCode										= CashCode
				related.CashLedgerCashReceiptHeader.BankTransactionCode	= CashLedgerCashReceiptHeader.BankTransactionCode
				related.CashLedgerCashReceiptHeader.TransactionNumber	= CashLedgerCashReceiptHeader.TransactionNumber

		CashLedgerFundTransferHeaderRel
			one-to-one relation to CashLedgerFundTransferHeader
			Field Mapping uses symbolic key
				related.CashManagementGroup 							= CashManagementGroup	
				related.CashCode										= CashCode
				related.CashLedgerFundTransferHeader					= CashLedgerFundTransferHeader
		
		CashLedgerElectronicFundsTransferTransactionRel
			one-to-one relation to CashLedgerElectronicFundsTransferTransaction
			Field Mapping uses symbolic key
				related.CashManagementGroup 												= CashManagementGroup	
				related.CashCode															= CashCode
				related.CashLedgerElectronicFundsTransferTransaction.BankTransactionCode	= CashLedgerElectronicFundsTransferTransaction.BankTransactionCode
				related.CashLedgerElectronicFundsTransferTransaction.TransactionIDNumber	= CashLedgerElectronicFundsTransferTransaction.TransactionIDNumber
			
		StaleDateOutputFileHeaderRel
			one-to-one relation to StaleDateOutputFileHeader
			Field Mapping uses symbolic key
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= LocalStaleDateOutputFileHeader
				
		EscheatmentOutputFileHeaderRel
			one-to-one relation to EscheatmentOutputFileHeader
			Field Mapping uses symbolic key
				related.CashManagementGroup			= CashManagementGroup
				related.EscheatmentOutputFileHeader	= LocalEscheatmentOutputFileHeader		
		
		BankStatementReconciliationsRel
    		one-to-many relation to BankStatementReconciliation
    		Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
				related.CashManagementAccount	= CashLedgerTransaction.CashManagementAccount
			Instance Selection

				where (related.ReconMatchNumber entered)

		
								
	Conditions
		TransactionMatchedToBankStatement
			restricted
			when (first BankStatementReconciliationsRel exists)
		
		IsStaleDateProcessingErrorSource
			restricted
			when (ErrorSource.StaleDateProcessing)
			
		IsEscheatmentProcessingErrorSource
			restricted
			when (ErrorSource.EscheatmentProcessing)

		IsTransactionErrorSource
			restricted
			when (ErrorSource.MassReleaseTransactions
			or    ErrorSource.StaleDateProcessing
			or    ErrorSource.EscheatmentProcessing)

		IsPaymentErrorSource
			restricted			
			when (ErrorSource.MassReleasePayments)

		IsReturnedPaymentErrorSource
			restricted		   
			when (ErrorSource.MassReleaseReturnedPayments)

		IsCashReceiptErrorSource
			restricted
			when (ErrorSource.MassReleaseCashReceipts)

		IsBankFundTransferErrorSource
			restricted
			when (ErrorSource.MassReleaseBankFundTransfers)

		IsEFTExtractErrorSource
			restricted
			when (ErrorSource.MassReleaseEFTExtracts)
						
		IsMassReleaseErrorSource
			restricted
			when (ErrorSource.MassReleaseTransactions
			or    ErrorSource.MassReleasePayments
			or    ErrorSource.MassReleaseReturnedPayments
			or    ErrorSource.MassReleaseCashReceipts
			or    ErrorSource.MassReleaseBankFundTransfers
			or    ErrorSource.MassReleaseEFTExtracts)
			
		IsMassReleaseTransactionsErrorSource
			restricted
			when (ErrorSource.MassReleaseTransactions)
			
		IsMassReleasePaymentsErrorSource
			restricted
			when (ErrorSource.MassReleasePayments)
			
		IsMassReleaseReturnedPaymentsErrorSource
			restricted
			when (ErrorSource.MassReleaseReturnedPayments)
		
		IsMassReleaseCashReceiptsErrorSource
			restricted
			when (ErrorSource.MassReleaseCashReceipts)			

		IsMassReleaseBankFundTransfersErrorSource
			restricted
			when (ErrorSource.MassReleaseBankFundTransfers)
			
		IsMassReleaseEFTExtractsErrorSource
			restricted
			when (ErrorSource.MassReleaseEFTExtracts)							

	Field Rules
		ErrorLevel
			initial value is 1	
			default to 1	
		
		ErrorDescription
			if (!ErrorType.MassReleaseError)
				ErrorDescription	= ErrorMessage
				
	Rule Blocks

	Local Fields
		LocalStaleDateOutputFileHeader		is a StaleDateOutputFileHeader
		LocalEscheatmentOutputFileHeader	is a EscheatmentOutputFileHeader
		LocalCashLedgerMassReleaseResult	is a CashLedgerMassReleaseResult

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted
			Entrance Rules
				LocalCashLedgerMassReleaseResult = CashLedgerMassReleaseResult
			Exit Rules
				if (IsMassReleaseErrorSource)	
					invoke StatusUpdate LocalCashLedgerMassReleaseResult.CashLedgerMassReleaseResult	
				
		RemoveStaleDateErrorFromList is an Instance Action
			valid when (IsStaleDateProcessingErrorSource)
			Entrance Rules
				if (CashLedgerTransactionRel exists
				and StaleDateOutputFileHeader = CashLedgerTransactionRel.StaleDateOutputFileHeader)
					invoke RemoveTransactionFromStaleDateProcess CashLedgerTransactionRel
			Action Rules	
				LocalStaleDateOutputFileHeader	= StaleDateOutputFileHeader
				decrement StaleDateOutputFileHeaderRel.TotalNumberOfRecords by 1
                decrement StaleDateOutputFileHeaderRel.TotalRecordAmount by CashLedgerTransaction.IssuedBankAmount
				invoke Delete	
				if (StaleDateOutputFileHeaderRel exists)
					invoke StatusUpdate StaleDateOutputFileHeaderRel	
				
		RemoveEscheatmentErrorFromList is an Instance Action
			valid when (IsEscheatmentProcessingErrorSource)
			Entrance Rules
				if (CashLedgerTransactionRel exists
				and EscheatmentOutputFileHeader = CashLedgerTransactionRel.EscheatmentOutputFileHeader)
					invoke RemoveTransactionFromEscheatmentProcess CashLedgerTransactionRel
			Action Rules	
				LocalEscheatmentOutputFileHeader	= EscheatmentOutputFileHeader
				invoke Delete	
				if (EscheatmentOutputFileHeaderRel exists)
					invoke StatusUpdate EscheatmentOutputFileHeaderRel
				
