PatientVisit is a BusinessClass
    owned by ic
    prefix is HPV
    classic name is HCPATVISIT

    Ontology
        symbolic key is PatientVisit
            classic set name is HPVSET1
            classic name is VISIT-NBR

    Patterns
        implements StaticJava
        implements Encrypted
        disable AuditIndex

    Persistent Fields

        Patient
            classic name is PATIENT-ID
        PatientClass
        Room                   is AlphaUpper size 10
        Bed                    is Numeric size 2
        AdmitDate              is Date
        DischargeDate          is Date
            classic name is DISCH-DATE
        LastTransactionDate    is Date
            classic name is LAST-TRANS-DT
        DiagnosticRelatedGroup is AlphaUpper size 15
            classic name is DIAG-REL-GRP
        Practitioner 
        	holds pii
            classic name is PRACTNR-ID
        SecondaryPractitioner  is a Practitioner 
        	holds pii
            classic name is SEC-PRACTNR-ID
        LastChargeSequence     is a SeqNbr
            classic name is LAST-CHRG-SEQ
            disable Auditing
        UpdateDate             is Date

    Conditions
        
        RecordExists
            when (PatientVisit exists)
            
    Relations

        PatientChargesRel
            classic name is HCPATCHRG
            one-to-many relation to PatientCharge
            Field Mapping uses symbolic key
                related.Company      = Company
                related.PatientVisit = PatientVisit
                
        PatientChargesDeletedRel
            one-to-many relation to PatientCharge
            Field Mapping uses symbolic key
                related.Company      = Company
                related.PatientVisit = PatientVisit
			Instance Selection
				include deleted records

    Sets

        Set2
            indexed
            Sort Order
                Company
                Patient
                PatientVisit

    Field Rules

        PatientClass
            required

        Patient
            required
		
		DischargeDate
			constraint (DischargeDate >= AdmitDate)
				"AdmitDateIsAfterDischargeDate"											
			
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
			Entrance Rules
				for each PatientChargesRel
					constraint (each.BatchNumber not entered)
						"CannotDeletePatientVisit;PatientChargesExists"					
		
		Purge is a Purge Action
			restricted
			Action Rules
				display "PatientVisit"
				invoke Purge PatientChargesDeletedRel

				
