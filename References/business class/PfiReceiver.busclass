PfiReceiver is a BusinessClass
    owned by pfi
    default label is "Receiver"
    prefix is PfiCR
    contains environment data
    disable data area copy
        preserve target data

    Ontology
        symbolic key is PfiReceiver

    Patterns
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements DynamicCreation

    Persistent Fields

        ReceiverType            is a PfiChannelType
        Description             is a PfiDescription
        PfiFlowDefinition
        LastMessageReceivedTime is TimeStamp
            disable Auditing
        FileReceiver            is a PfiFileReceiver
        JMSReceiver             is a PfiJMSReceiver
        EventHubReceiver        is a PfiEventHubReceiver
        IONReceiver             is a PfiIONReceiver
        UserReceiver            is a PfiUserReceiver
        LastError               is Text
            disable Auditing
        LastErrorType           is Numeric size 1
            States
                None     value is 0
                Channel  value is 1
                Receiver value is 2
            disable Auditing
        LastErrorDate           is TimeStamp
            default label is "LastErrorTime"
            disable Auditing
        StartupType             is Numeric size 1
            States
                Automatic value is 1
                Manual    value is 2
                Disabled  value is 3
        Status                  is Numeric size 2
            States
                Activating       value is 1
                Active           value is 2
                ActivateFailed   value is 3
                Deactivating     value is 4
                Inactive         value is 5
                DeactivateFailed value is 6
                ActiveWithIssue  value is 7
                Failed           value is 8
            disable Auditing

    Derived Fields

        UpdateMessage is a DerivedField
            type is Alpha size 200
            return "Changes to an active receiver will not take effect until the receiver is deactivated and reactivated. Do you want to continue?"

        InTransitionStatus is a DerivedField
            type is Boolean
            if (Status.Inactive or Status.Activating or Status.Deactivating)
                return true
            else
                return false

        InActiveStatus is a DerivedField
            type is Boolean
            default label is "Active"
            if (Status.Active or Status.ActiveWithIssue)
                return true
            else
                return false

        InFailedOrActiveWithIssueStatus is a DerivedField
            type is Boolean
            if (Status.ActivateFailed or Status.ActiveWithIssue or Status.Failed or Status.DeactivateFailed)
                return true
            else
                return false

        HasJMSMsgSelector is a DerivedField 
            type is Alpha 3
            default label is "ReceiverHasJMSMessageSelector"
            if (JMSReceiver.MsgSelector = blank)
                return "No"
            else
                return "Yes"

        IsActive is a DerivedField
            type is Boolean
            default label is "Active"
            if (Status.Activating or Status.Active or Status.ActiveWithIssue)
                return true
            else
                return false

        ReceiverName is a DerivedField
            type is Alpha size 512 
            if (ReceiverType.IONChannel)
                return IONReceiver.BODType + " : " + IONReceiver.ToLogicalId
            else
                return PfiReceiver

        HasWuRestarts is a DerivedField
            type is Boolean
            default label is "ReceiverHasWorkUnitRestarts"
            if (IONReceiver.NumWuRestarts > 0)
                return true
            else
                return false
        IONJDBCDriver is a DerivedField
            type is Alpha size 200
            return PfiChannel.IONChannel.JDBCDriver

        IONDatabaseURL is a DerivedField
            type is Alpha size 200
            return PfiChannel.IONChannel.DatabaseURL

        IONDatabaseSchema is a DerivedField
            type is Alpha size 200
            return PfiChannel.IONChannel.DbSchema





        DuplicateIONReceiver is a NativeField
            type is Alpha size 10000

    Local Fields





        LocalPfiReceiver is a PfiReceiver





        ErrorMessage is Alpha size 10000





        CheckForIONReceiverDuplicates is Boolean

    Rule Blocks

        FileReceiverBlock
            constraint (FileReceiver.FileName != blank)
                "FileNameIsRequired"

        JMSReceiverBlock
            constraint (JMSReceiver.Destination != blank)
                "DestinationIsRequired"

        IONReceiverBlock
            constraint (IONReceiver.RcvrType != blank)
                "ReceiverTypeIsRequired"
            constraint (IONReceiver.ToLogicalId != blank)
                "ToLogicalIdIsRequired"
            constraint (IONReceiver.ScanFreq > 0)
                "ScanFrequencyMustBeGreaterThanZero"

            if (IONReceiver.RcvrType.AppBOD)
                constraint (IONReceiver.BODType != blank)
                    "BODTypeIsRequired"
                constraint (IONReceiver.SeqMethod != blank)
                    "SequenceMethodIsRequired"
                constraint (IONReceiver.NumWuRestarts >= 0)
                    "NumberOfWorkUnitRestartsMustBeGreaterThanOrEqualToZero"
                constraint (IONReceiver.NumRescans >= 1)
                    "NumberOfRescansMustBeGreaterThanOrEqualToOne"
                if (IONReceiver.NumWuRestarts > 0)
                    constraint (IONReceiver.WuRestartWait > 0)
                        "WorkUnitRestartWaitMustBeGreaterThanZero"
            if (IONReceiver.RcvrType.IONPulse)
                constraint (IONReceiver.PulseType != blank)
                    "PulseTypeIsRequired"
            if (IONReceiver.RcvrType.BODGroup)
                constraint (IONReceiver.BODGroup != blank)
                    "BODGroupIsRequired"

        IONReceiverBlock2
            constraint (IONReceiver.RcvrType != blank)
                "ReceiverTypeIsRequired"
            constraint (IONReceiver.ToLogicalId != blank)
                "ToLogicalIdIsRequired"
            constraint (IONReceiver.ScanFreq > 0)
                "ScanFrequencyMustBeGreaterThanZero"
            if (CheckForIONReceiverDuplicates)






                ErrorMessage = DuplicateIONReceiver
                constraint (ErrorMessage = blank)
                    "<ErrorMessage>"

            if (IONReceiver.RcvrType.AppBOD)
                constraint (IONReceiver.BODType != blank)
                    "BODTypeIsRequired"
                constraint (IONReceiver.SeqMethod != blank)
                    "SequenceMethodIsRequired"
                constraint (IONReceiver.NumWuRestarts >= 0)
                    "NumberOfWorkUnitRestartsMustBeGreaterThanOrEqualToZero"
                constraint (IONReceiver.NumRescans >= 1)
                    "NumberOfRescansMustBeGreaterThanOrEqualToOne"
                if (IONReceiver.NumWuRestarts > 0)
                    constraint (IONReceiver.WuRestartWait > 0)
                        "WorkUnitRestartWaitMustBeGreaterThanZero"
            if (IONReceiver.RcvrType.IONPulse)
                constraint (IONReceiver.PulseType != blank)
                    "PulseTypeIsRequired"
            if (IONReceiver.RcvrType.BODGroup)
                constraint (IONReceiver.BODGroup != blank)
                    "BODGroupIsRequired"

        UserReceiverBlock
            constraint (UserReceiver.Action != blank)
                "ActionIsRequired"
            if (UserReceiver.Action.ServiceTrigger)
                constraint (UserReceiver.Service != blank)
                    "ServiceIsRequired"

    Relations





        PfiReceiverRel
            one-to-one relation to PfiReceiver
            Field Mapping uses symbolic key
                related.PfiChannel  = LocalPfiReceiver.PfiChannel
                related.PfiReceiver = LocalPfiReceiver.PfiReceiver

    Sets

        ByProcessDefinitionReceiver
            sql name is PfiCR01
            indexed
            Sort Order
                PfiFlowDefinition
                PfiReceiver
                PfiChannel

        ByBODTypeToLogicalId
            sql name is PfiCR02
            indexed
            Sort Order
                IONReceiver.BODType
                IONReceiver.ToLogicalId
                PfiReceiver
            Instance Selection
                where (ReceiverType.IONChannel)

    Field Rules

        ReceiverType
            force default to PfiChannel.ChannelType
            required
            cannot be changed

        PfiFlowDefinition
            if (IONReceiver.RcvrType.IONPulse)
                cannot be entered
                    "ProcessNotUsedAndMustBeBlankForION_\PULSE_\BOD"
            else
            if (IONReceiver.RcvrType.BODGroup)
                cannot be entered
                    "ProcessNotUsedAndMustBeBlankForBODGroups"
            else
            if (not ReceiverType.UserChannel)
                required

        StartupType
            default to StartupType.Manual
            required

        Status
            default to Status.Inactive
            required

        LastErrorType
            default to LastErrorType.None
            initial value is LastErrorType.None

    Actions




        CreateUI is a Create Action
            default label is "Create"

            Entrance Rules
                if (PfiReceiver = blank)
                    PfiReceiver = current timestamp

            Exit Rules
                if (ReceiverType.FileChannel)
                    include FileReceiverBlock
                if (ReceiverType.JMSChannel)
                    include JMSReceiverBlock
                if (ReceiverType.IONChannel)

                    CheckForIONReceiverDuplicates = true
                    include IONReceiverBlock2
                if (ReceiverType.UserChannel)
                    include UserReceiverBlock









        Create is an Import Action
            restricted

            Local Fields

                ImportRecord is Boolean

            Action Rules



                LocalPfiReceiver = PfiReceiver 
                if (PfiReceiverRel exists)
                    ImportRecord = false 
                else
                    if (ReceiverType.IONChannel and DuplicateIONReceiver != blank)
                        ImportRecord = false 
                    else
                        ImportRecord = true



                    if (ImportRecord)
                        invoke CreateForImport this instance
                            invoked.PfiReceiver             = PfiReceiver
                            invoked.ReceiverType            = ReceiverType
                            invoked.Description             = Description
                            invoked.PfiFlowDefinition       = PfiFlowDefinition
                            invoked.LastMessageReceivedTime = LastMessageReceivedTime
                            invoked.FileReceiver            = FileReceiver
                            invoked.JMSReceiver             = JMSReceiver
                            invoked.EventHubReceiver        = EventHubReceiver
                            invoked.IONReceiver             = IONReceiver
                            invoked.UserReceiver            = UserReceiver
                            invoked.LastError               = LastError
                            invoked.LastErrorType           = LastErrorType
                            invoked.LastErrorDate           = LastErrorDate
                            invoked.StartupType             = StartupType
                            invoked.Status                  = Status




        CreateForImport is a Create Action
            restricted

            Entrance Rules
                if (PfiReceiver = blank)
                    PfiReceiver = current timestamp

            Exit Rules
                if (ReceiverType.FileChannel)
                    include FileReceiverBlock
                if (ReceiverType.JMSChannel)
                    include JMSReceiverBlock
                if (ReceiverType.IONChannel)

                    CheckForIONReceiverDuplicates = false
                    include IONReceiverBlock2
                if (ReceiverType.UserChannel)
                    include UserReceiverBlock

        Update is an Action

            Action Rules
                if (old InActiveStatus)
                    confirmation required
                        "<UpdateMessage>"
                if (ReceiverType.FileChannel)
                    include FileReceiverBlock
                if (ReceiverType.JMSChannel)
                    include JMSReceiverBlock
                if (ReceiverType.IONChannel)

                    CheckForIONReceiverDuplicates = true
                    include IONReceiverBlock2
                if (ReceiverType.UserChannel)
                    include UserReceiverBlock

        RestrictedUpdate is an Update Action
            restricted

        RestrictedCreate is a Create Action
            restricted
            Entrance Rules
                if (PfiReceiver = blank)
                    PfiReceiver = current timestamp

            Exit Rules
                if (ReceiverType.FileChannel)
                    include FileReceiverBlock
                if (ReceiverType.JMSChannel)
                    include JMSReceiverBlock
                if (ReceiverType.IONChannel)
                    include IONReceiverBlock
                if (ReceiverType.UserChannel)
                    include UserReceiverBlock

        Delete is an Action
            Action Rules
                constraint (Status.Inactive)
                    "ReceiverMustBeInInactiveStateToDelete"

        Activate is an Instance Action
            Action Rules
                constraint (PfiChannel.IsEnabled)
                    "RelatedChannelMustBeEnabledToActivate"
                constraint (StartupType.Automatic or StartupType.Manual)
                    "StartupTypeMustBeAutomaticOrManualToActivate"
                constraint (Status.ActivateFailed or Status.Inactive or Status.Failed)
                    "ReceiverMustBeInActivateFailedOrInactiveOrFailedStateToActivate"
            manual update

        Deactivate is an Instance Action
            Action Rules
                constraint (Status.Active or Status.ActiveWithIssue)
                    "ReceiverIsNotActive"
            manual update

        ClearLastError is an Instance Action
            Action Rules
                LastError = blank
                LastErrorType = LastErrorType.None
                LastErrorDate = blank
		
		ProcessInboundDocument is an Import Action
			restricted
			Parameters
				Message 				is XMLDocument
				BodDetail 				is an IONBod
					default label is "BODDetail"
				AdditionalProperties	is an IONAdditionalProperties
			Entrance Rules
				invoke Create PfiReceiverIMSInboundQueue
					invoked.MessageId = BodDetail.MessageId
					invoked.BODType = BodDetail.BodType
					invoked.FromLogicalId = BodDetail.FromLogicalId
					invoked.ToLogicalId = BodDetail.ToLogicalId
					invoked.AccountingEntity = BodDetail.AccountingEntity
					invoked.LocationId = BodDetail.LocationId	
					invoked.DocumentId = BodDetail.DocumentId	
					invoked.VariationId = BodDetail.VariationId	
					invoked.RevisionId = BodDetail.RevisionId	
					invoked.BatchId = AdditionalProperties.BatchId	
					invoked.BatchSequence = AdditionalProperties.BatchSequence	
					invoked.BatchSize = AdditionalProperties.BatchSize	
					invoked.BatchRevision = AdditionalProperties.BatchRevision	
					invoked.BatchAbortIndicator = AdditionalProperties.BatchAbortIndicator	
					invoked.Source = AdditionalProperties.Source	
					invoked.Instances = AdditionalProperties.Instances
					invoked.BodValue = Message
							
    StateCycles

        ReceiverLifeCycle is a StateCycle
            state field is ReceiverType

            FileChannel     is a State
            JMSChannel      is a State
            EventHubChannel is a State
            IONChannel      is a State
            UserChannel     is a State
