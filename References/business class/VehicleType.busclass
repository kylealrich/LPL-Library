VehicleType is a BusinessClass
    owned by wh
    prefix is VHTY

    Ontology
        symbolic key is VehicleType

    Patterns
        disable AuditIndex
        
    Persistent Fields
        Description
        InteriorLength           is a LengthDimension
        InteriorHeight           is a HeightDimension
        InteriorWidth            is a WidthDimension
        ExteriorLength           is a LengthDimension
        ExteriorHeight           is a HeightDimension
        ExteriorWidth            is a WidthDimension
        VehicleWeightCapacity    is a DimensionSize
        VehicleVolumeCapacity    is a DimensionSize

    Conditions
        RecordExists
            when (VehicleType exists)

        ValidVolumeCapacity
            restricted
            when (VehicleVolumeCapacity <= ComputedVolumeCapacity)

        RelatesToVehicle
            when (VehicleRel exists)

        RequireInteriorLength
            restricted
            when (InteriorWidth entered
            or    InteriorHeight entered)
        
        RequireInteriorWidth
            restricted
            when (InteriorLength entered
            or    InteriorHeight entered)

        RequireInteriorHeight
            restricted
            when (InteriorLength entered
            or    InteriorWidth entered)

        RequireExteriorLength
            restricted
            when (ExteriorWidth entered
            or    ExteriorHeight entered)
        
        RequireExteriorWidth
            restricted
            when (ExteriorLength entered
            or    ExteriorHeight entered)

        RequireExteriorHeight
            restricted
            when (ExteriorLength entered
            or    ExteriorWidth entered)

    Derived Fields
        ComputedVolumeCapacity is a ComputeField
            type is like DimensionSize
            (InteriorHeight * InteriorLength * InteriorWidth)

        DerivedVehicleTypeDisplay is a MessageField
			default label is "VehicleType"
			"<VehicleType>_-_<VehicleType.Description>"

    Relations
        VehicleRel
            one-to-many relation to Vehicle
            Field Mapping uses symbolic key
                related.Company   = Company
            Instance Selection
                where (related.VehicleType = VehicleType)

	Field Rules
        VehicleType
            required

        Description
            required
            
        InteriorLength 
            if (RequireInteriorLength)
                required

        InteriorWidth
            if (RequireInteriorWidth)
                required

        InteriorHeight 
            if (RequireInteriorHeight)
                required

        ExteriorLength 
            if (RequireExteriorLength)
                required

        ExteriorWidth 
            if (RequireExteriorWidth)
                required

        ExteriorHeight 
            if (RequireExteriorHeight)
                required
     
        VehicleWeightCapacity
            required
            
        VehicleVolumeCapacity
            default to ComputedVolumeCapacity
            
            if (InteriorLength entered 
            or  InteriorHeight entered 
            or  InteriorWidth entered)
                constraint (ValidVolumeCapacity)
                    "VolumeCapacityShouldNotBeGreaterThanComputed_Interior_Dimensions"


	Actions		
        Create is a Create Action

        Update is an Update Action
            valid when (not RelatesToVehicle)
            
        Delete is a Delete Action
            valid when (not RelatesToVehicle)
    
				

