ReceivingDeliveryTicketLines is a BusinessClass
	owned by po
	prefix is RDTL
	sql name is RecDelTickLne
	classic name is PORDTL
	
	Ontology
		symbolic key is ReceivingDeliveryTicketLines
		
	Patterns
		implements StaticJava
		disable Auditing
 		disable EffectiveDated
	
	Persistent Fields
		SourceDocumentOrigin
		OperatingCompany 			is like InventoryCompany
		SourceDocumentNumeric
		SourceDocumentAlpha
		SourceDocumentLineNumber 	is a LineNumber
		OrderedLessCancelledQty 	is a Quantity
		BackOrderedQty				is like Quantity
		ReceivedQty					is like Quantity
	
	Derived Fields
		DerivedSourceDocumentLineNumber is a DerivedField
			type is Alpha 6
			if (SourceDocumentLineNumber entered)
				return SourceDocumentLineNumber
			else
				return blank

		DerivedUOM	is a DerivedField
			type is like UnitOfMeasure
			if (PurchaseOrderLineSourceRel exist)
				return PurchaseOrderLineSourceRel.EnteredUOM
			else
				return PurchaseOrderReceiptLine.PurchaseOrderLine.EnteredBuyUOM

		ItemRequiresSpecialHandling is a DerivedField
			type is Alpha 3
			if (HasLinesWithSpecialHandling)
				return "Yes"
			return "No"

	Conditions
		HasLinesWithSpecialHandling
			when (PurchaseOrderReceiptLine.DerivedSpecialHandlingCode entered)
	
	Relations
	
		PurchaseOrderLineSourceRel
			one-to-one relation to PurchaseOrderLineSource
			Field Mapping uses symbolic key
				related.Company												= ReceivingDeliveryTickets.Company
				related.PurchaseOrder										= PurchaseOrderReceiptLine.PurchaseOrderLine.PurchaseOrder
				related.PurchaseOrderLine									= PurchaseOrderReceiptLine.PurchaseOrderLine
				related.PurchaseOrderLineSource.SourceDocumentOrigin		= SourceDocumentOrigin
				related.PurchaseOrderLineSource.OperatingCompany			= OperatingCompany
				related.PurchaseOrderLineSource.SourceDocumentNumeric		= SourceDocumentNumeric
				related.PurchaseOrderLineSource.SourceDocumentAlpha			= SourceDocumentAlpha
				related.PurchaseOrderLineSource.SourceDocumentLineNumber	= SourceDocumentLineNumber

		PurchaseOrderLineDistributionsRel
			one-to-many relation to PurchaseOrderLineDistribution
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PurchaseOrder		= PurchaseOrderReceiptLine.PurchaseOrderLine.PurchaseOrder
				related.PurchaseOrderLine	= PurchaseOrderReceiptLine.PurchaseOrderLine

		PurchaseOrderReceiptLineCommentsForIDMRel 
			one-to-many relation to PurchaseOrderReceiptLineComment
			Field Mapping uses ByCommentType
				related.Company				 	 = Company
				related.PurchaseOrderReceipt 	 = PurchaseOrderReceiptLine.PurchaseOrderReceipt
				related.PurchaseOrderReceiptLine = PurchaseOrderReceiptLine
			Instance Selection
    			where (related.CommentType.PrintOnInternalDocuments
    			or     related.CommentType.PrintOnDeliveryTicket)  


	Field Rules
	
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted

		DeleteReceivingDeliveryTicketLines is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCompany	  	is like PurchasingCompany
				PrmActor 		is like Actor
				PrmRunBatchID	is like IDMPID
	
			Instance Selection
				where (Company = PrmCompany 
				and	   ReceivingDeliveryTickets.CurrentUser = PrmActor
				and	   ReceivingDeliveryTickets.RunBatchID 	= PrmRunBatchID)
				
			Action Rules
				Empty Set Rules
					invoke DeleteReceivingDeliveryTickets ReceivingDeliveryTickets
						invoked.PrmCompany 	  = PrmCompany
						invoked.PrmRunBatchID = PrmRunBatchID
						invoked.PrmActor 	  = PrmActor	
						
				Set Rules
					Exit Rules
						invoke DeleteReceivingDeliveryTickets ReceivingDeliveryTickets
							invoked.PrmCompany 	  = PrmCompany
							invoked.PrmRunBatchID = PrmRunBatchID
							invoked.PrmActor 	  = PrmActor	
			
				Instance Rules
					invoke Delete
					
