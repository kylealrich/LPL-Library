PurgeNotToBePostedTransactionDetail is a BusinessTask
    owned by GeneralLedger

    Parameters
        FinanceEnterpriseGroup
		AccountingEntity
		AccountingEntityGroup
		EntityOption			is Numeric 1
	    	States
				Single				value is 1
				Group				value is 2
		DateOption					is Numeric 1
	    	States
				Year				value is 1
				Range				value is 2
		PostingDateYear					is Year
		PostingDateRange			is a DateRange
		
    Parameter Rules
        FinanceEnterpriseGroup
        	initial value is actor.context.FinanceEnterpriseGroup
        	required
        EntityOption
        	required
        		"<FinanceEnterpriseGroup.AccountingEntityLabel>OptionIsRequired"
		AccountingEntity
			if (EntityOption.Single)
				required
					"<FinanceEnterpriseGroup.AccountingEntityLabel>IsRequired"
			else
				AccountingEntity = blank
		AccountingEntityGroup
			if (EntityOption.Group)
				required
					"<FinanceEnterpriseGroup.AccountingEntityLabel>GroupIsRequired"
			else
				AccountingEntityGroup = blank      	
		DateOption
			required
		PostingDateYear
			if (DateOption.Year)
				required
			else
				PostingDateYear = blank      	
		PostingDateRange
			if (DateOption.Range)
				required
			else
				PostingDateRange = blank      	
		
    Actions

        PurgeNotToBePostedTransactionDetail is an Instance Action
			confirmation required
				"NotToBePostedRecordsWith_Posting_DateWithinTheDateRangeWillBePermanentlyDeleted"
	
			Local Fields
				LocalDateCounter			is Numeric size 3

            Action Rules
				if (DateOption.Range)
					LocalDateCounter = PostingDateRange.End - PostingDateRange.Begin
					constraint (LocalDateCounter <= 366)
						"DateRangeMustBeLessThanOrEqualToAYear"
				else
					PostingDateRange.Begin	= PostingDateYear first date in year
					PostingDateRange.End	= PostingDateYear last date in year

				if (AccountingEntity entered)
					invoke PurgeNotToBePostedDetail GLTransactionDetail in background
						invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
						invoked.PrmAccountingEntity		  = AccountingEntity
						invoked.PrmPostingDateRange		  = PostingDateRange
				else
					invoke PurgeNotToBePostedDetailForGroup FinanceEnterpriseGroup in background
						invoked.PrmAccountingEntityGroup  = AccountingEntityGroup
						invoked.PrmPostingDateRange		  = PostingDateRange									
