CustomerRepresentativeContactMessage is a BusinessClass
    owned by ar
    sql name is CustomerContactMessage
    prefix is CRCM

    Ontology
    	symbolic key is CustomerRepresentativeContactMessage
    	    		
	Persistent Fields
		CreationDateTime	is TimeStamp
		MessageTitle		is Alpha 100
		MessageText			is Text
		Status				is Numeric 1
			States
				Unread			value is 1
				Read			value is 2
				Deleted			value is 3
				HardDeleted		value is 4
		ReleaseStatus		is Numeric 1
			States
				Draft			value is 1
				Released		value is 2
		Priority			is Numeric 1
			States
				Low				value is 1
				Normal			value is 2
				High			value is 3
		SystemGenerated		is Boolean
		Attachment

	Derived Fields
		CreationDate is a DerivedField
			type is Date
			return CreationDateTime date
			
	Conditions
		MessageByCustomerRepresentativeContact
			restricted
 			when (actor.agent(CustomerRepresentativeContact).CustomerGroup = CustomerGroup
 			and	  actor.agent(CustomerRepresentativeContact).CustomerRepresentative = CustomerRepresentative
 			and	  actor.agent(CustomerRepresentativeContact).CustomerRepresentativeContact = CustomerRepresentativeContact)

		Deleted
			restricted
			when (Status.Deleted)
			
		Read
			restricted
			when (Status.Read)
		
		Unread
			restricted
			when (Status.Unread)
			
		Active
			restricted
			when (ReleaseStatus.Released
			and	 (Status.Unread
			or    Status.Read))

		HasAttachment
			restricted
			when (Attachment entered)
		
		CustomerRepresentativeMessageExists
			restricted
			when (CustomerRepresentativeMessage entered)
		
		Editable
			restricted
			when (!CustomerRepresentativeMessageExists 
			and   !SystemGenerated 
			and   ReleaseStatus.Draft)

		IsCustomerRepresentativeContact
			restricted
			when (actor.agent(CustomerRepresentativeContact).CustomerGroup = CustomerGroup
			and	  actor.agent(CustomerRepresentativeContact).CustomerRepresentative = CustomerRepresentative
			and	  actor.agent(CustomerRepresentativeContact).CustomerRepresentativeContact = CustomerRepresentativeContact)

		DeleteForeverValid
			restricted
			when (Deleted
			and	  IsCustomerRepresentativeContact)

		MarkAsDeletedValid
			restricted
			when (!Deleted
			and	  IsCustomerRepresentativeContact)

		MoveToInboxValid
			restricted
			when (Deleted
			and	  IsCustomerRepresentativeContact)

		MarkAsUnreadValid
			restricted
			when (Read
			and   IsCustomerRepresentativeContact)

		MarkAsReadValid
			restricted
			when (Unread
			and	  IsCustomerRepresentativeContact)

	Sets
		ByCreationDateTime
			Sort Order
				CustomerGroup
    			CreationDateTime
 				CustomerRepresentativeContactMessage
 				CustomerRepresentativeMessage
 				CustomerRepresentative
 				CustomerRepresentativeContact		

		ByPriority
			Sort Order
				CustomerGroup
    			Priority
 				CustomerRepresentativeContactMessage
 				CustomerRepresentativeMessage
 				CustomerRepresentative
 				CustomerRepresentativeContact

		ByStatus
			Sort Order
				CustomerGroup
    			Status
 				CustomerRepresentativeContactMessage
 				CustomerRepresentativeMessage
 				CustomerRepresentative
 				CustomerRepresentativeContact

	Field Rules
		MessageTitle
			if (!SystemGenerated)
				required
			
		CreationDateTime
			initial value is current timestamp
			
		Status
			initial value is Status.Unread
		
		Priority
			initial value is Priority.Normal

		ReleaseStatus
			initial value is ReleaseStatus.Draft

 	Actions
		Create is a Create Action
			Exit Rules
				if (SystemGenerated 
				or  CustomerRepresentativeMessageExists)
					ReleaseStatus = ReleaseStatus.Released

		Update is an Action
			valid when (Editable)
		
		Delete is an Action
			valid when (Editable)

		Release is an Instance Action
			valid when (ReleaseStatus.Draft)

			Action Rules
				ReleaseStatus = ReleaseStatus.Released
		
		MarkAsRead is an Instance Action
			valid when (MarkAsReadValid)
			
			Action Rules
				Status = Status.Read
				
		MarkAsUnread is an Instance Action
			valid when (MarkAsUnreadValid)

			Action Rules
				Status = Status.Unread

		MoveToInbox is an Instance Action
			valid when (MoveToInboxValid)

			Action Rules
				Status = Status.Unread
				
		MarkAsDeleted is an Instance Action
			valid when (MarkAsDeletedValid)
			
			Action Rules
				Status = Status.Deleted
				
		DeleteForever is an Instance Action
			valid when (DeleteForeverValid)
			
			Action Rules
				invoke Delete
