FileCreationDataMapping is a BusinessClass
    owned by filecreation
    prefix is FCDMP

	Ontology
		symbolic key is FileCreationDataMapping

    Patterns
	    implements LightweightAuditing
        disable EffectiveDated
        disable AsOfDateProcessing

    Persistent Fields
		Description					is Alpha size 60
		MappingOrder				is Numeric 3
			default label is "Order"			
		CompareType					is Numeric 1
            States
 				Equal					value is 0
				Contains				value is 1

		AlphaFromValue				is Alpha 150
		DateFromValue				is Date
		DecimalFromValue			is Decimal 24.8
		NumericFromValue			is Numeric 20
		TimeFromValue				is Time
		BooleanFromValue			is Boolean

		AlphaToValue				is Alpha 150
		DateToValue					is Date
		DecimalToValue				is Decimal 24.8
		NumericToValue				is Numeric 20
		TimeToValue					is Time
		RecordHasBeenChanged		is Boolean
		
		DCKey						is Alpha 150
		DefaultMapping				is Boolean
		
	Local Fields
		LocalAlpha					is Alpha 150
		LocalJavaFormatting			is Alpha 40
		LocalDate					is Date
		LocalDecimal				is Decimal 24.8
		LocalNumeric				is Numeric 17
		LocalTime					is Time
		LocalLengthFormat			is Numeric 4
		LocalSubstringStart         is Numeric 4
		LocalSubstringEnd			is Numeric 4
		FieldBeingValidated			is Boolean 
		DoNotProcessRecord			is Boolean
		LocalAlphaLengthCompare     is Numeric 4 

	
	Field Groups

		FieldsNeededForRegenerateMappings
			MappingOrder
			CompareType
			AlphaFromValue				
			DateFromValue				
			DecimalFromValue			
			NumericFromValue			
			TimeFromValue			
			BooleanFromValue		
			AlphaToValue				
			DateToValue					
			DecimalToValue				
			NumericToValue			
			TimeToValue				
			DefaultMapping	
	
	Derived Fields
		DisplayFrom	is a DerivedField
			type is Alpha 150
			default label is "FromValue"
			if (FileCreationColumnDetail.FieldDataType.Alpha)
				return AlphaFromValue
			if (FileCreationColumnDetail.FieldDataType.Numeric)
				return NumericFromValue
			if (FileCreationColumnDetail.FieldDataType.Decimal)
				return DecimalFromValue
			if (FileCreationColumnDetail.FieldDataType.Date)
				return DateFromValue
			if (FileCreationColumnDetail.FieldDataType.Time)
				return TimeFromValue
			if (FileCreationColumnDetail.FieldDataType.Boolean)
				return BooleanFromValue
			if (FileCreationColumnDetail.FieldDataType.Text)
				return AlphaFromValue
			
		DisplayTo is a DerivedField
			type is Alpha 150
			default label is "ToValue"
			if (AlphaToValue entered)
				return AlphaToValue
			if (DateToValue entered)
				return DateToValue
			if (DecimalToValue entered)
				return DecimalToValue
			if (NumericToValue entered)
				return NumericToValue
			if (TimeToValue entered)
				return TimeToValue
				
		FormattedToValue is a DerivedField 
			type is Alpha 150
			if (FileCreationColumnDetail.FieldDataType.Alpha)
				include FileGeneration.FormatFieldAlpha
					replace AlphaValue with AlphaToValue
					replace Origin with FileCreationColumnDetail
				return LocalAlpha
					
			if (FileCreationColumnDetail.FieldDataType.Date)
				include FileGeneration.FormatFieldDate
					replace Date with DateToValue
					replace Origin with FileCreationColumnDetail
				return LocalAlpha
					
			if (FileCreationColumnDetail.FieldDataType.Decimal)
				include FileGeneration.FormatFieldDecimal
					replace Decimal with DecimalToValue
					replace Origin with FileCreationColumnDetail
				return LocalAlpha
					
			if (FileCreationColumnDetail.FieldDataType.Numeric)
				include FileGeneration.FormatFieldNumeric
					replace Numeric with NumericToValue
					replace Origin with FileCreationColumnDetail
				return LocalAlpha
					
			if (FileCreationColumnDetail.FieldDataType.Time)
				include FileGeneration.FormatFieldTime
					replace Time with TimeToValue
					replace Origin with FileCreationColumnDetail
				return LocalAlpha
				
		AlphaFromValueDerived is a DerivedField
			type is Alpha 151
			restricted
			default label is untranslatable 
			if (FileCreationColumnDetail.FieldDataType.Alpha
			or  FileCreationColumnDetail.FieldDataType.Text)
				AlphaFromValueDerived = AlphaFromValue + "a"
				return AlphaFromValueDerived
			else
				return AlphaFromValue
					
	Conditions
		CompareTypeContainsIsValid
			when (FileCreationColumnDetail.FieldDataType.Alpha
			or    FileCreationColumnDetail.FieldDataType.Text)
						
	Relations
		DataMappingSameOrderRel
			one-to-many relation to FileCreationDataMapping
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			 = FinanceEnterpriseGroup
				related.FileCreationSetup 		 = FileCreationSetup
				related.FileCreationRowDetail	 = FileCreationRowDetail
				related.FileCreationColumnDetail = FileCreationColumnDetail
			Instance Selection
				where (related.MappingOrder = MappingOrder
				and    related.FileCreationDataMapping not = FileCreationDataMapping)
				
		DefaultDataMappingRel
			one-to-many relation to FileCreationDataMapping
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			 = FinanceEnterpriseGroup
				related.FileCreationSetup 		 = FileCreationSetup
				related.FileCreationRowDetail	 = FileCreationRowDetail
				related.FileCreationColumnDetail = FileCreationColumnDetail
			Instance Selection
				where (related.DefaultMapping = true
				and    related.FileCreationDataMapping not = FileCreationDataMapping)
				
		SameFromValueDataMappingRel
			one-to-many relation to FileCreationDataMapping
			Field Mapping uses ByAlphaFromValue
				related.FinanceEnterpriseGroup			 = FinanceEnterpriseGroup
				related.FileCreationSetup 		 = FileCreationSetup
				related.FileCreationRowDetail	 = FileCreationRowDetail
				related.FileCreationColumnDetail = FileCreationColumnDetail
				related.AlphaFromValue     		 = AlphaFromValue
				related.DefaultMapping			 = false
			Instance Selection
		 		where (related.FileCreationDataMapping not = FileCreationDataMapping)
		 		
		FileCreationDataMappingCacheRel
			one-to-one relation to FileCreationDataMappingCache
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				   = FinanceEnterpriseGroup
				related.FileCreationSetup 			   = FileCreationSetup
				related.FileCreationRowMappingCache    = FileCreationRowDetail.RowOrder
				related.FileCreationColumnMappingCache = FileCreationColumnDetail.ColumnOrder
				related.FileCreationDataMappingCache   = FileCreationDataMapping
			
	
	Field Rules
		FileCreationDataMapping
			constraint (SameFromValueDataMappingRel not exists)
				"FromValueIsAlreadyDefined"	
			
		MappingOrder
			constraint (CompareType.Contains)
				"OrderIsOnlyValidWithACompareTypeOfContains"
				
			constraint (DataMappingSameOrderRel not exists)
				"OrderAlreadyUsed"
							
		CompareType
			if (CompareType.Contains)
				constraint (CompareTypeContainsIsValid)
					"CompareTypeOfContainsCanOnlyBeSelectedForAlphaOrTextFields"
				constraint (not DefaultMapping)
					"CompareTypeOfContainsCannotBeSelectedForDefaultMappings"
				
		AlphaFromValue
			if (FileCreationColumnDetail.FieldDataType.Date)
				force default to DateFromValue
			if (FileCreationColumnDetail.FieldDataType.Decimal)
				force default to DecimalFromValue
			if (FileCreationColumnDetail.FieldDataType.Numeric)
				force default to NumericFromValue
			if (FileCreationColumnDetail.FieldDataType.Time)
				force default to TimeFromValue
			if (FileCreationColumnDetail.FieldDataType.Boolean)
				force default to BooleanFromValue
			if (FileCreationColumnDetail.FieldDataType.Alpha
			or  FileCreationColumnDetail.FieldDataType.Text)
				constraint (not DefaultMapping)
					"FromValueCannotBeEnteredForDefaultMapping"	
			
				
		DCKey
			cannot be changed
			
		DefaultMapping							
			constraint (DefaultDataMappingRel not exists)
				"OnlyOneDefaultMappingCanBeDefined"
					
		DateFromValue	
			constraint (not DefaultMapping)
				"FromValueCannotBeEnteredForDefaultMapping"

						
		DecimalFromValue
			constraint (not DefaultMapping)
				"FromValueCannotBeEnteredForDefaultMapping"	
					
		NumericFromValue
			constraint (not DefaultMapping)
				"FromValueCannotBeEnteredForDefaultMapping"	
					
		TimeFromValue
			constraint (not DefaultMapping)
				"FromValueCannotBeEnteredForDefaultMapping"	
		
		BooleanFromValue	
			constraint (not DefaultMapping)
				"FromValueCannotBeEnteredForDefaultMapping"							
		
	Sets
		ByAlphaFromValue
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				FileCreationRowDetail
				FileCreationColumnDetail
				AlphaFromValue
				DefaultMapping
				FileCreationDataMapping
				
		ByDCKey
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				FileCreationRowDetail
				FileCreationColumnDetail
				DCKey
				FileCreationDataMapping
				
		ByCompareType
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				FileCreationRowDetail
				FileCreationColumnDetail
				CompareType
				MappingOrder
				FileCreationDataMapping

	
	Audit Entry Rules
		if (FileCreationSetup.UseDynamicRowProcessing
		and (FieldsNeededForRegenerateMappings changed
		or   action type.Delete
		or   action type.Create))
			if (!FileCreationSetup.RegenerateMappingsFlag.Needed)
				invoke SetRegenerateMappingsFlagToNeeded FileCreationSetup
			for each FileCreationRowDetail.RowIsLinkedRel
				if (!each.FileCreationSetup.RegenerateMappingsFlag.Needed)
					invoke SetRegenerateMappingsFlagToNeeded each.FileCreationSetup
												
														
	Actions
		Create is a Create Action

		ImportCreate is a Create Action
			restricted
			bypass field rules
							
		Update is an Update Action
		
			Action Rules
				if (Description changed
				or	AlphaFromValue changed
				or	DateFromValue changed
				or	DecimalFromValue changed
				or	NumericFromValue changed
				or	TimeFromValue changed
				or	BooleanFromValue changed
				or	AlphaToValue changed
				or	DateToValue changed
				or	DecimalToValue changed
				or	NumericToValue changed
				or	TimeToValue changed)
					RecordHasBeenChanged = true

		ImportUpdate is an Update Action
			restricted
			bypass field rules
							
		DataLoadUpdate is an Update Action
			restricted
																																																										
		Delete is a Delete Action
