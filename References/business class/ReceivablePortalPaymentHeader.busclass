ReceivablePortalPaymentHeader is a BusinessClass
	owned by ar

	prefix is RPPH

	Ontology
		symbolic key is ReceivablePortalPaymentHeader


	Patterns
		disable AuditIndex
		disable Auditing


	Persistent Fields


		ThirdPartyPaymentProcessingAction							is Alpha size 24		
			default label is "Action"


		ThirdPartyPaymentProcessingContactId						is Alpha size 36
			default label is "ContactId"


		ThirdPartyPaymentProcessingCustomerId						is a Customer
		ThirdPartyPaymentProcessingCompany							is a ReceivableCompany


		ThirdPartyPaymentProcessingDeveloperId						is Alpha size 12
			default label is "DeveloperId"
			restricted




		ThirdPartyPaymentProcessingGeneratedACHPayFormLink			is Text 
		ThirdPartyPaymentProcessingGeneratedCCPayFormLink			is Text 

		ThirdPartyPaymentProcessingGeneratedHashKey					is Alpha size 64 
		ThirdPartyPaymentProcessingGeneratedHexString				is Text 
		ThirdPartyPaymentProcessingGeneratedPayFormLink				is Text	




		ThirdPartyPaymentProcessingLocationId						is Alpha size 36	
			default label is "LocationId"

		ThirdPartyPaymentProcessingPayaJSONResponse					is JSONObject
		ThirdPartyPaymentProcessingPayaJSONResponseGet				is JSONObject

		ThirdPartyPaymentProcessingPaymentMethod					is Alpha size 5 
			default label is "PaymentMethod"

		ThirdPartyPaymentProcessingProductTransactionId				is Alpha size 36 
			default label is "ProductTransactionId"
		ThirdPartyPaymentProcessingProductTransactionIdACH			is Alpha size 36 
			default label is "ProductTransactionIdACH"
		ThirdPartyPaymentProcessingProductTransactionIdCC			is Alpha size 36 
			default label is "ProductTransactionIdCC"

		ThirdPartyPaymentProcessingRequest							is JSONObject

		ThirdPartyPaymentProcessingResponseAccountVaultId			is Alpha size 36

		ThirdPartyPaymentProcessingResponseCustomerID				is a Customer



		ThirdPartyPaymentProcessingResponsePaymentMethod			is Alpha size 5
		ThirdPartyPaymentProcessingResponseReasonCodeId				is a PayaResponseReasonCode	
		ThirdPartyPaymentProcessingResponseStatusId					is a PayaResponseStatusId	//IEFIN-99049 was "is Numeric size 3" 
		ThirdPartyPaymentProcessResponseTransactionAmount			is Decimal 10.2
		ThirdPartyPaymentProcessResponseTransactionApiId			is Numeric size 15
		ThirdPartyPaymentProcessingResponseTransactionId			is Alpha size 36 



		ThirdPartyPaymentProcessingShowCVV							is Alpha size 1
			default label is "ShowCVV"
		ThirdPartyPaymentProcessingTransactionAmount				is an InternationalAmount 
			default label is "TransactionAmount"
		ThirdPartyPaymentProcessingTransactionApiId					is Numeric size 15 
			default label is "TransactionApiId"
		ThirdPartyPaymentProcessingTransactionType					is Alpha size 24 
			default label is "TransactionType"

		ThirdPartyPaymentProcessingUserApiKey						is Alpha size 36 
			default label is "UserApiKey"
		ThirdPartyPaymentProcessingUserHashKey						is Alpha size 36 
			default label is "UserHashKey"
		ThirdPartyPaymentProcessingUserId							is Alpha size 36 
			default label is "UserId"

		ThirdPartyPaymentProcessingReceivablePaymentHeader			is a ReceivablePaymentHeader
			default label is "ReceivablePaymentHeader"


		ThirdPartyPaymentProcessingReceivableProcessLevel			is a ReceivableProcessLevel
			default label is "ReceivableProcessLevel"




	Local Fields
		LocalCustomerGroup													is a CustomerGroup
		LocalCustomerRepresentative											is a CustomerRepresentative
			context of LocalCustomerGroup
		LocalReceivablePayment												is a ReceivablePayment view
		SumOfReceivablePortalPaymentDetailRelAmountsDone					is Boolean
		SumOfReceivablePortalPaymentDetailRelPortalRequestedAppliedAmount	is an InternationalAmount
		LocalThirdPartyPaymentProcessingPayaJSONResponse					is JSONObject	
		LocalPortalRequestedAppliedAmount									is an InternationalAmount	

	Transient Fields
		TransientThirdPartyPaymentProcessingPaymentMethod			is Alpha size 5 
			default label is "PaymentMethod"
			States
				ACH						value is "ACH"
				CreditCard				value is "CC"



	Context Fields



	Field Rules
		CustomerGroup
			initial value is actor.context.CustomerGroup	
			force default to actor.context.CustomerGroup	


		CustomerRepresentative
			initial value is CustomerRepresentativeContact.CustomerRepresentative
			force default to CustomerRepresentativeContact.CustomerRepresentative


		ThirdPartyPaymentProcessingCustomerId
			initial value is CustomerRepresentativeContact.Customer
			force default to CustomerRepresentativeContact.Customer


		ThirdPartyPaymentProcessingCompany
			initial value is actor.context.CustomerGroup.DerivedThirdPartyPaymentProcessingCompany
			force default to actor.context.CustomerGroup.DerivedThirdPartyPaymentProcessingCompany







		ThirdPartyPaymentProcessingLocationId
			initial value is actor.context.CustomerGroup.ThirdPartyPaymentProcessingLocationId
			force default to actor.context.CustomerGroup.ThirdPartyPaymentProcessingLocationId


		ThirdPartyPaymentProcessingContactId
			initial value is actor.context.CustomerGroup.ThirdPartyPaymentProcessingContactId
			force default to actor.context.CustomerGroup.ThirdPartyPaymentProcessingContactId


		ThirdPartyPaymentProcessingUserApiKey
			initial value is actor.context.CustomerGroup.ThirdPartyPaymentProcessingUserApiKey
			force default to actor.context.CustomerGroup.ThirdPartyPaymentProcessingUserApiKey


		ThirdPartyPaymentProcessingUserHashKey
			initial value is actor.context.CustomerGroup.ThirdPartyPaymentProcessingUserHashKey
			force default to actor.context.CustomerGroup.ThirdPartyPaymentProcessingUserHashKey


		ThirdPartyPaymentProcessingUserId
			initial value is actor.context.CustomerGroup.ThirdPartyPaymentProcessingUserId
			force default to actor.context.CustomerGroup.ThirdPartyPaymentProcessingUserId


		ThirdPartyPaymentProcessingReceivableProcessLevel
			initial value is actor.context.CustomerGroup.DerivedThirdPartyPaymentProcessingCashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRelThirdPartyPaymentProcessingReceivableProcessLevel
			force default to actor.context.CustomerGroup.DerivedThirdPartyPaymentProcessingCashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRelThirdPartyPaymentProcessingReceivableProcessLevel


		ThirdPartyPaymentProcessingPaymentMethod
			if (TransientThirdPartyPaymentProcessingPaymentMethod = "ACH")
				ThirdPartyPaymentProcessingPaymentMethod = "ach"
			if (TransientThirdPartyPaymentProcessingPaymentMethod = "CC")
				ThirdPartyPaymentProcessingPaymentMethod = "cc"


		ThirdPartyPaymentProcessingAction
			if (ThirdPartyPaymentProcessingPaymentMethod entered)
				required
			if (ThirdPartyPaymentProcessingPaymentMethod = "ach")
				ThirdPartyPaymentProcessingAction = "debit"
			if (ThirdPartyPaymentProcessingPaymentMethod = "cc")
				ThirdPartyPaymentProcessingAction = "sale"
			if (ThirdPartyPaymentProcessingPaymentMethod not entered)
				initialize ThirdPartyPaymentProcessingAction


		ThirdPartyPaymentProcessingProductTransactionId
			if (ThirdPartyPaymentProcessingPaymentMethod entered)
				required
			if (ThirdPartyPaymentProcessingPaymentMethod = "ach")
				ThirdPartyPaymentProcessingProductTransactionId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingProductTransactionIdACH
			if (ThirdPartyPaymentProcessingPaymentMethod = "cc")
				ThirdPartyPaymentProcessingProductTransactionId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingProductTransactionIdCC
			if (ThirdPartyPaymentProcessingPaymentMethod not entered)
				initialize ThirdPartyPaymentProcessingProductTransactionId


	Form Invokes
		CreateReceivablePortalPaymentHeader
			if (!CustomerRepresentativeContactRel.PaymentInProgress)
				invoke Create ReceivablePortalPaymentHeader	
					invoked.CustomerGroup					= this instance.CustomerRepresentative.CustomerGroup
					invoked.CustomerRepresentative			= this instance.CustomerRepresentative

					invoked.ThirdPartyPaymentProcessingCustomerId					= this instance.ThirdPartyPaymentProcessingCustomerId







			else
				constraint (!CustomerRepresentativeContactRel.PaymentInProgress)
					"ActionNotAvailableWhileAnotherPaymentIsInProgress"



	Rule Blocks


		CreateReceivablePaymentHeaderRuleBlock



			invoke OpenBatchUnreleased.Create ReceivablePaymentHeader
				assign result to ThirdPartyPaymentProcessingReceivablePaymentHeader
				invoked.Company						= ThirdPartyPaymentProcessingCompany
				invoked.ReceivableProcessLevel		= ThirdPartyPaymentProcessingReceivableProcessLevel
				invoked.BatchCount					= 1
				invoked.Reference					= ThirdPartyPaymentProcessingResponseTransactionId
				invoked.BatchTotal					= ThirdPartyPaymentProcessingTransactionAmount	
				invoked.Origin						= "T" 






			invoke Unreleased.CreateCustomerPayment ReceivablePayment
				assign result to LocalReceivablePayment
				invoked.Company						= ThirdPartyPaymentProcessingCompany
				invoked.ReceivablePaymentHeader		= ThirdPartyPaymentProcessingReceivablePaymentHeader
				invoked.Customer					= ThirdPartyPaymentProcessingCustomerId
				invoked.PaymentNumber				= ThirdPartyPaymentProcessingTransactionApiId
				invoked.TransactionAmount			= ThirdPartyPaymentProcessingTransactionAmount	
				invoked.Origin						= "T" 









			for each ReceivablePortalPaymentDetailRel
				invoke Create ReceivablePaymentRemittance
					invoked.Company						= ThirdPartyPaymentProcessingCompany
					invoked.ReceivablePaymentHeader		= ThirdPartyPaymentProcessingReceivablePaymentHeader
					invoked.ReceivablePayment			= LocalReceivablePayment.ReceivablePayment
					invoked.ReceivableInvoiceType		= each.ReceivableInvoiceDetailType
					invoked.InvoiceCompany				= each.FromReceivableCompany	
					invoked.ReceivableInvoice			= each.Invoice
					invoked.InvoiceDetail				= each.PaymentSeq				


					if (each.ReceivableInvoiceDetailType.CreditMemo)
						invoked.AppldAmt					= each.PortalRequestedAppliedAmount * -1
					else

						invoked.AppldAmt					= each.PortalRequestedAppliedAmount




















			for each ReceivablePortalPaymentDetailRel	
				LocalPortalRequestedAppliedAmount	= each.PortalRequestedAppliedAmount	

				invoke PortalPaymentUpdate each.ReceivableInvoiceDetailRel

					invoked.PortalRequestedAppliedAmount	= LocalPortalRequestedAppliedAmount	
					invoked.PortalPaymentSelected			= true	

			if (CustomerGroup.ReleasePortalPaymentProcessAutoCashApplication)	
				invoke OpenBatchUnreleased.Release InstanceUnreleasedReceivablePaymentHeaderRel	

				invoke AutoCashApplication AutoCashApplication	
					invoked.PrmFinanceEnterpriseGroup	 = this instance.CustomerGroup.CustomerBusinessGroup.FinanceEnterpriseGroup
					invoked.PrmCompany					 = this instance.ThirdPartyPaymentProcessingCompany
					invoked.PrmGlDate					 = system current corporate date
					invoked.PrmBatchNumber				 = this instance.ThirdPartyPaymentProcessingReceivablePaymentHeader










		SumReceivablePortalPaymentDetailRelAmounts
			if (!SumOfReceivablePortalPaymentDetailRelAmountsDone)
				initialize SumOfReceivablePortalPaymentDetailRelPortalRequestedAppliedAmount
					for each ReceivablePortalPaymentDetailRel
						SumOfReceivablePortalPaymentDetailRelPortalRequestedAppliedAmount	 += each.PortalRequestedAppliedAmount
				SumOfReceivablePortalPaymentDetailRelAmountsDone = true


	Derived Fields
		DerivedSumOfReceivablePortalRequestedAppliedAmount is a DerivedField
			type is like InternationalAmount
			default label is "TotalRequestedAmountToPay"
			include SumReceivablePortalPaymentDetailRelAmounts
			return (SumOfReceivablePortalPaymentDetailRelPortalRequestedAppliedAmount)

		PayaJSONTransactionRequest is a DerivedField
			type is JSONObject
			PayaJSONTransactionRequest = template.PayaTransactionRequest_Template document for this instance

		PayaJSONPayFormRequest is a DerivedField
			type is JSONObject
			PayaJSONPayFormRequest = template.PayaPayFormRequest_Template document for this instance


		DerivedThirdPartyPaymentProcessingCashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRelThirdPartyPaymentProcessingReceivableProcessLevel is a DerivedField
			type is like ReceivableProcessLevel
			return first CashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRel.ThirdPartyPaymentProcessingReceivableProcessLevel

		DerivedThirdPartyPaymentProcessingCompany is a DerivedField
			type is like ReceivableCompany
			return first CashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRel.LegalEntity






		DerivedPaymentCurrency is a DerivedField
			type is Alpha 5
			return ThirdPartyPaymentProcessingPaymentCurrencyConfigurationParameterRel.Value

		DerivedThirdPartyPaymentProcessingPayformURI is a DerivedField
			type is Alpha size 250	
			return ThirdPartyPaymentProcessingPayformURIConfigurationParameterRel.Value

		DerivedThirdPartyPaymentProcessingEndpoint is a DerivedField
			type is Alpha size 250
			return ThirdPartyPaymentProcessingEndpointConfigurationParameterRel.Value

		DerivedThirdPartyPaymentProcessingUserId is a DerivedField
			type is Alpha size 36
			return CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.ThirdPartyPaymentProcessingUserId

		DerivedThirdPartyPaymentProcessingDeveloperId is a NativeField	
			type is Alpha size 12
			restricted


		DerivedThirdPartyPaymentProcessingUserHashKey is a DerivedField
			type is Alpha size 36
			return CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.ThirdPartyPaymentProcessingUserHashKey

		DerivedPaymentNumber is a DerivedField 
			type is AlphaUpper size 22
			restricted
			return (CustomerRepresentativeContact+"-"+ThirdPartyPaymentProcessingTransactionApiId)

		DerivedReceivablePortalPaymentDetailRelCount is a DerivedField
			type is Numeric size 21
			restricted
			return (instance count of ReceivablePortalPaymentDetailRel)

		TenantID is a DerivedField
			type is Alpha 25
			restricted
			return tenant id

		DerivedThirdPartyPaymentProcessingACHSecCode is a DerivedField
			type is Alpha size 3
			if (ThirdPartyPaymentProcessingPaymentMethod = "ach")
				return "WEB"
			else
				return blank


		DerivedThirdPartyPaymentProcessingProductionUseOnly is a DerivedField
			type is Boolean
			restricted
			if (ThirdPartyPaymentProcessingProductionUseOnly)
				return true
			else
				return false



	Relations

		CustomerRepresentativeContactRel
			one-to-one relation to CustomerRepresentativeContact
			Field Mapping uses symbolic key
				related.CustomerGroup					= CustomerGroup 
				related.CustomerRepresentative			= CustomerRepresentative 
				related.CustomerRepresentativeContact	= CustomerRepresentativeContact 


		ThisInstanceCustomerRepresentativeContactRel
			one-to-one relation to CustomerRepresentativeContact
			Field Mapping uses symbolic key
				related.CustomerGroup					= reference to this instance 
				related.CustomerRepresentative			= reference to this instance 
				related.CustomerRepresentativeContact	= reference to this instance 

		ActorContextCustomerRepresentativeContactRel
			one-to-one relation to CustomerRepresentativeContact
			Field Mapping uses symbolic key
				related.CustomerGroup					= actor.context.CustomerGroup 
				related.CustomerRepresentative			= actor.context.CustomerRepresentative 
				related.CustomerRepresentativeContact	= actor.context.CustomerRepresentativeContact 

		ActorContextCustomerGroupRel
			one-to-one relation to CustomerGroup
			Field Mapping uses symbolic key
				related.CustomerGroup					= actor.context.CustomerGroup 


		ReceivablePortalPaymentDetailRel is a ReceivablePortalPaymentDetail set


		ReceivableCompanyApplyCashAcrossReceivableCompaniesRel
			one-to-many relation to ReceivableCompany
			Field Mapping uses Set2
				related.CustomerGroupField.CustomerGroup	 = CustomerGroup
				related.Company								 = ThirdPartyPaymentProcessingCompany
			Instance Selection
				where (related.ApplyCashAcrossReceivableCompanies)


		ActorContextCustomerGroupDerivedThirdPartyPaymentProcessingCompanyApplyCashAcrossReceivableCompaniesRel
			one-to-many relation to ReceivableCompany
			Field Mapping uses Set2
				related.CustomerGroupField.CustomerGroup	 = actor.context.CustomerGroup
				related.Company								 = actor.context.CustomerGroup.DerivedThirdPartyPaymentProcessingCompany
			Instance Selection
				where (related.ApplyCashAcrossReceivableCompanies)


		InvoiceCustomerReceivableInvoiceDetailCompanyOnlyOpenRel 
			one-to-many relation to ReceivableInvoiceDetail
			Field Mapping uses OpenByCustomer
				related.Customer	= ThirdPartyPaymentProcessingCustomerId
			Instance Selection
				where (related.Company = CustomerRepresentativeContact.ReceivableCompany)


		InvoiceCustomerReceivableInvoiceDetailApplyCashAcrossReceivableCompaniesRel 
			one-to-many relation to ReceivableInvoiceDetail
			Field Mapping uses OpenByCustomer
				related.Customer	= ThirdPartyPaymentProcessingCustomerId

		InstanceUnreleasedReceivablePaymentHeaderRel	
			one-to-one relation to ReceivablePaymentHeader
			Field Mapping uses Set2	
				related.Company									= this instance.ThirdPartyPaymentProcessingCompany
				related.ReceivablePaymentHeader					= this instance.ThirdPartyPaymentProcessingReceivablePaymentHeader

		CashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRel
			one-to-many relation to CashManagementAccount
			Field Mapping uses symbolic key
				related.CashManagementGroup = CustomerGroup.CustomerBusinessGroup.FinanceEnterpriseGroup	
			Instance Selection
				where (related.ThirdPartyPaymentProcessingReceivableProcessLevel entered)

		ThirdPartyPaymentProcessingPaymentCurrencyConfigurationParameterRel	
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= "PAYACONNECT.PAYMENTCURRENCY"


		ThirdPartyPaymentProcessingEndpointConfigurationParameterRel
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= "PAYACONNECT.WSI.ENDPOINT"


		ThirdPartyPaymentProcessingPayformURIConfigurationParameterRel
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= "PAYACONNECT.WSI.PAYFORMURI"












	Conditions
		ActorContextCustomerGroupDerivedThirdPartyPaymentProcessingCompanyApplyCashAcrossReceivableCompaniesRelExists
			when (ActorContextCustomerGroupDerivedThirdPartyPaymentProcessingCompanyApplyCashAcrossReceivableCompaniesRel exists)

		ThirdPartyPaymentProcessingProductionUseOnly
			when (config.PAYACONNECT_ProductionUseOnly = true)

		WhenAllRequiredFieldsArePopulated
			when (CustomerGroup entered
			and CustomerRepresentative entered
			and CustomerRepresentativeContact entered
			and ThirdPartyPaymentProcessingCompany entered
			and ThirdPartyPaymentProcessingCustomerId entered

			and ThirdPartyPaymentProcessingLocationId entered
			and ThirdPartyPaymentProcessingContactId entered
			and ThirdPartyPaymentProcessingPaymentMethod entered
			and ThirdPartyPaymentProcessingAction entered
			and ThirdPartyPaymentProcessingTransactionApiId entered
			and ThirdPartyPaymentProcessingProductTransactionId entered)

		MultipleReceivablePortalPaymentDetailRelExist
			when (DerivedReceivablePortalPaymentDetailRelCount > 1)


	Actions

		Create is a Create Action



		Update is an Update Action
			Action Rules


		Delete is a Delete Action
			Action Rules



		BuildPayFormURL is an Instance Action
			restricted	


		PayWithACH is an Instance Action
			restricted 
			Parameters
				PrmThirdPartyPaymentProcessingCustomerRepresentative			is a CustomerRepresentative
				PrmThirdPartyPaymentProcessingCustomerRepresentativeContact	is a CustomerRepresentativeContact
				PrmThirdPartyPaymentProcessingCustomerId						is a Customer	
				PrmThirdPartyPaymentProcessingProductTransactionId				is Alpha size 36	
					default label is "ProductTransactionId"
				PrmThirdPartyPaymentProcessingTransactionAmount				is an InternationalAmount


				PrmThirdPartyPaymentProcessingCompany							is a ReceivableCompany

				PrmThirdPartyPaymentProcessingPayaJSONPayFormRequest			is JSONObject
				PrmThirdPartyPaymentProcessingPayaJSONTransactionRequest		is JSONObject


			Local Fields
				LocalThirdPartyPaymentProcessingTransactionApiId					is Numeric size 15

			Parameter Rules
				PrmThirdPartyPaymentProcessingProductTransactionId
					default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.ThirdPartyPaymentProcessingProductTransactionIdACH
					force default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.ThirdPartyPaymentProcessingProductTransactionIdACH

				PrmThirdPartyPaymentProcessingPayaJSONPayFormRequest
					initial value is CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONPayFormRequest
					default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONPayFormRequest
					force default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONPayFormRequest

				PrmThirdPartyPaymentProcessingPayaJSONTransactionRequest

					initial value is CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONTransactionRequest

				PrmThirdPartyPaymentProcessingTransactionAmount
					initial value is DerivedSumOfReceivablePortalRequestedAppliedAmount
					default to DerivedSumOfReceivablePortalRequestedAppliedAmount
					force default to DerivedSumOfReceivablePortalRequestedAppliedAmount

			Entrance Rules
				initialize ThirdPartyPaymentProcessingAction
				initialize ThirdPartyPaymentProcessingPaymentMethod
				initialize ThirdPartyPaymentProcessingTransactionAmount
				initialize ThirdPartyPaymentProcessingProductTransactionId
				invoke IncrementTransactionApiId ActorContextCustomerGroupRel	
				LocalThirdPartyPaymentProcessingTransactionApiId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingTransactionApiId	



				ThirdPartyPaymentProcessingTransactionApiId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingTransactionApiId 
				invoke Update actor.context.CustomerRepresentativeContact
					invoked.ThirdPartyPaymentProcessingTransactionApiId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingTransactionApiId	

			Action Rules

				ThirdPartyPaymentProcessingAction							 = "debit"
				ThirdPartyPaymentProcessingPaymentMethod					 = "ach"
				ThirdPartyPaymentProcessingTransactionAmount				 = PrmThirdPartyPaymentProcessingTransactionAmount
				ThirdPartyPaymentProcessingProductTransactionId				 = PrmThirdPartyPaymentProcessingProductTransactionId
















				invoke BuildPayFormURL



			Exit Rules




		PayWithCC is an Instance Action	
			restricted 
			Parameters
				PrmThirdPartyPaymentProcessingCustomerId						is a Customer
				PrmThirdPartyPaymentProcessingProductTransactionId				is Alpha size 36
					default label is "ProductTransactionId"
				PrmThirdPartyPaymentProcessingTransactionAmount					is an InternationalAmount	
				PrmThirdPartyPaymentProcessingTransactionApiId					is Numeric size 15
				PrmThirdPartyPaymentProcessingCompany							is a ReceivableCompany
				PrmThirdPartyPaymentProcessingPayaJSONPayFormRequest			is JSONObject
				PrmThirdPartyPaymentProcessingPayaJSONTransactionRequest		is JSONObject
				PrmThirdPartyPaymentProcessingCustomerRepresentative			is a CustomerRepresentative
				PrmThirdPartyPaymentProcessingCustomerRepresentativeContact		is a CustomerRepresentativeContact

			Local Fields
				LocalThirdPartyPaymentProcessingTransactionApiId					is Numeric size 15

			Parameter Rules
				PrmThirdPartyPaymentProcessingProductTransactionId
					default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.ThirdPartyPaymentProcessingProductTransactionIdCC
					force default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.ThirdPartyPaymentProcessingProductTransactionIdCC

				PrmThirdPartyPaymentProcessingPayaJSONPayFormRequest
					initial value is CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONPayFormRequest
					default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONPayFormRequest
					force default to CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONPayFormRequest

				PrmThirdPartyPaymentProcessingPayaJSONTransactionRequest

					initial value is CustomerRepresentativeContact.CustomerRepresentative.CustomerGroup.PayaJSONTransactionRequest

				PrmThirdPartyPaymentProcessingTransactionAmount	
					initial value is DerivedSumOfReceivablePortalRequestedAppliedAmount
					default to DerivedSumOfReceivablePortalRequestedAppliedAmount
					force default to DerivedSumOfReceivablePortalRequestedAppliedAmount


			Entrance Rules
				initialize ThirdPartyPaymentProcessingAction
				initialize ThirdPartyPaymentProcessingPaymentMethod
				initialize ThirdPartyPaymentProcessingTransactionAmount	
				initialize ThirdPartyPaymentProcessingProductTransactionId
				invoke IncrementTransactionApiId ActorContextCustomerGroupRel	
				LocalThirdPartyPaymentProcessingTransactionApiId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingTransactionApiId	

				ThirdPartyPaymentProcessingTransactionApiId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingTransactionApiId	

				invoke Update actor.context.CustomerRepresentativeContact
					invoked.ThirdPartyPaymentProcessingTransactionApiId = actor.context.CustomerGroup.ThirdPartyPaymentProcessingTransactionApiId 

			Action Rules

				ThirdPartyPaymentProcessingAction						 = "sale"
				ThirdPartyPaymentProcessingPaymentMethod				 = "cc"
				ThirdPartyPaymentProcessingTransactionAmount			 = PrmThirdPartyPaymentProcessingTransactionAmount	
				ThirdPartyPaymentProcessingProductTransactionId			 = PrmThirdPartyPaymentProcessingProductTransactionId









				invoke BuildPayFormURL



			Exit Rules



		GetResponseFromThirdPartyPaymentProcessing is an Instance Action
			Action Rules
				invoke Transaction PayaConnect
					invoked.PAYACONNECTWSIHTTPHEADERUSERID			= ThirdPartyPaymentProcessingUserId

					invoked.PAYACONNECTWSIHTTPHEADERUSERAPIKEY		= ThirdPartyPaymentProcessingUserApiKey

					invoked.PAYACONNECTWSIHTTPHEADERDEVELOPERID		= DerivedThirdPartyPaymentProcessingDeveloperId

					invoked.PAYACONNECTWSIHTTPHEADERCONTENTTYPE		= "application/json"
					invoked.TransactionApiId						= ThirdPartyPaymentProcessingTransactionApiId

					invoked.ContactId								= ThirdPartyPaymentProcessingContactId









					LocalThirdPartyPaymentProcessingPayaJSONResponse		= result.ThirdPartyPaymentProcessingResponseGet	

				ThirdPartyPaymentProcessingResponseTransactionId		 = LocalThirdPartyPaymentProcessingPayaJSONResponse select "$.transactions[0].id"	

				ThirdPartyPaymentProcessResponseTransactionApiId		 = LocalThirdPartyPaymentProcessingPayaJSONResponse select "$.transactions[0].transaction_api_id"	

				ThirdPartyPaymentProcessingResponseStatusId				 = LocalThirdPartyPaymentProcessingPayaJSONResponse select "$.transactions[0].status_id"	

				ThirdPartyPaymentProcessingResponseReasonCodeId			 = LocalThirdPartyPaymentProcessingPayaJSONResponse select "$.transactions[0].reason_code_id"	



				ThirdPartyPaymentProcessResponseTransactionAmount		 = LocalThirdPartyPaymentProcessingPayaJSONResponse select "$.transactions[0].transaction_amount"	


				invoke Update CustomerRepresentativeContact	
					invoked.ThirdPartyPaymentProcessResponseTransactionApiId	= ThirdPartyPaymentProcessResponseTransactionApiId	
					invoked.ThirdPartyPaymentProcessingResponseStatusId			= ThirdPartyPaymentProcessingResponseStatusId	
					invoked.ThirdPartyPaymentProcessingResponseReasonCodeId		= ThirdPartyPaymentProcessingResponseReasonCodeId	
					invoked.ThirdPartyPaymentProcessResponseTransactionAmount	= ThirdPartyPaymentProcessResponseTransactionAmount	



		InitializeThirdPartyPaymentProcessingGeneratedPayFormLink is an Instance Action	
			Action Rules
				initialize ThirdPartyPaymentProcessingGeneratedPayFormLink


		CreateReceivablePaymentHeaderInstanceAction is an Instance Action	
			restricted
			completion message is "PaymentProcessed"
			Entrance Rules

				initialize ThirdPartyPaymentProcessingReceivablePaymentHeader

			Action Rules	

				include CreateReceivablePaymentHeaderRuleBlock	
			Exit Rules	

				invoke Delete












		UpdateTransactionAmount is an Instance Action	
			restricted
			Entrance Rules

				initialize ThirdPartyPaymentProcessingTransactionAmount

			Action Rules	

				ThirdPartyPaymentProcessingTransactionAmount = DerivedSumOfReceivablePortalRequestedAppliedAmount	

			Exit Rules	


		UpdateProcessLevel is an Instance Action	
			restricted
			Entrance Rules

				initialize ThirdPartyPaymentProcessingReceivableProcessLevel

			Action Rules	



				ThirdPartyPaymentProcessingReceivableProcessLevel = DerivedThirdPartyPaymentProcessingCashManagementAccountSetupWithThirdPartyPaymentProcessingReceivableProcessLevelRelThirdPartyPaymentProcessingReceivableProcessLevel	

			Exit Rules	


		InitializeReceivablePortalPaymentHeader is an Instance Action	
			Action Rules
				initialize ReceivablePortalPaymentHeader




