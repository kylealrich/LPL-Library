DistributionClerk is a BusinessClass
    owned by wh
    prefix is DCLK
    representative text is "<DistributionClerk.Employee.FirstLastNameID>"
    representative image is DistributionClerk.Employee.ResourcePicture.File
		display as portrait image
		missing image is random background
			foreground text is "<DistributionClerk.Employee.Name.Initials>"

    Ontology
	    symbolic key is DistributionClerk

	Patterns
		implements StaticJava
		disable AuditIndex    

    Persistent Fields
		Company				        is an InventoryCompany
			disable surrogates
        DisableReceivingPutawayTask        is Boolean
        DisableShipmentPickingTask         is Boolean
        DisableShipmentPackingTask         is Boolean
        DisableShipmentFulfillmentTask     is Boolean
        DisableStockRotationTask           is Boolean
        DisablePhysicalInventoryTask       is Boolean

    Derived Fields
        CreateDistributionClerkDefaultLabel	is a LabelField
			restricted
			"CreateDistributionClerk"

		DistributionClerkProfileLabel is a LabelField
			restricted
			"<DistributionClerk.Employee.Name.PreferredFirstAndLastName>'sDistributionClerkProfile"

        ActiveMessage is a MessageField
			restricted
			"Active"

		InactiveMessage is a MessageField
			restricted
			"Inactive"

        DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return DistributionClerkProfileLabel
			else
				return CreateDistributionClerkDefaultLabel

        StatusMessage is a DerivedField
			type is MessageField
			default label is "Status"
			if (EmployeeIsActive)
				return ActiveMessage
			else
				return InactiveMessage

        DerivedEmployeeInitials is a DerivedField
            type is Text
            return EmployeeRel.Name.Initials

        DerivedEmployeeName is a DerivedField
			type is Alpha size 230
            default label is "Name"
            return EmployeeRel.PresentationNameSnapshot

        DerivedFirstLastNameID is a DerivedField
            type is Text
            default label is "Employee"
            return EmployeeRel.FirstLastNameID


        DerivedEmailContactMethod is a DerivedField
            type is Text
            return EmployeeRel.UseForWorkEmail.ContactMethod

        DerivedEmailAddress is a DerivedField
            type is Text
            default label is "EmailAddress"
            return EmployeeRel.EmployeeWorkEmailAddress

        DerivedIsWorkEmail is a DerivedField
            type is Text
            default label is "Used_asWorkEmail"
            if (first EmployeeContactEmailRel.IsWorkEmail)
                return "Yes"
            else
                return "No" 
        
        DerivedIsEmailPreferredContact is a DerivedField
            type is Text
            default label is "PreferredEmail"
            if (first EmployeeContactEmailRel.PreferredContact)
                return "Yes"
            else
                return "No"


        DerivedPhoneContactMethod is a DerivedField
            type is Text
            default label is "ContactMethod"
            return EmployeeRel.UseForWorkPhone.ContactMethod

        DerivedPhoneNumberDisplay is a DerivedField
            type is Text
            default label is "PhoneNumber"
            return first EmployeeContactPhoneRel.TelephoneDisplay

        DerivedIsWorkPhone is a DerivedField
            type is Text
            default label is "Used_asWorkPhone"
            if (first EmployeeContactPhoneRel.IsWorkTelephone)
                return "Yes"
            else
                return "No"

        DerivedIsPhonePreferredContact is a DerivedField
            type is Text
            default label is "PreferredTelephonePager_orFaxNumber"
            if (first EmployeeContactPhoneRel.PreferredContact)
                return "Yes"
            else
                return "No"
        

        DerivedIMAddress is a DerivedField
            type is Text
            default label is "InstantMessageAddress"
            return first EmployeeContactPhoneRel.ContactDetail.InstantMessageAddress

        DerivedIMNetwork is a DerivedField
            type is Text
            default label is "Network"
            return first EmployeeContactPhoneRel.ContactDetail.InstantMessageAddress.IMType
    
        DerivedIsIMPreferredContact is a DerivedField
            type is Text
            default label is "PreferredInstantMessage"
            if (first EmployeeContactPhoneRel.PreferredContact)
                return "Yes"
            else
                return "No"

        DerivedWhenAvailable is a DerivedField
            type is Text
            default label is "WhenAvailable"
            return first EmployeeContactPhoneRel.WhenAvailable

    Conditions
        RecordExists
            restricted
            when (DistributionClerk exists)

        EmployeeIsActive
            restricted
            when (EmployeeRel.IsActive)

        EmployeeWorkEmailAddressExists
            restricted
            when (EmployeeRel.EmployeeWorkEmailAddressExists)
        
        EmployeeWorkPhoneExists
            restricted
            when (EmployeeRel.EmployeeWorkPhoneExists)

        ShipmentPickingTaskDisabled
            restricted
            when (DisableShipmentPickingTask)

        ShipmentPackingTaskDisabled
            restricted
            when (DisableShipmentPackingTask)

        IsValidForActorContext
			restricted
			when ((actor.context.FinanceEnterpriseGroup != ""
			and   Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
			or   (actor.context.FinanceEnterpriseGroup = ""))        
        	
		IsMyDistributionClerk
			restricted
			when (DistributionClerk = actor.agent(Employee).Employee and IsValidForActorContext)

    Relations
        EmployeeRel
			one-to-one relation to Employee
			Field Mapping uses symbolic key
				related.HROrganization			        = HROrganization
				related.Employee				        = DistributionClerk

        EmployeeContactPhoneRel
            one-to-many relation to EmployeeContact
            Field Mapping uses ByContactMethod
                related.HROrganization                  = HROrganization
				related.Employee                        = DistributionClerk
				related.ContactMethod                   = EmployeeRel.UseForWorkPhone.ContactMethod
            Instance Selection
                where (related.EmployeeContact          = EmployeeRel.UseForWorkPhone)

        EmployeeContactEmailRel
            one-to-many relation to EmployeeContact
            Field Mapping uses ByContactMethod
                related.HROrganization                  = HROrganization
				related.Employee                        = DistributionClerk
				related.ContactMethod                   = EmployeeRel.UseForWorkEmail.ContactMethod
            Instance Selection
                where (related.EmployeeContact          = EmployeeRel.UseForWorkEmail)
    
        DistributionClerkZonesRel
            one-to-many relation to DistributionClerkZone
            Field Mapping uses symbolic key
                related.Company                         = Company
            Instance Selection
                where (related.DistributionClerk        = DistributionClerk)
                
        WarehouseZonesForClerkRel is a WarehouseZone set
            Instance Selection
                where (related.ZoneIsValidForDistributionClerk)

    Field Rules
        Company
        	required
                "RequiringCompany"
            constraint (Company.FinanceEnterpriseGroup.HROrganization = HROrganization)
				"FinanceEnterpriseGroupAssociatedWithCompanyDoesNotUseEnteredHROrganization"

            constraint (Company.AdvancedWarehouseInUse)
                "Distribution_ManagementIsNotEnabledFor_Company<Company>"

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action
