WarehouseZone is a BusinessClass
    owned by wh
    prefix is WHZM

    Ontology
	    symbolic key is WarehouseZone
    
	Patterns
		implements StaticJava
		disable AuditIndex
    
    Context Fields
        ContextDistributionClerk    is a DistributionClerk

    Persistent Fields
        Description
        Active   

    Derived Fields
        WarehouseZoneAndDescription is a MessageField
            default label is "Warehouse_Zone"
			"<WarehouseZone>_-_<Description>"
   
    Field Rules
        Active
            default to true

        Description
            required

    Conditions
        RecordExists
            when (WarehouseZone exists)

        ZoneIsValidForDistributionClerk
            restricted
            when (DistributionClerkZoneRel not exists)
    
    Relations
        WarehouseZoneComponentRel
            one-to-many relation to WarehouseZoneComponent
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseZone                   = WarehouseZone

        WarehouseStorageLocationRel
            one-to-many relation to WarehouseStorageLocation
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
            Instance Selection
                where (related.WarehouseZone            = WarehouseZone)

        WarehouseComponentTypeRel is a WarehouseComponentType set

        DistributionClerkZoneRel
            one-to-many relation to DistributionClerkZone
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.DistributionClerk               = ContextDistributionClerk
            Instance Selection
                where (related.WarehouseZone            = WarehouseZone)

        DistributionClerksOfWarehouseZoneRel
            one-to-many relation to DistributionClerkZone
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
            Instance Selection
                where (related.WarehouseZone            = WarehouseZone)

    Attach Rules
        constraint (Active)
            "Zone<WarehouseZone>IsNotActive"

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action

        AssignDistributionClerkToZone is an Instance Action
			completion message is "ClerkHasBeenAssignedToZone_<WarehouseZone>"
			default label is "Assign_Zone"
			Action Rules
				constraint (WarehouseZone.Active)
					"Zone<WarehouseZone>MustBeActive"

                constraint (ContextDistributionClerk.Employee.IsActive)
					"EmployeeIsNotActive"
                    
                invoke Create DistributionClerkZone
                    invoked.Company                     = Company
                    invoked.InventoryLocation           = InventoryLocation
                    invoked.DistributionClerk           = ContextDistributionClerk
                    invoked.WarehouseZone               = WarehouseZone
                    invoked.Active                      = true

        BatchCreateOfDistributionClerkZone is a Set Action
            restricted
            Local Fields
                LocalActor                  is Actor
            Parameters
                PrmDMClerk                  is a DistributionClerk
                PrmCompany                  is an InventoryCompany
                PrmLocation                 is an InventoryLocation
            Instance Selection
                where (Company              = PrmCompany
                and    InventoryLocation    = PrmLocation
                and    ZoneIsValidForDistributionClerk)
            Action Rules
                Empty Set Rules
                    LocalActor = actor
                    send notification
						to LocalActor
						description is "No_ZonesAreAvailableForAssignmentIn_Location<PrmLocation>."
						priority is low
                Instance Rules
                    invoke Create DistributionClerkZone
                        fill in fields from this instance
                        invoked.DistributionClerk       = PrmDMClerk
                        invoked.Active                  = true
