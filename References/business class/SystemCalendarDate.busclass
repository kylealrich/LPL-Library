SystemCalendarDate is a BusinessClass
	owned by sc
	prefix is Scldt

	Ontology
		symbolic key is SystemCalendarDate

    Patterns
        implements StaticJava

	Context Fields
		SystemCalendarPeriodGroup
		
	Persistent Fields

		IsWorkingDay		 is Boolean
			default label is "WorkDay"
		IsBankDay			 is Boolean
			default label is "BankDay"
		IsHoliday			 is Boolean
			default label is "Holiday"
		IsGoodsReceiptDay 	 is Boolean
			default label is "GoodsReceiptDay"
		IsDeliveryDay		 is Boolean
			default label is "DeliveryDay"
		JobSchedulingAllowed is Boolean
		TimeEntryAllowed	 is Boolean
		Capacity			 is Percent size 5.2
		AdjustPaymentDays	 is Decimal size 2.0
		AdjustDueDays		 is Decimal size 2.0
		WorkingDayNumber	 is Numeric 8
		BankDayNumber		 is Numeric 8
		HolidayNumber		 is Numeric 8
		GoodsRecDayNumber 	 is Numeric 8
		DeliveryDayNumber	 is Numeric 8
		JobSchdDayNumber	 is Numeric 8
			default label is "JobSchedulingDayNumber"
		TimeEntryDayNumber	 is Numeric 8
		Comment				 is Alpha 60
		HolidayText			 is Alpha 30	
			
	Derived Fields
		DayStatus is a DerivedField
			type is Alpha 30
			if (IsHoliday)
				if (HolidayText entered)
					return HolidayText
				else
					return Holiday
			else
			if (!IsWorkingDay)
				if (SystemCalendar.NonWorkingDayText entered)
					return SystemCalendar.NonWorkingDayText
				else
					return NonWorkingDay 
				
		Year is a DerivedField
			type is Year
			return SystemCalendarDate year
			
		Month is a DerivedField
			type is Numeric size 2
			return SystemCalendarDate month
						
		Holiday is a MessageField
			"Holiday"
			
		NonWorkingDay is a MessageField
			"NonWorkingDay"
										
		Sunday is a MessageField
			"Sunday"
			
		Monday is a MessageField
			"Monday"
			
		Tuesday is a MessageField
			"Tuesday"
			
		Wednesday is a MessageField
			"Wednesday"
			
		Thursday is a MessageField
			"Thursday"
			
		Friday is a MessageField
			"Friday"	
		
		Saturday is a MessageField
			"Saturday"
			
		WeekDayNumber is a DerivedField
			type is Numeric 1
			return SystemCalendarDate week day
			
		WeekDay is a DerivedField
			type is Alpha size 15
			if (SystemCalendarDate week day = 1)
				return Sunday
			else
			if (SystemCalendarDate week day = 2)
				return Monday
			else
			if (SystemCalendarDate week day = 3)
				return Tuesday
			else
			if (SystemCalendarDate week day = 4)
				return Wednesday
			else
			if (SystemCalendarDate week day = 5)
				return Thursday
			else
			if (SystemCalendarDate week day = 6)
				return Friday
			else
				return Saturday

		PeriodYear   is a DerivedField
			type is Year
			if (SystemCalendarPeriodGroup in context)

				return first PeriodInPeriodGroup.SystemCalendarPeriod.Year
					
		PeriodNumber is a DerivedField
			type is Numeric size 2
			if (SystemCalendarPeriodGroup in context)

				return first PeriodInPeriodGroup.SystemCalendarPeriod.Period
							
		PeriodBeginDate is a DerivedField
			type is Date
			return first PeriodInPeriodGroup.DateRange.FromDate
			
		PeriodEndDate is a DerivedField
			type is Date
			return first PeriodInPeriodGroup.DateRange.ToDate

	Conditions
		CommentEntered
			when (Comment entered)	
													
	Relations
	
		PeriodInPeriodGroup
			one-to-many relation to SystemCalendarPeriod
			Field Mapping uses symbolic key
				related.EnterpriseGroup			= EnterpriseGroup
				related.SystemCalendar          = SystemCalendar
				related.SystemCalendarPeriodGroup = SystemCalendarPeriodGroup
			Instance Selection
				where (SystemCalendarDate within related.DateRange)
													
	Sets
		ByWorkingDayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				WorkingDayNumber
				SystemCalendarDate

		ByBankDayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				BankDayNumber
				SystemCalendarDate	
																
		ByHolidayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				HolidayNumber
				SystemCalendarDate	
					
		ByGoodsRecDayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				GoodsRecDayNumber
				SystemCalendarDate	
				
		ByDeliveryDayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				DeliveryDayNumber
				SystemCalendarDate	
									
		ByJobSchdDayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				JobSchdDayNumber
				SystemCalendarDate	

		ByTimeEntryDayNumber
			duplicates
			indexed
			Sort Order
				EnterpriseGroup
				SystemCalendar
				TimeEntryDayNumber
				SystemCalendarDate	
															
	Field Rules


			









				
	Actions
		Create is a Create Action
			restricted
			Exit Rules
				invoke Update SystemCalendar
					invoked.RequiresSynchronization = true
					if (invoked.EarliestDateModified not entered
					or  SystemCalendarDate < invoked.EarliestDateModified)
						invoked.EarliestDateModified = SystemCalendarDate
										
		Update is an Update Action
			Exit Rules
				if (IsWorkingDay		 changed
				or  IsBankDay			 changed
				or	IsHoliday			 changed
				or	IsGoodsReceiptDay 	 changed
				or	IsDeliveryDay		 changed
				or  JobSchedulingAllowed changed
				or  TimeEntryAllowed	 changed)
					invoke Update SystemCalendar
						invoked.RequiresSynchronization = true
						if (invoked.EarliestDateModified not entered
						or  SystemCalendarDate < invoked.EarliestDateModified)
							invoked.EarliestDateModified = SystemCalendarDate
			
		
		Delete is a Delete Action	
			restricted																		
			Exit Rules
				invoke Update SystemCalendar
					invoked.RequiresSynchronization = true
					if (invoked.EarliestDateModified not entered
					or  SystemCalendarDate < invoked.EarliestDateModified)
						invoked.EarliestDateModified = SystemCalendarDate
	
		SynchronizeDates is a Set Action
			restricted
			run in foreground
			
			Parameters
				PrmEnterpriseGroup		is an EnterpriseGroup
					default label is "EnterpriseGroup"
				PrmSystemCalendar		is a SystemCalendar
					default label is "SystemCalendar"
				PrmEarliestDateModified is Date
					default label is "EarliestDateModified"
				PrmLastSyncedDate		is a SystemCalendarDate
					default label is "LastSyncedDate"

			Parameter Rules
				PrmLastSyncedDate
					if (PrmLastSyncedDate exists)
						TempWorkingDayNumber   = PrmLastSyncedDate.WorkingDayNumber
						TempBankDayNumber      = PrmLastSyncedDate.BankDayNumber
						TempGoodsRecDayNumber  = PrmLastSyncedDate.GoodsRecDayNumber
						TempDeliveryDayNumber  = PrmLastSyncedDate.DeliveryDayNumber
						TempHolidayNumber	   = PrmLastSyncedDate.HolidayNumber
						TempTimeEntryDayNumber = PrmLastSyncedDate.TimeEntryDayNumber
						TempJobSchdDayNumber   = PrmLastSyncedDate.JobSchdDayNumber
								
			Local Fields
				TempWorkingDayNumber   is Numeric 8
				TempBankDayNumber      is Numeric 8
				TempGoodsRecDayNumber  is Numeric 8
				TempDeliveryDayNumber  is Numeric 8
				TempHolidayNumber      is Numeric 8
				TempTimeEntryDayNumber is Numeric 8
				TempJobSchdDayNumber   is Numeric 8				
				
			Instance Selection
				where (EnterpriseGroup     = PrmEnterpriseGroup
				and    SystemCalendar      = PrmSystemCalendar
				and    SystemCalendarDate >= PrmEarliestDateModified)
							
			Action Rules				
				Instance Rules
				
					if (IsWorkingDay)
						TempWorkingDayNumber += 1
					WorkingDayNumber = TempWorkingDayNumber
					
					if (IsBankDay)
						TempBankDayNumber += 1
					BankDayNumber = TempBankDayNumber
					
					if (IsHoliday)
						TempHolidayNumber += 1
					HolidayNumber = TempHolidayNumber
					
					if (IsGoodsReceiptDay)
						TempGoodsRecDayNumber += 1
					GoodsRecDayNumber = TempGoodsRecDayNumber
					
					if (IsDeliveryDay)
						TempDeliveryDayNumber += 1
					DeliveryDayNumber = TempDeliveryDayNumber
					 
					if (JobSchedulingAllowed)
						TempJobSchdDayNumber += 1
					JobSchdDayNumber = TempJobSchdDayNumber
					
					if (TimeEntryAllowed)
						TempTimeEntryDayNumber += 1				
					TimeEntryDayNumber = TempTimeEntryDayNumber														
