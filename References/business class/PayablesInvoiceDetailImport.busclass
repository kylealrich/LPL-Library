PayablesInvoiceDetailImport is a BusinessClass
    owned by ma
    prefix is MOP
    classic name is MACINVDTL

    Ontology
        symbolic key is PayablesInvoiceDetailImport
            classic set name is MOPSET1

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields

        RunGroup
        Company						  is a MatchCompany
        Vendor
        OldVendor
        Invoice
        Suffix
        EDINumber
            classic name is EDI-NBR
		Sequence
            classic name is SEQ-NBR
        LineNumber
            classic name is LINE-NBR
        Item
        ItemDescription             is a Description
            classic name is ITEM-DESC
        VendorItem
            classic name is VEN-ITEM
        EnteredQuantity          is like Quantity
            classic name is ENTERED-QTY
        EnteredUOM               is a UnitOfMeasure
            classic name is ENT-UOM
        UnitCost                 is an InternationalCost
        ExtendedAmount           is an InternationalAmount
            classic name is EXTENDED-AMT
		BuyUOMMatchedQuantity	 is like Quantity
			default label is "BuyQuantity"
		VendorBuyUOM			 is a UnitOfMeasure
        Location                 is an InventoryLocation
        AddOnCharge
            classic name is AOC-CODE
        TotalAddOnChargeAmount   is an InternationalAmount
            classic name is TOTAL-AOC
        RetailUnitCost           is an InternationalCost
            classic name is RTL-UNIT-COST



        TaxCode
        TaxableUnitCost             is an InternationalCost
        InvoiceLineTaxable       is Boolean
            classic name is NO-TAX-FLAG
        TaxUsageCode
            classic name is TAX-USAGE-CD
        ICNCode
        SupplementaryQuantity    is like Quantity
            classic name is SUPLMNTARY-QTY
        StockWeight
            classic name is WEIGHT
        CrossReferenceVendor
            classic name is XREF-VENDOR
        CatchWeightCost          is an InternationalCost
            classic name is CATCH-WGT-COST
        CommodityCode			 is a CommCodes
        RetainagePercent         is a Pct
            classic name is RET-PCT
        ItemGTIN
            classic name is GTIN
        GlobalLineType
            classic name is GLBL-LINE-TYPE
        DistributionAccount      is a FinanceCodeBlock
            classic name for DistributionAccount.ToAccountingEntity is DIST-COMPANY
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for DistributionAccount.Project is ACTIVITY

        PurchaseOrder
		InterfacedPurchaseOrder		  is like PurchaseOrderImport
		InterfacedPOCode			  is like POCode

		Contract 

		SystemAssignedInvoiceDetail   is like PayablesInvoiceDetail
		
		Buyer

		EAMWorkOrder
		EAMWorkOrderActivity
		EAMProjectID
		EAMProjectType
		EAMProjectTaxCode
		EAMManufacturing
    	EAMTrade
    	EAMCommodityCode
    	EAMLineType
    	Chemical					  is Boolean
    	RebuildRepair				  is Boolean
    	TestRequired				  is Boolean
        RecordInError                 is Boolean
		ErrorMessage				  is Alpha up to 150
        Interfaced					  is Boolean
    	EAMTool						is a EAMToolField  
    	HSNSACCode

	Local Fields
		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150


		Select							is Boolean

		POLineLookup
		LocalTrigger					is Numeric 1
		LocalResultID					is like PayablesInvoiceInterfaceResult

		LocalPurchaseOrder				is like PurchaseOrder
		CheckCompany					is like Company
		CheckPurchaseOrder				is like PurchaseOrder

		LocalInvoiceDetailView          is a PayablesInvoiceDetail view
					
        LocalOriginalInvoice 			is like PayablesInvoice
		LocalOriginalInvoiceDetail 		is like PayablesInvoiceDetail
		LocalPOLine						is like LineNumber
		LocalContract					is like Contract
		LocalContractLine				is like LineNumber
		LocalQuantity					is like Quantity
		LocalUnitCost 					is like UnitCost
		AbsoluteUnitCost				is like UnitCost
		
	    QuantityAdjustment 				is Boolean
		CostAdjustment 					is Boolean

	Context Fields
		ContextMatchInvoice is a MatchInvoiceImport
		
	Derived Fields
		InvoiceDetailNotAllowedForExpenseInvoiceMsg is a MessageField
			"InvoiceDetailNotAllowedForExpenseInvoice"

		VendorItemDoesNotMatchPOLineMsg is a MessageField
			"VendorItemDoesNotMatchPurchaseOrderLineVendorItem"
			
		VendorItemExistsOnMultiplePOLinesMsg is a MessageField
			"VendorItemExistsOnMultiplePurchaseOrderLines"
			
		PurchaseOrderLineNotFoundMsg is a MessageField
			restricted
			"PurchaseOrderLineNotFoundForItem,VendorItemOrDescription"

		ContractLineNotFoundMsg is a MessageField
			restricted
			"ContractLineNotFoundForLineNumber,VendorOrItem"

		CannotInterfaceNoCostLineMsg is a MessageField
			restricted
			"CannotInterfaceNoCostPurchaseOrderLine"

		CannotDetermineItemUsingGTINMsg is a MessageField
			restricted
			"CannotDetermineItemUsingGTIN"

		GTINCheckDigitIsInvalidMsg is a MessageField
			restricted
			"GTINCheckDigitIsInvalid"

		AdditionalLineMismatchMsg is a MessageField
			restricted
			"AdditionalLineMismatchOnUnitCostOrUnitOfMeasure;ViewInterfacedLineViaLink"
					
		DerivedSequence is a DerivedField
			type is like Sequence
			return Sequence

    	NumericInterfacedPurchaseOrder is a DerivedField
    		type is like PurchaseOrder
    		if (InterfacedPurchaseOrder is numeric)
    			return InterfacedPurchaseOrder

		DerivedPurchaseOrder is a DerivedField
			type is like PurchaseOrder

			if  (PurchaseOrder entered)
				return PurchaseOrder
			else
			if (InterfacedPurchaseOrder entered)
				if  (InterfacedPurchaseOrderRel exists)
					return InterfacedPurchaseOrderRel.PurchaseOrder
				else
					CheckCompany = Company
					CheckPurchaseOrder = NumericInterfacedPurchaseOrder
					if  (CheckPurchaseOrderRel exists)
						return NumericInterfacedPurchaseOrder
			else
				return MatchInvoiceImportRel.PurchaseOrder	

		DerivedContract is a DerivedField 
			type is like Contract 
			if (Contract entered)
				return Contract
			else 
				return MatchInvoiceImportRel.ServiceContract 

        DerivedErrorMessage is a DerivedField
			type is Alpha up to 150
			if (ErrorMessage entered)
			    return ErrorMessage
			else
			    return MatchInvoiceImportRel.ErrorMessage
						 		
	Field Rules
		RunGroup
			required
			default to ContextMatchInvoice.RunGroup
			initial value is ContextMatchInvoice.RunGroup

		Invoice
			required
			initial value is ContextMatchInvoice.Invoice
		
		EDINumber
			initial value is ContextMatchInvoice.EDINumber

		Vendor
			initial value is ContextMatchInvoice.Vendor
			
		OldVendor
			initial value is ContextMatchInvoice.OldVendor
			
		Suffix
			initial value is ContextMatchInvoice.Suffix
				
		Sequence
			autosequence using ByRunGroup
			required

		SupplementaryQuantity
			if (ICNCode entered)
				required
					"SupplementaryQuantityRequiredIfICNCodeEntered"

		LineNumber
		    if (SystemAssignedInvoiceDetail entered)
		        cannot be changed
		            "LinePartiallyInterfaced;LineNumberCannotBeChanged"

		Buyer
		    default to PurchaseOrder.Buyer
		    default to MatchInvoiceImportRel.PurchaseOrder.Buyer
		    default to InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
		    default to MatchInvoiceImportRel.InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
		    		            
	Conditions
		SingleCSFPO
		    when (PurchaseOrder not entered
		    and   MatchInvoiceImportRel.PurchaseOrder entered)
		    
		PurchaseOrderEntered
		    when (PurchaseOrder entered)

		SingleInterfacedPO
		    when (InterfacedPurchaseOrder not entered
		    and   MatchInvoiceImportRel.InterfacedPurchaseOrder entered)
		    
		InterfacedPurchaseOrderEntered
		    when (InterfacedPurchaseOrder entered)

		ServiceContractInvoice
			when (MatchInvoiceImportRel.ServiceContract entered)
			
		IsHSNSACCodeEnabled
			restricted
			when (Company.GeneralLedgerCompany.RequireHSNSACCode)
	Relations
        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
                related.Company			 				= Company

        PayablesInvoiceRel
        	one-to-many relation to PayablesInvoice
        	Field Mapping uses ByCompanyVendorInvoice
        		related.Company		= Company
        		related.Vendor		= Vendor
        		related.Invoice		= Invoice
        		related.Suffix		= Suffix

        PayablesInvoiceDetailRel
        	one-to-one relation to PayablesInvoiceDetail
        	Field Mapping uses symbolic key
        		related.Company						= Company
        		related.PayablesInvoice				= MatchInvoiceImportRel.PayablesInvoice
                related.PurchaseOrder  				= LocalPurchaseOrder
                related.PurchaseOrderLine    		= LineNumber
                related.Contract	  				= blank
                related.ContractLine    			= blank
                related.PayablesInvoiceDetail		= SystemAssignedInvoiceDetail

        PayablesInvoiceDetailWithContractRel
        	one-to-one relation to PayablesInvoiceDetail
        	Field Mapping uses symbolic key
        		related.Company						= Company
        		related.PayablesInvoice				= MatchInvoiceImportRel.PayablesInvoice
                related.PurchaseOrder  				= LocalPurchaseOrder
                related.PurchaseOrderLine    		= LineNumber
                related.Contract	  				= PurchaseOrderLineRel.Contract
                related.ContractLine    			= PurchaseOrderLineRel.ContractLine
                related.PayablesInvoiceDetail		= SystemAssignedInvoiceDetail

        PayablesInvoiceDetailServiceContractRel
        	one-to-one relation to PayablesInvoiceDetail
        	Field Mapping uses symbolic key
        		related.Company						= Company
        		related.PayablesInvoice				= MatchInvoiceImportRel.PayablesInvoice
                related.PurchaseOrder  				= blank
                related.PurchaseOrderLine    		= blank
                related.Contract	  				= LocalContract
                related.ContractLine    			= LineNumber
                related.PayablesInvoiceDetail		= SystemAssignedInvoiceDetail

        MatchInvoiceImportRel
            one-to-one relation to MatchInvoiceImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix

        PayablesInvoiceAddOnChargeImportRel
            one-to-many relation to PayablesInvoiceAddOnChargeImport
            Field Mapping uses ByRunGroup
            	related.RunGroup						= RunGroup
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
				related.PurchaseOrder					= PurchaseOrder
				related.LineNumber						= LineNumber
				related.Sequence						= Sequence

        LocalPayablesInvoiceAddOnChargeImportRel
            one-to-many relation to PayablesInvoiceAddOnChargeImport
            Field Mapping uses ByRunGroup
            	related.RunGroup						= RunGroup
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
				related.PurchaseOrder					= PurchaseOrder
				related.LineNumber						= LocalPOLine
				related.Sequence						= Sequence

       	TaxEntityRel				 
            one-to-one relation to TaxEntity
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
                related.TaxEntity 						= Company.AccountingEntity
                
        EntityTaxCodeRel
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
                related.TaxEntity 						= Company.AccountingEntity
                related.TaxCode 						= TaxCode

		LocalInterfaceResultsRel
			one-to-one relation to PayablesInvoiceInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
				related.PayablesInvoiceInterfaceResult	= LocalResultID

        PurchaseOrderLineRel
            one-to-one relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company							= Company
                related.PurchaseOrder					= LocalPurchaseOrder 
                related.PurchaseOrderLine				= LineNumber

		POLineByItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= LocalPurchaseOrder 
                related.Item							= Item

        POLineByMatchDetailKeyRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set11
                related.Company							= Company
                related.PurchaseOrder					= LocalPurchaseOrder 
                related.MatchDetailKey					= ItemDescription

        POLineByVendorItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set15
                related.Company							= Company
                related.PurchaseOrder					= LocalPurchaseOrder 
                related.VendorItem						= VendorItem

        VendorItemRel
            one-to-many relation to VendorItem
            Field Mapping uses symbolic key
                related.ProcurementGroup 				= Company.ProcurementGroup
                related.Item           					= Item
                related.Vendor       					= Vendor
                
        ItemUOMRel
        	one-to-one relation to ItemUOM
        	Field Mapping uses symbolic key
        		related.ItemGroup         				= Company.ProcurementGroup
        		related.Item              				= Item
        		related.UnitOfMeasure     				= EnteredUOM
        		
		ItemGTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup  						= Company
				related.ItemGTIN 						= ItemGTIN

        CheckPurchaseOrderRel
            one-to-one relation to PurchaseOrder
            Field Mapping uses symbolic key
                related.Company							= CheckCompany
                related.PurchaseOrder					= CheckPurchaseOrder 

		DerivedPurchaseOrderRel	
			one-to-one relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company							= Company
				related.PurchaseOrder					= DerivedPurchaseOrder 

		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
                related.Company                     = Company
                related.PurchaseOrderImport 		= InterfacedPurchaseOrder
                related.POCode						= InterfacedPOCode

		HeaderPOLineByItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= MatchInvoiceImportRel.PurchaseOrder 
                related.Item							= Item

        HeaderPOLineByVendorItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set15
                related.Company							= Company
                related.PurchaseOrder					= MatchInvoiceImportRel.PurchaseOrder 
                related.VendorItem						= VendorItem

        HeaderPurchaseOrderLineRel
            one-to-one relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company							= Company
                related.PurchaseOrder					= MatchInvoiceImportRel.PurchaseOrder 
                related.PurchaseOrderLine				= LineNumber
			
		ThisLinePOLineByItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set7
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.Item							= Item

        ThisLinePOLineByVendorItemRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses Set15
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.VendorItem						= VendorItem

        ThisLinePurchaseOrderLineRel
            one-to-one relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company							= Company
                related.PurchaseOrder					= PurchaseOrder 
                related.PurchaseOrderLine				= LineNumber

        LocalOriginalInvoiceDetailRel
            one-to-many relation to PayablesInvoiceDetail
            Field Mapping uses symbolic key
                related.Company 						= Company
                related.PayablesInvoice 				= LocalOriginalInvoice
                related.PurchaseOrder  					= LocalPurchaseOrder
                related.PurchaseOrderLine    			= LocalPOLine
                related.Contract	  					= LocalContract
                related.ContractLine    				= LocalContractLine

        OriginalInvoiceDetailRel
            one-to-one relation to PayablesInvoiceDetail
            Field Mapping uses symbolic key
                related.Company 						= Company
                related.PayablesInvoice 				= LocalOriginalInvoice 
                related.PurchaseOrder  					= LocalPurchaseOrder
                related.PurchaseOrderLine    			= LocalPOLine
                related.Contract	  					= LocalContract
                related.ContractLine    				= LocalContractLine
                related.PayablesInvoiceDetail			= LocalOriginalInvoiceDetail

        LocalInterfacedDetailRel
            one-to-many relation to PayablesInvoiceDetail
            Field Mapping uses symbolic key
                related.Company 						= Company
                related.PayablesInvoice 				= MatchInvoiceImportRel.PayablesInvoice
                related.PurchaseOrder  					= LocalPurchaseOrder
                related.PurchaseOrderLine    			= LocalPOLine
                related.Contract	  					= LocalContract
                related.ContractLine    				= LocalContractLine

        LocalInterfacedDetailExactRel
            one-to-one relation to PayablesInvoiceDetail
            Field Mapping uses symbolic key
                related.Company 						= Company
                related.PayablesInvoice 				= MatchInvoiceImportRel.PayablesInvoice
                related.PurchaseOrder  					= LocalPurchaseOrder
                related.PurchaseOrderLine    			= LocalPOLine
                related.Contract	  					= LocalContract
                related.ContractLine    				= LocalContractLine
                related.PayablesInvoiceDetail			= LocalOriginalInvoiceDetail

		ContractLineRel
			one-to-one relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup			= PayablesCompanyRel.VendorGroup
				related.Contract				= LocalContract
				related.ContractLine			= LineNumber

		ContractLineByItemRel
			one-to-many relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup			= PayablesCompanyRel.VendorGroup
				related.Contract				= LocalContract
			Instance Selection
				where (related.ItemNumber		= Item)

		ContractLineByVendorItemRel
			one-to-many relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup			= PayablesCompanyRel.VendorGroup
				related.Contract				= LocalContract
			Instance Selection
				where (related.VendorItem		= VendorItem)

		ContractLineByItemDescriptionRel
			one-to-many relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup			= PayablesCompanyRel.VendorGroup
				related.Contract				= LocalContract
			Instance Selection
				where (related.ItemDescription		= ItemDescription)

	Sets
        ByRunGroup
            indexed
            Sort Order
                RunGroup
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
                LineNumber
                Sequence
                PayablesInvoiceDetailImport

		ByCompanyVendorInvoice
            indexed
            Sort Order
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
                LineNumber
				Sequence
				PayablesInvoiceDetailImport
										
	Create Rules
		constraint (MatchInvoiceImportRel exists)
			"InterfaceInvoiceDoesNotExist"
			
	Actions
		Create is a Create Action
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
				LocalContract      = DerivedContract 

				if (MatchInvoiceImportRel.PayablesInvoice entered)
					for each MatchInvoiceImportRel.PayablesInvoice.PayablesInvoiceDetailRel
						if (each.Item = Item
						or  each.VendorItem = VendorItem)
							confirmation required
								"ItemAlreadyExistsOnPartiallyInterfacedInvoice;ContinueWithAdd?"

		CreateNoRules is a Create Action
			restricted
			bypass field rules
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
				LocalContract      = DerivedContract 

			Action Rules
				if (Buyer not entered)
				    Buyer = PurchaseOrder.Buyer
				if (Buyer not entered)
				    Buyer = MatchInvoiceImportRel.PurchaseOrder.Buyer
				if (Buyer not entered)
				    Buyer = InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
				if (Buyer not entered)
				    Buyer = MatchInvoiceImportRel.InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
					
		Update is an Update Action
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
				LocalContract      = DerivedContract 

				if (LineNumber changed)
					LocalPOLine = old LineNumber 
					invoke FastUpdate LocalPayablesInvoiceAddOnChargeImportRel
						invoked.LineNumber          = LineNumber

		FastUpdate is an Update Action
			bypass field rules

		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules
				RunGroup = NewRunGroup
					
		ChangeSecondaryKeyFields is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
		        NewCompany			is a MatchCompany
		        NewVendor			is like Vendor
		        NewOldVendor		is like OldVendor
		        NewInvoice			is like Invoice
		        NewSuffix			is like Suffix
		        NewEDINumber		is like EDINumber
			Action Rules
				RunGroup 			= NewRunGroup
		        Company				= NewCompany
        		Vendor				= NewVendor
		        OldVendor			= NewOldVendor
		        Invoice				= NewInvoice
		        Suffix				= NewSuffix
		        EDINumber			= NewEDINumber
					
 		ClearError is an Instance Action
 			valid when (RecordInError)
			Parameters 
				NewRunGroup     is like RunGroup
			Parameter Rules 
				NewRunGroup 
			Action Rules
	 			initialize ErrorMessage
	 			RecordInError = false

				invoke ResetErrorMessage MatchInvoiceImportRel

				if (NewRunGroup entered)
					invoke Update MatchInvoiceImportRel
						invoked.RunGroup = NewRunGroup

		Delete is a Delete Action
		    Action Rules
		        if (SystemAssignedInvoiceDetail entered)
					LocalPurchaseOrder = DerivedPurchaseOrder
					LocalContract      = DerivedContract 
					if  (MatchInvoiceImportRel.ServiceContract entered)
						invoke Delete PayablesInvoiceDetailServiceContractRel
					else
					if (PurchaseOrderLineRel.Contract entered)
			            invoke Delete PayablesInvoiceDetailWithContractRel
					else
			            invoke Delete PayablesInvoiceDetailRel
					
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		InvoiceDetailLoad is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				PrmVendorGroup				is a VendorGroup	
				PrmVendor					is a Vendor
					context of PrmVendorGroup		
				PrmProcessLevel				is a PayablesProcessLevel
				PrmAuthorityCode			is a PayablesAuthorityCode
					context of PrmVendorGroup			
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				MatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				AllowMatchItemOrVendorItem			is Boolean
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"

			Local Fields




				LocalInterfacedDetailsCount			is Numeric 10


			Instance Selection
				where (RunGroup	= PrmRunGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (PrmVendor            			  not entered
				or      Vendor	= PrmVendor)
				and    (PrmVendorGroup					not entered		
				or      Company.PayablesCompany.VendorGroup = PrmVendorGroup))

			Sort Order
				RunGroup
				Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
                LineNumber
				Sequence

			Action Rules

				Empty Set Rules
					
					invoke InvoiceDetailLoadCreateDistributions PayablesInvoiceDetailImport
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
						invoked.PrmVendor					= PrmVendor
						invoked.PrmProcessLevel				= PrmProcessLevel
						invoked.PrmAuthorityCode			= PrmAuthorityCode
				        invoked.InvoiceProcess				= InvoiceProcess
						invoked.MatchPoint					= MatchPoint
						invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.PrmVendorGroup				= PrmVendorGroup	

				RunGroup Set Rules
						
					Exit Rules
						LocalResultID					= PrmInterfaceRun
						invoke Update LocalInterfaceResultsRel
							invoked.InterfacedDetailsCount			= LocalInterfacedDetailsCount

						invoke InvoiceDetailLoadCreateDistributions PayablesInvoiceDetailImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmVendor					= PrmVendor
							invoked.PrmProcessLevel				= PrmProcessLevel
							invoked.PrmAuthorityCode			= PrmAuthorityCode
					        invoked.InvoiceProcess				= InvoiceProcess
							invoked.MatchPoint					= MatchPoint
							invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.PrmVendorGroup				= PrmVendorGroup	

				Instance Rules
					Select = false
					if     ((PrmProcessLevel            			  not entered
					or       MatchInvoiceImportRel.ProcessLevel		= PrmProcessLevel)
					and     (PrmAuthorityCode       			      not entered
					or       MatchInvoiceImportRel.AuthorityCode	= PrmAuthorityCode)
	                and      PayablesCompanyRel.Company.FinanceEnterpriseGroup         = PrmFinanceEnterpriseGroup)
						LocalInterfacedDetailsCount			+= 1
						Select								= true


					if   (Select
					and   Interfaced
					and   MatchInvoiceImportRel.PayablesInvoice exists)
						RecordInError 						= false
						ErrorOccurred						= false
						initialize ErrorMessage
						LocalPurchaseOrder = DerivedPurchaseOrder
						LocalContract      = DerivedContract 

						if  (MatchInvoiceImportRel.ServiceContract entered)
							invoke Update PayablesInvoiceDetailServiceContractRel
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								invoked.EnteredQuantity					= EnteredQuantity
								invoked.EnteredUnitCost					= UnitCost
								invoked.EnteredUOM						= EnteredUOM
								if (LineNumber entered)
									invoked.BypassDistributionCreate		= true
						else
						if (PurchaseOrderLineRel.Contract entered)
			    	        invoke Update PayablesInvoiceDetailWithContractRel
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								invoked.EnteredQuantity					= EnteredQuantity
								invoked.EnteredUnitCost					= UnitCost
								invoked.EnteredUOM						= EnteredUOM
								if (LineNumber entered)
									invoked.BypassDistributionCreate		= true
						else						
			    	        invoke Update PayablesInvoiceDetailRel
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								invoked.EnteredQuantity					= EnteredQuantity
								invoked.EnteredUnitCost					= UnitCost
								invoked.EnteredUOM						= EnteredUOM
								if (LineNumber entered)
									invoked.BypassDistributionCreate		= true

						if (ErrorOccurred)
							RecordInError = true
							invoke SetError MatchInvoiceImportRel
								invoked.PrmErrorMessage			= LocalErrorMessage

					else
					if   (Select
					and   !Interfaced
					and   MatchInvoiceImportRel.PayablesInvoice exists)
					

						RecordInError 						= false
						ErrorOccurred						= false

						LocalPurchaseOrder = DerivedPurchaseOrder
						LocalContract = DerivedContract 

						if  (Item not entered
						and  ItemGTIN entered)

							Item								= first ItemGTINRel.Item
								
						if (!ErrorOccurred)

							if  (VendorItem not entered
							and  VendorItemRel exists)
								VendorItem						= VendorItemRel.VendorItem

						if (!ErrorOccurred
						and DerivedPurchaseOrder entered)

							initialize POLineLookup

							POLineLookup.Company				= Company
							POLineLookup.PurchaseOrder			= DerivedPurchaseOrder

							POLineLookup.PurchaseOrderLine		= LineNumber
							POLineLookup.Item					= Item
							POLineLookup.VendorItem				= VendorItem
							POLineLookup.Description			= ItemDescription
							POLineLookup.Location				= Location
							POLineLookup.AllowMatchItemOrVendorItem = AllowMatchItemOrVendorItem
							POLineLookup.InterfaceType 			= "I"
							
							if (Location not entered
							and MatchInvoiceImportRel.Location entered)

								POLineLookup.Location			= MatchInvoiceImportRel.Location
								
							LocalTrigger						= POLineLookup.ExecuteLookup

							if (LineNumber != POLineLookup.PurchaseOrderLine)
								invoke FastUpdate PayablesInvoiceAddOnChargeImportRel
									invoked.LineNumber			= POLineLookup.PurchaseOrderLine
									
							LineNumber							= POLineLookup.PurchaseOrderLine

							if  (POLineLookup.OutputErrorNumber entered)
								ErrorOccurred					= true
								LocalErrorMessage				= POLineLookup.OutputErrorMessage
							else
								Item							= POLineLookup.Item
								VendorItem						= POLineLookup.VendorItem
								if (EnteredUOM not entered)
								    EnteredUOM						= POLineLookup.PurchaseOrderLineRel.EnteredBuyUOM

						if (!ErrorOccurred
						and  MatchInvoiceImportRel.ServiceContract entered
						and  LineNumber not entered)
							if  (AllowMatchItemOrVendorItem)
								if  (Item entered
								and  instance count of ContractLineByItemRel = 1)
									LineNumber			= first ContractLineByItemRel.ContractLine
								else
								if  (VendorItem entered
								and  instance count of ContractLineByVendorItemRel = 1)
									LineNumber			= first ContractLineByVendorItemRel.ContractLine
								else
								if  (ItemDescription entered
								and  instance count of ContractLineByItemDescriptionRel = 1)
									LineNumber			= first ContractLineByItemDescriptionRel.ContractLine
							else								
								if  (Item entered
								and  instance count of ContractLineByItemRel = 1
								and (VendorItem not entered
								or   first ContractLineByItemRel.VendorItem = VendorItem))
									LineNumber			= first ContractLineByItemRel.ContractLine
								else
								if  (VendorItem entered
								and  instance count of ContractLineByVendorItemRel = 1
								and (Item not entered
								or   first ContractLineByItemRel.ItemNumber = Item))
									LineNumber			= first ContractLineByVendorItemRel.ContractLine
								else
								if  (ItemDescription entered
								and  instance count of ContractLineByItemDescriptionRel = 1
								and (Item not entered
								or   first ContractLineByItemRel.ItemNumber = Item)
								and (VendorItem not entered
								or   first ContractLineByItemRel.VendorItem = VendorItem))
									LineNumber			= first ContractLineByItemDescriptionRel.ContractLine

							if  (LineNumber not entered)
								ErrorOccurred			= true
								LocalErrorMessage		= ContractLineNotFoundMsg

						if (!ErrorOccurred)
							if  (MatchInvoiceImportRel.ProcessType.Basic
							or   MatchInvoiceImportRel.ProcessType.Freight)
								ErrorOccurred			= true
								LocalErrorMessage		= InvoiceDetailNotAllowedForExpenseInvoiceMsg
							else
							if  (AllowMatchItemOrVendorItem
							and  DerivedPurchaseOrder entered)
								if  (VendorItem entered)
									if  (PurchaseOrderLineRel exists
									and  PurchaseOrderLineRel.VendorItem != VendorItem)
										ErrorOccurred			= true
										LocalErrorMessage		= VendorItemDoesNotMatchPOLineMsg
									else
									if  (VendorItem entered
									and  LineNumber not entered
									and  instance count of POLineByVendorItemRel > 1)
										ErrorOccurred			= true
										LocalErrorMessage		= VendorItemExistsOnMultiplePOLinesMsg
							
						if (!ErrorOccurred)
							if  (PurchaseOrderLineRel exists
							and  PurchaseOrderLineRel.CostOption.NoCharge
							and  Vendor.AllowZeroCostInvoice.No
							and  !Company.AllowZeroCostInvoice)
								ErrorOccurred			= true
								LocalErrorMessage		= CannotInterfaceNoCostLineMsg


						if (!ErrorOccurred)
							if (!Interfaced)

								if  (!MatchInvoiceImportRel.InvoiceType.CreditMemo
								and  !MatchInvoiceImportRel.InvoiceType.DebitMemo)
									if (MatchInvoiceImportRel.ServiceContract entered)
									    LocalContractLine = LineNumber
									    initialize LocalPOLine
									else
									if  (PurchaseOrderLineRel.Contract entered)
										LocalPOLine = LineNumber
										LocalContract = PurchaseOrderLineRel.Contract
									    LocalContractLine = PurchaseOrderLineRel.ContractLine
									else
									    LocalPOLine = LineNumber
									    initialize LocalContract
									    initialize LocalContractLine

								if  (MatchInvoiceImportRel.InvoiceType.CreditMemo
								or   MatchInvoiceImportRel.InvoiceType.DebitMemo)

									if (MatchInvoiceImportRel.ServiceContract entered)
									    LocalContractLine = LineNumber
									    initialize LocalPOLine
									else
									    LocalPOLine = LineNumber
									    initialize LocalContractLine

					                LocalOriginalInvoice 			= MatchInvoiceImportRel.PayablesInvoice.MatchInvoiceReferenceRel.OriginalInvoice
									LocalOriginalInvoiceDetail 		= first LocalOriginalInvoiceDetailRel.PayablesInvoiceDetail




									if (UnitCost < 0)
									    AbsoluteUnitCost = UnitCost * -1
									else
									    AbsoluteUnitCost = UnitCost

									if  (MatchInvoiceImportRel.TaxEntityRel.ThirdParty.TaxEngine)
									    QuantityAdjustment = true
										CostAdjustment = false
									else 
									if  (MatchInvoiceImportRel.ServiceContract entered
									and (EnteredQuantity < 0
									or   AbsoluteUnitCost = ContractLineRel.BaseCost))
									    QuantityAdjustment = true
										CostAdjustment = false
									else
									if  (DerivedPurchaseOrder entered
									and (EnteredQuantity < 0
									or   AbsoluteUnitCost = PurchaseOrderLineRel.EnteredUnitCost))
									    QuantityAdjustment = true
										CostAdjustment = false
									else
									    CostAdjustment = true
										QuantityAdjustment = false


									invoke OnlineMemoCreate PayablesInvoiceDetail
										assign result to LocalInvoiceDetailView
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										fill in fields from this instance
										invoked.Company						= Company
										invoked.PayablesInvoice				= MatchInvoiceImportRel.PayablesInvoice
										invoked.CostOrQuantityAdjustment	= true
										if (MatchInvoiceImportRel.ServiceContract entered)
											invoked.Contract					= DerivedContract
											invoked.ContractLine				= LineNumber
										else
											invoked.PurchaseOrder				= DerivedPurchaseOrder
											invoked.PurchaseOrderLine			= LineNumber

										invoked.TransientDistributionAccount	= DistributionAccount 
								        invoked.RecordSource					= "LM"
										invoked.BypassInvoiceErrorChecking		= true
										invoked.FromInterface					= true
										if (LineNumber entered)
											invoked.BypassDistributionCreate	= true

										if (TaxEntityRel.ThirdParty.TaxEngine)
											invoked.TaxCode						= OriginalInvoiceDetailRel.TaxCode	
											invoked.TaxAmount					= OriginalInvoiceDetailRel.TaxAmount * -1									

										if (CostAdjustment)
											invoked.MemoAdjustType				= 1
											if (EnteredQuantity > 0)
												invoked.EnteredQuantity			= EnteredQuantity
											else
												invoked.EnteredQuantity			= EnteredQuantity * -1
												
											if (UnitCost < 0
                                            and PurchaseOrderLineRel.ItemType.Inventoried)
												invoked.EnteredUnitCost			= UnitCost * -1
											else
											    invoked.EnteredUnitCost			= UnitCost
										else
											invoked.MemoAdjustType				= 2
											if (EnteredQuantity < 0)
												invoked.EnteredQuantity			= EnteredQuantity
											else
												invoked.EnteredQuantity			= EnteredQuantity * -1
												
											if (UnitCost > 0)
												invoked.EnteredUnitCost			= UnitCost
											else
												invoked.EnteredUnitCost			= UnitCost * -1

								else
								if  (LocalInterfacedDetailRel exists
								and  !PurchaseOrderLineRel.ServiceCode.Amount
								and  !LocalInterfacedDetailRel.ContractLine.ServiceCode.Amount)

									LocalOriginalInvoiceDetail 		= first LocalInterfacedDetailRel.PayablesInvoiceDetail
									round UnitCost to nearest LocalInterfacedDetailRel.DerivedRoundToCost
									if  (UnitCost != LocalInterfacedDetailExactRel.EnteredUnitCost
									or   EnteredUOM != LocalInterfacedDetailExactRel.EnteredUOM)
										ErrorOccurred			= true
										LocalErrorMessage		= AdditionalLineMismatchMsg
									else
										LocalInvoiceDetailView	= LocalInterfacedDetailExactRel.PayablesInvoiceDetail
									    invoke Update LocalInterfacedDetailExactRel
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											invoked.EnteredQuantity					+= EnteredQuantity
								else

									invoke Create PayablesInvoiceDetail
										assign result to LocalInvoiceDetailView
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										fill in fields from this instance
										invoked.PayablesInvoice					= MatchInvoiceImportRel.PayablesInvoice
										invoked.EnteredQuantity					= EnteredQuantity
										invoked.EnteredUnitCost					= UnitCost
										invoked.EnteredUOM						= EnteredUOM
										if  (MatchInvoiceImportRel.ServiceContract entered)
											invoked.Contract					= DerivedContract
											invoked.ContractLine				= LineNumber
										else
											invoked.PurchaseOrder				= DerivedPurchaseOrder
											invoked.PurchaseOrderLine			= LineNumber
		
										if  (PurchaseOrderLineRel exists
										and  EnteredUOM not entered)
											invoked.EnteredUOM					= PurchaseOrderLineRel.VendorPriceUOM
											invoked.EnteredUOMMultiplier		= PurchaseOrderLineRel.VendorPriceUOMMultiplier
										else
										if  (PurchaseOrderLineRel exists
										and  EnteredUOM not entered)
											invoked.EnteredUOM					= PurchaseOrderLineRel.VendorPriceUOM
											invoked.EnteredUOMMultiplier		= PurchaseOrderLineRel.VendorPriceUOMMultiplier
										else
										if  (ItemUOMRel exists)
											invoked.EnteredUOMMultiplier		= ItemUOMRel.UOMConversion
			
										invoked.TransientDistributionAccount	= DistributionAccount 
								        invoked.RecordSource					= "LM"
										invoked.BypassInvoiceErrorChecking		= true
										if (LineNumber entered)
											invoked.BypassDistributionCreate	= true
	
						if (ErrorOccurred)
							RecordInError = true
							ErrorMessage = LocalErrorMessage
							invoke SetError MatchInvoiceImportRel
								invoked.PrmErrorMessage			= LocalErrorMessage
						else
							SystemAssignedInvoiceDetail = LocalInvoiceDetailView.PayablesInvoiceDetail
							Interfaced = true
	
	
		InvoiceDetailLoadCreateDistributions is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				PrmVendorGroup				is a VendorGroup	
				PrmVendor					is a Vendor
					context of PrmVendorGroup		
				PrmProcessLevel				is a PayablesProcessLevel
				PrmAuthorityCode			is a PayablesAuthorityCode
					context of PrmVendorGroup			
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				MatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				AllowMatchItemOrVendorItem			is Boolean
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"
			Local Fields



			Instance Selection
				where (RunGroup	= PrmRunGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (PrmVendor            			  not entered
				or      Vendor	= PrmVendor)
				and    (PrmVendorGroup					not entered		
				or      Company.PayablesCompany.VendorGroup = PrmVendorGroup))

			Sort Order
				RunGroup
				Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				Sequence
				
			Action Rules

				Empty Set Rules
						
					invoke AddOnChargeLoad PayablesInvoiceAddOnChargeImport
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
						invoked.PrmVendor					= PrmVendor
						invoked.PrmProcessLevel				= PrmProcessLevel
						invoked.PrmAuthorityCode			= PrmAuthorityCode
				        invoked.InvoiceProcess				= InvoiceProcess
						invoked.MatchPoint					= MatchPoint
						invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.PrmVendorGroup				= PrmVendorGroup	

				RunGroup Set Rules
					Entrance Rules

						
					Exit Rules


						invoke AddOnChargeLoad PayablesInvoiceAddOnChargeImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmVendor					= PrmVendor
							invoked.PrmProcessLevel				= PrmProcessLevel
							invoked.PrmAuthorityCode			= PrmAuthorityCode
					        invoked.InvoiceProcess				= InvoiceProcess
							invoked.MatchPoint					= MatchPoint
							invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.PrmVendorGroup				= PrmVendorGroup	


				Instance Rules

					if     ((PrmProcessLevel            			  not entered
					or       MatchInvoiceImportRel.ProcessLevel		= PrmProcessLevel)
					and     (PrmAuthorityCode       			      not entered
					or       MatchInvoiceImportRel.AuthorityCode	= PrmAuthorityCode)
	                and      PayablesCompanyRel.Company.FinanceEnterpriseGroup         = PrmFinanceEnterpriseGroup
					and      MatchInvoiceImportRel.PayablesInvoice exists
					and     !RecordInError)
					


						RecordInError 						= false
						ErrorOccurred						= false

						LocalPurchaseOrder = DerivedPurchaseOrder
						LocalContract      = DerivedContract 

						if (!ErrorOccurred
						and  Interfaced
						and  LineNumber entered)

							if  (MatchInvoiceImportRel.ServiceContract entered
							and  !PayablesInvoiceDetailServiceContractRel.InvoiceDistributionsForContractLineRel exists)
								invoke CreateDistributionsFromInterface PayablesInvoiceDetailServiceContractRel
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									if (DistributionAccount entered)
									    invoked.PrmOverrideAccount = DistributionAccount
							else
							if (PurchaseOrderLineRel.Contract entered
							and  !PayablesInvoiceDetailWithContractRel.InvoiceDistributionsForPurchaseOrderLineRel exists)

								invoke CreateDistributionsFromInterface PayablesInvoiceDetailWithContractRel
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									if (DistributionAccount entered)
									    invoked.PrmOverrideAccount = DistributionAccount
							else
							if (!PayablesInvoiceDetailRel.InvoiceDistributionsForPurchaseOrderLineRel exists)

								invoke CreateDistributionsFromInterface PayablesInvoiceDetailRel
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									if (DistributionAccount entered)
									    invoked.PrmOverrideAccount = DistributionAccount
							else 
							if (TaxEntityRel.ThirdParty.TaxEngine
							and !MatchInvoiceImportRel.PayablesInvoice.InvoiceSource.CreditForRebill)

								invoke CopyPOLineDistributions PayablesInvoiceDetailRel 
							else
							if (PayablesInvoiceDetailRel.TaxCode entered 
							and !PayablesInvoiceDetailRel.TaxDistributionsRel exists)

								if (TaxCode != PayablesInvoiceDetailRel.TaxCode)
									invoke FastUpdate PayablesInvoiceDetailRel 
										invoked.TaxCode = TaxCode 
										
								invoke CreateDistributionsFromInterface PayablesInvoiceDetailRel
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									invoked.TaxOnly = true 
									if (DistributionAccount entered)
									    invoked.PrmOverrideAccount = DistributionAccount


						if (ErrorOccurred)

							RecordInError = true
							invoke SetError MatchInvoiceImportRel
								invoked.PrmErrorMessage			= LocalErrorMessage
						else

							if  (!PayablesInvoiceAddOnChargeImportRel exists)


								invoke FastDelete
	
