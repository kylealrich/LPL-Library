TrueCostDriver is a BusinessClass
    owned by truecost
    prefix is TCDRV
	default label is "True\CostDriversList"
    Ontology
    	symbolic key is TrueCostDriver

    Persistent Fields

        Description

        IsUnitCostDriver		is Boolean

    Relations

		TrueCostDriverUnitCountRel
 			one-to-many relation to TrueCostDriver
 			Field Mapping uses symbolic key
 				related.TrueCostConfiguration 		= TrueCostConfiguration
 			Instance Selection
				where(IsUnitCountDriver)

		TrueCostDriverPerMinuteDurationRel
 			one-to-many relation to TrueCostDriver
 			Field Mapping uses symbolic key
 				related.TrueCostConfiguration 		= TrueCostConfiguration
 			Instance Selection
 				where(IsPerMinuteDurationDriver)

	Transient Fields
		TransientTrueCostDriver				is AlphaUpper 25
			default label is "True\CostDriver"
			derive value from TrueCostDriver


	Rule Blocks
		CreateUnitCostDrivers
				if (TrueCostDriverUnitCountRel exists)
					invoke Update TrueCostDriverUnitCountRel
						invoked.TrueCostConfiguration	=	TrueCostConfiguration
						invoked.TrueCostDriver			=   TrueCostDriverUnitCountRel.TrueCostDriver
						invoked.Description				=   "Actual Activity Count Volume"
						invoked.IsUnitCostDriver		=	true
				else
					invoke Create TrueCostDriver
						invoked.TrueCostConfiguration	=	TrueCostConfiguration
						invoked.TrueCostDriver			=   "UNIT COUNT"
						invoked.Description				=   "Actual Activity Count Volume"
						invoked.IsUnitCostDriver		=	true
				
				if (TrueCostDriverPerMinuteDurationRel exists)
					invoke Update TrueCostDriverPerMinuteDurationRel
						invoked.TrueCostConfiguration	=	TrueCostConfiguration
						invoked.TrueCostDriver			=   TrueCostDriverPerMinuteDurationRel.TrueCostDriver
						invoked.Description				=   "Actual Activity Duration Volume"
						invoked.IsUnitCostDriver		=	true
				else
					invoke Create TrueCostDriver
						invoked.TrueCostConfiguration	=	TrueCostConfiguration
						invoked.TrueCostDriver			=   "PER MINUTE DURATION"
						invoked.Description				=   "Actual Activity Duration Volume"
						invoked.IsUnitCostDriver		=	true
	Field Rules

	Conditions

		UnitCostDriversExist
			restricted
			when (TrueCostDriverUnitCountRel exists
			and TrueCostDriverPerMinuteDurationRel exists)
		
		IsUnitCountDriver
			restricted
			when(IsUnitCostDriver
			and (TrueCostDriver = "Unit Count"
			or TrueCostDriver = "UNIT COUNT"))
		
		IsPerMinuteDurationDriver
			restricted
			when(IsUnitCostDriver
			and (TrueCostDriver = "Per Minute Duration"
			or TrueCostDriver = "PER MINUTE DURATION"))
			
	Actions
 		Create is an Action

			valid when (!TrueCostDriver.IsUnitCostDriver)
			Field Rules
			Action Rules
				TrueCostDriver = TransientTrueCostDriver

			Exit Rules

    	Update is an Update Action

			valid when (!TrueCostDriver.IsUnitCostDriver)

			Field Rules

    		Exit Rules

    	Delete is a Delete Action

			valid when (!TrueCostDriver.IsUnitCostDriver)
    	

		GenerateUnitCostDrivers is an Instance Action
				valid when (!UnitCostDriversExist)
			Action Rules
				include CreateUnitCostDrivers
    	
