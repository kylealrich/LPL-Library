BillingCompany is a BusinessClass
    owned by bl
    prefix is OEC
    classic name is OECOMPANY

    Ontology
        symbolic key is BillingCompany
            classic set name is OECSET1

    Patterns
        implements StaticJava
        disable AuditIndex		

    Persistent Fields

        Name
        CompanyAddress						is a PostalAddressV2	
        	holds pii
            classic name for CompanyAddress.DeliveryAddress.AddressLine1 is ADDR1
            classic name for CompanyAddress.DeliveryAddress.AddressLine2 is ADDR2
            classic name for CompanyAddress.DeliveryAddress.AddressLine3 is ADDR3
            classic name for CompanyAddress.DeliveryAddress.AddressLine4 is ADDR4
            classic name for CompanyAddress.Municipality is CITY
            classic name for CompanyAddress.StateProvince is STATE
            classic name for CompanyAddress.PostalCode is ZIP
            classic name for CompanyAddress.Country is COUNTRY-CODE
            classic name for CompanyAddress.Region is REGION
        ManuallyNumbered                   is Boolean
            classic name is MAN-ORD-FL
        DefaultReason                      is an OrderCancelCreditReason
            classic name is DEF-REASON
        BatchControlTotal		           is Boolean
            classic name is BTCH-CNTRL-TL
        PostUnits                          is Boolean
            classic name is POST-UNITS-FL





        RouteAndStop                       is Boolean
            classic name is ROUTE-STOP-FL
        UnitOfMeasureRequired              is Boolean
            classic name is UOM-REQ-FL
        Currency
            classic name is CURRENCY-CODE
        MultipleCurrency               	   is Boolean
            classic name is MULTI-CURR-FL
        BaseNumberOfDecimals
            classic name is BASE-ND
        DefaultCurrencyOverride            is Boolean
            classic name is CURR-OVR-FL
        RateOverride                       is Boolean
            classic name is RATE-OVR-FL
        Reprice                            is Boolean
            classic name is REPRICE-FL
        RateFreezeCode					   is AlphaUpper size 1
            classic name is RATE-FRZ-CD
            States
	            FreezeRateAtEntry      value is "E"
	            FreezeRateWhenInvoiced value is "I"
        LocationBasedPricing               is Boolean
            classic name is PRICE-LOC-FL
        CurrencyBasedPricing               is Boolean
            classic name is PRICE-CURR-FL
        AuditOrder                         is Boolean
            classic name is AUDIT-ORD-FL
        AuditPrice                         is Boolean
            classic name is AUDIT-PRC-FL
        AllocateDate                       is AlphaUpper size 1
            classic name is ALLOC-DT-FL
            States
                EntryDate        value is "E"
                RequestDate      value is "R"
                ExpectedShipDate value is "S"
        InvoiceOption                  is AlphaUpper size 1
            classic name is INVC-OPT-FL
            States
                CompanyLevelInvoicing value is "C"
                ProcessLevelInvoicing value is "P"
        InvoiceEditOption                  is Boolean
            classic name is INVC-EDIT-OPT
        InterfaceCost                      is Boolean
            classic name is INTF-COST-FL
        CashOnDeliveryOption               is Boolean
            classic name is COD-INVC-OPT
        BillingOperatorControl             is Boolean
            classic name is BL-OPER-CNTRL
        BillEqlInvc                        is Boolean
        TaxCalulationAndPrinting
            classic name is TAX-PRINT-CD
        TaxPrice	                       is Boolean
            classic name is TAX-PRICE-FL
        DiscountAddOnCharge		           is Boolean
            classic name is DISC-AOC-FL
        TermsAddOnCharge               	   is Boolean
            classic name is TERMS-AOC-FL
        InvoiceCreation		               is Boolean
            classic name is INVC-CR-FL
        InvoiceEdit		                   is Boolean
            classic name is INVC-EDIT-FL
        InvoicePrint	                   is Boolean
            classic name is INVC-PRT-FL
        InvoiceUpdate					   is Boolean
            classic name is INVC-UPD-FL
        InvoiceDetail	                   is Boolean
            classic name is INVC-DTL-FL
        ReleaseCommentOptions              is a RelComOptX10InOecompany
            classic name is REL-COM-OPT
        AcknowledgeCommentOptions          is an AcknowledgeCommentOptionsOccurs10
            classic name is ACK-COM-OPT
        QuotationCommentOptions            is a QteComOptX10InOecompany
            classic name is QTE-COM-OPT
        DailyShipmentJournalCommentOptions is a JorComOptX10InOecompany
            classic name is JOR-COM-OPT
        InvoiceRegisterCommentOptions      is an InvComOptX10InOecompany
            classic name is INV-COM-OPT
        RemitToName                        is a Name	 
        	holds pii
            classic name is RMT-NAME
        RemitToAddress                     is a PostalAddressV2	
        	holds pii
            classic name for RemitToAddress.DeliveryAddress.AddressLine1 is RMT-ADDR1
            classic name for RemitToAddress.DeliveryAddress.AddressLine2 is RMT-ADDR2
            classic name for RemitToAddress.DeliveryAddress.AddressLine3 is RMT-ADDR3
            classic name for RemitToAddress.DeliveryAddress.AddressLine4 is RMT-ADDR4
            classic name for RemitToAddress.Municipality is RMT-CITY
            classic name for RemitToAddress.StateProvince is RMT-STATE
            classic name for RemitToAddress.PostalCode is RMT-ZIP
            classic name for RemitToAddress.County is RMT-COUNTY
            classic name for RemitToAddress.Country is RMT-CTRY-CODE
            classic name for RemitToAddress.Region is RMT-REGION
        Incomplete						   is Boolean
            classic name is INCOMPLETE-FL
        BillingCompanySetupIncomplete      is Boolean
            classic name is BL-INCOMPL-FL
        ManualReturnNumbering              is Boolean
        EuropeanCommunityCountry           is an EcCtryNbr
            classic name is EC-CTRY-NBR
        ISOCountry                         is an IsoCtryNbr
            classic name is ISO-CTRY-NBR
        RemitToEuropeanCommunityCountry    is a RmtEcCtry
            sql name is RToEuropeanCommunityCountry
            classic name is RMT-EC-CTRY
        RemitToISOCountry                  is a RmtIsoCtry
            classic name is RMT-ISO-CTRY
        BackOrderControl                   is AlphaUpper size 1
            classic name is BACK-ORD-CNTRL
            States
                OrderEntryOnly             value is "O"
                WarehouseOnly              value is "W"
                BothOrderEntryAndWarehouse value is "B"
                Ignore                     value is "I"
        TermsFreight	                   is Boolean
            classic name is TERMS-FRT-FL
        LimitReturns                       is AlphaUpper size 1
            States
                None         value is "N"
                Days         value is "D"
                SpecificDate value is "S"
        ReturnDays                         is Numeric size 6

        Rounding                           is Boolean
			classic name is RND-FLG
        SalesAccount                       is a FinanceCodeBlock
            classic name for SalesAccount.AccountingUnit is SLS-ACCT-UNIT
            classic name for SalesAccount.GeneralLedgerChartAccount is SLS-ACCOUNT
        ExpenseAccount                     is a FinanceCodeBlock
            classic name for ExpenseAccount.AccountingUnit is EXP-ACCT-UNIT
            classic name for ExpenseAccount.GeneralLedgerChartAccount is EXP-ACCOUNT
        SpecialOrderSalesAccount           is a FinanceCodeBlock
            classic name for SpecialOrderSalesAccount.AccountingUnit is MI-ACCT-UNIT
            classic name for SpecialOrderSalesAccount.GeneralLedgerChartAccount is MI-ACCOUNT
        SpecialOrderCOGSAccount            is a FinanceCodeBlock
            classic name for SpecialOrderCOGSAccount.AccountingUnit is MCG-ACCT-UNIT
            classic name for SpecialOrderCOGSAccount.GeneralLedgerChartAccount is MCG-ACCOUNT
        DiscountAccount                    is a FinanceCodeBlock
            classic name for DiscountAccount.AccountingUnit is DSC-ACCT-UNIT
            classic name for DiscountAccount.GeneralLedgerChartAccount is DSC-ACCOUNT
        RoundingAccount                    is a FinanceCodeBlock
            classic name for RoundingAccount.AccountingUnit is RND-ACCT-UNIT
            classic name for RoundingAccount.GeneralLedgerChartAccount is RND-ACCOUNT
		VATOutputOffset                    is a FinanceCodeBlock
		BillingInvoiceDocument
		ProformaInvoiceDocument
		MultipleSalesAccountEntry			is Boolean

		UseIDM								is Boolean
			default label is "UseIDMTemplate"
		UseIDMInInvoiceRegister				is Boolean
			protected
			restricted
		IDMDocumentForInvoiceRegister		is an IDMOutputSettings
			protected
			restricted
		InvoiceAndRegisterPrintIDMTemplate  is an IDMOutputSettings	
			protected
			restricted
		IDMOrderEntryDailyShipmentJournalTemplate		is an IDMOutputSettings
			protected
			restricted
		InvoiceRegisterTemplate				is an IDMTemplate
			default label is "InvoiceRegister"
		BillingInvoiceTemplate				is an IDMTemplate
			default label is "BillingInvoice"
		BillingInvoiceEmailTemplate			is an IDMTemplate
			default label is "BillingInvoice"
		BillingInvoiceEmailSubject			is Alpha size 255
			default label is "BillingInvoice"
			Text Variables
				CompanyName
				InvoiceNumber
				InvoiceDate	
		DailyShipmentJournalIDMTemplate is an IDMTemplate
			default label is "DailyShipmentJournal"
		InvoiceAndRegisterEmailTemplate     is an IDMTemplate
			default label is "InvoiceAndRegister"
		InvoiceAndRegisterEmailSubject      is Alpha size 255
			default label is "InvoiceAndRegister"
			Text Variables
				CompanyName
				CustomerName
						

		ProcessInvoiceBods					is Boolean		
		ManualInvoiceNumbering				is Boolean		
		FrontEndSplitSalesAccount			is Boolean
		ProcessShipmentBods					is Boolean
		SalesAccountOverride				is Boolean
			default label is "AllowSalesAccountOverrideForInventoryItems"
		PrintFullInvoiceNumber				is Boolean
		AllowMixedSignLines					is Boolean


	Context Fields
		IDMDateRange						is a DateRange
		IDMGLDateRange						is a DateRange


	Local Fields
		ValidateReceivablePrefix
		LocalPrefix 						is like CmPrefix
		LocalInvoiceType					is like ReceivableInvoiceType

		LocalProcessLevelForIDM				is AlphaUpper size 5
		LocalCustomerForIDM					is a Customer
		LocalSummarizedInvoicesByCustomer	is Boolean
		LocalCompanyLevelTotalsComputed		is Boolean
		LocalCompanyLevelTotalFreightAmount	is like InternationalAmount
		LocalCompanyLevelTotalTaxAmount		is like InternationalAmount
		LocalCompanyLevelTotalInvoiceNetAmt	is like InternationalAmount
		LocalPrintInvoicesOnly				is Boolean
		LocalPrintCreditMemosOnly			is Boolean
		LocalBillingTotalFreightAmount		is like InternationalAmount
		LocalBillingTotalTaxAmount			is like InternationalAmount
		LocalBillingTotalInvoiceNet			is like InternationalAmount
		LocalInvoiceIsCOD					is Boolean	
		LocalCutOffDate						is Date
		LocalAddSubtract					is AlphaUpper size 1
			States
				Add			value is "A"
				Subtract	value is "S"
		LocalEnteredInvoicePrefix			is AlphaUpper size 5
		LocalFromInvNumber					is Numeric size 8
		LocalToInvNumber					is Numeric size 8			
		LocalInventoryLocation				is like InventoryLocation  

		LocalCompanyGroup					is a GeneralLedgerCompanyGroup
		LocalIDMDocumentType				is Numeric size 1
			States
				InvoiceRegister 			value is 1
				InvoiceAndRegisterPrint		value is 2
		LocalPrintOption					is Numeric size 1
			States
				CreditMemosOnly				value is 1  
				InvoicesOnly				value is 2
				CODInvoicesOnly				value is 3
				InvoicesAndCreditMemos		value is 4
		IDMItem
		IDMAttributes
		IDMGenerateDocument
		LocalBillingInvoiceIDMEmailSubject		  is Alpha size 255 
			holds pii
			Text Variables
				CompanyName					value is Name	 
				InvoiceNumber				value is "IL1234"
				InvoiceDate					value is current corporate date
		LocalInvoiceAndRegisterIDMEmailSubject	  is Alpha size 255 
			holds pii
			Text Variables
				CompanyName					value is Name
				CustomerName				value is "John Doe"
		InvoiceAndRegisterIDMEmailSubjectPerCustomer is Alpha size 255 
			holds pii
			Text Variables
				CompanyName					value is Name
				CustomerName				value is LocalCustomerForIDM.Name
		LocalBackgroundGroup					is AlphaUpper up to 200
		
	Transient Fields
    	TransientInvoicePrefix 				is like CmPrefix
			derive value from InvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix
    	TransientCreditMemoPrefix 			is like CmPrefix
    		derive value from CreditMemoBillingPrefixRel.BillingInvoicePrefix.Prefix
    	TransientCreditMemoPrintOption 		is a CreditMemoPrintOption
    		derive value from CreditMemoBillingPrefixRel.BillingInvoicePrefix.CreditMemoPrintOption
    	TransientRecurringInvoicePrefix 	is like CmPrefix
			derive value from RecurringInvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix
    	TransientLastBatchNumber 			is Numeric size 10
    		derive value from InvoiceBatchBillingPrefixRel.LastDocNbr
    	TransientLastInvoice 				is Numeric size 10
    		derive value from InvoiceBillingPrefixRel.LastDocNbr
    	TransientLastCreditMemo 			is Numeric size 10
    		derive value from CreditMemoBillingPrefixRel.LastDocNbr
    	TransientLastRecurringInvoice 		is Numeric size 10
    		derive value from RecurringInvoiceBillingPrefixRel.LastDocNbr
    	TransientLastARBatch 				is Numeric size 10
    		derive value from ARBatchBillingPrefixRel.LastDocNbr
    	TransientDefaultReasonDescription 	is like Description
    		derive value from OrderCancelCreditReasonRel.Description
    	TransientDefaultReasonCode			is like OrderCancelCreditReason	  
    	TransientSummarizeInvoicesByCustomer is Alpha 3

    	TransientFromInvoiceDate		  	is Date
    	TransientToInvoiceDate				is Date
    	TransientFromGLDate					is Date
    	TransientToGLDate					is Date
    	TransientProcessLevel				is a BillingProcessLevel
		TransientCustomer					is a Customer
	    TransientFrInvAddSub      			is AlphaUpper size 1
            States
                AddDays      value is "A"
                    default label is "Add days"
                SubtractDays value is "S"
                    default label is "Subtract days"	    
        TransientFrInvDays        			is Numeric size 3
		TransientToInvAddSub      			is AlphaUpper size 1
            States
                AddDays      value is "A"
                    default label is "Add days"
                SubtractDays value is "S"
                    default label is "Subtract days"		
        TransientToInvDays        			is Numeric size 3
        TransientFrGlAddSub       			is AlphaUpper size 1
            States
                AddDays      value is "A"
                    default label is "Add days"
                SubtractDays value is "S"
                    default label is "Subtract days"        
        TransientFrGlDays         			is Numeric size 3
        TransientToGlAddSub       			is AlphaUpper size 1
            States
                AddDays      value is "A"
                    default label is "Add days"
                SubtractDays value is "S"
                    default label is "Subtract days"        
        TransientToGlDays         			is Numeric size 3   
		TransientEmailFrom					is an EmailAddress 
			holds pii

    	
	
	Rule Blocks
		ValidatePrefix		
			initialize ValidateReceivablePrefix
		   	ValidateReceivablePrefix.EditReceivableCompany		= Company
		   	ValidateReceivablePrefix.EditSystem					= "BL"
			ValidateReceivablePrefix.EditReceivablePrefix		= LocalPrefix	
			ValidateReceivablePrefix.EditReceivableInvoiceType	= LocalInvoiceType	 
		    constraint (ValidateReceivablePrefix.ErrorNumber not entered)
		      	"<ValidateReceivablePrefix.ErrorMessage>"	
		      	
    Derived Fields
    	
		RepresentativeText is a StringField
			type is Text
			default label is "Company"
			Company " - " Name
			
 		ActorSecurityGroup is a DerivedField
			type is like CompanySecurityGroup
			return actor.context.CompanySecurityGroup
			


    	DerivedFileName is a DerivedField
    		type is like IDMFileName
    		restricted
    		return "InvoiceRegister_" + Company + "_" + DerivedDate + InvoiceRegisterTemplate.DerivedOutputFormat

		DerivedFileNamePerCustomer is a DerivedField
			type is like IDMFileName
			restricted
			return "InvoiceRegister_" + Company + "_" + LocalCustomerForIDM.Name + "_" + DerivedDate + InvoiceRegisterTemplate.DerivedOutputFormat 
    		
    	DerivedOutputFileName is a DerivedField
    		type is like IDMFileName
    		restricted
    		return "\"" + DerivedFileName + "\""
    		
    	DerivedDate is a DerivedField
    		type is Alpha 8
    		restricted
    		return current corporate date
    		
    	DerivedCurrentDateForIDM is a DerivedField
    		type is Date
    		return current corporate date
    		
		DerivedCurrentTimeForIDM is a DerivedField
			type is Time
			return current corporate time
			
		DerivedCompanyLevelTotalFreight is a DerivedField
    		type is like InternationalAmount
	    		precision is Company.BaseNumberOfDecimals
			if (LocalIDMDocumentType.InvoiceRegister)
				return sum BillingInvoicesForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
			else
			if (LocalIDMDocumentType.InvoiceAndRegisterPrint)
				if (LocalEnteredInvoicePrefix entered)
					if (LocalSummarizedInvoicesByCustomer)
						return sum PrintedBillingInvoiceRegisterPerCustomerForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
					else	
						return sum PrintedBillingInvoiceRegisterForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
				else
					if (LocalSummarizedInvoicesByCustomer)
						return sum EditedBillingInvoiceRegisterPerCustomerForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
					else
						return sum EditedBillingInvoiceRegisterForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
    		
    	DerivedCompanyLevelTotalTax is a DerivedField
    		type is like InternationalAmount
    			precision is Company.BaseNumberOfDecimals
			if (LocalIDMDocumentType.InvoiceRegister)
				return sum BillingInvoicesForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount
			else
			if (LocalIDMDocumentType.InvoiceAndRegisterPrint)
				if (LocalEnteredInvoicePrefix entered)
					if (LocalSummarizedInvoicesByCustomer)
						return sum PrintedBillingInvoiceRegisterPerCustomerForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount						
					else	
						return sum PrintedBillingInvoiceRegisterForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount						
				else
					if (LocalSummarizedInvoicesByCustomer)
						return sum EditedBillingInvoiceRegisterPerCustomerForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount						
					else
						return sum EditedBillingInvoiceRegisterForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount						
    		
    	DerivedCompanyLevelTotalInvoiceNet is a DerivedField
    		type is like InternationalAmount
    			precision is Company.BaseNumberOfDecimals
			if (LocalIDMDocumentType.InvoiceRegister)
				return sum BillingInvoicesForIDMRel.DerivedInvoiceNetAmountForIDM 
			else
			if (LocalIDMDocumentType.InvoiceAndRegisterPrint)
				if (LocalEnteredInvoicePrefix entered)
					if (LocalSummarizedInvoicesByCustomer)
						return sum PrintedBillingInvoiceRegisterPerCustomerForIDMRel.DerivedInvoiceNetAmountForIDM
					else	
						return sum PrintedBillingInvoiceRegisterForIDMRel.DerivedInvoiceNetAmountForIDM
				else
					if (LocalSummarizedInvoicesByCustomer)
						return sum EditedBillingInvoiceRegisterPerCustomerForIDMRel.DerivedInvoiceNetAmountForIDM						
					else
						return sum EditedBillingInvoiceRegisterForIDMRel.DerivedInvoiceNetAmountForIDM
		
		InvoiceRegisterMessage is a MessageField
			"InvoiceRegister"

		DefaultInvoiceRegisterEmailSubjectPerCustomer is a MessageField
			restricted
			"InvoiceAndRegisterForCompany<Name>Customer<LocalCustomerForIDM.Name>"
			

		
		NoDefinedTemplateMessage is a MessageField
			"Warning:_\NoTemplateDefined"
		
    	ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforBillingCompany"
		
		ContextMessageText is a MessageField
			restricted
			"Company<Company>"
			
        ComType1 is an ArrayValueField
            type is AlphaUpper size 1
            restricted
            InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[1].ItemCommentType
            
        DefaultReasonCode					is a MessageField
        	restricted
			"DefaultCancelReasonCode"
			
		DerivedBillingProcessLevel	 is a StringField 
			type is Alpha 5
			BillingProcessLevelRel.BillingProcessLevel
			
		DerivedBillingProcessLevelName is a StringField 
			type is like Name
			holds pii
			BillingProcessLevelRel.Name
						
		DerivedBillingTotalFreightAmountForIDM	is a DerivedField
			type is like InternationalAmount
			return LocalBillingTotalFreightAmount

		DerivedBillingTotalTaxAmountForIDM is a DerivedField
			type is like InternationalAmount
			return LocalBillingTotalTaxAmount
		          		
		DerivedBillingTotalInvoiceNetForIDM	is a DerivedField
			type is like InternationalAmount
			return LocalBillingTotalInvoiceNet
			
		DerivedCurrentMonthForIDM	is a DerivedField
			type is Numeric size 2
			restricted	
			return DerivedCurrentDateForIDM month	
			
		DerivedCurrentDayForIDM	is a DerivedField
			type is Numeric size 2
			restricted
			return DerivedCurrentDateForIDM day
			
		DerivedCurrentYearForIDM	is a DerivedField
			type is Numeric size 5
			restricted
			return DerivedCurrentDateForIDM year	
			
		DerivedXMLNameInvoiceAndRegisterPrint	is a StringField
			type is Alpha 100
			restricted
			"Invoice And Register Print "
			"Company "
			Company
			" "
			DerivedCurrentMonthForIDM
			DerivedCurrentDayForIDM
			DerivedCurrentYearForIDM

		InvoiceAndRegisterEmailNotificationMsg is a MessageField
			restricted
			"InvoiceAndRegisterForCompany<Company>WasSentToCustomer<LocalCustomerForIDM>"
			
		DerivedBillingInvoiceIDMEmailSubject	is a DerivedField
			type is Alpha size 255
			restricted
			LocalBillingInvoiceIDMEmailSubject		=	BillingInvoiceEmailSubject
			return LocalBillingInvoiceIDMEmailSubject text			

		DerivedInvoiceAndRegisterIDMEmailSubject	is a DerivedField
			type is Alpha size 255
			restricted
			LocalInvoiceAndRegisterIDMEmailSubject	=	InvoiceAndRegisterEmailSubject
			return LocalInvoiceAndRegisterIDMEmailSubject text	
			
		UserDefaultPrinter is a DerivedField
            type is like IDMPrinter
            if (UseIDM)
            	return UserDefaultPrinterRel.IDMPrinter
			
    Conditions
		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)
			
		NoDocumentTemplateDefined
			restricted
			when (BillingInvoiceDocument not entered)
		
        IsCompanyLevelInvoicing 
        	restricted
            when (InvoiceOption.CompanyLevelInvoicing)

        IsCurrencyUsed 
        	restricted
            when (Currency entered)

        IsExpenseAccount
        	restricted
            when (ExpenseAccount.AccountingUnit entered
            or    ExpenseAccount.GeneralLedgerChartAccount entered)


        IsMiscellaneousChargesAccount 
        	restricted
            when (SpecialOrderCOGSAccount.AccountingUnit entered
            or    SpecialOrderCOGSAccount.GeneralLedgerChartAccount entered)


        IsMultiCurrency
        	restricted
            when (MultipleCurrency)

        IsOrdDiscountAccount
        	restricted
            when (DiscountAccount.AccountingUnit entered
            or    DiscountAccount.GeneralLedgerChartAccount entered)


        IsProcessLevelInvoicing
        	restricted
            when (InvoiceOption.ProcessLevelInvoicing)

        WasValueInCombinationOne
        	restricted
        	when (old TaxCalulationAndPrinting.CalcAndPrintInSummary
        	or    old TaxCalulationAndPrinting.CalcSummPrtDetailSumm)
        
        IsValueInCombinationOne
        	restricted
        	when (TaxCalulationAndPrinting.CalcAndPrintInSummary
        	or TaxCalulationAndPrinting.CalcSummPrtDetailSumm)
        
        WasValueInCombinationTwo
        	restricted
        	when (old TaxCalulationAndPrinting.CalcByLinePrintDetail
		    or 	  old TaxCalulationAndPrinting.CalcByLinePrintSummary
		    or 	  old TaxCalulationAndPrinting.CalcLinePrtDetailSumm
		    or 	  old TaxCalulationAndPrinting.PriceInclTaxPrintSummary)
		    
        IsValueInCombinationTwo
        	restricted
        	when (TaxCalulationAndPrinting.CalcByLinePrintDetail
		    or 	  TaxCalulationAndPrinting.CalcByLinePrintSummary
		    or 	  TaxCalulationAndPrinting.CalcLinePrtDetailSumm
		    or 	  TaxCalulationAndPrinting.PriceInclTaxPrintSummary)
        
    	IsUniqueInvoicePrefix
    		restricted
    		when (TransientInvoicePrefix			!= TransientCreditMemoPrefix
			and   TransientInvoicePrefix			!= TransientRecurringInvoicePrefix)
						
		IsUniqueCreditMemoPrefix
			restricted
    		when (TransientCreditMemoPrefix			!= TransientInvoicePrefix
			and   TransientCreditMemoPrefix			!= TransientRecurringInvoicePrefix)
			
		IsUniqueRecurringInvoicePrefix
			restricted
    		when (TransientRecurringInvoicePrefix	!= TransientCreditMemoPrefix
			and   TransientRecurringInvoicePrefix	!= TransientInvoicePrefix)
		IsReasonCodeNotExists
			restricted
			when (OrderCancelCreditReasonRel not exists)	
			
		IsReasonCodeExists
			restricted
			when (OrderCancelCreditReasonRel exists)	

		CommentType2Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[2].ItemCommentType entered)

		CommentType3Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[3].ItemCommentType entered)

		CommentType4Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[4].ItemCommentType entered)

		CommentType5Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[5].ItemCommentType entered)

		CommentType6Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[6].ItemCommentType entered)

		CommentType7Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[7].ItemCommentType entered)

		CommentType8Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[8].ItemCommentType entered)

		CommentType9Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[9].ItemCommentType entered)

		CommentType10Entered
			when (InventoryCompanyRel.ItemGroup.ItemCommentTypeArray.ItemCommentTypeGroup[10].ItemCommentType entered)
			
		HasDefaultPrinter
			restricted
			when (UserDefaultPrinterRel exists)

		CanEnableFrontEndSplits
			restricted
			when (Company.FinanceEnterpriseGroup.FrontEndSplits
			and MultipleSalesAccountEntry)

		FrontEndSplitsEnabled
			restricted
			when (CanEnableFrontEndSplits
			and FrontEndSplitSalesAccount)
			
		HasBillingReportExtendedPrintConfiguration
			restricted
			when (BillingReportExtendedPrintConfigurationRel exists)

    Relations
 		SecurityGroupRel
			one-to-one relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		  = Company.FinanceEnterpriseGroup
				related.DimensionGroupType	   		  = 2
				related.FinanceDimensionSecurityGroup = ActorSecurityGroup
				
		SecurityGroupDetailRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses Set1
				related.Company						= Company
				related.GeneralLedgerCompanyGroup  	= SecurityGroupRel.GeneralLedgerCompanyGroup   

		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = Company
   


		BillingProcessLevelRel
			one-to-many relation to BillingProcessLevel
			Field Mapping uses symbolic key
				related.Company				= Company
			Instance Selection
				where	(LocalProcessLevelForIDM not entered
				or		 related.BillingProcessLevel	= LocalProcessLevelForIDM)


        CustomerOrderLinesRel is a CustomerOrderLine set

        CustomerOrdersRel is a CustomerOrder set

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = Company
				
        BillingInvoicePrefixesRel is a BillingInvoicePrefix set

       	AuthBatchBillingPrefixRel		
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= blank  
				related.BillingInvoicePrefix	=  "CB"

		ARBatchBillingPrefixRel
			one-to-one relation to BillingInvoicePrefix
			Field Mapping uses symbolic key
				related.Company					= Company
				related.BillingProcessLevel		= blank
				related.BillingInvoicePrefix	=  "AB"

   		CreditMemoBillingPrefixRel				
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= blank  
				related.BillingInvoicePrefix	= "CM"	

		InvoiceBatchBillingPrefixRel	
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= blank  
				related.BillingInvoicePrefix	= "IB"	

		InvoiceBillingPrefixRel				
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= blank  
				related.BillingInvoicePrefix	= "IN"
				
		RecurringInvoiceBillingPrefixRel	
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= blank  
				related.BillingInvoicePrefix	= "RI"

        PriceDiscountCodesRel is a PriceDiscountCode set

        BillingInvoicesRel is a BillingInvoice set
		
		UnreleasedBillingInvoicesRel
			one-to-many relation to BillingInvoice
            Field Mapping uses symbolic key
                related.Company 				= Company
            Instance Selection
            	where (related.Status.Unreleased)
		
		UnpostedCPInvoicesRel
			one-to-many relation to BillingInvoice
			Field Mapping uses Set7
               	related.Company					 = Company
            Instance Selection    
                where (related.GeneralLedgerDate <= Company.AccountingEntity.CurrentPeriodEndDate)

        OrderCancelCreditReasonRel
            one-to-one relation to OrderCancelCreditReason
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.OrderCancelCreditReason = DefaultReason

		PriceBaseRel is a PriceBase set
			
		CustomerOrderReturnsRel is a CustomerOrderReturn set
		
		UnpostedCPCustomerReturnsRel is a CustomerOrderReturn set
			Instance Selection
				where (related.PostingDate <= Company.AccountingEntity.CurrentPeriodEndDate
				and related.Status < 2)
						
		UnpostedCPInvoiceImportsRel 
			one-to-many relation to BillingInvoiceImport
			Field Mapping uses Set2
				related.Company		= Company
			Instance Selection
				where (related.GeneralLedgerDate	<= 	Company.AccountingEntity.CurrentPeriodEndDate)

		PriceBooksRel is a PriceBook set
		





       	TaxEntityRel				 
            one-to-one relation to TaxEntity
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
                related.TaxEntity 				= Company.AccountingEntity

											
		BLSystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses symbolic key
				related.Company						= Company
				related.GeneralLedgerSystemCode		= "BL"

		BillingProcessLevelInvoicePrefixRel
			one-to-many relation to BillingInvoicePrefix
			Field Mapping uses symbolic key
				related.Company					= Company
				related.BillingProcessLevel		= LocalProcessLevelForIDM
			Instance Selection
				where (related.BillingInvoicePrefix.Invoice)
				
		BillingProcessLevelCreditMemosPrefixRel
			one-to-many relation to BillingInvoicePrefix
			Field Mapping uses symbolic key
				related.Company					= Company
				related.BillingProcessLevel		= LocalProcessLevelForIDM
			Instance Selection
				where (related.BillingInvoicePrefix.CreditMemo)
					
		EditedBillingInvoiceRegisterForIDMRel
			one-to-many relation to BillingInvoice
			Field Mapping uses Set4
				related.Company	= Company	
			Instance Selection
				where (related.Status.Edited
				and	   related.HoldCode not entered
				and	  (LocalProcessLevelForIDM not entered or related.BillingProcessLevel = LocalProcessLevelForIDM)
				and   (LocalCutOffDate not entered or related.InvoiceDate <= LocalCutOffDate)
				and	  (not LocalInvoiceIsCOD or related.CustomerOrder.CashOnDelivery = LocalInvoiceIsCOD)
				and   (not LocalPrintInvoicesOnly or not related.BillingInvoice.BillingInvoiceType.Credit)
				and   (not LocalPrintCreditMemosOnly or related.BillingInvoice.BillingInvoiceType.Credit))

		EditedBillingInvoiceRegisterPerCustomerForIDMRel
			one-to-many relation to BillingInvoice
			Field Mapping uses Set4
				related.Company	 = Company
				related.Customer = LocalCustomerForIDM
			Instance Selection
				where (related.Status.Edited
				and	   related.HoldCode not entered
				and	  (LocalProcessLevelForIDM not entered or related.BillingProcessLevel = LocalProcessLevelForIDM)
				and   (LocalCutOffDate not entered or related.InvoiceDate <= LocalCutOffDate)
				and	  (not LocalInvoiceIsCOD or related.CustomerOrder.CashOnDelivery = LocalInvoiceIsCOD)
				and   (not LocalPrintInvoicesOnly or not related.BillingInvoice.BillingInvoiceType.Credit)
				and   (not LocalPrintCreditMemosOnly or related.BillingInvoice.BillingInvoiceType.Credit))
						
		PrintedBillingInvoiceRegisterForIDMRel
			one-to-many relation to BillingInvoice
			Field Mapping uses Set4
				related.Company	= Company	
			Instance Selection
				where ((related.Status.Printed or related.Status.ARGLUpdated)
				and   (LocalProcessLevelForIDM not entered or related.BillingProcessLevel = LocalProcessLevelForIDM)
				and   (LocalEnteredInvoicePrefix not entered or related.BillingInvoice.InvoicePrefix = LocalEnteredInvoicePrefix)
				and	  (LocalFromInvNumber not entered or related.BillingInvoice.InvoiceNumber >= LocalFromInvNumber)
				and	  (LocalToInvNumber not entered or related.BillingInvoice.InvoiceNumber   <= LocalToInvNumber))

		PrintedBillingInvoiceRegisterPerCustomerForIDMRel
			one-to-many relation to BillingInvoice
		    Field Mapping uses Set4
                related.Company  = Company
                related.Customer = LocalCustomerForIDM
           	Instance Selection
				where ((related.Status.Printed or related.Status.ARGLUpdated)
				and   (LocalProcessLevelForIDM not entered or related.BillingProcessLevel = LocalProcessLevelForIDM)
				and   (LocalEnteredInvoicePrefix not entered or related.BillingInvoice.InvoicePrefix = LocalEnteredInvoicePrefix)
				and	  (LocalFromInvNumber not entered or related.BillingInvoice.InvoiceNumber >= LocalFromInvNumber)
				and	  (LocalToInvNumber not entered or related.BillingInvoice.InvoiceNumber   <= LocalToInvNumber))

						
		InventoryLocationForOrderEntryDailyShipmentJournalIDMRel
			one-to-one relation to InventoryLocation
			Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation 	= LocalInventoryLocation

		CompanyGroupRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup 	= LocalCompanyGroup
				related.Company						= Company
				
		UserDefaultPrinterRel
			one-to-one relation to UserDefaultPrinter
			Field Mapping uses symbolic key
				related.UserDefaultPrinter.Actor	= actor

    	BillingInvoicesForIDMRel
    		one-to-many relation to BillingInvoice
			Field Mapping uses Set9
				related.Company				= Company
				related.BillingProcessLevel	= LocalProcessLevelForIDM
			Instance Selection
				where	(related.Status.ARGLUpdated
				and		 related.InvoiceDate 		within IDMDateRange
				and		 related.GeneralLedgerDate  within IDMGLDateRange
				and		(LocalCustomerForIDM not entered or related.Customer = LocalCustomerForIDM))

		CompanyCustomerRel
			one-to-one relation to CompanyCustomer	
			Field Mapping uses symbolic key
				related.Company		= Company	
				related.Customer	= LocalCustomerForIDM
				
				
		BillingReportExtendedPrintConfigurationRel
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= "BillingReportExtendedPrint"	
					
    Field Rules
		Company
			constraint (InventoryCompanyRel exists)
				"InventoryControlCompanyDoesNotExist"		
			
			constraint (TaxEntityRel exists)
				"TaxEntityDoesNotExist"					
			
		Name
            required										
        
        LocationBasedPricing
        	if (PriceBaseRel exists)
 				cannot be changed
        			"CannotChangeLocationBasedPricingOption,APriceBaseExist"		
        		
		InvoiceOption
      		required
        	if (BillingInvoicesRel exists)
        		cannot be changed
        			"CannotChangeOption;InvoicesExist"						
			
        CashOnDeliveryOption
    		constraint (IsCompanyLevelInvoicing) 	
    			"CannotSetCashOnDeliveryOption;InvoiceOptionIsNotByCompanyLevelInvoicing"	
        			
        DiscountAddOnCharge
    		constraint (IsCompanyLevelInvoicing)
    			"CannotSetDiscountORAddOnChargeOption;InvoiceOptionIsNotByCompanyLevelInvoicing"	
        
        TermsAddOnCharge
    		constraint (IsCompanyLevelInvoicing)	
    			"CannotSetTermsDiscountOption;InvoiceOptionIsNotByCompanyLevelInvoicing"	
        			
        TermsFreight
    		constraint (IsCompanyLevelInvoicing)	
    			"CannotSetTermsFreightOption;InvoiceOptionIsNotByCompanyLevelInvoicing"	
        
        InterfaceCost
    		constraint (IsCompanyLevelInvoicing)	
    			"CannotSetPostCostForInterface;InvoiceOptionIsNotByCompanyLevelInvoicing"	
       
       	Currency
       		force default to Company.GeneralLedgerCompany.Currency
        
        MultipleCurrency
    		constraint (Company.ReceivableCompany.MultiCurrencyProcessing)
    			"RecievableCompanyDoesNotAllowMultipleCurrency"		
    			
		BaseNumberOfDecimals
			force default to Company.ReceivableCompany.BaseNumberOfDecimals
       	
       	DefaultCurrencyOverride
       		constraint (IsMultiCurrency)
   				"CannotSetCurrencyOverrideOptionForSingleCurrencyCompany"	
       	
       	RateOverride
       		constraint (IsMultiCurrency)
				"CannotSetRateOverrideOptionForSingleCurrencyCompany"		
       			
       	CurrencyBasedPricing
   			constraint (IsMultiCurrency)
   				"CannotSetCurrencyBasedPricingOptionForSingleCurrencyCompany"	
        		
        RateFreezeCode
        	required		
        		"MustEnterCurrencyRateFreeze"		
        		
        TaxCalulationAndPrinting
        	if (InvoiceOption.CompanyLevelInvoicing)
        		required
        			"MustEnterTaxOption"									
        			
        		if (TaxCalulationAndPrinting.CalcAndPrintInSummary
        		or TaxCalulationAndPrinting.CalcLinePrtDetailSumm
        		or TaxCalulationAndPrinting.CalcSummPrtDetailSumm)
        			constraint (TaxEntityRel.ThirdParty.InforTax) 
        				"OptionsCalcAndPrintInSummary,CalcLinePrtDetailSumm,AndCalcSummPrtDetailSummNotValidWithThirdPartyTaxSystems" 
				
				if (TaxCalulationAndPrinting changed)
					if (WasValueInCombinationOne 
					and IsValueInCombinationTwo)
						constraint (UnreleasedBillingInvoicesRel not exists)
							"Invoice(s)Exist;CanOnlyChangeToOptionCalcAndPrintInSummaryOrCalcSummPrtDetailSumm"				
						
					if (WasValueInCombinationTwo
					and IsValueInCombinationOne)
						constraint (UnreleasedBillingInvoicesRel not exists)
							"Invoice(s)Exist;CanOnlyChangeToOptionCalcByLinePrintDetail,CalcByLinePrintSummary,PriceInclTaxPrintSummary,OrCalcLinePrtDetailSumm"  
				
			if (InvoiceOption.ProcessLevelInvoicing)
    			cannot be entered
    				"CannotEnterTaxPrintOption;InvoiceOptionIsNotByCompanyLevelInvoicing"	
        				
        DefaultReason
			if (TransientDefaultReasonCode not entered)
				required
					"DefaultCancellationReasonIsRequired"

			if (OrderCancelCreditReasonRel exists) 
				constraint (OrderCancelCreditReasonRel.Canceled)
					"ReasonCodeInvalidForCancelations"					
       
        TransientDefaultReasonCode
        	if (DefaultReason not entered)
        		required
        
        ManualReturnNumbering
			if (CustomerOrderReturnsRel exists)
				cannot be changed
					"CannotChangeReturnNumberingOption;ReturnsExist"		
					
		ManuallyNumbered
			if (CustomerOrdersRel exists)
				cannot be changed
					"CannotChangeOrderNumberingOption;CustomerOrdersExist"	
		
        AllocateDate
            default to AllocateDate.ExpectedShipDate 

        BackOrderControl
            default to BackOrderControl.OrderEntryOnly

        LimitReturns
            default to LimitReturns.None
            
        ReturnDays
        	if (LimitReturns.Days)
            	required
        			"ReturnDaysAreRequired"					
            
            if (LimitReturns.None 
            or LimitReturns.SpecificDate)
        		cannot be entered
        			"RetrunDaysAreNotAllowed"  				

		RouteAndStop
			if (CustomerOrdersRel exists)
				cannot be changed
					"CannotChangeRoute-stop,OrdersExist"		

		InvoiceDetail
			constraint (IsCompanyLevelInvoicing)
				"CannotSetPrintDetail;InvoiceOptionIsNotByCompanyLevelInvoicing"	
		
		TransientInvoicePrefix
			if (IsCompanyLevelInvoicing)
				required
					"MustEnterInvoicePrefix"						

			   	if (action type.Create
			   	or (action type.Update 
			   	and TransientInvoicePrefix not = InvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix))
				   	LocalPrefix			= TransientInvoicePrefix
				   	LocalInvoiceType	= "I"
				   	include ValidatePrefix
			else
				cannot be entered
					"CannotEnterInvoicePrefix;InvoiceOptionIsNotByCompanyLevelInvoicing" 
			
			constraint (IsUniqueInvoicePrefix)
				"Invoice,CreditMemo,RecurringPrefixMustBeUnique"				
					
					
		TransientCreditMemoPrefix
			if (IsCompanyLevelInvoicing)
				required
					"MustEnterCreditMemoPrefix"			

			   	if (action type.Create
			   	or (action type.Update 
			   	and TransientCreditMemoPrefix not = CreditMemoBillingPrefixRel.BillingInvoicePrefix.Prefix))
					LocalPrefix			= TransientCreditMemoPrefix
					LocalInvoiceType	= "C"
				   	include ValidatePrefix
			else
				cannot be entered
					"CannotEnterCreditMemoPrefix;InvoiceOptionIsNotByCompanyLevelInvoicing"		
			
			constraint (IsUniqueCreditMemoPrefix)
				"Invoice,CreditMemo,RecurringPrefixMustBeUnique"				
			
							
		TransientRecurringInvoicePrefix
			if (IsCompanyLevelInvoicing)
				required
					"MustEnterRecurringInvoicePrefix"			

			   	if (action type.Create
			   	or (action type.Update 
			   	and TransientRecurringInvoicePrefix not = RecurringInvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix))
					LocalPrefix			= TransientRecurringInvoicePrefix
					LocalInvoiceType	= "I"
				   	include ValidatePrefix				
			else
				cannot be entered
					"CannotEnterRecurringPrefix;InvoiceOptionIsNotByCompanyLevelInvoicing"		

			constraint (IsUniqueRecurringInvoicePrefix)
				"Invoice,CreditMemo,RecurringPrefixMustBeUnique"				
					
		TransientDefaultReasonDescription
			default to DefaultReasonCode
	
		SalesAccount
			required
		
		ExpenseAccount
			required
		
		SpecialOrderSalesAccount
			required
		
		SpecialOrderCOGSAccount
			required
		
		DiscountAccount
			required
					
		RoundingAccount
			if (IsCompanyLevelInvoicing
			and Rounding)
				required
				
		InvoiceRegisterTemplate	
			if (UseIDM)
				required
				
				constraint (InvoiceRegisterTemplate.IDMDocumentType.InvoiceRegister)
					"InvalidTemplate"

		BillingInvoiceTemplate	
			constraint (BillingInvoiceTemplate.IDMDocumentType.BillingInvoice)
				"InvalidTemplate"			
			
		BillingInvoiceEmailTemplate		
			constraint (BillingInvoiceEmailTemplate.IDMDocumentType.BillingInvoiceEmail)
				"InvalidTemplate"	
				
		DailyShipmentJournalIDMTemplate
			if (UseIDM)
				required
			constraint (DailyShipmentJournalIDMTemplate.IDMDocumentType.DailyShipmentJournal)
				"InvalidTemplate"
				
		FrontEndSplitSalesAccount
			constraint (MultipleSalesAccountEntry)
				"MultipleSalesAccountEntryMustBeEnabledBeforeEnablingFrontEndSplitSalesAccount"
							
	Actions

    			
		Create is a Create Action
			Exit Rules
				if (TransientDefaultReasonCode entered) 
    				invoke Create OrderCancelCreditReason
    					invoked.Company					= Company
    					invoked.OrderCancelCreditReason	= TransientDefaultReasonCode
    					invoked.Description				= TransientDefaultReasonDescription//"DefaultReason" 
    					invoked.Canceled				= true
    					invoked.Billing					= true
    					invoked.RemoveBooking 			= true
    					invoked.Active					= true
					DefaultReason	= 	TransientDefaultReasonCode

        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "ON"	
        		
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "RN"	
        			
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix 	= "CB"	
        		
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "TI"	
        		
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix 	= "IB"	
        			

        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "IN"
        			invoked.Prefix					= TransientInvoicePrefix	

				invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "CM"
        			invoked.Prefix					= TransientCreditMemoPrefix	

        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "RI"
        			invoked.Prefix					= TransientRecurringInvoicePrefix	
        			
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
					initialize invoked.BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "AB"

		T2VCreate is a Create Action				
			restricted 
			default label is untranslatable 
			bypass field rules 
			        	
		Update is an Update Action
			Exit Rules
				if (DefaultReason changed) 
					if (OrderCancelCreditReasonRel not exists)
        				invoke Create OrderCancelCreditReason
        					invoked.Company					= Company
        					invoked.OrderCancelCreditReason	= DefaultReason
        					invoked.Description				= TransientDefaultReasonDescription//"DefaultReason" 
        					invoked.Canceled				= true
        					invoked.Billing					= true
        					invoked.RemoveBooking 			= true
        					invoked.Active					= true


				if (TransientInvoicePrefix changed) 
					invoke Update InvoiceBillingPrefixRel
						if (IsCompanyLevelInvoicing)
		    				invoked.Prefix									 = TransientInvoicePrefix
		    			else
		    				invoked.Prefix									 = blank
		    			display "<TransientInvoicePrefix>"
	    			
				if (TransientCreditMemoPrefix changed)
	    			invoke Update CreditMemoBillingPrefixRel
						if (IsCompanyLevelInvoicing)	
		    				invoked.Prefix									 = TransientCreditMemoPrefix
		    			else
		    				invoked.Prefix									 = blank 	
		    			display "<TransientCreditMemoPrefix>"
	    		
				invoke Update CreditMemoBillingPrefixRel 
	    			if (IsCompanyLevelInvoicing
	    			and TransientCreditMemoPrintOption entered)
	    				invoked.CreditMemoPrintOption					= TransientCreditMemoPrintOption
	    			else
						initialize invoked.CreditMemoPrintOption

				if (TransientRecurringInvoicePrefix changed)
	    			invoke Update RecurringInvoiceBillingPrefixRel
						if (IsCompanyLevelInvoicing)	
	    					invoked.Prefix									 = TransientRecurringInvoicePrefix
	    				else
	    					invoked.Prefix									 = blank	
						display "<TransientRecurringInvoicePrefix>"
				
				TransientCreditMemoPrintOption = CreditMemoBillingPrefixRel.BillingInvoicePrefix.CreditMemoPrintOption		

		T2VUpdate is an Update Action				
			restricted 
			default label is untranslatable 
			bypass field rules 

		Delete is a Delete Action
			Entrance Rules
				constraint (CustomerOrdersRel not exists)
					"CannotDelete;CustomerOrdersExist"			
					
				constraint (PriceDiscountCodesRel not exists)
					"CannotDelete;DiscountCodesExist"			
				
				invoke Delete BillingInvoicePrefixesRel
				invoke Delete PriceBooksRel

		BillingSystemClose is a Set Action
			run in foreground
			restricted
			Parameters
				FinanceEnterpriseGroup
				PrmCompany					is a BillingCompany
					default label is "Company"
				PrmCompanyGroup				is a GeneralLedgerCompanyGroup		
					default label is "CompanyGroup"

			Local Fields
				LocalActor												is an Actor
				LocalNotificationDetails								is LPLText
				LocalProcessedCompanies         						is LPLText
				LocalCompaniesCounter									is Numeric 4
				LocalProcessedCompaniesCounter				            is Numeric 4
				LocalNotProcessedCompanies      						is LPLText
				LocalNotProcessedCompaniesCounter			        	is Numeric 4
			
			Parameter Rules
				FinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
				PrmCompany
					if (PrmCompany entered)
						constraint (PrmCompanyGroup not entered)
							"CannotEnterBoth_CompanyAnd_Company_Group"
						constraint (PrmCompany.BLSystemClosingControlRel.Control = true)
							"CompanyIsNotSetUpFor_Close_Control"
						constraint (PrmCompany.BLSystemClosingControlRel.CurrentPeriodNotClosed)
							"PeriodIsAlreadyClosedFor_Billing_System.ClosePeriodFor_Global_LedgerToRunAgain."
					if (PrmCompany not entered)
						constraint (PrmCompanyGroup entered)
							"The_Company_GroupOr_CompanyParameterIsRequired"
				PrmCompanyGroup
					if (PrmCompanyGroup entered)
						LocalCompanyGroup = PrmCompanyGroup
						constraint (PrmCompanyGroup.BLSystemClosingControlCompaniesWithControlRel exists)
							"NoCompanyInThe_Company_GroupIsSetUpFor_Billing_System_Close_Control"
						constraint (PrmCompanyGroup.BLSystemClosingControlCompaniesWithOpenPeriodRel exists)
							"PeriodIsAlreadyClosedForThe_Billing_SystemForAllCompaniesWith_Billing_System_Closing_ControlInThe_Company_Group"
							
			Instance Selection
				where (((PrmCompany entered 
				and      Company		= PrmCompany)
        		or		(PrmCompanyGroup entered 
        		and      CompanyGroupRel exists))	
				and     (BLSystemClosingControlRel.Control 	= true))				
			Sort Order
				Company
				
			Action Rules
				Set Rules
					Entrance Rules
						LocalActor = actor
					Exit Rules
						if (PrmCompanyGroup entered)	
							LocalNotificationDetails += "Billing system closing completed for company group " + PrmCompanyGroup + "."
							LocalNotificationDetails += "\u000a"
							if (LocalProcessedCompaniesCounter entered)
								LocalNotificationDetails +="Processed " + LocalProcessedCompaniesCounter + " out of " + LocalCompaniesCounter + " companies: "+ LocalProcessedCompanies + " with billing system closing control."
								LocalNotificationDetails += "\u000a"
							if (LocalNotProcessedCompaniesCounter entered)
								LocalNotificationDetails += "Unable to process " + LocalNotProcessedCompaniesCounter + " out of " + LocalCompaniesCounter + " companies. "
								LocalNotificationDetails += "Period is already closed for Billing System for company: " + LocalNotProcessedCompanies + " with billing system closing control."
							send notification
								to LocalActor
								description is "BillingSystemClosingCompletedForCompanyGroup<PrmCompanyGroup>"
								priority is high
								detail is "<LocalNotificationDetails>"
				Instance Rules
					constraint (!UnpostedCPInvoicesRel exists)
						"UnpostedInvoiceExistFor_Company<Company>"		
					constraint (!UnpostedCPCustomerReturnsRel exists)
						"UnpostedReturnExistsIn<Company>"
					constraint (!UnpostedCPInvoiceImportsRel exists)
						"UnpostedInterfaceInvoicesExistFor_Company<Company>"
					increment LocalCompaniesCounter
					if (Company.BLSystemClosingControlRel.CurrentPeriodNotClosed)
						increment LocalProcessedCompaniesCounter
						if (LocalProcessedCompaniesCounter = 1)
							LocalProcessedCompanies 		 = Company
						else
							LocalProcessedCompanies      	+= ", "
							LocalProcessedCompanies      	+= Company
						invoke UpdateCompanySystemClosingControl BLSystemClosingControlRel
							invoked.PrmCompany		 			= Company
							invoked.PrmSystem				 	= "BL"
					else
				
						increment LocalNotProcessedCompaniesCounter
						if (LocalNotProcessedCompaniesCounter = 1)
							LocalNotProcessedCompanies 	        	 = Company
						else
							LocalNotProcessedCompanies	           	+= ", "
							LocalNotProcessedCompanies 	            += Company

					if (PrmCompanyGroup not entered)
						send notification
							to LocalActor
							description is "BillingSystemClosingCompleted.Company:<Company>_<Company.Name>.Period:<Company.BLSystemClosingControlRel.ClosedPeriod.PeriodName>"
							priority is high
							detail is "BillingSystemClosingCompleted.Company:<Company>_<Company.Name>.Period:<Company.BLSystemClosingControlRel.ClosedPeriod.PeriodName>"


    	PrintInvoiceRegister is an Instance Action
    		default label is untranslatable
    		restricted
    		Parameters
    			PrmFromIDMDate		is Date
    			PrmToIDMDate		is Date
    			PrmFromGLIDMDate	is Date
    			PrmToGLIDMDate		is Date
    			PrmProcessLevel		is AlphaUpper size 5
    			PrmCustomer			is Numeric size 9
    		    PrmFrInvAddSub      is AlphaUpper size 1
                PrmFrInvDays        is Numeric size 3
        		PrmToInvAddSub      is AlphaUpper size 1
                PrmToInvDays        is Numeric size 3
		        PrmFrGlAddSub       is AlphaUpper size 1
		        PrmFrGlDays         is Numeric size 3
		        PrmToGlAddSub       is AlphaUpper size 1
		        PrmToGlDays         is Numeric size 3                
                    		
    		Parameter Rules
    			PrmFromIDMDate
    				required
    			PrmToIDMDate
    				required
    			PrmFromGLIDMDate
    				required
    			PrmToGLIDMDate
    				required
    			
    		Action Rules
    			LocalProcessLevelForIDM  = PrmProcessLevel
    			TransientProcessLevel    = PrmProcessLevel
    			LocalCustomerForIDM		 = PrmCustomer
    			TransientCustomer		 = PrmCustomer
    			IDMDateRange.Begin		 = PrmFromIDMDate
    			TransientFromInvoiceDate = PrmFromIDMDate
    			IDMDateRange.End		 = PrmToIDMDate
    			TransientToInvoiceDate   = PrmToIDMDate
    			IDMGLDateRange.Begin	 = PrmFromGLIDMDate
    			TransientFromGLDate   	 = PrmFromGLIDMDate
    			IDMGLDateRange.End		 = PrmToGLIDMDate
    			TransientToGLDate		 = PrmToGLIDMDate
    		    TransientFrInvAddSub     = PrmFrInvAddSub      
                TransientFrInvDays       = PrmFrInvDays        
        		TransientToInvAddSub	 = PrmToInvAddSub    
                TransientToInvDays		 = PrmToInvDays        
		        TransientFrGlAddSub		 = PrmFrGlAddSub      
		        TransientFrGlDays		 = PrmFrGlDays         
		        TransientToGlAddSub		 = PrmToGlAddSub     
		        TransientToGlDays		 = PrmToGlDays           
    			LocalIDMDocumentType	 = 1 
  				TransientSummarizeInvoicesByCustomer = "No"  			

    			initialize IDMGenerateDocument
    		
    			IDMGenerateDocument.IDMXMLDefinition.Busclass 	  = reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName 	  = "BillingCompanyDetailsForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName = "InvoiceRegister"
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName	 = "BillingProcessLevelRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName   	 = "BillingProcessLevelForIDMList"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1 = 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag		 = "Lines"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag		 = "Line"    			
    			
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName  = "BillingInvoicesForIDMRel"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName 	 = "BillingInvoiceListForIDM"
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1 = 1
    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection2 = 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag		 = "LineInvoice"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag		 = "Invoice"      			
    			
				initialize IDMAttributes
				
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "BillingCompany"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= Company
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "BillingCompanyName"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= Name
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "PrintDate"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeDate  = current corporate date
    			
    			IDMGenerateDocument.IDMAttributes	 = IDMAttributes
    			IDMGenerateDocument.TemplateUniqueId = InvoiceRegisterTemplate.IDMUniqueId
    			IDMGenerateDocument.DocumentType	 = "FSM_InvoiceRegister"
    			IDMGenerateDocument.FileName		 = DerivedFileName

    			IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
    			
    			invoke CreateFromGenerateDocument IDMJob
    				invoked.Actor				= actor
					invoked.Description			= InvoiceRegisterMessage
					invoked.IDMGenerateDocument = IDMGenerateDocument
        								
		PrintInvoiceAndRegisterToIDM is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmProcessLevel							is AlphaUpper size 5
				PrmInvoiceDate							is Date
				PrmPrintOption							is Numeric size 1
					States
						CreditMemosOnly					value is 1  
						InvoicesOnly					value is 2
						CODInvoicesOnly					value is 3
						InvoicesAndCreditMemos			value is 4
				PrmAddSubtract							is AlphaUpper size 1
					States
						Add								value is "A"
						Subtract						value is "S"
				PrmCutOffNumberOfDays					is Numeric size 3
				PrmInvoicePrefix						is AlphaUpper size 5
				PrmFromInvoiceNumber					is Numeric size 8
				PrmToInvoiceNumber						is Numeric size 8
				PrmIDMPrinter				 			is an IDMPrinter
				PrmBillingInvoiceOutputHeader			is a BillingInvoiceOutputHeader
				PrmSummarizeInvoicesByCustomer			is Boolean	 
				PrmPrintInvoicesPerCustomer				is Boolean
				PrmIDMPrinterSetting					is an IDMPrinterSettings

			Local Fields
				LocalActor								is an Actor
				LocalExecute							is Boolean
				LocalGenerate							is Boolean
				AsyncId									is an AsyncActionRequest	
				LocalBypassEmail 						is Boolean	
				InvoiceAndRegisterIDMJobView 			is an IDMJob view	

			
			Rule Blocks
				
				InvoiceRegisterXMLDefinitionAndRequest
					initialize IDMGenerateDocument
	
					IDMGenerateDocument.IDMXMLDefinition.Busclass		= reference to this instance
					IDMGenerateDocument.IDMXMLDefinition.ListName		= "BillingCompanyDetailsForIDM"
					IDMGenerateDocument.IDMXMLDefinition.DocumentName	= "InvoiceRegister"
					
    				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName	 = "BillingProcessLevelRel"
	    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName   	 = "BillingProcessLevelForIDMList"
	    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1 = 1
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag		 = "Lines"
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag		 = "Line"
						
					if (LocalEnteredInvoicePrefix entered)
						IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName  = "PrintedBillingInvoiceRegisterForIDMRel"		
	    			else
	    				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName  = "EditedBillingInvoiceRegisterForIDMRel"
	    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName 	 = "BillingInvoiceListForIDM"
	    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1 = 1
	    			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection2 = 1
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag		 = "LineInvoice"
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag		 = "Invoice"							

					initialize IDMAttributes
					
					IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "BillingCompany"
					IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= Company
					IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "BillingCompanyName"
					IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= Name					
					IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "PrintDate"
					IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeDate  = current corporate date
					
					if (PrmSummarizeInvoicesByCustomer)
						IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "Customer"
						IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= LocalCustomerForIDM
						IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "CustomerName"
						IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= LocalCustomerForIDM.Name
						IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "BillingInvoiceOutputHeader"
						IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= PrmBillingInvoiceOutputHeader
						IDMGenerateDocument.FileName		 = DerivedFileNamePerCustomer
					else
						IDMGenerateDocument.FileName		 = DerivedFileName
						
					IDMGenerateDocument.IDMAttributes	 	 = IDMAttributes
					IDMGenerateDocument.TemplateUniqueId 	 = InvoiceRegisterTemplate.IDMUniqueId
					IDMGenerateDocument.DocumentType		 = "FSM_InvoiceRegister"
					IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
					
					invoke CreateFromGenerateDocument IDMJob
						assign result to InvoiceAndRegisterIDMJobView					
						invoked.Actor				= actor
						invoked.Description			= InvoiceRegisterMessage
						invoked.IDMGenerateDocument	= IDMGenerateDocument
				
				GeneratePrintedBillingInvoice
					LocalBackgroundGroup = "IDMBillingInvoice" + "_" + PrmBillingInvoiceOutputHeader + "_" + Company + "_" + LocalCustomerForIDM
					invoke GeneratePrintedBillingInvoiceRegisterIDM BillingInvoice in background group (LocalBackgroundGroup)
						assign async action request id to AsyncId
						invoked.BillingCompany		= Company
						invoked.ProcessLevel 		= PrmProcessLevel
						invoked.BillingCustomer     = LocalCustomerForIDM	
						invoked.InvoicePrefix   	= PrmInvoicePrefix
						invoked.FromInvoiceNumber	= PrmFromInvoiceNumber					         
						invoked.ToInvoiceNumber		= PrmToInvoiceNumber	 
						invoked.IDMPrinter			= PrmIDMPrinter
						invoked.Generate			= true
						invoked.BypassEmail			= LocalBypassEmail
						invoked.BillingInvoiceOutputHeader = PrmBillingInvoiceOutputHeader
						invoked.PrmBackgroundGroup = LocalBackgroundGroup
						invoked.PrmPrintInvoicesPerCustomer = PrmPrintInvoicesPerCustomer
						invoked.PrmIDMPrinterSetting = PrmIDMPrinterSetting
							
				GenerateEditedBillingInvoice
					LocalBackgroundGroup = "IDMBillingInvoice" + "_" + PrmBillingInvoiceOutputHeader + "_" + Company + "_" + LocalCustomerForIDM
					invoke GenerateEditedBillingInvoiceRegisterIDM BillingInvoice in background group (LocalBackgroundGroup)
						assign async action request id to AsyncId
						invoked.BillingCompany		 = Company
						invoked.ProcessLevel 		 = PrmProcessLevel
						invoked.BillingCustomer      = LocalCustomerForIDM	
						invoked.CutOffDate			 = LocalCutOffDate
						invoked.FromInvoiceNumber	 = PrmFromInvoiceNumber					         
						invoked.ToInvoiceNumber		 = PrmToInvoiceNumber	
						invoked.InvoiceIsCOD		 = LocalInvoiceIsCOD
						invoked.PrintInvoicesOnly    = LocalPrintInvoicesOnly
						invoked.PrintCreditMemosOnly = LocalPrintCreditMemosOnly
						invoked.IDMPrinter			 = PrmIDMPrinter
						invoked.Generate			 = true
						invoked.BypassEmail			 = LocalBypassEmail
						invoked.PrmBackgroundGroup = LocalBackgroundGroup
						invoked.BillingInvoiceOutputHeader = PrmBillingInvoiceOutputHeader	
						invoked.PrmPrintInvoicesPerCustomer = PrmPrintInvoicesPerCustomer	
						invoked.PrmIDMPrinterSetting = PrmIDMPrinterSetting
												
				EmailInvoiceAndRegisterPerCustomer
					invoke EmailInvoiceAndRegister in background
						run after background group (LocalBackgroundGroup)
						invoked.PrmInvoiceAndRegisterMDSID = InvoiceAndRegisterIDMJobView.IDMJob.MDSID
						invoked.PrmCustomer				   = LocalCustomerForIDM
						invoked.PrmIDMGenerateDocument	   = IDMGenerateDocument	
								
			Action Rules
				LocalActor 							= actor
				LocalAddSubtract					= PrmAddSubtract
				LocalCutOffDate						= PrmInvoiceDate
				LocalPrintOption					= PrmPrintOption
				LocalIDMDocumentType				= 2 
				

				TransientFromInvoiceDate			= "19000101"
				TransientToInvoiceDate				= "21991231"
				TransientFrInvAddSub				= "S" 
				initialize TransientFrInvDays
				TransientToInvAddSub				= "S"
				initialize TransientToInvDays
				TransientFromGLDate					= "19000101"
				TransientToGLDate					= "21991231"
				TransientFrGlAddSub					= "S"
				initialize TransientFrGlDays
				TransientToGlAddSub					= "S"
				initialize TransientToGlDays
				TransientProcessLevel				= PrmProcessLevel
				initialize TransientCustomer 
				initialize LocalBypassEmail
				
				if (IsProcessLevelInvoicing)
					LocalProcessLevelForIDM			= PrmProcessLevel
				
				if (PrmInvoiceDate not entered) 
					if (PrmCutOffNumberOfDays = 0)
						LocalCutOffDate	= current corporate date
					else
						if (PrmAddSubtract.Add)
							LocalCutOffDate	= current corporate date + PrmCutOffNumberOfDays
						else
							LocalCutOffDate = current corporate date - PrmCutOffNumberOfDays
				
				if (PrmInvoicePrefix entered)
					LocalFromInvNumber					= PrmFromInvoiceNumber
					LocalToInvNumber					= PrmToInvoiceNumber
					LocalEnteredInvoicePrefix			= PrmInvoicePrefix

				if (PrmPrintOption.CreditMemosOnly)
					LocalPrintCreditMemosOnly           = true
				else
				if (PrmPrintOption.InvoicesOnly)
					LocalPrintInvoicesOnly				= true 
				else
				if (PrmPrintOption.CODInvoicesOnly)
					LocalInvoiceIsCOD 					= true

				LocalSummarizedInvoicesByCustomer = PrmSummarizeInvoicesByCustomer	
							
				if (LocalEnteredInvoicePrefix entered)
					if (PrintedBillingInvoiceRegisterForIDMRel not exists)
						send notification
							to LocalActor
							description is "InvoiceAndRegisterPrintIDMDocumentGenerationFailed."
							priority is high
							detail is "NoDataToReprint.PleaseCheckTheEnteredInvoicePrefixAndInvoiceRange[<LocalEnteredInvoicePrefix><LocalFromInvNumber>-<LocalToInvNumber>]AndRe-runTheBatchProgram."
					else
						LocalGenerate = true
				
				else
					if (EditedBillingInvoiceRegisterForIDMRel not exists)
						send notification
							to LocalActor
							description is "InvoiceAndRegisterPrintIDMDocumentGenerationFailed."
							priority is high
							detail is "NoDataToPrint.PleaseCheckTheEnteredCompany,CutoffDate,OrPrintOptionAndRe-runTheBatchProgram."
					else
						LocalGenerate = true

				if (LocalGenerate)
					if (PrmSummarizeInvoicesByCustomer) 
						TransientSummarizeInvoicesByCustomer = "Yes"
						LocalBypassEmail = true 
					
					if (PrmPrintInvoicesPerCustomer
					or  PrmSummarizeInvoicesByCustomer)
						if (LocalEnteredInvoicePrefix entered)
							for each distinct Customer in PrintedBillingInvoiceRegisterForIDMRel
								LocalCustomerForIDM = each.Customer	
								TransientCustomer   = each.Customer	
								
								include InvoiceRegisterXMLDefinitionAndRequest
								include GeneratePrintedBillingInvoice

								if (CompanyCustomerRel.EmailBillingInvoice
								and InvoiceAndRegisterIDMJobView.IDMJob.MDSID entered
								and PrmSummarizeInvoicesByCustomer)
									include EmailInvoiceAndRegisterPerCustomer								
						else
							for each distinct Customer in EditedBillingInvoiceRegisterForIDMRel
								LocalCustomerForIDM = each.Customer
								TransientCustomer   = each.Customer	
								
								include InvoiceRegisterXMLDefinitionAndRequest
								include GenerateEditedBillingInvoice

								if (CompanyCustomerRel.EmailBillingInvoice
								and InvoiceAndRegisterIDMJobView.IDMJob.MDSID entered
								and PrmSummarizeInvoicesByCustomer)
									include EmailInvoiceAndRegisterPerCustomer

					else 
						TransientSummarizeInvoicesByCustomer = "No"
						if (LocalEnteredInvoicePrefix entered)
							include InvoiceRegisterXMLDefinitionAndRequest
							include GeneratePrintedBillingInvoice
						else
							include InvoiceRegisterXMLDefinitionAndRequest
							include GenerateEditedBillingInvoice							

		CreateOrderEntryDailyShipmentJournal is an Instance Action
			restricted
			Parameters
				PrmInventoryLocation		is an InventoryLocation
				PrmProcessLevel				is a BillingProcessLevel
				PrmShipmentType				is Numeric 1
					States
						AllShipments	value is 1
						CODShipments	value is 2
				PrmShipDateCutoff			is Date
				PrmReleaseOption			is AlphaUpper 1
					States
						ReleaseBatchToInvoicePrint		value is "R"
						ReleaseAndBypassInvoicePrint	value is "B"
						DoNotRelease					value is "N"
				PrmBatchNumber				is Numeric 6
				PrmIDMPrinter				is an IDMPrinter
				PrmInvoiceDate				is Date
						
			Parameter Rules
				PrmShipmentType
					initial value is 1
					default to 1
					
				PrmReleaseOption
					initial value is "R"
					default to "R"
					
				PrmShipDateCutoff
					default to current date
					
			Action Rules
				LocalInventoryLocation 	= PrmInventoryLocation
										
				if (InventoryLocationForOrderEntryDailyShipmentJournalIDMRel exists)
					invoke GenerateOrderEntryDailyShipmentJournalInIDM InventoryLocationForOrderEntryDailyShipmentJournalIDMRel
						invoked.PrmProcessLevel 	= PrmProcessLevel
						invoked.PrmShipmentType 	= PrmShipmentType
						invoked.PrmShipDateCutoff 	= PrmShipDateCutoff
						invoked.PrmReleaseOption 	= PrmReleaseOption
						invoked.PrmBatchNumber		= PrmBatchNumber
						invoked.PrmIDMPrinter		= PrmIDMPrinter
						invoked.PrmInvoiceDate		= PrmInvoiceDate


		EmailInvoiceAndRegister is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmInvoiceAndRegisterMDSID 		is an IDMPID
				PrmCustomer						is a Customer
				PrmIDMGenerateDocument			is a IDMGenerateDocument
			
			Local Fields 
				LocalRecepientEmailAddress		is like MultipleEmailAddress 
					holds pii

			Action Rules
				initialize LocalCustomerForIDM
				initialize LocalRecepientEmailAddress
				LocalCustomerForIDM	= PrmCustomer
				IDMGenerateDocument = PrmIDMGenerateDocument

				if (not CompanyCustomerRel.BillingInvoiceEmailOption.ContactsOnly)
					LocalRecepientEmailAddress += CompanyCustomerRel.EmailAddress

				for each CompanyCustomerRel.CompanyCustomerContactRel
					if (each.EmailBillingInvoice
					and each.EmailAddress entered)
						if (LocalRecepientEmailAddress entered)
							LocalRecepientEmailAddress += ","
						LocalRecepientEmailAddress += each.EmailAddress

				initialize IDMItem
				IDMItem.DocumentType		 = "FSM_InvoiceRegister"
				IDMItem.IDMXMLDefinition	 = IDMGenerateDocument.IDMXMLDefinition
				IDMItem.IDMUniqueId			 = PrmInvoiceAndRegisterMDSID
				IDMItem.IDMEmail.From		 = CompanyCustomerRel.BillingInvoiceFromAndReplyToEmail
				TransientEmailFrom			 = CompanyCustomerRel.BillingInvoiceFromAndReplyToEmail
				IDMItem.IDMEmail.To			 = LocalRecepientEmailAddress
				IDMItem.IncludeRelDoc		 = true


				if (InvoiceAndRegisterEmailSubject entered)
					InvoiceAndRegisterIDMEmailSubjectPerCustomer = InvoiceAndRegisterEmailSubject
					IDMItem.IDMEmail.Subject = InvoiceAndRegisterIDMEmailSubjectPerCustomer text
				else
					IDMItem.IDMEmail.Subject = DefaultInvoiceRegisterEmailSubjectPerCustomer
											   

				if (InvoiceAndRegisterEmailTemplate entered)				
					IDMItem.EmailTemplateUniqueID = InvoiceAndRegisterEmailTemplate.IDMUniqueId
				else
					IDMItem.IDMEmail.Body 	 = blank

				constraint (IDMItem.GetItemDetails)
					"DocumentDoesNotExistInIDM"

				IDMItem.IDMPID    = IDMItem.IDMItemDetails.PID
				
				invoke SendToEmail IDMJob
					invoked.Description	 = InvoiceAndRegisterEmailNotificationMsg
					invoked.FileName	 = DerivedFileNamePerCustomer
					invoked.IDMItem		 = IDMItem


