InterfaceAnalysis is a BusinessClass
    owned by ap
    prefix is PANY

    Ontology
        symbolic key is InterfaceAnalysis

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
		RunTime				is TimeStamp
		Status				is Numeric size 1
			States
				Created				value is 1
				Processing 			value is 2
				Completed 			value is 3

	Field Rules
        RunTime
            initial value is current timestamp
            force default to current timestamp
		
	Local Fields
		InterfaceAnalysisGroup		is AlphaUpper up to 200
		LastBackgroundGroup			is AlphaUpper up to 200
	
	Derived Fields
			
	Relations

		EDIInvoiceMatchInterfaceInputRel
			one-to-many relation to EDIInvoiceMatchInterfaceInput
			Field Mapping uses symbolic key
		
		MatchInvoiceImportRel
			one-to-many relation to MatchInvoiceImport
			Field Mapping uses symbolic key
		
		DocumentInterfaceInvoiceRel
			one-to-many relation to DocumentInterfaceInvoice
			Field Mapping uses symbolic key
		
		PayablesInvoiceImportRel
			one-to-many relation to PayablesInvoiceImport
			Field Mapping uses symbolic key

	Conditions
		IsCompleted
			when (Status.Completed)

	Sets
	
	Actions 
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted
		
		SetStatusCompleted is an Instance Action
			restricted
			Action Rules
				Status = Status.Completed
		
		Purge is a Purge Action
			restricted
		
		PurgeRecords is a Set Action
			confirmation required
				"AllCompletedRecordsUpToProvidedRunTimeWillBePurged.Continue?"
			disable checkpoint
			run in background
			Parameters
				PrmRunTime	is TimeStamp
					default label is "RunTime"
			Parameter Rules
				PrmRunTime
					required
			Instance Selection
				where (RunTime <= PrmRunTime
				and    Status.Completed)
			Action Rules
				Instance Rules
					invoke Purge

		CreateInterfaceAnalysis is a Set Action
			completion message is "CreateInterfaceAnalysisSubmitted"
			no records message is "CreateInterfaceAnalysisSubmitted"
			Instance Selection			
				where (RunTime not entered)

			Action Rules
				Empty Set Rules
					invoke CreateAllRecords InterfaceAnalysis
		
		CreateAllRecords is a Create Action
			restricted
			Action Rules
				Status = Status.Processing
			Exit Rules
				InterfaceAnalysisGroup		 		= "InterfaceAnalysis" + RunTime
				LastBackgroundGroup					= InterfaceAnalysisGroup

				invoke UpdateInterfaceAnalysisDetails EDIInvoiceMatchInterfaceInput in background group(InterfaceAnalysisGroup)
					invoked.PrmInterfaceAnalysis	= InterfaceAnalysis
				
				invoke UpdateInterfaceAnalysisDetails MatchInvoiceImport in background group(InterfaceAnalysisGroup)
					invoked.PrmInterfaceAnalysis	= InterfaceAnalysis
				
				invoke UpdateInterfaceAnalysisDetails PayablesInvoiceImport in background group(InterfaceAnalysisGroup)
					invoked.PrmInterfaceAnalysis	= InterfaceAnalysis
				
				invoke UpdateInterfaceAnalysisDetails DocumentInterfaceInvoice in background group(InterfaceAnalysisGroup)
					invoked.PrmInterfaceAnalysis	= InterfaceAnalysis

				invoke UpdateInterfaceAnalysisDetails PayablesInvoice in background group(InterfaceAnalysisGroup)
					invoked.PrmInterfaceAnalysis	= InterfaceAnalysis

				invoke SetStatusCompleted in background
					run after background group (LastBackgroundGroup)
