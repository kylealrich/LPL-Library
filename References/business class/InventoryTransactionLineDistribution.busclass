InventoryTransactionLineDistribution is a BusinessClass
    owned by ic
    prefix is ITD
    sql name is ICDistribution

    Ontology
        symbolic key is InventoryTransactionLineDistribution

    Patterns
		implements ContextualParent
		implements InlineUserFields
			size is 500	        
        implements StaticJava
        disable AuditIndex
		implements Archivable

    Persistent Fields             
        PostingType                 	is AlphaUpper size 2
            States
                Inventory           value is "I1"
                Offset              value is "O1"
                RNIExpenseAccount   value is "E1"
                ReceivedNotInvoiced value is "RN"
                MatchedNotReceived  value is "MN"
                AddOnCost           value is "A"
                Absorption			value is "IA"
                ProductionCredit	value is "IP"
                Waste				value is "CW"
                MaterialExpense		value is "IM"
                Burden				value is "BR"
        Status                      	is Numeric size 1
            States
                Unposted            value is 0
                Posted              value is 1
                NotToBePosted       value is 2
        DistributionDate 				is a PostingDate
		AddOnCharge
		BurdenCode
        		
	Context Fields
		WorkOrderRange
		DistributionDateRange			is a DateRange
		GeneralLedgerCompanyGroup

			
	Transient Fields

		BypassActiveCodeBlockEdit
		GLFinanceCodeBlock		 		is a TransactionCodeBlock				 		
			derive value from GLTransactionDetailRel.FinanceCodeBlock 
		GLTransactionAmount		 		is a CurrencyAmount			
			derive value from GLTransactionDetailRel.TransactionAmount 				 
        GLBaseAmount					is a FinanceCurrencyAmount
        	derive value from GLTransactionDetailRel.ReportCurrencyAmount
       	PostingDate                     is Date
       		derive value from GLTransactionDetailRel.PostingDate
		System							is a GeneralLedgerSystemCode
			derive value from GLTransactionDetailRel.System
       	CurrencyCode					is a FromCurrency
       		derive value from GLTransactionDetailRel.CurrencyCode    		
		UnitsAmount
			derive value from GLTransactionDetailRel.UnitsAmount
		TransactionDate					is an ExchangeDate
			derive value from GLTransactionDetailRel.TransactionDate
       	JournalizeGroup
       		derive value from GLTransactionDetailRel.JournalizeGroup
		Reference 						
			derive value from GLTransactionDetailRel.Reference
		GeneralLedgerEvent 			
			derive value from GLTransactionDetailRel.GeneralLedgerEvent
		DocumentNumber
			derive value from GLTransactionDetailRel.DocumentNumber




		BypassUnitAndAmountEdit
		
	Local Fields
		LocalEnterpriseGroup			is like FinanceEnterpriseGroup
		AccountingEntity

	Derived Fields
		DerivedCompany					is a DerivedField
			type is like InventoryCompany

			if (PostingType.Inventory)
				return Company
			else
				if (PostingType.Offset
				and InventoryTransaction.IsInventoryIssuesOrInventoryTransfer) 
					return InventoryTransaction.FromToCompanyLocation.FromToCompany
				else
					if (PostingType.Offset)
						return Company  
				
		DerivedLocation					is a DerivedField
			type is like InventoryLocation
		
			if (PostingType.Inventory)
				return InventoryLocation 
			else
				if (PostingType.Offset)
					if (InventoryTransaction.IsInventoryIssues)
						return InventoryTransaction.FromToCompanyLocation.RequestingLocation	
					else
					if (InventoryTransaction.IsDirectOrInventoryTransfer)
						return InventoryTransaction.FromToCompanyLocation.FromToLocation
					else
						return InventoryLocation

		DerivedCompanyName					is a DerivedField 
			type is like Name
			holds pii

			if (PostingType.Inventory)
				return Company.Name
			else
				if (PostingType.Offset
				and InventoryTransaction.IsInventoryIssuesOrInventoryTransfer) 
					return InventoryTransaction.FromToCompanyLocation.FromToCompany.Name
				else
					if (PostingType.Offset)
						return Company.Name 
				
		DerivedLocationName					is a DerivedField 
			type is like Name
			holds pii
		
			if (PostingType.Inventory)
				return InventoryLocation.Name 
			else
				if (PostingType.Offset)
					if (InventoryTransaction.IsInventoryIssues)
						return InventoryTransaction.FromToCompanyLocation.RequestingLocation.Name	
					else
					if (InventoryTransaction.IsDirectOrInventoryTransfer)
						return InventoryTransaction.FromToCompanyLocation.FromToLocation.Name
					else
						return InventoryLocation.Name


		DerivedRequestingLocation	is a DerivedField
			type is like RequestingLocation
			restricted
			return InventoryTransactionLine.DerivedRequestingLocation
		DerivedPurchaseOrder	is a DerivedField
			type is like PurchaseOrder
			restricted
			return InventoryTransactionLine.DerivedPurchaseOrder
		DerivedVendor	is a DerivedField
			type is like Vendor
			restricted
			return InventoryTransactionLine.DerivedVendor
		DerivedBuyer	is a DerivedField
			type is like Buyer
			restricted
			return InventoryTransactionLine.DerivedBuyer
		DerivedDocumentType is a DerivedField
			type is like InventoryDocumentType
			default label is "DocumentType"
			return InventoryTransaction.InventoryDocumentType

		DerivedTransaction is a DerivedField
			type is like InventoryTransaction
			default label is "Transaction"
			return InventoryTransaction

		DerivedItem is a DerivedField
			type is like Item
			default label is "Item"
			return InventoryTransactionLine.Item

		DerivedItemDescription is a DerivedField
			type is like Description
			default label is "ItemDescription"
			return InventoryTransactionLine.Item.Description

		DerivedQuantity is a DerivedField
			type is like Quantity
			default label is "Quantity"
			return InventoryTransactionLine.Quantity

		DerivedUOM is a DerivedField
			type is like UnitOfMeasure
			default label is "UOM"
			return InventoryTransactionLine.TransactionUOM

		DerivedUnitCost is a DerivedField
			type is like UnitCost
			default label is "UnitCost"
			return InventoryTransactionLine.UnitCost

		DerivedTotalAmount is a DerivedField
			type is like InternationalAmount
			default label is "Amount"
			return InventoryTransactionLine.DerivedTransactionAmount

		DerivedExtendedAmount is a DerivedField
			type is like InternationalAmount
			default label is "ExtendedAmount"
			return InventoryTransactionLine.DerivedLineExtendedAmount

		DerivedLineUnitCost is a DerivedField		
			type is like InternationalCost
			return InventoryTransactionLine.DerivedUnitCost		

		DerivedReceivedQuantity is a DerivedField
			type is like Quantity
			default label is "ReceivedQuantity"
			return InventoryTransactionLine.DerivedReceivedQuantity

		OriginatingTransactionDocument		is a DerivedField
			type is Numeric 10
			default label is "OriginatingDocument"
			return InventoryTransactionLine.OriginatingTransactionDocument

		OriginatingTransactionLine			is a DerivedField
			type is Numeric 6
			default label is "OriginatingLine"
			return InventoryTransactionLine.OriginatingTransactionLine

		DerivedRequisition					is a DerivedField
			type is like Requisition
			restricted
			return InventoryTransactionLine.DerivedRequisition
			
		DerivedRequisitionLine				is a DerivedField
			type is like RequisitionLine
			restricted
			return InventoryTransactionLine.DerivedRequisitionLine
			
		DerivedRequester					is a DerivedField
			type is like Requester
			restricted
			return InventoryTransactionLine.DerivedRequester
			



		MobileGLTransactionAmount is a DerivedField
			type is Alpha 20
			default label is "TransactionAmount"
			return GLTransactionAmount + " " + CurrencyCode



	Conditions
		
		WithinDistributionDateRange
			restricted
			when (DistributionDateRange not entered
			or	 (DistributionDateRange entered
			and	  DistributionDate within DistributionDateRange))
			
		WithinCompanyGroup
			restricted
			when (GeneralLedgerCompanyGroup not entered
			or    GLCompanyGroupMemberRel exists)
			
		IsFromRequisition
			restricted
			when (InventoryTransactionLineForDrillBackRel.IsRequisition)
			
		IsRequisitionReturn
			restricted
			when (InventoryTransactionLineForDrillBackRel.IsRequisitionReturn)
			
		IsOffsetPosting
			restricted
			when (PostingType.Offset)
			
		UpdateCommitments
			when (IsFromRequisition
			and   !IsRequisitionReturn
			and   IsOffsetPosting)

		FromWorkOrder
			when (TransactionSystemCode.WorkOrder)
			
		WithinWorkOrderRange 
			restricted
			when (WorkOrderRange not entered
			or	GLTransactionDetailRel.DocumentNumber within WorkOrderRange)
		
		WithinPostingDateRange
			restricted
			when (DistributionDateRange not entered
			or	 (DistributionDateRange entered
			and	  PostingDate within DistributionDateRange))

		SkipInventoryTransactionPurging
			restricted
			when (GLTransactionDetailRel.FinanceCodeBlock.Project entered
			and !GLTransactionDetailRel.FinanceCodeBlock.Project.ProjectStatus.AllowPurge)

	Relations    

		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 
				related.OriginatingTransaction = reference to this instance   

      	GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup                  = LocalEnterpriseGroup	
				related.GeneralLedgerSystemCode                 = "IC"

		InventoryTransactionLineForDrillBackRel
			one-to-one relation to InventoryTransactionLine
			Field Mapping uses symbolic key
				related.Company					                = Company
				related.InventoryLocation	                    = InventoryLocation
				related.InventoryTransaction                    = InventoryTransaction
				related.TransactionSystemCode                   = TransactionSystemCode
				related.InventoryTransactionLine 	 		    = InventoryTransactionLine
				
		RQGeneralLedgerSystemCodeRel 
			one-to-many relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode	= "RQ"
			
		GLCompanyGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup				= GeneralLedgerCompanyGroup
				related.Company									= Company
						
	Rule Blocks
									
		CreateGLTransactionDetail
			if (GLTransactionAmount = 0)
				BypassUnitAndAmountEdit 						= true			
			invoke Released.Create GLTransactionDetail          
				fill in fields from this instance		 
				invoked.OriginatingTransaction 					= reference to this instance
				invoked.FinanceEnterpriseGroup					= Company.FinanceEnterpriseGroup
				invoked.AccountingEntity						= Company.AccountingEntity
				invoked.CurrencyCode							= CurrencyCode
				invoked.System									= TransactionSystemCode //"IC"	
				invoked.JournalizeGroup							= JournalizeGroup				  
				invoked.FinanceCodeBlock						= GLFinanceCodeBlock		
				invoked.ReportCurrencyAmount 					= GLBaseAmount 
				invoked.TransactionAmount						= GLTransactionAmount
				invoked.Reference								= "InventoryTransactionLineDistribution"
				invoked.Description			  					= InventoryTransactionLine.Item.Description
				invoked.AutoReverse 							= false
					
	Actions
		Create is a Create Action
			restricted
			Action Rules	


				DistributionDate = PostingDate
				AccountingEntity = Company.AccountingEntity			
				include CreateGLTransactionDetail
					
				
		Update is an Update Action
			restricted



		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
			bypass relational integrity rules
			restricted
			Entrance Rules
				invoke PurgeValidSubLedgerTrx GLTransactionDetailRel
		
		RoundingUpdate is an Instance Action
			restricted
			Parameters
				PrmRoundingDifference	is a CurrencyAmount
			
			Action Rules
				if (PrmRoundingDifference entered)
					invoke Released.Update GLTransactionDetailRel
						invoked.TransactionAmount = GLTransactionAmount - PrmRoundingDifference
					initialize GLBaseAmount
					initialize GLTransactionAmount
			

		JournalizeDistributionsFromBatch is a Set Action		

			restricted
			completion message is "<CompletionMessage>"
			
			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"			
				PrmCompany				is a Company
					default label is "Company"

			Parameter Rules
				PrmEnterpriseGroup
					required
				PrmCompany
					required


			Local Fields
				LocalJournalizeGroup is a JournalizeGroup
				CompletionMessage	 is Alpha 150
				RecordCount			 is Numeric 10

			Instance Selection
				where  (Company				= PrmCompany
				and    Status.Unposted
				and    InventoryTransactionLineDistribution.TransactionSystemCode="WO")        

			Sort Order  
				Company
				Status


			Action Rules								
				Empty Set Rules
					CompletionMessage = "TransactionLineDistributionPostingCompleteForJournalizeGroup...:NoRecordsFoundToJournalize<LocalJournalizeGroup>"


				Set Rules
					Entrance Rules
						LocalEnterpriseGroup 			= PrmEnterpriseGroup
			            increment GeneralLedgerSystemCodeRel.LastJournalizeGroup
				        LocalJournalizeGroup			= GeneralLedgerSystemCodeRel.DerivedJournalizeGroup

						initialize RecordCount
					
					Exit Rules
						CompletionMessage = "TransactionLineDistributionPostingCompleteForJournalizeGroup...:<LocalJournalizeGroup>;<RecordCount>...RecordsProcessed"
				
				Company Set Rules

					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmEnterpriseGroup in background
							invoked.PrmJournalizeGroup	= LocalJournalizeGroup		

						CompletionMessage = "TransactionLineDistributionPostingCompleteForJournalizeGroup...:<LocalJournalizeGroup>;<RecordCount>...RecordsProcessed"	

				Instance Rules
					increment RecordCount
					if (GLTransactionDetailRel exist)
						invoke UpdateJournalizeGroup GLTransactionDetailRel	 
							invoked.PrmJournalizeGroup 	= LocalJournalizeGroup  
						Status = Status.Posted
						
					if (RQGeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit
					or  RQGeneralLedgerSystemCodeRel.EncumbranceOption.Track) 
						if (UpdateCommitments)
							invoke UpdateRequisitionCommitment InventoryTransactionLineForDrillBackRel

												

		JournalizeDistributions is a Set Action		

			completion message is "<CompletionMessage>"
			
			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"			
				PrmCompany				is a Company
					default label is "Company"
				PrmGeneralLedgerCompanyGroup is a GeneralLedgerCompanyGroup
					default label is "CompanyGroup"
				PrmPostThruDate			is Date           
					default label is "PostThruDate"
			Parameter Rules
				PrmEnterpriseGroup
					initial value is Company.FinanceEnterpriseGroup
					LocalEnterpriseGroup = Company.FinanceEnterpriseGroup
					required
				PrmCompany
					if (PrmGeneralLedgerCompanyGroup not entered)
						required
							"CompanyOrCompanyGroupIsRequired"
				PrmGeneralLedgerCompanyGroup
					if (PrmCompany entered)
						cannot be entered
							"CannotEnterCompanyGroupIfCompanyIsEntered"
				PrmPostThruDate
					required
				 
			Local Fields
				LocalJournalizeGroup is a JournalizeGroup
				CompletionMessage	 is Alpha 150
				RecordCount			 is Numeric 10

			Instance Selection
				where  ((PrmCompany not entered or Company = PrmCompany)
				and     (PrmGeneralLedgerCompanyGroup not entered or GLCompanyGroupMemberRel exists)
				and      DistributionDate     <= PrmPostThruDate
				and      Status.Unposted)        

			Sort Order  
				Company
				Status
				DistributionDate


			Action Rules								
				Empty Set Rules


					CompletionMessage = "TransactionLineDistributionPostingCompleteForJournalizeGroup...:NoRecordsFoundToJournalize<LocalJournalizeGroup>"


				Set Rules
					Entrance Rules
						LocalEnterpriseGroup 			= PrmEnterpriseGroup
			            increment GeneralLedgerSystemCodeRel.LastJournalizeGroup
				        LocalJournalizeGroup			= GeneralLedgerSystemCodeRel.DerivedJournalizeGroup

						initialize RecordCount
				
					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmEnterpriseGroup in background
							invoked.PrmJournalizeGroup	= LocalJournalizeGroup		

						CompletionMessage = "TransactionLineDistributionPostingCompleteForJournalizeGroup...:<LocalJournalizeGroup>;<RecordCount>...RecordsProcessed"

				Instance Rules
					increment RecordCount
					if (GLTransactionDetailRel exist)
						invoke UpdateJournalizeGroup GLTransactionDetailRel	 
							invoked.PrmJournalizeGroup 	= LocalJournalizeGroup  
						Status = Status.Posted
					if (RQGeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit
					or  RQGeneralLedgerSystemCodeRel.EncumbranceOption.Track) 
						if (UpdateCommitments)
							invoke UpdateRequisitionCommitment InventoryTransactionLineForDrillBackRel




		
		InventoryTransactionLineDistributionPurge is a Set Action
			restricted
			Parameters
				PrmCompany									is an InventoryCompany
					default label is "Company"
				PrmInventoryTransaction						is like InventoryTransaction
					default label is "InventoryTransaction"
				PrmInventoryLocation						is like InventoryLocation
					default label is "InventoryLocation"
				PrmInventoryTransactionLine					is like InventoryTransactionLine
					default label is "InventoryTransactionLine"
				PrmTransactionSystemCode					is like TransactionSystemCode
					default label is "TransactionSystemCode"
				PrmLineNumber								is like LineNumber
					default label is "LineNumber"
				
			Instance Selection
				include deleted records
				where(Company												= PrmCompany
				and   InventoryTransaction									= PrmInventoryTransaction
				and	  InventoryLocation										= PrmInventoryLocation
				and   InventoryTransactionLine								= PrmInventoryTransactionLine
				and	  TransactionSystemCode									= PrmTransactionSystemCode
				and	  InventoryTransactionLine.LineNumber					= PrmLineNumber)
			Action Rules
				Instance Rules
					invoke Purge

					
