ReceivableProcessLevelReportGroup is a BusinessClass
	owned by ar
	prefix is PLG
	sql name is RProcessLevelReportGroup
	classic name is PROCLVLGRP

	Ontology
		symbolic key is ReceivableProcessLevelReportGroup
			classic set name is PLGSET1
			classic name for ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader is PL-GROUP

	Patterns
		implements StaticJava
		disable AuditIndex
	 
	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

	Relations
		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		PlGroupDetlRel
			one-to-many relation to ReceivableProcessLevelReportDetail
			required
			Field Mapping uses symbolic key
				related.Company								 = Company
				related.ReceivableProcessLevelReportHeader	 = ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader

		ProcessLevelGroupRel
			one-to-one relation to ReceivableProcessLevelReportGroup
			Field Mapping uses Set2
				related.Company		= Company
				related.ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader = ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader

		ProcessLevelGroup2Rel
			one-to-many relation to ReceivableProcessLevelReportGroup
			Field Mapping uses symbolic key
				related.Company		= Company
			Instance Selection
				where (related.ReceivableProcessLevelReportGroup.SeqNbr = ReceivableProcessLevelReportGroup.SeqNbr)
		

		ProcessLevelGroupHeaderRel
			one-to-one relation to ReceivableProcessLevelReportHeader
			Field Mapping uses symbolic key
				related.Company		= Company
				related.ReceivableProcessLevelReportHeader	= ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader

		ProcessLevelGroupDetailRel
			one-to-many relation to ReceivableProcessLevelReportDetail
			Field Mapping uses symbolic key
				related.Company		= Company
				related.ReceivableProcessLevelReportHeader	= ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader

	Sets

		Set2
			indexed
			Sort Order
				Company
				ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader


	Actions
		Create is a Create Action
			Action Rules
				constraint (ReceivableProcessLevelReportGroup.RptGroup entered)
					"ReportGroupRequired"
				constraint (ReceivableProcessLevelReportGroup.SeqNbr entered)
					"SequenceNumberRequired"
				constraint (ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader entered)
					"ProcessLevelGroupRequired"
				constraint (!ProcessLevelGroup2Rel exists)
					"SequenceNumberAlreadyExists"
				constraint (ProcessLevelGroupHeaderRel exists)
					"ProcessLevelGroup<ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader>DoesNotExist"	
				constraint (!Company.CompanySetUpIsIncomplete)
					"CompanyIsIncomplete"		
				constraint (ProcessLevelGroupDetailRel exists)
					"ProcessLevelGroup<ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader>DoesNotContainAnyProcessLevels;CannotAdd"	
				constraint (!ProcessLevelGroupRel exists)
					"ProcessLevelGroup<ReceivableProcessLevelReportGroup.ReceivableProcessLevelReportHeader>IsAlreadyAssignedToReportGroup<ProcessLevelGroupRel.ReceivableProcessLevelReportGroup.RptGroup>" 

		Delete is a Delete Action


