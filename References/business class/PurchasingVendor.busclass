PurchasingVendor is a BusinessClass
    owned by po
    prefix is POV
    classic name is POVENDOR

    Ontology
        symbolic key is PurchasingVendor
            classic set name is POVSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Buyer
            classic name is BUYER-CODE
        OpenPurchaseLimit                           is an InternationalAmount
            classic name is OPEN-PO-LIMIT
        OpenPurchaseAmount                          is an InternationalAmount
            classic name is OPEN-PO-AMT
            disable Auditing
        LeadtimeDays
        IssueMethod                                 is a PurchaseOrderIssueMethod
            classic name is ISSUE-METH
        RevisedIssueMethod                          is a PurchaseOrderIssueMethod
            classic name is REV-ISSUE-METH
        EDINumber
        FaxNumber
            classic name for FaxNumber.InternationalPrefix is FAX-PREFIX
            classic name for FaxNumber.SubscriberNumber is FAX-NUMBER
            classic name for FaxNumber.Extension is FAX-EXT
        MaximumOrderAmount                          is an InternationalAmount
            classic name is MAX-ORDER-AMT
        FreightTerm
            classic name is FREIGHT-TERMS
        ShipVia
        ShipTerm
            classic name is FOB-CODE
        NormalDeliveryDay
            classic name is NORM-DEL-DAY
        OvershipmentTolerancePercentage
            sql name is OTolerancePercentage
            classic name is OSHIP-TOL-PCT
        UndershipmentTolerancePercentage
            sql name is UTolerancePercentage
            classic name is USHIP-TOL-PCT
        OvershipmentToleranceOption
            classic name is ZERO-OSHIP-FL
        UndershipmentToleranceOption
            classic name is ZERO-USHIP-FL
        CancelAllBackorder                          is Boolean
            classic name is CANCEL-ALL-BO
            restricted 
        ReturnMaterialAuthorizationRequired         is Boolean
            sql name is RMaterialAuthorizationRequired
            classic name is RMA-REQUIRED
        ReturnMaterialAuthorizationDocumentRequired is Boolean
            sql name is RMADocumentRequired
            classic name is RMA-DOC-REQ
        VendorReturnShippingMethod                  is a ShipVia
            classic name is OVEND-SHIP-VIA
        LocationControl
            classic name is REQ-LOC-LEVEL
        MixContractsOnPurchaseOrder                 is Boolean
            classic name is REQ-CONT-LEVEL
        RevisedEDINumber                            is an EDINumber
            classic name is REV-EDI-NUMBER
        RevisedEDITransaction                       is AlphaUpper size 1
            classic name is REV-EDI-TRANS
            States
                NotApplicable     value is blank
                POTransaction     value is "P"
                ChangeTransaction value is "C"
        RevisionsInclude                            is AlphaUpper size 1
            classic name is REVISIONS-INCL
            States
                RevisionsOnly     value is "O"
                RevisionsComplete value is "C"
                Reissue           value is "I"
                NotApplicable     value is "N"
        ContactName                                 is a VendorName 
        	holds pii
            classic name is VENDOR-CONTCT
        PhoneNumber                                 is a TelephoneNumber 
        	holds pii
            classic name for PhoneNumber.InternationalPrefix is PHONE-PREFIX
            classic name for PhoneNumber.SubscriberNumber is PHONE-NUM
            classic name for PhoneNumber.Extension is PHONE-EXT
        POCode
        MinimumOrderWeight
            classic name is MIN-ORD-WEIGHT
        UpdatePurchaseOrderCost                     is Numeric size 1
            default label is "OverrideVendorDeterminedCost"
            classic name is UPD-PO-COST
            States
                DoNotUpdatePOCostFromReceipt				value is 1
                UpdatePOCostFromReceiptOrAcknowledgement	value is 2
        LoadingPort
        UnloadingPort
        AllowChangeToPunchoutPurchaseOrder          is Boolean
            sql name is AChangeToPunchoutPurchaseOrder
            classic name is PUNCHOUT-CHG-F
        EmailAddress 	 							is an EmailAddressMulti 
        	holds pii
        ReorderOnSunday                             is Boolean
            classic name is REORDER-SUN-FL
        ReorderOnMonday                             is Boolean
            classic name is REORDER-MON-FL
        ReorderOnTuesday                            is Boolean
            classic name is REORDER-TUE-FL
        ReorderOnWednesday                          is Boolean
            classic name is REORDER-WED-FL
        ReorderOnThursday                           is Boolean
            classic name is REORDER-THU-FL
        ReorderOnFriday                             is Boolean
            classic name is REORDER-FRI-FL
        ReorderOnSaturday                           is Boolean
            classic name is REORDER-SAT-FL
        LocationIDPrintOption                       is Numeric size 1
            classic name is LOC-ID-PRT
            States
                DoNotPrint					value is 0
                PrintHINOnPOLine			value is 1
                PrintGLNOnPOLine			value is 2
                PrintGLNOnPOHeaderOnly		value is 3
                PrintGLNOnPOHeaderAndLine	value is 4
        OverrideContractCost                       is Boolean
            classic name is OVER-AGRMT
        OverrideLastPOLastCost                      is Boolean
            classic name is OVER-LC-LP
        OneSourceDocumentToOnePO                    is Numeric size 1
            classic name is ONE-SRC-ONE-PO
            States
                No									value is 0
                OneRequisitionToOnePO				value is 1
                OneCustomerOrderToOnePO				value is 2
                RequisitionAndCustomerOrderToOnePO	value is 3
        MinimumPurchaseOrderAmount                  is an InternationalAmount
            classic name is MIN-PO-AMT
        GlobalLocationNumber
            classic name is GLN-NBR
        GTINInUse                                   is Boolean
            classic name is GTIN-USED
        UpdateItemFromGTIN                          is Boolean
            classic name is GTIN-ITEM-SYNC
        PrintVisitNumber                            is Boolean
            classic name is DSP-VISIT-NBR
        TransmitVisitNumber                         is Boolean
            classic name is EDI-VISIT-NBR
        MaskVisitNumber                             is Boolean
            classic name is MSK-VISIT-NBR
        PrintMedicalRecordNumber                    is Boolean
            classic name is DSP-PAT-ID
        TransmitMedicalRecordNumber                 is Boolean
            classic name is EDI-PAT-ID
        MaskMedicalRecordNumber                     is Boolean
            classic name is MSK-PAT-ID
        PrintCaseID                                 is Boolean
            classic name is DSP-CASE-NBR
        TransmitCaseID                              is Boolean
            classic name is EDI-CASE-NBR
        MaskCaseID                                  is Boolean
            classic name is MSK-CASE-NBR
        PrintProcedure                              is Boolean
            classic name is DSP-PROC
        TransmitProcedure                           is Boolean
            classic name is EDI-PROC
		PrintPatientProcedureModifier				is Boolean
			classic name is DSP-PROC-MOD
		TransmitPatientProcedureModifier			is Boolean
			classic name is EDI-PROC-MOD
        PrintProcedureDate                          is Boolean
            classic name is DSP-PROC-DT
        TransmitProcedureDate                       is Boolean
            classic name is EDI-PROC-DT
        PrintPractitioner                           is Boolean
            classic name is DSP-PHYS-NAME
        TransmitPractitioner                        is Boolean
            classic name is EDI-PHYS-NAME
        PrintNurseCoordinator                       is Boolean
            classic name is DSP-NRS-COORD
        TransmitNurseCoordinator                    is Boolean
            classic name is EDI-NRS-COORD
		PrintPatientName							is Boolean
			classic name is DSP-PAT-NAME
		TransmitPatientName							is Boolean
			classic name is EDI-PAT-NAME
        MaskPatientName                             is Boolean
		PrintPatientBirthdate						is Boolean
			classic name is DSP-BIRTHDATE
		TransmitPatientBirthdate					is Boolean
			classic name is EDI-BIRTHDATE
        MaskPatientBirthdate                        is Boolean
		PrintPatientGender							is Boolean
			classic name is DSP-GENDER
		TransmitPatientGender						is Boolean
			classic name is EDI-GENDER
        MaskPatientGender	                        is Boolean
		PrintPatientSocialSecurityNumber			is Boolean
			classic name is DSP-SSN
		TransmitPatientSocialSecurityNumber			is Boolean
			classic name is EDI-SSN
		MaskPatientSocialSecurityNumber				is Boolean
			classic name is MSK-SSN
        PrintSalesRepresentative                    is Boolean
            classic name is DSP-SALES-REP
        TransmitSalesRepresentative                 is Boolean
            classic name is EDI-SALES-REP
        PrintSupplierOrderNumber                    is Boolean
            classic name is DSP-ORD-NBR
        TransmitSupplierOrderNumber                 is Boolean
            classic name is EDI-ORD-NBR
        PrintRequester                              is Boolean
            classic name is DSP-REQUESTOR
        TransmitRequester                           is Boolean
            classic name is EDI-REQUESTOR
        PrintLotNumber                              is Boolean
            classic name is DSP-LOT
        TransmitLotNumber                           is Boolean
            classic name is EDI-LOT
        PrintSerialNumber                           is Boolean
            classic name is DSP-SER-NBR
        TransmitSerialNumber                        is Boolean
            classic name is EDI-SER-NBR
        PrintUserDefined1                           is Boolean
            classic name is DSP-USERDEF1
        TransmitUserDefined1                        is Boolean
            classic name is EDI-USERDEF1
        MaskUserDefined1 		                   	is Boolean
            classic name is MSK-USERDEF1
        PrintUserDefined2                          	is Boolean
            classic name is DSP-USERDEF2
        TransmitUserDefined2                        is Boolean
            classic name is EDI-USERDEF2
        MaskUserDefined2 		                   	is Boolean
            classic name is MSK-USERDEF2
        PrintUniqueDeviceId                        is Boolean
            classic name is UDI-FL
        TransmitUniqueDeviceId                     is Boolean
            classic name is EDI-UDI-FL
        PrintGTIN                                  is Boolean
            classic name is GTIN-FL
        TransmitGTIN                               is Boolean
            classic name is EDI-GTIN-FL
        PrintManufacturingDate                     is Boolean
            classic name is MANUF-DATE-FL
        TransmitManufacturingDate                  is Boolean
            classic name is EDI-MANUF-FL
        PrintExpirationDate                        is Boolean
            classic name is EXPIRE-DATE-FL
        TransmitExpirationDate                     is Boolean
            classic name is EDI-EXPIRE-FL

        AllowVendorItemUpdate                       is Boolean
			classic name is ALLOW-VI-UPDT
		OrderMinimumAmountEdit						is Numeric size 1
			classic name is ORDER-MIN-EDIT
			States
				PurchaseOrders							value is 0
				PurchaseOrdersAndRequisitions			value is 1
				
		AribaEnabled								is Numeric size 1
			States
				No									value is 0
				Integrated							value is 1
				Flipper								value is 2
				Light								value is 3
		SellerAribaNetworkId						is Alpha size 20
		NamedPlace									is Text
		UseIDMTemplate								is Boolean
		IDMTemplate
		PurchaseOrderDocument
        DeliveryToleranceOption				  is Numeric size 1
			States
				DefaultToNextLevel 		 value is 0
				UseDeliveryToleranceDays value is 1
				DisableDeliveryTolerance value is 2
		DeliveryToleranceDays				  is Numeric size 3

    Derived Fields

		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforVendor"

		ContextMessageText is a MessageField
			restricted
			"Vendor<Vendor><Vendor.VendorName>"

        LocationIndicator is a ConditionalField
            type is AlphaUpper size 1
            classic name is LOC-INDICATOR
            if (PurchaseFromLocationExists)
                "*"
            else
                blank

        DerivedPurchasingVendorAttachmentCount is a DerivedField
			type is Alpha size up to 20
			restricted
			if (PurchasingVendorAttachment set exists)
				return "(" + instance count of PurchasingVendorAttachment set + ")"
			else
				return ""
        
        FormattedFaxNumber is a DerivedField
            type is MessageField
            default label is untranslatable:"Fax"
            return (FaxNumber.InternationalPrefix + blank + FaxNumber.SubscriberNumber + blank + FaxNumber.Extension)
        
        FormattedPhoneNumber is a DerivedField
            type is MessageField
            default label is untranslatable:"Phone"
            return (PhoneNumber.InternationalPrefix + blank + PhoneNumber.SubscriberNumber + blank + PhoneNumber.Extension)

    Local Fields

        LocalVendorGroup            is like VendorGroup
        LocalVendor                 is like Vendor
    
    Conditions

        HasBuyer
            classic name is BUYER
            restricted
            when (Buyer entered)

        HasShipTerm
            classic name is FOB-CODE
            restricted
            when (ShipTerm entered)

        HasGlobalLocationNumber
            classic name is HAS-GLN-NBR
            restricted
            when (GlobalLocationNumber entered)

        PurchaseFromLocationExists
            classic name is LOCATION-EXIST
            restricted
            when (first PurchaseFromLocationsRel exists)

        HasFreightTerm
            classic name is POFRTTERM
            restricted
            when (FreightTerm entered)

		AnonymizeLogAnonymizeRelExists	
			restricted
			when (first AnonymizeLogAnonymizeRel exists)
		
		AnonymizeLogPurgeRelExists	
			restricted
			when (first AnonymizeLogPurgeRel exists)
			
		VendorRecordExists
			restricted
			when (Vendor entered)

		VendorGroupRecordExists
			restricted
			when (VendorGroup entered)

        IsUseDeliveryTolerance
			restricted
			when (DeliveryToleranceOption.UseDeliveryToleranceDays)	
        
        IsZeroDeliveryToleranceDays
            restricted
            when (DeliveryToleranceDays not entered)

    Relations

        PurchaseFromLocationsRel
            one-to-many relation to PurchaseFromLocation
            Field Mapping uses symbolic key
                related.VendorGroup = VendorGroup
                related.Vendor      = Vendor
                
        CrossReferenceVendorRel
        	one-to-many relation to CrossReferenceVendor
			Field Mapping uses symbolic key
                related.VendorGroup = VendorGroup
                related.Vendor      = Vendor
                
                










        VendorRel
        	one-to-one relation to Vendor
        	Field Mapping uses symbolic key
        		related.VendorGroup		= VendorGroup
        		related.Vendor			= Vendor
                
        VendorPurchaseFromLocationRel is a PurchaseFromLocation set
        
        PurchasingCompanyRel
        	one-to-many relation to PurchasingCompany
            Field Mapping uses Set2
                related.ProcurementGroup = VendorGroup
                
        VendorItemRel
        	one-to-many relation to VendorItem
        	Field Mapping uses Set5
        		related.ProcurementGroup = VendorGroup
        		related.Vendor			 = Vendor

        ActiveVendorItemRel
        	one-to-many relation to VendorItem
        	Field Mapping uses Set5
        		related.ProcurementGroup = VendorGroup
        		related.Vendor			 = Vendor
            Instance Selection
                where (related.Active)

    	PurchaseOrderRel
    		one-to-many relation to PurchaseOrder
    		Field Mapping uses Set2
    			related.Company       	= PurchasingCompanyRel.Company
                related.Vendor			= Vendor
        
        VendorGroupRel
        	one-to-one relation to VendorGroup
        	Field Mapping uses symbolic key
        		related.VendorGroup		= Vendor.VendorGroup
        
		AnonymizeLogAnonymizeRel	
			one-to-many relation to AnonymizeLog
			Field Mapping uses ByVendor
				related.FinanceEnterpriseGroup	= VendorGroup.FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
			Instance Selection
				where (related.Status.Anonymized)
				
		AnonymizeLogPurgeRel	
			one-to-many relation to AnonymizeLog
			Field Mapping uses ByVendor
				related.FinanceEnterpriseGroup	= VendorGroup.FinanceEnterpriseGroup
				related.VendorGroup				= VendorGroup
				related.Vendor					= Vendor
			Instance Selection
				where (related.Status.AuditLogPurged)
				
		ProcurementGroupRel
			one-to-one relation to ProcurementGroup
			Field Mapping uses symbolic key
				related.ProcurementGroup = VendorGroup  

		PurchasingVendorAccountsRel
			one-to-many relation to PurchasingVendorAccount
			Field Mapping uses symbolic key
				related.VendorGroup = VendorGroup
				related.Vendor		= Vendor
				
		ActivePurchaseOrderDocumentsRel
			one-to-many relation to PurchaseOrderDocument
			Field Mapping uses symbolic key
				related.Company									= blank
			Instance Selection
				where (related.VendorGroup						= VendorGroup
				and    related.Vendor							= Vendor
				and    related.PurchaseFromLocation not entered
				and	   related.Active)
	
        MobileSupplyChainVendorRel
            one-to-one relation to MobileSupplyChainVendor
            Field Mapping uses symbolic key
                related.VendorGroup     = LocalVendorGroup
                related.Vendor          = LocalVendor

    Field Rules
    	IssueMethod
            default to IssueMethod.Paper 
            
        RevisedIssueMethod
            default to RevisedIssueMethod.Paper
    				
		FaxNumber
			if (action type.Create)
        		default to VendorRel.FaxNumber
        	
        	if (IssueMethod.Fax
        	or RevisedIssueMethod.Fax)
        		required
        			"FaxNumberIsRequiredWhenIssueMethodIsFax"
        
        EDINumber
        	if (IssueMethod.EDI)
        		default to VendorRel.EDINumber
        		required
        			"EDINumberIsRequiredWhenIssueMethodIsEDI"
        	else
        		cannot be entered
        			"IssueMethodMustBeEDIWhenEDINumberIsEntered"					
    	
		EmailAddress
			if (action type.Create)
        		default to VendorRel.EmailAddress
        	
        	if (IssueMethod.Email
        	or	RevisedIssueMethod.Email)
				required
					"EmailAddressIsRequiredWhenIssueMethodIsEmail"
							
		RevisedEDINumber
        	if (RevisedIssueMethod.EDI)
        		default to VendorRel.EDINumber
        		required
        			"RevisedEDINumberIsRequiredWhenRevisedIssueMethodIsEDI"
        	else	
				cannot be entered
					"RevisedIssueMethodMustBeEDIWhenRevisedEDINumberIsEntered"
							
        RevisionsInclude
            default to RevisionsInclude.RevisionsOnly
            
            if (RevisedIssueMethod.DoNotPrint)
            	constraint (RevisionsInclude.NotApplicable)
            		"NotApplicableIsRequiredForThisRevisionMethod<RevisedIssueMethod>"
            		
        RevisedEDITransaction
        	if (not RevisedIssueMethod.EDI) 
        		cannot be entered
        			"IssueMethodMustBeEDIWhenRevisedEDITransactionIsEntered"

        MixContractsOnPurchaseOrder
            initial value is true


        LocationControl
			default to LocationControl.CombineRequisitionLinesToPOLine
            if (!OneSourceDocumentToOnePO.No)                            
                constraint (LocationControl.CannotCombineRequisitions)    
                    "LocationControlShouldBeCannotCombineRequisitionsIfSingleDocumentToAPurchaseOrderIsSelected"

           
		OneSourceDocumentToOnePO
            if (LocationControl.CannotCombineRequisitions)
                constraint (!OneSourceDocumentToOnePO.No)
                    "SingleDocumentToAPurchaseOrderMustBeSelectedIfLocationControlIsCannotCombineRequisitions"


		MaximumOrderAmount
			constraint (MaximumOrderAmount >= 0)
				"CannotEnterANegativeValue"
				
			if (MaximumOrderAmount entered
			and MinimumPurchaseOrderAmount entered)
				constraint (MaximumOrderAmount >= MinimumPurchaseOrderAmount)
					"MaximumPurchaseOrderAmountShouldBeGreaterThanMinimumPurchaseOrderAmount"

					
		MinimumPurchaseOrderAmount
			constraint (MinimumPurchaseOrderAmount >= 0)
				"CannotEnterANegativeValue"
				
		OvershipmentTolerancePercentage
        	if (not OvershipmentToleranceOption.UseOvershipmentTolerance)
				cannot be entered
					"OvershipmentTolerancePercentageMustBeZeroWhenNotUsingOvershipmentTolerance"							
		
        UpdatePurchaseOrderCost
            default to UpdatePurchaseOrderCost.DoNotUpdatePOCostFromReceipt
            
        			
        UndershipmentTolerancePercentage
			if (not UndershipmentToleranceOption.UseUndershipmentTolerance)
				cannot be entered
					"UndershipmentTolerancePercentageMustBeZeroWhenNotUsingUndershipmentTolerance"               				 
		
			constraint (UndershipmentTolerancePercentage <= 100%)
					"UndershipmentTolerancePercentageCanNotBeGreaterThan100"								
        PhoneNumber
        	if (action type.Create)
        		default to VendorRel.PhoneNumber  
        	
		OpenPurchaseLimit


			constraint (OpenPurchaseLimit > OpenPurchaseAmount)
				"OpenPurchaseLimitMustBeGreaterThanCumulativeOpenPurchaseAmount"
						
							
		GTINInUse
			if (GTINInUse changed
			and GTINInUse)
				for each ActiveVendorItemRel
					constraint (each.ManufacturerNumber entered)
						"VendorItem:<each.VendorItem>MustHaveManufacturerInfoToUseGTINs"
		
		ContactName
			default to VendorRel.ContactName
			
		UseIDMTemplate
			if (action type.Update
			and UseIDMTemplate changed
			and UseIDMTemplate)
				initialize PurchaseOrderDocument

        IDMTemplate
            if (UseIDMTemplate)
 				if (IDMTemplate entered)
					constraint (IDMTemplate.IDMDocumentType.PurchaseOrder)
						"InvalidTemplate"	               
            else    
                initialize


		PurchaseOrderDocument
			constraint (PurchaseOrderDocument.Company not entered
			or			PurchaseOrderDocument.PurchaseFromLocation not entered)
				"PurchaseOrderTemplateIsNotValidForThePurchasingVendor"	

        DeliveryToleranceOption
			initial value is DeliveryToleranceOption.DefaultToNextLevel
			default to DeliveryToleranceOption.DefaultToNextLevel

		DeliveryToleranceDays
			if (DeliveryToleranceOption.DisableDeliveryTolerance
            or  DeliveryToleranceOption.DefaultToNextLevel)
				initialize
                cannot be entered
					"DeliveryToleranceDisabledOrDefaultedToNextLevel;CannotEnterToleranceDays"

                
	Actions
        Create is a Create Action
								
        Update is an Update Action
				
        Delete is a Delete Action
			Entrance Rules
        		constraint (VendorPurchaseFromLocationRel not exists)
        			"CannotDelete;VendorHasPurchaseFromLocation"
		
		UpdateGlobalLocationNumber is an Instance Action
			restricted
			Parameters
				PrmGlobalLocationNumber	is a GlobalLocationNumber
			Action Rules
				GlobalLocationNumber	=	PrmGlobalLocationNumber	

		FastUpdate is an Update Action
			restricted
			refresh and lock this instance
			bypass field rules
		
		UpdateOpenPurchaseAmount is an Instance Action
			restricted
			Parameters
				PrmOpenPurchaseAmount is an InternationalAmount
			Action Rules
				OpenPurchaseAmount += PrmOpenPurchaseAmount
		
		Anonymize is an Instance Action	
			restricted
			Action Rules
				if (PurchaseFromLocationExists)
					invoke Anonymize VendorPurchaseFromLocationRel
				
				initialize ContactName
				initialize EDINumber
				initialize FaxNumber
				initialize RevisedEDINumber
				initialize PhoneNumber
				initialize EmailAddress
				initialize Buyer
				 	
			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= VendorGroup.FinanceEnterpriseGroup
					invoked.Status						= 1
					invoked.AffectedBusinessClass		= "PO1"
					invoked.VendorGroup					= VendorGroup
					invoked.Vendor						= Vendor
	
	    PurgeAuditLog is an Instance Action	
	    	restricted
	        Action Rules
	        	if (PurchaseFromLocationExists)
					invoke PurgeAuditLog VendorPurchaseFromLocationRel
							
				invoke purge audit log entries
				 
          	Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= VendorGroup.FinanceEnterpriseGroup
					invoked.Status						= 2
					invoked.AffectedBusinessClass		= "PO1"
					invoked.VendorGroup					= VendorGroup
					invoked.Vendor						= Vendor
          			
		Purge is a Purge Action
			restricted

        CreateMobileSupplyChainVendor is a Set Action 
            restricted
            Parameters
                PrmFinanceEnterpriseGroup              is a FinanceEnterpriseGroup
                    default label is "FinanceEnterpriseGroup"
            Sort Order
                VendorGroup
                Vendor
            Instance Selection
                where (VendorGroup.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
            Action Rules
                Instance Rules
                    LocalVendorGroup = VendorGroup
                    LocalVendor      = Vendor
                    if (MobileSupplyChainVendorRel exists)
                        invoke Update MobileSupplyChainVendorRel
                            invoked.MiscellaneousReceiving = true  
                    else
                        invoke Create MobileSupplyChainVendor
                            invoked.VendorGroup                = VendorGroup
                            invoked.Vendor                     = Vendor
                            invoked.MiscellaneousReceiving     = true                     
