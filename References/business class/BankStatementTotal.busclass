BankStatementTotal is a BusinessClass
    owned by cashmgmt
    prefix is CMBST

    Ontology
    	symbolic key is BankStatementTotal
		
 	Patterns
		implements DynamicCreation 
 		implements AnalyticCube
 			disable continuous update
 			dynamically calculate totals
 			
    Persistent Fields
		TransactionType					is Numeric 2
			States
				Balance			value is 1
				Activity		value is 2
		FinancialYear					is Year
		FinancialPeriod					is Numeric 3
		CashManagementLocation
		FinancialInstitution
		AccountEndingBalance			is an InternationalAmount
		AccountAvailableBalance			is an InternationalAmount
		AccountDailyActivityTotal		is an InternationalAmount
		AccountTransactionAmount		is an InternationalAmount
		AccountCurrency					is a Currency
		BaseEndingBalance				is an InternationalAmount
		BaseAvailableBalance			is an InternationalAmount
		BaseDailyActivityTotal			is an InternationalAmount
		BaseTransactionAmount			is an InternationalAmount
		BaseCurrency					is a Currency
		BaseExchangeRate				is a CurrencyExchangeRate
		MultiplyBaseCurrencyRate		is Boolean
		CompanyEndingBalance			is an InternationalAmount
		CompanyAvailableBalance			is an InternationalAmount
		CompanyDailyActivityTotal		is an InternationalAmount
		CompanyTransactionAmount		is an InternationalAmount
		CompanyCurrency					is a Currency
		CompanyExchangeRate				is a CurrencyExchangeRate
		MultiplyCompanyCurrencyRate		is Boolean
		LocationEndingBalance			is an InternationalAmount
		LocationAvailableBalance		is an InternationalAmount
		LocationDailyActivityTotal		is an InternationalAmount
		LocationTransactionAmount		is an InternationalAmount
		LocationCurrency				is a Currency
		LocationExchangeRate			is a CurrencyExchangeRate
		MultiplyLocationCurrencyRate	is Boolean
		LastUpdatedTimeStamp			is TimeStamp

	Local Fields 								     
		LocalPostingDate				is a PostingDate
		
 	Derived Fields
		AccountBeginningBalance is a DerivedField
			type is like InternationalAmount
			return (AccountEndingBalance - AccountDailyActivityTotal)
			
		BaseBeginningBalance is a DerivedField
			type is like InternationalAmount
			return (BaseEndingBalance - BaseDailyActivityTotal)
		
		BaseTargetBalance is a DerivedField
			type is like InternationalAmount 
			if (TransactionType.Balance)
				if (BaseCurrency = AccountCurrency)
					return BankStatementTotal.CashManagementAccount.CashPositionTarget
				else
				if (MultiplyBaseCurrencyRate)
					return (BankStatementTotal.CashManagementAccount.CashPositionTarget * BaseExchangeRate)
				else
					return (BankStatementTotal.CashManagementAccount.CashPositionTarget / BaseExchangeRate)
						
		CompanyBeginningBalance is a DerivedField
			type is like InternationalAmount
			return (CompanyEndingBalance - CompanyDailyActivityTotal)
			
		LocationBeginningBalance is a DerivedField
			type is like InternationalAmount
			return (LocationEndingBalance - LocationDailyActivityTotal)
		
		CurrentDate is a DerivedField
			type is Date
			restricted
			return first ImportedBankStatementByDateRel.StatementDate
			
		CurrentPeriod is a DerivedField
			type is Numeric 3
			restricted
			return CurrentDate year day
		
		CurrentYear is a DerivedField
			type is Numeric 4
			restricted
			return CurrentDate year
				
		PeriodDateYearDay is a DerivedField
			type is Numeric 3
			restricted
			default label is "Day/Year"
			return BankStatementTotal.FinancialPeriodDate year day
		
		PeriodDateYear is a DerivedField
			type is Numeric 4
			restricted
			return BankStatementTotal.FinancialPeriodDate year
	
		CorporateBaseReceipts is a DerivedField
			type is like InternationalAmount
			if (BankStatementTotal.CashTransactionCategory.NaturalBalance.Credit)
				return BaseTransactionAmount
		
		CorporateBaseDisbursements is a DerivedField
			type is like InternationalAmount
			if (BankStatementTotal.CashTransactionCategory.NaturalBalance.Debit)
				return BaseTransactionAmount
		
		CorporateBaseNetActivity is a DerivedField
			type is like InternationalAmount
			default label is "NetActivity"
			return (CorporateBaseReceipts + CorporateBaseDisbursements)
		
		CloseYearBeginDate is a DerivedField
			type is Date
			LocalPostingDate = BankStatementTotal.FinancialPeriodDate
			return (first PeriodsInThisYear.DerivedBeginGregorian)
		
	Field Rules
		TransactionType
			required
			
		FinancialYear	
			LocalPostingDate = BankStatementTotal.FinancialPeriodDate
			FinancialYear = first GeneralLedgerClosePeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCloseYear
			
		FinancialPeriod	
			FinancialPeriod = (BankStatementTotal.FinancialPeriodDate - CloseYearBeginDate + 1)
		
		CashManagementLocation
			required
			
		FinancialInstitution
			required

		BaseEndingBalance
			if (TransactionType.Balance)
				if (BaseCurrency = AccountCurrency)
					force default to AccountEndingBalance
				else
				if (MultiplyBaseCurrencyRate)
					BaseEndingBalance = (AccountEndingBalance * BaseExchangeRate)
				else
					BaseEndingBalance = (AccountEndingBalance / BaseExchangeRate)
		
		BaseAvailableBalance
			if (TransactionType.Balance)
				if (BaseCurrency = AccountCurrency)
					force default to AccountAvailableBalance
				else
				if (MultiplyBaseCurrencyRate)
					BaseAvailableBalance = (AccountAvailableBalance * BaseExchangeRate)
				else
					BaseAvailableBalance = (AccountAvailableBalance / BaseExchangeRate)
							
		CompanyEndingBalance
			if (TransactionType.Balance)
				if (CompanyCurrency = AccountCurrency)
					force default to AccountEndingBalance
				else
				if (MultiplyCompanyCurrencyRate)
					CompanyEndingBalance = (AccountEndingBalance * CompanyExchangeRate)
				else
					CompanyEndingBalance = (AccountEndingBalance / CompanyExchangeRate)
		
		CompanyAvailableBalance
			if (TransactionType.Balance)
				if (CompanyCurrency = AccountCurrency)
					force default to AccountAvailableBalance
				else
				if (MultiplyCompanyCurrencyRate)
					CompanyAvailableBalance = (AccountAvailableBalance * CompanyExchangeRate)
				else
					CompanyAvailableBalance = (AccountAvailableBalance / CompanyExchangeRate)
							
		LocationEndingBalance
			if (TransactionType.Balance)
				if (LocationCurrency = AccountCurrency)
					force default to AccountEndingBalance
				else
				if (MultiplyLocationCurrencyRate)
					LocationEndingBalance = (AccountEndingBalance * LocationExchangeRate)
				else
					LocationEndingBalance = (AccountEndingBalance / LocationExchangeRate)
		
		LocationAvailableBalance
			if (TransactionType.Balance)
				if (LocationCurrency = AccountCurrency)
					force default to AccountAvailableBalance
				else
				if (MultiplyLocationCurrencyRate)
					LocationAvailableBalance = (AccountAvailableBalance * LocationExchangeRate)
				else
					LocationAvailableBalance = (AccountAvailableBalance / LocationExchangeRate)
							
		LastUpdatedTimeStamp
			force default to current timestamp
						
	Dimensions
    	PeriodDateYearDay 
	    	is a daily period dimension with year of PeriodDateYear
	    		current year is CurrentYear
	    		current period is CurrentPeriod
		BankStatementTotal.LegalEntity
			dimension name is LegalEntity
		BankStatementTotal.CashManagementAccount
			dimension name is CashManagementAccount
		BankStatementTotal.CashTransactionCategory
			dimension name is CashTransactionCategory
		FinancialInstitution
		CashManagementLocation
		AccountCurrency
			
	Measures
		AccountEndingBalance
		AccountAvailableBalance
		BaseEndingBalance
		BaseAvailableBalance
		CompanyEndingBalance
		CompanyAvailableBalance
		LocationEndingBalance
		LocationAvailableBalance
		BaseTargetBalance
		AccountTransactionAmount
		BaseTransactionAmount
		CompanyTransactionAmount
		LocationTransactionAmount
		CorporateBaseReceipts
		CorporateBaseDisbursements
		CorporateBaseNetActivity
		
	Sets
		ByTransactionType
			Sort Order
				CashManagementGroup
				TransactionType
				BankStatementTotal.FinancialPeriodDate
				BankStatementTotal.LegalEntity
				BankStatementTotal.CashManagementAccount
				BankStatementTotal.CashTransactionCategory
				
		ByDescendingDate
			Sort Order
				CashManagementGroup
				BankStatementTotal.LegalEntity
				BankStatementTotal.CashManagementAccount
				BankStatementTotal.CashTransactionCategory
				BankStatementTotal.FinancialPeriodDate		descending

  	Relations
		ImportedBankStatementByDateRel 
			one-to-many relation to BankStatement
			Field Mapping uses ByStatementDate
				related.CashManagementGroup = CashManagementGroup
			Instance Selection
				where (related.StatementType.PreviousDay)		

		BankStatementLineByAccountRel
			one-to-many relation to BankStatementLine
			Field Mapping uses ByStatementDate
				related.CashManagementGroup 	= CashManagementGroup
				related.StatementDate			= BankStatementTotal.FinancialPeriodDate
				related.CashManagementAccount	= BankStatementTotal.CashManagementAccount
						
		BankStatementLineByCategoryRel
			one-to-many relation to BankStatementLine
			Field Mapping uses ByStatementDateCategory
				related.CashManagementGroup 	= CashManagementGroup
				related.StatementDate			= BankStatementTotal.FinancialPeriodDate
				related.CashManagementAccount	= BankStatementTotal.CashManagementAccount
				related.CashTransactionCategory	= BankStatementTotal.CashTransactionCategory
		
		GeneralLedgerClosePeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDate
				related.FinanceEnterpriseGroup		  	= CashManagementGroup
				related.GeneralLedgerCloseConfiguration = BankStatementTotal.LegalEntity.AccountingEntity.CloseConfiguration
			Instance Selection
				where (related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date >= LocalPostingDate)
		
		PeriodsInThisYear
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup 			= CashManagementGroup
				related.GeneralLedgerCloseConfiguration = BankStatementTotal.LegalEntity.AccountingEntity.CloseConfiguration
				related.GeneralLedgerCloseYear          = first GeneralLedgerClosePeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCloseYear
								
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
