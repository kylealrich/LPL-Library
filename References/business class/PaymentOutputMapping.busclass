PaymentOutputMapping is a BusinessClass
	owned by cb
	prefix is POFM

	Ontology
		symbolic key is PaymentOutputMapping

	Patterns
		disable AuditIndex
		
	Persistent Fields
		Description
		Delimiter		is Alpha 1
		RemoveQuotes	is Boolean
			default label is "RemoveQuotesAroundFieldNamesAndValues"
		Active

	Field Rules
		Description
			default to PaymentOutputMapping

		Delimiter
			default to ","
			constraint (Delimiter matches ".*[!@#$%^&*(),./<>?;':\"\\\|\[\]{}`~\-_=+]") 
				"DelimiterCannotContainAlphaOrNumericCharacters"

		Active
			initial value is true
	
			if (Active changed)
				if (!Active)
					constraint (!AttachedToCashPaymentFormat)
						"CannotInactivate;PaymentOutputMappingIsAssignedToCashPaymentFormat<first CashPaymentFormat set.CashCode>-<first CashPaymentFormat set.CashPaymentFormat.BankTransactionCode>"
						
	Attach Rules
		if (parentcontext.name != "PaymentOutputMappingDetail"
		and parentcontext.name != "PaymentOutputMappingRemittance")
			constraint (Active)
				"PaymentOutputMappingIsInactive"
	
	Conditions
		HasDetailsDefined
			restricted
			when (PaymentOutputMappingDetail set exists)
			
		HasRemittancesDefined
			restricted
			when (PaymentOutputMappingRemittance set exists)
		
		AttachedToCashPaymentFormat
			restricted
			when (CashPaymentFormat set exists)
				
	Relations
		PaymentOutputMappingDetailRel
			one-to-many relation to PaymentOutputMappingDetail
			Field Mapping uses ByPosition
				related.CashManagementGroup		= CashManagementGroup
				related.PaymentOutputMapping	= PaymentOutputMapping
		
		PaymentOutputMappingRemittanceRel
			one-to-many relation to PaymentOutputMappingRemittance
			Field Mapping uses ByPosition
				related.CashManagementGroup		= CashManagementGroup
				related.PaymentOutputMapping	= PaymentOutputMapping
		
		PaymentOutputFileDetailRel
			one-to-many relation to PaymentOutputFileDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
		
		PaymentOutputFileRemittanceRel
			one-to-many relation to PaymentOutputFileRemittance
			Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
							
	Actions
		Create is a Create Action

		Update is an Update Action

		UpdateDelimiter is an Instance Action
			restricted
			Action Rules
				Delimiter = ","

		Delete is a Purge Action
			
		CreateDetailFieldMappings is an Instance Action
			valid when (!HasDetailsDefined)
			Local Fields
				LocalData 				is Alpha up to 80
				LocalFieldCounter		is Numeric size 3
				LocalCounter			is Numeric size 5
				LocalDataColArray		is a OutputDataColArray
				LocalFieldNames			is Alpha 4096
				
			Action Rules
				initialize LocalData
				initialize LocalFieldNames
				initialize LocalDataColArray
				LocalFieldCounter = 0
				LocalCounter = 1
				LocalFieldNames = first PaymentOutputFileDetailRel.CsvFieldNames
				LocalFieldNames -= ""
				
				while (LocalCounter < 4096)
					if (LocalFieldNames[LocalCounter:LocalCounter] = " ")
						end while
					else
					if (LocalFieldNames[LocalCounter:LocalCounter] = ",")
						LocalFieldCounter +=1
						LocalDataColArray.ColArray[LocalFieldCounter] = LocalData
						initialize LocalData
					else
						LocalData = LocalData + LocalFieldNames[LocalCounter:LocalCounter]

					LocalCounter +=1

				LocalFieldCounter +=1
				LocalDataColArray.ColArray[LocalFieldCounter] = LocalData
				
				for each LocalDataColArray.ColArray
					if (each entered)
						invoke Create PaymentOutputMappingDetail
							invoked.CashManagementGroup 	= CashManagementGroup
							invoked.PaymentOutputMapping 	= PaymentOutputMapping
							invoked.Field					= each
					else
						end for each
						
		CreateRemittanceFieldMappings is an Instance Action
			valid when (!HasRemittancesDefined)
			Local Fields
				LocalRemitData 			is Alpha up to 80
				LocalRemitFieldCounter	is Numeric size 3
				LocalRemitCounter		is Numeric size 5
				LocalRemitDataColArray	is a OutputDataColArray
				LocalRemitFieldNames	is Alpha 4096
				
			Action Rules
				initialize LocalRemitData
				initialize LocalRemitFieldNames
				initialize LocalRemitDataColArray
				LocalRemitFieldCounter = 0
				LocalRemitCounter = 1
				LocalRemitFieldNames = first PaymentOutputFileRemittanceRel.CsvFieldNames
				LocalRemitFieldNames -= ""
				
				while (LocalRemitCounter < 4096)
					if (LocalRemitFieldNames[LocalRemitCounter:LocalRemitCounter] = " ")
						end while
					else
					if (LocalRemitFieldNames[LocalRemitCounter:LocalRemitCounter] = ",")
						LocalRemitFieldCounter +=1
						LocalRemitDataColArray.ColArray[LocalRemitFieldCounter] = LocalRemitData
						initialize LocalRemitData
					else
						LocalRemitData = LocalRemitData + LocalRemitFieldNames[LocalRemitCounter:LocalRemitCounter]

					LocalRemitCounter +=1

				LocalRemitFieldCounter +=1
				LocalRemitDataColArray.ColArray[LocalRemitFieldCounter] = LocalRemitData
				
				for each LocalRemitDataColArray.ColArray
					if (each entered)
						invoke Create PaymentOutputMappingRemittance
							invoked.CashManagementGroup 	= CashManagementGroup
							invoked.PaymentOutputMapping 	= PaymentOutputMapping
							invoked.Field					= each
					else
						end for each
