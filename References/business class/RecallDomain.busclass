RecallDomain is a BusinessClass
    owned by recall
    prefix is RMRD

    Ontology
    	symbolic key is RecallDomain
    	
    Persistent Fields
    	Description 		is Alpha size 250
    	Active				is Boolean
   	
   	Local Fields
   		LocalRecallNotice	is a RecallNotice
   		
   	Context Fields
		RecallNoticeContext is a RecallNotice
		  	
   	Field Rules
   		Description
   			required
   				"DescriptionIsRequired"
   		
   		Active
            default to true
            
  	Attach Rules
		constraint (Active)
			"DomainIsInactive"
	
	Conditions
		HasOrganizationUsers
			restricted
			when (RecallOrganizationUserRel exists)
			
	Relations
		RecallOrganizationUserRel
			one-to-many relation to RecallOrganizationUser
			Field Mapping uses ByDomainOrganization
				related.RecallGroup	 = RecallGroup
				related.RecallDomain = RecallDomain
		
		RecallNoticeDomainRel
			one-to-one relation to RecallNoticeDomain
			Field Mapping uses symbolic key
				related.RecallGroup	 					= RecallGroup
				related.RecallNotice					= LocalRecallNotice
				related.RecallNoticeDomain.RecallDomain = RecallDomain
						
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		AttachToRecallNoticeDomain is an Instance Action
			valid when (!RecallNoticeContext.HistoricalRecallNotice) 
			default label is "AttachDomain"
			
			Parameters
				RecallGroup
				RecallNotice
				RecallDomain
				
			Parameter Rules
				RecallGroup
					required
				RecallNotice
					required
				RecallDomain
					required
			
			Action Rules
				constraint (!RecallNotice.HistoricalRecallNotice)
					"CanOnlyAttachDomainWhenNoticeIsNotInAHistoricalStatus"
					
				LocalRecallNotice = RecallNotice
				
				if (!RecallNoticeDomainRel exists)
					invoke Create RecallNoticeDomain
						invoked.RecallGroup		= RecallGroup
						invoked.RecallNotice	= RecallNotice
						invoked.RecallDomain	= RecallDomain
					
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
