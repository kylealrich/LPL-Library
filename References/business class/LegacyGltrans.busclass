


                                                            
LegacyGltrans is a BusinessClass             
    owned by glif                             
    prefix is GMG          
   
    Patterns   
        implements S3ClassicAttachments
        disable EffectiveDated 


    Persistent Fields 
 
        Company      is Numeric 4
        FiscalYear   is Numeric 4
        AcctPeriod   is Numeric 2	
        ControlGroup is Numeric 8
        System	     is AlphaUpper 2
        JeType	     is AlphaUpper 1
        JeSequence   is Numeric 2
        LineNbr      is Numeric 6
        ObjId        is Numeric 12
        Status       is Numeric 1
        VarLevels    is AlphaUpper 30
        AcctUnit     is AlphaUpper 15 
        Account	     is Numeric 6
        SubAccount   is Numeric 4
        SourceCode   is AlphaUpper 2
        Date	     is Date
        Reference    is Alpha 10 		
        Description  is Alpha 30
        BaseAmount   is Decimal 18.2
        BaseNd	     is Numeric 1
        UnitsAmount  is Decimal 15.2
        AutoRev	     is AlphaUpper 1
        ToCompany    is Numeric 4
        BaseZone     is AlphaUpper 15
        PostingDate  is Date
        Activity     is AlphaUpper 15
        AcctCategory is AlphaUpper 5
        CurrencyCode is AlphaUpper 5
        Baserate     is Decimal 14.7
        TranAmount   is Decimal 18.2
        TranNd       is Numeric 1
        AcctCurrency is AlphaUpper 5
        AcctRate     is Decimal 14.7
        AcctAmount   is Decimal 18.2
        AcctNd	     is Numeric 1
        OrigCompany  is Numeric 4
        OrigProgram  is AlphaUpper 5
        Operator     is AlphaUpper 10
        ToBaseAmt    is Decimal 18.2
        ToBaseNd     is Numeric 1		
        ToBaserate   is Decimal 14.7
        ToBaseCurr   is AlphaUpper 5
        Reconcile    is AlphaUpper 2
        NegativeAdj  is AlphaUpper 1
        EffectDate   is Date
        UpdateDate   is Date
        SeqTrnsNbr   is Numeric 10
        JbkTrnsNbr   is Numeric 10
        ConsolAcFlag is AlphaUpper 1
            States
                No  value is blank
                Yes value is "Y"
        CtFrCompany  is Numeric 4
        RptAmount1   is Decimal 18.2
        RptRate1     is Decimal 14.7
        RptNd1       is Numeric 1
        RptAmount2   is Decimal 18.2
        RptRate2     is Decimal 14.7
        RptNd2       is Numeric 1	
        CreatedBy    is AlphaUpper 10
        LastUpdtDate is Date
        LastUpdtTime is Numeric 8
        LastUpdateBy is AlphaUpper 10

	Local Fields
		LocalOldCompany					is Alpha 4	
        LocalOldAcctUnit    			is AlphaUpper 15
        LocalOldAccount					is Alpha 6
        LocalOldSubAccount				is Alpha 4
        LocalRunGroup					is AlphaUpper 15
        LocalFinanceEnterpriseGroup2	is AlphaUpper 4
		LocalACCurrencyCode             is AlphaUpper 5
		LocalACFiscalYear               is Numeric 4
		LocalACAcctPeriod               is Numeric 2
		LocalTempActivity               is AlphaUpper 15
		LocalUsrStatus                  is AlphaUpper 2
		LocalYearDiff                   is Numeric 2
		LocalContractActivity           is AlphaUpper 15
	    LocalGeneralLedgerEvent			is AlphaUpper 15
	    ConstructedYear                 is Numeric 4
		ConstructedMonth                is Alpha 2
		ConstructedTransDay             is Alpha 2
	    LocalTransDate                  is Alpha 8
	    LocalContract                   is AlphaUpper 15
	   	I1								is Numeric 3
		I2                              is Numeric 3		
    
    Derived Fields

        JeType1   is a StringField
            type is Alpha size 1
            restricted
            JeType

        ShortDesc is a StringField
            type is Alpha size 15
            restricted
            Description

		ConstructedOldAcctNbr is a StringField
			type is AlphaUpper 10
			restricted
			DerivedOldAccount
			DerivedOldSubAccount
			
		OneByteCompany is a StringField
    		type is AlphaUpper 35
    		restricted
    		LocalOldCompany
    		"   "
			LocalOldAcctUnit	
    	
    	TwoByteCompany is a StringField
    		type is AlphaUpper 35
    		restricted
    		LocalOldCompany
    		"  "
    		LocalOldAcctUnit
    		   		
    	ThreeByteCompany is a StringField
    		type is AlphaUpper 35
    		restricted
    		LocalOldCompany
    		" "
    		LocalOldAcctUnit
		
		FourByteCompany is a StringField
    		type is AlphaUpper 35
    		restricted
    		LocalOldCompany
    		LocalOldAcctUnit
		
    	DerivedOldCompany is a DerivedField
    		type is AlphaUpper 35
    		restricted
    		if (LocalOldCompany size = 1)
    			return OneByteCompany
    		else
    		if (LocalOldCompany size = 2)
    			return TwoByteCompany
    		else
    		if (LocalOldCompany size = 3)
    			return ThreeByteCompany
       		else
    			return FourByteCompany
    			





    	
    	DerivedRunGroup is a StringField
			type is AlphaUpper 15
			restricted
			Company
			"GLMIGR"
    	
    	DerivedRunGroupDetail is a StringField
			type is AlphaUpper 15
			restricted
			Company
			"GL2MIGR"
			
    	DerivedRunGroupAC is a StringField
			type is AlphaUpper 15
			restricted
			Company
			"ACMIGR"
    	
    	OneByteAccount is a StringField
    		type is AlphaUpper 6
    		restricted
    		"00000"
			LocalOldAccount	
    	
    	TwoByteAccount is a StringField
    		type is AlphaUpper 6
    		restricted
    		"0000"
    		LocalOldAccount
    		   		
    	ThreeByteAccount is a StringField
    		type is AlphaUpper 6
    		restricted
    		"000"
    		LocalOldAccount
		
		FourByteAccount is a StringField
    		type is AlphaUpper 6
    		restricted
    		"00" 
    		LocalOldAccount
		
		FiveByteAccount is a StringField
    		type is AlphaUpper 6
    		restricted
    		"0" 
    		LocalOldAccount
    	
    	SixByteAccount is a StringField
    		type is AlphaUpper 6
    		restricted
    		LocalOldAccount
    		
    	OneByteSubAccount is a StringField
    		type is AlphaUpper  4
    		restricted
    		"000"
			LocalOldSubAccount	
    	
    	TwoByteSubAccount is a StringField
    		type is AlphaUpper  4
    		restricted
    		"00"
    		LocalOldSubAccount
    		   		
    	ThreeByteSubAccount is a StringField
    		type is AlphaUpper 4
    		restricted
    		"0"
    		LocalOldSubAccount
		
		FourByteSubAccount is a StringField
    		type is AlphaUpper 4
    		restricted
    		LocalOldSubAccount
    		
    	DerivedOldAccount is a DerivedField
    		type is AlphaUpper 6
    		restricted
    		if (LocalOldAccount size = 1)
    			return OneByteAccount
    		else
    		if (LocalOldAccount size = 2)
    			return TwoByteAccount
    		else
    		if (LocalOldAccount size = 3)
    			return ThreeByteAccount
       		else
       		if (LocalOldAccount size = 4)
    			return FourByteAccount
    		else
       		if (LocalOldAccount size = 5)
    			return FiveByteAccount	
    		else	
    		    return SixByteAccount
    		    
    	DerivedOldSubAccount is a DerivedField
    		type is AlphaUpper 4
    		restricted
    		if (LocalOldSubAccount size = 1)
    			return OneByteSubAccount
    		else
    		if (LocalOldSubAccount size = 2)
    			return TwoByteSubAccount
    		else
    		if (LocalOldSubAccount size = 3)
    			return ThreeByteSubAccount
       		else
    			return FourByteSubAccount
    			
    	GLJournalEntries is a DerivedField
	        type is Alpha size 80
	        restricted
	        return "LegacyGltrans.MigrateToGltransrel"
	        
	    DerivedPeriodDescription is a StringField
			type is AlphaUpper 30
			restricted
			"ACMIGR"
			"-"
			LocalACCurrencyCode
			"-"
			LocalACFiscalYear
			"-"
			LocalACAcctPeriod
    		
        DerivedOffsetPeriodDescription is a StringField
			type is AlphaUpper 30
			restricted
			"Offset ACMIGR"
			"-"
			LocalACCurrencyCode
			"-"
			LocalACFiscalYear
			"-"
			LocalACAcctPeriod
			
	    DerivedContractPeriodDescription is a StringField
			type is AlphaUpper 30
			restricted
			"ACMIGR"
			"-"
			LocalACFiscalYear
			"-"
			LocalACAcctPeriod
			"-"
			LocalTempActivity
    		
        DerivedContractOffsetPeriodDescription is a StringField
			type is AlphaUpper 30
			restricted
			"Offset Contract ACMIGR"
			"-"
			LocalACFiscalYear
			"-"
			LocalACAcctPeriod
	  	
	  	ConstructedACTransactionDate is a StringField
	        type is Alpha 8
	        restricted
			ConstructedYear
			ConstructedMonth
			ConstructedTransDay
	  	
#ifdef module Migration  
	  	DerivedACTransactionDate		is a DerivedField
			type is Date
			restricted	 
		    LocalTransDate = CompanyRel.PerEndDates.PerEndDate[LocalACAcctPeriod] - LocalYearDiff as years 		
			ConstructedYear = LocalTransDate[1:4]
			ConstructedMonth = LocalTransDate[5:6]
			ConstructedTransDay = "01" 
		    return ConstructedACTransactionDate
#endif
	  		  	
	  	FixRunGroupDetail is a StringField
			type is AlphaUpper 15
			restricted
			Company
			"FIXGLTRANS"
    
    Conditions




        AutoReverse
            when (JeType = "A"
            or    JeType = "R")
            
        ValidAmount
            when (TranAmount  != 0
            or    UnitsAmount != 0
            or    BaseAmount  != 0)
        




    Relations

    	LegacyGltransRel
    		one-to-many relation to LegacyGltrans
    		Field Mapping uses Set1
    		Instance Selection	
    			where (related.ControlGroup = ControlGroup
    			and 	related.JeSequence  = JeSequence)
    			
    	GeneralLedgerEventRel
			one-to-one relation to GeneralLedgerEvent
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= LocalFinanceEnterpriseGroup2
				related.GeneralLedgerEvent			= LocalGeneralLedgerEvent
 
#ifdef module Migration   	
    	MgcoaffilRel
    		one-to-one relation to Mgcoaffil
    		Field Mapping uses Set1
    			related.S3company	= Company
    			
		GltransrelRel
			one-to-many relation to	Gltransrel
			Field Mapping uses Set1
				related.RunGroup                        = LocalRunGroup
				
		AcactivityRel
			one-to-one relation to Acactivity
			Field Mapping uses Set1
				related.Activity	= Activity 
		
    	Acactivity2Rel
			one-to-one relation to Acactivity
			Field Mapping uses Set1
				related.Activity	= LocalTempActivity 
				
	   	Acactivity3Rel
			one-to-one relation to Acactivity
			Field Mapping uses Set1
				related.Activity	= LocalContractActivity 
				
		CompanyRel
            one-to-one relation to LegacyGlsystem
            required
            Field Mapping uses Set1
                related.Company = Company  
                




				
	    LegacyAcstatusRel
			one-to-one relation to LegacyAcstatus
			Field Mapping uses Set1
				related.UsrStatus		        = LocalUsrStatus
				
	    AccntractRel
            one-to-many relation to Accntract
            Field Mapping uses Set2
                related.Activity                = LocalTempActivity
                





        
        AccntractxRel
            one-to-many relation to Accntractx
            Field Mapping uses Set1
                related.Contract = LocalContract
            Instance Selection	
                where (related.ContractFlag = "Y")
                
        MgglsyscdRel
			one-to-many relation to Mgglsyscd
			Field Mapping uses Set1
    			
		MgfegdftRel
			one-to-one relation to Mgfegdft
			Field Mapping uses Set1
				related.FinEntGroup					= LocalFinanceEnterpriseGroup2	
				    			
#endif
    			
    Sets

        Set1
            primary
            indexed
            Sort Order
                Company
                FiscalYear
                AcctPeriod
                ControlGroup
                System
                JeType
                JeSequence
                LineNbr

        Set2
            not indexed
            Sort Order
                Company
                Status
                FiscalYear
                AcctPeriod
                ControlGroup
                System
                JeType
                JeSequence
                LineNbr
       
        Set3
            not indexed
            duplicates
            Sort Order
                Company
                Status
                FiscalYear
                AcctUnit
                Account
                SubAccount
                Activity
                AcctCategory
                CurrencyCode
                PostingDate
                System
                AcctPeriod
        
        Set7

            indexed
            Sort Order
                ObjId

	Actions
#ifdef module Migration
		Create is a Create Action 
		Update is an Update Action 
		Delete is a Delete Action
	
		MigrateToGltransrel is a Set Action
			completion message is "JournalEntriesMigratedToGltransrelFor<PrmFinanceEnterpriseGroup>"
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmMigrationPlan is a MigrationPlan
					default label is "MigrationPlan"
			    PrmCompany 				  	is a S3CompanyMapping
					default label is "GeneralLedgerCompany"
					
			Parameter Rules
			    PrmMigrationPlan
			        required
			
			Local Fields
				LocalSequenceNumber 		is Numeric 12

				LocalCounter	 			is Numeric 6	
				LocalFinanceEnterpriseGroup is a FinanceEnterpriseGroup
				LocalMigrationPlanTask		is a MigrationPlanTask view
				CompanyRunGroup             is AlphaUpper 15
				AsyncId						is a AsyncActionRequest	
				TempFinanceEnterpriseGroup  is AlphaUpper 4
               	LocalSystemCount            is Numeric 3 
	        	LocalValidSystemCode        is AlphaUpper 2  
	        	LocalSystemCodeMigArray		is a SystemCodeMigArray
	        	LocalMapGLOnlyActivity	    is Numeric 2
	        	
			
			Instance Selection
				where (Company = PrmCompany 
				and    Status = 1
				and    FiscalYear >= PrmCompany.StartYear)
          
			Sort Order
                Company
                Status
                FiscalYear
                AcctPeriod
                ControlGroup
                System
                JeType
                JeSequence
                LineNbr

			Entrance Rules
				LocalSequenceNumber = 0
				
			Action Rules
				Set Rules
					Entrance Rules
						LocalFinanceEnterpriseGroup = MgcoaffilRel.FinEntGroup
						
						I1 = 0
						LocalSystemCount = 0
				        if (MgglsyscdRel exists)
				      		for each MgglsyscdRel
					    		I1 += 1
								LocalSystemCodeMigArray.SystemCodeMigData[I1].S3SystemCode       = each.S3SystemCode
					    		LocalSystemCodeMigArray.SystemCodeMigData[I1].CSFSystemCode      = each.CSFSystemCode
				    			LocalSystemCount += 1
					Exit Rules
									
				Company Set Rules
					Exit Rules	
					    LocalRunGroup                = CompanyRunGroup
					    LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
						if (GltransrelRel exists)
							invoke ImportToGL Gltransrel in background
								assign async action request id to AsyncId
								invoked.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
								invoked.PrmRunGroup				= LocalRunGroup		
                    
                    	    invoke InitiateTransactionInterface PrmMigrationPlan in background
							    run after AsyncId	
							    assign async action request id to AsyncId
							   	invoked.PrmEnterpriseGroup      	= LocalFinanceEnterpriseGroup2
							   	invoked.PrmRunGroup 				= LocalRunGroup	
							   	invoked.PrmCompany                  = PrmCompany
							   	invoked.PrmPostTransactions         = false
							   	invoked.PrmNoPartialUpdate          = false
							   	
			
                AcctPeriod Set Rules
                	Entrance Rules
                		LocalCounter 		= 0



                JeSequence Set Rules
                	Entrance Rules
                		LocalCounter		+= 1

                Instance Rules
					LocalSequenceNumber		+= 1


					CompanyRunGroup                        = DerivedRunGroup
                    TempFinanceEnterpriseGroup             = PrmFinanceEnterpriseGroup
                    LocalFinanceEnterpriseGroup2           = PrmFinanceEnterpriseGroup
                    LocalMapGLOnlyActivity                 = MgfegdftRel.MapGLOnlyActivity
                    
                    
                    initialize LocalValidSystemCode
                    I2      		= 1
			        if (LocalSystemCodeMigArray.SystemCodeMigData[1].CSFSystemCode not entered)
				        LocalValidSystemCode = System
			        else
			            while (I2  <= LocalSystemCount)
        				    if (System = LocalSystemCodeMigArray.SystemCodeMigData[I2].S3SystemCode)
					            LocalValidSystemCode = LocalSystemCodeMigArray.SystemCodeMigData[I2].CSFSystemCode
				                end while
				            else
				                I2              += 1
				    if (LocalValidSystemCode = blank)
				        LocalValidSystemCode = System
                    
                    

                   	LocalOldCompany         = ToCompany
		            LocalOldAcctUnit        = AcctUnit

		            LocalOldAccount         = Account
	                LocalOldSubAccount      = SubAccount
	                
		            


					
					if (UnitsAmount != 0 or TranAmount !=0 or BaseAmount != 0)
						invoke Create Gltransrel  
							fill in fields from this instance

							
							invoked.Activity        = Activity
							invoked.AcctCategory    = AcctCategory
                            if (AcactivityRel.PostingFlag = blank)
			                    initialize invoked.Activity 
			                   	initialize invoked.AcctCategory
			                    if (LocalMapGLOnlyActivity = 0)
			                        initialize invoked.GLOnlyActivity 
			                    	initialize invoked.GLOnlyAcctCategory
			                    else

			                        invoked.GLOnlyActivity       = Activity 
			                    	invoked.GLOnlyAcctCategory   = AcctCategory							
							
							invoked.RunGroup		= DerivedRunGroup
							invoked.SeqNumber		= LocalSequenceNumber        
							invoked.Company         = Company
							invoked.DocumentNbr 	= ControlGroup
		                	invoked.OldCompany      = DerivedOldCompany	
							if (SubAccount entered)
								invoked.OldAcctNbr	= ConstructedOldAcctNbr
							else
								invoked.OldAcctNbr	= DerivedOldAccount


							LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
							LocalGeneralLedgerEvent   = SourceCode
				    		if (GeneralLedgerEventRel exists)
				    		    invoked.SourceCode    = SourceCode
					        else
					      		invoked.SourceCode    = "TC"
							invoked.Date		    = Date 
							invoked.Reference		= Reference

                            invoked.System          = LocalValidSystemCode 


                        	invoked.ProgramCode		= LocalCounter



                            invoked.AutoRev         = AutoRev
		                	invoked.PostingDate		= PostingDate 

        MigrateActivityGltransToGltransrel is a Set Action
			completion message is "MigrateActivityGltansToGltransrelFor<PrmFinanceEnterpriseGroup>"
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmMigrationPlan is a MigrationPlan
					default label is "MigrationPlan"
			    PrmCompany 				  	is a S3CompanyMapping
					default label is "GeneralLedgerCompany"
				PrmDtlYear 				  	is Numeric 4
					default label is "DetailYear"	
					
			Parameter Rules
			    PrmMigrationPlan
			        required
			    PrmDtlYear
			        required
			
			Local Fields
				LocalSequenceNumber 		is Numeric 12
				LocalCounter	 			is Numeric 6	
				LocalFinanceEnterpriseGroup is a FinanceEnterpriseGroup
				LocalMigrationPlanTask		is a MigrationPlanTask view
				CompanyRunGroup             is AlphaUpper 15
				AsyncId						is a AsyncActionRequest	
				TempFinanceEnterpriseGroup  is AlphaUpper 4
				LocalYearDiffActivity       is Numeric 2
				LocalACCompany              is Numeric 4
                LocalACDate                 is Date
	        	LocalACCurrencyCode2        is AlphaUpper 5
	        	LocalACSystem	            is AlphaUpper 2 
	            LocalACPostingDate          is Date
	            LocalACActivity             is AlphaUpper 15
	            LocalACAcctCategory         is AlphaUpper 5
	        	LocalACFiscalYear2          is Numeric 4
		        LocalACAcctPeriod2          is Numeric 2
		        LocalACToCompany            is Numeric 4
	            LocalACAcctUnit             is AlphaUpper 15
			    LocalACAccount              is Numeric 6
		        LocalACSubAccount           is Numeric 4
		        LocalSystemCount            is Numeric 3 
	        	LocalValidSystemCode        is AlphaUpper 2  
	        	LocalSystemCodeMigArray		is a SystemCodeMigArray
	        	LocalMapGLOnlyActivity   	is Numeric 2
	        	





















			


			Instance Selection
				where (Company   = PrmCompany
				and    Status    = 9
				and    Activity  != blank
				and    FiscalYear >= PrmCompany.StartYear
				and    FiscalYear < PrmDtlYear)















			
			Sort Order
			    Company
			    Status
				FiscalYear
				AcctUnit
				Account
				SubAccount
				Activity
				AcctCategory
				CurrencyCode
				PostingDate
				System
				AcctPeriod
			
			Accumulators
				TranAmountTotal
				UnitsAmountTotal
				RptAmount1Total
				RptAmount2Total
				BaseAmountTotal
			
			Entrance Rules
				LocalSequenceNumber = 0
				
			Action Rules
				Set Rules
					Entrance Rules
						LocalFinanceEnterpriseGroup = MgcoaffilRel.FinEntGroup						
						I1 = 0
						LocalSystemCount = 0
				        if (MgglsyscdRel exists)
				      		for each MgglsyscdRel
					    		I1 += 1
								LocalSystemCodeMigArray.SystemCodeMigData[I1].S3SystemCode       = each.S3SystemCode
					    		LocalSystemCodeMigArray.SystemCodeMigData[I1].CSFSystemCode      = each.CSFSystemCode
				    			LocalSystemCount += 1
						
					Exit Rules
									
				Company Set Rules
					Exit Rules	
					    LocalRunGroup                = CompanyRunGroup
					    LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
						if (GltransrelRel exists)
							invoke ImportToGL Gltransrel in background
								assign async action request id to AsyncId
								invoked.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
								invoked.PrmRunGroup				= LocalRunGroup		
                    
                    	    invoke InitiateTransactionInterface PrmMigrationPlan in background
							    run after AsyncId	
							    assign async action request id to AsyncId
							   	invoked.PrmEnterpriseGroup      	= LocalFinanceEnterpriseGroup2
							   	invoked.PrmRunGroup 				= LocalRunGroup	
							   	invoked.PrmCompany                  = PrmCompany
							   	invoked.PrmPostTransactions         = true
							   	invoked.PrmNoPartialUpdate          = true
							   	invoked.PrmBypassProjectEdits       = true
							   	
				AcctPeriod Set Rules
					Entrance Rules
						TranAmountTotal        = 0
						UnitsAmountTotal       = 0
						RptAmount1Total        = 0
						RptAmount2Total        = 0
						BaseAmountTotal        = 0
						LocalACCompany         = Company
	                	LocalACToCompany       = ToCompany
	                   	LocalACAcctUnit        = AcctUnit
			           	LocalACAccount         = Account
		                LocalACSubAccount      = SubAccount
						LocalACDate            = Date
						LocalACCurrencyCode2   = CurrencyCode
						LocalACSystem	       = System 
	                	LocalACPostingDate     = PostingDate
	                	LocalACActivity        = Activity
	                	LocalACAcctCategory    = AcctCategory
			            LocalACFiscalYear2     = FiscalYear
			            LocalACAcctPeriod2     = AcctPeriod
						
	           		Exit Rules
		                TempFinanceEnterpriseGroup             = PrmFinanceEnterpriseGroup
		                LocalYearDiff                          = LocalYearDiffActivity
		                LocalFinanceEnterpriseGroup2           = TempFinanceEnterpriseGroup
		                LocalMapGLOnlyActivity                 = MgfegdftRel.MapGLOnlyActivity
		                
                        initialize LocalValidSystemCode
                        I2      		= 1
			            if (LocalSystemCodeMigArray.SystemCodeMigData[1].CSFSystemCode not entered)
				            LocalValidSystemCode = LocalACSystem
			            else
    			            while (I2  <= LocalSystemCount)
            				    if (LocalACSystem = LocalSystemCodeMigArray.SystemCodeMigData[I2].S3SystemCode)
		    			            LocalValidSystemCode = LocalSystemCodeMigArray.SystemCodeMigData[I2].CSFSystemCode
			    	                end while
				                else
				                    I2              += 1
				        if (LocalValidSystemCode = blank)
				            LocalValidSystemCode = LocalACSystem
                    
						if (TranAmountTotal != 0 or UnitsAmountTotal !=0 or BaseAmountTotal !=0)
							invoke Create Gltransrel  
								invoked.RunGroup        = CompanyRunGroup
								LocalSequenceNumber += 1
								invoked.SeqNumber		= LocalSequenceNumber 
								invoked.Company         = LocalACCompany
			                	LocalOldCompany         = LocalACToCompany
			                	LocalOldAcctUnit        = LocalACAcctUnit
			                	LocalOldAccount         = LocalACAccount
		                        LocalOldSubAccount      = LocalACSubAccount
			                	invoked.OldCompany      = DerivedOldCompany	
								if (LocalOldSubAccount entered)
									invoked.OldAcctNbr	= ConstructedOldAcctNbr
								else
									invoked.OldAcctNbr	= DerivedOldAccount
								LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
	    						LocalGeneralLedgerEvent   = SourceCode
		    		    		if (GeneralLedgerEventRel exists)
			    	    		    invoked.SourceCode    = SourceCode
				    	        else
					        		invoked.SourceCode    = "TC"
								LocalACCurrencyCode     = LocalACCurrencyCode2
								LocalACFiscalYear       = LocalACFiscalYear2
								LocalACAcctPeriod       = LocalACAcctPeriod2
								invoked.Date            = DerivedACTransactionDate
								invoked.Reference		= Reference
						       	
						       	LocalTempActivity       = LocalACActivity
						   		if (Acactivity2Rel.PostingFlag = "C")
								    invoked.Description     = DerivedContractPeriodDescription
								else
								    invoked.Description     = DerivedPeriodDescription
								
								invoked.CurrencyCode    = LocalACCurrencyCode2
								invoked.UnitsAmount     = UnitsAmountTotal
								invoked.TranAmount		= TranAmountTotal
								invoked.RptAmount1      = RptAmount1Total
								invoked.RptAmount2      = RptAmount2Total
								invoked.BaseAmount      = BaseAmountTotal
								invoked.Baserate        = 1.0
                                invoked.System          = LocalValidSystemCode 
								invoked.ProgramCode     = "ACMIGR"
								invoked.AutoRev    		= "N"
			                	invoked.PostingDate		= CompanyRel.PerEndDates.PerEndDate[LocalACAcctPeriod2] - LocalYearDiffActivity as years 



			                	LocalTempActivity       = LocalACActivity
			                   	if (Acactivity2Rel.PostingFlag = "C")  
			                   	    LocalContract = AccntractRel.Contract
			                   	    if (AccntractxRel.exists)
			                	        invoked.Activity        = first AccntractxRel.Activity
			               	            invoked.AcctCategory    = LocalACAcctCategory
			                	else   
			                    	if (Acactivity2Rel.PostingFlag = blank)
			                    	    initialize invoked.Activity 
			                    	   	initialize invoked.AcctCategory
			                    	    if (LocalMapGLOnlyActivity = 0)
			                    	        initialize invoked.GLOnlyActivity 
			                    	    	initialize invoked.GLOnlyAcctCategory
			                    	    else

			                    	        invoked.GLOnlyActivity       = LocalACActivity 
			                    	    	invoked.GLOnlyAcctCategory   = LocalACAcctCategory
			                    	else
			                	        invoked.Activity        = LocalACActivity
			                	        invoked.AcctCategory    = LocalACAcctCategory





	                        	if (Acactivity2Rel.PostingFlag != blank or LocalMapGLOnlyActivity =0)
	                        		invoked.Billed            = 3
         		                	invoked.RevenueRecognized = 1
         		                	invoked.Capitalize        = 1
         		                else
         		                	initialize invoked.Billed
         		                	initialize invoked.RevenueRecognized
         		                	initialize invoked.Capitalize

			                
			                invoke Create Gltransrel  
								LocalSequenceNumber += 1
								invoked.SeqNumber       = LocalSequenceNumber
                                invoked.RunGroup        = CompanyRunGroup 
								LocalSequenceNumber += 1
								invoked.SeqNumber		= LocalSequenceNumber 
								invoked.Company         = LocalACCompany

			                	LocalOldCompany         = LocalACToCompany
			                	LocalOldAcctUnit        = LocalACAcctUnit
			                	LocalOldAccount         = LocalACAccount
		                        LocalOldSubAccount      = LocalACSubAccount
			                	invoked.OldCompany      = DerivedOldCompany	
								if (LocalOldSubAccount entered)
									invoked.OldAcctNbr	= ConstructedOldAcctNbr
								else
									invoked.OldAcctNbr	= DerivedOldAccount

								LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
				    			LocalGeneralLedgerEvent   = SourceCode
				        		if (GeneralLedgerEventRel exists)
				    	    	    invoked.SourceCode    = SourceCode
					            else
					      		    invoked.SourceCode    = "TC"


								LocalACCurrencyCode     = LocalACCurrencyCode2
								LocalACFiscalYear       = LocalACFiscalYear2
								LocalACAcctPeriod       = LocalACAcctPeriod2
								invoked.Date            = DerivedACTransactionDate
								invoked.Reference		= Reference
								if (Acactivity2Rel.PostingFlag = "C")
					    			invoked.Description     = DerivedContractOffsetPeriodDescription
								else
			        				invoked.Description     = DerivedOffsetPeriodDescription
								invoked.CurrencyCode    = LocalACCurrencyCode2
								invoked.UnitsAmount     = UnitsAmountTotal * -1
								invoked.TranAmount		= TranAmountTotal * -1
								invoked.RptAmount1      = RptAmount1Total * -1
								invoked.RptAmount2      = RptAmount2Total * -1
								invoked.BaseAmount      = BaseAmountTotal * -1
								invoked.Baserate        = 1.0



                                invoked.System          = LocalValidSystemCode
								invoked.ProgramCode     = "ACMIGR"
								invoked.AutoRev    		= "N"



			                  	invoked.PostingDate		= CompanyRel.PerEndDates.PerEndDate[LocalACAcctPeriod2] - LocalYearDiffActivity as years 
			                	initialize invoked.Activity 	
			                	initialize invoked.AcctCategory

                Instance Rules
                    TranAmountTotal			+= TranAmount
					UnitsAmountTotal        += UnitsAmount
					RptAmount1Total         += RptAmount1
					RptAmount2Total         += RptAmount2
					BaseAmountTotal         += BaseAmount

					CompanyRunGroup			= DerivedRunGroupAC
					LocalYearDiffActivity   = (CompanyRel.FiscalYear - FiscalYear)
    

		MigrateToGltransrelInDetail is a Set Action
			completion message is "JournalEntriesMigratedToGltransrelInDetailFor<PrmFinanceEnterpriseGroup>"
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmMigrationPlan is a MigrationPlan
					default label is "MigrationPlan"
			    PrmCompany 				  	is a S3CompanyMapping
					default label is "GeneralLedgerCompany"
			    PrmDtlYear 				  	is Numeric 4
					default label is "DetailYear"
					
			Parameter Rules
			    PrmMigrationPlan
			        required
			    PrmDtlYear
					required
			
			Local Fields
				LocalSequenceNumber 		is Numeric 12

				LocalCounter	 			is Numeric 6	
				LocalFinanceEnterpriseGroup is a FinanceEnterpriseGroup
				LocalMigrationPlanTask		is a MigrationPlanTask view
				CompanyRunGroup             is AlphaUpper 15
				AsyncId						is a AsyncActionRequest	
				TempFinanceEnterpriseGroup  is AlphaUpper 4
			    LocalSystemCount            is Numeric 3 
	        	LocalValidSystemCode        is AlphaUpper 2  
	        	LocalSystemCodeMigArray		is a SystemCodeMigArray
	           	LocalMapGLOnlyActivity   	is Numeric 2
















			

			Instance Selection
				where (Company = PrmCompany 
				and    Status = 9
				and    FiscalYear >= PrmDtlYear
				and    AcctPeriod != 99
                and    ValidAmount)
			
			Sort Order
                Company
                Status
                FiscalYear
                AcctPeriod
                ControlGroup
                System
                JeType
                JeSequence
                LineNbr

			Entrance Rules
				LocalSequenceNumber = 0
				
			Action Rules
				Set Rules
					Entrance Rules
						LocalFinanceEnterpriseGroup  = MgcoaffilRel.FinEntGroup
						LocalFinanceEnterpriseGroup2 = MgcoaffilRel.FinEntGroup
						LocalMapGLOnlyActivity       = MgfegdftRel.MapGLOnlyActivity
						
						I1 = 0
						LocalSystemCount = 0
				        if (MgglsyscdRel exists)
				      		for each MgglsyscdRel
					    		I1 += 1
								LocalSystemCodeMigArray.SystemCodeMigData[I1].S3SystemCode       = each.S3SystemCode
					    		LocalSystemCodeMigArray.SystemCodeMigData[I1].CSFSystemCode      = each.CSFSystemCode
				    			LocalSystemCount += 1
						
					Exit Rules
									
				Company Set Rules
					Exit Rules	
					    LocalRunGroup                = CompanyRunGroup
					    LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
						if (GltransrelRel exists)
							invoke ImportToGL Gltransrel in background
								assign async action request id to AsyncId
								invoked.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
								invoked.PrmRunGroup				= LocalRunGroup		
                    
                    	    invoke InitiateTransactionInterface PrmMigrationPlan in background
							    run after AsyncId	
							    assign async action request id to AsyncId
							   	invoked.PrmEnterpriseGroup      	= LocalFinanceEnterpriseGroup2
							   	invoked.PrmRunGroup 				= LocalRunGroup	
							   	invoked.PrmCompany                  = PrmCompany
							   	invoked.PrmPostTransactions         = true
							   	invoked.PrmNoPartialUpdate          = false
							   	invoked.PrmBypassProjectEdits       = true
			
                AcctPeriod Set Rules
                	Entrance Rules
                		LocalCounter 		= 0

                JeSequence Set Rules
                	Entrance Rules
                		LocalCounter		+= 1

                Instance Rules
					LocalSequenceNumber		+= 1
					CompanyRunGroup                        = DerivedRunGroupDetail
                    TempFinanceEnterpriseGroup             = PrmFinanceEnterpriseGroup
                   	LocalYearDiff 				    	   = (CompanyRel.FiscalYear - FiscalYear)
                   	LocalUsrStatus                         = AcactivityRel.UsrStatus
                   	LocalTempActivity                      = Activity
                   	
                    initialize LocalValidSystemCode
                    I2      		= 1
			        if (LocalSystemCodeMigArray.SystemCodeMigData[1].CSFSystemCode not entered)
				        LocalValidSystemCode = System
			        else
			            while (I2  <= LocalSystemCount)
        				    if (System = LocalSystemCodeMigArray.SystemCodeMigData[I2].S3SystemCode)
					            LocalValidSystemCode = LocalSystemCodeMigArray.SystemCodeMigData[I2].CSFSystemCode
				                end while
				            else
				                I2              += 1
				    if (LocalValidSystemCode = blank)
				        LocalValidSystemCode = System
                    
					if (((JeType = "A" or JeType = "R") 
					and  FiscalYear = PrmDtlYear
					and  AcctPeriod = 1)
					or   (JeType != "A"
					and   JeType != "R"))
						invoke Create Gltransrel  
							fill in fields from this instance
							invoked.RunGroup		= DerivedRunGroupDetail
							invoked.SeqNumber		= LocalSequenceNumber 
							invoked.Company         = Company
							invoked.DocumentNbr 	= ControlGroup
		                	LocalOldCompany         = ToCompany
		                	LocalOldAcctUnit        = AcctUnit
		                	LocalOldAccount         = Account
	                        LocalOldSubAccount      = SubAccount
		                	invoked.OldCompany      = DerivedOldCompany	
							if (SubAccount entered)
								invoked.OldAcctNbr	= ConstructedOldAcctNbr
							else
								invoked.OldAcctNbr	= DerivedOldAccount
    						LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
    						LocalGeneralLedgerEvent   = SourceCode
				    		if (GeneralLedgerEventRel exists)
				    		    invoked.SourceCode    = SourceCode
					        else
					      		invoked.SourceCode    = "TC"
							invoked.Date		    = Date 
							invoked.Reference		= Reference
                            invoked.System          = LocalValidSystemCode 
							invoked.ProgramCode		= LocalCounter
							invoked.AutoRev    		= AutoRev

			               	if (AcactivityRel.PostingFlag = "C")
			                    LocalContract = AccntractRel.Contract
			                    if (AccntractxRel.exists)
			                	    invoked.Activity        = first AccntractxRel.Activity
			               	        invoked.AcctCategory    = AcctCategory
			               	else
			                   	if (AcactivityRel.PostingFlag = blank)
			                    	initialize invoked.Activity
			                   	    initialize invoked.AcctCategory 
			                   	    if (LocalMapGLOnlyActivity = 0)
			                   	        initialize invoked.GLOnlyActivity 
			                   	    	initialize invoked.GLOnlyAcctCategory
			                   	    else

			                   	        invoked.GLOnlyActivity       = Activity 
			                   	    	invoked.GLOnlyAcctCategory   = AcctCategory	
						    	else
						        	invoked.Activity        = Activity
							        invoked.AcctCategory    = AcctCategory
		                	invoked.PostingDate		= CompanyRel.PerEndDates.PerEndDate[AcctPeriod] - LocalYearDiff as years 
		                    invoked.Baserate               = 1.0    	

		                	if (Activity != blank)
                                initialize invoked.ToBaseAmt
                                initialize invoked.ToBaseRate






               	             	if (AcactivityRel.PostingFlag != blank or LocalMapGLOnlyActivity =0)
                             		invoked.Billed             = 3
   		                        	invoked.RevenueRecognized  = 1
                                	invoked.Capitalize         = 1 
                                else
         		                	initialize invoked.Billed
         		                	initialize invoked.RevenueRecognized
         		                	initialize invoked.Capitalize
		
		FixGltransAutoReverse is a Set Action
			completion message is "FixGltransAutoReverse"
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmMigrationPlan is a MigrationPlan
					default label is "MigrationPlan"
			    PrmCompany 				  	is a S3CompanyMapping
					default label is "GeneralLedgerCompany"
			    PrmDtlYear 				  	is Numeric 4
					default label is "DetailYear"
					
			Parameter Rules
			    PrmMigrationPlan
			        required
			    PrmDtlYear
					required
			
			Local Fields
				LocalSequenceNumber 		is Numeric 12

				LocalCounter	 			is Numeric 6	
				LocalFinanceEnterpriseGroup is a FinanceEnterpriseGroup
				LocalMigrationPlanTask		is a MigrationPlanTask view
				CompanyRunGroup             is AlphaUpper 15
				AsyncId						is a AsyncActionRequest	
				TempFinanceEnterpriseGroup  is AlphaUpper 4


			Instance Selection
				where ((Company = PrmCompany 
				and    Status = 9
				and    FiscalYear = PrmDtlYear
				and    AcctPeriod = 1
				and    AutoReverse)
				and    ((Activity != blank
				and	    AcactivityRel.PostingFlag = "P"
				or      AcactivityRel.PostingFlag = blank)
				or      Activity = blank))
			
			Sort Order
                Company
                Status
                FiscalYear
                AcctPeriod
                ControlGroup
                System
                JeType
                JeSequence
                LineNbr

			Entrance Rules
				LocalSequenceNumber = 0
				
			Action Rules
				Set Rules
					Entrance Rules
						LocalFinanceEnterpriseGroup = MgcoaffilRel.FinEntGroup
						
					Exit Rules
									
				Company Set Rules
					Exit Rules	
					    LocalRunGroup                = CompanyRunGroup
					    LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
						if (GltransrelRel exists)
							invoke ImportToGL Gltransrel in background
								assign async action request id to AsyncId
								invoked.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
								invoked.PrmRunGroup				= LocalRunGroup		
                    








			
                AcctPeriod Set Rules
                	Entrance Rules
                		LocalCounter 		= 0

                JeSequence Set Rules
                	Entrance Rules
                		LocalCounter		+= 1

                Instance Rules
					LocalSequenceNumber		+= 1
					CompanyRunGroup                        = FixRunGroupDetail
                    TempFinanceEnterpriseGroup             = PrmFinanceEnterpriseGroup
                   	LocalYearDiff 				    	   = (CompanyRel.FiscalYear - FiscalYear)
                   	LocalUsrStatus                         = AcactivityRel.UsrStatus
                    
				   	if (UnitsAmount != 0 or TranAmount != 0 or BaseAmount != 0)
				    	invoke Create Gltransrel  
					    	fill in fields from this instance
						    invoked.RunGroup		= FixRunGroupDetail
					    	invoked.SeqNumber		= LocalSequenceNumber 
					    	invoked.Company         = Company
						    invoked.DocumentNbr 	= ControlGroup
		                   	LocalOldCompany         = ToCompany
		                   	LocalOldAcctUnit        = AcctUnit
		               	    LocalOldAccount         = Account
	                        LocalOldSubAccount      = SubAccount
		                   	invoked.OldCompany      = DerivedOldCompany	
					    	if (SubAccount entered)
						    	invoked.OldAcctNbr	= ConstructedOldAcctNbr
					    	else
						    	invoked.OldAcctNbr	= DerivedOldAccount
        					LocalFinanceEnterpriseGroup2 = TempFinanceEnterpriseGroup
        					LocalGeneralLedgerEvent   = SourceCode
		    	    		if (GeneralLedgerEventRel exists)
			        		    invoked.SourceCode    = SourceCode
				   	        else
				          		invoked.SourceCode    = "TC"
					    	invoked.Date		    = Date 
						    invoked.Reference		= Reference
					    	invoked.System		 	= System 
    						invoked.ProgramCode		= LocalCounter
    						invoked.AutoRev    		= AutoRev
    			           	if (AcactivityRel.PostingFlag = blank)
	    		               	initialize invoked.Activity
		    	               	initialize invoked.AcctCategory 	
			    			else
				  		    	invoked.Activity        = Activity
				    		    invoked.AcctCategory    = AcctCategory
    		               	invoked.PostingDate		= CompanyRel.PerEndDates.PerEndDate[AcctPeriod] - LocalYearDiff as years 
    		               	if (Activity != blank)
                                initialize invoked.ToBaseAmt
                                initialize invoked.ToBaseRate
                               	if (AcactivityRel.PostingFlag = "P")
                                    if (AcactivityRel.BillableFlag = "Y")
                                        invoked.Billed             = 3
         	                	        invoked.RevenueRecognized  = 1
         		                    if (LegacyAcstatusRel.CapitalFlag = "X") 
                                        invoked.Capitalize            = 1 
                                invoked.Baserate               = 1.0    
#endif   
