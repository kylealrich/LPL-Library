InvoiceRoutingCode is a BusinessClass
    owned by apautomation
    prefix is APRC

    Ontology
    	symbolic key is InvoiceRoutingCode

    Persistent Fields
		Description			 				is Alpha 250
		InitialApproverSelectionMethod		is Numeric 1
			default label is "InitialApprovalLevel"
			States
				FirstApprovalLevel									value is 1
				FirstApprovalLevelAuthorizedToApproveInvoiceAmount	value is 2
				SpecificApprovalLevel								value is 3
		FinalApproverSelectionMethod		is Numeric 1
			default label is "FinalApprovalLevel"
			States
				LastApprovalLevel									value is 1
				FirstApprovalLevelAuthorizedToApproveInvoiceAmount	value is 2
		AssignApproversUsingAuthorityCodes	is Boolean
			default label is "AssignApproversUsingRoles"
		InitialApprovalLevel				is an InvoiceRoutingCodeResource
			default label is "Level"
		Active
		Currency				
		UsesTeamMemberAssignment			is Boolean		

	Transient Fields
		TransientInitialApprovalLevel		is like InvoiceRoutingCodeResource		
			default label is "Level"
			derive value from DerivedInitialApprovalLevel

	Derived Fields
		DerivedFirstApproverName is a DerivedField
			type is Alpha 230
			restricted 
			if (InvoiceRoutingCodeResourceRel exists)
				return first InvoiceRoutingCodeResourceRel.Approver.PreferredSimplePresentationName
			else
				return ""

		DerivedFirstApproverEmailAddress is a DerivedField 
			type is EmailAddressField
			holds pii
			restricted 
			if (InvoiceRoutingCodeResourceRel exists)
				return first InvoiceRoutingCodeResourceRel.Approver.EmailAddress
			else
				return ""

		DerivedFirstApproverActor is a DerivedField
			type is Actor
			restricted 
			if (InvoiceRoutingCodeResourceRel exists)
				return first InvoiceRoutingCodeResourceRel.Approver.FinanceResourceActor
			else
				return ""

		DerivedFirstApprover is a DerivedField
			type is Numeric 13
			restricted 
			if (InvoiceRoutingCodeResourceRel exists)
				return first InvoiceRoutingCodeResourceRel.Approver
			else
				return ""

		DerivedLevelCount is a DerivedField
			type is Numeric 4
			restricted
			return instance count of InvoiceRoutingCodeResource set

		CreateMessage is a LabelField
			restricted
			"CreateInvoiceApprovalCode"
		
		FormLabel is a LabelField
			restricted
			"InvoiceApprovalCode:_"

		RepresentativeText is a StringField
			type is Text
			default label is "InvoiceApprovalCode"
			InvoiceRoutingCode " - " Description
		
		DerivedInvoiceRoutingCodeTitle	is a DerivedField
			type is Alpha size up to 132
			default label is "InvoiceApprovalCode"
			return RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return FormLabel + DerivedInvoiceRoutingCodeTitle
			else
				return CreateMessage

		DerivedInitialApprovalLevel is a DerivedField		
			type is like InvoiceRoutingCodeResource
			restricted 
			return InitialApprovalLevel.ApprovalLevel

		DerivedMaxApprovalAmount is a DerivedField			
				type is like InternationalAmount
				restricted
				return last InvoiceRoutingCodeMaxAmountRel.MaxApprovalAmount
	Context Fields

	Conditions
		RecordExists
			restricted
			when (InvoiceRoutingCode exists)
		
		UsingFirstAndLastApprovers
			restricted
			when ((InitialApproverSelectionMethod.FirstApprovalLevel
			or	  InitialApproverSelectionMethod.SpecificApprovalLevel)
			and	  FinalApproverSelectionMethod.LastApprovalLevel)

		ResourcesExist
			restricted
			when (InvoiceRoutingCodeResourceRel exists)

		ActiveResourcesExist		
			restricted
			when (ActiveInvoiceRoutingCodeResourceRel exists)

		InvalidEscalationDays
			restricted
			when (last InvoiceRoutingCodeResourceRel.InvalidEscalationDays)


		ActiveResourceAtEachApprovalLevel
			restricted
			when (instance count of InvoiceRoutingCodeResourceRel = instance count of ActiveInvoiceRoutingCodeResourceRel)

	Relations
		AllInvoiceRoutingCodeResourceRel is a InvoiceRoutingCodeResource set

		InvoiceRoutingCodeResourceRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode

		ActiveInvoiceRoutingCodeResourceRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.ActiveResource
				or     related.ActiveResourceRole)		

		InvoiceRoutingCodeResourceSpecificLevelRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.ApprovalLevel >= InitialApprovalLevel.ApprovalLevel)

		InvoiceRoutingCodeResourceNoMaxAmountRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByAmount
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.MaxApprovalAmount not entered
				or	   related.MaxApprovalAmount = 0)

		InvoiceRoutingCodeGroupLevelRel		
			one-to-many relation to InvoiceRoutingCodeGroupLevel
			Field Mapping uses symbolic key
				related.VendorGroup			= VendorGroup
			Instance Selection
				where (related.InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel	= InvoiceRoutingCode)

		TransientInvoiceRoutingCodeResourceRel		
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where(related.ApprovalLevel = TransientInitialApprovalLevel)

		InvoiceRoutingCodeResourceRoutingTeamMatchRel		
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCode
			Instance Selection
				where (related.RoutingFinanceTeamMatch)
		InvoiceRoutingCodeMaxAmountRel		
					one-to-many relation to InvoiceRoutingCodeResource
					Field Mapping uses ByAmount
						related.VendorGroup			= VendorGroup
						related.InvoiceRoutingCode	= InvoiceRoutingCode
	Field Rules
		Description
			required

		InitialApproverSelectionMethod
			initial value is 1
			default to 1
			if (!InitialApproverSelectionMethod.FirstApprovalLevel
			and !InitialApproverSelectionMethod.SpecificApprovalLevel)
				if (!UsesTeamMemberAssignment)		
					constraint (!InvoiceRoutingCodeResourceNoMaxAmountRel exists)
						"AllInvoiceApprovalLevelsMustHaveAMaximumApprovalAmountEnteredWhenInitialApprovalLevelIsNotFirstApprovalLevelOrSpecificApprovalLevel"

		FinalApproverSelectionMethod
			initial value is 1
			default to 1

		AssignApproversUsingAuthorityCodes
			if (AssignApproversUsingAuthorityCodes changed)
				constraint (!AllInvoiceRoutingCodeResourceRel exists)
					"CannotChangeTheValueOfAssignApproversUsingRolesIfInvoiceApprovalLevelsAreDefined"

		InitialApprovalLevel
			InitialApprovalLevel = TransientInvoiceRoutingCodeResourceRel.InvoiceRoutingCodeResource		
			if (InitialApproverSelectionMethod.SpecificApprovalLevel)
				required

		Active		
			if (Active changed)
				if (Active = true)
					for each InvoiceRoutingCodeGroupLevelRel
						invoke Update each
							invoked.Active	= true
				else
					for each InvoiceRoutingCodeGroupLevelRel
						invoke Update each
							invoked.Active	= false

		UsesTeamMemberAssignment		
			if (UsesTeamMemberAssignment !entered)		
				constraint (!any AllInvoiceRoutingCodeResourceRel.AmountDefinedAtMemberLevel)
					"CannotHave_\Amount_\Defined_\At_\Member_\LevelWhen_\Uses_\Team_\Member_\AssignmentNotFlagged"
			constraint (!AllInvoiceRoutingCodeResourceRel.IndividualOrRoleEntered)
				"InvoiceApprovalLevelsMustUseFinanceTeams"
			constraint (!InitialApproverSelectionMethod.FirstApprovalLevelAuthorizedToApproveInvoiceAmount)
				"InitialApprovalLevelCannotBe_\First_\Approval_\Level_\Authorized_\To_\Approve_\Invoice_\AmountWhenUsingTeamMemberAssignments"
			constraint (!AssignApproversUsingAuthorityCodes)
				"Cannot_\Assign_\Approvers_\Using_\RolesWhenUsingTeamMemberAssignments"
			constraint (!AllInvoiceRoutingCodeResourceRel.OverrideNextApprovalLevel)
				"InvoiceApprovalLevelsCannotBeSetTo_\Skip_\Next_\Approval_\Level"

	Attach Rules
		if (parentcontext.name != "InvoiceRoutingCodeResource"
		and parentcontext.name != "InvoiceRoutingRule")
			constraint (Active)
				"InvoiceApprovalCodeIsInactive"
			
	Actions
		Create is a Create Action
			completion message is "InvoiceApprovalCodeCreated"
		
		Update is an Update Action
			completion message is "InvoiceApprovalCodeUpdated"
			Exit Rules		
				if (old UsesTeamMemberAssignment = "true"
				and UsesTeamMemberAssignment = "false")
					constraint (!InvoiceRoutingCodeResourceRel.AmountDefinedAtMemberLevel)
						"MaximumApprovalAmountCannotBeDefinedAtMemberLevel"
	
		Delete is a Delete Action
			completion message is "InvoiceApprovalCodeDeleted"
