InvoiceRoutingCodeGroupLevel is a BusinessClass
	owned by apautomation
	prefix is APRGL

	Ontology
		symbolic key is InvoiceRoutingCodeGroupLevel

	Patterns
		implements Resequence on CodeGroupLevel
			new sequence field is NewCodeGroupLevel
			set is ByCodeGroupLevel

	Persistent Fields
		CodeGroupLevel					is a DisplayOrder
		Active

	Transient Fields
		NewCodeGroupLevel				is a DisplayOrder

	Conditions
		LastCodeGroupLevel
			restricted
			when (NextInvoiceRoutingCodeGroupLevelRel !exists)

		NextCodeGroupLevelExists
			restricted
			when (NextInvoiceRoutingCodeGroupLevelRel exists)

		ActiveResource
			restricted
			when (ActiveInvoiceRoutingCodeResourceRel exists)

		RoutingFinanceTeamMatch		
			restricted
			when (InvoiceRoutingCodeResourceRoutingTeamMatchRel exists)

		RoutingTeamMemberMissingMaxAmount		
			restricted
			when (InvoiceRoutingCodeResourceMissingMaxAmountRel exists)

	Relations
		CodeGroupLevelRel
			one-to-many relation to InvoiceRoutingCodeGroupLevel
			Field Mapping uses ByCodeGroupLevel 
				related.VendorGroup				= VendorGroup
				related.InvoiceRoutingCodeGroup	= InvoiceRoutingCodeGroup

		NextInvoiceRoutingCodeGroupLevelRel
			one-to-many relation to InvoiceRoutingCodeGroupLevel
			Field Mapping uses ByCodeGroupLevel
				related.VendorGroup				= VendorGroup
				related.InvoiceRoutingCodeGroup	= InvoiceRoutingCodeGroup
			Instance Selection
				where (related.CodeGroupLevel	> CodeGroupLevel
				and    related.Active
				and    related.ActiveResource)

		ActiveInvoiceRoutingCodeResourceRel
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= InvoiceRoutingCodeGroup.VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCodeGroupLevel
			Instance Selection
				where (related.ActiveResource
				or     related.ActiveResourceRole)

		InvoiceRoutingCodeResourceRoutingTeamMatchRel		
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= InvoiceRoutingCodeGroup.VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCodeGroupLevel
			Instance Selection
				where (related.RoutingFinanceTeamMatch)

		InvoiceRoutingCodeResourceMissingMaxAmountRel		
			one-to-many relation to InvoiceRoutingCodeResource
			Field Mapping uses ByApprovalLevel
				related.VendorGroup			= InvoiceRoutingCodeGroup.VendorGroup
				related.InvoiceRoutingCode	= InvoiceRoutingCodeGroupLevel
			Instance Selection
				where (related.RoutingTeamMemberMissingMaxAmount)

	Sets
		ByCodeGroupLevel
			duplicates
			Sort Order
				VendorGroup
				InvoiceRoutingCodeGroup
				CodeGroupLevel

	Field Rules
		CodeGroupLevel
			initial value is last CodeGroupLevelRel.CodeGroupLevel + 1

	Attach Rules
		constraint (Active)
			"ApprovalCodeGroupLevel<InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel>IsInactive"

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
			Action Rules
				if (first CodeGroupLevelRel.CodeGroupLevel = CodeGroupLevel)
					invoke UpdateCodeGroupLevel last CodeGroupLevelRel
						invoked.NewCodeGroupLevel = last CodeGroupLevelRel.CodeGroupLevel - 1
				else
					invoke UpdateCodeGroupLevel first CodeGroupLevelRel
						invoked.NewCodeGroupLevel = 1

		UpdateCodeGroupLevel is an Update Action
			restricted

