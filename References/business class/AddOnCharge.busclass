AddOnCharge is a BusinessClass
	owned by po
	prefix is PAM
	classic name is POAOCMAST

	Ontology
		symbolic key is AddOnCharge
			classic set name is PAMSET1
			classic name is AOC-CODE

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		Description
		AddOnChargePercent
			classic name is AOC-RATE
		SpreadMethod
		AddOnChargeType
		Taxable                      is Boolean
			classic name is TAXABLE-FLAG
		TaxCode


		Active                       is Boolean
			classic name is ACTIVE-FLAG


		PrintOnPO                    is Boolean
			classic name is AOC-ON-PO
		Summarize                    is Boolean
			classic name is SUMMARY-FLAG

		LandedAddOnCharge            is Boolean
			classic name is LANDED-FLAG
		TrackType
		PostingAccount               is a FinanceCodeBlockFull


		AddOnChargeDifferenceAccount is a FinanceCodeBlockFull


		CommodityCode				 is a CommCodes
		UNSPSCCode
		HSNSACCode 
		OneSourceTaxCallOverride            is Numeric 1
		    States
		        UseCommodityCodeFromLargestPOLine	value is 1
				UseCommodityCode 					value is 2
				UseUNSPSCCode 						value is 3
		        
	Context Fields
		PurchaseOrder
		ContractLine
		PayablesInvoice
		AddOnChargeOrigin

	Local Fields
		LocalCalculateTax			is a CalculateTax
		LocalCalculateTaxExecuted	is AlphaUpper 1
		LocalGeneralLedgerSystemCode is a GeneralLedgerSystemCode
		LocalPostingDate			is a PostingDate
		LocalTransactionDate		is a ExchangeDate


	Transient Fields
		TransientAccountingEntity is an AccountingEntity
			derive value from Company.AccountingEntity

	Derived Fields
		DerivedTotalAOCAmount is a DerivedField
			type is like InternationalAmount
			return (sum ContextPurchaseOrderAndLineAddOnChargesAndForPrintingRel.TotalAddOnChargeAmount)
			
		DerivedAOCIDName is a MessageField
			default label is "AddOnCharge"
			"<AddOnCharge>_-_<Description>"

		DerivedCommodityCodeNoDash is a StringField
			type is like CommCodes
			CommodityCode.CommodityCodeRel.CommodityCode.Segment[1]
			CommodityCode.CommodityCodeRel.CommodityCode.Segment[2]
			CommodityCode.CommodityCodeRel.CommodityCode.Segment[3]
			CommodityCode.CommodityCodeRel.CommodityCode.Segment[4]
			CommodityCode.CommodityCodeRel.CommodityCode.Segment[5]

	Conditions
		IsActive
			restricted
			classic name is ACTIVE-AOC
			when (Active)

		IsCostAddOnCharge
			restricted
			when (AddOnChargeType.Cost)

		CanUseForEventHeader
			restricted
			when (!SpreadMethod.NoSpread
			or    !LandedAddOnCharge)

		IsLineDetailsExist
			restricted
			when (PurchaseOrderAndLineAddOnChargesRel exists
			or 	  PayablesInvoiceAddOnChargesRel exists)

		PurchaseOrderExists
			restricted
			when (ContextPurchaseOrderAndLineAddOnChargesRel exists)

		PurchaseOrderExistsAndForPrinting
			restricted
			when (ContextPurchaseOrderAndLineAddOnChargesAndForPrintingRel exists)

		OneSourceGeneric
			restricted
			when  (TaxEntityRel.ThirdParty.Sabrix
			and  TaxConfigurationRel.OneSourceVersionType.Generic)

		BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "PurchaseOrderLineDistributionDetail"
			or 	  parentcontext.name = "PurchaseOrderEDIOutput")
			
	Relations

		PurchaseOrderAndLineAddOnChargesRel
			one-to-many relation to PurchaseOrderAndLineAddOnCharge
			Field Mapping uses Set2
				related.Company		=	Company
				related.AddOnCharge	=	AddOnCharge

		ContextPurchaseOrderAndLineAddOnChargesRel
			one-to-many relation to PurchaseOrderAndLineAddOnCharge
			Field Mapping uses Set2
				related.Company				=	Company
				related.AddOnCharge			=	AddOnCharge
			Instance Selection
				where (related.PurchaseOrder =	PurchaseOrder)

		ContextPurchaseOrderAndLineAddOnChargesAndForPrintingRel
			one-to-many relation to PurchaseOrderAndLineAddOnCharge
			Field Mapping uses Set2
				related.Company				=	Company
				related.AddOnCharge			=	AddOnCharge
			Instance Selection
				where (related.PurchaseOrder =	PurchaseOrder
				and	   related.Vendor		 =	PurchaseOrder.Vendor
				and	   related.PrintOnPO	 =	true
				and   (related.PurchaseOrderLine not entered or not related.PurchaseOrderLine.LineFullyCancelled))

		PayablesInvoiceAddOnChargesRel
			one-to-many relation to PayablesInvoiceAddOnCharge
			Field Mapping uses symbolic key
				related.Company				= 	Company
			Instance Selection
				where (related.AddOnCharge	=	AddOnCharge)


		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup        = GeneralLedgerCompanyRel.FinanceEnterpriseGroup	
				related.GeneralLedgerSystemCode       = "IC"

		TaxConfigurationRel						
			one-to-one relation to TaxConfiguration
			Field Mapping uses symbolic key
				related.TaxConfiguration		= GeneralLedgerCompanyRel.FinanceEnterpriseGroup

	   	TaxEntityRel
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.TaxEntity 				= Company.AccountingEntity

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

	Sets

		Set3
			indexed
			Sort Order
				AddOnCharge
				Company

	Field Rules

		Description
			required

		AddOnChargePercent
			constraint (SpreadMethod.Rate)
				"SpreadMethodMustBeRate"						// "@e.MA08.110"

		SpreadMethod
			initial value is SpreadMethod.NoSpread
			default to SpreadMethod.NoSpread

		AddOnChargeType
			initial value is AddOnChargeType.Cost
			default to AddOnChargeType.Cost

			cannot be changed






		TaxCode
			if (Taxable)
				required
					"TaxCodeRequiredIfTaxableIsSet"						//"@e.MA08.124"
			else
				cannot be entered
					"TaxCodeCannotBeEntered"
			if (not OneSourceGeneric
			and !TaxEntityRel.ThirdParty.VertexOSeries)
				initialize LocalCalculateTax
				LocalCalculateTax.TaxEntity 		= Company.AccountingEntity	
				LocalCalculateTax.TaxCode			= TaxCode
				LocalCalculateTax.Function       	= "I"



				LocalCalculateTaxExecuted			= LocalCalculateTax.ExecuteCalculateTax
				constraint (LocalCalculateTax.OutputErrorNumber not entered)
					"<LocalCalculateTax.OutputErrorMessage>"

			if (!LandedAddOnCharge)
				constraint (!LocalCalculateTax.TaxTable.TaxTableOutput.LandCostFlag)
					"CannotHaveLandedTaxOnNonLandedAddOnCharge"								//"@e.MA08.122"

		Active
			initial value is true
			default to true

		PrintOnPO
			initial value is true
			default to true

		TrackType
			initial value is TrackType.OtherAOC
			default to TrackType.OtherAOC

			if (IsLineDetailsExist)
				cannot be changed
					"CannotChangeTrackingType;PurchaseOrderOrPayablesInvoiceLineRecordsExist"						//"@e.MA08.107"

		PostingAccount
			if (!LandedAddOnCharge)
				required
					"PostingAccountIsRequiredForNon-landedAddOnCharge"														// "@e.MA08.123"

		AddOnChargeDifferenceAccount
			if (!SpreadMethod.NoSpread
			and !LandedAddOnCharge)
				required
					"AccountInformationRequiredWithThisSpreadMethod"			// "@e.MA08.112"

		OneSourceTaxCallOverride
			if (!OneSourceTaxCallOverride.UseCommodityCode)
				initialize CommodityCode
			else
				constraint (CommodityCode entered)
					"CommodityCodeRequired"

			if (!OneSourceTaxCallOverride.UseUNSPSCCode)
				initialize UNSPSCCode
			else
				constraint (UNSPSCCode entered)
					"UNSPSCCodeCodeRequired"
	Attach Rules
		if (not BypassRuleForTheseFiles)
			if (PayablesInvoice entered) 
				if (AddOnChargeOrigin not in context)
					constraint (Active)
						"AddOnChargeCode<AddOnCharge>MustBeActive"
			else 
				if (AddOnChargeOrigin not entered)
					constraint (Active)
						"AddOnChargeCode<AddOnCharge>MustBeActive"

	Actions
		Create is a Create Action
			Action Rules
				if (GeneralLedgerSystemCodeRel exists)
					LocalGeneralLedgerSystemCode = GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode
				if (GeneralLedgerCompanyRel.FinanceEnterpriseGroup.ProjectDateEdit.PostingDate)
					LocalPostingDate = current corporate date
				if (GeneralLedgerCompanyRel.FinanceEnterpriseGroup.ProjectDateEdit.TransactionDate)
					LocalTransactionDate = current corporate date


		Update is an Update Action
			Action Rules
				if (GeneralLedgerSystemCodeRel exists)
					LocalGeneralLedgerSystemCode = GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode
				if (GeneralLedgerCompanyRel.FinanceEnterpriseGroup.ProjectDateEdit.PostingDate)
					LocalPostingDate = current corporate date
				if (GeneralLedgerCompanyRel.FinanceEnterpriseGroup.ProjectDateEdit.TransactionDate)
					LocalTransactionDate = current corporate date

		Delete is a Delete Action

		AttachToContract is an Instance Action
			valid when (ContractLine entered)
			Parameters
				ContractGroup
				Contract
				ContractLine

			Action Rules

				invoke Create ContractLineAOC
					invoked.ContractGroup   = ContractGroup
					invoked.Contract        = Contract
					invoked.ContractLine    = ContractLine
					invoked.Company         = Company
					invoked.AOC             = AddOnCharge
					invoked.AOCRate         = AddOnChargePercent
					invoked.Taxable         = Taxable
					invoked.TaxCode         = TaxCode
