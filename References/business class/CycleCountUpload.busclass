CycleCountUpload is a BusinessClass
    prefix is MSCCU

    Ontology
        symbolic key is CycleCountUpload

    Persistent Fields
        MobileSupplyChainUpload
        SelectId                is like PhysicalInventorySelect
        CountedBy               is an Actor
        TimeStarted             is TimeStamp
        TimeSubmitted           is TimeStamp
        Status                  is Numeric size 1
            States
                InProgress          value is 1
                ProcessedWithError  value is 8
                Completed           value is 9
        ErrorStatus     is Numeric size 2
            States
                NoError                     value is 0
                FreezeRecordsDeleted        value is 1
                SelectIDUpdated             value is 2
                SelectIDVarianceInProgress  value is 3
                SelectIDNotExisting         value is 4
    
    Derived Fields
        DerivedDuration is a DerivedField
            type is Numeric 6
            return TimeSubmitted - TimeStarted
        
        DerivedDurationName	is a MessageField
			default label is "Duration"
			"<DerivedDuration>_secs"
        
        DerivedLinesProcessed is a DerivedField
            type is Numeric 6
            return instance count of CycleCountLineUploadRel
        
        DerivedErrorMessage is a DerivedField
            type is MessageField
            if(ErrorStatus.FreezeRecordsDeleted )
                return DerivedFreezeRecordsDeletedMessage
            else
            if(ErrorStatus.SelectIDUpdated)
                return DerivedSelectIDUpdatedMessage
            else
            if(ErrorStatus.SelectIDVarianceInProgress)
                return DerivedSelectIDVarianceInProgressMessage
            else 
            if (ErrorStatus.SelectIDNotExisting)
                return DerivedSelectIDNotExistingMessage
        
        DerivedFreezeRecordsDeletedMessage is a MessageField
            restricted
            "FreezeRecordsAlreadyDeleted"
        
        DerivedSelectIDUpdatedMessage is a MessageField
            restricted
            "SelectIDISBeingModified"

        DerivedSelectIDVarianceInProgressMessage is a MessageField
            restricted
            "SelectIDWithVarianceInProgress"

        DerivedSelectIDNotExistingMessage is a MessageField
            restricted
            "SelectIDNotExisting"
    
    Relations
        CycleCountLineUploadRel
            one-to-many relation to CycleCountLineUpload
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.CycleCountUpload        = CycleCountUpload
        
        PhysicalInventorySelectRel
			one-to-one relation to PhysicalInventorySelect
			Field Mapping uses symbolic key
				related.Company                     = Company
				related.PhysicalInventorySelect		= CycleCountUpload.SelectId
    
    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted
        
        ValidatePhysicalSelect is an Instance Action
            restricted
            Action Rules
                if(PhysicalInventorySelectRel exists)
                    if(PhysicalInventorySelectRel.Status.SelectIDNotInUse)
                        ErrorStatus     = ErrorStatus.FreezeRecordsDeleted
                    else
                    if(PhysicalInventorySelectRel.Status.SelectIDUpdated)
                        ErrorStatus     = ErrorStatus.SelectIDUpdated
                    else
                    if(PhysicalInventorySelectRel.Status.VariancesInProgress)
                        ErrorStatus     = ErrorStatus.SelectIDVarianceInProgress
                else
                    ErrorStatus     = ErrorStatus.SelectIDNotExisting
        
        TransitionToComplete is an Instance Action
            restricted
            Action Rules
                Status  = Status.Completed
        
        TransitionToError is an Instance Action
            restricted
            Action Rules
                Status  = Status.ProcessedWithError
    Sets
        ByTimeSubmitted
            Sort Order
                TimeSubmitted descending
                Company
                CycleCountUpload
