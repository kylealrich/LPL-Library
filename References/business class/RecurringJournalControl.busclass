RecurringJournalControl is a BusinessClass
    owned by GeneralLedger
    prefix is GLRJC

    Ontology
    	symbolic key is RecurringJournalControl

    Patterns

    Persistent Fields
		JournalType						is Numeric 1
			States
				Normal			value is 0
				InterEntity		value is 1
        Status            				is Numeric 1
            States
                Unreleased			value is 0
                Released			value is 1
                Deleted				value is 6
        Description						is Alpha up to 60
        	translatable
		LastProcessedPeriod				is a GeneralLedgerClosePeriod
		BeginningPeriod					is a GeneralLedgerClosePeriod
		EndPeriod						is a GeneralLedgerClosePeriod
		TotalDebit						is an InternationalAmount
		TotalCredit						is an InternationalAmount
		UnitDebit						is a UnitsAmount
		UnitCredit						is a UnitsAmount
		AutoDebit						is an InternationalAmount
		AutoCredit						is an InternationalAmount
		AutoUnitDebit					is a UnitsAmount
		AutoUnitCredit					is a UnitsAmount
		PostingDay						is Numeric 2
		BaseZoneAccountingUnit			is an AccountingUnit

		FunctionalDebitAmount			is an InternationalAmount				
			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
		FunctionalCreditAmount			is an InternationalAmount				
			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
		AlternateDebitAmount			is an InternationalAmount				
			precision is AccountingEntity.AlternateCurrency.NumberOfDecimals
		AlternateCreditAmount			is an InternationalAmount				
			precision is AccountingEntity.AlternateCurrency.NumberOfDecimals
		Alternate2DebitAmount			is an InternationalAmount				
			precision is AccountingEntity.AlternateCurrency2.NumberOfDecimals
		Alternate2CreditAmount			is an InternationalAmount				
			precision is AccountingEntity.AlternateCurrency2.NumberOfDecimals
		Alternate3DebitAmount			is an InternationalAmount				
			precision is AccountingEntity.AlternateCurrency3.NumberOfDecimals
		Alternate3CreditAmount			is an InternationalAmount				
        	precision is AccountingEntity.AlternateCurrency3.NumberOfDecimals
        Rpt1DebitAmount					is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyOne.NumberOfDecimals
        Rpt1CreditAmount				is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyOne.NumberOfDecimals
        Rpt2DebitAmount					is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyTwo.NumberOfDecimals
        Rpt2CreditAmount				is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyTwo.NumberOfDecimals
        Rpt3DebitAmount					is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyThree.NumberOfDecimals
        Rpt3CreditAmount				is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyThree.NumberOfDecimals
        Rpt4DebitAmount					is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyFour.NumberOfDecimals
        Rpt4CreditAmount				is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyFour.NumberOfDecimals
        Rpt5DebitAmount					is an InternationalAmount
        	precision is FinanceEnterpriseGroup.ReportCurrencyFive.NumberOfDecimals
        Rpt5CreditAmount				is an InternationalAmount
			precision is FinanceEnterpriseGroup.ReportCurrencyFive.NumberOfDecimals

		Currency
		GeneralLedgerEvent
			default label is "GlobalLedgerEvent"
        AutoReverse			        	is Boolean
        Reference
		DocumentNumber
		DescriptionDefault				is Numeric 1
			States
				JournalHeader			value is 0
				PriorTransactionLine 	value is 1
		TransactionDay					is Numeric 2
    	System							is a GeneralLedgerSystemCode


        BaseZoneDimension1				is a FinanceDimension1
		Ledger
		ProcessingJournal				is Boolean

		TransactionOrigin				is BusinessObjectReference
		AutoZero						is Boolean
#ifdef module sharedfinance
		ApprovalCode
		ApprovalLevel					is Numeric 8
		ReassignToApprovalLevel			is an ApprovalCodeResource
		Approver						is a FinanceResource
		ApproverTeam					is a FinanceTeamField
#endif
#ifndef module sharedfinance
		ApprovalCode					is AlphaUpper 30
		ApprovalLevel					is Numeric 8
		ReassignToApprovalLevel			is Numeric 8
		Approver						is Numeric 13
		ApproverTeam					is a GlonlyFinanceTeamField
#endif
		ApprovalPeriod					is a GeneralLedgerClosePeriod
		PendingBudgetEdit				is Boolean
		CopyComments					is Boolean

	Local Fields
		LocalClosePeriod				is a GeneralLedgerClosePeriod
#ifdef module sharedfinance
		LocalApprover				   	is a FinanceResource  
		LocalApproverTeam			   	is a FinanceTeamField  
#endif
#ifndef module sharedfinance
		LocalApprover				   	is Numeric 13
		LocalApproverTeam			   	is AlphaUpper 20		
#endif
		LocalApprovalLevel			   	is Numeric 8
		LocalApproverList			   	is Alpha 250
		LocalFirstApproverAssigned	  	is Boolean
    	LocalSystemCode					is a GeneralLedgerSystemCode
		AsyncId							is a AsyncActionRequest

	Transient Fields
		StatusDisplay					is Alpha 20
			derive value from DerivedStatusDisplay
  			States
				PendingApproval		value is "PendingApproval"
				Processed			value is "Processed"
				Ready				value is "Ready"
    	OriginatingSystem				is like GeneralLedgerSystemCode
			derive value from DerivedOriginatingSystem
			States
				AP					value is "AP"
				GL					value is "GL"
				RJ					value is "RJ"
			
		
	Rule Blocks
		InitiateJournalFlow
			trigger "RecurringJournalApproval" PA service
				resume on error
				title is "<JournalApprovalTitle>"
				Variables
					include persistent fields from RecurringJournalControl
				URLs
					"<linkback(webapp is JournalApprover navigation is RecurringJournalLinkbackNav text is \"ViewJournal\")>"


		SendRejectNotificationToUser
			send notification
				to	create stamp.actor
				description is	"<JournalRejectedMessage>"
				priority is very high

				linkback(webapp is JournalApprover navigation is RecurringJournalLinkbackNav text is "RecurringJournalRejected")				
												
		SendManualApproveNotificationToUser
			send notification
				to	create stamp.actor
				description is	"<JournalApprovedMessage>"
				priority is very high

				linkback(webapp is JournalApprover navigation is RecurringJournalLinkbackNav text is "RecurringJournalApproved")				

		PeriodsForYearRange
			for each EntityLedgerPeriodRangeRel
				LocalClosePeriod		= each.GeneralLedgerClosePeriod
				if (!RecurringJournalScheduleRel exist)
					invoke Create RecurringJournalSchedule
						invoked.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
						invoked.AccountingEntity				= AccountingEntity
						invoked.RecurringJournalControl			= RecurringJournalControl
						invoked.GeneralLedgerClosePeriod		= each.GeneralLedgerClosePeriod
						invoked.PeriodSelected					= true

#ifdef module sharedfinance
		BuildTeamApproverActorList
			LocalApproverList = ""
			LocalFirstApproverAssigned = false
			for each FinanceTeamMembersFromCurrentApprovalLevelRel
				if (LocalFirstApproverAssigned)
					LocalApproverList = LocalApproverList + "," + each.FinanceTeamMember.TeamMember.FinanceResourceActor
				else
					LocalApproverList = each.FinanceTeamMember.TeamMember.FinanceResourceActor
					LocalFirstApproverAssigned = true

		GetNextEscalationApprovalLevel
			LocalApprovalLevel		= ApprovalLevel
			if (first LocalApprovalCodeLevelRel.EscalateTo.NextApprovalLevel)
				LocalApprovalLevel	= ApprovalLevel + 1
				LocalApprover		= first LocalApprovalCodeLevelRel.Approver
				LocalApproverTeam	= first LocalApprovalCodeLevelRel.ApprovalTeam
			else
				LocalApprovalLevel	= first LocalApprovalCodeLevelRel.EscalationApprovalLevel.ApprovalLevel
				LocalApprover		= first LocalApprovalCodeLevelRel.Approver
				LocalApproverTeam	= first LocalApprovalCodeLevelRel.ApprovalTeam

		GetNextApprovalLevel
			if (ApprovalLevel < 1)
				LocalApprovalLevel	= first ApprovalCodeResourceRel.ApprovalLevel
				LocalApprover		= first ApprovalCodeResourceRel.Approver
				LocalApproverTeam	= first ApprovalCodeResourceRel.ApprovalTeam
			else
				LocalApprovalLevel		= ApprovalLevel + 1
				if (LocalApprovalCodeLevelRel exists)
					LocalApprovalLevel	= first LocalApprovalCodeLevelRel.ApprovalLevel
					LocalApprover		= first LocalApprovalCodeLevelRel.Approver
					LocalApproverTeam	= first LocalApprovalCodeLevelRel.ApprovalTeam
				else
					initialize LocalApprovalLevel
					initialize LocalApprover
					initialize LocalApproverTeam
#endif					








	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 50
			restricted
			"InforERPEnterpriseFinancialsRecurringJournal"
			
		ContextMessageText is a StringField
			type is Alpha 30
			restricted
			"RecurringJournalControl<RecurringJournalControl>"
		
		ProcesStatusMsg 				is a StringField
			type is Alpha 16
			"Process Status:"
		
		DeletedMessage					is a MessageField
			restricted
			"Deleted"
		NoSelectMessage					is a MessageField
			restricted
			"NoSelect"
		OutOfRangeMessage				is a MessageField
			restricted
			"OutOfRange"
		ProcessedMessage				is a MessageField
			restricted
			"Processed"
		ReadyMessage					is a MessageField
			restricted
			"Ready"
		ApprovalMessage				is a MessageField
			restricted
			"Pending\Approval"		
		DerivedStatusDisplay			is a ConditionalField
			type is Alpha 20
			if (Status.Deleted)
				DeletedMessage
			else
			if (!RecurringJournalSchedule set exists)
				NoSelectMessage
			else
			if (AccountingEntity.CurrentPeriod < BeginningPeriod)
				OutOfRangeMessage
			else
			if (AccountingEntity.CurrentPeriod > EndPeriod)
				OutOfRangeMessage
			else
			if (ApprovalPeriod entered)
				ApprovalMessage
			else
			if (AccountingEntity.CurrentPeriod = LastProcessedPeriod)
				ProcessedMessage
			else
				ReadyMessage
		DerivedEntryAmount				is a DerivedField
			type is like InternationalAmount
			restricted
			return (TotalDebit + TotalCredit)			
		DerivedEntryUnit				is a DerivedField
			type is like InternationalAmount
			restricted
			return (UnitDebit + UnitCredit)			
		DerivedAutoAmount				is a DerivedField
			type is like InternationalAmount
			restricted
			return (AutoDebit + AutoCredit)			
		DerivedOriginatingSystem	is a DerivedField
			type is like GeneralLedgerSystemCode
			restricted
			if (TransactionOrigin.BusinessClassName		= "RecurringJournalControl")
				return "RJ"
			else
				if (TransactionOrigin.BusinessClassName		= "GeneralLedgerTransaction")
					return "GL"
				else
					if (TransactionOrigin.BusinessClassName		= "PayablesInvoiceDistribution")
						return "AP"
		CardViewEntityLabel is a StringField
			type is Alpha 80 
			FinanceEnterpriseGroup.AccountingEntityLabel
			":"
			AccountingEntity
		CardViewJournalControlLabel is a StringField
			type is Alpha 20 
			RecurringJournalControl
			" - "
			Description
		CardViewJournalStatusLabel is a StringField
			type is Alpha 80 
			"System: "
			OriginatingSystem
			"    "
			"Process Status: "
			DerivedStatusDisplay
			"    "
			"Journal Status: "
			DerivedJournalStatus
		UnreleasedMessage 			is a MessageField
			restricted
			"Unreleased"
		ReleasedMessage 			is a MessageField
			restricted
			"Released"
		BudgetMessage	 			is a MessageField
			restricted
			"BudgetError"
		ReleaseWithBudgetEditsMessage is a MessageField
			restricted
			"JournalWillBeReleasedIfBudgetEditsArePassed"
		ReleaseNoEditsMessage 		is a MessageField
			restricted
			"ReleaseComplete"
		ProcessedPeriodMsg 			is a MessageField
			"Process_Period:"
		ReleaseMessage 				is a DerivedField
			type is Text
			restricted
			if (LocalSystemCode.EncumbranceOption.TrackAndEdit)
   				return ReleaseWithBudgetEditsMessage
   			else
   				return ReleaseNoEditsMessage
		DerivedJournalStatus		is a ConditionalField
			type is Alpha 20
			if (Status.Deleted)
				DeletedMessage
			else
			if (BudgetErrorsExist)
				BudgetMessage
			else
			if (Status.Unreleased)
				UnreleasedMessage
			else
				ReleasedMessage
		RecurringJournalControlLinkBack 	is a MessageField
        	restricted
			"<linkback(webapp is JournalApprover navigation is RecurringJournalLinkbackNav text is \"ViewJournal\")>"
		JournalApprovalTitle		is a StringField
			type is Alpha 150
			restricted
			FinanceEnterpriseGroup.AccountingEntityLabel
			"-"
			AccountingEntity
			":RecurringJournal-"
			RecurringJournalControl
		JournalMessage				is a MessageField
			restricted
			"RecurringJournal"
		RejectedMessage 			is a MessageField
			restricted
			"Rejected"
		ApprovedMessage 			is a MessageField
			restricted
			"Approved"
		JournalRejectedMessage		is a StringField
			type is Alpha 150
			restricted
			JournalMessage
			" "
			RecurringJournalControl
			" "
			RejectedMessage
		JournalApprovedMessage		is a StringField
			type is Alpha 150
			restricted
			JournalMessage
			" "
			RecurringJournalControl
			" "
			ApprovedMessage
		DerivedCurrentApprovalResource is a DerivedField
			type is Numeric 9
			restricted
#ifdef module sharedfinance
			return first CurrentApprovalCodeLevelRel.Approver
#endif
#ifndef module sharedfinance
			return 0
#endif
		DerivedCurrentApprovalActor is a DerivedField
			type is Actor
			restricted
#ifdef module sharedfinance
			return first CurrentApprovalCodeLevelRel.Approver.FinanceResourceActor
#endif
#ifndef module sharedfinance
			return blank
#endif
		DerivedCurrentApprovalTeam is a DerivedField
			type is AlphaUpper 20
			restricted
#ifdef module sharedfinance
			return first CurrentApprovalCodeLevelRel.ApprovalTeam
#endif
#ifndef module sharedfinance
			return blank
#endif
		DerivedCurrentTeamActorList is a DerivedField
			type is Alpha 250
			restricted
#ifdef module sharedfinance
			include BuildTeamApproverActorList
			return LocalApproverList
#endif
#ifndef module sharedfinance
			return blank
#endif
		DerivedCurrentApproverEscalationDays is a DerivedField
			type is Numeric 6
			restricted
#ifdef module sharedfinance
			if (first CurrentApprovalCodeLevelRel.EscalationDays > 0)
				return first CurrentApprovalCodeLevelRel.EscalationDays	
			else
				return 10000
#endif
#ifndef module sharedfinance
			return 0
#endif
		DerivedCurrentApproverEscalationHours is a DerivedField
			type is Decimal 6.2
			restricted
#ifdef module sharedfinance
			if (first CurrentApprovalCodeLevelRel.EscalationHours > 0)
				return first CurrentApprovalCodeLevelRel.EscalationHours
			else
				return 9999.99
#endif
#ifndef module sharedfinance
			return 0
#endif
		NoAmountsOrUnitsErrorMessage 	is a MessageField
			restricted
			"CannotRelease;NoTransactionExistWithAmountsOrUnits"
		DebitCreditErrorMessage 		is a MessageField
			restricted
			"CannotRelease;DebitsDoNotEqualCredits"
		AutoDebitCreditErrorMessage 	is a MessageField
			restricted
			"CannotRelease;AutoDebitsDoNotEqualAutoCredits"
		DerivedJournalizeEditMessage		is a DerivedField				
			type is Alpha 150
			if (JournalWithoutAmountOrUnit)
				return NoAmountsOrUnitsErrorMessage
			if (DerivedEntryAmount entered)
				return DebitCreditErrorMessage
			if (DerivedAutoAmount entered)
				return AutoDebitCreditErrorMessage
		DerivedOrigDocumentReleased	is a DerivedField
			type is Boolean
			restricted
			if (TransactionOrigin.BusinessClassName		= "GeneralLedgerTransaction"
			and TransactionOrigin(GeneralLedgerTransaction).Status.Posted)
				return true
#ifdef module ap				
			if  (TransactionOrigin.BusinessClassName		= "PayablesInvoiceDistribution"
			and !TransactionOrigin(PayablesInvoiceDistribution).Status.Unreleased)
				return true
#endif

		DerivedResponsibilityMatrixRelease is a DerivedField
			type is Boolean
			restricted
			if (!ResponsibilityMatrixPendingApprovalRel exists
			and !ResponsibilityMatrixRejectedRel exists
			and Status.Released)
				return true
			else
				return false

	Relations
		DefaultZoneAccountingUnitRel
			one-to-one relation to AccountingUnitHierarchy
			Field Mapping uses ByDefaultZoneAccountingUnitInStructure
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.AccountingUnitStructure				= FinanceEnterpriseGroup.EnterpriseAUStructure
				related.AccountingEntity					= AccountingEntity
		DefaultZoneFinanceDimension1Rel
			one-to-one relation to FinanceDimension1Hierarchy
			Field Mapping uses ByDefaultZoneDimensionInStructure
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FinanceDimension1Structure	= FinanceEnterpriseGroup.EnterpriseDim1Structure
		EntityLedgerPeriodRangeRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses ByEntityLedger
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.Ledger						= Ledger
			Instance Selection
				where (related.GeneralLedgerClosePeriod 	>= BeginningPeriod
				and    related.GeneralLedgerClosePeriod 	<= EndPeriod)
		RecurringJournalScheduleRel
			one-to-one relation to RecurringJournalSchedule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 								 = FinanceEnterpriseGroup
				related.AccountingEntity									 = AccountingEntity
				related.RecurringJournalControl 							 = RecurringJournalControl
				related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = LocalClosePeriod
		ProcessRecurringJournalScheduleRel is a RecurringJournalSchedule set
		CodeBlockRelationRel
			one-to-one relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.FromField				= 0
				related.ToField					= 1
		CodeBlockRelationDetailRel
			one-to-one relation to CodeBlockRelationDetail
			Field Mapping uses ByKeyValues
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.CodeBlockRelation		= CodeBlockRelationRel.CodeBlockRelation
				related.KeyFromValue			= Ledger
				related.KeyToValue				= AccountingEntity
		EntityLedgerCurrentPeriodRel
			one-to-one relation to EntityLedgerPeriod
			Field Mapping uses ByEntityLedger
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.Ledger						= Ledger
				related.GeneralLedgerClosePeriod	= BeginningPeriod
#ifdef module sharedfinance
		CurrentApprovalCodeLevelRel
			one-to-many relation to ApprovalCodeResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ApprovalCode			= ApprovalCode
				related.ApprovalLevel			= ApprovalLevel
		ApprovalCodeResourceRel
			one-to-many relation to ApprovalCodeResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ApprovalCode			= ApprovalCode
			Instance Selection
				where (!related.Approver.FinanceResourceActor = update stamp.actor
				and    !related.Approver.FinanceResourceActor = create stamp.actor)
		LocalApprovalCodeLevelRel
			one-to-many relation to ApprovalCodeResource
			Field Mapping uses ByApprovalLevel
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ApprovalCode			= ApprovalCode
				related.ApprovalLevel			= LocalApprovalLevel
			Instance Selection
				where (!related.Approver.FinanceResourceActor = update stamp.actor
				and    !related.Approver.FinanceResourceActor = create stamp.actor)
		FinanceTeamMembersFromCurrentApprovalLevelRel
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.FinanceTeam				= first CurrentApprovalCodeLevelRel.ApprovalTeam
			Instance Selection
				where (!related.FinanceTeamMember.TeamMember.FinanceResourceActor = update stamp.actor  
				and    !related.FinanceTeamMember.TeamMember.FinanceResourceActor = create stamp.actor) 
		ApprovalCodeResourceByResourceRel
			one-to-one relation to ApprovalCodeResource
			Field Mapping uses ByApprover
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ApprovalCode			= ApprovalCode
				related.Approver				= LocalApprover
#endif
		RecurringJournalStatisticalRel
			one-to-many relation to RecurringJournalTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntity		= AccountingEntity
				related.RecurringJournalControl = RecurringJournalControl
			Instance Selection
				where (related.IsStatisticalAccount
				and	  ((!related.UnitsAmount entered)
				or	   (related.TransactionAmount entered)))
		RecurringJournalEncumbranceRel
			one-to-many relation to RecurringJournalEncumbrance
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.AccountingEntity				= AccountingEntity
				related.RecurringJournalControl 		= RecurringJournalControl
		BudgetEditErrorRel
			one-to-many relation to BudgetEditError
			Field Mapping uses ByBudgetGroup
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.BudgetEditError.BudgetEditGroup		= UniqueID
		RecurringJournalCommentRel is a RecurringJournalComment set
		RecurringJournalDocumentRel is a RecurringJournalDocument set
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup	= actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity		= AccountingEntity
#ifdef module cb			
		TransactionsRequiringFundCashEditRel is a RecurringJournalTransaction set
			Instance Selection
				where (related.FinanceCodeBlock.GeneralLedgerChartAccount.AccountSubType	= "CASH")

		CashCodeFundEditRel
			one-to-many relation to CashCodeFundEdit
			Field Mapping uses ByOriginatingTransaction
		 		related.CashManagementGroup		= FinanceEnterpriseGroup
		  		related.OriginatingTransaction	= reference to this instance







#endif

		ResponsibilityMatrixRejectedRel is a RecurringJournalTransaction set
			Instance Selection
				where (related.ResponsibilityMatrixApprovalProcessorRel.Status.Rejected)

		ResponsibilityMatrixPendingApprovalRel is a RecurringJournalTransaction set
			Instance Selection
				where (related.ResponsibilityMatrixApprovalProcessorRel.Status.PendingApproval)

	Conditions
		HasComments
			restricted
			when (RecurringJournalCommentRel exists)
		HasDocuments
			restricted
			when (RecurringJournalDocumentRel exists)
		RecurringJournalControlExists
			restricted
			when (RecurringJournalControl entered)
		JournalWithoutAmountOrUnit
			restricted
			when (!TotalDebit entered
			and   !TotalCredit entered
			and   !UnitDebit entered
			and   !UnitCredit entered)
		IsValidToRelease
			restricted
			when (Status.Unreleased
			and  !RecuringBridgeExists
			and  !PendingBudgetEdit)
		IsValidForInterfaceJournal
			restricted
			when (Status.Unreleased
			and   RecuringBridgeExists
			and   DerivedOrigDocumentReleased
			and  !PendingBudgetEdit)
		IsValidToUnrelease
			restricted
			when (Status.Released
			and  !ApprovalPeriod entered)
		CanDeleteReleasedJournal
			restricted
			when (Status.Released
			and  (System	= "RJ"
			or    System	= "AP")
			and   !PendingApproval)		
		DeletedRecurringJournals
			restricted
			when (Status.Deleted)
		CanDeleteUnreleasedJournal
			restricted
			when (Status.Unreleased
			and  (System	= "RJ"
			or    System	= "AP"))
		DimensionIsZoned
			restricted
			when (FinanceEnterpriseGroup.Dimension1ZoneBalancing)
		EntityIsZoned		
			restricted
			when (AccountingEntity.AccountingUnitZoneBalancing)
		RecuringBridgeExists
			restricted
			when (RecurringJournalBridge set exists)
		PendingApproval
			restricted
			when (ApprovalPeriod entered)
		JournalizeProcess
			restricted
			when (ApprovalPeriod entered
			or    ProcessingJournal)
		BudgetErrorsExist
			restricted
			when (BudgetEditErrorRel exists
			and   Status.Unreleased)
		EncumbrancesExist
			restricted
			when (RecurringJournalEncumbranceRel exists)

		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))
		CanEnableCopyComments
			restricted
			when (RecuringBridgeExists
			and !CopyComments)
		CanDisableCopyComments
			restricted
			when (RecuringBridgeExists
			and CopyComments)	
		NoRecurringJournalSchedule 
			when (!RecurringJournalSchedule set exists)
		OutOfRangeJournal
			when (AccountingEntity.CurrentPeriod < BeginningPeriod
			or AccountingEntity.CurrentPeriod > EndPeriod)
		RecurringJournalProcessed
			when (AccountingEntity.CurrentPeriod = LastProcessedPeriod)
		ReadyMessageCondition 
			when (!DeletedRecurringJournals
			and !NoRecurringJournalSchedule
			and !OutOfRangeJournal
			and !RecurringJournalProcessed
			and !PendingApproval)

		ManualAllowed
			restricted
			when (PendingApproval
			and !ResponsibilityMatrixPendingApprovalRel exists)

    Sets
		ByTransactionOrigin
			Sort Order
				TransactionOrigin
    	ByRJUniqueID
    		indexed
    		Sort Order
    			UniqueID					

	Field Rules
		System
			initial value is "RJ"
			default to "RJ"
		GeneralLedgerEvent
			initial value is "JE"
			default to "JE"
		Description
			required
		JournalType
		Currency
			default to AccountingEntity.FunctionalCurrency
			if (Currency changed)
				constraint (TotalCredit = 0
				or			TotalDebit = 0)
					"CannotChangeCurrency;TransactionAmountsExist"
		BaseZoneAccountingUnit

			if (EntityIsZoned)
				default to DefaultZoneAccountingUnitRel.ZoneAccountingUnit
			else 
				force default to blank
		BaseZoneDimension1

			if (DimensionIsZoned)
				default to DefaultZoneFinanceDimension1Rel.ZoneDimension
			else 
				force default to blank
		BeginningPeriod
			initial value is AccountingEntity.CurrentPeriod
			default to AccountingEntity.CurrentPeriod
			if (RecurringJournalBridge set exists)
				cannot be changed
					"CannotChangeBeginningPeriodWhenScheduleExists"
		EndPeriod
			default to AccountingEntity.CurrentPeriod
			if (RecurringJournalBridge set exists)
				cannot be changed
					"CannotChangeEndPeriodWhenScheduleExists"
			constraint (EndPeriod >= BeginningPeriod)		
				"BeginningPeriodCannotBeAfterEndPeriod"
		Ledger
			initial value is FinanceEnterpriseGroup.CoreLedger
			default to FinanceEnterpriseGroup.CoreLedger
			constraint (CodeBlockRelationDetailRel exists)
				"Invalid<FinanceEnterpriseGroup.LedgerMessage>For<FinanceEnterpriseGroup.AccountingEntityLabel>"
			constraint (!Ledger.CurrencyLedger)
				"CannotEnterCurrency<FinanceEnterpriseGroup.LedgerMessage>"
			required
		
		TransactionOrigin
			default to reference to this instance
			
 	Actions
#ifdef module sharedfinance

		UpdateApprovalLevel is an Instance Action
			restricted
			Parameters
				ParmEscalate	is Boolean
				ParmReassign	is Boolean
			Action Rules
				if (ParmReassign)
					if (ReassignToApprovalLevel entered)
						ApprovalLevel = ReassignToApprovalLevel.ApprovalLevel
						initialize ReassignToApprovalLevel
				else
				if (ParmEscalate)
					include GetNextEscalationApprovalLevel
					ApprovalLevel		= LocalApprovalLevel
					Approver			= LocalApprover
					ApproverTeam		= LocalApproverTeam
				else
					include GetNextApprovalLevel
					ApprovalLevel		= LocalApprovalLevel
					Approver			= LocalApprover
					ApproverTeam		= LocalApproverTeam
#endif
		ResetLastUpdateUser is an Instance Action		
			restricted									
			Action Rules								
				initialize update stamp.actor		    

		UpdateRecurringJournal is an Instance Action
			restricted
			Parameters
				PrmProcessingJournal		is Boolean
					default label is "ProcessingJournal"
				PrmLastProcessedPeriod		is like GeneralLedgerClosePeriod
					default label is "LastProcessedPeriod"
			Action Rules
				ProcessingJournal			= PrmProcessingJournal
				if (AutoZero)
					Status		= 0
				if (PrmLastProcessedPeriod entered)
					if (PrmLastProcessedPeriod		= "NONE")
						initialize LastProcessedPeriod
					else
						LastProcessedPeriod			= PrmLastProcessedPeriod

		UpdateJournalTotals is an Instance Action
			restricted
			Parameters
				PrmTotalDrAmount			is an InternationalAmount
					default label is "TotalDrAmount"
				PrmTotalCrAmount			is an InternationalAmount
					default label is "TotalCrAmount"
				PrmUnitsDrAmount			is an UnitsAmount
					default label is "UnitsDrAmount"
				PrmUnitsCrAmount			is an UnitsAmount
					default label is "UnitsCrAmount"
				PrmAutoReverseDrAmount		is an InternationalAmount
					default label is "AutoReverseDrAmount"
				PrmAutoReverseCrAmount		is an InternationalAmount
					default label is "AutoReverseCrAmount"
				PrmAutoReverseUnitsDrAmount is an UnitsAmount
					default label is "AutoReverseUnitsDrAmount"
				PrmAutoReverseUnitsCrAmount is an UnitsAmount
					default label is "AutoReverseUnitsCrAmount"

			Action Rules
				TotalDebit					+= PrmTotalDrAmount
				TotalCredit					+= PrmTotalCrAmount
	    		UnitDebit					+= PrmUnitsDrAmount
	    		UnitCredit				    += PrmUnitsCrAmount
	        	AutoDebit					+= PrmAutoReverseDrAmount
				AutoCredit					+= PrmAutoReverseCrAmount
	    		AutoUnitDebit				+= PrmAutoReverseUnitsDrAmount
	    		AutoUnitCredit				+= PrmAutoReverseUnitsCrAmount








 		CheckEncumbranceForJournal is a Set Action
 			run in background
			completion message is "EncumbranceValidationComplete"
			Parameters
				PrmEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"

			Parameter Rules
				PrmEnterpriseGroup
					required

			Local Fields

			Instance Selection
				where (FinanceEnterpriseGroup		= PrmEnterpriseGroup
				and    Status.Released
				and   (TotalDebit entered
				or 	   UnitDebit entered)
				and    DerivedEntryAmount 			= 0
				and    DerivedEntryUnit				= 0
				and    DerivedAutoAmount 			= 0
				and   (!AutoZero))

			Set Is
				PrmEnterpriseGroup

			Action Rules
				Empty Set Rules
				Set Rules
					Entrance Rules

					Exit Rules

				Instance Rules
					LocalSystemCode		= "RJ"
					if (!LocalSystemCode.EncumbranceOption.NotUsed)
						if 	(RecuringBridgeExists
						and !EncumbrancesExist)
							invoke CreateRecurringEncumbrance RecurringJournalBridge in foreground
								invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity	= AccountingEntity
								invoked.PrmJournalControl	= RecurringJournalControl
						else						
							if (!EncumbrancesExist)
								invoke CreateRecurringEncumbrance RecurringJournalTransaction in foreground
									invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
									invoked.PrmAccountingEntity	= AccountingEntity
									invoked.PrmJournalControl	= RecurringJournalControl

						invoke EditTotalsRoute BudgetTemplateGroupTotal
							invoked.PrmFinanceEnterpriseGroup					= FinanceEnterpriseGroup
							invoked.PrmBudgetEditGroup							= UniqueID
							invoked.PrmMode										= BudgetEditMode.Hard
							if (LocalSystemCode.EncumbranceOption.Track)
								invoked.PrmMode									= BudgetEditMode.UpdateOnly
							invoked.PrmBusinessClassName				= "RecurringJournalControl"
							invoked.PrmEditContext						= "CheckEncumbranceForJournal"
							initialize invoked.PrmBudgetEditCallBack.BudgetEditCallBackReference

						invoke UpdateToReleased GLCommit in foreground
							invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
							invoked.PrmUniqueID							= UniqueID
							invoked.PrmAccountingEntity					= AccountingEntity

		FastUpdate is an Update Action
			restricted
			bypass field rules
		
		RebuildCommitmentTotals is an Instance Action
			restricted
			Parameters
				IsBatch        is Boolean
			Action Rules
				invoke PurgeGroupTotalsForEditGroup BudgetTemplateGroupTotal in background
					assign async action request id to AsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmBudgetEditGroup		  = UniqueID

				invoke RecalculateBTGT RecurringJournalTransaction in background
					run after AsyncId
					assign async action request id to AsyncId
					invoked.PrmSkipAttachRule			= true				
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmAccountingEntity			= AccountingEntity
					invoked.PrmJournalControl			= RecurringJournalControl
			Exit Rules
				if (initiating action != "FinanceEnterpriseGroup.InterEntityNetting")
					if (!System.EncumbranceOption.NotUsed)
						invoke EditTotalsRoute BudgetTemplateGroupTotal in background
							run after AsyncId
							invoked.PrmFinanceEnterpriseGroup					= FinanceEnterpriseGroup
							invoked.PrmBudgetEditGroup							= UniqueID
							if (System.EncumbranceOption.TrackAndEdit)
								invoked.PrmMode									= BudgetEditMode.Hard
							else
								invoked.PrmMode									= BudgetEditMode.UpdateOnly
							invoked.PrmBudgetEditCallBack.BudgetEditCallBackReference	= reference to this instance
							invoked.PrmBusinessClassName				= "RecurringJournalControl"
							invoked.PrmEditContext						= "RebuildCommitmentTotals"

							
	StateCycles
		RecurringJournalControlLifeCycle is a StateCycle
			state field is Status
			
			Unreleased	is a State
		 		Create is a Create Action
					completion message is "JournalCreated"

					Entrance Rules








					Action Rules

					Exit Rules
						include PeriodsForYearRange
		
		    	Update is an Update Action
					completion message is "JournalUpdated"
					valid when (!RecuringBridgeExists)

					Entrance Rules








					Action Rules
						if (BeginningPeriod changed
						or 	EndPeriod changed)
							for each RecurringJournalSchedule set
								if (!each.PeriodProcessed)
									invoke Delete each								
							include PeriodsForYearRange





				Release is an Instance Action
					completion message is "<ReleaseMessage>"
					valid when (IsValidToRelease)
					Entrance Rules
						constraint (!JournalWithoutAmountOrUnit)
							"<NoAmountsOrUnitsErrorMessage>"
						constraint (!DerivedEntryAmount entered)
							"<DebitCreditErrorMessage>"
						constraint (!DerivedAutoAmount entered)
							"<AutoDebitCreditErrorMessage>"
						constraint (!RecurringJournalStatisticalRel exists)
							"CannotRelease;ErrorWithStatisticalAccount:<first RecurringJournalStatisticalRel.FinanceCodeBlock.GeneralLedgerChartAccount>"
						LocalSystemCode		= "RJ"

						if ((!AccountingEntity.JournalApproval.None						
						and  TotalDebit	> AccountingEntity.JournalApprovalAmount)
						or  (!TotalDebit entered
						and  UnitDebit entered
						and	 AccountingEntity.JournalApproval.AmountAndUnit))
							constraint (ApprovalCode entered)
								"ApprovalCodeIsRequired"
								

#ifdef module cb			
						if (FinanceEnterpriseGroup.FundAccounting)
							invoke UpdateCashCodeFundEdit TransactionsRequiringFundCashEditRel


#endif

					Action Rules
						LocalSystemCode		= "RJ"
						if (AutoZero
						or  LocalSystemCode.EncumbranceOption.NotUsed)
							make transition to Released
						else
							if  (PendingBudgetEdit
							and !LocalSystemCode.EncumbranceOption.NotUsed)
								invoke UpdateToReleased GLCommit in foreground
									invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
									invoked.PrmUniqueID							= UniqueID
									invoked.PrmAccountingEntity					= AccountingEntity
								PendingBudgetEdit				= false
								make transition to Released
							else
								PendingBudgetEdit				= true
								invoke CreateRecurringEncumbrance RecurringJournalTransaction in foreground
									invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
									invoked.PrmAccountingEntity	= AccountingEntity
									invoked.PrmJournalControl	= RecurringJournalControl

								invoke EditTotalsRoute BudgetTemplateGroupTotal
									invoked.PrmFinanceEnterpriseGroup					= FinanceEnterpriseGroup
									invoked.PrmBudgetEditGroup							= UniqueID
									invoked.PrmMode										= BudgetEditMode.Hard
									if (LocalSystemCode.EncumbranceOption.Track)
										invoked.PrmMode									= BudgetEditMode.UpdateOnly
									invoked.PrmBudgetEditCallBack.BudgetEditCallBackReference	= reference to this instance
									invoked.PrmBusinessClassName				= "RecurringJournalControl"
									invoked.PrmEditContext						= "Release"								

				AutoRelease is an Instance Action
					default label is "EditBudget"
					valid when (IsValidForInterfaceJournal)
					completion message is "JournalReleased"
					Action Rules
						LocalSystemCode		= "RJ"
						if (LocalSystemCode.EncumbranceOption.NotUsed)
							make transition to Released
						else
							if  (PendingBudgetEdit
							and !LocalSystemCode.EncumbranceOption.NotUsed)
								invoke UpdateToReleased GLCommit in foreground
									invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
									invoked.PrmUniqueID							= UniqueID
									invoked.PrmAccountingEntity					= AccountingEntity
								PendingBudgetEdit				= false
								make transition to Released
							else
								PendingBudgetEdit				= true
								invoke CreateRecurringEncumbrance RecurringJournalBridge in foreground
									invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
									invoked.PrmAccountingEntity	= AccountingEntity
									invoked.PrmJournalControl	= RecurringJournalControl

								invoke EditTotalsRoute BudgetTemplateGroupTotal
									invoked.PrmFinanceEnterpriseGroup					= FinanceEnterpriseGroup
									invoked.PrmBudgetEditGroup							= UniqueID
									invoked.PrmMode										= BudgetEditMode.Hard
									if (LocalSystemCode.EncumbranceOption.Track)
										invoked.PrmMode									= BudgetEditMode.UpdateOnly
									invoked.PrmBudgetEditCallBack.BudgetEditCallBackReference	= reference to this instance
									invoked.PrmBusinessClassName				= "RecurringJournalControl"
									invoked.PrmEditContext						= "AutoRelease"								

#ifdef module cb			
						if (FinanceEnterpriseGroup.FundAccounting)
							invoke UpdateCashCodeFundEdit TransactionsRequiringFundCashEditRel
#endif

				ClearPendingFlag is an Instance Action
					restricted
					Action Rules
						PendingBudgetEdit = false	






				Delete is an Instance Action
					default label is "MoveToDeleted"
		    		valid when (CanDeleteUnreleasedJournal)
					confirmation required
					Action Rules
						if (last RecurringJournalBridge set.RecurringJournalBridge > 100)
							invoke PurgeForRJControl RecurringJournalBridge in background
								invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity				= AccountingEntity
								invoked.PrmRecurringJournalControl		= RecurringJournalControl
						else
							invoke PurgeForRJControl RecurringJournalBridge in foreground
								invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity				= AccountingEntity
								invoked.PrmRecurringJournalControl		= RecurringJournalControl
						make transition to Deleted		
		
				EnableCopyComments is an Instance Action
					valid when (CanEnableCopyComments)
					Action Rules
						invoke FastUpdate
							invoked.CopyComments = true
				
				DisableCopyComments is an Instance Action
					valid when (CanDisableCopyComments)
					Action Rules
						invoke FastUpdate
							invoked.CopyComments = false
		
			Released is a State
						
				Unrelease is an Instance Action
					completion message is "JournalUnreleased"
					valid when (IsValidToUnrelease)

					Entrance Rules
						
						constraint (!RecuringBridgeExists)
							"RecurringJournalCannotBeUnreleased"
						if (!AutoZero)
							LocalSystemCode		= "RJ"
							if (LocalSystemCode.EncumbranceOption.TrackAndEdit
							or  LocalSystemCode.EncumbranceOption.Track)
								invoke Purge BudgetEditErrorRel in foreground
								invoke Purge RecurringJournalEncumbranceRel in foreground

					Action Rules
						make transition to Unreleased
#ifdef module cb			
						if (FinanceEnterpriseGroup.FundAccounting)
							invoke Delete CashCodeFundEditRel
#endif

				AutoUnrelease is an Instance Action
					completion message is "JournalUnreleased"
					restricted

					Entrance Rules
						constraint (LastProcessedPeriod	not entered)
							"RecurringJournalCannotBeUnreleased"
						LocalSystemCode		= "RJ"
						if (LocalSystemCode.EncumbranceOption.TrackAndEdit
						or  LocalSystemCode.EncumbranceOption.Track)
							invoke Purge RecurringJournalEncumbranceRel in foreground

					Action Rules
						make transition to Unreleased
#ifdef module cb			
						if (FinanceEnterpriseGroup.FundAccounting)
							invoke Delete CashCodeFundEditRel
#endif

				ApproveRecurringJournal is an Instance Action
					completion message is "JournalApprove"
					restricted
					Parameters
						PrmApprovalPeriod			is like GeneralLedgerClosePeriod
							default label is "ApprovalPeriod"

					Local Fields	
						LocalReleaseJournal				is Boolean

					Entrance Rules
#ifdef module sharedfinance
						constraint (ApprovalCode entered)
							"ApprovalCodeIsRequired"

						if (ApprovalCode entered										
						and !AccountingEntity.JournalApproval.None						
						and AccountingEntity.BypassApprovalCode	!= ApprovalCode)		


							LocalApprover = create stamp.actor.agent(Employee).Employee
							constraint (!ApprovalCodeResourceByResourceRel exists)
								"PersonCreatingTheJournalCannotBeTheApprover"
							constraint (ApprovalCode.HasApprovalLevels) 
								"CannotComplete;TheApprovalCodeHasNoApprovalLevels"
							constraint (!ApprovalCode.HasTeamWithNoMembers) 
								"CannotComplete;TheApprovalCodeHasATeamWithNoMembers"
							constraint (!ApprovalCode.HasInvalidEscalations)
								"CannotComplete:TheApprovalCodeHasInvalidEscalationSettings"
							constraint (!ApprovalCode.HasInactiveResources)
								"CannotComplete;TheApprovalCodeHasInactiveResources"
#endif
					Action Rules
						ApprovalPeriod		= PrmApprovalPeriod
#ifdef module sharedfinance
						include GetNextApprovalLevel
#endif						
						ApprovalLevel	= LocalApprovalLevel
						Approver 		= LocalApprover
						ApproverTeam 	= LocalApproverTeam
						if (!LocalApprover entered)
							constraint (LocalApproverTeam entered)
								"PersonUpdatingJournalCannotBeTheApprover"
							constraint (DerivedCurrentTeamActorList entered)
								"PersonUpdatingJournalCannotBeATeamMember"
						include InitiateJournalFlow

				Reject is an Instance Action
					restricted
					Action Rules
						include SendRejectNotificationToUser
						initialize ApprovalLevel
						initialize Approver
						initialize ApproverTeam
						initialize ApprovalPeriod

				ManualReject is an Instance Action
		    		valid when (ManualAllowed)
					Action Rules
						constraint (ApprovalPeriod entered)
							"CannotManualReject"
						include SendRejectNotificationToUser
						cancel "RecurringJournalApproval" PA service
						initialize ApprovalLevel
						initialize Approver
						initialize ApproverTeam		
						initialize ApprovalPeriod
			
		   		ManualApprove is an Instance Action
		    		valid when (ManualAllowed)
					confirmation required
						"ThisWillBypassTheProcessFlowApprovalProcess;DoYouWantToContinue?"
					Entrance Rules
						constraint (ApprovalPeriod entered)
							"CannotManualApprove"
						constraint (!create stamp.actor	= actor)
							"PersonCreatingTheJournalCannotBeTheApprover"
						constraint (!update stamp.actor	= actor)
							"PersonUpdatingTheJournalCannotBeTheApprover"
						
					Action Rules			
						include SendManualApproveNotificationToUser
						cancel "RecurringJournalApproval" PA service
						LocalClosePeriod		= ApprovalPeriod
						invoke ApprovedJournalize RecurringJournalScheduleRel
						initialize ApprovalLevel
						initialize Approver
						initialize ApproverTeam
						initialize ApprovalPeriod

				Approve is an Instance Action
					restricted
					Action Rules
						LocalClosePeriod		= ApprovalPeriod
						invoke ApprovedJournalize RecurringJournalScheduleRel
						initialize ApprovalLevel
						initialize Approver
						initialize ApproverTeam
						initialize ApprovalPeriod

				RejectWithReasonCode is an Instance Action
					restricted
					subject is RejectJournalEntry
					reason code required
					action comment required
		
					Action Rules
						initialize ApprovalLevel
						initialize Approver
						initialize ApproverTeam		
						include SendRejectNotificationToUser
						initialize ApprovalPeriod
			
					Exit Rules
						invoke Create RecurringJournalComment
							invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
							invoked.AccountingEntity			= AccountingEntity
							invoked.RecurringJournalControl		= RecurringJournalControl
#ifdef module sharedfinance
							invoked.Title						= reason code
#endif
#ifndef module sharedfinance
							invoked.Title						= action comment
#endif
							invoked.Comment						= action comment

				Delete is an Instance Action
					default label is "MoveToDeleted"
		    		valid when (CanDeleteReleasedJournal)
					confirmation required
					Action Rules			
						if (last RecurringJournalBridge set.RecurringJournalBridge > 100)
							invoke PurgeForRJControl RecurringJournalBridge in background
								invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity				= AccountingEntity
								invoked.PrmRecurringJournalControl		= RecurringJournalControl
						else
							invoke PurgeForRJControl RecurringJournalBridge in foreground
								invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity				= AccountingEntity
								invoked.PrmRecurringJournalControl		= RecurringJournalControl
						make transition to Deleted
#ifdef module cb			
						if (FinanceEnterpriseGroup.FundAccounting)
							invoke Delete CashCodeFundEditRel
#endif

				UpdateApprovalPeriod is an Instance Action
					restricted
					Parameters
						PrmApprovalPeriod			is like GeneralLedgerClosePeriod
							default label is "ApprovalPeriod"
					Action Rules
						ApprovalPeriod		= 	PrmApprovalPeriod

				InitializeApprovalPeriod is an Instance Action
					restricted
					Action Rules
						initialize ApprovalPeriod

			Deleted is a State
				
				Undelete is an Instance Action
					Action Rules
						make transition to Unreleased

