PfiTask is a BusinessClass
    owned by pfi
    prefix is PfiTA
    default label is "Task"
    framework type is ProcessFlow

    Ontology
        symbolic key is PfiTask

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements CompoundDocument
            Document Components
                PfiUserTask set
                PfiUserCategory set
		implements AuditLogEntryActions

    Persistent Fields

        Description     is a PfiDescription
        DisplayInbasket is Numeric size 2
            States
                StandardInbasket value is 1
                CustomInbasket1  value is 2
                CustomInbasket2  value is 3
                CustomInbasket3  value is 4
                CustomInbasket4  value is 5
                CustomInbasket5  value is 6
                CustomInbasket6  value is 7
                CustomInbasket7  value is 8
                CustomInbasket8  value is 9
                CustomInbasket9  value is 10
                CustomInbasket10 value is 11

    Derived Fields

        TaskDescription is a DerivedField
            type is Alpha size 100 
            default label is "Task"
            if (PfiTask.TaskType.User)
                return PfiUserProfileRel.TaskDescription
            else
            if (PfiTask.TaskType.Group)
                if (Description != blank)
                    return Description
                else
                    return PfiTask.TaskName
            else
                return Description

    Conditions

        IsGroupTask
            default label is "TaskTypeIsGroup"
            when (PfiTask.TaskType.Group)

    Field Rules

        PfiTask
            default PfiTask.TaskType to PfiTask.TaskType.Group

        DisplayInbasket
            default to DisplayInbasket.StandardInbasket
            initial value is DisplayInbasket.StandardInbasket

    Relations

        PfiUserProfileRel
            one-to-one relation to PfiUserProfile
            Field Mapping uses symbolic key
                related.PfiUserProfile = PfiTask.TaskName
