PayablesIncomeCode is a BusinessClass
	owned by ap
	prefix is TYP
	classic name is APINCCODE

	Ontology
		symbolic key is PayablesIncomeCode
			classic set name is TYPSET1
			classic name is INCOME-CODE

	Patterns
		implements StaticJava
		disable AuditIndex

	Attach Rules
		constraint (Active)
			"IncomeCodeIsNotActive"

	Persistent Fields
		Description
		FormType		is AlphaUpper size 1
			classic name is FORMS-IND
			States
				NonemployeeCompensation		value is "N"		
				Miscellaneous				value is "M"
				Interest					value is "I"
				Dividends					value is "D"
				GovernmentPayments			value is "G"
				MerchantCardAndThirdParty	value is "K"
				ForeignPerson				value is "S"
		BoxNumber		is AlphaRight size 2
			classic name is BOX-NBR


		BoxIdentifier	is AlphaUpper size 3
		Active

	Local Fields		
		BypassEdits		is Boolean

	Conditions
		BoxIdentifierEntered		
			restricted
			when (BoxIdentifier entered)

	Field Rules
		BoxNumber
			if (!BypassEdits)		
				if (FormType not entered)
					cannot be entered
						"BoxNumberMustBeBlankIfNoFormTypeSelected"                
				if (FormType.NonemployeeCompensation)		
					required
						"BoxNumberMust=1,_2"
					if (BoxIdentifier not entered
					and (BoxNumber = " 1"
					or   BoxNumber = " 2"	))
						BoxNumber = BoxNumber
					else
						constraint (false)
							"BoxNumberMust=1,_2"	
				if (FormType.Miscellaneous)
					required
						"BoxNumberMust=1,_2,_3,_5,_6,_7,_8,_9,_10,_11,_12,_14,_15"    
					if (BoxIdentifier not entered
					and (BoxNumber = " 1"
					or   BoxNumber = " 2"
					or   BoxNumber = " 3"
					or   BoxNumber = " 5"
					or   BoxNumber = " 6"
					or   BoxNumber = " 7"
					or   BoxNumber = " 8"
					or   BoxNumber = " 9"
					or   BoxNumber = "10"
					or 	 BoxNumber = "11"
					or   BoxNumber = "12"
					or   BoxNumber = "14"
					or   BoxNumber = "15"))
						BoxNumber = BoxNumber
					else
						constraint (false)
							"BoxNumberMust=1,_2,_3,_5,_6,_7,_8,_9,_10,_11,_12,_14,_15"  
				if (FormType.Dividends)
					required
						"BoxNumberMust_=_1a,1b,2a,2b,2c,2d,2e,2f,3,5,6,7,9,10,12,13"       

					if (BoxNumber = "1A"
					or  BoxNumber = "1B"
					or  BoxNumber = "2A"
					or  BoxNumber = "2B"
					or  BoxNumber = "2C"
					or  BoxNumber = "2D"
					or  BoxNumber = "2E"
					or  BoxNumber = "2F"
					or  BoxNumber = " 3"
					or  BoxNumber = " 5"
					or  BoxNumber = " 6"
					or  BoxNumber = " 7"
					or  BoxNumber = " 9"
					or  BoxNumber = "10"
					or  BoxNumber = "12"
					or  BoxNumber = "13")
						BoxNumber = BoxNumber
					else
						constraint (false)
							"BoxNumberMust_=_1a,1b,2a,2b,2c,2d,2e,2f,3,5,6,7,9,10,12,13"      
				if (FormType.Interest)
					required
						"BoxNumberMust_=_1,_2,_3,_5,_6,_8Or_9"                    
					if (BoxNumber = " 1"
					or  BoxNumber = " 2"
					or  BoxNumber = " 3"
					or  BoxNumber = " 5"
					or  BoxNumber = " 6"
					or  BoxNumber = " 8"
					or  BoxNumber = " 9")
						BoxNumber = BoxNumber
					else
						constraint (false)
							"BoxNumberMust_=_1,_2,_3,_5,_6,_8Or_9"                   
				if (FormType.GovernmentPayments)
					required
						"BoxNumberMust_=_1,_5,_6,_7Or_9"                          
					if (BoxNumber = " 1"
					or  BoxNumber = " 5"
					or  BoxNumber = " 6"
					or  BoxNumber = " 7"
					or  BoxNumber = " 9")
						BoxNumber = BoxNumber
					else
						constraint (false)
							"BoxNumberMust_=_1,_5,_6,_7Or_9"                         
				if (FormType.MerchantCardAndThirdParty)
					required
						"BoxNumberMust_=_1,_1aOr_1b"                                       
					if (BoxNumber = " 1"
					or  BoxNumber = "1A"
					or  BoxNumber = "1B")
						BoxNumber = BoxNumber
					else
						constraint (false)
							"BoxNumberMust_=_1,_1aOr_1b"                                      
				if (FormType.ForeignPerson)
					required
						"Inc_\CodeBoxMustBeNotBeBlank"                            

		BoxIdentifier
			if (!BypassEdits)		
				if (FormType.NonemployeeCompensation		
				or  FormType.Dividends
				or  FormType.Interest
				or  FormType.GovernmentPayments
				or  FormType.MerchantCardAndThirdParty)
					cannot be entered
						"MustBeBlank"

	Actions
		Create is a Create Action

		Update is an Update Action
			Entrance Rules
				if (Active changed		
				and Active = false)
					BypassEdits = true

		Delete is a Delete Action



				





