SourcingEventQuestion is a BusinessClass 
    owned by ss
    prefix is EVQ

    Ontology
    	symbolic key is SourcingEventQuestion
    	   			    			
    Patterns
        implements CompoundDocument 
        	Document Components
				SourcingEventQuestionValue set
				SourcingEventQuestionAllocation set
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Persistent Fields
		QuestionText					is Text
			sql name is SEQUE
			text searchable
   		ResponseType					is Numeric size 1
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
   				List 				value is 4 
   				YesNo 				value is 5
   				YesNoText			value is 6 
   		Attachment
   		DisplayOrder
   		Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion				is like Question
   		CMQuestion						is Boolean
   		QuestionWeighting				
   		LastQuestionValueDisplayOrder	is Numeric 6
   			disable Auditing
   		LastQuestionAnswerDisplayOrder	is Numeric 6
   			disable Auditing
   		ResponseRules            is Numeric size 1
            States
                NotRequired                                value is 1
                ResponseRequired                        value is 2
                ResponseRequiredAndAttachmentRequired   value is 3
        YesNoResponseRules       is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                    value is 1
                ResponseRequired                            value is 2
                ResponseRequiredAndAttachmentRequired       value is 3
                ResponseRequiredAndAttachmentRequiredIfYes  value is 4
                ResponseRequiredAndAttachmentRequiredIfNo   value is 5
        YesNoTextResponseRules   is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                            value is 1
                ResponseRequiredAndTextResponseRequired             value is 2
                ResponseRequiredAndTextOrAttachmentRequired         value is 3
                ResponseRequiredAndTextOrAttachmentRequiredIfYes    value is 4
                ResponseRequiredAndTextOrAttachmentRequiredIfNo        value is 5
                ResponseRequiredAndTextAndAttachmentRequired        value is 6
                ResponseRequiredAndTextAndAttachmentRequiredIfYes     value is 7
                ResponseRequiredAndTextAndAttachmentRequiredIfNo    value is 8
		AllowResponseAttachment			is Boolean
		
	Transient Fields
		NewDisplayOrder					is a DisplayOrder
		CreateFromRepository			is Boolean

	Local Fields
		LocalConditionalQuestion	is like Question
		LocalListValue				is like SourcingEventQuestionValue
		LocalYesOrNo				is Numeric 1 
           	States
           		Yes	value is 1
           		No	value is 2 
		FromRepository				is Boolean 
		FromConditional				is Boolean		
		LocalFromConditional		is Boolean
		LocalQuestion				is like Question

   	Derived Fields
   		IsQuestionRequired is a ConditionalField
   			type is Alpha 2
   			if (!ResponseRequired)
   				blank
			else
				"*"

   		AnswerMessage is a MessageField
   			restricted
   			"Answer"

   		RequiredQuestion is a MessageField
   			"Required"
   		
		DerivedQuestionText is a DerivedField
			type is Alpha 250
			return QuestionText

   		AnswerTextWithRequired is a StringField
 			type is Text
   			restricted
   			IsQuestionRequired 
   			" "
   			AnswerMessage

 		IsAttachmentRequired is a ConditionalField
 			type is Alpha 2
   			restricted
   			if (!AlwaysRequireResponseAttachment)
   				blank
			else
				"*"
	
		AttachDocumentMessage is a MessageField
   			restricted
			"AttachDocument"

   		AttachmentTextWithRequired is a StringField
 			type is Text
   			restricted
   			IsAttachmentRequired
   			" "
   			AttachDocumentMessage

   	Field Groups
   		RevisionControlled
   			QuestionText					
   			ResponseType					
	   		Attachment
	   		AllowResponseAttachment
	   		ResponseRules
            YesNoResponseRules
            YesNoTextResponseRules
   			
   	Conditions
		IsYesNo
   			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)
		
		HasListValues
   			restricted
			when (SourcingEventQuestionValue set exists)
		
		EligibleForCM
   			restricted
			when (SourcingEvent.ContractOutputExists
			or    SourcingEvent.ContractOutput)
			
		HasAttachment
   			restricted
			when (Attachment entered)
		
		YesNoQuestionWithWeighting
   			restricted
			when (ResponseType.YesNo
			and   QuestionWeighting entered) 
		
		YesNoTextQuestionWithWeighting
   			restricted
			when (ResponseType.YesNoText
			and   QuestionWeighting entered)
			
		ListQuestionWithWeighting
   			restricted
			when (ResponseType.List
			and   QuestionWeighting entered)
		
		ListQuestion
			restricted
			when (ResponseType.List)
		
		EligibleForWeighting
   			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText
			or 	  ResponseType.List)

		QuestionEntered
   			restricted
			when (Question entered)
			
		CanCreateConditionalQuestion
			restricted 
			when (YesNoResponseType
			and  !QuestionIsAnswered)
		
		CanCreateConditionalListQuestion
			restricted 
			when (ResponseType.List
			and  !QuestionIsAnswered)

		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)
		
		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)
		
		RequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and  !QuestionIsAnswered)

		QuestionIsAnswered
			restricted
			when (SourcingEventQuestionResponseRel exists)

		DisplayAnswerRequiredText 
			restricted
			when (RequiredQuestionNoAnswer
			or    MayRequireAttachmentNoAttachment)

		MayRequireAttachmentNoAttachment 
			restricted 
			when (MayRequireResponseAttachment
			and  !AnswerAttachmentExists)

		HasAnySourcingEventConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		RepositoryQuestion 
			restricted 
			when (Question entered)

		RepositoryHasConditionalQuestions	
			restricted 
			when (RepositoryConditionalQuestionRel exists)

		AllowedToChangeEventQuestions	
			restricted 
			when (!RepositoryQuestion
			or    RepositoryConditionalQuestionRel !exists)

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)
		
		HasDirectConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		AnswerAttachmentExists
			restricted
			when (SourcingEventQuestionAnswerAttachmentRel exists)

		HasWeighting
   			restricted
			when (QuestionWeighting entered)
			
		HadWeighting
   			restricted
			when (old QuestionWeighting entered)
			
		HasRemainingAttachmentHeaderQuestions
   			restricted
			when (OtherRequiredAttachmentsRel exists)
			
		HasRemainingAttachmentQuestions
   			restricted
			when (OtherRequiredAttachmentsRel exists
			or    RequiredLineAttachmentsRel exists)

		AnyQuestionAnswers
   			restricted
			when (AnyQuestionAnswersRel exists
			or    SESupplierQuestionResponseRel exists)
			
		ResponseRequired
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    ResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        MayRequireResponseAttachment
   			restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        AlwaysRequireResponseAttachment
   			restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
            
        NonYesNoResponseType
   			restricted
            when (ResponseType.Date
            or    ResponseType.List
            or    ResponseType.Text
            or    ResponseType.Number)   

		SourcingEventResponseExists
			restricted
			when (SourcingEvent.AllowsConditionalQuestions
			and   SourcingEventResponseRel exists)

	Relations
		AnyQuestionAnswersRel is a SourcingEventQuestionResponse set

		ScoreAllocationEntered is a SourcingEventQuestionAllocation set
			Instance Selection
				where (related.ScoreAllocation entered)
				
		SourcingEventQandARel
			one-to-many relation to SourcingEventQandA
			Field Mapping uses part of key
				related.Company					= Company
				related.SourcingEvent			= SourcingEvent
		SourcingEventResponseRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses ByEvent
				related.Company					= Company
				related.SourcingEvent			= SourcingEvent

		QuestionResponseAttachmentsRel 
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventQuestion   = SourcingEventQuestion
			Instance Selection
				where (related.HasQuestionAnswerAttachment)
				
		SourcingEventQuestionResponseRel
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company					= Company
				related.SourcingEvent			= SourcingEvent
				related.SourcingEventQuestion	= SourcingEventQuestion

		SESupplierQuestionResponseRel
			one-to-many relation to SESupplierQuestionResponse
			Field Mapping uses ByQuestion
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion   = SourcingEventQuestion

		LineConditionalYesNoQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup			= Company.SourcingGroup 
				related.Question					= Question 
			Instance Selection
				where ((SourcingEventQuestion.ResponseType.YesNo
				and	    related.ConditionalQuestion.QuestionAnswer.QuestionAnswerGroup.YesNoAnswer	= LocalYesOrNo)		
				or     (SourcingEventQuestion.ResponseType.YesNoText
				and     related.ConditionalQuestion.QuestionAnswer.QuestionAnswerGroup.YesNoText.YesNo	= LocalYesOrNo))

		SameRepositoryQuestionRel 
			one-to-many relation to SourcingEventQuestion 
			Field Mapping uses ByQuestion
				related.Company					= Company 
				related.SourcingEvent			= SourcingEvent 
				related.Question				= LocalConditionalQuestion

		SourcingEventSupplierQuestionRel 
			one-to-many relation to SourcingEventSupplierQuestion 
			Field Mapping uses symbolic key
				related.NotifiedSupplier.SupplierGroup		= SourcingEventResponseRel.NotifiedSupplier.SupplierGroup
	 			related.NotifiedSupplier.Supplier			= SourcingEventResponseRel.NotifiedSupplier.Supplier
	 			related.NotifiedSupplier.SupplierSourceId	= SourcingEventResponseRel.NotifiedSupplier.SupplierSourceId
				related.Company								= Company 
				related.SourcingEvent						= SourcingEvent
			Instance Selection
				where (related.SourcingEventQuestion		= SourcingEventQuestion) 

		LineConditionalListQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup			= Company.SourcingGroup 
				related.Question					= Question 
				related.QuestionAnswer				= LocalListValue

		SourcingEventQuestionAnswerAttachmentRel is a ContractQuestionAnswer set
			Instance Selection
				where (related.Attachment entered)

		CreatedFromQuestionRel 
			one-to-one relation to SourcingEventQuestion 
			Field Mapping uses symbolic key 
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent 
				related.SourcingEventQuestion				= CreatedFromQuestion

		ConditionalQuestionsDirectRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		ConditionalLocalQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= LocalQuestion 

		RepositoryConditionalQuestionRel    
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		SubmittedQuestionResponsesRel
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventQuestion   = SourcingEventQuestion
			Instance Selection
				where (related.SourcingEventResponse.Status.Submitted)
				
		SubmittedSupplierQuestionResponsesRel
			one-to-many relation to SESupplierQuestionResponse
			Field Mapping uses ByQuestion
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion   = SourcingEventQuestion
			Instance Selection
				where (related.SourcingEventResponse.Status.Submitted)
				
		SubmittedResponsesRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses ByEvent
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
			Instance Selection
				where (related.Status.Submitted)

		OtherRequiredAttachmentsRel 
			one-to-many relation to SourcingEventQuestion
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.SourcingEvent     		= SourcingEvent
			Instance Selection
				where (related.AlwaysRequireResponseAttachment
				and    related.SourcingEventQuestion   !=SourcingEventQuestion)
				
		RequiredLineAttachmentsRel
			one-to-many relation to SourcingEventLineQuestion
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.SourcingEvent     		= SourcingEvent
			Instance Selection
				where (related.AlwaysRequireResponseAttachment)

	Sets
		ByDisplayOrder
			duplicates
 			Sort Order
 				Company
 				SourcingEvent
 				DisplayOrder

		ByQuestion
			Sort Order
				Company
    			SourcingEvent
    			Question
    			SourcingEventQuestion
    			
    	ByQuestionFirst
    		Sort Order
    			Question
    			Company
    			SourcingEvent
    			SourcingEventQuestion
 		
   	Field Rules
   		QuestionText
   			required
   		ResponseType
   			required
   		QuestionWeighting
            if (QuestionWeighting entered)
                constraint (ResponseRequired)
                    "MustEnterAResponseRuleThatRequiresAResponseWhenEnteringQuestionWeighting"
            constraint (EligibleForWeighting)
                "CanOnlyDefineWeightingForList,YesNo,AndYesNoTextQuestions"
            if ((ResponseRules changed
            or  YesNoResponseRules changed
            or  YesNoTextResponseRules changed)
            and !ResponseRequired
            and QuestionWeighting entered
            and ScoreAllocationEntered exists)
                constraint (ResponseRequired)
                    "TheQuestionMustRequireAResponseWhenEventScoreAllocationIsDefined"
        ResponseRules
            if (NonYesNoResponseType)
                required
                    "MustSelectAResponseRule"
                YesNoResponseRules = 0
                YesNoTextResponseRules = 0
                    
        YesNoResponseRules
            if (ResponseType.YesNo)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoTextResponseRules= 0
        
        YesNoTextResponseRules
            if (ResponseType.YesNoText)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoResponseRules= 0
        AllowResponseAttachment
            if (MayRequireResponseAttachment)
                AllowResponseAttachment = true

   	Rule Blocks
   		CreateCorrectAnswer
   			if (IsYesNo
   			and !CreateFromRepository
   			and !SourcingEvent.CreateByCopy
   			and !SourcingEventQuestionAllocation set exists)
				invoke InternalCreate SourcingEventQuestionAllocation
					fill in fields from SourcingEventQuestion
					if (ResponseType.YesNo)
						invoked.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.YesNoValue = 1
					if (ResponseType.YesNoText)
						invoked.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.YesNoText.YesNo = 1
							
				invoke InternalCreate SourcingEventQuestionAllocation
					fill in fields from SourcingEventQuestion
					if (ResponseType.YesNo)
						invoked.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.YesNoValue = 2
					if (ResponseType.YesNoText)
						invoked.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.YesNoText.YesNo = 2
   			
   		UpdateScores
   		
			invoke CalculateEventScoreSet SourcingEventResponse
				invoked.ParmCompany		= Company
				invoked.ParmEvent       = SourcingEvent 
   				   					
	Create Exit Rules

		if (SourcingEventResponseExists)
			for each SourcingEventResponseRel
				invoke Create SourcingEventSupplierQuestion
					fill in fields from SourcingEventQuestion
					invoked.NotifiedSupplier		= each.NotifiedSupplier


 	Actions
		Create is a Create Action
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEvent.LastQuestionDisplayOrder 

			Action Rules
						
				LocalFromConditional = FromConditional 

				if (!SourcingEvent.TwoStepBiddingUsed
				and !SourcingEvent.BestAndFinalOffer)
					constraint (SourcingEvent.InActionableState)
						"CannotAddQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				else
					if (SourcingEvent.TwoStepBiddingUsed)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterStepTwo_(Pricing)HasStarted"
					else 
					if (SourcingEvent.BestAndFinalOffer)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterBestAndFinalProcessHasStarted"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddQuestionWhileEventIsPendingEventApproval"

				constraint (!AnyQuestionAnswers)
					"CannotAddQuestionWhenAnswersExist"
				
				if (!SourcingEvent.AllowsConditionalQuestions)
					constraint (ConditionalQuestionsDirectRel !exists)
						"CannotAddAQuestionWithConditionalQuestionsToThisSourcingEvent"

				if (!SourcingEvent.CreateByCopy)
					CMQuestion = true

				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddQuestion)
					invoke Amend Open.Notified SourcingEvent
			
			Exit Rules
				include CreateCorrectAnswer

		CreateFromWizard is a Create Action   
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEvent.LastQuestionDisplayOrder 

			Action Rules
				if (!SourcingEvent.TwoStepBiddingUsed
				and !SourcingEvent.BestAndFinalOffer)
					constraint (SourcingEvent.InActionableState)
						"CannotAddQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				else
					if (SourcingEvent.TwoStepBiddingUsed)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterStepTwo_(Pricing)HasStarted"
					else 
					if (SourcingEvent.BestAndFinalOffer)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterBestAndFinalProcessHasStarted"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddQuestionWhileEventIsPendingEventApproval"

				if (!SourcingEvent.CreateByCopy)
					CMQuestion = true

				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddQuestion)
					invoke Amend Open.Notified SourcingEvent
			
			Exit Rules
				include CreateCorrectAnswer
		
		CreateConditionalQuestion is an Instance Action
			valid when (CanCreateConditionalQuestion)
			Parameters 
            	ParmYesOrNo     		  is Numeric size 1
					default label is "YesOrNo"
					States
            			Yes	value is 1
            			No	value is 2   
				ParmConditionalQuestion is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmYesOrNo 
					required 
				ParmConditionalQuestion 
					required
			
			Action Rules

				constraint (ParmConditionalQuestion.Active)
					"CannotUseAnInactiveQuestion"
				LocalConditionalQuestion	= ParmConditionalQuestion
				LocalYesOrNo                = ParmYesOrNo
				constraint (SameRepositoryQuestionRel !exists)
					"ConditionalQuestionAlreadyExistsAsASourcingEventQuestion"
				constraint (LineConditionalYesNoQuestionRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (SourcingEventQuestion.Question entered)
					constraint (ParmConditionalQuestion != SourcingEventQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsQuestion"

		CreateConditionalListQuestion is an Instance Action
			valid when (CanCreateConditionalListQuestion)
			Parameters 
            	ParmListValue     		  is a SourcingEventQuestionValue 
					default label is "ListValue"
				ParmConditionalQuestion 	is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmListValue 
					required 
				ParmConditionalQuestion 
					required
			
			Action Rules

				LocalConditionalQuestion	= ParmConditionalQuestion
				LocalListValue              = ParmListValue
				constraint (LineConditionalListQuestionRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (SourcingEventQuestion.Question entered)
					constraint (ParmConditionalQuestion != SourcingEventQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsRepositoryQuestion"

		Update is an Update Action
			valid when (SourcingEvent.InEditableState)
		   	Action Rules
				if (YesNoOrList)
					constraint (AllowedToChangeEventQuestions)
						"CannotChangeYesNoOrListQuestionThatIsARepositoryQuestionThatHasConditionalQuestions"

				if (Question entered
				and (QuestionText changed
				or	 ResponseType changed
				or	 Attachment changed))
					initialize Question
				
				if (RevisionControlled changed)
					constraint (SourcingEvent.InActionableState)
						"CannotUpdateQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
					constraint (!SourcingEvent.NeedsApproval)
						"CannotUpdateQuestionWhileEventIsPendingEventApproval"

				if  (AlwaysRequireResponseAttachment changed
				and !AlwaysRequireResponseAttachment)
					constraint (!QuestionResponseAttachmentsRel exists)
						"CannotChangeAllowAttachmentUploadWithAnswerFlag:SuppliersHaveAnsweredQuestionWithAttachments"
				
				if (SourcingEvent.Status.Open)
					if (AlwaysRequireResponseAttachment changed
					and !AlwaysRequireResponseAttachment)
					
						if (SourcingEvent.TwoStepBidding.Separate)
							constraint (HasRemainingAttachmentHeaderQuestions)
								"CannotDeleteQuestionForTwoStepBiddingEventWhenNoOtherQuestionsWithRequiredAnswerAttachmentsExist"
								
						if (SourcingEvent.TwoStepBidding.Combined)
							constraint (HasRemainingAttachmentQuestions)
								"CannotDeleteQuestionForTwoStepBiddingEventWhenNoOtherQuestionsWithRequiredAnswerAttachmentsExist"
				
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateQuestion
				and RevisionControlled changed)
					invoke Amend Open.Notified SourcingEvent
				
				if (!ResponseType.List
				and SourcingEventQuestionValue set exists)
					invoke Delete SourcingEventQuestionValue set
				
				if (!EligibleForWeighting
				and SourcingEventQuestionAllocation set exists)
					invoke Delete SourcingEventQuestionAllocation set
					
				if (ResponseType.List
				and SourcingEventQuestionValue set exists
				and !SourcingEventQuestionAllocation set exists)
					for each SourcingEventQuestionValue set
						invoke InternalCreate SourcingEventQuestionAllocation 
							invoked.Company 				= each.Company
							invoked.SourcingEvent 			= each.SourcingEvent
							invoked.SourcingEventQuestion 	= each.SourcingEventQuestion
							invoked.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.SourcingEventQuestionValue = each.SourcingEventQuestionValue
							fill in fields from each
							
				if (ResponseType changed)
					constraint (!AnyQuestionAnswers)
						"CannotChangeQuestionTypeWhenAnswersExist"
				
				include CreateCorrectAnswer

				if (HasWeighting
				or  HadWeighting)
					if ((SourcingEvent.AllowsConditionalQuestions
					and  SubmittedSupplierQuestionResponsesRel exists)
					or  (!SourcingEvent.AllowsConditionalQuestions
				    and  SubmittedQuestionResponsesRel exists))
						include UpdateScores
																			
			Exit Rules
				if (SourcingEventResponseExists)
					for each SourcingEventResponseRel
					
						for each SourcingEventSupplierQuestionRel
							invoke Update each.SourcingEventSupplierQuestion
								fill in fields from SourcingEventQuestion
									except invoked.DisplayOrder
								initialize invoked.Question

		Delete is a Delete Action
			valid when (SourcingEvent.InEditableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteQuestionWhileEventIsPendingEventApproval"

				constraint (!AnyQuestionAnswers)
					"CannotDeleteQuestionWhenAnswersExist"
				
				if (SourcingEvent.Status.Open)
					if (SourcingEvent.TwoStepBidding.Separate)
						constraint (HasRemainingAttachmentHeaderQuestions)
							"CannotDeleteQuestionForTwoStepBiddingEventWhenNoOtherQuestionsWithRequiredAnswerAttachmentsExist"
							
					if (SourcingEvent.TwoStepBidding.Combined)
						constraint (HasRemainingAttachmentQuestions)
							"CannotDeleteQuestionForTwoStepBiddingEventWhenNoOtherQuestionsWithRequiredAnswerAttachmentsExist"
				
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.DeleteQuestion)
					invoke Amend Open.Notified SourcingEvent

				if (HasWeighting)
					if ((SourcingEvent.AllowsConditionalQuestions
					and  SubmittedSupplierQuestionResponsesRel exists)
					or  (!SourcingEvent.AllowsConditionalQuestions
				    and  SubmittedQuestionResponsesRel exists))
						include UpdateScores

		DeleteConditionalQuestions is a Set Action  
			restricted 
			Parameters 
			 	ParmCompany					is a SourcingCompany 
			 	ParmSourcingEvent			is a SourcingEvent 
			 	ParmOriginalQuestion		is a SourcingEventQuestion
				ParmLastOldQuestion			is a SourcingEventQuestion   

			Instance Selection 
			 	where (ParmCompany				= Company
			 	and    ParmSourcingEvent		= SourcingEvent 
			 	and    ParmOriginalQuestion		= CreatedFromQuestion
				and    SourcingEventQuestion	<= ParmLastOldQuestion)

			Action Rules
			 	Instance Rules 
					invoke Delete		
