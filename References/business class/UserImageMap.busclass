UserImageMap is a BusinessClass
    owned by repository
    prefix is URIM
    framework type is LPLConfiguration

	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing
		implements DynamicCreation
		implements ConfigurationMetadata
		implements AuditLogEntryActions
				
    Ontology
        symbolic key is UserImageMap
    


    Persistent Fields
    	Actor
    	BusinessImageMap
    	Delta is Text
    	InActive is Boolean
    		default label is "Inactive"
    	Description		is a ConfigDescription
    	
    Derived Fields
    	ContextKey is a DerivedField
    		type is LPLName
    		if (Actor entered)
	    		return  "Actor  '" + Actor + "', " + BusinessImageMap
    		return  "blank, " + BusinessImageMap







    		
    Field Rules
    	BusinessImageMap
    		required
    		    			
    Conditions
   		IsActor
   			default label is "Actor"
   			when (Actor entered)
    
    	Global
    		when (!IsActor)

    Actions
    	Create is a Create Action
    	
    	Update is an Update Action
    	
    	Delete is a Delete Action
    	
    	Purge is a Purge Action
			restricted
    	
    	Activate is an Instance Action
    		valid when (InActive)
    		Action Rules
    			InActive = false
    			
    	Deactivate is an Instance Action
    		valid when (not InActive)
    		Action Rules
    			InActive = true
    			
		DeletePersonalizationsForActor is a Set Action		 
			Parameters
				ParamActor is an Actor
					default label is "Actor"
				ParamDeletedActor is like Actor
					default label is "DeletedActor"
					
			Parameter Rules
				ParamActor
					if (ParamActor entered)
						ParamDeletedActor = ParamActor
					else
					if (ParamDeletedActor not entered)
						required
							"MustEnterActorOrDeletedActor"

			Instance Selection
				where (Actor = ParamDeletedActor)

			Sort Order
				Actor
				BusinessImageMap
				
			Action Rules
				Instance Rules
					invoke Delete		  
					
		TagItem is an Instance Action
			Parameters
				ParamConfigurationTag is a ConfigurationTag
					default label is "Tag"
			
			Entrance Rules
				if (InActive)
					confirmation required
						"UserImageMapIsInactive,TagAnyway?"
						
			Action Rules
				invoke Create ConfigurationTagItem
					invoked.ConfigurationTag = ParamConfigurationTag
					invoked.ConfigurationTagItem.Reference = reference to this instance		  
		   				
		PurgePersonalizationsForActor is a Set Action
			Parameters		 
				ParamActor is an Actor
					default label is "Actor"
				ParamDeletedActor is like Actor
					default label is "DeletedActor"
				
			Parameter Rules
				ParamActor
					if (ParamActor entered)
						ParamDeletedActor = ParamActor
					else
					if (ParamDeletedActor not entered)
						required
							"MustEnterActorOrDeletedActor"
					
			Instance Selection
				include deleted records
				where (Actor = ParamDeletedActor)

			Sort Order
				Actor
				BusinessImageMap
				
			Action Rules
				Instance Rules
					invoke Purge   	
    
    Sets
    	ActorSet
    		Sort Order
    			Actor
    			BusinessImageMap
 
 		ImageMapSet
 			Sort Order
 				BusinessImageMap
 				Actor
 				
		GlobalImageMapSet
			Sort Order
				BusinessImageMap
			Instance Selection
				where (Global)


