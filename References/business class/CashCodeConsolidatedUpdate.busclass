CashCodeConsolidatedUpdate is a BusinessClass
    owned by cb
    prefix is CCU

    Ontology
        symbolic key is CashCodeConsolidatedUpdate

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
        IssuedAmount            is an InternationalAmount
        IssuedCount             is a NbrOfTrans
        UnreconciledAmount		is an InternationalAmount
        ReconciledCount         is a NbrOfTrans
        ReconciledAmount        is an InternationalAmount
        VoidedAmount            is an InternationalAmount
        VoidedCount             is a NbrOfTrans
        StopPaidAmount          is an InternationalAmount
        StopPaidCount           is a NbrOfTrans


	Derived Fields
						

	Local Fields
		LocalCashManagementGroup	is like CashManagementGroup
		LocalCashCode				is a CashCode
		LocalCompany				is like GeneralLedgerCompany
		LocalBankTransactionCode	is a BankTransactionCode
						
	Conditions

            	            	            	
    Sets


	Relations

		CashLedgerConsolidationCashCodeRel
			one-to-one relation to CashLedgerConsolidation
			Field Mapping uses symbolic key  
				related.CashManagementGroup 								= LocalCashManagementGroup	
				related.CashCode 											= LocalCashCode
				related.CashLedgerConsolidation.BankTransactionCode 		= LocalBankTransactionCode
				related.CashLedgerConsolidation.Company						= LocalCompany  	
				related.CashLedgerConsolidation.LastMaintenanceDate			= current corporate date

        CashLedgerConsolidationsPreviousDayRel
            one-to-many relation to CashLedgerConsolidation
            Field Mapping uses Set5
            	related.CashManagementGroup		= LocalCashManagementGroup	
                related.CashCode				= LocalCashCode
			Instance Selection            				
                where (related.CashLedgerConsolidation.LastMaintenanceDate 	< current corporate date
                and    related.CashLedgerConsolidation.BankTransactionCode 	!entered
				and    related.CashLedgerConsolidation.Company 				!entered)

										
	Rule Blocks

		
	Actions
		Create is a Create Action
		
		Update is an Update Action

		Delete is a Delete Action
											
		UpdateCashCodeBalances is a Set Action
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
					
			Parameter Rules
				PrmCashManagementGroup
					required
						"RunGroupIsRequired"
							
			Local Fields


			Instance Selection
				where (CashManagementGroup	= PrmCashManagementGroup)
			
			Sort Order
				CashManagementGroup
				CashCode
	            CashCodeConsolidatedUpdate.Company
	            CashCodeConsolidatedUpdate.BankTransactionCode
	            CashCodeConsolidatedUpdate.SequenceNumber

  			Accumulators
		        TotalIssuedAmount
		        TotalIssuedCount
		        TotalReconciledAmount
		        TotalReconciledCount
		        TotalUnreconciledAmount
				TotalUnreconciledCount			
				
			Action Rules
				Empty Set Rules

				Set Rules
					Exit Rules
				
				CashManagementGroup Set Rules
					Entrance Rules
					Exit Rules

				CashCode Set Rules
					Entrance Rules
						if (CashCode.SummarizeLedgerBalances)
							LocalCashManagementGroup	= PrmCashManagementGroup
							LocalCashCode				= CashCode
							initialize LocalBankTransactionCode
							initialize LocalCompany
							if (CashLedgerConsolidationCashCodeRel	!exists)
								invoke ForwardPreviousDayBalances first CashLedgerConsolidationsPreviousDayRel
					Exit Rules
						invoke BypassRulesUpdate CashCode
							invoked.CurrentReconciledBalance 	+= TotalReconciledAmount
							invoked.UnreconciledBalance 		+= TotalUnreconciledAmount
							invoked.UnreconciledRecordCount		+= TotalUnreconciledCount		
						if (CashCode.SummarizeLedgerBalances)
							initialize LocalCompany
							initialize LocalBankTransactionCode
							if (CashLedgerConsolidationCashCodeRel exists)
								invoke Update CashLedgerConsolidationCashCodeRel
									invoked.IssuedAmount			+= TotalIssuedAmount
									invoked.AccountEndingBalance	+= TotalIssuedAmount
									invoked.OpenEndingBalance		+= TotalUnreconciledAmount
									invoked.IssuedCount				+= TotalIssuedCount
									invoked.ReconciledAmount		+= TotalReconciledAmount
									invoked.BankEndingBalance		+= TotalReconciledAmount
									invoked.ReconciledCount			+= TotalReconciledCount
							else
								invoke Create CashLedgerConsolidation
									invoked.CashManagementGroup							= LocalCashManagementGroup
									invoked.CashCode									= LocalCashCode
									invoked.CashLedgerConsolidation.LastMaintenanceDate	= current corporate date
									invoked.LastMaintenanceTime 						= current corporate time
									invoked.IssuedAmount								= TotalIssuedAmount
									invoked.AccountEndingBalance						= TotalIssuedAmount
									invoked.OpenEndingBalance							= TotalUnreconciledAmount
									invoked.IssuedCount									= TotalIssuedCount
									invoked.ReconciledAmount							= TotalReconciledAmount
									invoked.BankEndingBalance							= TotalReconciledAmount
									invoked.ReconciledCount								= TotalReconciledCount
							
				CashCodeConsolidatedUpdate.Company Set Rules
					Entrance Rules

					Exit Rules
						if (CashCode.SummarizeLedgerBalances)
							initialize LocalBankTransactionCode
							if (CashLedgerConsolidationCashCodeRel exists)
								invoke Update CashLedgerConsolidationCashCodeRel
									invoked.IssuedAmount			+= TotalIssuedAmount
									invoked.AccountEndingBalance	+= TotalIssuedAmount
									invoked.OpenEndingBalance		+= TotalUnreconciledAmount
									invoked.IssuedCount				+= TotalIssuedCount
									invoked.ReconciledAmount		+= TotalReconciledAmount
									invoked.BankEndingBalance		+= TotalReconciledAmount
									invoked.ReconciledCount			+= TotalReconciledCount
							else
								invoke Create CashLedgerConsolidation
									invoked.CashManagementGroup							= LocalCashManagementGroup
									invoked.CashCode									= LocalCashCode
									invoked.CashLedgerConsolidation.Company				= LocalCompany
									invoked.CashLedgerConsolidation.LastMaintenanceDate	= current corporate date
									invoked.LastMaintenanceTime 						= current corporate time
									invoked.IssuedAmount								= TotalIssuedAmount
									invoked.AccountEndingBalance						= TotalIssuedAmount
									invoked.OpenEndingBalance							= TotalUnreconciledAmount
									invoked.IssuedCount									= TotalIssuedCount
									invoked.ReconciledAmount							= TotalReconciledAmount
									invoked.BankEndingBalance							= TotalReconciledAmount
									invoked.ReconciledCount								= TotalReconciledCount
								

				CashCodeConsolidatedUpdate.BankTransactionCode Set Rules
					Entrance Rules
						LocalBankTransactionCode	= CashCodeConsolidatedUpdate.BankTransactionCode
					
					Exit Rules
						if (CashCode.SummarizeLedgerBalances)
							if (CashLedgerConsolidationCashCodeRel exists)
								invoke Update CashLedgerConsolidationCashCodeRel
									invoked.IssuedAmount		+= TotalIssuedAmount
									invoked.IssuedCount			+= TotalIssuedCount
									invoked.OpenEndingBalance	+= TotalUnreconciledAmount
									invoked.ReconciledAmount	+= TotalReconciledAmount
									invoked.ReconciledCount		+= TotalReconciledCount
							else
								invoke Create CashLedgerConsolidation
									invoked.CashManagementGroup							= LocalCashManagementGroup
									invoked.CashCode									= LocalCashCode
									invoked.CashLedgerConsolidation.BankTransactionCode	= LocalBankTransactionCode
									invoked.CashLedgerConsolidation.Company				= LocalCompany
									invoked.CashLedgerConsolidation.LastMaintenanceDate	= current corporate date
									invoked.LastMaintenanceTime 						= current corporate time
									invoked.IssuedAmount								= TotalIssuedAmount
									invoked.IssuedCount									= TotalIssuedCount
									invoked.OpenEndingBalance							= TotalUnreconciledAmount
									invoked.ReconciledAmount							= TotalReconciledAmount
									invoked.ReconciledCount								= TotalReconciledCount

							
				Instance Rules
					LocalCashManagementGroup	= PrmCashManagementGroup
					LocalCashCode				= CashCode
					LocalBankTransactionCode	= CashCodeConsolidatedUpdate.BankTransactionCode
			        TotalReconciledAmount		+= ReconciledAmount
			        TotalUnreconciledAmount		+= UnreconciledAmount
					TotalUnreconciledCount		+= (IssuedCount - ReconciledCount)		
					if (CashCode.SummarizeLedgerBalances)
						LocalCompany				= CashCodeConsolidatedUpdate.Company
				        TotalIssuedAmount			+= IssuedAmount
				        TotalIssuedCount			+= IssuedCount
				        TotalReconciledCount		+= ReconciledCount
			        
			        invoke Delete
					
