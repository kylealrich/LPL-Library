MobileSupplyChainCompanyPrinter is a BusinessClass
	owned by mscm
	prefix is MSCOP
	sql name is "MSCMCompanyPrinter"
	Ontology
		symbolic key is MobileSupplyChainCompanyPrinter
			
	Patterns
	
	Persistent Fields
		Default			is Boolean
		
	Field Rules
		Default
			if (OverrideDefault)
				if (DefaultCompanyPrinterRel exists)
					invoke SetDefaultToFalse DefaultCompanyPrinterRel
			if (not OverrideDefault and Default)
				constraint (DefaultCompanyPrinterRel not exists)
					"ThereIsAlreadyADefaultPrinterForTheCompany"
								
	Derived Fields
					
	Transient Fields
		OverrideDefault is Boolean 
			default label is "OverrideDefaultIfExisting"
	
	Relations
		DefaultCompanyPrinterRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.Default
				and    related.MobileSupplyChainPrinter.PrinterType  = MobileSupplyChainPrinter.PrinterType
				and    related.MobileSupplyChainPrinter 			!= MobileSupplyChainPrinter)
		
		MobileSupplyChainLocationPrinterRel
			one-to-many relation to MobileSupplyChainLocation
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.DefaultReportPrinter = MobileSupplyChainPrinter
				or	   related.DefaultLabelPrinter  = MobileSupplyChainPrinter)
		
        MobileSupplyChainResourceRel
			one-to-one relation to MobileSupplyChainResource
			Field Mapping uses symbolic key
				related.HROrganization 					= actor.agent(Employee).HROrganization
				related.MobileSupplyChainResource   	= actor.agent(Employee).Employee
		
        MobileSupplyChainResourceCompanyAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization					= actor.agent(Employee).HROrganization
    			related.MobileSupplyChainResource		= actor.agent(Employee).Employee
    			related.Company							= Company
				
	Conditions
		IsAccessibleByMobileSupplyChainResource
			restricted
			when (MobileSupplyChainResourceRel.GlobalLocationAccess or MobileSupplyChainResourceCompanyAccessRel exists)
	Actions
		Create is a Create Action
								
		Update is an Update Action
		
		Delete is a Delete Action
			Action Rules
				constraint (not MobileSupplyChainLocationPrinterRel exists)
					"CannotDelete.ThisCompanyPrinterIsAssociatedToAtleastOneInventoryLocation."
					
		SetDefaultToFalse is an Instance Action
			restricted
			Action Rules
				Default = false				
				
	Sets
		ByCompany
			Sort Order
				Company
				MobileSupplyChainPrinter
		ByPrinter
			Sort Order
				MobileSupplyChainPrinter		
			
		
		
	
	
