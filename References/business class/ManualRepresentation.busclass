ManualRepresentation is a BusinessClass
    owned by repository
    prefix is MREP

    Ontology
        symbolic key is ManualRepresentation

    Patterns
        implements DynamicCreation
        disable EffectiveDated
        disable AsOfDateProcessing


    Persistent Fields

        Description     is Alpha size up to 255
            translatable
        ManualCode      is BinaryDocument
        Title 			is DocumentTitle size 100

	Conditions

		StandardTemplate
			restricted
	        when (com.lawson.rdtech.type.Field.endsWith(ManualRepresentation, "_ST"))

	Actions

		CreateTemplate is a Create Action
        	Action Rules
    			constraint (com.lawson.rdtech.type.Field.endsWith(ManualRepresentation, "_ST"))
	                "NameMustEndWith_'\_ST'"

		UpdateTemplate is an Update Action
			valid when (StandardTemplate)

		DeleteTemplate is a Delete Action
			valid when (StandardTemplate)

		Create is a Create Action
        	Action Rules
    			constraint (!com.lawson.rdtech.type.Field.endsWith(ManualRepresentation, "_ST"))
	                "NameMustNotEndWith_'\_ST'"

		Delete is a Delete Action
			valid when (!StandardTemplate)
			Exit Rules
				invoke FireEvent

		Update is an Update Action
			valid when (!StandardTemplate)
			Exit Rules
				invoke FireEvent

        Copy is an Instance Action
            Parameters
				PrmManualRepresentation		is Alpha size 50
					default label is "ManualRepresentation"
                PrmDescription          	is Alpha size up to 255
					default label is "Description"

            Parameter Rules
				PrmManualRepresentation
					required
                PrmDescription
					initial value is Description
                	default to Description

			Action Rules
    			constraint (!com.lawson.rdtech.type.Field.endsWith(PrmManualRepresentation, "_ST"))
	                "NameMustNotEndWith_'\_ST'"

				invoke Create ManualRepresentation
					invoked.ManualRepresentation = PrmManualRepresentation
					invoked.Description 		 = PrmDescription
					fill in fields from this instance
					
		FireEvent is an Instance Action
			restricted			
