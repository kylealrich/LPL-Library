PayablesInvoiceAddOnChargeImport is a BusinessClass
    owned by ma
    prefix is MAO
    sql name is PInvoiceAddOnChargeImport
    classic name is MACINVAOC

    Ontology
        symbolic key is PayablesInvoiceAddOnChargeImport
            classic set name is MAOSET0

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
        RunGroup
        Company						  is a MatchCompany
        Vendor
        OldVendor
        Invoice
        Suffix
        EDINumber
            classic name is EDI-NBR
        Sequence 
            classic name is SEQ-NBR
        AddOnCharge
            classic name is AOC-CODE
        PurchaseOrder
		InterfacedPurchaseOrder		  is like PurchaseOrderImport
		InterfacedPOCode			  is like POCode
        LineNumber
            classic name is LINE-NBR
        EnteredAddOnChargeAmount      is an InternationalAmount
            classic name is ENT-AOC-AMOUNT



        PurchaseFromLocation
            classic name is PURCH-FR-LOC
        Location                      is an InventoryLocation
        AddOnChargePercent
            classic name is AOC-RATE
        AddOnChargeEntryMethod
            classic name is ENTRY
        Quantity
        UnitCost                      is an InternationalCost
        TotalAddOnChargeAmount        is an InternationalAmount
            classic name is TOTAL-AOC
        TaxCode
        Taxable                       is Boolean
            classic name is TAXABLE-FLAG
        TaxUsageCode
            classic name is TAX-USAGE-CD
        LandedAddOnCharge             is Boolean
            classic name is LANDED-FLAG
        LandedUnitCost                is an InternationalCost
            classic name is LAND-UNIT-CST
        PrintOnPO                     is Boolean
            classic name is AOC-ON-PO
        Summarize                     is Boolean
            classic name is SUMMARY-FLAG
        ZeroCost                      is Boolean
            classic name is ZERO-COST-FLG
        Canceled                      is Boolean
            classic name is CANCELLED-FL
        Closed
            classic name is CLOSED-FL
        CreatedDuringSpread
            classic name is INV-SPRD-CRET
        AccruedTaxAmount              is an InternationalAmount
            classic name is ACCR-TAX-AMT
        TotalTaxAmount                is an InternationalAmount
            classic name is EXT-TAX-AMT
        BillingInvoiceAmount          is an InternationalAmount
            classic name is BL-INVC-AMT
        CrossReferenceVendor
            classic name is XREF-VENDOR
        InvoiceTaxAmount              is an InternationalAmount
            classic name is INV-TAX-AMOUNT
        Currency
            classic name is CURRENCY-CODE
        ReceiptCurrencyConversionRate is an EnteredCurrencyConversionRate
            classic name is REC-CNV-RATE
        TrackType
        PostingAccount                is a FinanceCodeBlock
            classic name for PostingAccount.AccountingUnit is DST-ACCT-UNIT
            classic name for PostingAccount.GeneralLedgerChartAccount is DST-ACCOUNT
            classic name for PostingAccount.Project is ACTIVITY
        RecordInError                 is Boolean
		ErrorMessage				  is Alpha up to 150
		Buyer

	Local Fields
		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150
		LocalResultID					is like PayablesInvoiceInterfaceResult

		LocalPurchaseOrder				is like PurchaseOrder
		CheckCompany					is like Company
		CheckPurchaseOrder				is like PurchaseOrder

		NextSentence					is Boolean
		
	Derived Fields
		LineAOCNotAllowedOnServiceContractMsg is a MessageField
			"LineAddOnChargeNotAllowedOnServiceContract"
			
		AOCNotAllowedForExpenseInvoiceMsg is a MessageField
			"AddOnChargeNotAllowedForExpenseInvoice"
			
		InvoiceDetailNotFoundMsg is a MessageField
			"InvoiceDetailNotFoundForAddOnCharge"
		
    	NumericInterfacedPurchaseOrder is a DerivedField
    		type is like PurchaseOrder
    		if (InterfacedPurchaseOrder is numeric)
    			return InterfacedPurchaseOrder

		DerivedPurchaseOrder is a DerivedField
			type is like PurchaseOrder

			if  (PurchaseOrder entered)
				return PurchaseOrder
			else
			if (InterfacedPurchaseOrder entered)
				if  (InterfacedPurchaseOrderRel exists)
					return InterfacedPurchaseOrderRel.PurchaseOrder
				else
					CheckCompany = Company
					CheckPurchaseOrder = NumericInterfacedPurchaseOrder
					if  (CheckPurchaseOrderRel exists)
						return NumericInterfacedPurchaseOrder
			else
				return MatchInvoiceImportRel.PurchaseOrder	
		 		

	Context Fields
		ContextMatchInvoice is a MatchInvoiceImport
		ContextPayablesInvoiceDetailImport is a PayablesInvoiceDetailImport

	Field Rules
		RunGroup
			required
			default to ContextMatchInvoice.RunGroup
			initial value is ContextMatchInvoice.RunGroup

		Invoice
			required
			initial value is ContextMatchInvoice.Invoice
		
		EDINumber
			initial value is ContextMatchInvoice.EDINumber

		Vendor
			initial value is ContextMatchInvoice.Vendor
			
		OldVendor
			initial value is ContextMatchInvoice.OldVendor
			
		Suffix
			initial value is ContextMatchInvoice.Suffix
				
		Buyer
		    default to PurchaseOrder.Buyer
		    default to MatchInvoiceImportRel.PurchaseOrder.Buyer
		    default to InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
		    default to MatchInvoiceImportRel.InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
		    		            
		Sequence
			initial value is ContextPayablesInvoiceDetailImport.Sequence
			default to ContextPayablesInvoiceDetailImport.Sequence
				
	Relations








        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
                related.Company			 				= Company

        MatchInvoiceImportRel
            one-to-one relation to MatchInvoiceImport
            Field Mapping uses ByCompanyVendorInvoice
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix

        PayablesInvoiceDetailImportRel
            one-to-many relation to PayablesInvoiceDetailImport
            Field Mapping uses ByRunGroup
            	related.RunGroup						= RunGroup
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
				related.LineNumber						= LineNumber
				related.Sequence						= Sequence

		OtherAddOnChargesForSameLineRel
            one-to-many relation to PayablesInvoiceAddOnChargeImport
            Field Mapping uses ByRunGroup
            	related.RunGroup						= RunGroup
                related.Company			 				= Company
				related.Vendor							= Vendor
				related.OldVendor						= OldVendor
				related.EDINumber						= EDINumber
				related.Invoice							= Invoice
				related.Suffix							= Suffix
				related.PurchaseOrder					= PurchaseOrder
				related.LineNumber						= LineNumber
				related.Sequence						= Sequence
			Instance Selection
				where (related.PayablesInvoiceAddOnChargeImport != PayablesInvoiceAddOnChargeImport)
		

       	TaxEntityRel				 
            one-to-one relation to TaxEntity
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
                related.TaxEntity 						= Company.AccountingEntity
                
        EntityTaxCodeRel
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
                related.TaxEntity 						= Company.AccountingEntity
                related.TaxCode 						= TaxCode

		LocalInterfaceResultsRel
			one-to-one relation to PayablesInvoiceInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
				related.PayablesInvoiceInterfaceResult	= LocalResultID

		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
                related.Company                     = Company
                related.PurchaseOrderImport 		= InterfacedPurchaseOrder
                related.POCode						= InterfacedPOCode
			
        CheckPurchaseOrderRel
            one-to-one relation to PurchaseOrder
            Field Mapping uses symbolic key
                related.Company							= CheckCompany
                related.PurchaseOrder					= CheckPurchaseOrder 

    Sets

        ByRunGroup
            indexed
            Sort Order
                RunGroup
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
		        PurchaseOrder
        		LineNumber
                Sequence
                AddOnCharge
                PayablesInvoiceAddOnChargeImport

		ByCompanyVendorInvoice
            indexed
            Sort Order
                Company
                Vendor
                OldVendor
				EDINumber
                Invoice
                Suffix
		        PurchaseOrder
        		LineNumber
                Sequence
                AddOnCharge
                PayablesInvoiceAddOnChargeImport

	Create Rules
		constraint (MatchInvoiceImportRel exists)
			"InterfaceInvoiceDoesNotExist"
			
	Actions
		Create is a Create Action
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
			Action Rules
					
		CreateNoRules is a Create Action
			restricted
			bypass field rules
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
				display "CreatingForLine<LineNumber>AOC<AddOnCharge>Sequence<Sequence>"
			Action Rules
				if (Buyer not entered)
				    Buyer = PurchaseOrder.Buyer
				if (Buyer not entered)
				    Buyer = MatchInvoiceImportRel.PurchaseOrder.Buyer
				if (Buyer not entered)
				    Buyer = InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
				if (Buyer not entered)
				    Buyer = MatchInvoiceImportRel.InterfacedPurchaseOrderRel.PurchaseOrder.Buyer
		
		Update is an Update Action
			Entrance Rules
				LocalPurchaseOrder = DerivedPurchaseOrder
			Action Rules
					
		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules
				RunGroup = NewRunGroup
					
		ChangeSecondaryKeyFields is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
		        NewCompany			is a MatchCompany
		        NewVendor			is like Vendor
		        NewOldVendor		is like OldVendor
		        NewInvoice			is like Invoice
		        NewSuffix			is like Suffix
		        NewEDINumber		is like EDINumber
			Action Rules
				RunGroup 			= NewRunGroup
		        Company				= NewCompany
        		Vendor				= NewVendor
		        OldVendor			= NewOldVendor
		        Invoice				= NewInvoice
		        Suffix				= NewSuffix
		        EDINumber			= NewEDINumber
					
 		ClearError is an Instance Action
 			valid when (RecordInError)
			Action Rules
	 			initialize ErrorMessage
	 			RecordInError = false

		Delete is a Delete Action
					
		FastUpdate is an Update Action
			restricted
			bypass field rules

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		AddOnChargeLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				PrmVendorGroup				is a VendorGroup	
				PrmVendor					is a Vendor
					context of PrmVendorGroup			
				PrmProcessLevel				is a PayablesProcessLevel
				PrmAuthorityCode			is a PayablesAuthorityCode
					context of PrmVendorGroup				
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				MatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				AllowMatchItemOrVendorItem			is Boolean
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"
			Local Fields
				LocalInterfacedAddOnCostsAmountTotal	is an InternationalAmount
				LocalInterfacedAddOnCostsCount			is Numeric 10
				LocalInterfaceAddOnCostsErrorCount		is Numeric 10

			Instance Selection
				where (RunGroup	= PrmRunGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (PrmVendor            			  not entered
				or      Vendor	= PrmVendor)
				and    (PrmVendorGroup					not entered		
				or      Company.PayablesCompany.VendorGroup = PrmVendorGroup))

			Sort Order
				RunGroup
				Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				Sequence

			Action Rules

				Empty Set Rules
					invoke SpreadAddOnChargeLoad PayablesInvoiceAddOnChargeImport
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
						invoked.PrmVendor					= PrmVendor
						invoked.PrmProcessLevel				= PrmProcessLevel
						invoked.PrmAuthorityCode			= PrmAuthorityCode
				        invoked.InvoiceProcess				= InvoiceProcess
						invoked.MatchPoint					= MatchPoint
						invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.PrmVendorGroup				= PrmVendorGroup	

				RunGroup Set Rules
					Entrance Rules

					Exit Rules
						LocalResultID					= PrmInterfaceRun
						invoke Update LocalInterfaceResultsRel
							invoked.InterfacedAddOnCostsCount		= LocalInterfacedAddOnCostsCount

						invoke SpreadAddOnChargeLoad PayablesInvoiceAddOnChargeImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmVendor					= PrmVendor
							invoked.PrmProcessLevel				= PrmProcessLevel
							invoked.PrmAuthorityCode			= PrmAuthorityCode
					        invoked.InvoiceProcess				= InvoiceProcess
							invoked.MatchPoint					= MatchPoint
							invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.PrmVendorGroup				= PrmVendorGroup	


				Instance Rules
					display "ProcessAOCForInvoice=<Invoice>LineNumber=<LineNumber>AOC=<AddOnCharge>time=<current timestamp>" //PIDErr<PayablesInvoiceDetailImportRel.RecordInError>"
					if    (LineNumber entered
					and    PayablesInvoiceDetailImportRel.RecordInError)
						NextSentence = true
					else
					if    ((Sequence entered
					or      AddOnCharge.SpreadMethod.NoSpread)
					and    (PrmProcessLevel            			  not entered
					or      MatchInvoiceImportRel.ProcessLevel		= PrmProcessLevel)
					and    (PrmAuthorityCode       			      not entered
					or      MatchInvoiceImportRel.AuthorityCode	= PrmAuthorityCode)
	                and     PayablesCompanyRel.Company.FinanceEnterpriseGroup         = PrmFinanceEnterpriseGroup
					and    (MatchInvoiceImportRel.PayablesInvoice exists))

					
						RecordInError 						= false
						ErrorOccurred						= false

						LocalPurchaseOrder = DerivedPurchaseOrder
						LocalInterfacedAddOnCostsCount		+= 1

						if  (MatchInvoiceImportRel.ServiceContract entered
						and  LineNumber entered)
							ErrorOccurred			= true
							LocalErrorMessage		= LineAOCNotAllowedOnServiceContractMsg
						else
						if  (MatchInvoiceImportRel.ProcessType.Basic
						or   MatchInvoiceImportRel.ProcessType.Freight)
							ErrorOccurred			= true
							LocalErrorMessage		= AOCNotAllowedForExpenseInvoiceMsg
						else
						if  (first PayablesInvoiceDetailImportRel.LineNumber entered
						and  !PayablesInvoiceDetailImportRel exists)
							ErrorOccurred			= true
							LocalErrorMessage		= InvoiceDetailNotFoundMsg
						else
						if  (LineNumber entered)
							if  ((MatchInvoiceImportRel.InvoiceType.CreditMemo
							or    MatchInvoiceImportRel.InvoiceType.DebitMemo)
							and   MatchInvoiceImportRel.CreditMemoReferenceInvoice entered)
								if  (Quantity entered
								and  UnitCost entered)


									invoke MemoCreate PayablesInvoiceAddOnCharge
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										fill in fields from this instance
										invoked.PayablesInvoice					= MatchInvoiceImportRel.PayablesInvoice
										invoked.PayablesInvoiceAddOnCharge		= 1

										if  (Quantity < 0)
											invoked.EnteredQuantity				= Quantity * -1
										else
											invoked.EnteredQuantity				= Quantity

										if  (UnitCost < 0)
											invoked.EnteredUnitCost				= UnitCost
										else
											invoked.EnteredUnitCost				= UnitCost * -1

										if  (MatchInvoiceImportRel.ServiceContract entered)
											invoked.Contract					= MatchInvoiceImportRel.ServiceContract
											invoked.ContractLine				= LineNumber
										else
											invoked.PurchaseOrder				= DerivedPurchaseOrder
											invoked.PurchaseOrderLine			= LineNumber
		
								        invoked.RecordSource					= "LM"
										invoked.BypassInvoiceErrorChecking		= true
										invoked.BypassAllowUpdate				= true
										invoked.CreateFromBatchSwitch			= true

										if (EnteredAddOnChargeAmount entered)
											invoked.TotalAddOnChargeAmount 		= EnteredAddOnChargeAmount
											invoked.AddOnChargeAmount			= EnteredAddOnChargeAmount
										else
											invoked.TotalAddOnChargeAmount 		= TotalAddOnChargeAmount
											invoked.AddOnChargeAmount			= TotalAddOnChargeAmount
								else


									invoke MemoCreate PayablesInvoiceAddOnCharge
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										fill in fields from this instance
										invoked.PayablesInvoice					= MatchInvoiceImportRel.PayablesInvoice
										invoked.PayablesInvoiceAddOnCharge		= 2

										if  (Quantity < 0)
											invoked.EnteredQuantity				= Quantity
										else
											invoked.EnteredQuantity				= Quantity * -1

										if  (UnitCost < 0)
											invoked.EnteredUnitCost				= UnitCost * -1
										else
											invoked.EnteredUnitCost				= UnitCost

										if  (MatchInvoiceImportRel.ServiceContract entered)
											invoked.Contract					= MatchInvoiceImportRel.ServiceContract
											invoked.ContractLine				= LineNumber
										else
											invoked.PurchaseOrder				= DerivedPurchaseOrder
											invoked.PurchaseOrderLine			= LineNumber
		
								        invoked.RecordSource					= "LM"
										invoked.BypassInvoiceErrorChecking		= true
										invoked.BypassAllowUpdate				= true
										invoked.CreateFromBatchSwitch			= true
										
										if (EnteredAddOnChargeAmount entered)
											invoked.TotalAddOnChargeAmount 		= EnteredAddOnChargeAmount
											invoked.AddOnChargeAmount			= EnteredAddOnChargeAmount
										else
											invoked.TotalAddOnChargeAmount 		= TotalAddOnChargeAmount
											invoked.AddOnChargeAmount			= TotalAddOnChargeAmount
							else

								invoke Create PayablesInvoiceAddOnCharge
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.PayablesInvoice					= MatchInvoiceImportRel.PayablesInvoice
									invoked.PurchaseOrder					= DerivedPurchaseOrder
									invoked.PurchaseOrderLine				= LineNumber
									invoked.EnteredUnitCost					= UnitCost
									invoked.EnteredQuantity					= Quantity
									if (EnteredAddOnChargeAmount entered)
										invoked.TotalAddOnChargeAmount 		= EnteredAddOnChargeAmount
										invoked.AddOnChargeAmount			= EnteredAddOnChargeAmount
									else
										invoked.TotalAddOnChargeAmount 		= TotalAddOnChargeAmount
										invoked.AddOnChargeAmount			= TotalAddOnChargeAmount
									invoked.BypassInvoiceErrorChecking		= true
									invoked.BypassAllowUpdate				= true
									invoked.CreateFromBatchSwitch			= true

						else

							invoke CreateMiscellaneousAddOnCosts PayablesInvoiceAddOnCharge
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
								invoked.PurchaseOrder					= DerivedPurchaseOrder
								invoked.PayablesInvoice					= MatchInvoiceImportRel.PayablesInvoice
								invoked.EnteredUnitCost					= EnteredAddOnChargeAmount
								invoked.EnteredQuantity					= 1
								invoked.MiscellaneousAccount			= PostingAccount
								invoked.AddOnChargeAmount				= EnteredAddOnChargeAmount
								invoked.TotalDistributionAmount			= EnteredAddOnChargeAmount
								invoked.BypassInvoiceErrorChecking		= true
								invoked.BypassAllowUpdate				= true
								
						if (ErrorOccurred)

							RecordInError = true
							invoke SetError MatchInvoiceImportRel
								invoked.PrmErrorMessage					= LocalErrorMessage
						else
							if (Quantity entered
							and UnitCost entered)
								LocalInterfacedAddOnCostsAmountTotal	+= Quantity * UnitCost
							else
							if  (TotalAddOnChargeAmount entered)
								LocalInterfacedAddOnCostsAmountTotal	+= TotalAddOnChargeAmount
							else
								LocalInterfacedAddOnCostsAmountTotal	+= EnteredAddOnChargeAmount

							if  (Sequence entered
							and  !OtherAddOnChargesForSameLineRel exists)

								invoke FastDelete PayablesInvoiceDetailImportRel
							

							invoke FastDelete
	

		SpreadAddOnChargeLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				PrmVendor					is a Vendor
					context of PrmVendorGroup		
				PrmProcessLevel				is a PayablesProcessLevel
				PrmAuthorityCode			is a PayablesAuthorityCode
					context of PrmVendorGroup			
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				MatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				AllowMatchItemOrVendorItem			is Boolean
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult
				PrmVendorGroup				is a VendorGroup	

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"
			Local Fields
				LocalInterfacedAddOnCostsAmountTotal	is an InternationalAmount
				LocalInterfacedAddOnCostsCount			is Numeric 10
				LocalInterfaceAddOnCostsErrorCount		is Numeric 10

			Instance Selection
				where (RunGroup	= PrmRunGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (PrmVendor            			  not entered
				or      Vendor	= PrmVendor)
				and    (PrmVendorGroup					not entered		
				or      Company.PayablesCompany.VendorGroup = PrmVendorGroup))

			Sort Order
				RunGroup
				Company
				Vendor
				OldVendor
				EDINumber
				Invoice
				Suffix
				Sequence

			Action Rules

				Empty Set Rules
					invoke MatchDistributionLoad MatchInvoiceDistributionImport
						invoked.PrmFinanceEnterpriseGroup   = PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
						invoked.PrmVendor					= PrmVendor
						invoked.PrmProcessLevel				= PrmProcessLevel
						invoked.PrmAuthorityCode			= PrmAuthorityCode
				        invoked.InvoiceProcess				= InvoiceProcess
						invoked.MatchPoint					= MatchPoint
						invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
						invoked.PrmInterfaceRun				= PrmInterfaceRun
						invoked.PrmVendorGroup				= PrmVendorGroup	

				RunGroup Set Rules
					Entrance Rules

					Exit Rules
						LocalResultID					= PrmInterfaceRun
						invoke Update LocalInterfaceResultsRel
							invoked.InterfacedAddOnCostsCount		+= LocalInterfacedAddOnCostsCount
						

						invoke MatchDistributionLoad MatchInvoiceDistributionImport
							invoked.PrmFinanceEnterpriseGroup   = PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmVendor					= PrmVendor
							invoked.PrmProcessLevel				= PrmProcessLevel
							invoked.PrmAuthorityCode			= PrmAuthorityCode
					        invoked.InvoiceProcess				= InvoiceProcess
							invoked.MatchPoint					= MatchPoint
							invoked.AllowMatchItemOrVendorItem			= AllowMatchItemOrVendorItem
							invoked.PrmInterfaceRun				= PrmInterfaceRun
							invoked.PrmVendorGroup				= PrmVendorGroup	


				Instance Rules

					if     (Sequence not entered
					and     not AddOnCharge.SpreadMethod.NoSpread
					and    (PrmProcessLevel            			  not entered
					or      MatchInvoiceImportRel.ProcessLevel		= PrmProcessLevel)
					and    (PrmAuthorityCode       			      not entered
					or      MatchInvoiceImportRel.AuthorityCode	= PrmAuthorityCode)
	                and     PayablesCompanyRel.Company.FinanceEnterpriseGroup         = PrmFinanceEnterpriseGroup
					and     !MatchInvoiceImportRel.RecordInError)

					
						RecordInError 						= false
						ErrorOccurred						= false
						LocalPurchaseOrder					= DerivedPurchaseOrder
						LocalInterfacedAddOnCostsCount		+= 1

						if  (MatchInvoiceImportRel.ServiceContract entered)
							ErrorOccurred			= true
							LocalErrorMessage		= LineAOCNotAllowedOnServiceContractMsg
						else
						if  (MatchInvoiceImportRel.ProcessType.Basic
						or   MatchInvoiceImportRel.ProcessType.Freight)
							ErrorOccurred			= true
							LocalErrorMessage		= AOCNotAllowedForExpenseInvoiceMsg
						else

							invoke Create PayablesInvoiceSpreadAddOnCharge
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								invoked.Company							= Company 
								invoked.Vendor							= Vendor
								invoked.PayablesInvoice					= MatchInvoiceImportRel.PayablesInvoice
								invoked.PurchaseOrder					= DerivedPurchaseOrder
								invoked.AddOnCharge						= AddOnCharge
								invoked.SpreadAmount					= EnteredAddOnChargeAmount
								invoked.AddOnChargePercent				= AddOnChargePercent
								invoked.BypassAllowUpdate				= true
								
						if (ErrorOccurred)

							RecordInError = true
							invoke SetError MatchInvoiceImportRel
								invoked.PrmErrorMessage					= LocalErrorMessage
						else
							if (Quantity entered
							and UnitCost entered)
								LocalInterfacedAddOnCostsAmountTotal	+= Quantity * UnitCost
							else
							if  (TotalAddOnChargeAmount entered)
								LocalInterfacedAddOnCostsAmountTotal	+= TotalAddOnChargeAmount
							else
								LocalInterfacedAddOnCostsAmountTotal	+= EnteredAddOnChargeAmount
							if  (Sequence entered
							and  !OtherAddOnChargesForSameLineRel exists)

								invoke FastDelete PayablesInvoiceDetailImportRel
							

							invoke FastDelete
	
						commit transaction  
