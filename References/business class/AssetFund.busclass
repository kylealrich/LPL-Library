AssetFund is a BusinessClass
	owned by am
	prefix is AsstF
	
	Ontology
		symbolic key is AssetFund
		
	Patterns
		implements StaticJava
		
	Persistent Fields
		AccountingEntity				
		AccountingUnit					
		Fund 							is an AssetFundField	
		Project			
		FinanceDimension1
		FinanceDimension2
		FinanceDimension3
		FinanceDimension4
		FinanceDimension5
		FinanceDimension6
		FinanceDimension7
		FinanceDimension8
		FinanceDimension9
		FinanceDimension10
		AssetManagementInterfaceResult
			delete ignored
		TransactionAmount				is an InternationalAmount
			default label is "Amount"
			precision is LocalCurrency.NumberOfDecimals 
		PercentContribution				is Percent size 9.6
		RecordType						is Numeric 1
			States
				Active				value is 0
				Old 				value is 1
				AdjustmentAddition 	value is 2
			
	Transient Fields
		LocalCurrency					is a FromCurrency
			derive value from Asset.Currency		
		UpdateFromTransfer				is Boolean
			
	Local Fields
		LocalProject					is like Project
		LocalFinanceDimension1			is like FinanceDimension1
		LocalFinanceDimension2			is like FinanceDimension2
		LocalFinanceDimension3			is like FinanceDimension3
		LocalFinanceDimension4			is like FinanceDimension4
		LocalFinanceDimension5			is like FinanceDimension5
		LocalFinanceDimension6			is like FinanceDimension6
		LocalFinanceDimension7			is like FinanceDimension7
		LocalFinanceDimension8			is like FinanceDimension8
		LocalFinanceDimension9			is like FinanceDimension9
		LocalFinanceDimension10			is like FinanceDimension10
		LocalAccountingEntity			is like AccountingEntity
		LocalAccountingUnit				is like AccountingUnit
		LocalFund 						is an AssetFundField	
		LocalAssetItem					is like AssetItem
		LocalAssetItemFundsTotal		is an InternationalAmount
		LocalItemCost					is an InternationalAmount
		LocalPercentContribution		is Percent size 9.6
		LocalTransactionAmount			is an InternationalAmount

	Derived Fields
		WholeNumberPercent is a DerivedField
			type is Numeric 3		
			return (PercentContribution * 100)
				
		DerivedTransactionAmount is a DerivedField
			type is like InternationalAmount
			return sum AssetItemFundsRel.TransactionAmount	
			
		AssetTransferAmount is a DerivedField
			type is like InternationalAmount
			return AssetTransferRel.AssetTransferTo.ToItemTotalTransactionCost	
		
		AssetFundTransferAmount is a DerivedField
			type is like InternationalAmount
			return sum TransferredAssetItemsRel.DerivedTransferAmount
			
		TransferFundPercentOfTotal is a DerivedField
			type is Decimal size 9.6
			return (AssetFundTransferAmount / AssetTransferAmount)	
			
		CalculatedFundAmountsMayBeRounded is a MessageField
			"CalculatedFundAmountsMayBeRounded"				
			
	Conditions
		FundOverride
			restricted
			when (Asset.FundOverride)
			
		UpdateAllowed
			restricted
			when (FundOverride
			and Asset.FundOverride = true)	
			
		AccountingUnitIsFundDimension
			restricted
			when (FinanceEnterpriseGroup.FundDimension = 1)
			
		IsTransferred
			restricted
			when (TransferredAssetItemsRel exists)
			
        AccountingUnitSelected
        	when (AssetFundControlRel.AccountingUnit)
        ProjectSelected
        	when (AssetFundControlRel.Project)      	
		FinanceDimension1Selected
        	when (AssetFundControlRel.FinanceDimension1)
		FinanceDimension2Selected
        	when (AssetFundControlRel.FinanceDimension2)
		FinanceDimension3Selected
        	when (AssetFundControlRel.FinanceDimension3)
		FinanceDimension4Selected
        	when (AssetFundControlRel.FinanceDimension4)
		FinanceDimension5Selected
        	when (AssetFundControlRel.FinanceDimension5)
		FinanceDimension6Selected
        	when (AssetFundControlRel.FinanceDimension6)
		FinanceDimension7Selected
        	when (AssetFundControlRel.FinanceDimension7)
		FinanceDimension8Selected
        	when (AssetFundControlRel.FinanceDimension8)
		FinanceDimension9Selected
        	when (AssetFundControlRel.FinanceDimension9)
		FinanceDimension10Selected
        	when (AssetFundControlRel.FinanceDimension10)

		IsNotReleasedOrDisposed
			restricted
			when (FundOverride
			and Asset.FundOverride = true
			and	!Asset.Status.Released
			and !Asset.Status.Disposed
			and !AssetInProgress)

		AssetInProgress
			restricted
			when ((Asset.Status.PostRelease)
			and	 (Asset.AssetProcess.Transfer
			or	 Asset.AssetProcess.Disposal
			or	 Asset.AssetProcess.Reinstatement
			or	 Asset.AssetProcess.Revalue
			or	 Asset.AssetProcess.Impairment))

		AllowDelete
			restricted
			when (FundOverride
			and Asset.FundOverride = true
			and	!Asset.Status.Released
			and !Asset.Status.Disposed
			and !AssetInProgress
			and Asset.Status.Unreleased)

		AllowDeleteFund
			restricted
			when (FundOverride
			and Asset.FundOverride = true
			and	!Asset.Status.Released
			and !Asset.Status.Disposed
			and !AssetInProgress
			and Asset.AdjustmentInProgress)

		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
		
	Relations
		AssetFundControlRel
			one-to-one relation to AssetFundControl
			Field Mapping uses symbolic key
				related.AssetFundControl		= FinanceEnterpriseGroup
							
		ThisInstanceRel
			one-to-one relation to AssetFund
			Field Mapping uses ByAssetFund				
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset					= Asset 
				related.RecordType				= 0
				related.AccountingEntity		= AccountingEntity		
				related.AccountingUnit			= AccountingUnit
				related.Project					= Project
				related.FinanceDimension1		= FinanceDimension1
				related.FinanceDimension2		= FinanceDimension2
				related.FinanceDimension3		= FinanceDimension3				
				related.FinanceDimension4		= FinanceDimension4
				related.FinanceDimension5		= FinanceDimension5
				related.FinanceDimension6		= FinanceDimension6		
				related.FinanceDimension7		= FinanceDimension7
				related.FinanceDimension8		= FinanceDimension8
				related.FinanceDimension9		= FinanceDimension9
				related.FinanceDimension10		= FinanceDimension10


	   	AssetItemsRel
            one-to-many relation to AssetItem
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
                related.Asset 					= Asset

		AssetFundsRel
			one-to-many relation to AssetFund
			Field Mapping uses ByAssetFund
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset					= Asset 
				related.RecordType				= 0

		ToAssetTransferFundRel
			one-to-one relation to AssetFund
			Field Mapping uses ByAssetFund
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup 
				related.Asset							= AssetTransferRel.AssetTransferTo.ToAsset
				related.RecordType						= 0
				related.AccountingEntity				= AccountingEntity
				related.AccountingUnit					= AccountingUnit
				related.Project							= Project	
				related.FinanceDimension1				= FinanceDimension1
				related.FinanceDimension2				= FinanceDimension2
				related.FinanceDimension3				= FinanceDimension3
				related.FinanceDimension4				= FinanceDimension4
				related.FinanceDimension5				= FinanceDimension5
				related.FinanceDimension6				= FinanceDimension6
				related.FinanceDimension7				= FinanceDimension7
				related.FinanceDimension8				= FinanceDimension8
				related.FinanceDimension9				= FinanceDimension9
				related.FinanceDimension10				= FinanceDimension10

		LocalAssetFundRel
			one-to-one relation to AssetFund
			Field Mapping uses ByAssetFund
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup 
				related.Asset							= Asset
				related.RecordType						= 0
				related.AccountingEntity				= LocalAccountingEntity
				related.AccountingUnit					= LocalAccountingUnit
				related.Project							= LocalProject
				related.FinanceDimension1				= LocalFinanceDimension1
				related.FinanceDimension2				= LocalFinanceDimension2
				related.FinanceDimension3				= LocalFinanceDimension3
				related.FinanceDimension4				= LocalFinanceDimension4
				related.FinanceDimension5				= LocalFinanceDimension5
				related.FinanceDimension6				= LocalFinanceDimension6
				related.FinanceDimension7				= LocalFinanceDimension7
				related.FinanceDimension8				= LocalFinanceDimension8
				related.FinanceDimension9				= LocalFinanceDimension9
				related.FinanceDimension10				= LocalFinanceDimension10
			
		OtherAssetFundsRel
			one-to-many relation to AssetFund
			Field Mapping uses ByAssetFund
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset					= Asset 
				related.RecordType				= 0 
			Instance Selection
				where (related.AssetFund != AssetFund)			
		
						
		AssetItemFundsRel
			one-to-many relation to AssetItemFund
			Field Mapping uses ByAssetFundItem
				related.Asset					= Asset 
				related.RecordType				= 0 
				related.AccountingEntity		= AccountingEntity		
				related.AccountingUnit			= AccountingUnit
				related.Project					= Project
				related.FinanceDimension1		= FinanceDimension1
				related.FinanceDimension2		= FinanceDimension2
				related.FinanceDimension3		= FinanceDimension3				
				related.FinanceDimension4		= FinanceDimension4
				related.FinanceDimension5		= FinanceDimension5
				related.FinanceDimension6		= FinanceDimension6		
				related.FinanceDimension7		= FinanceDimension7
				related.FinanceDimension8		= FinanceDimension8
				related.FinanceDimension9		= FinanceDimension9
				related.FinanceDimension10		= FinanceDimension10

		TransferredAssetItemsRel
			one-to-many relation to AssetItemFund
			Field Mapping uses ByAssetFundItem
				related.Asset					= Asset 
				related.RecordType				= 0
				related.AccountingEntity		= AccountingEntity		
				related.AccountingUnit			= AccountingUnit
				related.Project					= Project
				related.FinanceDimension1		= FinanceDimension1
				related.FinanceDimension2		= FinanceDimension2
				related.FinanceDimension3		= FinanceDimension3				
				related.FinanceDimension4		= FinanceDimension4
				related.FinanceDimension5		= FinanceDimension5
				related.FinanceDimension6		= FinanceDimension6		
				related.FinanceDimension7		= FinanceDimension7
				related.FinanceDimension8		= FinanceDimension8
				related.FinanceDimension9		= FinanceDimension9
				related.FinanceDimension10		= FinanceDimension10			
			Instance Selection
				where (related.IsTransferred)
		
		AssetTransferRel
			one-to-one relation to AssetTransfer
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset					= Asset
				related.AssetTransfer			= Asset.LastAssetSequence

		AllAssetItemFundsRel
			one-to-many relation to AssetItemFund
			Field Mapping uses ByAssetFundItem
				related.Asset					= Asset 
				related.RecordType				= 0
			
		AssetItemFundRel		
			one-to-one relation to AssetItemFund
			Field Mapping uses ByAssetFundItem
				related.Asset					= Asset 
				related.RecordType				= 0
				related.AccountingEntity		= LocalAccountingEntity		
				related.AccountingUnit			= LocalAccountingUnit
				related.Project					= Project
				related.FinanceDimension1		= FinanceDimension1
				related.FinanceDimension2		= FinanceDimension2
				related.FinanceDimension3		= FinanceDimension3				
				related.FinanceDimension4		= FinanceDimension4
				related.FinanceDimension5		= FinanceDimension5
				related.FinanceDimension6		= FinanceDimension6		
				related.FinanceDimension7		= FinanceDimension7
				related.FinanceDimension8		= FinanceDimension8
				related.FinanceDimension9		= FinanceDimension9
				related.FinanceDimension10		= FinanceDimension10
				related.AssetItem				= LocalAssetItem			

		AssetFundRecordTypeRel
			one-to-one relation to AssetFund
			Field Mapping uses ByAssetFund				
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset					= Asset
				related.RecordType				= 1
				related.AccountingEntity		= AccountingEntity		
				related.AccountingUnit			= AccountingUnit
				related.Project					= Project
				related.FinanceDimension1		= FinanceDimension1
				related.FinanceDimension2		= FinanceDimension2
				related.FinanceDimension3		= FinanceDimension3				
				related.FinanceDimension4		= FinanceDimension4
				related.FinanceDimension5		= FinanceDimension5
				related.FinanceDimension6		= FinanceDimension6		
				related.FinanceDimension7		= FinanceDimension7
				related.FinanceDimension8		= FinanceDimension8
				related.FinanceDimension9		= FinanceDimension9
				related.FinanceDimension10		= FinanceDimension10

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
            Field Mapping uses symbolic key
            	related.Company = Asset.Company
					
	Sets
		ByAssetFund
			Sort Order
				FinanceEnterpriseGroup
				Asset
				RecordType
				AccountingEntity
				AccountingUnit
				Project			
				FinanceDimension1
				FinanceDimension2
				FinanceDimension3
				FinanceDimension4
				FinanceDimension5
				FinanceDimension6
				FinanceDimension7
				FinanceDimension8
				FinanceDimension9
				FinanceDimension10

		ByAssetRecordFund
			Sort Order
				FinanceEnterpriseGroup
				Asset
				RecordType
				AssetFund
				AccountingEntity
				AccountingUnit
				Project			
				FinanceDimension1
				FinanceDimension2
				FinanceDimension3
				FinanceDimension4
				FinanceDimension5
				FinanceDimension6
				FinanceDimension7
				FinanceDimension8
				FinanceDimension9
				FinanceDimension10
		
		ByAssetManagementInterfaceResult
            indexed
            Sort Order
                FinanceEnterpriseGroup
				AssetManagementInterfaceResult
                Asset
                AssetFund
								
	Field Rules
		AccountingEntity
			required
				"<actor.context.FinanceEnterpriseGroup.AccountingEntityLabel>IsRequired"					
		AccountingUnit					
			if (AccountingUnitIsFundDimension)
				required	
					"<actor.context.FinanceEnterpriseGroup.AccountingUnitLabel>IsRequired"
			if (AccountingUnit entered)
				constraint (AccountingUnitSelected)
					"<actor.context.FinanceEnterpriseGroup.AccountingUnitLabel>IsNotAValidFundDimension"
		Project		
			if (Project entered)
				constraint (ProjectSelected)
					"<actor.context.FinanceEnterpriseGroup.ProjectLabel>IsNotAValidFundDimension"					
		FinanceDimension1
			if (!AccountingUnitIsFundDimension)
				required	
			if (FinanceDimension1 entered)
				constraint (FinanceDimension1Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension1Label>IsNotAValidFundDimension"		
		FinanceDimension2
			if (FinanceDimension2 entered)
				constraint (FinanceDimension2Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension2Label>IsNotAValidFundDimension"		
		FinanceDimension3
			if (FinanceDimension3 entered)
				constraint (FinanceDimension3Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension3Label>IsNotAValidFundDimension"
		FinanceDimension4
			if (FinanceDimension4 entered)
				constraint (FinanceDimension4Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension4Label>IsNotAValidFundDimension"
		FinanceDimension5
			if (FinanceDimension5 entered)
				constraint (FinanceDimension5Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension5Label>IsNotAValidFundDimension"
		FinanceDimension6
			if (FinanceDimension6 entered)
				constraint (FinanceDimension6Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension6Label>IsNotAValidFundDimension"
		FinanceDimension7
			if (FinanceDimension7 entered)
				constraint (FinanceDimension7Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension7Label>IsNotAValidFundDimension"
		FinanceDimension8
			if (FinanceDimension8 entered)
				constraint (FinanceDimension8Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension8Label>IsNotAValidFundDimension"
		FinanceDimension9
			if (FinanceDimension9 entered)
				constraint (FinanceDimension9Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension9Label>IsNotAValidFundDimension"
		FinanceDimension10
			if (FinanceDimension10 entered)
				constraint (FinanceDimension10Selected)
					"<actor.context.FinanceEnterpriseGroup.FinanceDimension10Label>IsNotAValidFundDimension"	
		RecordType
			default to 0

	Rule Blocks
		UpdateFund
			if (Asset.FundOverride = true)
				TransactionAmount 	= (PercentContribution * Asset.ItemTotalTransactionCost)		
			else
				TransactionAmount	= sum AssetItemFundsRel.TransactionAmount
				PercentContribution = (TransactionAmount / Asset.ItemTotalTransactionCost)			
	

	Actions
		Create is an Action
			valid when (IsNotReleasedOrDisposed)

			Field Rules
				PercentContribution
					required
						"PercentContributionIsRequired"
					
					
			Action Rules
				constraint (!ThisInstanceRel exists)
					"FundIsAlreadyAssignedToAsset"
				if (FinanceEnterpriseGroup.FundDimension.AccountingUnit)
					constraint (AccountingUnit entered)
						"FundDimensionIsRequired"
					Fund = AccountingUnit	
						
				else
					constraint (FinanceDimension1 entered)	
						"FundDimensionIsRequired"
					Fund = FinanceDimension1	

				if (Asset.Status.PostRelease
				and parentcontext.name = "AssetFund")
					RecordType		= 2
					invoke Update Asset.UnreleasedAssetAdjustmentRel
						invoked.FundsAllocationUpdated         = true

				if (Asset.FundOverride = true)
					if (sum AssetFundsRel.PercentContribution = 100)
						TransactionAmount = (Asset.ItemTotalTransactionCost - sum OtherAssetFundsRel.TransactionAmount)
					else
						TransactionAmount 	= (PercentContribution * Asset.ItemTotalTransactionCost)
					if (RecordType.Active)
						invoke UpdateItemFunds

		CreateFromItemFund is a Create Action
			restricted
			Exit Rules
				include UpdateFund	
				
		CreateFromAssetGenerate is a Create Action
			restricted				

		CreateOldFund is a Create Action
			restricted

		UpdateFromAssetGenerate is an Update Action
			restricted
						
		Update is an Action
			valid when (IsNotReleasedOrDisposed)
			Field Rules
				Fund
					cannot be changed
						"CannotBeChanged"	
				AccountingEntity
					cannot be changed
						"CannotBeChanged"	
				AccountingUnit
					cannot be changed
						"CannotBeChanged"
				Project
					cannot be changed
						"CannotBeChanged"
				FinanceDimension1
					cannot be changed
						"CannotBeChanged"
				FinanceDimension2
					cannot be changed
						"CannotBeChanged"
				FinanceDimension3
					cannot be changed
						"CannotBeChanged"
				FinanceDimension4
					cannot be changed
						"CannotBeChanged"
				FinanceDimension5						
					cannot be changed
						"CannotBeChanged"
				FinanceDimension6						
					cannot be changed
						"CannotBeChanged"
				FinanceDimension7
					cannot be changed
						"CannotBeChanged"
				FinanceDimension8
					cannot be changed
						"CannotBeChanged"
				FinanceDimension9						
					cannot be changed
						"CannotBeChanged"
				FinanceDimension10						
					cannot be changed
						"CannotBeChanged"
				PercentContribution
					required
						"PercentContributionIsRequired"
												
			Action Rules
				if(Asset.Status.PostRelease)
					if(RecordType.Active)
						if(AssetFundRecordTypeRel not exist)
							invoke CreateOldFund AssetFund
								invoked.AccountingEntity		= old AccountingEntity
								invoked.AccountingUnit			= old AccountingUnit
								invoked.Fund					= old Fund
								invoked.Project					= old Project
								invoked.FinanceDimension1		= old FinanceDimension1
								invoked.FinanceDimension2 		= old FinanceDimension2
								invoked.FinanceDimension3 		= old FinanceDimension3
								invoked.FinanceDimension4		= old FinanceDimension4
								invoked.FinanceDimension5 		= old FinanceDimension5
								invoked.FinanceDimension6 		= old FinanceDimension6
								invoked.FinanceDimension7		= old FinanceDimension7
								invoked.FinanceDimension8 		= old FinanceDimension8
								invoked.FinanceDimension9 		= old FinanceDimension9
								invoked.FinanceDimension10		= old FinanceDimension10
								invoked.PercentContribution     = old PercentContribution
								invoked.TransactionAmount		= old TransactionAmount
								invoked.RecordType				= 1
							invoke Update Asset.UnreleasedAssetAdjustmentRel
								invoked.FundsAllocationUpdated         = true

				if (Asset.FundOverride = true)
					if (PercentContribution changed)
						TransactionAmount = (PercentContribution * Asset.ItemTotalTransactionCost)
					if(!Asset.Status.PostRelease)	
						invoke UpdateFromAssetFund AssetItemFundsRel
							invoked.PercentContribution	= PercentContribution			
			Exit Rules
				if (!Asset.Status.PostRelease)
					invoke UpdateFundAllocation Asset


		UpdateFromItemFund is an Instance Action   
			restricted
			Action Rules
				if (Asset.FundOverride 	= true)
					TransactionAmount 	= (PercentContribution * Asset.ItemTotalTransactionCost)
				else 	
					if (!AssetItemFundsRel exists)  
						invoke Delete
					else	
						LocalAssetItemFundsTotal	= sum AssetItemFundsRel.TransactionAmount 	
						TransactionAmount 			= LocalAssetItemFundsTotal		
						PercentContribution			= TransactionAmount / Asset.ItemTotalTransactionCost
				
																
		RefreshFund is an Instance Action
			restricted
			Action Rules
				if (Asset.FundOverride 	= true)
					TransactionAmount 	= (PercentContribution * Asset.ItemTotalTransactionCost)
				else 	
					if (!AssetItemFundsRel exists)  
						invoke Delete
					else	
						LocalAssetItemFundsTotal	= sum AssetItemFundsRel.TransactionAmount 	
						TransactionAmount 			= LocalAssetItemFundsTotal		
						PercentContribution			= TransactionAmount / Asset.ItemTotalTransactionCost
						
		
		RefreshFundAmount is an Instance Action
			restricted
			Parameters
				PrmPercentContribution		is Percent size 9.6
				PrmTransactionAmount		is an InternationalAmount	
			Action Rules
				if (PrmPercentContribution entered)
					PercentContribution		= PrmPercentContribution
				if (PrmTransactionAmount entered)	
					TransactionAmount 		= PrmTransactionAmount	
			
			
		
		UpdateItemFunds is an Instance Action
			restricted
			Action Rules
				for each AssetItemsRel
					LocalItemCost			= each.TransactionItemCost
					LocalAssetItem 			= each.AssetItem
					LocalAccountingEntity	= AccountingEntity
					LocalAccountingUnit		= AccountingUnit
					if (Fund not entered)
						if (FinanceEnterpriseGroup.FundDimension.AccountingUnit)
							constraint (AccountingUnit entered)
								"FundDimensionIsRequired"
							LocalFund = AccountingUnit
						else 
							constraint (FinanceDimension1 entered)	
								"FundDimensionIsRequired"
							LocalFund = FinanceDimension1	
					else 
						LocalFund				= Fund					
					if (Asset.FundOverride 	= true)	
						if (AssetItemFundRel exists)
							invoke Update AssetItemFundRel 
								invoked.PercentContribution 	= PercentContribution
								invoked.TransactionAmount		= (PercentContribution * each.TransactionItemCost)
								invoked.RecordsDeleted			= false
						else 
							invoke Create AssetItemFundRel
								invoked.Asset					= Asset
								invoked.AssetItem				= LocalAssetItem
								invoked.AccountingEntity		= LocalAccountingEntity		
								invoked.AccountingUnit			= LocalAccountingUnit
								invoked.Fund					= LocalFund								
								invoked.FinanceDimension1		= FinanceDimension1
								invoked.PercentContribution 	= PercentContribution
								invoked.TransactionAmount		= (PercentContribution * LocalItemCost)										
								fill in fields from this instance

		UpdateTransferToAssetFund is an Instance Action   
			restricted
			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"			
				PrmAccountingEntity		is an AccountingEntity		
				PrmAccountingUnit		is an AccountingUnit			
				PrmProject				is a Project
				PrmFinanceDimension1	is a FinanceDimension1
				PrmFinanceDimension2	is a FinanceDimension2
				PrmFinanceDimension3	is a FinanceDimension3
				PrmFinanceDimension4	is a FinanceDimension4
				PrmFinanceDimension5	is a FinanceDimension5
				PrmFinanceDimension6	is a FinanceDimension6
				PrmFinanceDimension7	is a FinanceDimension7
				PrmFinanceDimension8	is a FinanceDimension8
				PrmFinanceDimension9	is a FinanceDimension9
				PrmFinanceDimension10	is a FinanceDimension10
				PrmTransactionAmount	is an InternationalAmount
				PrmPercentContribution	is Percent size 9.6

			Action Rules
				LocalAccountingEntity 		= PrmAccountingEntity
				LocalAccountingUnit 		= PrmAccountingUnit			
				LocalProject				= PrmProject
				if (FinanceEnterpriseGroup.FundDimension.AccountingUnit)
					LocalFund 				= PrmAccountingUnit
				else 
					LocalFund 				= PrmFinanceDimension1
				LocalFinanceDimension1		= PrmFinanceDimension1
				LocalFinanceDimension2		= PrmFinanceDimension2
				LocalFinanceDimension3		= PrmFinanceDimension3
				LocalFinanceDimension4		= PrmFinanceDimension4
				LocalFinanceDimension5		= PrmFinanceDimension5
				LocalFinanceDimension6		= PrmFinanceDimension6
				LocalFinanceDimension7		= PrmFinanceDimension7
				LocalFinanceDimension8		= PrmFinanceDimension8
				LocalFinanceDimension9		= PrmFinanceDimension9
				LocalFinanceDimension10		= PrmFinanceDimension10
				LocalPercentContribution	= PrmPercentContribution
				if (AssetTransferRel.FullOrPartial.Full)
					LocalTransactionAmount 	= PrmPercentContribution * Asset.ItemTotalTransactionCost
					display "AF_520__Asset<Asset>__FinanceDimension1<LocalFinanceDimension1>__PercentContribution<LocalPercentContribution>__TransactionAmount<LocalTransactionAmount>"
				else 	
					LocalTransactionAmount	= PrmPercentContribution * AssetTransferRel.AssetTransferTo.ToItemTotalTransactionCost
					display "AF_522__Asset<Asset>__FinanceDimension1<LocalFinanceDimension1>__PercentContribution<LocalPercentContribution>__TransactionAmount<LocalTransactionAmount>"

				if (LocalAssetFundRel exists)
					invoke Update LocalAssetFundRel 
						invoked.TransactionAmount 		= LocalTransactionAmount
						invoked.PercentContribution		= LocalPercentContribution	
					invoke Update ToAssetTransferFundRel
						invoked.TransactionAmount 		= LocalTransactionAmount
						invoked.PercentContribution		= LocalPercentContribution			
				else	
					invoke CreateFromAssetGenerate AssetFund
						invoked.Asset					= Asset
						invoked.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
						invoked.AccountingEntity 		= PrmAccountingEntity
						invoked.AccountingUnit 			= PrmAccountingUnit			
						invoked.Project					= PrmProject
						if (FinanceEnterpriseGroup.FundDimension.AccountingUnit)
							invoked.Fund 				= PrmAccountingUnit
						else 
							invoked.Fund 				= PrmFinanceDimension1
						invoked.FinanceDimension1		= PrmFinanceDimension1
						invoked.FinanceDimension2		= PrmFinanceDimension2
						invoked.FinanceDimension3		= PrmFinanceDimension3
						invoked.FinanceDimension4		= PrmFinanceDimension4
						invoked.FinanceDimension5		= PrmFinanceDimension5
						invoked.FinanceDimension6		= PrmFinanceDimension6
						invoked.FinanceDimension7		= PrmFinanceDimension7
						invoked.FinanceDimension8		= PrmFinanceDimension8
						invoked.FinanceDimension9		= PrmFinanceDimension9
						invoked.FinanceDimension10		= PrmFinanceDimension10
						invoked.TransactionAmount 		= LocalTransactionAmount
						invoked.PercentContribution		= LocalPercentContribution	
						fill in fields from this instance	
							except invoked.AssetFund

		UpdateAssetFund is an Update Action
			restricted
		
		Delete is an Action
			valid when (AllowDelete)	
			Entrance Rules
			Exit Rules
				invoke Delete AssetItemFundsRel

		DeleteAssetFund is an Action
			default label is "Delete"
			valid when (AllowDeleteFund)
			Action Rules
				invoke UpdateAssetItemFund AssetItemFundsRel
					invoked.RecordsDeleted = true
				if(AssetFundRecordTypeRel not exists)
					invoke CreateOldFund
						fill in fields from this instance
						invoked.RecordType				=  1
				invoke DeleteFund
			
		DeleteFromAsset is a Delete Action
			restricted

		DeleteFund is a Delete Action
			restricted	

