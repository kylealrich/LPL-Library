BurdenCode is a BusinessClass
	owned by ic
	prefix is IBT
	classic name is ICBRDNTMPL

	Ontology
		symbolic key is BurdenCode
			classic set name is IBTSET1
			classic name for InventoryLocation is LOCATION

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		BurdenType                   is AlphaUpper size 1
			States
				FlatAmount                          value is "A"
				AmountPerFinishedGoodsUnit          value is "F"
				PercentOfFinishedGoodsInventoryCost value is "P"
				AmountPerBurdenUnit                 value is "U"

		Description
		Vendor
		FlatAmount                   is an InternationalAmount
			classic name is EST-AMT
		AmountPerUnit                is an InternationalAmount
			classic name is EST-AMT-PER-U
		PerBurdenUnitQuantity        is Decimal size 13.2
			classic name is EST-QTY
		PerBurdenUnitMultiplier      is Decimal size 4.1
			classic name is EST-MULT
		PercentageOfInventoryCost    is Percent size 5.2
			classic name is EST-PCT
		AmountPerUnitLabel           is Alpha size 10
			classic name is AMT-PER-U-LBL
		PerBurdenUnitQuantityLabel   is Alpha size 10
			classic name is QTY-LBL
		PerBurdenUnitMultiplierLabel is Alpha size 10
			classic name is MULT-LBL
		Active                       is Boolean
			classic name is ACTIVE-STATUS
		InUse                        is Boolean
			classic name is USED-FL
		AlternateUnitOfMeasure       is a UnitOfMeasure
			classic name is EST-UOM
		DistributionAccount          is a FinanceCodeBlockFull 
			classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
			classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT

	Local Fields

		LocalItem									is an Item

	Derived Fields
		RepresentativeText is a StringField
			type is Text
			default label is "BurdenCode"
			BurdenCode " - " Description

		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforProductionOrder"

		ContextMessageText is a MessageField
			restricted
			"BurdenCode<BurdenCode>ForLocation<InventoryLocation>InCompany<Company>"

		AmountLabel									is a MessageField
			restricted
			"UnitCost"
		QuantityLabel								is a MessageField
			restricted
			"Quantity"
		MultiplierLabel								is a MessageField
			restricted
			"Multiplier"

	Conditions

		IsAmountPerBurdenUnit
			classic name is AMTPERBRDNUNIT
			restricted
			when (BurdenType.AmountPerBurdenUnit)

		IsAmountPerFGUnit
			classic name is AMTPERFGBRDN
			restricted
			when (BurdenType.AmountPerFinishedGoodsUnit)

		IsInUse
			classic name is BRDNUSEDONTMPL
			restricted
			when (InUse)

		IsFlatAmount
			classic name is FLATAMOUNTBRDN
			restricted
			when (BurdenType.FlatAmount)

		IsPercentOfFGInventoryCost
			classic name is PCTOFFGINVCOST
			restricted
			when (BurdenType.PercentOfFinishedGoodsInventoryCost)

		HasVendor
			classic name is VENDOR-ENTERED
			restricted
			when (Vendor entered)

		CompanyEntered
			restricted
			when (Company entered)

		InventoryLocationEntered
			restricted
			when (InventoryLocation entered)

		BurdenCodeEntered
			restricted
			when (BurdenCode entered)

	Relations

		PayablesCompanyRel
			classic name is APCOMPANY
			one-to-one relation to PayablesCompany
			valid when (HasVendor)
			Field Mapping uses symbolic key
				related.Company = Company

		FinishedGoodsBurdensRel is a FinishedGoodsBurden set

		FinishedGoodsBurdenCommentsRel is a FinishedGoodsBurdenComment set

		ActiveKitItemRel
			one-to-many relation to KitItem
			Field Mapping uses symbolic key
				related.ItemGroup      				= Company.ItemGroup
				related.KitItem.Company        		= Company
				related.KitItem.InventoryLocation 	= InventoryLocation
				related.KitItem.Item				= LocalItem
			Instance Selection
				where (related.IsActive)


	Attach Rules
		constraint (Active)
			"BurdenCode<BurdenCode>MustBeActive"

	Field Rules
		BurdenType
			required
			cannot be changed

		Description
			required

		DistributionAccount
			required




		FlatAmount
			if (BurdenType.FlatAmount)
				required

				constraint (FlatAmount > 0)
					"AmountShouldBeGreaterThanZero"

			else
				cannot be entered
					"AmountCannotBeEnteredForBurdenType:<BurdenType>"

		AmountPerUnit
			if (BurdenType.AmountPerFinishedGoodsUnit
			or BurdenType.AmountPerBurdenUnit)
				required

				constraint (AmountPerUnit > 0)
					"AmountPerUnitShouldBeGreaterThanZero"

			else
			if (BurdenType.FlatAmount
			or BurdenType.PercentOfFinishedGoodsInventoryCost)
				cannot be entered
					"AmountPerUnitCannotBeEnteredForBurdenType:<BurdenType>"


		AmountPerUnitLabel
			initial value is AmountLabel
			if (BurdenType.AmountPerBurdenUnit)
				default to AmountLabel
			else
				initialize AmountPerUnitLabel
				cannot be entered
					"AmountPerUnitLabelCannotBeEnteredForBurdenType:<BurdenType>"

		PerBurdenUnitQuantityLabel
			initial value is QuantityLabel
			if (BurdenType.AmountPerBurdenUnit)
				default to QuantityLabel
			else
				initialize PerBurdenUnitQuantityLabel
				cannot be entered
					"QuantityLabelCannotBeEnteredForBurdenType:<BurdenType>"

		PerBurdenUnitQuantity
			if (BurdenType.AmountPerBurdenUnit)
				required

				constraint (PerBurdenUnitQuantity > 0)
					"QuantityShouldBeGreaterThanZero"

			else
				cannot be entered
					"QuantityCannotBeEnteredForBurdenType:<BurdenType>"

		PerBurdenUnitMultiplierLabel
			initial value is MultiplierLabel
			if (BurdenType.AmountPerBurdenUnit)
				default to MultiplierLabel
			else
				initialize PerBurdenUnitMultiplierLabel
				cannot be entered
					"MultiplierLabelCannotBeEnteredForBurdenType:<BurdenType>"

		PerBurdenUnitMultiplier
			if (BurdenType.AmountPerBurdenUnit)
				default to 1

				constraint (PerBurdenUnitMultiplier > 0)
					"MultiplierShouldBeGreaterThanZero"

			else
				cannot be entered
					"MultiplierCannotBeEnteredForBurdenType:<BurdenType>"

		PercentageOfInventoryCost
			if (BurdenType.PercentOfFinishedGoodsInventoryCost)
				required

				constraint (PercentageOfInventoryCost > 0)
					"PercentageShouldBeGreaterThanZero"

			else
				cannot be entered
					"PercentageCannotBeEnteredForBurdenType:<BurdenType>"

		Active
			initial value is true
			if (Active changed)
				if (old Active
				and !Active)
					for each FinishedGoodsBurdensRel
						LocalItem		= each.Item
						constraint (ActiveKitItemRel not exist)
							"BurdenIsUsedOnActiveKitItem:<LocalItem>"




	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action


