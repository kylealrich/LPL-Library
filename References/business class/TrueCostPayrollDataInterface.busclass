TrueCostPayrollDataInterface is a BusinessClass
	owned by truecost
	prefix is TCPDI
	
	Ontology
		symbolic key is TrueCostPayrollDataInterface
		
	
	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing
		
	Persistent Fields
		Company						is Alpha 12
		Department					is Alpha 15
		Position					is Alpha 15
		PerEndDate					is Date
		WageAmount					is Decimal 19.3
		Hours						is Decimal 19.3
	
	Field Rules
		Company
			required
		Position
			required
		PerEndDate
			required
		WageAmount
			required
		Hours
			required
		Department
			required
			
	Local Fields
		LocalTrueCostConfiguration		is like TrueCostConfiguration
		LocalDate						is Date
		LocalLastDate					is Date
	Derived Fields
		ComputeAverage					is a ComputeField
			type is Decimal 19.3
			(WageAmount/Hours)
			
		LastDayOfMonth			is a DerivedField
			type is Date
			restricted
			while (!LocalDate is last day in month)
				LocalDate += 1 day
			return LocalDate
			
	
	Relations
		
		TrueCostResourceRateRel
			one-to-one relation to TrueCostResourceRateTable
			Field Mapping uses symbolic key
				related.TrueCostConfiguration = LocalTrueCostConfiguration
				related.TrueCostResourceRateTable.AccountingEntity = Company
				related.TrueCostResourceRateTable.Department = Department
				related.TrueCostResourceRateTable.ResourceName = Position
				related.TrueCostResourceRateTable.EffectivePeriod = LocalLastDate
				
		TrueCostPayrollDataInterfaceRel
			one-to-many relation to TrueCostPayrollDataInterface
			Field Mapping uses symbolic key
			Instance Selection
				where(related.Company		= 	Company)
				
			
	Actions

		Create is a Create Action
			
		Update is an Update Action
			
		Delete is a Delete Action
		
		GenerateResourceCapacityRates				is a Set Action
			Parameters
				PrmTCConfig						is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				
			Parameter Rules	
				PrmTCConfig
					required
						"True\CostConfigurationIsRequired"
			
			
			Local Fields
				
				LocalFirstCount						is Numeric 10
				LocalFinalCount						is Numeric 10
				LocalActor							is an Actor
			Action Rules
				
				Set Rules
					Entrance Rules
						LocalTrueCostConfiguration = PrmTCConfig
						LocalActor = actor
						LocalFirstCount = instance count of TrueCostPayrollDataInterfaceRel					
					Exit Rules
						LocalFinalCount =	LocalFirstCount - instance count of TrueCostPayrollDataInterfaceRel					
						send notification
							to LocalActor
							description is "GenerateResourceRateData"
							priority is high
							detail is "NumberOfSuccessfulRecords<LocalFinalCount>.___NumberOfFailedRecords<instance count of TrueCostPayrollDataInterfaceRel>.___TotalRecordsProcessed<LocalFirstCount>"
				
				
				Instance Rules
					LocalDate = PerEndDate
					LocalLastDate = LastDayOfMonth
					if (!LocalTrueCostConfiguration entered)
						LocalTrueCostConfiguration = PrmTCConfig
					if (!TrueCostResourceRateRel exists)
						invoke Create TrueCostResourceRateTable
							invoked.TrueCostResourceRateTable.TrueCostConfiguration = LocalTrueCostConfiguration
							invoked.TrueCostResourceRateTable.AccountingEntity 		= Company
							invoked.TrueCostResourceRateTable.Department 			= Department
							invoked.TrueCostResourceRateTable.ResourceName			= Position
							invoked.TrueCostResourceRateTable.EffectivePeriod		= LocalLastDate
							invoked.TrueCostResourceRateTable.WageAmount			= WageAmount
							invoked.TrueCostResourceRateTable.Hours					= Hours
							invoked.TrueCostResourceRateTable.AverageResourceRate	= ComputeAverage
							
					else
						invoke Update TrueCostResourceRateRel
							invoked.TrueCostResourceRateTable.WageAmount 		+= WageAmount
							invoked.TrueCostResourceRateTable.Hours 			+= Hours
							
					invoke Delete

					
						
				
