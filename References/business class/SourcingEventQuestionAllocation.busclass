SourcingEventQuestionAllocation is a BusinessClass
    owned by ss
    prefix is EVQA
    sql name is SEQCorrectAnswer

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Ontology
    	symbolic key is SourcingEventQuestionAllocation
    	
	Persistent Fields
		DisplayOrder
		ScoreAllocation
		
	Transient Fields
		NewDisplayOrder		is a DisplayOrder

	Local Fields
		LocalDisplayOrder	is a DisplayOrder

	Conditions
    	ValidCreateAction
    		restricted
    		when (SourcingEventQuestion.ResponseType.List
    		and	  SourcingEvent.InActionableState)

	Sets
		ByDisplayOrder
			duplicates
			Sort Order
				Company
				SourcingEvent
				SourcingEventQuestion
				DisplayOrder
				
	Actions
		Create is a Create Action
			valid when (ValidCreateAction)
			
			Field Rules
				DisplayOrder
					autosequence using SourcingEventQuestion.LastQuestionAnswerDisplayOrder

    		Action Rules
    			constraint (SourcingEventQuestion.ResponseType.List)
    				"CanOnlyAddValuesToListTypeQuestions"
				constraint (SourcingEvent.InActionableState)
					"CannotAddQuestionAllocationWhileEventIsPendingAward,Cancelled,OrClosed"

			Exit Rules
				if (SourcingEventQuestion.ResponseType.List)
					invoke Update SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.SourcingEventQuestionValue
						invoked.DisplayOrder = DisplayOrder

		InternalCreate is a Create Action
			restricted

     	Update is an Update Action
     		valid when (SourcingEvent.InActionableState)

     		Action Rules
    			constraint (SourcingEvent.InActionableState)
					"CannotUpdateQuestionAllocationWhileEventIsPendingAward,Cancelled,OrClosed"
				LocalDisplayOrder = NewDisplayOrder

			Exit Rules
				if (LocalDisplayOrder entered
				and	SourcingEventQuestion.ResponseType.List)
					invoke Update SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.SourcingEventQuestionValue
						invoked.NewDisplayOrder = LocalDisplayOrder
						
     	Delete is a Delete Action		
			valid when (SourcingEvent.InActionableState)
			
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteQuestionAllocationWhileEventIsPendingAward,Cancelled,OrClosed"
				invoke Delete SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.SourcingEventQuestionValue
