PfiEDICarrier is a BusinessClass
    owned by pfi
    prefix is PfiEC
    contains environment data
	disable data area copy
		preserve target data
    default label is "EDICarrier"

    Ontology
        symbolic key is PfiEDICarrier

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements DynamicCreation

    Persistent Fields

        EDIMethod   is Numeric size 1
            States
                SFTP        value is 2
        EDIProvider is Numeric size 1
            States
                Infor       value is 1
                External    value is 2
        PfiEDIFileTransfer
        DoNotDelete is Boolean
            default label is "DoNotDeleteFilesInTheServer"
		Encrypt is Boolean
			default label is "EncryptFileBeforeSending"
		Decrypt is Boolean
			default label is "DecryptFileAfterReceiving"
		EncryptionType is Numeric size 2
			States
				PGP	value is 1
		DecryptionType is Numeric size 2
			States
				PGP value is 1
		EncryptionServiceName is Alpha size up to 200
		Sign is Boolean
		SignServiceName is Alpha size up to 200
		DecryptionServiceName is Alpha size up to 200
		Verify is Boolean
		VerifyServiceName is Alpha size up to 200
		IsEnabled is Boolean
		    default label is "Enabled"
		ContinueOnError is Boolean
			default label is "ContinueOnInboundFileError"
		EnableIntegrityPacket is Boolean
		
    Derived Fields

        PrivatekeyText is a NativeField
            type is Text
            default label is "PrivateKeyText"

        HostKeyText is a NativeField
            type is Text

        ApplyEncryption is a NativeField
            type is Boolean

    Field Rules


        PfiEDIFileTransfer
            required
		EDIMethod
			default to EDIMethod.SFTP
			initial value is EDIMethod.SFTP
            if (EDIMethod = 1)
            	EDIMethod = EDIMethod.SFTP			
		EncryptionType
			default to EncryptionType.PGP
			initial value is EncryptionType.PGP
		EncryptionServiceName
			if (Encrypt)
				required
		
		DecryptionServiceName
			if (Decrypt)
				required
				
		SignServiceName
			if (Encrypt and Sign)
				required
				
		VerifyServiceName
			if (Decrypt and Verify)
				required	
					
		DecryptionType
			default to DecryptionType.PGP
			initial value is DecryptionType.PGP
			
        EDIProvider
            default to 1
            initial value is 1

        PfiEDIFileTransfer.AuthenticationType
            default to PfiEDIFileTransfer.AuthenticationType.UserAndPassword
            if (EDIMethod.SFTP)
                required

        PfiEDIFileTransfer.PrivatekeyPath
            if (EDIMethod.SFTP and PfiEDIFileTransfer.AuthenticationType.UserAndPrivatekey)
                required
                
		IsEnabled
			default to true
			initial value is true
			
    Actions

        Create is a Create Action
            Action Rules
                constraint (ApplyEncryption)
                    "EncryptionNotApplied"

        Update is an Update Action
            Action Rules
                constraint (ApplyEncryption)
                    "EncryptionNotApplied"

        Delete is a Delete Action

        TestConnection is an Instance Action
        
        TestConnectionWithTraceLog is an Instance Action
