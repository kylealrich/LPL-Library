SupplierContactPreMigrationIssues is a BusinessClass
	owned by procurement
	prefix is SCPI
	sql name is SCPMI

	Ontology
		symbolic key is SupplierContactPreMigrationIssues

	Patterns
		disable Auditing
		disable EffectiveDated

	Persistent Fields
		SupplierGroup
		Supplier
			delete ignored
		SupplierSourceId
			delete ignored
		SupplierContactEmailAddress				is like EmailAddress
		SupplierContactName						is like PersonName
		ActorEmailAddress						is like EmailAddress
		ActorName								is like PersonName
		ProcessMessage							is Alpha 150
		DuplicateSupplierGroup					is like SupplierGroup
		DuplicateSupplier						is like Supplier
		DuplicateSupplierSourceId				is like SupplierSourceId
		DuplicateEmailAddress					is like EmailAddress
		DuplicateName							is like PersonName
		PreventsMigration						is Boolean


		RunMode									is Numeric 1
			States
				ReportOnly					value is 0
				ReportAndUpdate				value is 1
		RunDateStart							is TimeStamp
		RunDateEnd								is TimeStamp
		SupplierSourceIdCount					is Numeric 9  
		SupplierSourceIdErrorCount				is Numeric 9  
		MissingActorCount						is Numeric 9
		ContactAndActorMissingEmailCount		is Numeric 9
		ContactMissingEmailCount				is Numeric 9
		ActorMissingEmailCount					is Numeric 9
		ContactAndActorEmailMismatchCount		is Numeric 9
		ContactAndActorMissingFirstNameCount	is Numeric 9
		ContactMissingFirstNameCount			is Numeric 9
		ActorMissingFirstNameCount				is Numeric 9
		ContactAndActorFirstNameMismatchCount	is Numeric 9
		ContactAndActorMissingLastNameCount		is Numeric 9
		ContactMissingLastNameCount				is Numeric 9
		ActorMissingLastNameCount				is Numeric 9
		ContactAndActorLastNameMismatchCount	is Numeric 9
		DuplicateEmailAddressCount				is Numeric 9

		
	Derived Fields
		RunModeMessage is a DerivedField
			type is Alpha 50
			if (RunMode.ReportOnly)
				return ReportModeUpdateMessage
			else
				return UpdateModeUpdateMessage

		ReportModeUpdateMessage is a MessageField
			"Program_Will_Fix_In_Update_Mode"
			
		UpdateModeUpdateMessage is a MessageField
			"Program_Fixed"

		FixManualMessage is a MessageField
			"Fix_Manually"

		TotalRecordsWithIssuesMessage is a MessageField
			"(SomeRecordsMayHaveMoreThan1Issue)"

		HeaderProgramFixMessage is a DerivedField
			type is Alpha 50
			if (RunMode.ReportOnly)
				return HeaderReportModeFixMessage
			else
				return HeaderUpdateModeFixMessage

		HeaderReportModeFixMessage is a MessageField
			"SystemWillFixInUpdateMode"

		HeaderUpdateModeFixMessage is a MessageField
			"SystemFixedInUpdateMode"

		PremigrationCompleteMF is a MessageField
			"Congratulations!_Supplier_Contact_PremigrationCompleted._No_PremigrationIssuesFound."

	Conditions
		IsSummary
			when (SupplierContactPreMigrationIssues.RecordType.Summary)

		IsDetail
			when (SupplierContactPreMigrationIssues.RecordType.Detail)

		HasIssueRecords
			when (SupplierSourceIdErrorCount > 0)

		IsForDuplicateEmail
			when (DuplicateSupplierGroup entered)

		SummaryExists
			when (SupplierContactPreMigrationIssuesSummaryRel exists)

	Relations
		IssuesDetailsForNonDuplicatesRel
			one-to-many relation to SupplierContactPreMigrationIssues
			Field Mapping uses ByRecordType 
				related.SupplierContactPreMigrationIssues.RecordType	= 2 
			Instance Selection
				where (!related.IsForDuplicateEmail)

		IssuesDetailsForDuplicatesRel
			one-to-many relation to SupplierContactPreMigrationIssues
			Field Mapping uses ByRecordType 
				related.SupplierContactPreMigrationIssues.RecordType	= 2 
			Instance Selection
				where (related.IsForDuplicateEmail)

		SupplierSourceIdRel
			one-to-one relation to SupplierSourceId
			Field Mapping uses symbolic key
				related.SupplierGroup		= SupplierGroup
				related.Supplier			= Supplier
				related.SupplierSourceId	= SupplierSourceId

		DuplicateSupplierSourceIdRel
			one-to-one relation to SupplierSourceId
			Field Mapping uses symbolic key
				related.SupplierGroup		= DuplicateSupplierGroup
				related.Supplier			= DuplicateSupplier
				related.SupplierSourceId	= DuplicateSupplierSourceId

		SupplierContactActorRel
			one-to-one relation to Actor
			Field Mapping uses symbolic key
				related.Actor		= SupplierSourceIdRel.LoginName

		SupplierContactPreMigrationIssuesSummaryRel
			one-to-one relation to SupplierContactPreMigrationIssues
			Field Mapping uses symbolic key
				related.SupplierContactPreMigrationIssues.RecordType	= 1 
				related.SupplierContactPreMigrationIssues.Sequence		= 1

	Sets
		ByRecordType
			Sort Order
				SupplierContactPreMigrationIssues.RecordType
				SupplierContactPreMigrationIssues.Sequence


	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted

		ExternalEntityPreMigrationCheck is a Set Action
			Parameters
				PrmRunMode			is Numeric 1
					States
						ReportOnly			value is 0
						ReportAndUpdate		value is 1

			Action Rules
				Empty Set Rules
					invoke ExternalEntityPreMigrationCheck SupplierSourceId
						invoked.PrmRunMode = PrmRunMode
				Set Rules
					Entrance Rules
						invoke ExternalEntityPreMigrationCheck SupplierSourceId
							invoked.PrmRunMode = PrmRunMode
