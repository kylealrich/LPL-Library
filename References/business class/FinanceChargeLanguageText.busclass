FinanceChargeLanguageText is a BusinessClass
	owned by ar
	prefix is ANT
	classic name is ARPAYNOTTX

	Ontology
		symbolic key is FinanceChargeLanguageText
			classic set name is ANTNEWPRIMARYINDEX

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		CommentTexts is a CommentTextX4InArpaynottx
			classic name is COMMENT-TEXT

	Transient Fields
		TransientEditNotes		is Boolean
		TransientNotes			is Text
			derive value from TransientNotesDerived

	Local Fields

		LocalNotes	 			is Text
		LocalNotesLineLimit		is Numeric size 2
		LocalNotesLineTextLimit	is Numeric size 2
		LocalAssignCode			is Numeric size 3
		LocalValidateNotes		is Boolean


	Derived Fields

		CommentText1 is an ArrayValueField
			type is AlphaUpper size 40
			CommentTexts.CommentText[1]

		CommentText2 is an ArrayValueField
			type is AlphaUpper size 40
			CommentTexts.CommentText[2]

		CommentText3 is an ArrayValueField
			type is AlphaUpper size 40
			CommentTexts.CommentText[3]

		CommentText4 is an ArrayValueField
			type is AlphaUpper size 40
			CommentTexts.CommentText[4]

		AssignNotes				is a NativeField
			type is Numeric size 3
			restricted

		TransientNotesDerived	is a DerivedField
			type is Text
			restricted
			initialize LocalAssignCode
			LocalValidateNotes = false
			LocalAssignCode = AssignNotes
			return LocalNotes

		ExcessLineMessageText is a MessageField
			restricted
			"ExceededMaximumLinesAllowed;LimitIs<LocalNotesLineLimit>"

		ExcessLineTextMessageText is a MessageField
			restricted
			"ExceededMaximumAllowedCharactersInALine;LimitIs<LocalNotesLineTextLimit>"

		AssignErrorMessage is a DerivedField
			type is MessageField
			restricted
			if (LocalAssignCode = 101)
				return ExcessLineMessageText
			if (LocalAssignCode = 102)
				return ExcessLineTextMessageText
			return blank

	Field Rules
		TransientEditNotes
			initial value is true
		TransientNotes
			required
				"AtLeastOneLineOfTextRequired"
			if (TransientNotes entered)
				initialize LocalAssignCode
				LocalValidateNotes = true
				LocalAssignCode = AssignNotes
				constraint (LocalAssignCode = 100)
					"<AssignErrorMessage>"

	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

	Relations
		FinanceChargeLanguageTextBlankRel
			one-to-one relation to FinanceChargeLanguageText
			required
			Field Mapping uses Set1
				related.Company 							= Company
				related.FinanceChargeLanguageText.TextCode 	= FinanceChargeLanguageText.TextCode
				related.IsoLocale							= blank

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company


	Sets
		Set1
			not indexed
			Sort Order
				Company
				FinanceChargeLanguageText
				IsoLocale

	Actions
		Create is a Create Action

		T2VCreate is a Create Action				
			restricted 
			default label is untranslatable 
			bypass field rules 
			
		Update is an Update Action

		Delete is a Delete Action

