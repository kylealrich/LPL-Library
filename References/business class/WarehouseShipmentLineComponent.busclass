WarehouseShipmentLineComponent is a BusinessClass
    owned by wh
    prefix is WHC
    classic name is WHSHIPCOMP

    Ontology
        symbolic key is WarehouseShipmentLineComponent
            classic set name is WHCSET1
            classic name is COMPONENT-SEQ
            classic name for InventoryLocation is LOCATION
            classic name for WarehouseShipment is SHIPMENT-NBR
            classic name for WarehouseShipmentLine is LINE-NBR

    Patterns
        implements ContextualParent
        implements StaticJava
        disable AuditIndex
		implements Archivable

    Persistent Fields
        WarehouseDemand
            classic name for WarehouseDemand.DemandSystemCode is SYSTEM-CD
            classic name for WarehouseDemand.DemandDocument is DOC-NBR
			delete ignored
        Item
        Quantity
        DetailQuantity   is a Quantity
            classic name is DETAIL-QTY
        DetailRequired   is Boolean
            classic name is DETAIL-REQ
            protected
        UnitCost         is an InternationalCost
        GlobalLineType
            classic name is GLBL-LINE-TYPE
        ItemLocation
	
	Local Fields
		LocalComponent     is Numeric size 3
		LocalDelete        is Boolean
		LocalShipment	   is like WarehouseShipment
		LocalShipmentLine  is like WarehouseShipmentLine		
		NewInventoryTransactionLine		is an InventoryTransactionLine view
		InventoryCostDefault
		

		LocalGLFinanceCodeBlock					is a TransactionCodeBlock          
		LocalGLTranAmount               		is an InternationalAmount
		LocalGLUnitsAmount						is a UnitsAmount
		LocalPostingType						is AlphaUpper size 2
            States
                Inventory           	value is "I1"
                Offset              	value is "O1"
		LocalTotalTransactionAmount     		is an InternationalAmount
		LocalOriginalShippedQuantity    		is an InternationalAmount
		LocalTotalDistributionAmount    		is an InternationalAmount
		LocalRemainingQuantityToBeDistributed 	is an InternationalAmount	
		LocalDistributionQuantity				is like Quantity
		LocalTotalUnitsAmount					is a UnitsAmount
		LocalTotalDistributionUnitsAmount 		is a UnitsAmount

		
	Rule Blocks
	
		CreateDistributions
			invoke Create InventoryTransactionLineDistribution
				fill in fields from this instance
				invoked.InventoryTransaction				= PrmInventoryTransaction.InventoryTransaction
				invoked.InventoryTransactionLine			= NewInventoryTransactionLine.InventoryTransactionLine
				invoked.TransactionSystemCode				= NewInventoryTransactionLine.TransactionSystemCode
				invoked.System								= WarehouseShipment.WarehouseDemand.DemandSystemCode		
				invoked.PostingType							= LocalPostingType
				invoked.PostingDate							= NewInventoryTransactionLine.InventoryTransactionLine.GeneralLedgerDate
				invoked.GLFinanceCodeBlock					= LocalGLFinanceCodeBlock
				invoked.CurrencyCode						= Company.AccountingEntity.FunctionalCurrency
				invoked.GLTransactionAmount					= LocalGLTranAmount
				if (not LocalGLFinanceCodeBlock.GeneralLedgerChartAccount.AllowUnits.No)
					invoked.UnitsAmount						= LocalGLUnitsAmount
				invoked.TransactionDate						= NewInventoryTransactionLine.InventoryTransactionLine.TransactionDate
				invoked.GeneralLedgerEvent					= PrmInventoryTransaction.InventoryDocumentType
				invoked.DocumentNumber						= PrmInventoryTransaction
				invoked.AddOnCharge 						= NewInventoryTransactionLine.LocalAddOnCharge						
				initialize invoked.Status			
			
	Derived Fields
		ErrorMessage 	 is a MessageField
			restricted
			"PerformUndo;Shipment<WarehouseShipment>InShippedState"
		
		DerivedMessage 	 is a DerivedField
			type is	MessageField
			restricted
			if (WarehouseShipment.Status.Shipped)
				return ErrorMessage
				
		DerivedItemCommentCheck is a DerivedField
			type is Alpha 3
			if (ComponentItemCommentsRel exists)
				return "Yes"
			return "No"
			
		DerivedIDMPickListQuantity is a DerivedField
			type is like Quantity
				precision is Item.NumberOfDecimalsQuantity
			return Quantity

		DerivedWarehouseShipmentLineDetailBin is a DerivedField
			type is like Bin
			return first WarehouseShipmentLineDetailsRel.WarehouseShipmentLineDetail.Bin

		DerivedWarehouseShipmentLineDetailLot is a DerivedField
			type is like ItemLot
			return first WarehouseShipmentLineDetailsRel.WarehouseShipmentLineDetail.Lot
		
		DerivedWarehouseShipmentLineDetailSublot is a DerivedField
			type is like Sublot
			return first WarehouseShipmentLineDetailsRel.WarehouseShipmentLineDetail.Sublot

		DerivedWarehouseShipmentLineDetailSerial is a DerivedField
			type is like ItemSerialNumber
			return first WarehouseShipmentLineDetailsRel.WarehouseShipmentLineDetail.Serial

		DerivedWarehouseShipmentLineDetailLotExpirationDate is a DerivedField
			type is Date
			return first WarehouseShipmentLineDetailsRel.WarehouseShipmentLineDetail.Lot.LotExpirationDate
		
		DerivedLineComponentHasSpecialHandling is a DerivedField
			type is Alpha 3
			if (Item.SpecialHandlingCode entered)
				return "Yes"
			return "No"

		DerivedEmptyTitle is a DerivedField
			type is Alpha size 2
			return blank

		DerivedSpecialHandlingDescription		is a DerivedField
			type is like Description
			return Item.SpecialHandlingCode.Description

		DerivedSpecialHandlingCode 				is a DerivedField
			type is like SpecialHandlingCode
			return Item.SpecialHandlingCode

		DerivedSpecialHandlingInstructions			is a DerivedField
			type is like Description4
			return Item.SpecialHandlingInstructions

		DerivedTimeSensitive 					is a DerivedField
			type is Boolean
			return Item.SpecialHandlingCode.TimeSensitive

		DerivedHandlingTimeInMinutes				is a DerivedField
			type is Numeric 3
			return Item.HandlingTimeInMinutes
		
    Conditions
        IsBinTracked
        	restricted
            when (ItemLocation.BinTracked
            and   Item.LotTracked.NotLotTracked
            and   Item.SerialTracked.NotSerialTracked)

        RequiresWalkthru
	        restricted
            classic name is WALKTHRU
            when (DetailRequired)
		
		IsPrintedPickedOrPacked
			restricted
			when (WarehouseShipment.Status < 3)
		
        IsDetailRequired		
        	restricted							
        	when (ItemLocation.IsLotTracked
        	or    ItemLocation.IsSerialTracked
        	or    ItemLocation.BinTracked
        	or    IsMultipleUOMTracked)
        	
        IsMultipleUOMTracked
        	restricted
            when (first StockOnHandDetailRel exists
            and   Item.LotTracked.NotLotTracked
            and   Item.SerialTracked.NotSerialTracked
            and   not ItemLocation.BinTracked)			
        
        IsOnlySerialTracked
        	restricted
        	when (ItemLocation.IsSerialTracked
        	and  !ItemLocation.BinTracked)

        IsOnlyLotTrackedAtIssue
        	restricted
        	when (ItemLocation.LotTracked.LotRequiredAtIssue
        	and  !ItemLocation.BinTracked)
        	
        IsPlainCatchWeightItem
        	restricted
        	when (Item.IsCatchWeightItem
        	and  !ItemLocation.BinTracked
        	and  !ItemLocation.IsLotTracked
        	and  !IsMultipleUOMTracked) 
        	
        HasPickListLineDetail
        	restricted
        	when (WarehousePickListPrintDetailRel exists)

		HasSpecialHandling
			when (Item.SpecialHandlingCode entered)	

		HasSpecialHandlingInstructions
			when (Item.SpecialHandlingInstructions entered)
    Relations

        WarehouseShipmentLineDetailsRel 		is a WarehouseShipmentLineDetail set	
        
        WarehouseShipmentLineDetailsForMergeRel	is a WarehouseShipmentLineDetail set
        	Instance Selection
        		where (related.WarehouseShipment				= LocalShipment
        		and    related.WarehouseShipmentLine			= LocalShipmentLine
        		and    related.WarehouseShipmentLineComponent   = LocalComponent)
 
		WarehouseDemandLineRel
			one-to-one relation to WarehouseDemandLine
			Field Mapping uses Set1
                related.Company                               = Company
                related.WarehouseDemand.DemandSystemCode      = WarehouseShipment.WarehouseDemand.DemandSystemCode
                related.WarehouseDemand.DemandDocument        = WarehouseShipment.WarehouseDemand.DemandDocument
				related.WarehouseDemand.DemandCompany	 	  = WarehouseShipment.WarehouseDemand.DemandCompany
                related.WarehouseDemandLine.LineNumber        = WarehouseShipmentLine
                related.WarehouseDemandLine.ComponentSequence = WarehouseShipmentLineComponent
                related.InventoryLocation                     = InventoryLocation
        
        StockOnHandDetailRel
            one-to-many relation to StockOnHandDetail
            Field Mapping uses symbolic key
                related.Company           			  = Company
                related.InventoryLocation 			  = InventoryLocation
                related.Item              			  = Item						
    	
    	OtherLineComponentsRel
            one-to-many relation to WarehouseShipmentLineComponent
            Field Mapping uses symbolic key    	
				related.Company 						 = Company
				related.InventoryLocation 		 		 = InventoryLocation
			Instance Selection
				where (related.WarehouseShipment				!= WarehouseShipment

				and    related.WarehouseShipment.WarehouseDemand = WarehouseShipment.WarehouseDemand
				and    related.WarehouseShipment.Status			 = WarehouseShipment.Status)
		
		DuplicateLineComponentRel
            one-to-one relation to WarehouseShipmentLineComponent
            Field Mapping uses symbolic key 
            	related.Company 						 = Company
				related.InventoryLocation 		 		 = InventoryLocation
				related.WarehouseShipment				 = WarehouseShipment
				related.WarehouseShipmentLine			 = WarehouseShipmentLine
				related.WarehouseShipmentLineComponent	 = LocalComponent		    

		RequisitionRel 
			one-to-one relation to Requisition
            Field Mapping uses symbolic key
                related.Company     = WarehouseDemand.Company
                related.Requisition = WarehouseDemand.DocumentNumberNumeric

		RequisitionLineRel													
			one-to-one relation to RequisitionLine
            Field Mapping uses symbolic key
                related.Company         			= WarehouseShipment.CompanyAndLocation.RequestingCompany
                related.Requisition                 = WarehouseShipment.WarehouseDemand.DemandDocument
                related.RequisitionLine 			= WarehouseShipmentLine    
                
        WarehousePickListPrintDetailRel	is a WarehousePickListPrintDetail set
        
        ComponentItemCommentsRel
        	one-to-many relation to ItemComment
        	Field Mapping uses symbolic key
        		related.ItemGroup 	= Item.ItemGroup
        		related.Item		= Item
			Instance Selection
				where (related.IsCommentNotExpired)
        
		InventoryTransactionLineDistributionRel
        	one-to-many relation to InventoryTransactionLineDistribution
        	Field Mapping uses symbolic key
        		related.Company						= NewInventoryTransactionLine.InventoryTransactionLine.Company
        		related.InventoryLocation			= NewInventoryTransactionLine.InventoryTransactionLine.InventoryLocation
        		related.InventoryTransaction		= NewInventoryTransactionLine.InventoryTransactionLine.InventoryTransaction
        		related.TransactionSystemCode		= NewInventoryTransactionLine.InventoryTransactionLine.TransactionSystemCode
        		related.InventoryTransactionLine	= NewInventoryTransactionLine.InventoryTransactionLine.InventoryTransactionLine

		SupplyRecordsRel
			one-to-one relation to WarehouseDemandLine
			Field Mapping uses Set7
				related.Company									= WarehouseShipment.CompanyAndLocation.RequestingCompany
				related.InventoryLocation						= WarehouseShipment.CompanyAndLocation.RequestingLocation
                related.WarehouseDemand.DemandSystemCode      	= WarehouseShipment.WarehouseDemand.DemandSystemCode
                related.WarehouseDemand.DemandDocument        	= WarehouseShipment.WarehouseDemand.DemandDocument
				related.WarehouseDemand.DemandCompany	 		= WarehouseShipment.WarehouseDemand.DemandCompany
                related.WarehouseDemandLine.LineNumber			= WarehouseShipmentLine
                related.WarehouseDemandLine.ComponentSequence	= WarehouseShipmentLineComponent

		FromToCompanyItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company						= WarehouseShipment.CompanyAndLocation.RequestingCompany
				related.InventoryLocation			= WarehouseShipment.CompanyAndLocation.RequestingLocation
				related.Item						= Item

		OtherOnHandItemSerialNumbersRel
			one-to-many relation to ItemSerialNumber
			Field Mapping uses Set3
				related.Company						= Company
				related.InventoryLocation			= InventoryLocation
				related.Item						= Item

    Sets

        Set2
            indexed
            Instance Selection
                where (RequiresWalkthru)
            Sort Order
                Company
                InventoryLocation
                WarehouseShipment
                WarehouseShipmentLine
                WarehouseShipmentLineComponent
                Item

        Set3
            indexed
            Sort Order
                Company
                InventoryLocation
                WarehouseShipment
                WarehouseShipmentLineComponent
                WarehouseShipmentLine
				
	Actions
		Create is a Create Action							
			restricted
					
		Update is an Update Action
			valid when (IsPrintedPickedOrPacked)
			Action Rules
				constraint (IsPrintedPickedOrPacked)
					"CannotUpdateLineComponent;ShipmentIsIn<WarehouseShipment.Status>State<DerivedMessage>"					
		
		FastUpdate is an Update Action
			restricted
			bypass field rules
		
		AdjustQuantities is an Instance Action
			restricted
			Parameters
				PrmQuantity					is like Quantity
			Local Fields
				LocalQuantityForAdjustment	is like Quantity
			Action Rules
				Quantity = PrmQuantity
				
				LocalQuantityForAdjustment = Quantity - old Quantity
				
				if (LocalQuantityForAdjustment > 0)
					if (IsOnlySerialTracked)
						for each OtherOnHandItemSerialNumbersRel
							invoke Create WarehouseShipmentLineDetail
								invoked.Company								= Company	
								invoked.InventoryLocation					= InventoryLocation
								invoked.WarehouseShipment					= WarehouseShipment
								invoked.WarehouseShipmentLine				= WarehouseShipmentLine
								invoked.WarehouseShipmentLineComponent		= WarehouseShipmentLineComponent
								invoked.WarehouseShipmentLineDetail.Bin 	= each.Bin
								invoked.WarehouseShipmentLineDetail.Serial 	= each.ItemSerialNumber
								
							LocalQuantityForAdjustment = LocalQuantityForAdjustment - 1
							
							if (LocalQuantityForAdjustment = 0)
								end for each

		Delete is a Delete Action
			restricted
		
		Merge is an Instance Action
			restricted
			Action Rules
				for each OtherLineComponentsRel
					LocalShipment  		= each.WarehouseShipment
					LocalShipmentLine 	= each.WarehouseShipmentLine
					LocalComponent 		= each.WarehouseShipmentLineComponent
					initialize LocalDelete
					if (Item = each.Item)
						Quantity 	   += each.Quantity
						DetailQuantity += each.DetailQuantity
						
						invoke Merge WarehouseShipmentLineDetailsRel
						
						LocalDelete = true
					else
						if (DuplicateLineComponentRel.Item != each.Item) 
							invoke Create 
								fill in fields from each
								invoked.WarehouseShipment	  			= WarehouseShipment
								invoked.WarehouseShipmentLine 			= WarehouseShipmentLine	
								invoked.WarehouseShipmentLineComponent  = LocalComponent
							
							for each WarehouseShipmentLineDetailsForMergeRel
								invoke Create WarehouseShipmentLineDetail
									fill in fields from each
									invoked.WarehouseShipment 				= WarehouseShipment
									invoked.WarehouseShipmentLine			= WarehouseShipmentLine
									invoked.WarehouseShipmentLineComponent	= LocalComponent
									invoked.CopyDetails						= true
							
							LocalDelete = true
					
					if (LocalDelete)
						invoke Delete each
					
		UpdateFromWarehouse is an Instance Action
			restricted
			Parameters
				PrmDetailQuantity is a Quantity
			Action Rules
				DetailQuantity += PrmDetailQuantity
		
		ValidateFromWarehouse is an Instance Action
			restricted
			Action Rules
				if (IsDetailRequired)
					constraint (DetailQuantity = Quantity)
						"DetailQuantitiesMismatchForLine<WarehouseShipmentLine>Component<WarehouseShipmentLineComponent>"		

		PopulateWarehouseDemandCompany is a Set Action 
			restricted
			run in background
			Instance Selection
				where (WarehouseDemand.DemandCompany not entered)
			Action Rules
				Instance Rules
					WarehouseDemand.DemandCompany = WarehouseShipment.WarehouseDemand.DemandCompany

		CreateComponentTransactionLine is an Instance Action
			restricted
			
			Parameters
				PrmInventoryCompany		is an InventoryCompany
				PrmInventoryLocation	is an InventoryLocation
				PrmInventoryTransaction is an InventoryTransaction
				PrmWarehouseShipment    is a WarehouseShipment
				PrmOffsetAccount		is a FinanceCodeBlock
							
			Parameter Rules
				PrmInventoryCompany
		    		required
		    	PrmInventoryLocation
		    		required
		    	PrmWarehouseShipment
		    		required
		    	PrmInventoryTransaction
		    		required
			
			Action Rules

				initialize InventoryCostDefault
				InventoryCostDefault.InCompany 				= Company
				InventoryCostDefault.InInventoryLocation 	= InventoryLocation
				InventoryCostDefault.InItem 				= Item
				InventoryCostDefault.InQuantity 			= Quantity											
				UnitCost = InventoryCostDefault.DefaultInventoryCost
			

				increment ItemLocation.TransactionCounter
			    




				invoke Unreleased.Create InventoryTransactionLine
					assign result to NewInventoryTransactionLine
					fill in fields from this instance
					invoked.TransactionSystemCode 						= WarehouseShipment.WarehouseDemand.DemandSystemCode
					invoked.InventoryTransaction						= PrmInventoryTransaction
					invoked.InventoryTransactionLine.WarehouseShipment	= WarehouseShipment
					invoked.InventoryTransactionLine.LineNumber			= WarehouseShipmentLine								
					invoked.InventoryTransactionLine.ComponentSequence  = WarehouseDemandLineRel.WarehouseDemandLine.ComponentSequence
					invoked.Item										= WarehouseDemandLineRel.Item
					invoked.OriginatingTransaction						= reference to this instance
					invoked.Quantity									= Quantity
					invoked.TransactionUOM								= Item.StockUOM
					invoked.KitType										= WarehouseDemandLineRel.KitType
					invoked.ItemEntryMethod								= WarehouseDemandLineRel.ItemEntryMethod
					invoked.EnteredItem									= WarehouseDemandLineRel.EnteredItem	
					invoked.StockOnHandQuantity							= WarehouseDemandLineRel.ItemLocation.StockOnHandQuantity
					invoked.GeneralLedgerDate							= PrmInventoryTransaction.GeneralLedgerDate
					if (PrmInventoryTransaction.InventoryDocumentType.InventoryIssue
					and WarehouseShipmentLine.InProcessQuantity > 0)
						initialize invoked.UnitCost
					if (WarehouseDemandLineRel.ItemLocation.BinTracked)
						invoked.MultipleBins							= true
					if (PrmInventoryTransaction.IsDirectTransfer or PrmInventoryTransaction.IsInventoryTransfer)
						invoked.OffsetAccount							= PrmOffsetAccount
					else								
					if (RequisitionLineRel exists)
						invoked.OffsetAccount							= RequisitionLineRel.RequisitionLineDistributionsRel.DistributionAccount
						if (InventoryLocation.EAMLocation.EAMLocation)
							invoked.EAMWorkOrderNumber 					= RequisitionLineRel.EAMWorkOrderNumber   
							invoked.EAMOperationNumber           		= RequisitionLineRel.EAMOperationNumber
							invoked.EAMSequenceNumber					= RequisitionLineRel.EAMSequenceNumber
					invoked.LocalSkipTransactionCreate					= true
				
				if (WarehouseShipmentLineDetailsRel exists)
					for each WarehouseShipmentLineDetailsRel
						invoke CreateInventoryTransactionLineDetail each  
							invoked.PrmInventoryTransaction			    	= PrmInventoryTransaction
							invoked.PrmTransactionSystemCode				= WarehouseShipment.WarehouseDemand.DemandSystemCode
							invoked.PrmInventoryTransactionLine 			= NewInventoryTransactionLine.InventoryTransactionLine
				

				if (PrmInventoryTransaction.IsInventoryIssues)

					initialize LocalGLFinanceCodeBlock
					initialize LocalGLTranAmount
					initialize LocalGLUnitsAmount
					initialize LocalPostingType
			
					LocalPostingType 						= InventoryTransactionLineDistribution.PostingType.Inventory
					LocalGLFinanceCodeBlock					= ItemLocation.GeneralLedgerCategory.InventoryAccount
					LocalGLTranAmount						= ((Quantity * UnitCost) * -1)
					LocalGLUnitsAmount						= (Quantity * -1)
					include CreateDistributions
					

					initialize LocalGLFinanceCodeBlock
					initialize LocalPostingType
					initialize LocalTotalDistributionAmount
					
					LocalPostingType 						= InventoryTransactionLineDistribution.PostingType.Offset
					LocalTotalTransactionAmount 			= LocalGLTranAmount * -1
					LocalTotalUnitsAmount 					= Quantity 	
					
					if (RequisitionLineRel.DistributionBy.ByQuantity)
						LocalOriginalShippedQuantity 		= WarehouseDemandLineRel.ShippedQuantity
					LocalRemainingQuantityToBeDistributed 	= Quantity

					for each RequisitionLineRel.RequisitionLineDistributionsRel
						LocalGLFinanceCodeBlock				= each.DistributionAccount
						

						if (RequisitionLineRel.DistributionBy.ByQuantity)				
							if (LocalOriginalShippedQuantity >= each.DerivedDistributionQuantityInStock)
								LocalOriginalShippedQuantity 			-= each.DerivedDistributionQuantityInStock
								initialize LocalGLTranAmount
								initialize LocalGLUnitsAmount
							else
								if (LocalOriginalShippedQuantity > 0)
									LocalDistributionQuantity			= (each.DerivedDistributionQuantityInStock - LocalOriginalShippedQuantity)
									initialize LocalOriginalShippedQuantity
								else
									LocalDistributionQuantity 			= each.DerivedDistributionQuantityInStock
		
								if (LocalDistributionQuantity > LocalRemainingQuantityToBeDistributed) 
									LocalDistributionQuantity 			= LocalRemainingQuantityToBeDistributed
									initialize LocalRemainingQuantityToBeDistributed
								else 
									LocalRemainingQuantityToBeDistributed -= LocalDistributionQuantity
								LocalGLTranAmount						= LocalDistributionQuantity * UnitCost
								LocalGLUnitsAmount						= LocalDistributionQuantity 
						

						else
						if (RequisitionLineRel.DistributionBy.ByPercent)
							LocalGLTranAmount							= each.DistributionPercent * LocalTotalTransactionAmount
							LocalGLUnitsAmount							= each.DistributionPercent * LocalTotalUnitsAmount		  			
						

						else
							LocalGLTranAmount							= ((each.DistributionAmount / RequisitionLineRel.ApprovalValue ) * LocalTotalTransactionAmount)
							LocalGLUnitsAmount   						= ((each.DistributionAmount / RequisitionLineRel.ApprovalValue ) * LocalTotalUnitsAmount)						

						LocalTotalDistributionAmount 		+= LocalGLTranAmount
						LocalTotalDistributionUnitsAmount 	+= LocalGLUnitsAmount
						include CreateDistributions
					
					if (LocalTotalTransactionAmount != LocalTotalDistributionAmount)
						invoke Update first InventoryTransactionLineDistributionRel
							invoked.GLTransactionAmount 	= (LocalTotalTransactionAmount - LocalTotalDistributionAmount)
							invoked.UnitsAmount				= (LocalTotalUnitsAmount - LocalTotalDistributionUnitsAmount)
							

				if ((PrmInventoryTransaction.InventoryDocumentType.InventoryTransfer
				and PrmInventoryTransaction.IntransitTransfer)
				or  PrmInventoryTransaction.IsDirectTransfer)	
					invoke UpdateFromWarehouse SupplyRecordsRel
						invoked.PrmQuantity	= Quantity * -1


				invoke UpdateQuantities ItemLocation
					invoked.PrmInProcessQuantity						= (Quantity * -1)			


				invoke UpdateFromWarehouse WarehouseDemandLineRel			
					invoked.PrmShippedQuantity							= Quantity

				if (IsDetailRequired)
					for each WarehouseShipmentLineDetailsRel
						invoke UpdateStockOnHandDetailAllocatedQuantity each


				invoke Release NewInventoryTransactionLine.InventoryTransactionLine
					invoked.PrmSkipTransactionCreate					= true
					if (PrmInventoryTransaction.InventoryDocumentType.InventoryIssue)
						invoked.PrmSkipDistributions					= true
				

		Purge is a Purge Action
			restricted
			
					
