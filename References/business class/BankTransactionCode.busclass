BankTransactionCode is a BusinessClass
    owned by cb
    prefix is CBI
    classic name is CBBANKINST

    Ontology
        symbolic key is BankTransactionCode
            classic set name is CBISET1
            classic name is BANK-INST-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    
    Attach Rules
    	constraint (Active
    	or          BypassRuleForTheseFiles)
    		"BankTransactionCode<BankTransactionCode>IsNotActive"		
   		
    
    Persistent Fields

        Description
            classic name is BANK-INST-DESC
        BankTransactionType                     is AlphaUpper size 1
            classic name is BANK-INST-TYPE
            default label is "TransactionCategory"
            States
                BankService           value is "B"
                CashPayment           value is "C"
                BillOfExchangePayment value is "D"
        TransactionNumberValue                  is AlphaUpper size 1
            classic name is TRANS-VALUE
            States
                AlphaAllowed 		 value is "A"
                NumericOnly  		 value is "N"


        AutomaticReconciliation                 is Boolean
            classic name is AUTO-RECON
        TransactionOrigin                       is a PaymentDocumentOrigin
            classic name is PMT-DOC-ORIGIN
        PaymentOutputOption                     is AlphaUpper size 1
            classic name is OUTPUT-OPTION
            States
                PrintedDocument    		value is "P"
                ElectronicFile    		value is "T"
                WireTransfer			value is "W"
                NoOutput 				value is "N"
                Swift					value is "S"
        ReceivableAcceptanceAccounting          is Boolean
            classic name is AR-ACCEPT-FL
        ReceivableGeneralLedgerAccounting       is Boolean
            sql name is RGeneralLedgerAccounting
            classic name is AR-GL-ACCEPT
            default label is "ReceivableGlobalLedgerAccounting"
        PayableAcceptanceAccounting             is Boolean
            classic name is AP-ACCEPT-FL
        ElectronicPrenotification               is Boolean
            classic name is PRENOTE-REQ
        ElectronicAlternateTransactionCode
            classic name for ElectronicAlternateTransactionCode.BankTransactionCode is ALTERNATE-PMT
        TransactionType                         is AlphaUpper size 1
            classic name is ACCT-BAL-TYPE
           	default label is "BankingTransactionType"
            States
                DebitTransaction  value is "D"
                CreditTransaction value is "C"
        ManualPaymentNumberReservation          is Boolean
            classic name is MN-PMT-RESERVE
        TransactionIDNumber
            classic name is TRANS-IDENT
        	disable Auditing
        Active                                  is Boolean
            classic name is ACTIVE-STATUS
        UsedFlag                                is Boolean
        PaymentSummarization                    is Boolean
            classic name is TAPE-PMT-SUMM
        ElectronicPaymentFormat                 is AlphaUpper size 3
            classic name is PMT-FORMAT
        ElectronicPaymentOrganizationIdentifier is AlphaUpper size 15
            sql name is EPaymentOrganizationIdentifier
            classic name is ORG-IDENTIFIER
        ElectronicPaymentInstruction1           is a PmtInstruct
            classic name is PMT-INSTRUCT1
        ElectronicPaymentInstruction2           is a PmtInstruct
            classic name is PMT-INSTRUCT2
        ElectronicPaymentUserField1             is AlphaUpper size 30
            classic name is USER-FIELD1
        ElectronicPaymentUserField2             is a UserField2
            classic name is USER-FIELD2
        SortOption
        PaymentOption                           is AlphaUpper size 1
            classic name is PAYMENT-OPT
            States
                PaymentDate value is "P"
                DueDate     value is "D"
        PlusGiroWithBankGiro                    is Boolean
            classic name is PG-WITH-BG











        AutoTransactionNumbering 				is Boolean
        AllowNegativeReceipts					is Boolean	
        AllowDetailPaymentReject                is Boolean  

    Conditions
        IsCashPaymentAndActive
            restricted
            when (BankTransactionType.CashPayment
            and   Active)
    
    	BypassRuleForTheseFiles
			restricted
			when (parentcontext.name = "BankStatementReconciliation"
			or    parentcontext.name = "CashRequirementsError"
            or    parentcontext.name = "PayablesJobErrorResult")
    
		IsValidForActorContext
			restricted
			when ((actor.context.FinanceEnterpriseGroup != ""
			and   CashManagementGroup = actor.context.FinanceEnterpriseGroup)
			or   (actor.context.FinanceEnterpriseGroup = ""))

        IsCashPayment
        	restricted
            when (BankTransactionType.CashPayment)

        IsBankServiceCreditTransaction
        	restricted
            classic name is CRED-BNK-SERV
            when (BankTransactionType.BankService
            and   TransactionType.CreditTransaction)

        IsBankServiceDebitTransaction
        	restricted
            classic name is DEBIT-BNK-SERV
            when (BankTransactionType.BankService
            and   TransactionType.DebitTransaction)

        IsBillOfExchangePayment
        	restricted
            classic name is DRAFT-PAYMENT
            when (BankTransactionType.BillOfExchangePayment)

        IsBankServiceTransaction
        	restricted
            classic name is NON-PMT-TRANS
            when (BankTransactionType.BankService)

        IsCashOrBillOfExchangePayment
        	restricted
            classic name is PMT-TRANS
            when (BankTransactionType.CashPayment
            or    BankTransactionType.BillOfExchangePayment)
            
		IsCashPaymentAndOriginReceivable
			restricted
			when (IsCashPayment
			and   TransactionOrigin.Receivable)

		AutoNumberingAllowed
			restricted
			when (IsCashPayment
			or    IsBankServiceTransaction)

		PayablesElectronicTransferIDsAreApplicable
			restricted
			when (BankTransactionCodeRel exists)

        IsBankServiceOrCashPaymentAndOriginReceivable                   
            restricted
            when (BankTransactionType.BankService
			or	 (BankTransactionType.CashPayment
			and	  TransactionOrigin.Receivable))

    Derived Fields

		BankTransactionCodeName is a MessageField
			default label is "BankTransacationCode"
			"<BankTransactionCode>_-_<BankTransactionCode.Description>"

		TransactionCategoryAndOrigin is a MessageField	
			default label is "TransactionCodeAndOrigin"
			"<BankTransactionType>_-_<TransactionOrigin>"

        ElectronicPaymentCreationMessage is a MessageField
            "For_Cash_Payment_FormatUsingIDM\,ThisIsSetTo_Email_Only"


    Relations

        CbtransRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.BankTransactionCode 	= BankTransactionCode

		CashPaymentFormatsWithoutRejectCodeRel
			one-to-many relation to CashPaymentFormat
            Field Mapping uses ByBankTransactionCode
            	related.CashManagementGroup 					= CashManagementGroup	
                related.CashPaymentFormat.BankTransactionCode 	= BankTransactionCode
			Instance Selection
				where (related.BankRejectionBankTransactionCode 	  not entered)                

        BankTransactionCodeRel
            one-to-one relation to BankTransactionCode
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.BankTransactionCode 	= BankTransactionCode
    
    
    Sets


    Field Rules

        TransactionType
            required
			if (BankTransactionType.BankService)
				required
                    "BankingTransactionTypeCannotBeBlankForABankService"    
			if (BankTransactionType.CashPayment
			or 	BankTransactionType.BillOfExchangePayment)
				TransactionType = TransactionType.DebitTransaction	
				
			
        Description
            required

        BankTransactionType
            required
            cannot be changed

        PaymentSummarization

			constraint (!CashPaymentFormatsWithoutRejectCodeRel exists)	
				"CashPaymentFormatForCashCode:<first CashPaymentFormatsWithoutRejectCodeRel.CashCode>,PayablesProcessGroup:<first CashPaymentFormatsWithoutRejectCodeRel.CashPaymentFormat.PayablesProcessGroup>RequiresARejectionBankTransactionCode"
            constraint (!AllowDetailPaymentReject)                   
                "AllowDetailPaymentRejectNotValidWithPaymentSummarization"

        Active
            default to true

        PaymentOption
            default to "P"

        SortOption
            default to "0"

		TransactionOrigin
			if (BankTransactionType.CashPayment
			or  BankTransactionType.BillOfExchangePayment)
				required

 		PaymentOutputOption
			if (BankTransactionType.CashPayment
			or  BankTransactionType.BillOfExchangePayment)
				required	





		TransactionNumberValue
			default to TransactionNumberValue.NumericOnly
						







																
		ManualPaymentNumberReservation
            constraint (not TransactionNumberValue.AlphaAllowed)
                "CannotSelectPmtReservationWhenNumberingIsAlpha"       
            constraint (BankTransactionType.CashPayment)
                "MustBeACashPaymentTransaction"                





				
        PayableAcceptanceAccounting
            if  (BankTransactionType.BillOfExchangePayment)
                if (TransactionOrigin.Payable)
                    constraint (not PayableAcceptanceAccounting)
                        "A\P_\AcceptanceMust_=_\NIf_\Trans_\Origin_=_\P"       

		ReceivableAcceptanceAccounting
			if 	(BankTransactionType.BillOfExchangePayment)
				if (TransactionOrigin.Payable)
					constraint (not ReceivableAcceptanceAccounting)
						"A\R_\AcceptanceMust_=_\NIf_\Trans_\Origin_=_\P"	        

		ElectronicPrenotification
			constraint (PaymentOutputOption.ElectronicFile
			or			PaymentOutputOption.Swift)
				"NotUsedWithThisOutputOption"                              
            constraint (BankTransactionType.CashPayment)
                "MustBeACashPaymentTransaction"                
			constraint (ElectronicAlternateTransactionCode.BankTransactionCode entered)
				"AlternateTransactionCodeRequiredIfPrenotificationIsChecked"   //_=_\Y"    

		ElectronicAlternateTransactionCode
			constraint (ElectronicPrenotification)
				"AlternateTransactionCodeMustBeBlankIfPrenotificationIsUnchecked"   //_=_\N"    
			constraint (ElectronicAlternateTransactionCode.BankTransactionCode != BankTransactionCode)
				"AlternateTransactionCodeCannot_=TransactionCode"                  
			constraint (ElectronicAlternateTransactionCode.BankTransactionCode.BankTransactionType.CashPayment)
				"MustBeACashPaymentTransaction"                            
			
		AutoTransactionNumbering
			constraint (TransactionNumberValue.NumericOnly)
				"TransactionNumberingMustBeSetToNumericToUseCashLedgerAutoTransactionNumbering"
				
		AllowNegativeReceipts	
			constraint (TransactionType.CreditTransaction)
				"BankingTransactonTypeMustBeSetToCreditTransactionToUseTheAllowNegativeReceiptsFeature" 		
				
	Actions

		CreateCashPayment is a Create Action
			Entrance Rules
		    	BankTransactionType = BankTransactionType.CashPayment

		CreateBillOfExchangePayment is a Create Action
			Entrance Rules
		    	BankTransactionType = BankTransactionType.BillOfExchangePayment

		CreateBankService is a Create Action
			Entrance Rules
		    	BankTransactionType = BankTransactionType.BankService

		Update is an Update Action
								
		Delete is a Delete Action
		
		IncrementTransactionIDNumber is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance 
			Action Rules
				TransactionIDNumber = TransactionIDNumber + 1
				
		UpdateTransactionIdNumber is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmTransactionIDNumber is a TransactionIDNumber
			Action Rules
				TransactionIDNumber = PrmTransactionIDNumber
				
		SetAsInUse is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				UsedFlag = true
		
		FastUpdate is an Update Action
			restricted
			bypass field rules
				
				
