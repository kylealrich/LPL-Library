Ledger is a BusinessClass
    owned by GeneralLedger
    prefix is GLLED
    representative text is "<DerivedRepresentativeText>"


    Ontology
    	symbolic key is Ledger
        
	Patterns
		implements ChildHierarchy
			children are ChildrenRel
			child set is HierarchyDetailRel
				child is SubordinateLedger
			top node when (IsTopNode)

	Context Fields
		ReportingBasis
    Persistent Fields
        Description
		Active				is Boolean
		RecordType
		CloseLedger			is Boolean
		TopNode				is AlphaUpper 20 
		IsTopNode			is Boolean
		CurrencyLedger		is Boolean
		RetainedEarningsLedger	is Boolean		
		EliminationLedger		is Boolean
		RemoveFromReportingBasisInProgress is Boolean

	Local Fields
		LocalTopNode			is AlphaUpper 20
		LocalLedger				is a Ledger
		AccountAnalysisLedgerTopNode	is like Ledger
		RemoveRBBackground		is a AsyncActionRequest

	Derived Fields
		DerivedRepresentativeText	is a DerivedField
			type is Alpha 120
			restricted
			if (IsTopNode)
				return Description
			else				
				return DerivedRepresentativeTextString
		DerivedRepresentativeTextString is a StringField	
			type is Alpha 120
			Ledger
			" - "
			Description
		DerivedTopNode	is a DerivedField
			type is AlphaUpper 20
			restricted
			return ReportingBasis.LedgerTopNode				
		DerivedReportingBasis	is a DerivedField
			type is AlphaUpper 15
			restricted
			return ReportingBasis				
		StatusLabel				is a StringField
			type is Alpha 40 
			"Active"
			Active

		ActiveMessage is a LabelField
			"Active"

		InactiveMessage is a LabelField
			"Inactive"
		DerivedStatus is a DerivedField
			type is Alpha 15
			if (Active)
				return ActiveMessage
			if (!Active)
				return InactiveMessage

    Conditions
		LedgerForElimination
			restricted
			when (EliminationLedger)
    	UserDefinedLedger
    		restricted
    		when (RecordType.User)
		SubordinatesExist
			restricted
			when (HierarchyDetailRel exists)
		HierarchyRecordsExist
			restricted
			when (SubordinateInTopNodeRel exists)
		LedgersForReportingBasis
			restricted
			when (ParentRel exists)
		HelperListLedgers
			restricted
			when (UserDefinedLedger
			and  !IsTopNode
			and  !HelperListLedgerRel exists)
    	ForCurrencyLedger
    		restricted
    		when (TopNode not entered)
		BasisLedgersWithoutCurrencyAndClose
			restricted
			when (ParentRel exists
			and   !CloseLedger
			and   !CurrencyLedger)
		FutureChanges
			restricted
			when (this instance.has future changes)
		BasisUserLedgers
			restricted
			when (ParentRel exists
			and   RecordType.User
			and   !CloseLedger
			and   !CurrencyLedger)
		NotValidForJournalControl   
			restricted
			when (IsTopNode
			or  CloseLedger
			or  RetainedEarningsLedger)		
		SecurityGroupAllowsAccess
			when (actor.context.LedgerBasisSecurityGroup = blank
			or RecordType.System
			or LedgerSecurityGroupMemberRel exists)
		RemoveFromReportingBasisIsProcessing
			restricted
			when (RemoveFromReportingBasisInProgress = true)
		RemoveFromReportingBasisIsValid
			restricted
			when (RemoveFromReportingBasisInProgress = false
			and !ReportingBasis.IsForAccountAnalysis)

	Relations
		ChildrenRel
			one-to-many relation to Ledger
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				related.Ledger					= HierarchyDetailRel.SubordinateLedger
		ChildrenNoCloseRel		
			one-to-many relation to Ledger
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Ledger					= HierarchyDetailRel.SubordinateLedger
			Instance Selection
				where (!related.CloseLedger)
		RetainedEarningsProcessRel		
			one-to-many relation to Ledger
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Ledger					= HierarchyDetailRel.SubordinateLedger
			Instance Selection
				where (!related.CloseLedger
				and    !related.RetainedEarningsLedger)
		HierarchyDetailRel
			one-to-many relation to LedgerHierarchy
			Field Mapping uses ByParent
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Ledger					= Ledger
		ParentRel
			one-to-many relation to LedgerHierarchy
			Field Mapping uses BySubordinate
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.SubordinateLedger		= Ledger
			Instance Selection
				where (related.Ledger.TopNode	= DerivedTopNode)
		SubordinateInTopNodeRel
			one-to-many relation to LedgerHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TopNode					= DerivedTopNode
				related.SubordinateLedger		= Ledger			
		HierarchyRecordRel
			one-to-one relation to LedgerHierarchy
			Field Mapping uses SubordinateInTopNode
				related.FinanceEnterpriseGroup       = FinanceEnterpriseGroup
				related.TopNode           			 = DerivedTopNode
				related.SubordinateLedger 			 = LocalLedger 								
		HierarchyRecordForAccountAnalysisRel
			one-to-one relation to LedgerHierarchy
			Field Mapping uses SubordinateInTopNode
				related.FinanceEnterpriseGroup       = FinanceEnterpriseGroup
				related.TopNode           			 = AccountAnalysisLedgerTopNode
				related.SubordinateLedger 			 = LocalLedger 								
		HelperListLedgerRel
			one-to-many relation to LedgerHierarchy
			Field Mapping uses SubordinateInTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TopNode					= ReportingBasis.LedgerTopNode
				related.SubordinateLedger		= Ledger 
		AccountingEntityRel		is a AccountingEntity set
		ReportingBasisForLedgerRel
			one-to-many relation to LedgerHierarchy
			Field Mapping uses BySubordinate
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.SubordinateLedger		= Ledger
		ReportingBasisRel
			one-to-many relation to ReportingBasis
			Field Mapping uses ByLedgerTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.LedgerTopNode			= ReportingBasisForLedgerRel.TopNode
				related.AccountAnalysisString	= blank
		LedgerSecurityGroupMemberRel
            one-to-one relation to LedgerBasisGroupMember
            Field Mapping uses part of key
                related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
                related.LedgerBasisGroup            = actor.context.LedgerBasisSecurityGroup.FinanceDimensionStructure
                related.Ledger                      = Ledger

	Sets				
				
	Field Rules
		Active
			initial value is true
			default to true
			if (RecordType.System)
				constraint (Active)
					"System_LedgersCannotBeInactive"				
		RecordType
			cannot be changed
				"Record_TypeIsASystemSettingAndCannotBeChanged"
			if (!ReportingBasis entered
			and !parentcontext.name = "FinanceEnterpriseGroup")
				if (action type.Create)
					constraint (RecordType.User)
						"SystemLedgersCannotBeManuallyCreated"
		CloseLedger
			cannot be changed
				"Close_LedgerIsASystemSettingAndCannotBeChanged"
			constraint (ReportingBasis entered)
				"SystemLedgersCannotBeManuallyCreatedOrUpdated"
		TopNode
			cannot be changed
				"Top_NodeIsASystemSettingAndCannotBeChanged"
			constraint (ReportingBasis entered)
				"SystemLedgersCannotBeManuallyCreatedOrUpdated"
		IsTopNode
			cannot be changed
				"Is_Top_NodeIsASystemSettingAndCannotBeChanged"
			constraint (ReportingBasis entered)
				"SystemLedgersCannotBeManuallyCreatedOrUpdated"
		CurrencyLedger
			cannot be changed
				"Currency_LedgerIsASystemSettingAndCannotBeChanged"
			constraint (ReportingBasis entered)
				"SystemLedgersCannotBeManuallyCreatedOrUpdated"
										
	Attach Rules
		if (!parentcontext.name = "LedgerHierarchy"
		and !parentcontext.name = "EntityLedgerPeriod"
		and !parentcontext.name = "GainLossResult"
		and !parentcontext.name = "CodeBlockRelationRule"
		and !parentcontext.name = "GLReport"
		and !parentcontext.name = "GLReportGrouping"
		and !parentcontext.name = "GLReportGroupingDetail"
		and !parentcontext.name = "GLReportFilterDimension"
		and !parentcontext.name = "GLColumnFilterDimension")
			constraint (Active)
				"LedgerIsInactive"	
			
    Actions
		T2VCreate is a Create Action
			restricted
			default label is untranslatable
			bypass field rules

		T2VUpdate is an Update Action
			restricted
			default label is untranslatable
			bypass field rules
		
		Create is a Create Action
			Entrance Rules

				if (UserDefinedLedger)
					constraint (Ledger size <= 15)
						"OnlyFifteenCharactersCanBeEntered"
					constraint (Description entered)
						"DescriptionIsRequired"
				else
					Active = true
					
		Update is an Update Action
			Entrance Rules
				if (UserDefinedLedger)
					constraint (Description entered)
						"DescriptionIsRequired"
			Action Rules
				if (FinanceEnterpriseGroup.EnableRelatedPartyElimination)
					if (EliminationLedger changed
					and !EliminationLedger)
						confirmation required
							"ThisChangeWillInvalidateAllInterEntityRelationsReferencingThisLedger;Proceed?"

		Delete is a Delete Action
			valid when (UserDefinedLedger)
			Entrance Rules
				constraint (!ReportingBasisForLedgerRel exists)
					"CannotDelete;LedgerUsedInReportingBasis<first ReportingBasisRel.ReportingBasis>"











		
		DeleteSystemLedger is a Delete Action
			restricted

		Purge is a Purge Action
			restricted

		AddToReportingBasis is an Instance Action
			valid when(!ReportingBasis.IsForAccountAnalysis)
			Action Rules


				invoke Create LedgerHierarchy
					invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroup				
					invoked.Ledger						= DerivedTopNode 
					invoked.SubordinateLedger    		= Ledger
					invoked.TopNode						= DerivedTopNode 

		RemoveFromReportingBasis is an Instance Action
			valid when(RemoveFromReportingBasisIsValid)	
			Entrance Rules
				constraint (UserDefinedLedger)
					"OnlyUserAssignedLedgersCanBeRemoved"
			Action Rules
				RemoveFromReportingBasisInProgress = true
				LocalLedger		= Ledger
				invoke DeleteInBackground LedgerHierarchy in background
					assign async action request id to RemoveRBBackground
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmLedger						= DerivedTopNode 
					invoked.PrmSubordinateLedger    		= LocalLedger
				invoke DeleteInBackgroundComplete in background
					run after RemoveRBBackground

		DeleteInBackgroundComplete is an Instance Action
			restricted
			Action Rules
				RemoveFromReportingBasisInProgress = false
