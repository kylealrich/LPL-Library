CustodialAccountEdit is a BusinessClass
	owned by cam
	prefix is CamAe

    Ontology
    	symbolic key is CustodialAccountEdit

    Patterns
        implements DynamicCreation
		disable AuditIndex
		disable Auditing 
		disable EffectiveDated
    	
    Persistent Fields
 		FinanceEnterpriseGroup
  		OriginatingTransaction		is BusinessObjectReference
  		AccountingEntity
			default label is "<FinanceEnterpriseGroup.AccountingEntityLabel>"
        FinanceDimension10
			default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>"
 		EditAmount					is an InternationalAmount
 		CommittedAmount				is an InternationalAmount
 		ExceedsBalance				is Boolean
 		EditGroup					is like CustodialDetailAmount
				    			
	Derived Fields
    	DerivedEditAmount is a DerivedField
			type is like InternationalAmount
			if (EditGroup entered)
				return (sum CustodialAccountEditByEditGroupRel.EditAmount + EditAmount)
			else
				return EditAmount

		DerivedLocalRoundTo is a DerivedField
			type is Decimal 5.4
			restricted
			if (LocalNumberOfDecimals = 2)
				return .01
			else
			if (LocalNumberOfDecimals = 0)
				return 1
			else
			if (LocalNumberOfDecimals = 3)
				return .001
			else
			if (LocalNumberOfDecimals = 4)
				return .0001

		ErrorMessage is a MessageField
			restricted
			"DistributionAmountExceedsAvailableBalance:_<first CustodialDetailAmountRel.EditAvailableAmount>,_For_<FinanceEnterpriseGroup.FinanceDimension10Label>_<FinanceDimension10>"

		DerivedSummary is a DerivedField
			type is like CustodialAccountManagement
			restricted
			return CustodialDetailAmountRel.CustodialAccountManagement			

	Local Fields
		LocalNumberOfDecimals					is Numeric 1

	Field Rules
		EditAmount
			EditGroup = CustodialDetailAmountRel.ParentCustodialDetailAmount
			if (FinanceEnterpriseGroup.CustodialAccountManagement
			and  DerivedEditAmount > 0 
			and  DerivedEditAmount > CustodialDetailAmountRel.EditAvailableAmount)
				ExceedsBalance = true
			else
				ExceedsBalance = false

	Conditions
		IncludedInEditGroup
			when (EditGroup entered)

	Relations
		CustodialDetailAmountRel
			one-to-many relation to CustodialDetailAmount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.CustodialAccountManagement.AccountingEntity = AccountingEntity
				and    related.CustodialPosting = FinanceDimension10
				and 	related.EditLevel.Individual)

		CustodialAccountEditByEditGroupRel
			one-to-many relation to CustodialAccountEdit
			Field Mapping uses ByEditGroup
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.OriginatingTransaction	= OriginatingTransaction
				related.EditGroup				= EditGroup
			Instance Selection
				where (related.CustodialAccountEdit != CustodialAccountEdit
				and	   related.DerivedSummary 		= DerivedSummary)

	Sets
		ByOriginatingTransaction
			Sort Order
		 		FinanceEnterpriseGroup
		  		OriginatingTransaction
		  		AccountingEntity
		 		FinanceDimension10

		ByExceedsBalance
			Sort Order
		 		FinanceEnterpriseGroup
		  		OriginatingTransaction
		  		ExceedsBalance
		 		FinanceDimension10

		ByEditGroup
			Instance Selection
				where (IncludedInEditGroup)
			Sort Order
		 		FinanceEnterpriseGroup
		  		OriginatingTransaction
		  		EditGroup
		 		FinanceDimension10

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

		InitializeEditAmount is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				initialize EditAmount

		UpdateDisbursements is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateDisbursements CustodialDetailAmountRel
					invoked.PrmDisbursements += EditAmount
				CommittedAmount += EditAmount	
				initialize EditAmount

		CancelDisbursements is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateDisbursements CustodialDetailAmountRel
					invoked.PrmDisbursements -= CommittedAmount
				initialize CommittedAmount
				
		UpdateCommittedAmount is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateCommittedAmount CustodialDetailAmountRel
					invoked.PrmCommittedAmount = EditAmount
				CommittedAmount += EditAmount
				initialize EditAmount

		CancelCommittedAmount is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateCommittedAmount CustodialDetailAmountRel
					invoked.PrmCommittedAmount = (CommittedAmount*-1)
				initialize CommittedAmount
				
		RelieveCommittedAmount is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmPaymentPercent		is a Percent
				PrmNumberOfDecimals		is Numeric 1
				PrmVoidReinstatement	is Boolean
			Local Fields
				LocalPaidAmount		is an InternationalAmount
			Action Rules
				if (PrmPaymentPercent != 1)
					LocalPaidAmount 		= CommittedAmount * PrmPaymentPercent * -1
					LocalNumberOfDecimals	= PrmNumberOfDecimals	
					round LocalPaidAmount to nearest DerivedLocalRoundTo
				else
					LocalPaidAmount = CommittedAmount * -1
				if (PrmVoidReinstatement)
					LocalPaidAmount = LocalPaidAmount * -1
				invoke UpdateCommittedAmount CustodialDetailAmountRel
					invoked.PrmCommittedAmount = LocalPaidAmount
				
		RelieveAmmendedCommittedAmount is an Instance Action		
			default label is untranslatable
			restricted
			Parameters
				PrmPaymentPercent		is a Percent
				PrmNumberOfDecimals		is Numeric 1
			Local Fields
				LocalPaidAmount		is an InternationalAmount
			Action Rules
				if (EditAmount	entered)	
					if (PrmPaymentPercent != 1)
						LocalPaidAmount			= EditAmount * PrmPaymentPercent * -1
						LocalNumberOfDecimals	= PrmNumberOfDecimals	
						round LocalPaidAmount to nearest DerivedLocalRoundTo
					else
						LocalPaidAmount			= EditAmount * -1
					invoke UpdateCommittedAmount CustodialDetailAmountRel
						invoked.PrmCommittedAmount = LocalPaidAmount
				
