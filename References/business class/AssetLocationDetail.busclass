AssetLocationDetail is a BusinessClass
    owned by am
    prefix is LOD
    classic name is AMLOCDTL

    Ontology
        symbolic key is AssetLocationDetail
            classic set name is LODSET1
            classic name is LOC-DTL
            classic name for AssetLocation is LOCATION-NAME

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Description
        Level1                is AlphaUpper size 14
        Level2                is AlphaUpper size 14
        Level3                is AlphaUpper size 14
        GeographicalType      is a GISType
            classic name is GEO-TYPE
        GeographicalLatitude  is a GISCoordinate
            classic name is GEO-LATITUDE
        GeographicalLongitude is a GISCoordinate
            classic name is GEO-LONGITUDE
        GeographicalAltitude  is a GISCoordinate
            classic name is GEO-ALTITUDE
        GeographicalDate      is TimeStamp
            classic name is GEO-DATE


	Local Fields
		I1						is Numeric 2
		I2						is Numeric 2
		LocalCharacter			is AlphaUpper 14
		LocalLocationDetail		is AlphaUpper 14

	Field Rules
		AssetLocationDetail
			LocalCharacter = "^^^^^^^^^^^^^^"
			I1 = AssetLocation.DetailLevel1Size - Level1 size
			I2 = AssetLocation.DetailLevel2Size - Level2 size

			LocalLocationDetail = Level1 + LocalCharacter[1:I1] + Level2 + LocalCharacter[1:I2] + Level3


			I1 = 1
			while (I1 < 14)

				if (LocalLocationDetail[I1] = "^")
					LocalLocationDetail[I1] = " "

				I1 += 1


			AssetLocationDetail = LocalLocationDetail

		Level1
			cannot be changed
				"Level1CannotBeChanged"
			constraint (Level1 size <= AssetLocation.DetailLevel1Size)
				"<Level1>ExceedsTheLocationLevelSize<AssetLocation.DetailLevel1Size>"

		Level2
			cannot be changed
				"Level2CannotBeChanged"
			constraint (Level2 size <= AssetLocation.DetailLevel2Size)
				"<Level2>ExceedsTheLocationLevelSize<AssetLocation.DetailLevel2Size>"

		Level3
			cannot be changed
				"Level3CannotBeChanged"
			constraint (Level3 size <= AssetLocation.DetailLevel3Size)
				"<Level3>ExceedsTheLocationLevelSize<AssetLocation.DetailLevel3Size>"
		GeographicalDate
			if (GeographicalLatitude changed
			or GeographicalLongitude changed
			or GeographicalAltitude changed)
				force default to current timestamp

	Actions
		Create is a Create Action
			Entrance Rules
				constraint (!AssetLocationDetail entered)
					"AssetLocationDetailIsSystemAssigned;SelectYesToProceesAndDiscardAssignedValue"
					

		Update is an Update Action

		Delete is a Delete Action            
