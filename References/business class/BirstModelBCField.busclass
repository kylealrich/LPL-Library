BirstModelBCField is a BusinessClass
	owned by la
	prefix is BMBF
	
	Ontology
		symbolic key is BirstModelBCField
		
	Patterns
		disable AsOfDateProcessing
		implements Resequence on OrderNumber
			new sequence field is NewOrderNumber
			set is ByOrderNumber

	Persistent Fields
		BusinessClass					is a snapshot of RepSetBC.BusinessClass
		RelatedValue					is Alpha size 300
		OrderNumber						is Numeric size 4
			default label is "SortOrder"
		BirstFieldName					is Alpha size 300
		SQLName							is Alpha size 3000
		IsAttribute						is Boolean
		    default label is "Attribute"
		IsMeasure						is Boolean
		    default label is "Measure"
		IsHierarchyLevel				is Boolean
		    default label is "HierarchyLevel"
		HierarchyParentField			is Alpha size 300
		HierarchyPostingField			is Alpha size 300
		FieldType						is Numeric size 1
            States
                BaseField value is 0
                FormulaField value is 1
		FieldDisplayType				is Numeric size 1
            States
                None				value is 0
                Code				value is 1
				Description			value is 2
				SequenceNumber		value is 3               
		IncludeInBirstModel				is Boolean
		DataType						is Alpha size 30
		Width							is Numeric size 4
		IsBooleanField					is Boolean
		    default label is "BooleanField"
		IsStateField					is Boolean
		    default label is "StateField"
		IsDimensionKey					is Boolean
		    default label is "DimensionKey"
		IsForeignKey					is Boolean
		    default label is "ForeignKey"
		ForeignKeyBusClass				is Alpha size 100
		ForeignKeyQuerySource			is a BirstModelQuerySource
		ForeignKeyHierarchyBC			is Alpha size 100
		FKHierarchyRequiredFilter		is Alpha size 100
		HierarchyTopNode				is Boolean
		HierarchyLevelNumber			is Numeric size 2
		HierarchyFieldType				is Numeric size 1
		    States
                None				value is 0
                LevelCode			value is 1
				LevelDescription	value is 2
				DimensionType		value is 3
				SequenceNumber		value is 4

	Field Rules



	Transient Fields
		NewOrderNumber is Numeric size 4
			default label is "NewSortOrder"			
			
	Derived Fields
	
		BirstTableName is a DerivedField
			type is Alpha size 60
			return BirstModelBC.DisplayName

		MeasurePrefix is a DerivedField
			type is Alpha size 10
			return BirstModelBC.MeasurePrefix

		UseColumnarVariationPattern is a DerivedField
			type is Boolean
			return RepSetBC.UseColumnarVariationPattern

		BCType is a DerivedField
			type is Numeric size 1
            return BirstModelBC.BCType

		ForeignKeyBusClassBirstTableName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyBusClassDimKeyRel.BirstTableName

		ForeignKeyBusClassBirstFieldName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyBusClassDimKeyRel.BirstFieldName

		ForeignKeyHierarchyBCBirstTableName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyHierarchyBCDimKeyRel.BirstTableName

		ForeignKeyHierarchyBCBirstFieldName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyHierarchyBCDimKeyRel.BirstFieldName

		ForeignKeyQuerySourceBirstTableName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyQuerySourceDimKeyRel.BirstTableName

		ForeignKeyQuerySourceBirstFieldName is a DerivedField
			type is Alpha size 300
			return first ForeignKeyQuerySourceDimKeyRel.BirstFieldName

	Sets
		ByOrderNumber
			indexed
			Sort Order
				BirstModel
				ReplicationSet
				RepSetBC
				OrderNumber
				BirstModelBCField

		ByRelatedValue
			indexed
			Sort Order
				BirstModel
				ReplicationSet
				RepSetBC
				RelatedValue

		ByForeignKey
			duplicates
			Sort Order
				BirstModel
				ReplicationSet
				RepSetBC
				IncludeInBirstModel
				IsForeignKey
				ForeignKeyBusClass
				BirstFieldName		

		ByBCRelatedValue
			indexed
			Sort Order
				BirstModel
				BusinessClass	
				RelatedValue
				
		ByBCFieldDisplayType
			duplicates
			Sort Order
				BirstModel
				BusinessClass
				FieldDisplayType

		ByBCForeignKeyBC
			duplicates
			Sort Order
				BirstModel
				BusinessClass	
				ForeignKeyBusClass

		ByBCHierarchyField
			duplicates
			Sort Order
				BirstModel
				BusinessClass
				HierarchyFieldType
				HierarchyLevelNumber			

		ByDimensionKey
			indexed
			Sort Order
				BirstModel
				BusinessClass
				IncludeInBirstModel
				IsDimensionKey
				RelatedValue

	Actions
		Create is an Action
		Update is an Action
		Delete is an Action
		Purge is a Purge Action
		
			
	Relations		
		ForeignKeyBusClassDimKeyRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByDimensionKey
				related.BirstModel			= BirstModel
				related.BusinessClass		= ForeignKeyBusClass
				related.IncludeInBirstModel	= true
				related.IsDimensionKey		= true

		ForeignKeyHierarchyBCDimKeyRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByDimensionKey
				related.BirstModel			= BirstModel
				related.BusinessClass		= ForeignKeyHierarchyBC
				related.IncludeInBirstModel	= true
				related.IsDimensionKey		= true
				
		ForeignKeyQuerySourceDimKeyRel
			one-to-many relation to BirstModelQuerySourceField
			Field Mapping uses ByDimensionKey
				related.BirstModel				= BirstModel
				related.BirstModelQuerySource	= ForeignKeyQuerySource
				related.IncludeInBirstModel		= true
				related.IsDimensionKey			= true
