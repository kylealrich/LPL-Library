ProcurementCardStatement is a BusinessClass
    owned by po
    prefix is PXH
    classic name is PDCHRGHDR

    Ontology
        symbolic key is ProcurementCardStatement
            classic set name is PXHSET1
            classic name is STATEMENT
            classic name for ProcurementCardProgram is PCARD-PROGRAM

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Invoice
        Company                is a GeneralLedgerCompany
			disable surrogates
        StatementDate          is Date
            classic name is STMT-DATE
        BankEntity
        StatementFromDate      is Date
            classic name is STMT-FR-DATE
        StatementToDate        is Date
            classic name is STMT-TO-DATE
        InterfaceInProcess				is Boolean
        OriginatingInterfaceRun			is a ProcurementCardStatementInterfaceResult          
			delete ignored
        TotalChargeCount       is a Quantity
            classic name is TOT-CHRG-CNT
        TotalChargeAmount      is an InternationalAmount
            classic name is TOT-CHRG-AMT
        TotalCreditCount       is a Quantity
            classic name is TOT-CREDIT-CNT
        TotalCreditAmount      is an InternationalAmount
            classic name is TOT-CREDIT-AMT
        StatementTotal         is an InternationalAmount
            classic name is STMT-TOTAL
        AdHocTotal             is an InternationalAmount
        AdHocTaxTotal          is an InternationalAmount
        Canceled               is Boolean
            classic name is CANCEL-FL
        CancelDate             is Date
        ShipToPostalCode       is a PostalCode
            classic name is SHP-TO-POST-CD
        ShipFromPostalCode     is a PostalCode
            classic name is SHP-FR-POST-CD
        DestinationCountryCode is Numeric size 5
            classic name is DEST-CTRY-CODE
			default label is "DestinationCountry/\JurisdictionCode"
        DiscountAmount         is an InternationalAmount
            classic name is DISCOUNT-AMT
        FreightAmount          is an InternationalAmount
            classic name is FREIGHT-AMT
        DutyAmount             is an InternationalAmount
            classic name is DUTY-AMT
        VatReferenceNumber     is AlphaUpper size 16
            classic name is VAT-REF-NBR
        VatTaxAmount           is an InternationalAmount
            classic name is VAT-TAX-AMT
        VatTaxRate             is a ChrgRate
        OrderDate              is Date
		NumberOfPOChargesReleased 		is Numeric 6
		NumberOfAdHocChargesReleased    is Numeric 6
		NumberOfPOChargesClosed         is Numeric 6
		NumberOfAdHocChargesClosed      is Numeric 6
		NumberOfPOChargesApproved       is Numeric 6
		NumberOfAdHocChargesApproved    is Numeric 6
		ValueOfPOChargesReleased        is an InternationalAmount
		ValueOfAdHocChargesReleased     is an InternationalAmount 
		ValueOfPOChargesClosed          is an InternationalAmount 
		ValueOfAdHocChargesClosed       is an InternationalAmount 
		ValueOfPOChargesApproved        is an InternationalAmount 
		ValueOfAdHocChargesApproved     is an InternationalAmount
		ManualStatusChanges             is Boolean
		PostingDate

	Local Fields
		LocalTestInvoice       is like Invoice
	
	Derived Fields
	
		TotalNumberOfChargesReleased is a ComputeField
			type is Numeric 6
			(NumberOfPOChargesReleased + NumberOfAdHocChargesReleased)
			
		TotalNumberOfChargesClosed is a ComputeField
			type is Numeric 6
			(NumberOfPOChargesClosed + NumberOfAdHocChargesClosed)
			
		TotalNumberOfChargesApproved is a ComputeField
		    type is Numeric 6
		    (NumberOfPOChargesApproved + NumberOfAdHocChargesApproved)
		    
		TotalValueOfChargesReleased is a DerivedField
			type is like InternationalAmount
			return (ValueOfPOChargesReleased + ValueOfAdHocChargesReleased)
			
		TotalValueOfChargesClosed is a DerivedField
			type is like InternationalAmount
			return (ValueOfPOChargesClosed + ValueOfAdHocChargesClosed)
			
		TotalValueOfChargesApproved is a DerivedField
			type is like InternationalAmount
			return (ValueOfPOChargesApproved + ValueOfAdHocChargesApproved)
		
		DerivedCompanyIDName is a MessageField
			default label is "Company"
			"<Company>_-_<Company.Name>"
		
		DerivedCompanyIDNameDisplay is a DerivedField
			type is MessageField
			if (Company entered)
				return DerivedCompanyIDName

	Conditions
		UninvoicedStatement
			when (Invoice not entered)
			
		HasAdhocTransactions
			restricted
			when (AdHocTransactionsRel exists)
			
		AvailableForInvoicing
			restricted
			when (UninvoicedStatement
			and   !InterfaceInProcess
			and   !Canceled)
			
		VendorTermsCodeEntered
			restricted
			when (VendorRel.TermsCode entered)
			
		InvoiceAlreadyExists
			restricted
			when (InvoiceExistsRel exists)
			
		ApprovedTransactionsExist
			restricted
			when (Invoice entered
			and  (ApprovedPOTransactionsRel exists
			or    ApprovedAdHocTransactionsRel exists))
			
		ApprovableTransactionsExist
			restricted
			when (Invoice entered
			and   (ApprovablePOTransactionsRel exists
			or    ApprovableAdHocTransactionsRel exists))
			
		DisputedTransactionsExist
			restricted
			when (Invoice entered
			and  (DisputedPOTransactionsRel exists
			or    DisputedAdHocTransactionsRel exists))
			
		ClosedTransactionsExist
			restricted
			when (Invoice entered
			and  (ClosedPOTransactionsRel exists
			or    ClosedAdHocTransactionsRel exists))
			
		VendorActive
			restricted
			when (VendorRel.VendorStatus = "A")
		
		HasUnreleasedInvoices
			restricted
			when (UnreleasedInvoicesRel exists)
			
		HasDistributionsOutOfBalance
			restricted
			when (DistributionsOutOfBalanceRel exists)
				
		FrontEndSplitsEnabled
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup.FrontEndSplits)

		AllowFrontEndSplit
			restricted
			when (any UpdateableNonZeroTransactionsRel.AllowFrontEndSplit
			and  !GeneralLedgerCompanyRel.FinanceEnterpriseGroup.UseFundedAmounts)

		HasFrontEndSplitDistributions
			restricted
			when (any UpdateableNonZeroTransactionsRel.HasFrontEndSplitDistributions)

	Relations
		
		PositiveTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			delete cascades
			Field Mapping uses symbolic key
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
			Instance Selection	
				where (related.ItemCharge > 0)
				
		NegativeTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			delete cascades
			Field Mapping uses symbolic key
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
			Instance Selection	
				where (related.ItemCharge < 0)
	
		AdHocTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			delete cascades
			Field Mapping uses ByStatusNotPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				
		ReleasedPOTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 1 
		
		ClosedPOTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 9 
		
		ApprovablePOTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
			Instance Selection 
				where (related.Status                           = 1 
			    or     related.Status                           = 3 
			    or     related.Status                           = 8) 
			    
		ApprovedPOTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 2 
		
		DisputedPOTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 3 
		
		ReleasedAdHocTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusNotPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 1 
		
		ClosedAdHocTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusNotPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 9 
		
		ApprovableAdHocTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusNotPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
			Instance Selection 
				where (related.Status                           = 1 
			    or     related.Status                           = 3 
			    or     related.Status                           = 8) 
			    
		ApprovedAdHocTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusNotPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 2 
		
		DisputedAdHocTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			Field Mapping uses ByStatusNotPO
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
				related.Status                                  = 3 
		
		DistributionsOutOfBalanceRel
			one-to-many relation to ProcurementCardStatementTransaction 
			Field Mapping uses ByDistributionOutOfBalance
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
		
		PayablesInvoicesRel
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByProcurementCardStatement
				related.ProcurementCardProgram    	= ProcurementCardProgram
				related.ProcurementCardStatement 	= ProcurementCardStatement	
	
		UnreleasedInvoicesRel
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByProcurementCardStatement
				related.ProcurementCardProgram    	= ProcurementCardProgram
				related.ProcurementCardStatement 	= ProcurementCardStatement	
			Instance Selection
				where (related.Status.Unreleased)
				
		InvoiceExistsRel
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByVendorInvoice
				related.Vendor                      = ProcurementCardProgram.Vendor
				related.Invoice                     = LocalTestInvoice
				related.Company                     = Company

		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup       = ProcurementGroup
				related.Vendor            = ProcurementCardProgram.Vendor
				
		ProcurementCardProgramCompaniesRel
			one-to-many relation to ProcurementCardProgramCompany
   			Field Mapping uses symbolic key
   				related.ProcurementGroup			   =  ProcurementGroup
   				related.ProcurementCardProgram         =  ProcurementCardProgram

		UpdateableNonZeroTransactionsRel
			one-to-many relation to ProcurementCardStatementTransaction
			delete cascades
			Field Mapping uses symbolic key
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatement
			Instance Selection	
				where (related.ItemCharge != 0
				and	related.IsUpdate)

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

	Sets
	
		ByStatementDateDescending
			Sort Order
				StatementDate descending
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatement
	
	Field Rules
	
		StatementFromDate
			required
		
		StatementToDate
			required
	
			constraint(StatementToDate >= StatementFromDate)
				"StatementToDateCannotBePriorToStatementFromDate"
		
		StatementDate
			default to current corporate date
	
	Actions
        Create is a Create Action
        	restricted
        		
        Update is an Update Action
        	restricted
        		
        Delete is a Delete Action
        	restricted

		UpdateTotals is a Set Action
			restricted
			Parameters
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatement
				
			Action Rules
				
				Instance Rules
					TotalChargeCount  = instance count of PositiveTransactionsRel
					TotalChargeAmount = sum PositiveTransactionsRel.ItemCharge
					TotalCreditCount  = instance count of NegativeTransactionsRel
					TotalCreditAmount = sum NegativeTransactionsRel.ItemCharge
					StatementTotal    = sum ProcurementCardStatementTransaction set.ItemCharge					
					AdHocTotal        = sum AdHocTransactionsRel.ItemCharge
					AdHocTaxTotal     = sum AdHocTransactionsRel.TaxAmount
		
		UpdateFromInterface is an Instance Action
			restricted
			Parameters
				SetInterfaceInProcessFalse 	is Boolean
				SetInterfaceInProcessTrue  	is Boolean
				PrmOriginatingInterfaceRun 	is like ProcurementCardStatementInterfaceResult
			
			Action Rules
				if (SetInterfaceInProcessFalse = true)
					InterfaceInProcess = false
					invoke UpdateTotals
						invoked.ProcurementGroup  			= ProcurementGroup
						invoked.ProcurementCardProgram 		= ProcurementCardProgram
						invoked.ProcurementCardStatement 	= ProcurementCardStatement
				if (SetInterfaceInProcessTrue = true)
					InterfaceInProcess = true
				if (PrmOriginatingInterfaceRun entered)
					OriginatingInterfaceRun = PrmOriginatingInterfaceRun
					
		ApproveAll is an Instance Action
			valid when (!UninvoicedStatement)
			
			Action Rules
			
				invoke ApproveAllTransactions ProcurementCardStatementTransaction
					invoked.ProcurementGroup 			= ProcurementGroup
					invoked.ProcurementCardProgram 		= ProcurementCardProgram
					invoked.ProcurementCardStatement 	= ProcurementCardStatement
					
			Exit Rules
				
				ManualStatusChanges = true
				invoke RecalculateChargeValues
						
		RejectAll is an Instance Action
			valid when (!UninvoicedStatement)
			
			Action Rules
			
				invoke RejectAllTransactions ProcurementCardStatementTransaction
					invoked.ProcurementGroup 			= ProcurementGroup
					invoked.ProcurementCardProgram 		= ProcurementCardProgram
					invoked.ProcurementCardStatement 	= ProcurementCardStatement
					
			Exit Rules
				
				ManualStatusChanges = true
				invoke RecalculateChargeValues
					
		CloseAll is an Instance Action
			valid when (!UninvoicedStatement)
			
			Action Rules
			
				invoke CloseAllTransactions ProcurementCardStatementTransaction
					invoked.PrmProcurementGroup 			= ProcurementGroup
					invoked.PrmProcurementCardProgram 		= ProcurementCardProgram
					invoked.PrmProcurementCardStatement 	= ProcurementCardStatement
					
			Exit Rules
				
				ManualStatusChanges = true
				invoke RecalculateChargeValues
					
		RecalculateChargeValues is an Instance Action
			valid when (ManualStatusChanges)
			
			Action Rules
			
				invoke CalculateChargeValues
					invoked.PrmProcurementGroup             = ProcurementGroup
					invoked.PrmProcurementCardProgram       = ProcurementCardProgram
					invoked.PrmProcurementCardStatement     = ProcurementCardStatement
		
		CalculateChargeValues is a Set Action
			restricted
			Parameters
				PrmProcurementGroup			is a ProcurementGroup
				PrmProcurementCardProgram	is a ProcurementCardProgram
				PrmProcurementCardStatement	is a ProcurementCardStatement
				
			Instance Selection
				where (PrmProcurementGroup 			= ProcurementGroup
				and	   PrmProcurementCardProgram 	= ProcurementCardProgram
				and	   PrmProcurementCardStatement 	= ProcurementCardStatement)
				
			Action Rules
				
				Instance Rules
				
					NumberOfPOChargesReleased 		= instance count of ReleasedPOTransactionsRel		
					NumberOfAdHocChargesReleased    = instance count of ReleasedAdHocTransactionsRel
					NumberOfPOChargesClosed         = instance count of ClosedPOTransactionsRel
					NumberOfAdHocChargesClosed      = instance count of ClosedAdHocTransactionsRel
					NumberOfPOChargesApproved       = instance count of ApprovedPOTransactionsRel
					NumberOfAdHocChargesApproved    = instance count of ApprovedAdHocTransactionsRel
					ValueOfPOChargesReleased        = sum ReleasedPOTransactionsRel.ItemCharge
					ValueOfAdHocChargesReleased     = sum ReleasedAdHocTransactionsRel.ItemCharge
					ValueOfPOChargesClosed          = sum ClosedPOTransactionsRel.ItemCharge
					ValueOfAdHocChargesClosed       = sum ClosedAdHocTransactionsRel.ItemCharge
					ValueOfPOChargesApproved        = sum ApprovedPOTransactionsRel.ItemCharge
					ValueOfAdHocChargesApproved     = sum ApprovedAdHocTransactionsRel.ItemCharge
					ManualStatusChanges             = false
		
		ProcurementCardInvoiceAndRelease is an Instance Action
			valid when (AvailableForInvoicing)
			Parameters
				PrmProcurementGroup          is a ProcurementGroup   
					default label is "ProcurementGroup"
				PrmProgram                   is a ProcurementCardProgram
					default label is "Program"
				PrmStatement                 is a ProcurementCardStatement
					default label is "Statement"
				PrmInvoice                   is like Invoice
					default label is "Invoice"
				SendEmail                    is Boolean
					default label is "SendEmailToProcurementCardUser"
				
        
        	Parameter Rules
        	
        		PrmProcurementGroup
        			initial value is ProcurementGroup
        			default to ProcurementGroup
        			required
        			
        		PrmProgram
        			initial value is ProcurementCardProgram
        			default to ProcurementCardProgram
        			required
        			
        			for each ProcurementCardProgramCompaniesRel
						constraint (each.HasDefaultProcessLevel)
							"ADefaultProcessLevelCouldNotBeFoundForAllCompanies"
							


						
					constraint (VendorActive)
						"ProgramPayVendor<PrmProgram.Vendor>IsNotActive"
						      
        		PrmStatement
        			initial value is ProcurementCardStatement
        			default to ProcurementCardStatement
        			required
        			if (!HasAdhocTransactions)
        				confirmation required
        					"StatementHasNoAdhocTransactions;PurchaseOrderTransactionsWillBeReleased;SelectYesToContinue"
        				
        		PrmInvoice
        			LocalTestInvoice  = PrmInvoice
        			initial value is PrmStatement
        			default to PrmStatement
        			if (PrmInvoice entered)
        				constraint (!InvoiceAlreadyExists)
        					"Invoice<PrmInvoice>AlreadyExistsForVendor;MustEnterADifferentInvoiceNumber"

			Action Rules
				
				invoke InvoiceAndReleaseProcurementCardTransactions ProcurementCardStatementTransaction
					invoked.ProcurementGroup    = PrmProcurementGroup
					invoked.PrmProgram          = PrmProgram
					invoked.PrmVendor			= PrmProgram.Vendor
					invoked.PrmStatement        = PrmStatement
					invoked.PrmInvoice          = PrmInvoice
        			invoked.SendEmail           = SendEmail
  		

		FrontEndSplitAllChargesDistributions	is an Instance Action
			valid when(AllowFrontEndSplit)
			Action Rules
				invoke SplitAllChargesDistributions ProcurementCardStatementTransaction in foreground
					invoked.PrmProcurementGroup			= ProcurementGroup
					invoked.PrmProcurementCardProgram	= ProcurementCardProgram
					invoked.PrmProcurementCardStatement	= ProcurementCardStatement

		DeleteAllChargesFrontEndSplitDistributions	is an Instance Action
			valid when(HasFrontEndSplitDistributions)
			Action Rules
				invoke DeleteFrontEndSplitAllChargesDistributions ProcurementCardStatementTransaction
					invoked.PrmProcurementGroup			= ProcurementGroup
					invoked.PrmProcurementCardProgram	= ProcurementCardProgram
					invoked.PrmProcurementCardStatement	= ProcurementCardStatement
