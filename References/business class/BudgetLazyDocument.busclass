BudgetLazyDocument is a BusinessClass
	owned by GeneralLedger
	prefix is BUDLD
   
    Ontology
    	symbolic key is BudgetLazyDocument

    Patterns

 		disable Auditing 
 		disable EffectiveDated

    Persistent Fields
		ReadyToRelease				is Boolean


		IsBatch						is Boolean
 		ActionCode					
		BudgetEditMode
		BudgetEditTotalsProcessing
		BudgetEditCallBackWrap
		BusinessClassName			is Alpha 100
		EditContext					is Alpha 256

		IsBudget					is Boolean		
		FinanceEnterpriseGroup		is like FinanceEnterpriseGroup
		Scenario					is like Scenario
		BudgetChangeOrder			is like BudgetChangeOrder
 
    Transient Fields
		
	Local Fields
		
	Derived Fields

	Conditions

	Sets

	Field Rules
					
	Relations
		NotReadyLazyInitsRel
			one-to-many relation to BudgetGroupLazyInit
			Field Mapping uses ByBudgetEditGroup
				related.BudgetEditGroup					= BudgetLazyDocument.DocumentID
			Instance Selection
				where (!related.BudgetGroup.LazyInitialized)
				
		AllLazyInitsRel
			one-to-many relation to BudgetGroupLazyInit
			Field Mapping uses ByBudgetEditGroup
				related.BudgetEditGroup					= BudgetLazyDocument.DocumentID

		BudgetChangeOrderRel
			one-to-one relation to BudgetChangeOrder
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.Scenario						= Scenario
				related.BudgetChangeOrder				= BudgetChangeOrder
	Attach Rules
		
	Rule Blocks

    Actions
	
    	Create is a Create Action
    		restricted
			Action Rules


		Update is an Action
			restricted

		Delete is a Delete Action
			restricted
			Action Rules


		Purge is a Delete Action
			restricted

		ReleaseRoute is an Instance Action
			restricted
			Action Rules
				if (!IsBudget)
					invoke Release
						invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
						invoked.PrmBusinessClassName			= BusinessClassName
						invoked.PrmBudgetEditGroup				= BudgetLazyDocument.DocumentID
						invoked.PrmIsBatch						= IsBatch	
						invoked.PrmActionCode					= ActionCode
						invoked.PrmMode							= BudgetEditMode
						invoked.PrmTotalsProcessing				= BudgetEditTotalsProcessing										
						invoked.PrmBudgetEditCallBack			= BudgetEditCallBackWrap.BudgetEditCallBack
						invoked.PrmEditContext					= EditContext
				else
				if (BudgetChangeOrderRel exists
				and BudgetChangeOrderRel.Status.EditInProgress)
					invoke ReleaseChangeOrder
						invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
						invoked.PrmScenario						= Scenario
						invoked.PrmBudgetChangeOrder			= BudgetChangeOrder					



		Release is an Instance Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmBudgetEditGroup			is UniqueID
					default label is "BudgetEditGroup"
				PrmMode						is a BudgetEditMode
					default label is "Mode"
				PrmIsBatch					is Boolean
					default label is "IsBatch"
				PrmTotalsProcessing			is a BudgetEditTotalsProcessing
					default label is "TotalsProcessing"
				PrmBudgetEditCallBack		is a BudgetEditCallBack
					default label is "CallBack"
				PrmActionCode				is an ActionCode
					default label is "ActionCode"
				PrmBusinessClassName			is Alpha 100
					default label is "BusinessClass"
				PrmEditContext				is Alpha 100
					default label is "EditContext"
			
			Action Rules




				if (NotReadyLazyInitsRel not exists)		

					invoke Delete
						resume on error
					invoke EditTotalsRoute BudgetTemplateGroupTotal
						invoked.PrmIsBatch					= PrmIsBatch
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmBudgetEditGroup			= BudgetLazyDocument.DocumentID
						invoked.PrmMode						= PrmMode
						invoked.PrmTotalsProcessing			= PrmTotalsProcessing
						invoked.PrmBudgetEditCallBack		= PrmBudgetEditCallBack
						invoked.PrmActionCode				= PrmActionCode
						invoked.PrmBusinessClassName		= PrmBusinessClassName
						invoked.PrmEditContext				= PrmEditContext
				else

					FinanceEnterpriseGroup						= PrmFinanceEnterpriseGroup
					ReadyToRelease								= true
					IsBatch										= PrmIsBatch
					BudgetEditCallBackWrap.FEG					= PrmFinanceEnterpriseGroup			
					BudgetEditMode								= PrmMode
					BudgetEditTotalsProcessing					= PrmTotalsProcessing
					BudgetEditCallBackWrap.BudgetEditCallBack	= PrmBudgetEditCallBack
					ActionCode									= PrmActionCode
					BusinessClassName							= PrmBusinessClassName
					EditContext									= PrmEditContext


		ReleaseChangeOrder is an Instance Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmBudgetEditGroup			is UniqueID
				PrmScenario					is a Scenario
				PrmBudgetChangeOrder		is a BudgetChangeOrder
			Action Rules
				if (NotReadyLazyInitsRel not exists)
					invoke LazyRelease PrmBudgetChangeOrder
					invoke Delete
				else
					ReadyToRelease							= true
					FinanceEnterpriseGroup					= PrmFinanceEnterpriseGroup
					Scenario								= PrmScenario
					BudgetChangeOrder						= PrmBudgetChangeOrder
							
