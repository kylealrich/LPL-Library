ProjectFundingSourceTemplate is a BusinessClass
    owned by Projects
    prefix is PRFST

    Ontology
        symbolic key is ProjectFundingSourceTemplate
	Patterns

    Persistent Fields
        Description  
		Type					    is a ProjectFundingType
		Department					is a FinanceDimension2Department
		Division					is a FinanceDimension2Division						
    	Priority					is Numeric size 2
    	FundingGroup				is Numeric size 2
    	BillingGroup				is a ProjectBillingGroup
    	Percent						is Percent size 8.5
    	Amount						is a CurrencyAmount
    		default label is "FundedAmount"
		RevenueAccountOnly			is a GeneralLedgerChartAccount	
		RevenueDimension			is a ProjectCodeBlock			
		RevenueCodeBlockOption		is Numeric size 1
			default label is "RevenueStructureOption"
			States
				FullStructure		value is 1
				PartialStructure	value is 2
				None				value is 3
		RevenueFullAccount			is a FinanceCodeBlockFullNoProjectFD2
			default label is "FullRevenueFinanceStructure"
		RevenuePartialAccount		is a FinanceCodeBlockNoProjectFD2
			default label is "PartialRevenueFinanceStructure"
		RevenueAdjCodeBlockOption	is a RevenueCodeBlockOption
			default label is "RevenueAdjustmentStructureOption"
		RevenueAdjFullAccount		is a FinanceCodeBlockFullNoProjectFD2
			default label is "FullRevenueAdjustmentFinanceStructure"
		RevenueAdjPartialAccount	is a FinanceCodeBlockNoProjectFD2
			default label is "PartialRevenueAdjustmentFinanceStructure"
		RevenueClearingAccount		is a FinanceCodeBlockFullNoProjectFD2BalanceSheet
			default label is "RevenueClearingFinanceStructure"
		OffsetAccount				is a FinanceCodeBlockFullOffset		
			default label is "OffsetFinanceStructure"
		BilledUnearnedAccount		is a FinanceCodeBlockFullNoProjectFD2BalanceSheet
			default label is "BilledUnearnedFinanceStructure"
		EarnedUnbilledAccount		is a FinanceCodeBlockFullNoProjectFD2BalanceSheet
			default label is "EarnedUnbilledFinanceStructure"
		MatchFundingSource			is a FinanceDimension2			
		ExpenseGroup				is a GLTransactionDetail group
			default label is "OverrideExpenseGroup"
		ExpenseGroupOption			is Numeric size 1
			States
				Include				value is 1
				Exclude				value is 2
		ProjectJurisdictionCode
#ifndef property federal		
		RetainagePercent			is Percent size 7.3
		RetainageMaximum			is a CurrencyAmount
		RetainageExpenseGroup		is a GLTransactionDetail group
		RetainageGroupOption		is Numeric size 1
			States
				Include				value is 1
				Exclude				value is 2
		RetainageCodeBlock			is a FinanceCodeBlockFullNoProjectFD2BalanceSheet
			default label is "RetainageFinanceStructure"
		AllowancePercent			is Percent size 7.3
		AllowanceFullAccount		is a FinanceCodeBlockFullNoProjectFD2
			default label is "AllowanceFinanceStructure"
		AllowanceTaxable			is Boolean
		InvoiceFinanceCodeBlock		is a FinanceCodeBlockFull
			default label is "ReceivableFinanceStructure"
#endif
		AdvanceBillFullAccount		is a FinanceCodeBlockFullNoProjectFD2Liability
			default label is "AdvanceBillingFinanceStructure"
		PassThroughAccount			is a FinanceCodeBlockFullNoProjectFD2BalanceSheet
			default label is "PassThroughFinanceStructure"
		CustomerPoNumber 			is Alpha size 22
			default label is "CustomerP\ONumber"
#ifdef module intercobilling
		FromCompany					is a ReceivableCompany
			default label is "ReceivableCompany"
		IntercompanyBillingRechargeItem
#endif
#ifndef module intercobilling
		FromCompany						is Numeric size 4
		IntercompanyBillingRechargeItem	is AlphaUpper size 30
#endif
#ifdef module ap
		FESEligibilityGroup			is a FrontEndSplitDistributions group
			default label is "OverrideFrontEndSplitEligibilityGroup"
   
        FESEligibilityGroupOption	is Numeric size 1
            States
                Include		value is 1
                Exclude		value is 2
#endif                
		InvoiceMinimum				is a CurrencyAmount

	Local Fields
	
		LocalProjectFundingSourceTemplate is a ProjectFundingSourceTemplate	

	Transient Fields
    Derived Fields
    
	Conditions
	
		IsExternalGrantOrCustomer
			restricted	
			when (Type.ExternalGrant
			or    Type.ExternalCustomer)						
#ifndef property federal			
		AllowanceProjectRequired
			restricted
			when (AllowancePercent entered)
			
		RetainageProjectRequired
			restricted
			when (RetainagePercent entered)
#endif
		FrontEndSplitsEnabled
			restricted
			when(FinanceEnterpriseGroup.FrontEndSplits)

    Relations
    
		NewProjectFundingSourceTemplateRel
			one-to-one relation to ProjectFundingSourceTemplate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	     = FinanceEnterpriseGroup
				related.ProjectFundingSourceTemplate = LocalProjectFundingSourceTemplate

	Sets
				
    Field Rules
    		
        Description  
    		required
    		
		Type
			required

		RevenueCodeBlockOption
			if (RevenueCodeBlockOption.None)
				constraint (Type.InternalFund)
					"RevenueStructureOptionOf_NoneValidForInternalFundingTypeOnly"
								    		
		RevenuePartialAccount
			if (RevenuePartialAccount entered)
				constraint (RevenueCodeBlockOption.PartialStructure)
					"CannotEnterPartialRevenueFinanceStructureUnlessRevenueStructureOptionIs_Partial"

		RevenueFullAccount
			if (RevenueFullAccount entered)
				constraint (RevenueCodeBlockOption.FullStructure)
					"CannotEnterFullRevenueFinanceStructureUnlessRevenueStructureOptionIs_Full"

		RevenueAdjPartialAccount
			if (RevenueAdjPartialAccount entered)
				constraint (RevenueAdjCodeBlockOption.PartialStructure)
					"CannotEnterPartialRevenueAdjustmentFinanceStructureUnlessRevenueAdjustmentStructureOptionIs_Partial"

		RevenueAdjFullAccount
			if (RevenueAdjFullAccount entered)
				constraint (RevenueAdjCodeBlockOption.FullStructure)
					"CannotEnterFullRevenueAdjustmentFinanceStructureUnlessRevenueAdjustmentStructureOptionIs_Full"

		RevenueClearingAccount
			if (!Type.InternalFund)		
				cannot be entered
					"RevenueClearingFinanceStructureInvalidForExternalFundingSource"

		BilledUnearnedAccount
			constraint (BilledUnearnedAccount.GeneralLedgerChartAccount.ChartSection.BalanceSheet)
				"AccountChartSectionMustBeBalanceSheet"
			if (Type.InternalFund)
				cannot be entered
					"BilledUnearnedFinanceStructureInvalidForInternalFundingSource"

		EarnedUnbilledAccount
			constraint (EarnedUnbilledAccount.GeneralLedgerChartAccount.ChartSection.BalanceSheet)
				"AccountChartSectionMustBeBalanceSheet"
			if (Type.InternalFund)			
				cannot be entered
					"EarnedUnbilledFinanceStructureInvalidForInternalFundingSource"
#ifndef property federal
#ifdef module ar 
		InvoiceFinanceCodeBlock
			if (Type.InternalFund)
				cannot be entered
					"ReceivableFinanceStructureInvalidForInternalFundingSource"
#endif
#endif
		OffsetAccount
			if (!Type.InternalFund
			and !Type.IntercompanyBilling)
				cannot be entered
					"OffsetAccountIsOnlyValidWhenFundTypeIsInternalFundingOrIntercompanyBilling"		
				
		ExpenseGroupOption
			if (ExpenseGroup entered)
				required
					"ExpenseGroupOptionRequiredWhenOverrideExpenseGroupEntered"				
			else
				cannot be entered
					"ExpenseGroupOptionOnlyValidWhenOverrideExpenseGroupEntered"				

		ProjectJurisdictionCode	
			constraint (IsExternalGrantOrCustomer)
				"JurisdictionCodeIsOnlyValidForExternalGrantOrExternalCustomer"
#ifndef property federal
		RetainagePercent
			constraint (IsExternalGrantOrCustomer)
				"RetainageValidForExternalGrantsAndCustomersOnly"
			
		RetainageMaximum
			constraint (RetainagePercent entered)
				"RetainageMaximumOnlyValidWhenRetainagePercentEntered"

		RetainageExpenseGroup
			constraint (RetainagePercent entered)
				"RetainageExpenseGroupOnlyValidWhenRetainagePercentEntered"
				
		RetainageGroupOption
			if (RetainageExpenseGroup entered)
				required
					"RetainageGroupOptionRequiredWhenRetainageExpenseGroupEntered"				
			else
				cannot be entered
					"RetainageGroupOptionOnlyValidWhenRetainageExpenseGroupEntered"

		RetainageCodeBlock
			if (RetainagePercent entered)
				required
					"RetainageFinanceStructureRequiredWhenRetainageProjectEntered"
			else
				cannot be entered
					"RetainageFinanceStructureOnlyValidWhenRetainageProjectEntered"

		AllowancePercent
			constraint (IsExternalGrantOrCustomer)
				"AllowanceIsOnlyValidForExternalGrantOrExternalCustomer"
				
		AllowanceFullAccount
			if (AllowancePercent entered)
				required
					"AllowanceFinanceStructureRequired"
			else
				cannot be entered
					"AllowanceFinanceStructureCannotBeEnteredWithoutAllowancePercent"
#endif
#ifdef module intercobilling
		FromCompany
			if (Type.IntercompanyBilling)
				required
					"ReceivableCompanyRequiredForIntercompanyBilling"
			else
				cannot be entered
					"ReceivableCompanyValidOnlyWhenFundingTypeIsIntercompanyBilling"
			constraint (FromCompany.AnyIntercompanyBillingFieldEntered)
				"ReceivableCompanyMustBeSetUpForIntercompanyBilling"

		IntercompanyBillingRechargeItem
			if (Type.IntercompanyBilling)
				required
					"IntercompanyBillingRechargeItemRequiredForIntercompanyBilling"
			else
				cannot be entered
					"IntercompanyBillingRechargeItemValidOnlyWhenFundingTypeIsIntercompanyBilling"
#endif
#ifdef module ap
 		FESEligibilityGroupOption
            if (FESEligibilityGroup entered)
                required
                    "FESEligibilityGroupOptionRequiredWhenFESEligibilityGroupEntered"
            else
                cannot be entered
                    "FESEligibilityGroupOptionOnlyValidWhenFESEligibilityGroupEntered"
#endif
					
	Actions
		Create is a Create Action
			Action Rules
		
		Update is an Update Action
			Action Rules
															
		Delete is a Delete Action
			Action Rules

		CopyTemplate is an Instance Action
			Parameters
				NewProjectFundingSourceTemplate	is like ProjectFundingSourceTemplate
				NewDescription					is like Description
			Parameter Rules
				NewProjectFundingSourceTemplate
					required
					LocalProjectFundingSourceTemplate = NewProjectFundingSourceTemplate 
					constraint (NewProjectFundingSourceTemplateRel not exists)
						"ProjectFundingSourceTemplateAlreadyExists"
				NewDescription
					required
					initial value is Description + "_Copy"
			Action Rules
				invoke Create
					fill in fields from this instance
					invoked.ProjectFundingSourceTemplate = NewProjectFundingSourceTemplate
					invoked.Description			         = NewDescription
			Exit Rules

		SetRevenueFields is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup     is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where ((FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				or      PrmFinanceEnterpriseGroup not entered)
				and     RevenueCodeBlockOption not entered)
			Action Rules
				Instance Rules
					if (RevenueAccountOnly entered)
						RevenueCodeBlockOption = 2
						RevenuePartialAccount.GeneralLedgerChartAccount = RevenueAccountOnly
						RevenuePartialAccount.FinanceDimension1 = RevenueDimension.FinanceDimension1
						RevenuePartialAccount.FinanceDimension3 = RevenueDimension.FinanceDimension3
						RevenuePartialAccount.FinanceDimension4 = RevenueDimension.FinanceDimension4
						RevenuePartialAccount.FinanceDimension5 = RevenueDimension.FinanceDimension5
						RevenuePartialAccount.FinanceDimension6 = RevenueDimension.FinanceDimension6
						RevenuePartialAccount.FinanceDimension7 = RevenueDimension.FinanceDimension7
						RevenuePartialAccount.FinanceDimension8 = RevenueDimension.FinanceDimension8
						RevenuePartialAccount.FinanceDimension9 = RevenueDimension.FinanceDimension9
						RevenuePartialAccount.FinanceDimension10 = RevenueDimension.FinanceDimension10
					else
					if (RevenueFullAccount entered)
						RevenueCodeBlockOption = 1
