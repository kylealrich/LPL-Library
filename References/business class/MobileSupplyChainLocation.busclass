MobileSupplyChainLocation is a BusinessClass
	owned by mscm
	prefix is MSLOC

	Ontology
		symbolic key is MobileSupplyChainLocation
    
    Patterns

	Persistent Fields
        Name                  
		LocationType        					is AlphaUpper size 1
			protected
			States
				ParAndCartLocation        			value is "P"
                RequestingLocation  				value is "R"
                ReceivingAndDeliveryLocation        value is blank   
				InventoryLocation   				value is "I"
        Active              					is Boolean
        DeliveryTo								is Boolean
		ReceivingAndDeliveryTransfer			is Boolean
		ReceivingShipTo							is Boolean


		DisplayCarrier 							is a MobileSupplyChainInheritDisplayOptions
		DisplayCarrierTrackingNumber			is a MobileSupplyChainInheritDisplayOptions
		DisplayAddReceiptButton					is a MobileSupplyChainInheritEnableOptions
		DisplayTrackingNumber 					is a MobileSupplyChainInheritEnableOptions
		PrintPurchaseOrderDeliveryLabel			is a MobileSupplyChainInheritEnableOptions
		PrintPurchaseOrderDeliveryTicket		is a MobileSupplyChainInheritEnableOptions
		DisplayReferenceNumber					is a MobileSupplyChainInheritDisplayOptions
		DisplayBillOfLading						is a MobileSupplyChainInheritDisplayOptions
		CheckCatchWeightTolerance				is a MobileSupplyChainInheritEnableOptions


        DefaultLabelPrinter						is a MobileSupplyChainPrinter
		DefaultReportPrinter					is a MobileSupplyChainPrinter


		MaximumAdditionalQuantityMultiplier		is Unsigned Decimal 3.0
		PickForParEnabled 						is Boolean	


		PrintInventoryAdjustmentTicket			is a MobileSupplyChainInheritEnableOptions
		PrintInventoryIssueTicket				is a MobileSupplyChainInheritEnableOptions
		PrintInventoryTransferTicket			is a MobileSupplyChainInheritEnableOptions 
		PrintInventoryReceivingTransferTicket	is a MobileSupplyChainInheritEnableOptions


		ItemNumberValidation                    is a MobileSupplyChainInheritEnableOptions
		TrackDeliveryOption                  	is Alpha 1
			States
				InheritEnterpriseSetup  			value is blank
				Container                   		value is "C"
                Shipment                    		value is "S"
                ShipmentLine                		value is "L"
        PrintPickingOptions                     is AlphaUpper size 1
            States
				InheritEnterpriseSetup  			value is blank
                DoNotPrint                      	value is "N"
                PrintDeliveryLabelOnly          	value is "L"
                PrintDeliveryDocumentOnly       	value is "D"
                PrintDeliveryLabelAndDocument   	value is "B"              
        DownloadedShipmentOrder					is AlphaUpper size 1
            States
				InheritEnterpriseSetup  			value is blank
                FIFO                       			value is "F"
                LIFO                        		value is "L"
        PrintPickingClusterOptions              is AlphaUpper size 1
            States
				InheritEnterpriseSetup  			value is blank
                DoNotPrint                      	value is "N"
                PrintDeliveryLabelOnly          	value is "L"
                PrintDeliveryDocumentOnly       	value is "D"
                PrintDeliveryLabelAndDocument   	value is "B" 


		DisplayInventoryReplenishmentLocationQOO 			is a MobileSupplyChainInheritEnableOptions
		DisplayInventoryReplenishmentLocationMinimumOrder 	is a MobileSupplyChainInheritEnableOptions

    Context Fields
        ResourceContext 		is a MobileSupplyChainResource   
        PrinterContext			is a MobileSupplyChainPrinter 

	Transient Fields
		TransientUseKanban		is Numeric size 1
			States
				Yes			value is 1
				No			value is 2	
		TransientKanbanNumberOfBins	is like MobileSupplyChainQuantity
			derive value from DerivedDefaultKanbanNumberOfBins		

	Derived Fields
		DerivedCompanyAndLocation is a LabelField
			"<Company>:<MobileSupplyChainLocation>"

		DerivedRADLocationIDName	is a MessageField
			default label is "MobileSupplyChainLocation"
			"<MobileSupplyChainLocation>_-_<DerivedLocationName>"	

		DerivedLocationName is a DerivedField
			type is like Name
			if (LocationType.ReceivingAndDeliveryLocation)
				return Name
			else
			if (LocationType.ParAndCartLocation or LocationType.InventoryLocation)
				return InventoryLocationRel.Name
			else
			if (LocationType.RequestingLocation)
				return RequestingLocationRel.Name

        DerivedLabelLocationNameSize25 is a DerivedField
            type is Alpha size 25
            return DerivedLocationName

        DerivedLabelLocationSize15 is a DerivedField
            type is AlphaUpper size 15
            return MobileSupplyChainLocation

        DerivedLabelCompanyNameSize20 is a DerivedField
            type is AlphaUpper size 20
            return Company.Name

		ActualDisplayReferenceNumber is a DerivedField
			type is like MobileSupplyChainInheritDisplayOptions
			if (DisplayReferenceNumber.InheritEnterpriseSetup)
				return Company.ActualDisplayReferenceNumber
			
			return DisplayReferenceNumber

		ActualDisplayBillOfLading is a DerivedField
			type is like MobileSupplyChainInheritDisplayOptions
			if (DisplayBillOfLading.InheritEnterpriseSetup)
				return Company.ActualDisplayBillOfLading
			
			return DisplayBillOfLading
		
		ActualDisplayCarrier is a DerivedField
			type is like MobileSupplyChainInheritDisplayOptions
			if (DisplayCarrier.InheritEnterpriseSetup)
				return Company.ActualDisplayCarrier
			
			return DisplayCarrier
		
		ActualDisplayCarrierTrackingNumber is a DerivedField
			type is like MobileSupplyChainInheritDisplayOptions
			if (DisplayCarrierTrackingNumber.InheritEnterpriseSetup)
				return Company.ActualDisplayCarrierTrackingNumber
			
			return DisplayCarrierTrackingNumber
		
		ActualTrackDeliveryOption is a DerivedField
			type is AlphaUpper size 1
			if (TrackDeliveryOption.InheritEnterpriseSetup)
				return Company.ActualTrackDeliveryOption
			
			return TrackDeliveryOption

		ActualPrintPickingOptions is a DerivedField
			type is AlphaUpper size 1
			if (PrintPickingOptions.InheritEnterpriseSetup)
				return Company.ActualPrintPickingOptions
			
			return PrintPickingOptions
		
		ActualDownloadedShipmentOrder is a DerivedField
			type is AlphaUpper size 1
			if (DownloadedShipmentOrder.InheritEnterpriseSetup)
				return Company.ActualDownloadedShipmentOrder
			
			return DownloadedShipmentOrder
		
		ActualPrintPickingClusterOptions is a DerivedField
			type is AlphaUpper size 1
			if (PrintPickingClusterOptions.InheritEnterpriseSetup)
				return Company.ActualPrintPickingClusterOptions
			
			return PrintPickingClusterOptions
		
		ActiveStatus is a DerivedField
			type is Boolean
			if ((LocationType.ParAndCartLocation or LocationType.InventoryLocation) 
			and InventoryLocationRel exists 
			and InventoryLocationRel.Status.Active)
				return true
			else
			if (LocationType.RequestingLocation 
			and RequestingLocationRel exists
			and RequestingLocationRel.Active)
				return true
			else
			if (LocationType.ReceivingAndDeliveryLocation and Active)
				return true
			return false

		DerivedStatus is a DerivedField
			type is MessageField
			if (ActiveStatus)
				return "Active"
			else	
				return "Inactive"	        

		DerivedDefaultMobileSupplyChainReportPrinter is a DerivedField
			type is like MobileSupplyChainPrinter
			if (DerivedReportPrinterFromHierarchy entered)
				return DerivedReportPrinterFromHierarchy
			else   
				return first CompanyReportPrinterListRel.MobileSupplyChainPrinter

		DerivedDefaultMobileSupplyChainLabelPrinter is a DerivedField
			type is like MobileSupplyChainPrinter
			if (DerivedLabelPrinterFromHierarchy entered)
				return DerivedLabelPrinterFromHierarchy
			else
				return first CompanyLabelPrinterListRel.MobileSupplyChainPrinter

		DerivedReportPrinterFromHierarchy is a DerivedField
			type is like MobileSupplyChainPrinter
			if (DefaultResourceAccessPrinterRel.DefaultReportPrinter entered)
				return DefaultResourceAccessPrinterRel.DefaultReportPrinter
			else
			if (DefaultReportPrinter entered)	
				return DefaultReportPrinter
			else
			if (DefaultCompanyReportPrinterRel exists)
				return DefaultCompanyReportPrinterRel.MobileSupplyChainPrinter 

		DerivedLabelPrinterFromHierarchy is a DerivedField
			type is like MobileSupplyChainPrinter
			if (DefaultResourceAccessPrinterRel.DefaultLabelPrinter entered)
				return DefaultResourceAccessPrinterRel.DefaultLabelPrinter
			else
			if (DefaultLabelPrinter entered)	
				return DefaultLabelPrinter
			else
			if (DefaultCompanyLabelPrinterRel exists)
				return DefaultCompanyLabelPrinterRel.MobileSupplyChainPrinter
		
		DerivedLocationType is a StringField
			type is Alpha 50
			default label is "LocationType"
			"InventoryLocationOnly"

        DerivedDefaultLabelTemplate is a DerivedField
			type is like LabelTemplate
			if (not ParOrExchangeCart)
				return first DefaultStockLocationLabelTemplateRel.LabelTemplate
			else
				return first DefaultParLocationLabelTemplateRel.LabelTemplate    

		KanbanItemBinMessage is a MessageField
			"ThereAreChangesInThe_Item_BinConfiguration._SeeUpdatesInThe_Configure_Item_Bins."

		DerivedDefaultKanbanNumberOfBins is a DerivedField
			type is like MobileSupplyChainQuantity
			return 2	
		
		DerivedCompanyName is a DerivedField
			type is like Name
			return Company.Name
		
		DerivedAddressLine1 is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.DeliveryAddress.AddressLine1
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.DeliveryAddress.AddressLine1

        DerivedAddressLine2 is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.DeliveryAddress.AddressLine2
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.DeliveryAddress.AddressLine2

        DerivedAddressLine3 is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.DeliveryAddress.AddressLine3
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.DeliveryAddress.AddressLine3

        DerivedAddressLine4 is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.DeliveryAddress.AddressLine4
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.DeliveryAddress.AddressLine4

        DerivedMunicipality is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.Municipality
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.Municipality

        DerivedStateProvince is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.StateProvince
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.StateProvince

        DerivedCountry is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.Country
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.Country
            
        DerivedPostalCode is a DerivedField
            type is Text
            if (LocationType.RequestingLocation)
                return RequestingLocationRel.PostalAddress.PostalCode
            else
			if (LocationType.InventoryLocation or LocationType.ParAndCartLocation)
                return InventoryLocationRel.PostalAddress.PostalCode

	Relations
		InventoryLocationRel
            one-to-one relation to InventoryLocation
            Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation	= MobileSupplyChainLocation				

		RequestingLocationRel
            one-to-one relation to RequestingLocation
            Field Mapping uses symbolic key
				related.Company				= Company
				related.RequestingLocation	= MobileSupplyChainLocation

		DefaultResourceAccessPrinterRel
			one-to-one relation to MobileSupplyChainResourceAccessPrinter
			Field Mapping uses symbolic key
				related.Company					 			= Company
				related.MobileSupplyChainResource  			= actor.agent(Employee).Employee
				related.MobileSupplyChainLocation			= MobileSupplyChainLocation
		
		DefaultCompanyReportPrinterRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.Default
				and    related.MobileSupplyChainPrinter.PrinterType.Report)
		
		DefaultCompanyLabelPrinterRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.Default
				and    related.MobileSupplyChainPrinter.PrinterType.Label)

        MobileSupplyChainResourceRel
			one-to-one relation to MobileSupplyChainResource
			Field Mapping uses symbolic key
				related.HROrganization 					= actor.agent(Employee).HROrganization
				related.MobileSupplyChainResource   	= actor.agent(Employee).Employee		

        MobileSupplyChainResourceCompanyAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization					= actor.agent(Employee).HROrganization
    			related.MobileSupplyChainResource		= actor.agent(Employee).Employee
    			related.Company							= Company
    			related.MobileSupplyChainLocation	    = blank	

		MobileSupplyChainResourceAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization					= actor.agent(Employee).HROrganization
    			related.MobileSupplyChainResource		= actor.agent(Employee).Employee
    			related.Company							= Company
    			related.MobileSupplyChainLocation	    = MobileSupplyChainLocation		

		MobileSupplyChainResourceContextCompanyAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization					= ResourceContext.HROrganization
    			related.MobileSupplyChainResource		= ResourceContext
    			related.Company							= Company
    			related.MobileSupplyChainLocation	    = blank	

        MobileSupplyChainResourceContextAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization					= ResourceContext.HROrganization
    			related.MobileSupplyChainResource		= ResourceContext
    			related.Company							= Company
    			related.MobileSupplyChainLocation	    = MobileSupplyChainLocation

        MobileSupplyChainConfigurationRel
            one-to-one relation to MobileSupplyChainConfiguration
            Field Mapping uses symbolic key
                related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup 

        DefaultStockLocationLabelTemplateRel
			one-to-many relation to LabelTemplate
			Field Mapping uses symbolic key
				related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup
			Instance Selection	
				where (related.Default
				and    related.LabelType.InventoryLocation)
				
		DefaultParLocationLabelTemplateRel
			one-to-many relation to LabelTemplate
			Field Mapping uses symbolic key
				related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup
			Instance Selection
				where (related.Default
				and    related.LabelType.ParLocation)  

		MobileSupplyChainPrinterRel
			one-to-many relation to MobileSupplyChainPrinter
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = Company.FinanceEnterpriseGroup
			Instance Selection
				where (related.HasValidCompanyPrinter)	

		ItemLocationNotesRel
			one-to-many relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company = Company
				related.InventoryLocation = MobileSupplyChainLocation
			Instance Selection
				where	(related.KanbanItemNotes entered
				and		related.Company 			= Company
				and 	related.InventoryLocation 	= MobileSupplyChainLocation)
		
		DockLogRel
			one-to-many relation to DockLog
			Field Mapping uses symbolic key
				related.Company								= Company
				related.MobileSupplyChainLocation			= MobileSupplyChainLocation
		
		MiscellaneousReceiptRel
			one-to-many relation to MiscellaneousReceipt
			Field Mapping uses symbolic key
			Instance Selection
				where (	related.DeliverToCompanyLocation.DeliverToCompany	= Company
				and		related.DeliverToCompanyLocation.DeliverToLocation	= MobileSupplyChainLocation)

		ItemLocationRel
			one-to-many relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company           	= Company
				related.InventoryLocation 	= MobileSupplyChainLocation

		KanbanItemRel
			one-to-many relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company			  = Company
				related.InventoryLocation = MobileSupplyChainLocation
			Instance Selection
				where (related.UseKanban)	

		CompanyReportPrinterListRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.MobileSupplyChainPrinter.PrinterType.Report)
		
		CompanyLabelPrinterListRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				where (related.MobileSupplyChainPrinter.PrinterType.Label)
                       
	Sets
		ByDefaultReportPrinter
			Sort Order
				DefaultReportPrinter
				Company
				MobileSupplyChainLocation

		ByDefaultLabelPrinter
			Sort Order
				DefaultLabelPrinter
				Company
				MobileSupplyChainLocation

	Field Rules
		DeliveryTo
			if (DefaultDeliverTo)
				force default to true
		MaximumAdditionalQuantityMultiplier
            if (IsParOrExchangeCartExist)
                default to 1
	
	Conditions
		DefaultDeliverTo
		 	when (LocationType.ParAndCartLocation 
			or	  LocationType.RequestingLocation
			or	 (LocationType.InventoryLocation and RequestingLocationRel exists))

		DeliverToDisabled
			when (not LocationType.ReceivingAndDeliveryLocation)

		IsRequestingLocation
			when (LocationType.RequestingLocation)

		ActualDisplayAddReceiptButton
			when ((DisplayAddReceiptButton.InheritEnterpriseSetup and Company.ActualDisplayAddReceiptButton) 
			or     DisplayAddReceiptButton.Enable)
		
		ActualDisplayTrackingNumber
			when ((DisplayTrackingNumber.InheritEnterpriseSetup and Company.ActualDisplayTrackingNumber) 
			or     DisplayTrackingNumber.Enable)

		ActualPrintPurchaseOrderReceivingTicket
			when ((PrintPurchaseOrderDeliveryTicket.InheritEnterpriseSetup and Company.ActualPrintPurchaseOrderDeliveryTicket) 
			or     PrintPurchaseOrderDeliveryTicket.Enable)

		ActualPrintPurchaseOrderReceivingLabel
			when ((PrintPurchaseOrderDeliveryLabel.InheritEnterpriseSetup and Company.ActualPrintPurchaseOrderDeliveryLabel) 
			or     PrintPurchaseOrderDeliveryLabel.Enable)

		ActualPrintInventoryIssueTicket
			when ((PrintInventoryIssueTicket.InheritEnterpriseSetup and Company.ActualPrintInventoryIssueTicket) 
			or     PrintInventoryIssueTicket.Enable)

		ActualPrintInventoryTransferTicket
			when ((PrintInventoryTransferTicket.InheritEnterpriseSetup and Company.ActualPrintInventoryTransferTicket) 
			or     PrintInventoryTransferTicket.Enable)
		
		ActualPrintInventoryAdjustmentTicket
			when ((PrintInventoryAdjustmentTicket.InheritEnterpriseSetup and Company.ActualPrintInventoryAdjustmentTicket) 
			or     PrintInventoryAdjustmentTicket.Enable)	

		ActualPrintInventoryReceivingTransferTicket
			when ((PrintInventoryReceivingTransferTicket.InheritEnterpriseSetup and Company.ActualPrintInventoryReceivingTransferTicket)
			or	   PrintInventoryReceivingTransferTicket.Enable)
		
		ActualItemNumberValidation
			when ((ItemNumberValidation.InheritEnterpriseSetup and Company.ActualItemNumberValidation) 
			or     ItemNumberValidation.Enable)	
		
		ActualPrintPurchaseOrderDeliveryLabel
			when (Company.ActualPrintPurchaseOrderDeliveryLabel)	
		
		ActualPrintPurchaseOrderDeliveryTicket
			when (Company.ActualPrintPurchaseOrderDeliveryTicket)
		
		ActualInventoryReplenishmentLocationDisplayQOO
			when ((DisplayInventoryReplenishmentLocationQOO.InheritEnterpriseSetup and Company.ActualInventoryReplenishmentCompanyDisplayQOO) 
			or     DisplayInventoryReplenishmentLocationQOO.Enable)	

		ActualInventoryReplenishmentLocationDisplayMinimumOrder
			when ((DisplayInventoryReplenishmentLocationMinimumOrder.InheritEnterpriseSetup and Company.ActualInventoryReplenishmentCompanyDisplayMinimumOrder) 
			or     DisplayInventoryReplenishmentLocationMinimumOrder.Enable)	

		ActualCheckCatchWeightTolerance
			when ((CheckCatchWeightTolerance.InheritEnterpriseSetup and Company.ActualCheckCatchWeightTolerance) 
			or     CheckCatchWeightTolerance.Enable)	

		IsInventoryLocationOnly
			restricted
			when (LocationType.InventoryLocation and not RequestingLocationRel exists)	
        
        IsAccessibleByMobileSupplyChainResource
			restricted
			when (MobileSupplyChainResourceRel.GlobalLocationAccess or MobileSupplyChainResourceCompanyAccessRel exists or MobileSupplyChainResourceAccessRel exists)	

        IsAccessibleByMobileSupplyChainResourceContext
			restricted
        	when (ResourceContext exists
        	and  (ResourceContext.GlobalLocationAccess or MobileSupplyChainResourceContextCompanyAccessRel exists or MobileSupplyChainResourceContextAccessRel exists))		    

        ParOrExchangeCart
			restricted
			when (LocationType.ParAndCartLocation)

		IsInventoryLocation
			restricted
			when (LocationType.InventoryLocation)		
		
		MobileSupplyChainEnabled
			restricted
			when (MobileSupplyChainConfigurationRel.MobileSupplyChainEnabled
			and   ActiveStatus)

		IsActive
			restricted
			when (ActiveStatus)	  

		IsParOrExchangeCartExist
            restricted
            when (ParOrExchangeCart
            and MobileSupplyChainLocation exists)	

		ShowMobileSupplyChainPrinterPanel
        	restricted
        	when (MobileSupplyChainLocation exists)	     

		HasKanbanItemBinNotes
			restricted
			when (ItemLocationNotesRel exists)	

		ItemLocationRelExists
			restricted
			when (ItemLocationRel exists)

		IsAccessibleParLocation
			restricted
			when (IsAccessibleByMobileSupplyChainResource
			and	  ParOrExchangeCart
			and   ItemLocationRelExists)

		IsAccessibleStockLocation
			restricted
			when (IsAccessibleByMobileSupplyChainResource
			and	  LocationType.InventoryLocation
			and	  ItemLocationRelExists)	

		IsAccessibleReceivingShipToLocation
			restricted
			when (IsAccessibleByMobileSupplyChainResource
			and	  ReceivingShipTo
			and	  ItemLocationRelExists)

		IsAccessibleDeliveryToLocation
			restricted
			when (IsAccessibleByMobileSupplyChainResource
			and	  DeliveryTo 
			and   ReceivingAndDeliveryTransfer
			and	  ItemLocationRelExists)

		HasKanbanItem
			restricted
			when (KanbanItemRel exists)		

		IsDeliveryTo
			restricted
			when(DeliveryTo)	

		IsReceivingShipTo
			restricted
			when(ReceivingShipTo)			      

		IsAWarehouseLocation
			when(InventoryLocationRel.UseAsWarehouseLocation)			      

	Actions
        Create is a Create Action
			valid when (actor.context.FinanceEnterpriseGroup.ShowMobileSupplyChain)
			Entrance Rules
				if (LocationType.ReceivingAndDeliveryLocation)	
        			constraint (MobileSupplyChainConfigurationRel exists)
            			"LocationCannotBeCreated.MobileSupplyChainIsNotExisting"

        Update is an Update Action
			valid when (actor.context.FinanceEnterpriseGroup.ShowMobileSupplyChain)
			Exit Rules
				if (MobileSupplyChainConfigurationRel exists)
					if (not TransientUseKanban.Yes)
						TransientKanbanNumberOfBins = 0

					if (TransientKanbanNumberOfBins < 2)
						constraint (TransientKanbanNumberOfBins >= 2)
							"NumberOf_BinsMustBeGreaterThanOrEqualTo2"

					if (TransientUseKanban entered)
						invoke MobileSupplyChainConfigureKanbanLocationItems ItemLocation
							confirmation required
								"UpdatingTheCurrentConfigurationWillReflectToExistingAndIncomingItemsInTheLocation\._Continue\?"	
							invoked.PrmInventoryCompany		= Company
							invoked.PrmInventoryLocation 	= MobileSupplyChainLocation
							if (TransientUseKanban.Yes)
								invoked.PrmUseKanban		= true
							else 
							if (TransientUseKanban.No)
								invoked.PrmUseKanban		= false
							invoked.PrmKanbanNumberOfBins	= TransientKanbanNumberOfBins
		
		Delete is a Delete Action

        AttachPrinter is an Instance Action
			valid when (MobileSupplyChainEnabled)
			Action Rules
				if (PrinterContext.PrinterType.Report)
					DefaultReportPrinter = PrinterContext
				else
					DefaultLabelPrinter = PrinterContext
		
		UnassignLocation is an Instance Action
			valid when (MobileSupplyChainEnabled)
			confirmation required
				"ThisWillUnassignLocationToCurrentPrinter"
			Action Rules
				if (PrinterContext.PrinterType.Report)
					initialize DefaultReportPrinter
				else
					initialize DefaultLabelPrinter						
		
		PrintLabel is an Instance Action
			valid when (MobileSupplyChainEnabled)
			Parameters
				PrmMobileSupplyChainPrinter				is a MobileSupplyChainPrinter
					default label is "MobileSupplyChainPrinter"
				PrmNumberOfCopies						is Unsigned Decimal 2.0
					default label is "NumberOfCopies"
				PrmMobileSupplyChainConfiguration		is a MobileSupplyChainConfiguration
					default label is "MobileSupplyChainConfiguration"
				PrmLabelTemplate  						is a LabelTemplate
					default label is "LabelTemplate"
				PrmTransactionType						is a MobileSupplyChainPrintingTransactionType
					default label is "LabelTransactionType"
			Parameter Rules
				PrmMobileSupplyChainPrinter
					initial value is DerivedDefaultMobileSupplyChainLabelPrinter
					default to DerivedDefaultMobileSupplyChainLabelPrinter					
					required
					constraint (PrmMobileSupplyChainPrinter.PrinterType.Label)
						"PrinterShouldOnlyBeLabelPrinter"
				PrmNumberOfCopies
					initial value is 1
					required
				PrmLabelTemplate
					initial value is DerivedDefaultLabelTemplate
					default to DerivedDefaultLabelTemplate
					required
					if (not ParOrExchangeCart)
						constraint (PrmLabelTemplate.LabelType.InventoryLocation)
							"LabelTypeShouldBeStockItem"
					else
						constraint (PrmLabelTemplate.LabelType.ParLocation)
							"LabelTypeShouldBeParItem"
				PrmTransactionType
					initial value is PrmTransactionType.LabelManagement
					default to PrmTransactionType.LabelManagement
			Action Rules				
				invoke Create MobileSupplyChainPrinterJob
		        	invoked.FinanceEnterpriseGroup          = Company.FinanceEnterpriseGroup
		            invoked.Company 						= Company
		            invoked.MobileSupplyChainPrinter 		= PrmMobileSupplyChainPrinter
                    invoked.NumberOfCopies 					= PrmNumberOfCopies
					invoked.TransactionType					= PrmTransactionType
					invoked.PrintDataFile 					= PrmLabelTemplate.TransientInventoryLocationBinaryDocument document for this instance
		
		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where (Company.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup)
			Set Is
				PrmFinanceEnterpriseGroup
			Action Rules
				Instance Rules
					invoke Delete
					
		UpdateLocationType is an Instance Action
			restricted
			Action Rules
				if (InventoryLocationRel not exists and LocationType.ReceivingAndDeliveryLocation)
					LocationType	= 	MobileSupplyChainLocation.LocationType.RequestingLocation
		
		DeleteRequestingLocation is an Instance Action
			restricted
			Action Rules
				if(LocationType.RequestingLocation)
					if (DockLogRel exists or MiscellaneousReceiptRel exists)
						LocationType	= LocationType.ReceivingAndDeliveryLocation
						Active			= true
					else
						invoke Delete
