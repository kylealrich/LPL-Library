CashTransactionImportResult is a BusinessClass
	owned by cb
	
	prefix is CBTIR
	
	Ontology
		symbolic key is CashTransactionImportResult
		
	Patterns
 		disable Auditing 
 		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		RunGroup			is AlphaUpper 30
		RunTime				is TimeStamp
		RunType				is Numeric 1
			States
				Payments			value is 1
				Receipts			value is 2
				Transactions		value is 3
				ReturnedPayments	value is 4
		Status				is Numeric 1
			States
				Complete			value is 1
				Incomplete 			value is 2
		ErrorRunGroup		is AlphaUpper 30
		OriginalRunGroup	is AlphaUpper 30
		JournalizeGroup


        InterfacedPaymentAmount is an InternationalAmount
			disable Auditing 
		InterfacedPaymentCount is Numeric 7
			disable Auditing
			default label is "CashLedgerPaymentsInterfaced" 

        InterfacedReceiptAmount is an InternationalAmount
			disable Auditing 
		InterfacedReceiptCount is Numeric 7
			disable Auditing
			default label is "CashReceiptsInterfaced" 

        InterfacedElectronicFundsAmount is an InternationalAmount
			disable Auditing 
		InterfacedElectronicFundsCount is Numeric 7
			disable Auditing 
			default label is "ElectronicFundsInterfaced"

        InterfacedTransactionsAmount is an InternationalAmount
			disable Auditing 
		InterfacedTransactionsCount is Numeric 7
			disable Auditing
			default label is "CashLedgerTransactionsInterfaced" 			

        InterfacedReturnedPaymentAmount is an InternationalAmount
			disable Auditing 
		InterfacedReturnedPaymentCount is Numeric 7
			disable Auditing
			default label is "ReturnedPaymentsInterfaced" 

		
	Derived Fields 

	Field Rules
		OriginalRunGroup
			if (RunGroup like "*ERRORS_*")
				if (ErrorRunGroupRel exists)
					OriginalRunGroup = first ErrorRunGroupRel.OriginalRunGroup
				else
					OriginalRunGroup = RunGroup
			else
				OriginalRunGroup = RunGroup
	
	Conditions
		IncompleteErrorsNotMoved
			restricted
			when (Status.Incomplete
			and	  ErrorRunGroup not entered)

		IncompleteErrorsMoved
			restricted
			when (Status.Incomplete
			and	  ErrorRunGroup entered)

		PaymentsIncompleteErrorsNotMoved
			restricted
			when (RunType.Payments
			and   IncompleteErrorsNotMoved)

		PaymentsIncompleteErrorsMoved
			restricted
			when (RunType.Payments
			and   IncompleteErrorsMoved)

		ReceiptsIncompleteErrorsNotMoved
			restricted
			when (RunType.Receipts
			and   IncompleteErrorsNotMoved)

		ReceiptsIncompleteErrorsMoved
			restricted
			when (RunType.Receipts
			and   IncompleteErrorsMoved)

		TransactionsIncompleteErrorsNotMoved
			restricted
			when (RunType.Transactions
			and   IncompleteErrorsNotMoved)

		TransactionsIncompleteErrorsMoved
			restricted
			when (RunType.Transactions
			and   IncompleteErrorsMoved)

		ReturnedPaymentsIncompleteErrorsNotMoved
			restricted
			when (RunType.ReturnedPayments
			and   IncompleteErrorsNotMoved)

		ReturnedPaymentsIncompleteErrorsMoved
			restricted
			when (RunType.ReturnedPayments
			and   IncompleteErrorsMoved)

		IncompleteWithErrors
			restricted
			when ((PaymentsIncompleteErrorsNotMoved
			and    RunGroupCashLedgerPaymentsRel exists)
			or    (PaymentsIncompleteErrorsMoved
			and    ErrorRunGroupCashLedgerPaymentsRel exists)
			or    (ReceiptsIncompleteErrorsNotMoved
			and    RunGroupCashLedgerReceiptsRel exists)
			or    (ReceiptsIncompleteErrorsMoved
			and    ErrorRunGroupCashLedgerReceiptsRel exists)
			or    (TransactionsIncompleteErrorsNotMoved
			and    RunGroupCashLedgerTransactionsRel exists)
			or    (TransactionsIncompleteErrorsMoved
			and    ErrorRunGroupCashLedgerTransactionsRel exists)
			or    (ReturnedPaymentsIncompleteErrorsNotMoved
			and    RunGroupCashLedgerReturnedPaymentsRel exists)
			or    (ReturnedPaymentsIncompleteErrorsMoved
			and    ErrorRunGroupCashLedgerReturnedPaymentsRel exists))
			
		HasPaymentsCreated
			restricted
			when (RunType.Payments
			and   CashLedgerPaymentsRel exists)

		HasReceiptsCreated
			restricted
			when (RunType.Receipts
			and   CashLedgerCashReceiptsHeaderRel exists)

		HasUnrealeasedReceiptsCreated				
			restricted
			when (RunType.Receipts
			and   CashLedgerCashReceiptsUnreleasedImportRel exists)			

		HasElectronicFundsTransfersCreated
			restricted
			when (RunType.Receipts
			and   CashLedgerElectronicFundsTransferTransactionsRel exists)

		HasTransactionsCreated
			restricted
			when (RunType.Transactions
			and   CashLedgerTransactionsRel exists)
			
		HasTransactionsWithReleaseErrorsCreated
			restricted
			when (RunType.Transactions
			and   CashLedgerTransactionsUnreleasedImportRel exists)			

		HasReturnedPaymentsCreated
			restricted
			when (RunType.ReturnedPayments
			and   CashLedgerReturnedPaymentsRel exists)
	
	Relations
		RunGroupCashLedgerPaymentsRel
			one-to-many relation to CashLedgerPaymentImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerPaymentImport.RunGroup = RunGroup)
				
		ErrorRunGroupCashLedgerPaymentsRel
			one-to-many relation to CashLedgerPaymentImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerPaymentImport.RunGroup = ErrorRunGroup)	

		RunGroupCashLedgerReceiptsRel
			one-to-many relation to CashLedgerReceiptImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerReceiptImport.RunGroup = RunGroup)
				
		ErrorRunGroupCashLedgerReceiptsRel
			one-to-many relation to CashLedgerReceiptImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerReceiptImport.RunGroup = ErrorRunGroup)	
				
		RunGroupCashLedgerTransactionsRel
			one-to-many relation to CashLedgerTransactionImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerTransactionImport.RunGroup = RunGroup)
				
		ErrorRunGroupCashLedgerTransactionsRel
			one-to-many relation to CashLedgerTransactionImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerTransactionImport.RunGroup = ErrorRunGroup)	

		RunGroupCashLedgerReturnedPaymentsRel
			one-to-many relation to CashLedgerReturnedPaymentImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerReturnedPaymentImport.RunGroup = RunGroup)
				
		ErrorRunGroupCashLedgerReturnedPaymentsRel
			one-to-many relation to CashLedgerReturnedPaymentImport
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup
			Instance Selection
				where (related.CashLedgerReturnedPaymentImport.RunGroup = ErrorRunGroup)	
								
		CashLedgerPaymentsRel is a CashLedgerPayment set
			Instance Selection
				where (!related.ImportInProcess)

		CashLedgerCashReceiptsHeaderRel is a CashLedgerCashReceiptHeader set
			Instance Selection
				where (!related.ImportInProcess)

		CashLedgerCashReceiptsUnreleasedImportRel is a CashLedgerCashReceiptHeader set		
			Instance Selection
				where (!related.ImportInProcess
				and    related.Status.Unreleased)

		CashLedgerElectronicFundsTransferTransactionsRel is a CashLedgerElectronicFundsTransferTransaction set
			Instance Selection
				where (!related.ImportInProcess)
		
		CashLedgerTransactionsRel is a CashLedgerTransaction set
			Instance Selection
				where (!related.ImportInProcess)
				
		CashLedgerTransactionsUnreleasedImportRel is a CashLedgerTransaction set
			Instance Selection
				where (!related.ImportInProcess
				and    related.Status.Unreleased
				and    related.ImportStatus entered)				

		CashLedgerReturnedPaymentsRel is a CashLedgerReturnedPayment set
			Instance Selection
				where (!related.ImportInProcess)
								
		ErrorRunGroupRel
			one-to-many relation to CashTransactionImportResult
			Field Mapping uses ByErrorRunGroup
				related.CashManagementGroup	= CashManagementGroup
				related.ErrorRunGroup		= RunGroup

		OpenCashTransactionImportResultRel
			one-to-many relation to CashTransactionImportResult
			Field Mapping uses ByStatus
				related.CashManagementGroup	= CashManagementGroup
				related.Status				= 0
			Instance Selection
				where (!related.RunType.Payments
				and     related.CashTransactionImportResult	!= CashTransactionImportResult)
				
	Sets
		ByRunTime
			Sort Order
				CashManagementGroup
				RunTime descending
				CashTransactionImportResult
		ByRunGroup
			Sort Order
				CashManagementGroup
				RunGroup
				CashTransactionImportResult
		ByRunType
			Sort Order
				CashManagementGroup
				RunType
				CashTransactionImportResult
		ByOriginalRunGroup
			Sort Order
				CashManagementGroup
				OriginalRunGroup
				RunTime descending
				CashTransactionImportResult
		ByErrorRunGroup
			Sort Order
				CashManagementGroup
				ErrorRunGroup
				CashTransactionImportResult
		ByStatus
			Sort Order
				CashManagementGroup
				Status
				RunType
				CashTransactionImportResult

					
	Actions
		ResetAllErrors is an Instance Action		
			valid when (IncompleteWithErrors)
			confirmation required
			completion message is "ResetInitiated"
			Local Fields
				LocalRunGroup			is AlphaUpper 30

			Action Rules
				if (ErrorRunGroup entered)
					LocalRunGroup = ErrorRunGroup
				else
					LocalRunGroup = OriginalRunGroup

				if (RunType = "1")
					invoke Error.ResetErrorsByRunGroup CashLedgerPaymentImport
						invoked.PrmFinanceEnterpriseGroup = CashManagementGroup.FinanceEnterpriseGroup
						invoked.PrmRunGroup				  = LocalRunGroup

				if (RunType = "2")
					invoke Error.ResetErrorsByRunGroup CashLedgerReceiptImport
						invoked.PrmFinanceEnterpriseGroup = CashManagementGroup.FinanceEnterpriseGroup
						invoked.PrmRunGroup				  = LocalRunGroup

				if (RunType = "3")
					invoke Error.ResetErrorsByRunGroup CashLedgerTransactionImport
						invoked.PrmFinanceEnterpriseGroup = CashManagementGroup.FinanceEnterpriseGroup
						invoked.PrmRunGroup				  = LocalRunGroup

				if (RunType = "4")
					invoke Error.ResetErrorsByRunGroup CashLedgerReturnedPaymentImport
						invoked.PrmFinanceEnterpriseGroup = CashManagementGroup.FinanceEnterpriseGroup
						invoked.PrmRunGroup				  = LocalRunGroup

		DeleteAllTransactions is an Instance Action		
			valid when (IncompleteWithErrors)
			confirmation required
			completion message is "DeleteInitiated"
			Local Fields
				LocalRunGroup			is AlphaUpper 30

			Action Rules
				if (ErrorRunGroup entered)
					LocalRunGroup = ErrorRunGroup
				else
					LocalRunGroup = OriginalRunGroup

				if (RunType = "1")
					invoke DeleteAllTransactionsForRunGroup CashLedgerPaymentImport
						invoked.PrmCashManagementGroup	= CashManagementGroup
						invoked.PrmRunGroup				= LocalRunGroup

				if (RunType = "2")
					invoke DeleteAllTransactionsForRunGroup CashLedgerReceiptImport
						invoked.PrmCashManagementGroup	= CashManagementGroup
						invoked.PrmRunGroup				= LocalRunGroup

				if (RunType = "3")
					invoke DeleteAllTransactionsForRunGroup CashLedgerTransactionImport
						invoked.PrmCashManagementGroup	= CashManagementGroup
						invoked.PrmRunGroup				= LocalRunGroup

				if (RunType = "4")
					invoke DeleteAllTransactionsForRunGroup CashLedgerReturnedPaymentImport
						invoked.PrmCashManagementGroup	= CashManagementGroup
						invoked.PrmRunGroup				= LocalRunGroup

		Create is an Action
			restricted
		Update is an Action
			restricted
			Exit Rules
				if (!RunType.Payments
				and OpenCashTransactionImportResultRel	!exists)
					invoke UpdateCashCodeBalances CashCodeConsolidatedUpdate
						invoked.PrmCashManagementGroup	= CashManagementGroup

		Delete is an Action
			Action Rules
				confirmation required
				
		Purge is a Purge Action
			Action Rules
				confirmation required											
