DBSessionDebugUnit is a BusinessClass
    owned by la
    prefix is DBSDU
    stored in environment
	contains environment data // TODO - This likely can be removed since "disable data area copy" has been added, once that pattern is fully implemented.
    disable data area copy
    
    Ontology
    	symbolic key is DBSessionDebugUnit
    
    Patterns
    	implements CRUD
		disable Auditing
		disable EffectiveDated
	
	Persistent Fields
		Start				is TimeStamp
		End					is TimeStamp
		TotalTimeMs			is Numeric size 15
		TotalAPITimeMs		is Numeric size 15
		LogFileName			is Alpha size up to 250
		Log 				is Text
		
	Derived Fields
		DebugLog					is a DerivedField
			type is BinaryDocument  
			return Log
			
		MimeType					is a DerivedField
			type is MimeType
			default label is "MIME_Type"
			return "text/plain"
					
		DebugLogAttachmentName		is a ConditionalField
			type is Alpha size 20
			if (DBSessionDebugUnit < 10)
				"DebugLog000" + DBSessionDebugUnit + ".txt"
			else
			if (DBSessionDebugUnit < 100)
				"DebugLog00" + DBSessionDebugUnit + ".txt"
			else
			if (DBSessionDebugUnit < 1000)
				"DebugLog0" + DBSessionDebugUnit + ".txt"
			else
				"DebugLog" + DBSessionDebugUnit + ".txt"
				
		PercentAPITime				is a ComputeField
			type is Percent 4.1
			(TotalAPITimeMs / TotalTimeMs)
			
	Sets
		ByStartDescending
			Sort Order
				DBSessionDebug
				Start				descending
				DBSessionDebugUnit
		
		ByTotalTimeMsDescending
			Sort Order
				DBSessionDebug
				TotalTimeMs			descending
				DBSessionDebugUnit

		ByTotalAPITimeMsDescending
			Sort Order
				DBSessionDebug
				TotalAPITimeMs		descending
				DBSessionDebugUnit
				
	Conditions
		TempLogFileNotLoaded
			when (LogFileName != blank)

	Relations
				
	Actions
	






		Delete is a Delete Action

		Update is an Update Action
		
		Purge is a Purge Action
			restricted
		
		DeleteAllLogsForSession is a Set Action
			disable checkpoint
			
			Parameters
				PrmDBSessionDebug is UniqueID
				    default label is "DBSessionDebug"
				
			Parameter Rules
				PrmDBSessionDebug
					required
				
			Instance Selection
				where (DBSessionDebug = PrmDBSessionDebug)
				
			Action Rules
				Instance Rules
					invoke Purge
				
