SupplierPortalLinksAndInstructions is a BusinessClass
	owned by procurement
	prefix is Supp
	sql name is SupplierPortalLink
	
	Ontology
		symbolic key is SupplierPortalLinksAndInstructions
	
	Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder
	
	Persistent Fields
		Link 							is a URL
		Instructions 					is a Description3
		Attachment                      is an AttachmentWithTitleAudited
		DisplayOrder
		DisplayToAnonymousUsers			is Boolean
		Active							is Boolean
	
	Local Fields
		LocalActor              is an Actor
	
	Transient Fields
		NewDisplayOrder			is a DisplayOrder
	
	Derived Fields
	
		ActorIsAnonymousDerived is a DerivedField
			type is Alpha 10
			LocalActor = actor
			if (LocalActor.IsAnonymous)
				return "true"
			else
				return "false"	

	Conditions
		HasALinkOrAttachment
			restricted
			when (HasALink
			or    AttachmentExists)
		
		ActorIsAnonymous
			restricted
			when (ActorIsAnonymousDerived = "true")		
		
		HasALink
			restricted
			when (Link entered)
			
		HasInstructions
			restricted
			when (Instructions entered
			and   Link !entered
			and   Attachment !entered)
			
		AttachmentExists
			restricted
			when (Attachment entered)

		CanDisplay
			restricted
			when (((DisplayToAnonymousUsers
			and   ActorIsAnonymous)
			or   !ActorIsAnonymous)
			and   Active
			and   SupplierGroup = session.key.SupplierGroup)
			
	Field Rules
   			
		Active
			default to true
    					
	Sets
		ByDisplayOrder
			duplicates
			Sort Order
 				SupplierGroup
 				DisplayOrder
 				SupplierPortalLinksAndInstructions
 				
	Actions 
		Create is a Create Action  
			restricted
			Action Rules
				if (Link !entered)
					constraint (Instructions entered)
						"MustEnterEitherAURLOrInstructions"
			
			Field Rules
	 			DisplayOrder
	 				autosequence using SupplierGroup.LastQuickLinkDisplayOrder
	    
		CreateLink is a Create Action
		
			Action Rules
				constraint (Link entered)
					"MustEnterLinkInTheFormOfA_\URL"
				constraint (Instructions entered)
					"MustEnterADescription"

			Field Rules
	 			DisplayOrder
	 				autosequence using SupplierGroup.LastQuickLinkDisplayOrder
	 									
		CreateInstructions is a Create Action
		
			Action Rules
				constraint (Instructions entered) 
					"MustEnterAnInstruction"

			Field Rules
	 			DisplayOrder
	 				autosequence using SupplierGroup.LastQuickLinkDisplayOrder
	 								
		CreateAttachment is a Create Action
		
			Action Rules
				constraint (Attachment entered)
					"AttachmentIsRequired"
					
				constraint (Instructions entered)
					"MustEnterADescription"

			Field Rules
	 			DisplayOrder
	 				autosequence using SupplierGroup.LastQuickLinkDisplayOrder
	 						
		Update is an Update Action  
				
		UpdateLink is an Update Action

			Action Rules
				constraint (Link entered)
					"MustEnterALink"
				constraint (Instructions entered)
					"MustEnterADescription"
							
		UpdateInstructions is an Update Action

			Action Rules
				constraint (Instructions entered) 
					"MustEnterAnInstruction"
							
		UpdateAttachment is an Update Action
		
			Action Rules
				constraint (Attachment entered)
					"AttachmentIsRequired"
					
				constraint (Instructions entered)
					"MustEnterADescription"
					
		Delete is a Delete Action

		
	
