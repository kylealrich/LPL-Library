EntityLedgerPeriod is a BusinessClass
	owned by GeneralLedger
	prefix is GLELP

	Ontology
		symbolic key is EntityLedgerPeriod

    Patterns
		disable Auditing 
 		disable EffectiveDated
		disable AsOfDateProcessing
		implements StaticJava 
	Context Fields
		ReportingBasis
		AccountingEntityGroup

	Persistent Fields
		PeriodStatus			is Numeric 1
	    	States
				Open			value is 0
				Backpost		value is 1
				Limited			value is 2
				Final			value is 3
		RevaluationTimeStamp	is TimeStamp
		TranslationTimeStamp	is TimeStamp
	
	Transient Fields
		ChangePeriodStatus		is Alpha 8
			derive value from DerivedPeriodStatus		
		GainLossOption			is Numeric 1
			default label is "GainLossOption"
			States
				Revalue					value is 1		
				Translation				value is 2		

	Local Fields
		LocalYear					is Year
		LocalFiscalYear				is Numeric 4
		LocalPeriod					is a GeneralLedgerClosePeriod 
		LocalSystem					is like GeneralLedgerSystemCode
		LocalAccountingEntityGroup	is a AccountingEntityGroup
		LocalErrorMessage			is Alpha 150
		LocalActor					is an Actor
		LocalDetailErrorMessage		is Alpha 150

	Derived Fields
		DerivedPeriodEndDate	is a DerivedField
			type is Date
			restricted
			return GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date
		DerivedPeriodStatus		is a DerivedField
			type is Alpha 8

			if (PeriodStatus.Open)
				return "Open"
			else
			if (PeriodStatus.Backpost)
				return "BackPost"
			else
			if (PeriodStatus.Limited)
				return "Limited"
			else
			if (PeriodStatus.Final)
				return "Final"
        DerivedFiscalYearPeriod			is a StringField
            type is Alpha size 30
            restricted
			GeneralLedgerClosePeriod.GeneralLedgerCloseYear
			"-"
			GeneralLedgerClosePeriod.PeriodName
        DerivedNextYear					is a DerivedField
            type is Numeric 4
            restricted
			return first EntityLedgerNextPeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCloseYear
        DerivedNextPeriod				is a DerivedField
            type is like GeneralLedgerCalendarPeriod
            restricted
			return first EntityLedgerNextPeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
        DerivedPreviousYearPeriod		is a StringField
            type is Alpha size 30
            restricted
			last EntityLedgerPreviousRecordRel.GeneralLedgerClosePeriod.GeneralLedgerCloseYear
			"-"
			last EntityLedgerPreviousRecordRel.GeneralLedgerClosePeriod.PeriodName
		JournalMessage				is a MessageField
			restricted
			"UnpostedJournalExistsFor"
		LedgerMessage				is a MessageField
			restricted
			"Ledger"
		PeriodMessage				is a MessageField
			restricted
			"Period"
		UnpostedJournalsMessage		is a StringField
			type is Alpha 150
			restricted
			JournalMessage
			" "
			FinanceEnterpriseGroup.AccountingEntityLabel
			" "
			AccountingEntity
			" "
			LedgerMessage
			" "
			Ledger
			" "
			PeriodMessage
			" "
			GeneralLedgerClosePeriod.PeriodName
		DerivedDetailErrorMessage		is a StringField
			type is Alpha 150
			restricted
			FinanceEnterpriseGroup.AccountingEntityLabel
			" "
			AccountingEntity
			" "
			LedgerMessage
			" "
			Ledger
	Relations
		GeneralLegerJournalControlRel
			one-to-many relation to GeneralLedgerJournalControl
			Field Mapping uses ByEntityLedgerJournal
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.Ledger						= Ledger
				related.GeneralLedgerClosePeriod	= GeneralLedgerClosePeriod
		EntityLedgerNextPeriodRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses ByEntityLedger
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.Ledger						= Ledger
			Instance Selection
				where (related.GeneralLedgerClosePeriod	> AccountingEntity.CurrentPeriod)
		EntityLedgerPreviousRecordRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses ByEntityLedger
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= AccountingEntity
				related.Ledger						= Ledger
			Instance Selection
				where (related.GeneralLedgerClosePeriod	< GeneralLedgerClosePeriod)
		GeneralLedgerClosePeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration		= AccountingEntity.CloseConfiguration
				related.GeneralLedgerCloseYear				= LocalYear
		SystemClosingControlRel
			one-to-many relation to SystemClosingControl
			Field Mapping uses ByControl
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.AccountingEntity					= AccountingEntity
				related.Control								= true
			Instance Selection
				where (related.ClosedPeriod					!= AccountingEntity.CurrentPeriod)
		AccountingEntityGroupMemberRel
			one-to-many relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.AccountingEntityGroup  = LocalAccountingEntityGroup
				related.AccountingEntity	   = AccountingEntity
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup   = actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity        = AccountingEntity
				
		BODGeneralLedgerClosePeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration		= GeneralLedgerClosePeriod.GeneralLedgerCloseConfiguration
				related.GeneralLedgerCloseYear				= GeneralLedgerClosePeriod.GeneralLedgerCloseYear
				related.GeneralLedgerClosePeriod			= GeneralLedgerClosePeriod
				

	Conditions
		PeriodIsLimitedClosed	
			restricted
			when (PeriodStatus.Limited)
		OpenPeriod
			restricted
			when (PeriodStatus.Open
			or	  PeriodStatus.Backpost)
		OpenPeriodOnly
			restricted
			when (PeriodStatus.Open)
		BackPostPeriod
			restricted
			when (PeriodStatus.Backpost)
		UnpostedJournals
			when (any GeneralLegerJournalControlRel.Status.Released
			or    any GeneralLegerJournalControlRel.Status.Unreleased
			or    any GeneralLegerJournalControlRel.Status.PendingApproval)
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))

    Sets
    	ByEntityLedger
    		Sort Order
    			FinanceEnterpriseGroup
    			AccountingEntity
    			Ledger
    			GeneralLedgerClosePeriod
    	ByYearPeriodEntityLedger
    		Sort Order
    			FinanceEnterpriseGroup
    			GeneralLedgerClosePeriod
    			AccountingEntity
    			Ledger
    	ByLedgerEntity
    		Sort Order
    			FinanceEnterpriseGroup
    			Ledger
    			AccountingEntity
    			GeneralLedgerClosePeriod
    	ByYearPeriodDescending
    		Sort Order
    			FinanceEnterpriseGroup
    			GeneralLedgerClosePeriod descending
    			AccountingEntity
    			Ledger

	Attach Rules

	Field Rules

	Rule Blocks
		SendNotificationToActor
			send notification
				to LocalActor
				description is	"<LocalErrorMessage>"
				priority is very high
				detail is "<LocalDetailErrorMessage>"

	Actions

		Purge is a Purge Action
			restricted

		DeleteAllLedgerPeriods is a Delete Action
			restricted


		DeleteAllSetLedgerPeriod is a Set Action	
			restricted
			Parameters
				PrmFinanceEnterpriseGroup 	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmAccountingEntity			is a AccountingEntity
					default label is "AccountingEntity"
				PrmLedger					is a Ledger
					default label is "Ledger" 
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmAccountingEntity
					required
				PrmLedger
					required
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and    AccountingEntity				= PrmAccountingEntity
				and	   Ledger						= PrmLedger)
			Action Rules
				Instance Rules
					invoke DeleteAllLedgerPeriods EntityLedgerPeriod
			

		ProcessEntityLedgerPeriod is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmCloseConfiguration		is a GeneralLedgerCloseConfiguration
					default label is "CloseConfiguration"
				PrmAccountingEntityGroup	is a AccountingEntityGroup
					default label is "AccountingEntityGroup"
				PrmLedgerGroup				is a Ledger group
					default label is "LedgerGroup"
				PrmAccountingEntity			is a AccountingEntity
					default label is "AccountingEntity"
				PrmLedger					is a Ledger
					default label is "Ledger"
				PrmYear					  	is like GeneralLedgerCloseYear
					default label is "Year"
				PrmPeriod				  	is a GeneralLedgerClosePeriod
					context of PrmCloseConfiguration
					default label is "Period"
				PrmActionOption			  	is Numeric 1
					default label is "ActionOption"
			    	States
						LimitedClose		value is 0
						OpenForBackpost		value is 1
						FinalClose			value is 2
				PrmPeriodRange			  	is a ClosePeriodRange
				PrmSkipAttachRule			is a SkipAttachRule		

			Local Fields				
		  		LocalDateRange				is a DateRange
				LocalSetActor				is an Actor
				CurrentAsyncId				is a AsyncActionRequest


			Parameter Rules
				PrmFinanceEnterpriseGroup
					CurrentAsyncId = current async action request id
					LocalSetActor	= actor
					required
				PrmAccountingEntityGroup
					LocalAccountingEntityGroup		= PrmAccountingEntityGroup
				PrmPeriodRange
					LocalDateRange.Begin			= PrmPeriodRange.Begin.GeneralLedgerCalendarPeriod.Date
					LocalDateRange.End				= PrmPeriodRange.End.GeneralLedgerCalendarPeriod.Date

			Instance Selection
				where (FinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup
				and   ((PrmCloseConfiguration entered
				and    AccountingEntity.CloseConfiguration	= PrmCloseConfiguration)
				or    !PrmCloseConfiguration entered)
				and   ((PrmYear entered
				and    GeneralLedgerClosePeriod.GeneralLedgerCloseYear	= PrmYear)
				or    !PrmYear entered)
				and   ((PrmPeriod entered
				and    GeneralLedgerClosePeriod				= PrmPeriod)
				or    !PrmPeriod entered)
				and   ((PrmPeriodRange entered
				and    GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date	within LocalDateRange)
				or    !PrmPeriodRange entered)
				and   ((PrmLedger entered
				and    Ledger								= PrmLedger)
				or    !PrmLedger entered)
				and   ((PrmAccountingEntity entered
				and    AccountingEntity						= PrmAccountingEntity)
				or    !PrmAccountingEntity entered)
				and   ((PrmLedgerGroup entered
				and    Ledger								within PrmLedgerGroup)
				or    !PrmLedgerGroup entered)
				and   ((PrmAccountingEntityGroup entered
				and    AccountingEntityGroupMemberRel exists)
				or    !PrmAccountingEntityGroup entered)
				and	  ((PrmActionOption.LimitedClose
				and    (PeriodStatus.Open
				or      PeriodStatus.Backpost))
				or 	  (PrmActionOption.OpenForBackpost
				and    PeriodStatus.Limited)
				or	  (PrmActionOption.FinalClose
				and    PeriodStatus.Limited)))

			Sort Order
				FinanceEnterpriseGroup
				GeneralLedgerClosePeriod
				PeriodStatus
				Ledger
				AccountingEntity

			Action Rules
				Instance Rules
					LocalActor	= LocalSetActor
					if  (PrmActionOption.OpenForBackpost
					and  PeriodStatus.Limited)
						invoke OpenForBackPost Limited this instance
							resume on error
								LocalErrorMessage		= error message
								LocalDetailErrorMessage	= DerivedDetailErrorMessage + " " + LocalErrorMessage
								include SendNotificationToActor
					else
						if  (PrmActionOption.FinalClose
						and  PeriodStatus.Limited)
							invoke SetActionFinalClose Limited this instance
								resume on error
									LocalErrorMessage		= error message
									LocalDetailErrorMessage	= DerivedDetailErrorMessage + " " + LocalErrorMessage
									include SendNotificationToActor
						else
							if (PrmActionOption.LimitedClose)
								if (UnpostedJournals)
									LocalErrorMessage		= UnpostedJournalsMessage
									LocalDetailErrorMessage	= UnpostedJournalsMessage
									include SendNotificationToActor
								else
									if (PeriodStatus.Open)
										invoke LimitedClose Open this instance
											resume on error
												LocalErrorMessage		= error message
												LocalDetailErrorMessage	= DerivedDetailErrorMessage + " " + LocalErrorMessage
												include SendNotificationToActor
									else
										if (PeriodStatus.Backpost)
											invoke LimitedClose Backpost this instance
												resume on error
													LocalErrorMessage		= error message
													LocalDetailErrorMessage	= DerivedDetailErrorMessage + " " + LocalErrorMessage
													include SendNotificationToActor

		UpdateGainLossTimeStamp is an Update Action
			restricted
			bypass field rules
			Action Rules
				if (GainLossOption.Revalue)
					RevaluationTimeStamp		= (current timestamp + 1)
				else
					TranslationTimeStamp		= (current timestamp + 1)

		UpdatePeriodStatus is an Instance Action
			restricted
			Parameters
				PrmPeriodStatus		is Numeric 1
					default label is "PeriodStatus"
			Action Rules
				PeriodStatus	= PrmPeriodStatus

		T2VCreate is a Create Action
			restricted

	StateCycles
		EntityLedgerPeriodLifeCycle is a StateCycle
			state field is PeriodStatus

			Open is a State

				Create is a Create Action
					restricted

				LimitedClose is an Instance Action
					valid when (!UnpostedJournals)
					Entrance Rules
						if (EntityLedgerPreviousRecordRel exists)
							constraint (!last EntityLedgerPreviousRecordRel.PeriodStatus.Open)
								"CannotClose<DerivedPreviousYearPeriod>IsOpen"
						if (Ledger	= FinanceEnterpriseGroup.CoreLedger)
							if (AccountingEntity.FiscalYear		< DerivedNextYear)
								LocalYear		= AccountingEntity.FiscalYear + 2
								constraint (GeneralLedgerClosePeriodRel exists)
									"PeriodsAreNotDefinedInCloseConfigurationForYear<LocalYear>"
						if (Ledger	= FinanceEnterpriseGroup.CoreLedger)
							if (SystemClosingControlRel exists)
								if (GeneralLedgerClosePeriod	= AccountingEntity.CurrentPeriod)
									LocalSystem = first SystemClosingControlRel.GeneralLedgerSystemCode
									constraint (first SystemClosingControlRel.ClosedPeriod	= AccountingEntity.CurrentPeriod)
										"<LocalSystem>SystemIsNotClosed"
					Action Rules
						if (Ledger	= FinanceEnterpriseGroup.CoreLedger)
							invoke UpdateFiscalYearPeriod AccountingEntity
								invoked.PrmEnterpriseGroup		= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity		= AccountingEntity
								invoked.PrmFiscalYear			= DerivedNextYear
								invoked.PrmCurrentPeriod.GeneralLedgerCalendarPeriod	= DerivedNextPeriod
						make transition to Limited

			Limited is a State
				Create is a Create Action
					restricted

				OpenForBackPost is an Instance Action
					Action Rules
						make transition to Backpost

				FinalClose is an Instance Action
					confirmation required
						"FinalClosedPeriodsCannotBeReopened"
					Action Rules
						make transition to Final
							if (Ledger	= FinanceEnterpriseGroup.CoreLedger)
								invoke TriggerFinancialCalendarRules BODGeneralLedgerClosePeriodRel
									invoked.PrmAccountingEntity = AccountingEntity
										
										

				SetActionFinalClose is an Instance Action
					restricted
					Action Rules
						make transition to Final
							if (Ledger	= FinanceEnterpriseGroup.CoreLedger)
								invoke TriggerFinancialCalendarRules BODGeneralLedgerClosePeriodRel
									invoked.PrmAccountingEntity = AccountingEntity

			Backpost is a State
				LimitedClose is an Instance Action
					valid when (!UnpostedJournals)
					Entrance Rules
					Action Rules
						make transition to Limited

			Final is a State
				
