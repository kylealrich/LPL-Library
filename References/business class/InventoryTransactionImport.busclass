InventoryTransactionImport is a BusinessClass
    owned by ic
    prefix is ITH
    classic name is ICCTRNHDR

    Ontology
        symbolic key is InventoryTransactionImport

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
        RunGroup
        RecordInError           is Boolean
        ErrorMessage			is Alpha 150
        ResultProcessedBy       is a InventoryTransactionInterfaceResult
        InventoryTransactionNumber is like InventoryTransaction
        ErrorLevel              is Numeric 2
        	States
        		Header          value is 1
        		Line            value is 2
        		Detail          value is 3
        TransactionDate       is TimeStamp
            classic name is TRANS-DATE
        GeneralLedgerDate     is Date
            classic name is GL-DATE
            default label is "GlobalLedgerDate"
        EstimatedDeliveryDate is Date
            classic name is EST-DEL-DATE
        IntransitTransfer     is Boolean
            classic name is INTRANSIT-FL
        IntransitDocument     is an InventoryTransaction
            classic name is INTRAN-RECV
        Reprint               is Boolean
            classic name is REPRINT-FL
        NumberOfCartons
        FromToCompanyLocation
            classic name for FromToCompanyLocation.FromToCompany is FR-TO-CO
            classic name for FromToCompanyLocation.FromToLocation is FR-TO-LOC
        Status                   is Numeric size 1
			States
				Unreleased					value is 0
				Released					value is 1
    			GlobalLedgerUpdated			value is 2  
    			NonGlobalLedger				value is 3  
    	IntransitReceivingStatus		is Numeric 1
    		States
    			NoReceiving					value is 0
				ReadyToReceive				value is 1
				ReceivingInProcess			value is 2
				ReceivingCompleted			value is 3
        CurrencyTable
    
    Local Fields
    	LocalErrorMessage						is Alpha 150
		ErrorOccurred                   		is Boolean
		InterfacedInventoryTransactionNumber   	is an InventoryTransaction view
		LocalInterfaceResult            		is like InventoryTransactionInterfaceResult
		LocalRunGroup                   		is like RunGroup
    
    Derived Fields
    
    	NoImportLinesMessage is a MessageField
    		restricted
    		"CannotAddNewInventoryTransaction;MustHaveLineRecordsToProcessInventoryTransaction"
    		
    	MismatchedSystemCodeMessage is a MessageField
    		restricted
    		"AnySystemCodeOtherThanICMustBeInterfacedWithAReleasedStatus"
    		
    	UnreleasedWrongTypeMessage is a MessageField
    		restricted
    		"ThisInventoryDocumentTypeMustHaveAStatusOfReleased"
    		
    	OnlyBinTransferMessage is a MessageField
    		restricted
    		"OnlyBinTransfersCanHaveAStatusOfNonGlobalLedger"
   
   		NoIntransitTransferMessage is a MessageField
   			restricted
   			"CannotImportIntransitReceivingDocuments"
		
		
		RecordErrorTagField is a DerivedField
			type is MessageField
			default label is "Status"
			if (IsInError)
				return RecordInErrorMsg
			else
				return NoRecordInErrorMsg

		RecordInErrorMsg is a MessageField
			restricted
			"RecordInError"
		
		NoRecordInErrorMsg is a MessageField
			restricted
			"NoRecordInError"

    Conditions

        IsInError
            classic name is REC-ERR
            restricted
            when (RecordInError)
        
		ImportLinesExist
			restricted
			when (InventoryTransactionImportLinesRel exists)
		
		RecordExists
			restricted
			when (InventoryTransactionImport exists)        
			
		InventoryTransactionExists
			restricted
			when (InventoryTransactionNumber entered)
			
		UnreleasedTypeOK
			restricted
			when (InventoryDocumentType.Adjustment
			or    InventoryDocumentType.InventoryIssue
			or    InventoryDocumentType.InventoryReceipt
			or    InventoryDocumentType.BinTransfer
			or    InventoryDocumentType.InventoryTransfer
			or	  InventoryDocumentType.IntransitReceiving)

    Relations

        InventoryTransactionImportLinesRel
            classic name is ICCTRNLIN
            one-to-many relation to InventoryTransactionImportLine
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryDocumentType       = InventoryDocumentType
                related.InventoryTransactionImport 	= InventoryTransactionImport
                
        InventoryTransactionImportLineDetailsRel
            classic name is ICCTRNLIN
            one-to-many relation to InventoryTransactionImportLineDetail
            delete cascades
            Field Mapping uses symbolic key
                related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryDocumentType       = InventoryDocumentType
                related.InventoryTransactionImport 	= InventoryTransactionImport
        
        InventoryTransactionImportByRunGroupRel
        	one-to-many relation to InventoryTransactionImport
        	Field Mapping uses ByRunGroup
        		related.RunGroup                    = LocalRunGroup
        
        InterfacedInventoryTransactionRel
         	one-to-many relation to InventoryTransaction
         	Field Mapping uses symbolic key
         		related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryTransaction    	= InventoryTransactionNumber
                
        InterfacedInventoryTransactionLinesRel
        	one-to-many relation to InventoryTransactionLine
        	Field Mapping uses symbolic key
        		related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryTransaction    	= InventoryTransactionNumber
                
        ProcessedInventoryTransactionLinesRel
        	one-to-many relation to InventoryTransactionLine
        	Field Mapping uses symbolic key
        		related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryTransaction    	= InventoryTransactionNumber
        	Instance Selection
				where (related.Status.Released
				or     related.Status.GlobalLedgerUpdated
				or     related.Status.IntermediateStatus
				or     related.Status.NonGlobalLedger) 
         		
        LocalInterfaceResultsRel
			one-to-one relation to InventoryTransactionInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= actor.context.FinanceEnterpriseGroup
				related.InventoryTransactionInterfaceResult		= LocalInterfaceResult 

	Sets
	
		ByRunGroup
			Sort Order
				RunGroup
				Company
				InventoryLocation
				InventoryDocumentType
				InventoryTransactionImport
	
	Field Rules
		RunGroup
			required
		Company
			required
		InventoryLocation
			required
		InventoryDocumentType
			required
	
	Actions
		Create is a Create Action
			Action Rules
		
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules
		
		Delete is a Delete Action

			Entrance Rules
			
				if (InventoryTransactionNumber entered)
					if (ErrorMessage entered)


						constraint (ProcessedInventoryTransactionLinesRel not exists)
							"CannotDelete;AllTransactionLinesShouldBeUnreleased"
						invoke SystemDelete InterfacedInventoryTransactionLinesRel
						invoke SystemDelete InterfacedInventoryTransactionRel
					else
						invoke FinishInterfaceResult
							invoked.PrmResult = ResultProcessedBy
							
		MassDelete is a Set Action
			Parameters
				PrmRunGroup                 is a RunGroup
					default label is "RunGroup"

			Parameter Rules
				PrmRunGroup
					required
						"RunGroupIsRequired"
					
			Instance Selection
				where (RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				InventoryTransactionImport
				
			Action Rules
				
				Instance Rules
				
					invoke Delete
		
		Update is an Update Action
			valid when (!InventoryTransactionExists)
		
			Exit Rules
				if (RunGroup changed)
					for each InventoryTransactionImportLineDetailsRel
						invoke Update each
							invoked.RunGroup = RunGroup
		
		InterfaceInventoryTransactions is a Set Action
			Parameters
				PrmRunGroup							is a RunGroup
					default label is "RunGroup"
				ReleaseInventoryTransactions        is Boolean
				IncludeInventoryTransactionsInError is Boolean
				PrmMoveErrorsToNewRunGroup  		is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix				is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				
				PrmRunGroup
					LocalRunGroup     = PrmRunGroup
					required
						"RunGroupIsRequired"
				
				ReleaseInventoryTransactions
					initial value is true
				
				IncludeInventoryTransactionsInError
					if (PrmRunGroup like "*ERRORS_*")
						default to true	
			
			Local Fields
				LocalInterfaceResultView		is a InventoryTransactionInterfaceResult view				
			
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and    Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

			Sort Order
				RunGroup
				Company
				InventoryTransactionImport
				
			Action Rules
			
				Empty Set Rules
				
					invoke Create InventoryTransactionInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.FinanceEnterpriseGroup			= actor.context.FinanceEnterpriseGroup
						invoked.RunTime							= current timestamp
						invoked.RunGroup						= PrmRunGroup
						invoked.IncludeInventoryTransactionsInError = IncludeInventoryTransactionsInError
						invoked.ReleaseInventoryTransactions 	= ReleaseInventoryTransactions
						invoked.MoveErrorsToNewRunGroup     	= PrmMoveErrorsToNewRunGroup	
						invoked.ErrorRunGroupPrefix         	= PrmErrorRunGroupPrefix
						invoked.Status                          = 1 
						
				RunGroup Set Rules
					Entrance Rules
			
						ErrorOccurred							= false
						
						invoke Create InventoryTransactionInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.FinanceEnterpriseGroup			= actor.context.FinanceEnterpriseGroup
							invoked.RunTime							= current timestamp
							invoked.RunGroup						= PrmRunGroup
							invoked.IncludeInventoryTransactionsInError = IncludeInventoryTransactionsInError
							invoked.ReleaseInventoryTransactions 	= ReleaseInventoryTransactions
							invoked.MoveErrorsToNewRunGroup     	= PrmMoveErrorsToNewRunGroup	
							invoked.ErrorRunGroupPrefix         	= PrmErrorRunGroupPrefix
							
					Exit Rules
					
						invoke InterfaceInventoryTransactionLineDetails InventoryTransactionImportLineDetail
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmResult                   = LocalInterfaceResultView.InventoryTransactionInterfaceResult

				Instance Rules
					if    (IncludeInventoryTransactionsInError
					or     !RecordInError)
						
						ErrorOccurred						= false
						RecordInError                       = false
						initialize ErrorLevel                          
						
						if (InventoryTransactionNumber entered)
							invoke UpdateFromInterface InterfacedInventoryTransactionRel
								invoked.PrmRunGroup = RunGroup
								invoked.PrmInventoryTransactionImport = InventoryTransactionImport
								invoked.PrmOriginatingInterfaceRun = LocalInterfaceResultView.InventoryTransactionInterfaceResult

						initialize ErrorMessage
						
						if (InventoryTransactionNumber !entered)
							if (!ImportLinesExist)
								ErrorOccurred 		= true
								LocalErrorMessage 	= NoImportLinesMessage
							else
							if (!Status.Released
							and !UnreleasedTypeOK)
								ErrorOccurred       = true
								LocalErrorMessage   = UnreleasedWrongTypeMessage
							else
							if (Status.NonGlobalLedger
							and InventoryDocumentType != "BT")
				
								ErrorOccurred       = true
								LocalErrorMessage   = OnlyBinTransferMessage
							else
							if (IntransitReceivingStatus > 0
							or  IntransitDocument entered
							or  InventoryDocumentType = "IR")
								ErrorOccurred       = true
								LocalErrorMessage   = NoIntransitTransferMessage
							else
								invoke CreateFromImport InventoryTransaction
									assign result to InterfacedInventoryTransactionNumber
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.Reference1 				= InventoryTransactionImport
									invoked.InterfaceInProcess		= 1
									invoked.OriginatingInterfaceRun = LocalInterfaceResultView.InventoryTransactionInterfaceResult
						ResultProcessedBy 				= LocalInterfaceResultView.InventoryTransactionInterfaceResult
						if (InventoryTransactionNumber !entered)
							InventoryTransactionNumber    	= InterfacedInventoryTransactionNumber.InventoryTransaction
							
						
						if (!ErrorOccurred)
							for each InventoryTransactionImportLine set
								if (!ErrorOccurred
								and !each.TransactionLineExists
								and (each.TransactionSystemCode = "IC"
								or   Status.Released))
									invoke CreateFromImport InventoryTransactionLine
										resume on error
											ErrorOccurred     = true
											LocalErrorMessage = error message
										fill in fields from each
										if (InventoryTransactionNumber entered) 
											invoked.InventoryTransaction    		= InventoryTransactionNumber
										else
											invoked.InventoryTransaction   			= InterfacedInventoryTransactionNumber.InventoryTransaction
										invoked.InventoryTransactionLine.LineNumber	= each.InventoryTransactionImportLine

										if (each.Bin not entered and each.BinDetailsExist)
											invoked.MultipleBins					= true
									
									if (!ErrorOccurred)
										if (each.DetailsExist)
											invoke Update each
												invoked.TransactionLineExists = true
										if (!each.DetailsExist)
											invoke FastDelete each
									if (ErrorOccurred)
										invoke SetError
											invoked.PrmErrorMessage = LocalErrorMessage
											invoked.PrmResult       = LocalInterfaceResultView.InventoryTransactionInterfaceResult
											invoked.PrmErrorLevel   = 2	
								else
									if (!ErrorOccurred
									and !each.TransactionLineExists
									and each.TransactionSystemCode != "IC"
									and !Status.Released)
										invoke SetError
											invoked.PrmErrorMessage = MismatchedSystemCodeMessage
											invoked.PrmResult       = LocalInterfaceResultView.InventoryTransactionInterfaceResult
											invoked.PrmErrorLevel   = 2		
						else
						if (ErrorOccurred)
							invoke SetError
								invoked.PrmErrorMessage 		= LocalErrorMessage
								invoked.PrmResult       		= LocalInterfaceResultView.InventoryTransactionInterfaceResult
								invoked.PrmErrorLevel   		= 1
								
		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like InventoryTransactionInterfaceResult
 				PrmErrorLevel           is Numeric size 2
 					
			Action Rules
				LocalInterfaceResult   = PrmResult
						
				RecordInError					= true
				ErrorMessage					= PrmErrorMessage
				ErrorLevel                      = PrmErrorLevel
				
				
		FinishLoadInterface is a Set Action
			restricted
			Parameters
				PrmRunGroup             is a RunGroup
				PrmResult               is like InventoryTransactionInterfaceResult
						       
			Instance Selection
				where (RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				Company
				
			Action Rules
				Empty Set Rules
					LocalInterfaceResult       = PrmResult
					invoke Finish LocalInterfaceResultsRel

				Instance Rules
					LocalInterfaceResult = PrmResult
					if (!RecordInError)
						invoke UpdateFromInterface InterfacedInventoryTransactionRel
							invoked.SetInterfaceInProcessFalse   = true
							if (LocalInterfaceResultsRel.ReleaseInventoryTransactions)
								invoked.ReleaseInventoryTransaction = true
							invoked.PrmRunGroup = RunGroup
							invoked.PrmInventoryTransactionImport = InventoryTransactionImport
					else
						LocalInterfaceResult = PrmResult
						invoke Finish LocalInterfaceResultsRel
							invoked.PrmInventoryTransactionImportCompany = Company
							invoked.PrmInventoryTransactionImportLocation = InventoryLocation
							invoked.PrmInventoryTransactionDocumentType = InventoryDocumentType
							invoked.PrmInventoryTransactionImport = InventoryTransactionImport
						
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
			
		UpdateRunGroup is an Instance Action
			restricted
			Parameters
				PrmRunGroup                is a RunGroup
				
			Action Rules
				RunGroup   = PrmRunGroup	

		FinishInterfaceResult is an Instance Action
			restricted

			Parameters
				PrmResult					is like InventoryTransactionInterfaceResult

			Action Rules
				LocalInterfaceResult = PrmResult
				invoke Finish LocalInterfaceResultsRel
					invoked.PrmInventoryTransactionImportCompany = Company
					invoked.PrmInventoryTransactionImportLocation = InventoryLocation
					invoked.PrmInventoryTransactionDocumentType = InventoryDocumentType
					invoked.PrmInventoryTransactionImport = InventoryTransactionImport
