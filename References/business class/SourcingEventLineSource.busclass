SourcingEventLineSource is a BusinessClass
    owned by ss
    prefix is EVS

    Ontology
    	symbolic key is SourcingEventLineSource 
    	
    Persistent Fields
    	ProcurementGroup
    	Vendor
    		context of SupplierGroup	
 		PurchaseFromLocation	
 			context of SupplierGroup
 		OriginCode				is AlphaUpper size 2
 			States
	            ContractManagement   value is "CM"
	            Requisitions         value is "RQ"
	            StrategicSourcing    value is "SS"
    	SourceDocumentNumeric	
    	OperatingCompany		
    	RequestingLocation 		is like RequestingLocation 
    	LineNumber 				 
    	Quantity				
    	ShipTo					is an InventoryLocation 
    	POCode					
    	SupplierGroup			
    	Supplier				 
    	AwardedQuantity			is a Quantity		
	
	Derived Fields
		UnprocessedQuantity is a DerivedField		
			type is like Quantity 	
			restricted		
			return (Quantity - AwardedQuantity)
			
		ViewContractMessage is a MessageField 	
			restricted
			"ViewContract"
			
		ViewRequisitionMessage is a MessageField 	
			restricted
			"ViewRequisition"
		
		RequestingLocationProcessLevel is a DerivedField
			type is Text
			restricted
			if (Requisitions)
				return RequestingLocationRel.ProcessLevel
			else
				return blank
		
		FromCm is a ConditionalField
			type is Text 	
			restricted
   			if (ContractManagement)
   				ViewContractMessage
   			else
   				blank
   				
   		FromRq is a ConditionalField
   			type is Text 	
   			if (Requisitions)
   				ViewRequisitionMessage
   			else
   				blank
   				
   		ContractLineCost is a DerivedField
   			type is like UnitCost 	
			restricted
   			if (ContractLineRel exists)
   				return (ContractLineRel.BaseCost)
   				
   		ContractLineUsage is a DerivedField
   			type is like Quantity 	
			restricted
   			if (ContractLineRel exists)
   				return (ContractLineRel.QuantityOrdered)
			
	Sets
		ByPurchaseOrderInterface
			indexed
			duplicates
			Sort Order
				Company
				OriginCode
				SourceDocumentNumeric
				OperatingCompany
				RequestingLocation
				LineNumber
		
		ByContractLine
			indexed
			duplicates
			Sort Order
				ProcurementGroup
				OriginCode
				SourceDocumentNumeric
				LineNumber
				
		ByRequisitionLine
			Sort Order
				Company
				SourceDocumentNumeric
				LineNumber
				SourcingEvent
				SourcingEventLine
			Instance Selection
				where (Requisitions)
				
	Conditions
		PointerfaceCreated 	
			restricted
			when (PurchaseOrderInterfaceRel exists)
	
		ContractManagement 	
			restricted
			when (OriginCode = "CM")
		Requisitions 	
			restricted
			when (OriginCode = "RQ")
		Sourcing 	
			restricted
			when (OriginCode = "SS")
			
		ReqLineNotClosed
			restricted
			when (!RequisitionLineRel.Closed)
		
		HasProcessLevel
			restricted
			when (RequestingLocationProcessLevel entered)
		
		RequisitionsComments 	
			restricted
			when (Requisitions
			and   RequisitionHeaderCommentsRel exists)
		
		LineToContract
			restricted
			when (SourcingEventLine.ContractOutput)

	Relations
		PurchaseOrderInterfaceRel
            one-to-one relation to PurchaseOrderInterface 
            Field Mapping uses Set10
                related.PurchaseOrderInterface.SourceDocumentOrigin     = OriginCode
                related.PurchaseOrderInterface.OperatingCompany			= OperatingCompany
                related.PurchaseOrderInterface.SourceDocumentNumeric	= SourceDocumentNumeric
                related.PurchaseOrderInterface.LineNumber				= LineNumber
                related.PurchaseOrderInterface.SourceDocumentAlpha		= blank
                related.PurchaseOrderInterface.RequestingLocation       = RequestingLocation
                
		PurchaseOrderInterfaceHeaderRel
            one-to-many relation to PurchaseOrderInterface 
            Field Mapping uses Set10
                related.PurchaseOrderInterface.SourceDocumentOrigin     = OriginCode
                related.PurchaseOrderInterface.OperatingCompany			= OperatingCompany
                related.PurchaseOrderInterface.SourceDocumentNumeric	= SourceDocumentNumeric
                
		RequisitionLineRel
			one-to-one relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company				= OperatingCompany
				related.Requisition			= SourceDocumentNumeric
				related.RequisitionLine		= LineNumber
                
		RequisitionHeaderRel
			one-to-one relation to Requisition
			Field Mapping uses symbolic key
				related.Company				= OperatingCompany
				related.Requisition			= SourceDocumentNumeric
				
		RequisitionHeaderCommentsRel
			one-to-many relation to RequisitionComment
			Field Mapping uses part of key
				related.Company				= OperatingCompany
				related.Requisition			= SourceDocumentNumeric
		
		RequisitionLineCommentsRel
			one-to-many relation to RequisitionLineComment
			Field Mapping uses part of key
				related.Company				= OperatingCompany
				related.Requisition			= SourceDocumentNumeric
				related.RequisitionLine		= LineNumber
		
		RequestingLocationRel
			one-to-one relation to RequestingLocation
			Field Mapping uses symbolic key
				related.Company	  	        = OperatingCompany
				related.RequestingLocation	= RequestingLocation

		ContractRel
			one-to-one relation to Contract
			valid when (ContractManagement)
			Field Mapping uses symbolic key
				related.ContractGroup		= ProcurementGroup
				related.Contract			= SourceDocumentNumeric
				
		ContractLineRel
			one-to-one relation to ContractLine
			valid when (ContractManagement)
			Field Mapping uses symbolic key
				related.ContractGroup		= ProcurementGroup.ContractGroupRel.ContractGroup
				related.Contract			= SourceDocumentNumeric
				related.ContractLine        = LineNumber
		
		ContractLineDistributionsRel
			one-to-many relation to ContractLineDistribution
			valid when (ContractManagement)
			Field Mapping uses symbolic key
				related.ContractGroup		= ProcurementGroup.ContractGroupRel.ContractGroup
				related.Contract			= SourceDocumentNumeric
				related.ContractLine        = LineNumber

		ReqProcurementTransactionDistributionRel
			one-to-many relation to RequisitionLineDistribution
			Field Mapping uses symbolic key
				related.Company             	= OperatingCompany
				related.Requisition				= SourceDocumentNumeric
				related.RequisitionLine         = LineNumber
				related.AddOnCharge             = blank
		
		PurchaseOrderLineSourceRel
			one-to-many relation to PurchaseOrderLineSource 
			Field Mapping uses FromStrategicSourcing
				related.PurchaseOrderLineSource.SourceDocumentOrigin     = OriginCode
        		related.PurchaseOrderLineSource.OperatingCompany         = OperatingCompany
        		related.PurchaseOrderLineSource.SourceDocumentNumeric    = SourceDocumentNumeric
				related.PurchaseOrderLineSource.SourceDocumentLineNumber = LineNumber
				
		SourcingEventLineResponseAOCRel
			one-to-many relation to SourcingEventLineResponseAOC
			Field Mapping uses ByEvent
				related.Company  			= Company
	            related.SourcingEvent 		= SourcingEvent
	            related.SourcingEventLine 	= SourcingEventLine
	            
	    PurchaseOrderLineForContractRel
	    	one-to-many relation to PurchaseOrderLine
	    	Field Mapping uses ByContractLine
	    		related.ProcurementGroup    = ProcurementGroup
	    		related.Contract            = SourceDocumentNumeric
	    		related.ContractLine        = LineNumber
	    		related.Company             = Company

	Actions
   		Create is a Create Action
   			restricted
   			Action Rules
   				constraint (!SourcingEvent.IsTemplate)
   					"CannotCreateLineSourcesForEventTemplates"
   			
   		Update is an Update Action
   			restricted
   			   					
   		Delete is a Delete Action
   			restricted
   			Action Rules
   				if (ContractManagement)		
		    		invoke FastUpdateToEventClear ContractLineRel
						
		DeletePurchaseOrderInterface is an Instance Action
			restricted
			Action Rules
				if (PointerfaceCreated)
					invoke Delete PurchaseOrderInterfaceRel
					
				 
