WarehouseLocationRuleDetail is a BusinessClass
    owned by wh
    prefix is WHU
    classic name is WHLOCRULE

    Ontology
        symbolic key is WarehouseLocationRuleDetail
        	classic set name is WHUSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

		LocationCompany is an InventoryCompany
			default label is "Company"
        InventoryLocation
            classic name is LOCATION
			context of LocationCompany
			
    Conditions
		IsLevelOne
			restricted
			classic name is LEVELONE
			when (WarehouseLocationRuleDetail = 1)
			
		DuplicateLocationExists
			restricted
			when (DuplicateLocationRel exists)
			
		RuleInUse	
			restricted
			when (RequestingLocationsUsingLocationRuleRel exists
			or	RequisitionsUsingLocationRuleRel  exists
			or 	RequisitionLinesUsingLocationRuleRel exists)

	Relations
		DuplicateLocationRel
			one-to-many relation to WarehouseLocationRuleDetail
			Field Mapping uses Set2
				related.Company					= Company
				related.WarehouseLocationRule	= WarehouseLocationRule
				related.LocationCompany 		= LocationCompany
				related.InventoryLocation		= InventoryLocation
			Instance Selection
				where (related.WarehouseLocationRuleDetail != WarehouseLocationRuleDetail)
				
		RequestingLocationsUsingLocationRuleRel
			one-to-many relation to RequestingLocation
			Field Mapping uses symbolic key
				related.Company					= Company
			Instance Selection
				where (related.LocationRule = WarehouseLocationRule)
				
		RequisitionsUsingLocationRuleRel
			one-to-many relation to Requisition
			Field Mapping uses symbolic key
				related.Company					= Company
			Instance Selection
				where (related.LocationRule = WarehouseLocationRule)
				
		RequisitionLinesUsingLocationRuleRel
			one-to-many relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company					= Company
			Instance Selection
				where (related.LocationRule = WarehouseLocationRule)
				
		OtherLocationRuleDetailsRel
			one-to-many relation to WarehouseLocationRuleDetail
			Field Mapping uses symbolic key
				related.Company					= Company
				related.WarehouseLocationRule	= WarehouseLocationRule
			Instance Selection
				where (related.WarehouseLocationRuleDetail != WarehouseLocationRuleDetail)	

    Sets

        Set2
            indexed
            Sort Order
                Company
                WarehouseLocationRule
                LocationCompany
                InventoryLocation
                WarehouseLocationRuleDetail

        Set3
            indexed
            Instance Selection
                where (IsLevelOne)
            Sort Order
                Company
                WarehouseLocationRule

    Field Rules

		InventoryLocation
			required
			constraint (InventoryLocation.LocationType.InventoryLocation)
				"CannotEnterParLocationOrExchangeLocation"
			constraint (!DuplicateLocationExists)
				"LocationAlreadyExistsForThisRule"

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
			Entrance Rules					
				if (OtherLocationRuleDetailsRel not exists)
					constraint (not RuleInUse)
						"CannotDeleteTheLastLocationOnAWarehouseLocationRuleWhileTheRuleIsInUse"
