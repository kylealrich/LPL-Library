FinanceDimension7Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD7UP   
    
    Ontology
    	symbolic key is FinanceDimension7Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension7			is like FinanceDimension7
    	FinanceDimension7				is like FinanceDimension7
		SequenceDimension7				is like FinanceDimension7
		ParentSequenceDimension7		is like FinanceDimension7
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description	
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension7Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension7 is a DerivedField
			type is like FinanceDimension7
			restricted
			if (ParentFinanceDimension7 entered)
				return DerivedParentFinanceDimension7String
			else
				return TopNodeString
		
		DerivedSequenceParentDimension7 is a DerivedField
			type is like FinanceDimension7
			restricted
			if (ParentSequenceDimension7 entered)
				return ParentSequenceDimension7
			else
				return DerivedParentFinanceDimension7 
		
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension7Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension7String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension7Structure.StructureSequence
			"_"
			ParentFinanceDimension7
		
		DerivedSummaryFinanceDimension7 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension7Structure.StructureSequence
			"_"
			FinanceDimension7
	
		DerivedSummarySequenceDimension7 is a DerivedField
			type is like FinanceDimension7
			restricted
			if (SequenceDimension7 entered)
				return SequenceDimension7
			else 
				return DerivedSummaryFinanceDimension7

			
	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
 
 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension7Structure
				FinanceDimension7
				FinanceDimension7Upload.RunGroup
				FinanceDimension7Upload.Sequence
 	
	Relations
		FinanceDimension7HierarchyRel
			one-to-one relation to FinanceDimension7Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension7Structure = FinanceDimension7Structure
				related.FinanceDimension7		   = FinanceDimension7
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension7Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension7Structure = FinanceDimension7Structure
			Instance Selection
				where (related.FinanceDimension7Upload.RunGroup = FinanceDimension7Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 12
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 12
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension7
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"
							
    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension7Upload.Sequence entered)
					FinanceDimension7Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension7Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted

		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension7Structure		is a FinanceDimension7Structure
					default label is "FinanceDimension7Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension7Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension7	is a FinanceDimension7
				DynamicRulesExist		is Boolean		
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension7Structure	    = PrmFinanceDimension7Structure
				and    FinanceDimension7Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension7Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension7Structure
				FinanceDimension7Upload.RunGroup
				FinanceDimension7Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
										
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension7 = FinanceDimension7
					else
						LocalFinanceDimension7 = DerivedSummarySequenceDimension7
					if (LocalFinanceDimension7 exists)
						if  (RecordType.Posting
						and !FinanceDimension7HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension7
								resume on error
									LocalErrorMessage		= error message
								invoked.PrmStructure		= FinanceDimension7Structure					
								invoked.PrmParentDimension  = DerivedSequenceParentDimension7
						else					
							if (DerivedSequenceParentDimension7 != LocalFinanceDimension7.DerivedParentDimension)
								invoke Move LocalFinanceDimension7
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension7
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension7
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension7 
										except invoked.ParentFinanceDimension7
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension7
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension7
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension7
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension7 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension7
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension7Structure		is a FinanceDimension7Structure
					default label is "FinanceDimension7Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension7Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension7Structure	    = PrmFinanceDimension7Structure
				and    FinanceDimension7Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension7Structure   is a FinanceDimension7Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension7Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension7Structure	    = PrmFinanceDimension7Structure
				and    FinanceDimension7Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
