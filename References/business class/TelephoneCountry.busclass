TelephoneCountry is a BusinessClass
    default label is "TelephoneCountry/\Jurisdiction"
    owned by person
	prefix is TCTRY
    stored in environment	

	Ontology
		symbolic key is TelephoneCountry
	
    Patterns
        implements StaticJava
        disable AsOfDateProcessing
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Persistent Fields
		Description 	is Alpha 50
		DisplayOrder    is Numeric 5
		
	Transient Fields
		NewDisplayOrder is Numeric size 5
		
	Sets
		ByDisplayOrder
			duplicates
			Sort Order
				DisplayOrder
				TelephoneCountry
		ByDescription
			Sort Order
				Description
				TelephoneCountry

	Relations
		OtherCountries
			one-to-many relation to TelephoneCountry
			Field Mapping uses ByDisplayOrder
			Instance Selection
				where (related.TelephoneCountry != TelephoneCountry)
	
	Conditions
		Never
			when (1 = 0)
					
	Derived Fields
		DescriptionString is a StringField
			type is Alpha size 60
			Description
			" (+"
			TelephoneCountry
			")"		
			
		DescriptionOrKey is a DerivedField
			type is Alpha 50
			restricted
			
			if (Description entered)
				return Description
				
			return TelephoneCountry
											
    Actions
    	Create is a Create Action
    		Action Rules
			    if (!DisplayOrder entered)  
					DisplayOrder = (last OtherCountries.DisplayOrder + 1)
					
    	Update is an Update Action
    	
    	Delete is a Delete Action  
    	
    	ResequenceAll is a Set Action
			run in foreground
		
			Sort Order
				Description
				TelephoneCountry		

			Instance Selection
				where (TelephoneCountry entered)
						
			Local Fields
				LastDisplayOrder is Numeric size 5
											
			Action Rules
				
				Instance Rules 
					LastDisplayOrder    += 1
					DisplayOrder 		= LastDisplayOrder 					 							
	
