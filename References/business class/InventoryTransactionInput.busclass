InventoryTransactionInput is a BusinessClass
    owned by ic
    prefix is ICCSV

    Ontology
    	symbolic key is InventoryTransactionInput
    
    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
        RunGroup
        Company                 is like Company   
        InventoryLocation      	is like InventoryLocation
        CsvRecordType			is Numeric size 1
        	States
        		Header          value is 1
        		Line            value is 2
        		Detail          value is 3		
        InventoryTransactionImport is like InventoryTransactionImport
        InventoryDocumentType   is like InventoryDocumentType
        InventoryTransactionImportLine is like InventoryTransactionImportLine
        InventoryTransactionNumber is like InventoryTransaction
        TransactionSystemCode   is like TransactionSystemCode
        TransactionDate			is TimeStamp
        GeneralLedgerDate		is Date
        	default label is "GlobalLedgerDate"
        EstimatedDeliveryDate	is Date
        FromToCompanyLocation	is like FromToCompanyLocation
        IntransitTransfer		is Boolean
        IntransitDocument		is like InventoryTransaction
        Reprint  				is Boolean
		Status                  is Numeric size 1
		IntransitReceivingStatus is Numeric 1
		CurrencyTable           is like CurrencyTable
		Item 					is like Item
		Quantity				
		UnitCost				is an InternationalCost
		UseLastIssueCost        is Boolean
		AddOnUnitCost			is an InternationalCost
		TransactionUOM			is like UnitOfMeasure
		Reference				
		Bin						is like Bin
		InventoryReasonCode		is like InventoryReasonCode
		LineComment				is AlphaUpper size 30
		OffsetAccount			is like TransactionCodeBlock
		AddOnCostAccount		is like TransactionCodeBlock
		FromToCompanyLocationBin is like FromToCompanyLocationBin
		NationalDrugCode        is like NationalDrugCode
		GrantNumber  
		EAMWorkOrderNumber      is a WorkOrderNumber
		SecondaryQuantity		
		OriginalQuantity		is Decimal size 13.4
		Serial					is like ItemSerialNumber
		Lot						is like ItemLot
		Sublot					is like Sublot
		LotOnHold				is Boolean
		LotExpirationDate		is Date
		
    Local Fields
    	
    	LocalRunGroup           				is a RunGroup
    	LocalErrorMessage       				is Alpha size 150
    	LocalCompany                    		is like Company
		LocalInventoryLocation                  is like InventoryLocation
		LocalDocumentType                       is like InventoryDocumentType 
		LocalInventoryTransactionImport         is like InventoryTransactionImport
		LocalInventoryTransactionImportLine     is like InventoryTransactionImportLine
		InterfacedInventoryTransactionNumber   	is an InventoryTransaction view
		LocalInterfaceResult            		is like InventoryTransactionInterfaceResult
    
    Derived Fields
    
    	NoImportLinesMessage is a MessageField
    		restricted
    		"CannotAddNewInventoryTransaction;MustHaveLineRecordsToProcessInventoryTransaction"
    
   		MismatchedSystemCodeMessage is a MessageField
    		restricted
    		"AnySystemCodeOtherThanICMustBeInterfacedWithAReleasedStatus"
    		
    	UnreleasedWrongTypeMessage is a MessageField
    		restricted
    		"ThisInventoryDocumentTypeMustHaveAStatusOfReleased"
    		
    	OnlyBinTransferMessage is a MessageField
    		restricted
    		"OnlyBinTransfersCanHaveAStatusOfNonGlobalLedger"
   
   		NoIntransitTransferMessage is a MessageField
   			restricted
   			"CannotImportIntransitReceivingDocuments"

    Conditions
    
    	InputLinesExist
			restricted
			when (InventoryTransactionInputLinesRel exists)
    
    	OtherDetailsForLineExist
    		restricted
    		when (OtherDetailsForLineRel exists)
    		
    	DetailsExist
    		restricted
    		when (OtherDetailsForLineRel exists)
    		
    	RunGroupExistsInImport
    		restricted
    		when (ImportRunGroupRel exists)
    		    
		UnreleasedTypeOK
			restricted
			when (InventoryDocumentType.Adjustment
			or    InventoryDocumentType.InventoryIssue
			or    InventoryDocumentType.InventoryReceipt
			or    InventoryDocumentType.BinTransfer
			or    InventoryDocumentType.InventoryTransfer)

    Relations
    
    	InventoryTransactionInputByRunGroupRel
        	one-to-many relation to InventoryTransactionInput
        	Field Mapping uses ByRunGroup
        		related.RunGroup                    = LocalRunGroup
			Instance Selection
				where (related.InventoryCompanyRel.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
        		
        InventoryTransactionInputLinesRel
            classic name is ICCTRNLIN
            one-to-many relation to InventoryTransactionInput
            delete cascades
            Field Mapping uses ByRunGroup
                related.RunGroup					= LocalRunGroup
				related.CsvRecordType               = 2
                related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryDocumentType       = InventoryDocumentType
                related.InventoryTransactionImport 	= InventoryTransactionImport
    			
    	InventoryTransactionsForCleanupRel
			one-to-many relation to InventoryTransactionInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.CsvRecordType               = 1
				related.Company           			= LocalCompany
				related.InventoryLocation           = LocalInventoryLocation
				related.InventoryDocumentType       = LocalDocumentType
				related.InventoryTransactionImport  = LocalInventoryTransactionImport
    
    	InventoryTransactionLinesForCleanupRel
			one-to-many relation to InventoryTransactionInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.CsvRecordType               = 2
				related.Company           			= LocalCompany
				related.InventoryLocation           = LocalInventoryLocation
				related.InventoryDocumentType       = LocalDocumentType
				related.InventoryTransactionImport  = LocalInventoryTransactionImport
				
		InventoryTransactionLineDetailsForCleanupRel
			one-to-many relation to InventoryTransactionInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.CsvRecordType               = 3
				related.Company           			= LocalCompany
				related.InventoryLocation           = LocalInventoryLocation
				related.InventoryDocumentType       = LocalDocumentType
				related.InventoryTransactionImport  = LocalInventoryTransactionImport
				
		OtherDetailsForLineRel
			one-to-many relation to InventoryTransactionInput
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				related.CsvRecordType               = 3
				related.Company           			= LocalCompany
				related.InventoryLocation           = LocalInventoryLocation
				related.InventoryDocumentType       = LocalDocumentType
				related.InventoryTransactionImport  = LocalInventoryTransactionImport
				related.InventoryTransactionImportLine = LocalInventoryTransactionImportLine
			Instance Selection
				where (related.InventoryTransactionInput      != InventoryTransactionInput)
				
		LocalInterfaceResultsRel
			one-to-one relation to InventoryTransactionInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= actor.context.FinanceEnterpriseGroup
				related.InventoryTransactionInterfaceResult		= LocalInterfaceResult 
				
		InventoryTransactionsRel
			one-to-one relation to InventoryTransaction
			Field Mapping uses symbolic key
				related.Company           			= Company
                related.InventoryLocation 			= InventoryLocation
                related.InventoryTransaction    	= InventoryTransactionNumber
    	
    	ImportRunGroupRel
			one-to-many relation to InventoryTransactionImport
			Field Mapping uses ByRunGroup
				related.RunGroup                    = RunGroup 
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
				     
    	InventoryCompanyRel
    		one-to-one relation to InventoryCompany
    		Field Mapping uses symbolic key
    			related.Company                 = Company
    	
    Sets

        ByRunGroup
			Sort Order
				RunGroup
				CsvRecordType
				Company
				InventoryLocation
				InventoryDocumentType
				InventoryTransactionImport
				InventoryTransactionImportLine
        
    Field Rules
    	
    	RunGroup
    		required
    		
    	CsvRecordType
    		required
    		
    	Company
			required
		InventoryLocation
			required
		InventoryDocumentType
			required
    	
    	InventoryTransactionImport
    		required
    		
    	InventoryTransactionImportLine
    		if (CsvRecordType = 2
    		or  CsvRecordType = 3)
    			required
    			
    	Item 
    		if (CsvRecordType = 2)
    			required
    			
    	Quantity
    		if (CsvRecordType = 2)
    			required
    
    Actions

        Create is a Create Action
        	
        Update is an Update Action

        Delete is a Delete Action
        
		InterfaceInventoryTransactions is a Set Action
			Parameters
				PrmRunGroup							is a RunGroup
					default label is "RunGroup"
				ReleaseInventoryTransactions        is Boolean
				PrmMoveErrorsToNewRunGroup  		is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix				is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				
				PrmRunGroup
					LocalRunGroup     = PrmRunGroup
					required
						"RunGroupIsRequired"
					constraint (!RunGroupExistsInImport)
						"RunGroupAlreadyExistsInInterfaceRecords;MustFinishProcessingRunGroupBeforeUsingAgain"
				
				ReleaseInventoryTransactions
					initial value is true
				
			Local Fields
				LocalInterfaceResultView		is a InventoryTransactionInterfaceResult view
				LocalErrorOccurred              is Boolean				
			
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and    CsvRecordType = 1
				and    InventoryCompanyRel.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

			Sort Order
				RunGroup
				
			Action Rules
			
				Empty Set Rules
					
					invoke Create InventoryTransactionInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.FinanceEnterpriseGroup			= actor.context.FinanceEnterpriseGroup
						invoked.RunTime							= current timestamp
						invoked.RunGroup						= PrmRunGroup
						invoked.ReleaseInventoryTransactions 	= ReleaseInventoryTransactions
						invoked.MoveErrorsToNewRunGroup     	= PrmMoveErrorsToNewRunGroup	
						invoked.ErrorRunGroupPrefix         	= PrmErrorRunGroupPrefix
						invoked.Status                          = 1 
											
				RunGroup Set Rules
					Entrance Rules
			
						LocalErrorOccurred							= false
						
						invoke Create InventoryTransactionInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.FinanceEnterpriseGroup			= actor.context.FinanceEnterpriseGroup
							invoked.RunTime							= current timestamp
							invoked.RunGroup						= PrmRunGroup
							invoked.ReleaseInventoryTransactions 	= ReleaseInventoryTransactions
							invoked.MoveErrorsToNewRunGroup     	= PrmMoveErrorsToNewRunGroup	
							invoked.ErrorRunGroupPrefix         	= PrmErrorRunGroupPrefix
							
					Exit Rules
					
						invoke InterfaceInventoryTransactionLineDetails 
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmResult                   = LocalInterfaceResultView.InventoryTransactionInterfaceResult

				Instance Rules
						
					LocalErrorOccurred					= false
					initialize LocalErrorMessage
					
					LocalRunGroup = PrmRunGroup
					if (InputLinesExist)
						invoke CreateFromImport InventoryTransaction
							assign result to InterfacedInventoryTransactionNumber
							resume on error
								LocalErrorOccurred	= true
								LocalErrorMessage	= error message
							fill in fields from this instance
							invoked.InterfaceInProcess		  							= 1
							invoked.OriginatingInterfaceRun   							= LocalInterfaceResultView.InventoryTransactionInterfaceResult
					else
					if (!InputLinesExist)
						LocalErrorOccurred	= true
						LocalErrorMessage 	= NoImportLinesMessage
					else
					if (!Status = 1
					and !UnreleasedTypeOK)
						LocalErrorOccurred  = true
						LocalErrorMessage   = UnreleasedWrongTypeMessage
					else
					if (Status = 3
					and InventoryDocumentType != "BT")
						LocalErrorOccurred  = true
						LocalErrorMessage   = OnlyBinTransferMessage
					else
					if (IntransitReceivingStatus > 0
					or  IntransitTransfer = true
					or  IntransitDocument entered
					or  InventoryDocumentType = "IR")
						LocalErrorOccurred  = true
						LocalErrorMessage   = NoIntransitTransferMessage
					
					InventoryTransactionNumber = InterfacedInventoryTransactionNumber.InventoryTransaction    		
					if (!LocalErrorOccurred)
						for each InventoryTransactionInputLinesRel
							if (!LocalErrorOccurred)
								invoke CreateFromImport InventoryTransactionLine
									resume on error
										LocalErrorOccurred     	= true
										LocalErrorMessage 		= error message
									fill in fields from each
									invoked.InventoryTransaction    	= InterfacedInventoryTransactionNumber.InventoryTransaction
								if (!LocalErrorOccurred)
									if (!each.DetailsExist)
										invoke FastDelete each
								if (LocalErrorOccurred)
									invoke SetError
										invoked.PrmErrorMessage = LocalErrorMessage
										invoked.PrmResult       = LocalInterfaceResultView.InventoryTransactionInterfaceResult
										invoked.PrmErrorLevel   = 2	
					else
					if (LocalErrorOccurred)
						invoke SetError
							invoked.PrmErrorMessage 		= LocalErrorMessage
							invoked.PrmResult       		= LocalInterfaceResultView.InventoryTransactionInterfaceResult
							invoked.PrmErrorLevel   		= 1
								
		InterfaceInventoryTransactionLineDetails is a Set Action    
			restricted
			Parameters
				PrmRunGroup					is a RunGroup
				PrmResult                   is like InventoryTransactionInterfaceResult
				
			Local Fields
				LocalHasError                          is Boolean
			
			Instance Selection			
				where  (RunGroup		= PrmRunGroup
				and     CsvRecordType 	= 3
				and     InventoryCompanyRel.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
				
			Sort Order
				RunGroup
				Company	
				InventoryLocation
				InventoryTransactionImport
				
			Action Rules
				Empty Set Rules
				
					invoke FinishLoadInterface 
						invoked.PrmRunGroup  		= PrmRunGroup
						invoked.PrmResult    		= PrmResult
					
				RunGroup Set Rules
					Exit Rules
						
						invoke FinishLoadInterface 
							invoked.PrmRunGroup  		= PrmRunGroup
							invoked.PrmResult    		= PrmResult
			
				Instance Rules
					
					LocalHasError 	= false
					invoke Create InventoryTransactionLineDetail
						resume on error
							LocalHasError  		= true
							LocalErrorMessage	= error message
						fill in fields from this instance
						invoked.InventoryTransaction      	   = InventoryTransactionNumber
						invoked.InventoryTransactionLine       = InventoryTransactionImportLine
											
					if (LocalHasError)
						invoke SetError 
							invoked.PrmErrorMessage = LocalErrorMessage
							invoked.PrmResult       = PrmResult
							invoked.PrmErrorLevel   = 3
					else
					if (!LocalHasError)
						LocalRunGroup                       = RunGroup
						LocalCompany                  		= Company
						LocalInventoryLocation        		= InventoryLocation
						LocalDocumentType             		= InventoryDocumentType
						LocalInventoryTransactionImport     = InventoryTransactionImport
						LocalInventoryTransactionImportLine = InventoryTransactionImportLine
						if (!OtherDetailsForLineExist)
							invoke FastDelete InventoryTransactionLinesForCleanupRel
						invoke FastDelete
		
		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like InventoryTransactionInterfaceResult
 				PrmErrorLevel           is Numeric size 2
 					
			Action Rules
				LocalRunGroup                       = RunGroup
				LocalCompany                  		= Company
				LocalInventoryLocation        		= InventoryLocation
				LocalDocumentType             		= InventoryDocumentType
				LocalInventoryTransactionImport     = InventoryTransactionImport
				LocalInventoryTransactionImportLine = InventoryTransactionImportLine
										
				if (PrmErrorLevel = 1)
					invoke CreateNoRules InventoryTransactionImport
						fill in fields from this instance
						invoked.RecordInError					= true
						invoked.ErrorMessage					= PrmErrorMessage
						invoked.ErrorLevel                      = PrmErrorLevel
						invoked.ResultProcessedBy               = PrmResult
				else 
					invoke CreateNoRules InventoryTransactionImport
						fill in fields from first InventoryTransactionsForCleanupRel
						invoked.RecordInError					= true
						invoked.ErrorMessage					= PrmErrorMessage
						invoked.ErrorLevel                      = PrmErrorLevel
						invoked.ResultProcessedBy               = PrmResult
								
				if (InventoryTransactionLinesForCleanupRel exists)
					for each InventoryTransactionLinesForCleanupRel
						invoke CreateNoRules InventoryTransactionImportLine
							fill in fields from each
						invoke FastDelete each
				
				if (InventoryTransactionLineDetailsForCleanupRel exists)
					for each InventoryTransactionLineDetailsForCleanupRel
						invoke CreateNoRules InventoryTransactionImportLineDetail
							fill in fields from each
						invoke FastDelete each		
				
				invoke FastDelete first InventoryTransactionsForCleanupRel
				LocalInterfaceResult = PrmResult
				invoke UpdateErrorInformation LocalInterfaceResultsRel
				
		FinishLoadInterface is a Set Action
			restricted
			Parameters
				PrmRunGroup             is a RunGroup
				PrmResult               is like InventoryTransactionInterfaceResult
						       
			Instance Selection
				where (RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				Company
				
			Action Rules
				Empty Set Rules
					LocalInterfaceResult       = PrmResult
					invoke Finish LocalInterfaceResultsRel
				
				RunGroup Set Rules
								
					Exit Rules
						LocalInterfaceResult       = PrmResult
						invoke Finish LocalInterfaceResultsRel

				Instance Rules
					LocalInterfaceResult = PrmResult
					invoke UpdateFromInterface InventoryTransactionsRel
						invoked.SetInterfaceInProcessFalse   = true
						if (LocalInterfaceResultsRel.ReleaseInventoryTransactions)
							invoked.ReleaseInventoryTransaction = true
					invoke FastDelete
						
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
			
