SupplierDiversityResponse is a BusinessClass
    owned by procurement
    prefix is SPDR

    Ontology
		symbolic key is	SupplierDiversityResponse
			
    Persistent Fields
		EffectiveDate			is Date
		ExpirationDate			is Date
		ReviewDate				is Date
		CertificationComplete	is Boolean 
		Alerts 
		Attachment
		Active                  is Boolean
	
	Context Fields
		ContextSupplyManagementReport	is a SupplyManagementReport
		Category
		CloseDateRange                  is a DateRange
		Company                         is a SourcingCompany
	
	Attach Rules
		constraint (CanUseForDocument)
			"DiversityCodeNotValidForUse"
	
	Field Rules
		EffectiveDate
					
			if (VendorGroupRel.DiversityValidWhenWithinDates)
				required
					
		ExpirationDate
			if (VendorGroupRel.DiversityValidWhenWithinDates)
				required
			
			constraint (ExpirationDate >= EffectiveDate)
				"ExpirationDateMustBeGreaterThanEffectiveDate"
	Conditions
		ExpirationDateYellowAlert
			restricted
			when (ExpirationDate entered
			and   Alerts.YellowAlert != 0
			and   current corporate date >= YellowAlertDate
			and   YellowAlertDate <= RedAlertDate)

		ExpirationDateRedAlert
			restricted
			when (ExpirationDate entered
			and   Alerts.RedAlert != 0
			and   current corporate date >= RedAlertDate
			and   RedAlertDate <= ExpirationDate)
			
		DatesEntered
			restricted
			when (EffectiveDate entered)
			
		VendorDiversityExists
			restricted
			when (VendorDiversitiesRel exists)

		CanUseForDocument
			restricted
			when (Active
			and  ((VendorGroupRel.DiversityValidWhenCertificationComplete
			and   CertificationComplete)
			or    !VendorGroupRel.DiversityValidWhenCertificationComplete)
			and  ((VendorGroupRel.DiversityValidWhenHasAttachment
			and    Attachment entered)
			or     !VendorGroupRel.DiversityValidWhenHasAttachment))
		
		IsActive
			restricted
			when (Active)
		
		NonDiverse
			restricted			
			when (PayablesDiversityCode not entered)
		
		EventNotificationAwardByDiversityRelExists
			restricted
			when (EventNotificationAwardByDiversityRel exists)
			
		HasAttachment
			restricted
			when (Attachment entered)
			
		EffectiveDateEntered
			restricted
			when (EffectiveDate entered)
			
		ExpirationDateEntered
			restricted
			when (ExpirationDate entered)

		EffectiveDateNotEnteredAndRequired
			restricted
			when (!EffectiveDateEntered
			and   VendorGroupRel.DiversityValidWhenWithinDates)

		WillDefaultAlertDays
			restricted
			when (SupplierGroup.YellowAlert entered)
		
		DiversityIsCurrent
			when ((EffectiveDate !entered
			or    EffectiveDate <= current corporate date)
			and  (ExpirationDate !entered
			or    ExpirationDate >= current corporate date))
			
		OnlySuppliersWithBids
			when (DerivedNumberOfEventResponses > 0
			or    DerivedNumberOfEventsNotified > 0)   
			
		PortalSupplier
			restricted
 			when ((SupplierGroup 	= actor.agent(SupplierSourceId).SupplierGroup
 			and    Supplier	   		= actor.agent(SupplierSourceId).Supplier))

	Sets
		ByDiversityCode
			Sort Order
				SupplierGroup
				PayablesDiversityCode
				Supplier
				SupplierDiversityResponse
				
		BySupplier	
			duplicates
			Sort Order
				SupplierGroup
				Supplier
		
	Relations
        
        PayablesDiversityCodeRel	
        	one-to-one relation to PayablesDiversityCode
        	Field Mapping uses symbolic key
            	related.VendorGroup				= SupplierGroup
        		related.PayablesDiversityCode	= PayablesDiversityCode
        
        VendorGroupRel
        	one-to-one relation to VendorGroup
        	Field Mapping uses symbolic key
        		related.VendorGroup             = SupplierGroup
        
        VendorDiversitiesRel
        	one-to-many relation to VendorDiversity
        	Field Mapping uses ByDateDescending 
        		related.VendorGroup				= SupplierGroup
        		related.Vendor					= Supplier.Vendor
        		related.VendorLocation          = blank
        		related.DiversityCode       	= PayablesDiversityCode
        
        SourcingEventResponseRel
        	one-to-many relation to SourcingEventResponse
        	Field Mapping uses BySupplier
        		related.NotifiedSupplier.SupplierGroup		= SupplierGroup
        		related.NotifiedSupplier.Supplier			= Supplier    
        	Instance Selection
        		where (related.SourcingEvent.FinalizedDate >= EffectiveDate  	
				and	   related.SourcingEvent.FinalizedDate <= ExpirationDate)		
        		
        SourcingEventLineResponseRel
        	one-to-many relation to SourcingEventLineResponse
        	Field Mapping uses BySupplier
        		related.NotifiedSupplier.SupplierGroup		= SupplierGroup
        		related.NotifiedSupplier.Supplier			= Supplier    	
        	Instance Selection
        		where (EffectiveDate					= blank
        		or	  (related.SourcingEvent.FinalizedDate >= EffectiveDate  	
				and	   related.SourcingEvent.FinalizedDate <= ExpirationDate))		
        		
        SourcingEventNotificationRel
        	one-to-many relation to SourcingEventNotification
        	Field Mapping uses BySupplier
        		related.NotifiedSupplier.SupplierGroup		= SupplierGroup
        		related.NotifiedSupplier.Supplier			= Supplier    	
        	Instance Selection
        		where (EffectiveDate					= blank
        		or	  (related.SourcingEvent.FinalizedDate >= EffectiveDate  	
				and	   related.SourcingEvent.FinalizedDate <= ExpirationDate))
	
		CompletedSupplierPerformanceEvaluationsRel
            one-to-many relation to SupplierPerformanceEvaluation 
            Field Mapping uses symbolic key
            	related.SupplierGroup 	= SupplierGroup
            	related.Supplier		= Supplier
            Instance Selection
            	where (related.IsEvaluationComplete	= true)
            	
        SupplierVendorMismatchRel
        	one-to-one relation to SupplierVendorMismatch
        	Field Mapping uses symbolic key
        		related.SupplierGroup 	= SupplierGroup
            	related.Supplier		= Supplier
         
        SupplierGroupExtensionRel
			one-to-one relation to SupplierGroupExtension
			Field Mapping uses symbolic key
				related.SupplierGroup   = SupplierGroup


		EventResponsesRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses BySupplier
				related.NotifiedSupplier.SupplierGroup	= SupplierGroup
 				related.NotifiedSupplier.Supplier		= Supplier
 			Instance Selection
 				where ((Company !entered
 				or     related.Company                 = Company)
 				and   (Category !entered
 				or     related.SourcingEvent.Category  = Category)
 				and   (related.SourcingEvent.CloseDate  within CloseDateRange
 				or     CloseDateRange !entered))
	
		EventNotifiedRel
			one-to-many relation to SourcingEventNotification
			Field Mapping uses BySupplier
				related.NotifiedSupplier.SupplierGroup	= SupplierGroup
 				related.NotifiedSupplier.Supplier		= Supplier
 			Instance Selection
 				where ((Company !entered
 				or     related.Company                 = Company)
 				and   (Category !entered
 				or     related.SourcingEvent.Category  = Category)
 				and   (related.SourcingEvent.CloseDate  within CloseDateRange
 				or     CloseDateRange !entered)) 

		EventResponsesNoBidRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses BySupplier
				related.NotifiedSupplier.SupplierGroup	= SupplierGroup
 				related.NotifiedSupplier.Supplier		= Supplier
 			Instance Selection
 				where (related.NoBid
 				and   (Company !entered
 				or     related.Company                 = Company)
 				and   (Category !entered
 				or     related.SourcingEvent.Category  = Category)
 				and   (related.CloseDate  within CloseDateRange
 				or     CloseDateRange !entered))


		EventNotificationAwardByDiversityRel
			one-to-many relation to SourcingEventNotification
			Field Mapping uses BySupplier
				related.NotifiedSupplier.SupplierGroup	= SupplierGroup
 				related.NotifiedSupplier.Supplier		= Supplier
			Instance Selection
				where (related.AwardHistoryListNotEmpty)
		
	Derived Fields
		YellowAlertDate is a ComputeField
			type is TimeStamp
			restricted
			(ExpirationDate - Alerts.YellowAlert as days)

		RedAlertDate is a ComputeField
			type is TimeStamp
			restricted
			(ExpirationDate - Alerts.RedAlert as days)
	 

		DerivedNumberOfEventResponses is a ComputeField
			type is Numeric size 6
			(instance count of EventResponsesRel)
				
		DerivedNumberOfEventsNotified is a ComputeField
			type is Numeric size 6
			(instance count of EventNotifiedRel)
			

		DerivedNumberOfEventResponsesNoBid is a ComputeField
			type is Numeric size 6
			(instance count of EventResponsesNoBidRel)

		DerivedTotalBid is a DerivedField
			type is Decimal size 21.2
			return (sum EventResponsesRel.AllEventLineResponseTotalBid)

		DerivedTotalAwards is a DerivedField
			type is Decimal size 21.2
			return (sum EventResponsesRel.AllEventLineResponseTotalAwards)
		
		DerivedTotalAwardPercent is a DerivedField
			type is Percent	size 6.2
			return (DerivedTotalAwards/DerivedTotalBid)



	
		DerivedTotalEventsBidBySupplier is a DerivedField
			type is Numeric size 21
			return (sum EventNotificationAwardByDiversityRel.DerivedTotalEventsBid)
		
		DerivedTotalEventsAwardedBySupplier is a DerivedField
			type is Numeric size 21
			return (sum EventNotificationAwardByDiversityRel.DerivedTotalEventsAwarded)
		
		DerivedEventsBidPercentBySupplier is a ComputeField	
    		type is Percent size 6.2
    		(DerivedTotalEventsAwardedBySupplier/ DerivedTotalEventsBidBySupplier)

		DerivedTotalEventLinesBidBySupplier is a DerivedField
			type is Numeric size 6
			return (sum EventNotificationAwardByDiversityRel.DerivedTotalEventLinesBid)
		
		DerivedTotalEventLinesAwardedBySupplier is a DerivedField
			type is Numeric size 6
			return (sum EventNotificationAwardByDiversityRel.DerivedTotalEventLinesAwarded)
		
		DerivedEventLinesBidPercentBySupplier is a ComputeField	
    		type is Percent size 6.2
    		(DerivedTotalEventLinesAwardedBySupplier/ DerivedTotalEventLinesBidBySupplier)

 		DerivedBidTotalBySupplier is a DerivedField
			type is like InternationalAmount
			return (sum EventNotificationAwardByDiversityRel.DerivedBidTotal)
 		
 		DerivedAwardedTotalBySupplier is a DerivedField
			type is Decimal size 21.2
			return (sum EventNotificationAwardByDiversityRel.DerivedAwardedTotal)
 
		DerivedTotalBidPercentBySupplier is a ComputeField	
    		type is Percent size 6.2
    		(DerivedAwardedTotalBySupplier/ DerivedBidTotalBySupplier)
		

		
	Rule Blocks
		UpdateValidationEmail
			send email
				to SupplierGroup.NotificationEmailAddress
				from SupplierGroup.AdminEmailAddress
				cc Supplier.PrimaryContact.EmailAddress
				subject "<SupplierGroupExtensionRel.FinalUpdatedValidationEmailSubject>"
				Contents
					"<SupplierGroupExtensionRel.FinalUpdatedValidationEmailContent>"
					"DiversityInformationWasModifiedOn<update stamp>"
												
	Actions
		Create is a Create Action

			Field Rules
			
				Alerts
					
					if (ExpirationDate entered)
						if (Alerts.YellowAlert !entered)
							Alerts.YellowAlert = SupplierGroup.YellowAlert
						if (Alerts.RedAlert !entered)
							Alerts.RedAlert    = SupplierGroup.RedAlert
					if (ExpirationDate !entered)
						initialize Alerts
						
			Action Rules
				Active = true
			
			Exit Rules

				if  (SupplierGroup.AutoCompare
				and  Supplier.Vendor entered)
					invoke CompareSupplierAndVendor Supplier
						invoked.ParmSupplierGroup		= SupplierGroup
						invoked.ParmSupplier        	= Supplier
						invoked.CompareDiversity		= true

				if (SupplierGroup.RequireUpdatedSupplierValidation
				and	Supplier.SupplierStatus.Validated)
					invoke StatusUpdate Supplier
					
					include UpdateValidationEmail
								
		Update is an Update Action
			valid when (Supplier.IsUpdatable)
			Action Rules
				if (ExpirationDate changed)
					if (ExpirationDate !entered)
						initialize Alerts
					if (old ExpirationDate !entered)
						if (Alerts.YellowAlert !entered)
							Alerts.YellowAlert = SupplierGroup.YellowAlert
						if (Alerts.RedAlert !entered)
							Alerts.RedAlert    = SupplierGroup.RedAlert					
			
				if  (SupplierGroup.AutoCompare
				and  Supplier.Vendor entered
				and (EffectiveDate  changed
				or   ExpirationDate changed
				or   ReviewDate     changed))
					invoke CompareSupplierAndVendor Supplier
						invoked.ParmSupplierGroup		= SupplierGroup
						invoked.ParmSupplier        	= Supplier
						invoked.CompareDiversity		= true			
			
			Exit Rules
				if (SupplierGroup.RequireUpdatedSupplierValidation
				and	Supplier.SupplierStatus.Validated)
					invoke StatusUpdate Supplier
					
					include UpdateValidationEmail
											
		Delete is a Delete Action
			valid when (Supplier.IsUpdatable)
			Exit Rules
				if (SupplierGroup.RequireUpdatedSupplierValidation
				and	Supplier.SupplierStatus.Validated)
					invoke StatusUpdate Supplier
						
					include UpdateValidationEmail
					
		AttachDiversityToContract is an Instance Action
			Parameters
				ContractGroup
				Contract 
			Action Rules
				invoke Create ContractDiversity
					invoked.ContractGroup          = ContractGroup
					invoked.Contract               = Contract
					invoked.PayablesDiversityCode  = PayablesDiversityCode
		
		DiversityCompare is an Instance Action
			restricted
			Action Rules
			
				if  (PayablesDiversityCodeRel.Active)
					if  (VendorDiversitiesRel exists
					and  first VendorDiversitiesRel.DiversityExpirationDate !=ExpirationDate)
					 	if (!SupplierVendorMismatchRel exists)
					 			invoke Create SupplierVendorMismatch
									invoked.SupplierGroup                        = SupplierGroup
									invoked.Supplier                             = Supplier
									invoked.Vendor						         = Supplier.Vendor
					 	invoke Create SupplierVendorMismatchDetail
								invoked.SupplierGroup		  	= SupplierGroup
								invoked.Supplier				= Supplier
								if (first VendorDiversitiesRel.DiversityExpirationDate < ExpirationDate)
									invoked.Type					= 25
								if (first VendorDiversitiesRel.DiversityExpirationDate > ExpirationDate)
									invoked.Type 					= 26
								invoked.PayablesDiversityCode	= PayablesDiversityCode
								invoked.SupplierValue 			= ExpirationDate
								invoked.VendorValue				= VendorDiversitiesRel.DiversityExpirationDate
								
					if  (VendorDiversitiesRel exists
					and  first VendorDiversitiesRel.DiversityEffectiveDate !=EffectiveDate)
					 	if (!SupplierVendorMismatchRel exists)
					 			invoke Create SupplierVendorMismatch
									invoked.SupplierGroup                        = SupplierGroup
									invoked.Supplier                             = Supplier
									invoked.Vendor						         = Supplier.Vendor
					 	invoke Create SupplierVendorMismatchDetail
								invoked.SupplierGroup		  	= SupplierGroup
								invoked.Supplier				= Supplier
								invoked.Type					= 30
								invoked.PayablesDiversityCode	= PayablesDiversityCode
								invoked.SupplierValue 			= EffectiveDate
								invoked.VendorValue				= VendorDiversitiesRel.DiversityEffectiveDate
								invoked.DiversityCodeDcertDate  = VendorDiversitiesRel.DiversityExpirationDate
								
					if  (VendorDiversitiesRel exists
					and  first VendorDiversitiesRel.ReviewDate !=ReviewDate)
					 	if (!SupplierVendorMismatchRel exists)
					 			invoke Create SupplierVendorMismatch
									invoked.SupplierGroup                        = SupplierGroup
									invoked.Supplier                             = Supplier
									invoked.Vendor						         = Supplier.Vendor
					 	invoke Create SupplierVendorMismatchDetail
								invoked.SupplierGroup		  	= SupplierGroup
								invoked.Supplier				= Supplier
								invoked.Type					= 29
								invoked.PayablesDiversityCode	= PayablesDiversityCode
								invoked.SupplierValue 			= ReviewDate
								invoked.VendorValue				= VendorDiversitiesRel.ReviewDate
					
					if  (!VendorDiversitiesRel exists)
					 	if (!SupplierVendorMismatchRel exists)
					 			invoke Create SupplierVendorMismatch
									invoked.SupplierGroup                        = SupplierGroup
									invoked.Supplier                             = Supplier
									invoked.Vendor						         = Supplier.Vendor
					 	invoke Create SupplierVendorMismatchDetail
								invoked.SupplierGroup		  	= SupplierGroup
								invoked.Supplier				= Supplier
								invoked.Type 					= 23
								invoked.PayablesDiversityCode	= PayablesDiversityCode
								invoked.SupplierValue 			= PayablesDiversityCode
								invoked.DiversityCodeDcertDate  = ExpirationDate
								invoked.VendorValue				= "None"			
					
					
			
			
