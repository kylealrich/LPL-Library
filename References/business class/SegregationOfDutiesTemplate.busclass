SegregationOfDutiesTemplate is a BusinessClass
    owned by security
    prefix is SODT

    Ontology
        symbolic key is SegregationOfDutiesTemplate

    Patterns
    	implements CreateStamp
    	implements UpdateStamp
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        disable Auditing

    Persistent Fields
    	TemplateName                is Alpha size up to 256
        Description                 is Alpha size up to 500
        DataArea
        SODTemplateType				
        ExcludedRoles				is a RoleArray
        Priority					is Numeric size 1
        	States
        		Low					value is 1
        		Medium				value is 2
        		High				value is 3
        FilterBy                    is Alpha size up to 15
            default label is "RunBy"
            States
                User 			    value is "User"
                	default label is "Actor"
                Role 			    value is "Role"
                SecurityClass	    value is "SecurityClass"
        Actor            
        Role
        SecurityClass


    Field Rules
		DataArea
			initial value is parentcontext.dataarea			
			default to parentcontext.dataarea
		
		Priority
			initial value is Priority.Low
			default to Priority.Low
				
        FilterBy
            required
            
	Conditions
		IsFilterByUser
			when (FilterBy = FilterBy.User)
			
		IsFilterByRole
			when (FilterBy = FilterBy.Role)
			
		IsFilterBySecurityClass
			when (FilterBy = FilterBy.SecurityClass)
			
		IsHighPriority
			when (Priority = Priority.High)
			
		IsMediumPriority
			when (Priority = Priority.Medium)
			
		IsLowPriority
			when (Priority = Priority.Low)
			
        AnalysisExist
        	when (SegregationOfDutiesAnalysisRel exist)
			
	Sets
		ByCreateStamp
            indexed
            Sort Order
                create stamp 
                SegregationOfDutiesTemplate                		

	Relations
		SegregationOfDutiesTemplateObjectRel
		    one-to-many relation to SegregationOfDutiesObjectType
		    Field Mapping uses ByTemplate
		        related.SegregationOfDutiesTemplate   = SegregationOfDutiesTemplate

		SegregationOfDutiesAnalysisRel
		    one-to-many relation to SegregationOfDutiesAnalysis
		    Field Mapping uses ByTemplate
		        related.SegregationOfDutiesTemplate   = SegregationOfDutiesTemplate
        	
    Actions	
        Create is a Create Action

        Update is an Update Action

		Delete is a Delete Action

        PurgeTemplate is a Purge Action
        
        PurgeAllTemplates is a Set Action
        	confirmation required
        		"AreYouSureYouWantToPurgeAllTemplates?"
			Action Rules
				Instance Rules
					invoke PurgeTemplate
					
		RunReport is an Instance Action
			Action Rules
				constraint (not AnalysisExist)
        			"CannotRunReport.ResultAlreadyExist."
        			
				invoke CreateReport SegregationOfDutiesAnalysisGenerator in background
					invoked.SegregationOfDutiesTemplate = SegregationOfDutiesTemplate
					
		PurgeAllResultsForTemplate is an Instance Action
			Action Rules
				invoke PurgeResultsForTemplate SegregationOfDutiesAnalysis 
					invoked.TemplateId = SegregationOfDutiesTemplate
					
		CopyTemplate is an Instance Action
		    Parameters
		        ParamTemplateID                is Alpha size up to 80
		            default label is "TemplateID"
		        ParamFilterBy                    is Alpha size up to 15
		            default label is "RunBy"
		            States
		                User 			    value is "User"
		                	default label is "Actor"
		                Role 			    value is "Role"
		                SecurityClass	    value is "SecurityClass"
		        ParamActor                      is an Actor    
		            default label is "Actor"   
		        ParamRole                       is a Role
		            default label is "Role"  
		        ParamSecurityClass              is a SecurityClass
		            default label is "SecurityClass"
		
		    Parameter Rules
		        ParamTemplateID   required
		        ParamFilterBy       required
		
		    Action Rules
		        invoke Create
		            fill in fields from this instance
		            invoked.SegregationOfDutiesTemplate = ParamTemplateID
		            invoked.FilterBy = ParamFilterBy
		            if (ParamFilterBy = ParamFilterBy.User)
		                invoked.Actor = ParamActor
		            if (ParamFilterBy = ParamFilterBy.Role)
		                invoked.Role = ParamRole
		            if (ParamFilterBy = ParamFilterBy.SecurityClass)
		                invoked.SecurityClass = ParamSecurityClass
		
		        for each SegregationOfDutiesTemplateObjectRel
		            invoke Create each
		                fill in fields from each
		                invoked.SegregationOfDutiesTemplate = ParamTemplateID
		                invoked.BusinessViewName = each.SegregationOfDutiesObjectType.SecurableObjectName
