CloseManagementGroup is a BusinessClass
	owned by closemgmt
	prefix is CMGRP

	Ontology
		symbolic key is CloseManagementGroup

	Patterns

    Persistent Fields
		Description								is Alpha 100
		CorporateCalendar						is a SystemCalendar
		CorporatePeriodGroup					is a SystemCalendarPeriodGroup
		StartLateReasonCodeRequired				is Boolean
		CloseLateReasonCodeRequired				is Boolean
		RejectReasonCodeRequired				is Boolean
		ReopenTaskReasonCodeRequired			is Boolean
		VoidReasonCodeRequired					is Boolean
		EstimateReasonCodeRequired				is Boolean
		RejectOneTimeTaskReasonCodeRequired		is Boolean
		AllowOneTimeTasks						is Boolean
		RequireApprovalOfOneTimeTasks			is Boolean
		OneTimeTaskDefaultApprovalCode			is an ApprovalCode
		OverrideResourceEmailNotifications		is Boolean
		OverrideResourceLandmarkNotifications	is Boolean
		AllowResourceToReassignToResource		is Boolean
		AllowResourceToViewCompletedTasks		is Boolean
        AllowResourceToReopenCompletedTasks		is Boolean
		AllowResourceToVoidTasks				is Boolean
		AllowExternalEmails						is Boolean
		SendExternalEmailsOnOpenOfPeriod		is Boolean
		ExternalFromEmailAddress				is an EmailAddress 
			holds pii
		DefaultExternalEmailSubject				is Alpha 100
			Text Variables
 				TaskName							value is ClosePeriodTask.TaskName
 				ScheduledBeginDate					value is ClosePeriodTask.ScheduleForTask.BeginDate
 				ScheduledDueDate					value is ClosePeriodTask.ScheduleForTask.DueDate
 				ClosePeriod							value is ClosePeriodTask.ClosePeriod
		DefaultExternalEmailContent				is RichText
			Text Variables
 				TaskName							value is ClosePeriodTask.TaskName
 				ScheduledBeginDate					value is ClosePeriodTask.ScheduleForTask.BeginDate
 				ScheduledDueDate					value is ClosePeriodTask.ScheduleForTask.DueDate
 				ClosePeriod							value is ClosePeriodTask.ClosePeriod
 				TaskOwner							value is ClosePeriodTask.TaskOwner.TeamMember.PreferredSimplePresentationName
 		AutoUpdateAnalyticCubeFile				is Boolean
 		AutoRefreshReloadAnalyticCube			is Boolean	

		RejectReconReasonCodeRequired			is Boolean
		ReopenReconReasonCodeRequired			is Boolean

		JournalEntryApprovalRequired			is Boolean
		RejectJournalEntryReasonCodeRequired	is Boolean
		ReopenJournalReasonCodeRequired			is Boolean
		HasReconciliationsForAccountingUnits	is Boolean


    Derived Fields
		BlankField is a DerivedField
			type is Alpha 1
			restricted
			return blank

		DerivedTaskOwner is a DerivedField
			type is Numeric 1
			return 0
			
   	Field Rules
   		CloseManagementGroup
   			initial value is actor.context.FinanceEnterpriseGroup
   			
   		Description
   			required
			default to CloseManagementGroup.FinanceEnterpriseGroup.Description			

		CorporateCalendar
			if (CorporatePeriodGroup entered)
				required
					"Corporate_CalendarRequiredWhen_Corporate_Period_GroupEntered"

		RequireApprovalOfOneTimeTasks
			constraint (AllowOneTimeTasks)
				"CannotSetRequireApprovalForOneTimeTasksWhenNotAllowingOneTimeTasks"

		OneTimeTaskDefaultApprovalCode
			constraint (AllowOneTimeTasks)
				"CannotHaveDefaultApprovalCodeForOneTimeTasksWhenNotAllowingOneTimeTasks"
			constraint (RequireApprovalOfOneTimeTasks)
				"CannotHaveDefaultApprovalCodeForOneTimeTasksWhenNotRequiringApproval"

		AllowResourceToReopenCompletedTasks
			constraint (AllowResourceToViewCompletedTasks)
				"MustAllowCloseResourceToViewCompletedTasksInOrderToReopenThem"

		ExternalFromEmailAddress
			if (AllowExternalEmails)
				required
					"Send_FromRequiredWhenAllowingExternalEmails"

	Context Fields
		EnterpriseGroup
		ClosePeriodTask

    Conditions
 		CloseManagementGroupExists
 			restricted
 			when (CloseManagementGroup exists)
 			
		HasTaskTypes
			restricted
			when (CloseTaskTypeRel exists)

        HasMultipleClosePeriodsOpen
			restricted
            when (instance count of OpenPeriodsRel > 1)
            
        CubeOfflineMode 
			restricted
			when (ClosePeriodTaskAnalyticCubeRel.Mode != "0" )
			
		CubeOnlineMode
			restricted
			when (ClosePeriodTaskAnalyticCubeRel.Mode = "0")

	Relations
		FinanceTeamRel is a FinanceTeam set

		FinanceResourceRel
			one-to-many relation to FinanceResource
			Field Mapping uses symbolic key
				related.HROrganization	= CloseManagementGroup.HROrganization

		CompanyRel 
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key

		FinanceTeamMemberRel 
			one-to-many relation to FinanceTeamMember
    		Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = CloseManagementGroup
			Instance Selection
				where (related.ActiveResource)	

		CalendarPeriodGroupRel
			one-to-many relation to SystemCalendarPeriodGroup
			Field Mapping uses symbolic key
					related.EnterpriseGroup = CloseManagementGroup
		
		ClosePeriodTaskAnalyticCubeRel
			one-to-one relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
				related.BusinessClass = "ClosePeriodTaskCube"

		CloseTaskTypeRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = CloseManagementGroup

		ManagerMyOpenPeriodProcessesAllRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByPeriodAndOwner
				related.CloseManagementGroup	= CloseManagementGroup
				related.ClosePeriod				= OpenPeriodsRel.ClosePeriod
				related.TaskOwner				= actor.agent(FinanceResource).FinanceResource
			Instance Selection
				where (related.TaskLevel.Summary
				and   !related.IsTopLevel)

		OpenPeriodsRel
			one-to-many relation to ClosePeriod 
			Field Mapping uses ByOpenPeriodDescend
				related.CloseManagementGroup	= CloseManagementGroup
			Instance Selection
				where (related.CloseManagementStatus.Open)

		ManagerMyOpenPeriodProcessesOpenRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByPeriodAndOwner
				related.CloseManagementGroup	= CloseManagementGroup
				related.ClosePeriod				= OpenPeriodsRel.ClosePeriod
				related.TaskOwner				= actor.agent(FinanceResource).FinanceResource
			Instance Selection
				where (related.TaskLevel.Summary
				and   !related.IsTopLevel
				and   (related.TaskStatus.Scheduled
				or     related.TaskStatus.InProcess
				or     related.TaskStatus.PendingApproval))

		ManagerMyOpenPeriodProcessesClosedRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByPeriodAndOwner
				related.CloseManagementGroup	= CloseManagementGroup
				related.ClosePeriod				= OpenPeriodsRel.ClosePeriod
				related.TaskOwner				= actor.agent(FinanceResource).FinanceResource
			Instance Selection
				where (related.TaskLevel.Summary
				and   !related.IsTopLevel
				and   (related.TaskStatus.Closed
				or     related.TaskStatus.Voided))

		ManagerMyTeamOpenPeriodProcessesAllRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByPeriodAndOwner
				related.CloseManagementGroup	= CloseManagementGroup
				related.ClosePeriod				= OpenPeriodsRel.ClosePeriod
				related.TaskOwner				= DerivedTaskOwner 
			Instance Selection
				where (related.TaskLevel.Summary
				and   !related.IsTopLevel
				and    related.TaskForMyTeam)

		ManagerMyTeamOpenPeriodProcessesOpenRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByPeriodAndOwner
				related.CloseManagementGroup	= CloseManagementGroup
				related.ClosePeriod				= OpenPeriodsRel.ClosePeriod
				related.TaskOwner				= DerivedTaskOwner 
			Instance Selection
				where (related.TaskLevel.Summary
				and   !related.IsTopLevel
				and    related.TaskForMyTeam
				and   (related.TaskStatus.Scheduled
				or     related.TaskStatus.InProcess
				or     related.TaskStatus.PendingApproval))

		ManagerMyTeamOpenPeriodProcessesClosedRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByPeriodAndOwner
				related.CloseManagementGroup	= CloseManagementGroup
				related.ClosePeriod				= OpenPeriodsRel.ClosePeriod
				related.TaskOwner				= DerivedTaskOwner 
			Instance Selection
				where (related.TaskLevel.Summary
				and   !related.IsTopLevel
				and    related.TaskForMyTeam
				and   (related.TaskStatus.Closed
				or     related.TaskStatus.Voided))

	Actions	
		Create is a Create Action

		Update is an Update Action

		Delete is a Purge Action

		UpdateClosePeriodTaskCube is a Set Action
			restricted
			completion message is "UpdateAnalyticCubeSubmitted"
			Parameters
				PrmRefreshReload	is a CubeUpdateOption
			Parameter Rules
				PrmRefreshReload
					initial value is 1 
					required
					
			Action Rules
				Set Rules
					Exit Rules
						if (PrmRefreshReload.Refresh)
							invoke Refresh ClosePeriodTaskAnalyticCubeRel in background
						else
							invoke Reload ClosePeriodTaskAnalyticCubeRel in background






















