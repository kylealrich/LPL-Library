PeriodEndDistribution is a BusinessClass
	owned by reconciliation
	prefix is PERD

	Ontology
		symbolic key is PeriodEndDistribution

	Persistent Fields								
		ReconciliationCodeBlock		is a snapshot of PeriodEndReconciliation.ReconciliationCodeBlock
		DistributionAccount			is a FinanceCodeBlock
			default label is "AccountInformation"
		DistributionAmount			is a FinanceCurrencyAmountGroup
		Status						is Numeric 1
    		States
    			Entered			value is 0
    			Released		value is 1
    			Posted			value is 2
    			PendingApproval	value is 3    
		Description
    	Comment						is Text
		RelatedDistribution			is a PeriodEndDistribution
			delete ignored
		OffsetDistribution			is Boolean
		ReversedDistribution		is Boolean 
		CreatedFromAmortization		is Boolean
		ReconciliationDetail
			context of PeriodEndReconciliation.ReconciliationAccount
		ReconciliationPeriodDetail
			context of PeriodEndReconciliation.ReconciliationAccount
		TransactionReconDetail
			context of PeriodEndReconciliation.ReconciliationAccount
			delete ignored
    	ReleasedTimestamp			is TimeStamp
		PostedTimestamp			    is TimeStamp
		PendingApprovalTimestamp	is TimeStamp
		OriginatingTransaction		is BusinessObjectReference

	Transient Fields
		GLFromEntity			 	is an AccountingEntity
    		derive value from PeriodEndReconciliation.ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity
		GLFinanceCodeBlock		 	is a TransactionCodeBlock				 		
			derive value from GLTransactionDetailRel.FinanceCodeBlock
			default label is "DistributionAccount"
		GLCurrencyCode				is a FromCurrency
			derive value from PeriodEndReconciliation.ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity.FunctionalCurrency
		GLTransactionAmount		 	is a CurrencyAmount			
			derive value from GLTransactionDetailRel.TransactionAmount	
			default label is "DistributionAmount"
			precision is PeriodEndReconciliation.ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity.FunctionalCurrency.NumberOfDecimals
        GLFinanceCurrencyAmount		is a FinanceCurrencyAmount
        	derive value from GLTransactionDetailRel.ReportCurrencyAmount
		GLJournalizeGroup 			is like JournalizeGroup
			derive value from GLTransactionDetailRel.JournalizeGroup
		TransientReferenceToThisInstance is BusinessObjectReference
			derive value from reference to this instance
		TransactionDate				is an ExchangeDate
			derive value from PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date
		PostingDate
			derive value from PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date
			
	Local Fields
		BypassNegativeRateEdit
		BypassUnitAndAmountEdit

	Context Fields
		ContextReconciliationStructure	is a ReconciliationAccountStructure
		ContextPeriodEndReconciliation	is a PeriodEndReconciliation
		
	Derived Fields
		DerivedCommentLinkName is a DerivedField
			type is Alpha 12
			if (Comment entered)
				return ViewCommentMessage
			else
				return AddCommentMessage

		AddCommentMessage is a MessageField
			"AddComment"		

		ViewCommentMessage is a MessageField
			"ViewComment"	

		PeriodEndDistributionPostingMessage is a MessageField 
			restricted
			"PeriodEndReconciliationDistributionPosting"
		
		PeriodDistributions is a MessageField
			restricted
			"PeriodDistribution"
			
	Field Rules
		ReconciliationCodeBlock
			force default to PeriodEndReconciliation.ReconciliationCodeBlock
			
		DistributionAccount
			force default to GLTransactionDetailRel.FinanceCodeBlock
		
		DistributionAmount
			DistributionAmount.ExchangeDate = TransactionDate
        	DistributionAmount.CurrencyAmount = GLTransactionAmount
			BypassNegativeRateEdit = true
			
		Description
			required
		
	Relations
		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 

				related.OriginatingTransaction	= reference to this instance			

		RelatedAmortizationDistributionsRel
			one-to-many relation to PeriodEndDistribution
			Field Mapping uses ByReconciliationAccount
				related.ReconciliationManagementGroup   						= ReconciliationManagementGroup
				related.ReconciliationAccountStructure  						= ReconciliationAccountStructure
				related.PeriodEndReconciliation.ReconciliationAccount	  	    = PeriodEndReconciliation.ReconciliationAccount
				related.ReconciliationDetail		    						= ReconciliationDetail
				related.PeriodEndReconciliation.GeneralLedgerCloseConfiguration	= PeriodEndReconciliation.GeneralLedgerCloseConfiguration
				related.PeriodEndReconciliation.GeneralLedgerClosePeriod	    = PeriodEndReconciliation.GeneralLedgerClosePeriod
				related.PeriodEndReconciliation.SequenceNumber		    		= PeriodEndReconciliation.SequenceNumber
			Instance Selection
				where (related.ReconciliationPeriodDetail = ReconciliationPeriodDetail
				and   !related.OffsetDistribution)

		ClosedPeriodRel
			one-to-one relation to EntityLedgerPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup   = ReconciliationManagementGroup
				related.AccountingEntity		 = DistributionAccount.ToAccountingEntity
				related.GeneralLedgerClosePeriod = PeriodEndReconciliation.GeneralLedgerClosePeriod
				related.Ledger                   = DistributionAccount.Ledger

		GeneralLedgerTransactionRel	
			one-to-one relation to GeneralLedgerTransaction
			Field Mapping uses symbolic key		 
				related.FinanceEnterpriseGroup									= OriginatingTransaction(GeneralLedgerTransaction).GeneralLedgerTransaction.FinanceEnterpriseGroup
				related.AccountingEntity										= OriginatingTransaction(GeneralLedgerTransaction).GeneralLedgerTransaction.AccountingEntity
				related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod 	= OriginatingTransaction(GeneralLedgerTransaction).GeneralLedgerTransaction.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				related.GeneralLedgerJournalControl								= OriginatingTransaction(GeneralLedgerTransaction).GeneralLedgerTransaction.GeneralLedgerJournalControl
				related.GeneralLedgerTransaction								= OriginatingTransaction(GeneralLedgerTransaction).GeneralLedgerTransaction.GeneralLedgerTransaction

	Sets
		ByDistributionStatus
			Sort Order
				ReconciliationManagementGroup
				Status
				PeriodEndReconciliation.GeneralLedgerClosePeriod
				ReconciliationAccountStructure
				PeriodEndReconciliation.ReconciliationAccount
				PeriodEndReconciliation.SequenceNumber
				PeriodEndReconciliation.GeneralLedgerCloseConfiguration
				PeriodEndDistribution
				
		ByReconciliationAccount
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				PeriodEndReconciliation.ReconciliationAccount
				ReconciliationDetail
				PeriodEndReconciliation.GeneralLedgerCloseConfiguration
				PeriodEndReconciliation.GeneralLedgerClosePeriod
				PeriodEndReconciliation.SequenceNumber
				PeriodEndDistribution
	
		ByPeriodEndDistribtion
			Sort Order
				ReconciliationManagementGroup
				ReconciliationAccountStructure
				PeriodEndReconciliation.ReconciliationAccount
				PeriodEndReconciliation.GeneralLedgerCloseConfiguration
				PeriodEndReconciliation.GeneralLedgerClosePeriod
				PeriodEndReconciliation.SequenceNumber
				PeriodEndDistribution
				
	Conditions
		PeriodEndReconciliationCanBeUpdated
			when (PeriodEndReconciliation.ReconciliationCanBeUpdated)		

		DistributionCanBeCreated
			when (PeriodEndReconciliationCanBeUpdated
			and   !PeriodEndReconciliation.ReconciliationCodeBlock.GeneralLedgerChartAccount.SystemAccount.UndistributedRetainedEarnings)

		DistributionCanBeUpdated
			when (!CreatedFromAmortization
			and	  Status.Entered
			and   PeriodEndReconciliationCanBeUpdated)

		CatchUpPeriodPosting
			when (PeriodEndReconciliation.GeneralLedgerClosePeriod = ReconciliationDetail.CatchUpPeriod)
			
		CanViewCommentLink
			when (DistributionExists
			and  (Status.Entered
			or    Comment entered))
			
		DistributionExists
			when (PeriodEndDistribution exists)
			
		IsPeriodClosed
			when (!Status.Posted   
			and ClosedPeriodRel.PeriodStatus > 1 )   

		GeneralLedgerTransactionExists
			when (GeneralLedgerTransactionRel exists)

	Rule Blocks
		CreateGLTransactionDetail
			invoke Create GLTransactionDetail		 
				fill in fields from this instance
				invoked.OriginatingTransaction 				= reference to this instance
				invoked.FinanceEnterpriseGroup				= ReconciliationManagementGroup  
				invoked.CurrencyCode						= GLCurrencyCode
				invoked.System								= "RM"
				invoked.TransactionAmount					= GLTransactionAmount
				invoked.FinanceCodeBlock					= GLFinanceCodeBlock
				invoked.ReportCurrencyAmount 				= GLFinanceCurrencyAmount
				invoked.Description			  				= Description		
            	invoked.GeneralLedgerEvent 	    			= "AK"
				invoked.AccountingEntity					= PeriodEndReconciliation.ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity
				invoked.TransactionDate						= TransactionDate
				invoked.PostingDate							= PostingDate
				invoked.ControlDocumentNumber 				= PeriodEndReconciliation.ReconciliationAccount.ReconciliationAccountControl
				invoked.DocumentNumber		  				= PeriodEndReconciliation.ReconciliationAccount.ReconciliationAccountControl
				invoked.AutoReverse 						= false
				invoked.ReportCurrencyAmount.KeepRateOnly 	= true

		UpdateGLTransactionDetail
			invoke Update GLTransactionDetailRel
				invoked.TransactionAmount					= GLTransactionAmount
				invoked.FinanceCodeBlock					= GLFinanceCodeBlock
				invoked.ReportCurrencyAmount 				= GLFinanceCurrencyAmount
				invoked.Description			  				= Description
				invoked.TransactionDate						= TransactionDate
				invoked.PostingDate							= PostingDate
				invoked.ReportCurrencyAmount.KeepRateOnly	= true
				
  	Actions
		FastUpdate is an Update Action
			restricted
			bypass field rules
		
		ReleaseAllDistributions is a Set Action
			restricted
			Parameters
				PrmReconciliationManagementGroup	is a ReconciliationManagementGroup
				PrmReconciliationAccountStructure	is a ReconciliationAccountStructure
				PrmReconciliationAccount			is a ReconciliationAccount
				PrmGeneralLedgerCloseConfiguration	is a GeneralLedgerCloseConfiguration
				PrmGeneralLedgerClosePeriod			is a GeneralLedgerClosePeriod
					
			Parameter Rules
				PrmReconciliationManagementGroup	required
				PrmReconciliationAccountStructure	required
				PrmReconciliationAccount			required
				PrmGeneralLedgerCloseConfiguration	required
				PrmGeneralLedgerClosePeriod			required
				
			Instance Selection
				where (ReconciliationManagementGroup = PrmReconciliationManagementGroup
				and	   ReconciliationAccountStructure = PrmReconciliationAccountStructure
				and	   PeriodEndReconciliation.ReconciliationAccount = PrmReconciliationAccount
				and	   PeriodEndReconciliation.GeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration
				and	   PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = PrmGeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				and    Status.Entered)
			
			Action Rules
				Instance Rules
					invoke ReleaseDistribution Entered this instance
					
		RejectAllDistributions is a Set Action
			restricted
			Parameters
				PrmReconciliationManagementGroup	is a ReconciliationManagementGroup
				PrmReconciliationAccountStructure	is a ReconciliationAccountStructure
				PrmReconciliationAccount			is a ReconciliationAccount
				PrmGeneralLedgerCloseConfiguration	is a GeneralLedgerCloseConfiguration
				PrmGeneralLedgerClosePeriod			is a GeneralLedgerClosePeriod
				PrmPeriodEndReconciliation			is a SequenceNumber
					
			Parameter Rules
				PrmReconciliationManagementGroup	required
				PrmReconciliationAccountStructure	required
				PrmReconciliationAccount			required
				PrmGeneralLedgerCloseConfiguration	required
				PrmGeneralLedgerClosePeriod			required

				
			Instance Selection
				where (ReconciliationManagementGroup = PrmReconciliationManagementGroup
				and	   ReconciliationAccountStructure = PrmReconciliationAccountStructure
				and	   PeriodEndReconciliation.ReconciliationAccount = PrmReconciliationAccount
				and    PeriodEndReconciliation.GeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration
				and	   PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = PrmGeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				and    PeriodEndReconciliation.SequenceNumber = PrmPeriodEndReconciliation
				and    Status.PendingApproval)
			
			Action Rules
				Instance Rules
					invoke RejectDistributionRelease PendingApproval PeriodEndDistribution

		ApproveAllDistributions is a Set Action
			restricted
			Parameters
				PrmReconciliationManagementGroup	is a ReconciliationManagementGroup
				PrmReconciliationAccountStructure	is a ReconciliationAccountStructure
				PrmReconciliationAccount			is a ReconciliationAccount
				PrmGeneralLedgerCloseConfiguration	is a GeneralLedgerCloseConfiguration
				PrmGeneralLedgerClosePeriod			is a GeneralLedgerClosePeriod
				PrmPeriodEndReconciliation			is a SequenceNumber
					
			Parameter Rules
				PrmReconciliationManagementGroup	required
				PrmReconciliationAccountStructure	required
				PrmReconciliationAccount			required
				PrmGeneralLedgerCloseConfiguration	required
				PrmGeneralLedgerClosePeriod			required

				
			Instance Selection
				where (ReconciliationManagementGroup = PrmReconciliationManagementGroup
				and	   ReconciliationAccountStructure = PrmReconciliationAccountStructure
				and	   PeriodEndReconciliation.ReconciliationAccount = PrmReconciliationAccount
				and    PeriodEndReconciliation.GeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration
				and	   PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = PrmGeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				and    PeriodEndReconciliation.SequenceNumber = PrmPeriodEndReconciliation
				and    Status.PendingApproval)
			
			Action Rules
				Instance Rules
					invoke ReleaseDistribution PendingApproval PeriodEndDistribution
					
		Purge is a Purge Action
			restricted
			Entrance Rules
				if (GLTransactionDetailRel exist) 
					invoke Delete GLTransactionDetailRel					

		JournalizeDistributions is a Set Action
			completion message is "<CompletionMessage>"
			Parameters
				PrmReconciliationManagementGroup	is a ReconciliationManagementGroup
				PrmReconciliationAccountStructure	is a ReconciliationAccountStructure
				PrmReconciliationAccount			is a ReconciliationAccount
				PrmPeriodEndReconciliation			is a SequenceNumber
				PrmGeneralLedgerCloseConfiguration	is a GeneralLedgerCloseConfiguration
				PrmGeneralLedgerClosePeriod			is a GeneralLedgerClosePeriod
				PrmPostThruPeriod					is Date
					default label is "PostThruDate"
				PrmDescription						is a Description
					default label is "Description"

			Parameter Rules
				PrmReconciliationManagementGroup
					required
				PrmPostThruPeriod		 
					required
				PrmDescription
					initial value is PeriodEndDistributionPostingMessage
					default to PeriodEndDistributionPostingMessage
					
			Local Fields
				CompletionMessage			is Alpha 150
				LocalJournalizeGroup		is a JournalizeGroup
				LocalSystemCode            	is a GeneralLedgerSystemCode
				
			Instance Selection
				where (ReconciliationManagementGroup = PrmReconciliationManagementGroup
				and    Status.Released
				and  ((!PrmGeneralLedgerClosePeriod entered
				and 	PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date <= PrmPostThruPeriod)
				or	  (PrmGeneralLedgerClosePeriod entered
				and    PeriodEndReconciliation.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod = PrmGeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod))
				and   (!PrmReconciliationAccountStructure entered
				or	  (PrmReconciliationAccountStructure entered
				and    ReconciliationAccountStructure = PrmReconciliationAccountStructure))
				and   (!PrmReconciliationAccount entered
				or	  (PrmReconciliationAccount entered
				and    PeriodEndReconciliation.ReconciliationAccount = PrmReconciliationAccount))
				and   (!PrmPeriodEndReconciliation entered
				or	  (PrmPeriodEndReconciliation entered
				and    PeriodEndReconciliation.SequenceNumber = PrmPeriodEndReconciliation))
				and   (!PrmGeneralLedgerCloseConfiguration entered
				or	  (PrmGeneralLedgerCloseConfiguration entered
				and    PeriodEndReconciliation.GeneralLedgerCloseConfiguration = PrmGeneralLedgerCloseConfiguration)))

			Sort Order  
				ReconciliationManagementGroup
				Status
				PeriodEndReconciliation.GeneralLedgerClosePeriod
				ReconciliationAccountStructure
				PeriodEndReconciliation.ReconciliationAccount
				PeriodEndReconciliation.SequenceNumber
				PeriodEndReconciliation.GeneralLedgerCloseConfiguration
				PeriodEndDistribution
				
			Action Rules								
				Empty Set Rules
					CompletionMessage = "PeriodEndDistributionPostingCompleteForJournalizeGroup...:NoRecordsFoundToJournalize<LocalJournalizeGroup>"

				Set Rules
					Entrance Rules
						LocalSystemCode = "RM" 
    					increment LocalSystemCode.LastJournalizeGroup
            			LocalJournalizeGroup = LocalSystemCode.DerivedJournalizeGroup
						
					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmReconciliationManagementGroup.FinanceEnterpriseGroup in background
							invoked.PrmJournalizeGroup = LocalJournalizeGroup
							invoked.PrmJournalizeGroupDescription = PrmDescription

						CompletionMessage = "PeriodEndDistributionPostingCompleteForJournalizeGroup...:<LocalJournalizeGroup>"
						
				Instance Rules
					if (GLTransactionDetailRel exist)
						invoke UpdateJournalizeGroup GLTransactionDetailRel	 
							invoked.PrmJournalizeGroup = LocalJournalizeGroup  
  						make transition to Posted

		CreateWithAllocationCode is a Set Action
			valid when (PeriodEndReconciliationCanBeUpdated)
			run in foreground
			completion message is "DistributionsCreated"
			no records message is "DistributionsCreated"

			Parameters
				PrmReconciliationManagementGroup	is a ReconciliationManagementGroup
				PrmAllocationCode					is a CashAllocationCode
					default label is "AllocationCode"
				PrmAllocationAmount					is an InternationalAmount
					default label is "Amount"
				PrmDescription						is a Description
					default label is "Description"

			Parameter Rules
				PrmReconciliationManagementGroup
					required
					
				PrmAllocationCode
					required
					
				PrmAllocationAmount
					if(PrmAllocationCode.AllocationType.Percent)
						required			

			Local Fields
				LocalTransactionAmount				is an InternationalAmount
				DistributionRunningTotal			is an InternationalAmount
				LastPeriodEndDistribution			is a PeriodEndDistribution view

			Instance Selection
				where (false)
				
			Action Rules
				Empty Set Rules
					initialize DistributionRunningTotal
					LocalTransactionAmount = PrmAllocationAmount
					for each PrmAllocationCode.CashAllocationCodeDetail set
						invoke Create PeriodEndDistribution
							assign result to LastPeriodEndDistribution
							invoked.ReconciliationManagementGroup							= PrmReconciliationManagementGroup
							invoked.ReconciliationAccountStructure							= ContextReconciliationStructure
							invoked.PeriodEndReconciliation.ReconciliationAccount			= ContextPeriodEndReconciliation.ReconciliationAccount	
							invoked.PeriodEndReconciliation.GeneralLedgerCloseConfiguration = ContextPeriodEndReconciliation.GeneralLedgerCloseConfiguration
							invoked.PeriodEndReconciliation.GeneralLedgerClosePeriod		= ContextPeriodEndReconciliation.GeneralLedgerClosePeriod
							invoked.PeriodEndReconciliation.SequenceNumber					= ContextPeriodEndReconciliation.SequenceNumber
							invoked.ReconciliationCodeBlock									= each.AllocationAccount
							invoked.GLFinanceCodeBlock					  					= each.AllocationAccount
							if (PrmAllocationCode.AllocationType.Amount)
								invoked.GLTransactionAmount									= each.AllocationAmount
								invoked.DistributionAmount.CurrencyAmount					= each.AllocationAmount
							if (PrmAllocationCode.AllocationType.Percent)
								invoked.GLTransactionAmount									= (LocalTransactionAmount * each.AllocationPercent)
								invoked.DistributionAmount.CurrencyAmount					= (LocalTransactionAmount * each.AllocationPercent)
							if (PrmAllocationCode.AllocationType.AccountOnly)
								invoked.BypassUnitAndAmountEdit 							=  true
							if (PrmDescription entered)
								invoked.Description							  				= PrmDescription
							else
								invoked.Description							  				= PeriodDistributions
							invoked.Comment													= each.Comment
						
						DistributionRunningTotal += (LocalTransactionAmount * each.AllocationPercent)

					if ((LocalTransactionAmount - DistributionRunningTotal) != 0)
						invoke Update LastPeriodEndDistribution.PeriodEndDistribution
							invoked.DistributionAmount.CurrencyAmount += (LocalTransactionAmount - DistributionRunningTotal)

					if (LastPeriodEndDistribution.PeriodEndReconciliation.Status.Open)
						invoke TransitionToInProcessStatus LastPeriodEndDistribution.PeriodEndReconciliation

  	StateCycles
		StatementDistributionLifeCycle is a StateCycle
			state field is Status
		
			Entered is a State
		  		Create is a Create Action
		  			valid when (DistributionCanBeCreated)
		  			Action Rules
		  				constraint (!PeriodEndReconciliation.ReconciliationCodeBlock.GeneralLedgerChartAccount.SystemAccount.UndistributedRetainedEarnings)
							"CannotCreateDistribution;OffsetCannotPostTo<PeriodEndReconciliation.ReconciliationCodeBlock.GeneralLedgerChartAccount.SystemAccount>_<ReconciliationManagementGroup.FinanceEnterpriseGroup.AccountLabel>"

						include CreateGLTransactionDetail
		  				
		  			Exit Rules
						if (PeriodEndReconciliation.Status.Open)
							invoke TransitionToInProcessStatus PeriodEndReconciliation
		
		  		Update is an Update Action
		  			valid when (DistributionCanBeUpdated)
		  			Action Rules
						constraint (!TransactionReconDetail entered)
							"DistributionsCreatedFromADetailTransactionReconciliationCannotBeChanged"
							
						include UpdateGLTransactionDetail
				
				UpdateFromDetailReconciliation is an Update Action
		  			restricted
		  			bypass field rules
		  			Action Rules
						include UpdateGLTransactionDetail
								
		  		Delete is a Delete Action
		  			valid when (DistributionCanBeUpdated)
		  			Action Rules
	  					constraint (!TransactionReconDetail entered)
							"DistributionsMustBeMaintainedFromTheDetailTransactionReconciliationQueue"
					
		  			Entrance Rules
						if (GLTransactionDetailRel exist) 
							invoke Delete GLTransactionDetailRel
				
				DeleteFromDetailReconciliation is a Delete Action
		  			restricted
		  			Entrance Rules
						if (GLTransactionDetailRel exist) 
							invoke Delete GLTransactionDetailRel
							
		  		DeleteAmortizationDistribution is a Delete Action
		  			restricted
		  			Entrance Rules
						if (GLTransactionDetailRel exist) 
							invoke Delete GLTransactionDetailRel
							
		  		ReleaseDistribution is an Instance Action
		  			restricted
		  			Action Rules
						if (GLTransactionDetailRel exist)
							invoke Release GLTransactionDetailRel
		  				make transition to Released
		  		
		  		SetToPendingApproval is an Instance Action
		  			restricted
		  			Action Rules
		  				make transition to PendingApproval
		  	
		  	PendingApproval is a State
				on entrance to PendingApproval
					Action Rules
						PendingApprovalTimestamp = current timestamp

		  		ReleaseDistribution is an Instance Action
		  			restricted
		  			Action Rules
						if (GLTransactionDetailRel exist)
							invoke Release GLTransactionDetailRel
		  				make transition to Released

		  		RejectDistributionRelease is an Instance Action
		  			restricted
		  			Action Rules
						make transition to Entered
		  				initialize PendingApprovalTimestamp

		  	Released is a State
				on entrance to Released
					Action Rules
						ReleasedTimestamp = current timestamp

		  		PostDistribution is an Instance Action
		  			restricted
		  			Action Rules
		  				make transition to Posted

				Delete is a Delete Action
					restricted
					Entrance Rules
						if (GLTransactionDetailRel exist) 
							invoke Delete GLTransactionDetailRel
													
		  	Posted is a State
				on entrance to Posted
					Action Rules
						PostedTimestamp = current timestamp
