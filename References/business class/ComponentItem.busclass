ComponentItem is a BusinessClass
    owned by ic
    prefix is CI

    Ontology
        symbolic key is ComponentItem

    Patterns
        implements StaticJava
        implements DynamicCreation

    Persistent Fields
        Item
        ComponentUOM            is a UnitOfMeasure
            default label is "UOM"
        Manufacturer
        ManufacturerNumber
        OpenQuantity            is an UnsignedQuantity
        HoldQuantity            is an UnsignedQuantity

    Transient Fields
        TransientTotalQuantity      is an UnsignedQuantity
            derive value from DerivedTotalQuantity
            default label is "TotalQuantity"
            
    Derived Fields

        ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforComponentItem"

        ContextMessageText is a MessageField
            restricted
            "ComponentItem<ComponentItem><Item>"

        DerivedOpenQuantity     is a DerivedField
            type is like UnsignedQuantity
            if (action type.Update)
                return 0
            else
                return OpenQuantity

        DerivedHoldQuantity     is a DerivedField
            type is like UnsignedQuantity
            if (action type.Update)
                return 0
            else
                return HoldQuantity

        DerivedTotalQuantity    is a DerivedField
            type is like UnsignedQuantity
            if (action type.Update)
                return 0
            else
                return (OpenQuantity + HoldQuantity) 

        ItemNotEnteredMessage is a MessageField
            "ItemIsRequired"

        ItemDoesNotExistMessage is a MessageField
            "ItemDoesNotExist"

        ItemIsNotAnItemMasterItemMessage is a MessageField
            "ItemIsNotAnItemMasterItem"

        NoClinicalSystemUseSetupMessage is a MessageField
            "ItemIsNotSetAsClinicalSystemUse"

        InactiveItemMessage is a MessageField
            "ItemIsInactive"

        DiscontinuedItemMessage is a MessageField
            "ItemIsDiscontinued"

        InactiveAndNotClinicalSystemUseMessage is a MessageField
            "ItemIsInactiveAndIsNotSetAsClinicalSystemUse"

        DiscontinuedAndNotClinicalSystemUseMessage is a MessageField
            "ItemIsDiscontinuedAndIsNotSetAsClinicalSystemUse"

        ItemAlertDisplayMessage is a DerivedField
            type is MessageField
            if (Item not entered)
                return ItemNotEnteredMessage
            else
                if (Item not exists)
                    return ItemIsNotAnItemMasterItemMessage
                else 









                if (not Item.Active)
                    return InactiveItemMessage
                else 
                if (Item.Discontinued)
                    return DiscontinuedItemMessage

        DefaultFirstAndLastName			is a DerivedField
            type is MessageField
            return first PreferenceCard.PreferenceCardProviderRel.Provider.DisplayProviderFullName

        DefaultProvider			is a DerivedField
            type is like Provider
            return first PreferenceCard.PreferenceCardProviderRel.Provider
        
        DefaultClinicalProcedureName			is a DerivedField
            type is Alpha size 100
            return first PreferenceCard.PreferenceCardProcedureRel.PreferenceCardProcedure.Procedure.ClinicalProcedure.ClinicalProcedureName

    Relations
        ComponentItemErrorRel
            one-to-many relation to PreferenceCardError
            Field Mapping uses symbolic key
                related.ItemGroup                   = ItemGroup
                related.PreferenceCard              = PreferenceCard
            Instance Selection
                where (related.ErrorRecordType.ComponentItem
                and    related.ErrorRecordNumber    = ComponentItem)

        SameComponentItemRel
            one-to-many relation to ComponentItem
            Field Mapping uses ByComponentItem
                related.ItemGroup                   = ItemGroup
                related.Item                        = Item
                related.PreferenceCard              = PreferenceCard
            Instance Selection
                where (related.ComponentItem        != ComponentItem)

        ValidItemUOMRel
            one-to-many relation to ItemUOM
            Field Mapping uses symbolic key
                related.ItemGroup           = ItemGroup
                related.Item                = Item
                related.UnitOfMeasure       = ComponentUOM
            Instance Selection
                where (related.OKForTransaction)

    Conditions
        RecordExists
            restricted
            when (ComponentItem exists)

        ValidRecordExists
            restricted
            when (RecordExists
            and   Item entered 
            and   Item exists)

        InvalidComponentItem
            restricted
            when (Item not entered
            or    Item not exists

            or    not Item.Active
            or    Item.Discontinued)

        DisplayAlert
            restricted
            when (RecordExists
            and   InvalidComponentItem)

    Sets
        ByComponentItem
            duplicates
            Sort Order
                ItemGroup
                Item
                PreferenceCard

    Field Rules
        ComponentUOM
            default to Item.StockUOM

        Manufacturer
            force default to Item.Manufacturer

        ManufacturerNumber
            force default to Item.ManufacturerNumber

    Rule Blocks
        CreateOrUpdateEdits
            invoke Purge ComponentItemErrorRel


            if (Item not entered)
                invoke Create PreferenceCardError
                    fill in fields from this instance
                    invoked.ErrorRecordType                 = 1 
                    invoked.ErrorMessageNumber              = 1
                    invoked.ErrorPreferenceCard             = PreferenceCard
                    invoked.ErrorRecordNumber               = ComponentItem
            else 
                if (Item exists)
                    if (SameComponentItemRel exists)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType             = 1 
                            invoked.ErrorMessageNumber          = 3
                            invoked.ErrorPreferenceCard         = PreferenceCard
                            invoked.ErrorRecordNumber           = ComponentItem
                            invoked.ErrorItem                   = Item


                    if (not Item.Active)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType         = 1 
                            invoked.ErrorMessageNumber      = 4
                            invoked.ErrorPreferenceCard     = PreferenceCard
                            invoked.ErrorRecordNumber       = ComponentItem


                    if (ValidItemUOMRel not exists)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType         = 1 
                            invoked.ErrorMessageNumber      = 5
                            invoked.ErrorPreferenceCard     = PreferenceCard
                            invoked.ErrorRecordNumber       = ComponentItem
                            invoked.ErrorItem               = Item
                            invoked.ErrorUOM                = ComponentUOM











    Actions
        Create is a Create Action
            Exit Rules
                include CreateOrUpdateEdits

        Update is an Update Action
            Entrance Rules
                include CreateOrUpdateEdits

        Delete is a Delete Action
            Action Rules
                invoke Purge ComponentItemErrorRel

        ReviewErrors is an Instance Action
            restricted
            Action Rules
                include CreateOrUpdateEdits
