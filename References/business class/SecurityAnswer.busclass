SecurityAnswer is a BusinessClass
    owned by security
    prefix is SECA
    stored in environment
	contains environment data
	disable data area copy
		preserve target data

    Ontology
        part of Identity
            relative key is SecurityQuestion

    Patterns
        implements TemplateDriven by SecurityQuestion
            create instance
                when (AnswerText entered)
        disable AuditIndex

    Persistent Fields
        AnswerText     is Alpha size 100


    Transient Fields
        ValidateAnswer is Alpha size 100
        
    Sets
    	BySvcSecAns
    		indexed
    		duplicates
    		Sort Order
    			Service
    			SecurityQuestion
    			
    Relations
        IdentityActorRel
            one-to-one relation to IdentityActor
            Field Mapping uses symbolic key
                related.Service  = Service
                related.Identity = Identity
                related.Actor    = actor
                
        IdentityRel
   			one-to-one relation to Identity
    		Field Mapping uses symbolic key
        		related.Service  = Service
        		related.Identity = Identity
        			
    			
    Derived Fields
    	NoSecurityAnswersMF is a MessageField
     		"No_Security_Answers"
        
    	MaskedAnswerText is a DerivedField
			type is Alpha size 9
			default label is untranslatable
			if (AnswerText != blank)
				return (AnswerText[0:3] + "******")
        
