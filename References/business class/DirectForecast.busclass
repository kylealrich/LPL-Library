DirectForecast is a BusinessClass
	owned by cashmgmt
	prefix is CMDF	

	Ontology
		symbolic key is DirectForecast
		
	Persistent Fields								
		Description				is Alpha 250
		GeneralLedgerCompany
			default label is "GlobalLedgerCompany"
		CashManagementAccount
		CashAccountGroup		is a CashManagementAccount group
		ReportCurrency			is a Currency
		ForecastDate			is Date
		RangeCount				is Numeric 2
		PeriodDays1				is Numeric 3
		PeriodDays2				is Numeric 3
		PeriodDays3				is Numeric 3
		PeriodDays4				is Numeric 3
		ForecastBy				is Numeric 1 
			States
				CompanyCurrency			value is 1		
				CashAccountCurrency		value is 2		
				ReportCurrency			value is 3
		IncludePayables			is Boolean 
		IncludeReceivables		is Boolean 
		IncludeProcurement		is Boolean 

	Transient Fields
		IncludeDiscount			is AlphaUpper size 1		
			default label is "IncludePayablesDiscount"
			derive value from DerivedIncludeDiscount
			States 
				Yes				value is "Y"
				No				value is "N"

	Local Fields
		LocalForecastPeriod		is Numeric 2

	Derived Fields
		DerivedIncludeDiscount is a DerivedField		
			type is AlphaUpper size 1
			restricted
			if (CashManagementGroup.ShowDiscountedAmount)
				return "Y"
			return "N"

		PeriodFromDate is a DerivedField
			type is Date
			restricted
			initialize PeriodFromDate
			if (LocalForecastPeriod	= 2)
				return (ForecastDate + PeriodDays1 + 1)
			else
			if (LocalForecastPeriod	= 3)
				return (ForecastDate + PeriodDays2 + 1)
			else
			if (LocalForecastPeriod	= 4)
				return (ForecastDate + PeriodDays3 + 1)
			else
			if (LocalForecastPeriod	= 5)
				return (ForecastDate + PeriodEndInRange + 1)

		PeriodToDate is a DerivedField
			type is Date
			restricted
			initialize PeriodToDate
			if (LocalForecastPeriod	= 1)
				return (ForecastDate + PeriodDays1)
			else
			if (LocalForecastPeriod	= 2)
				return (ForecastDate + PeriodDays2)
			else
			if (LocalForecastPeriod	= 3)
				return (ForecastDate + PeriodDays3)
			else
			if (LocalForecastPeriod	= 4)
				return (ForecastDate + PeriodDays4)

			else
			if (LocalForecastPeriod	= 5)
				return (ForecastDate + PeriodDays1 as years)
		
		PeriodEndInRange is a DerivedField
			type is Numeric 3
			restricted
			if (RangeCount = 1)
				return PeriodDays1
			else
			if (RangeCount = 2)
				return PeriodDays2
			else
			if (RangeCount = 3)
				return PeriodDays3
			else
			if (RangeCount = 4)
				return PeriodDays4

		ForecastCurrency is a DerivedField
			type is Alpha 30
			if (ForecastBy.CompanyCurrency)
				return GeneralLedgerCompany.Currency
			if (ForecastBy.CashAccountCurrency)
				return CashManagementAccount.Currency
			if (ForecastBy.ReportCurrency)
				return ReportCurrency


		ForecastDaysRange is a StringField
			type is Alpha 20
			PeriodDays1
			"-" 
			PeriodDays2
			"-" 
			PeriodDays3
			"-" 
			PeriodDays4

	Field Rules
		Description
			required
		
		GeneralLedgerCompany
			if (!CashManagementAccount	entered
			and !CashAccountGroup		entered)
				required
					"CompanyOrAccountOrAccountGroupIsRequired"
					



					
		CashAccountGroup
			constraint (CashManagementAccount !entered)
				"CannotEnterCashManagementAccountAndCashAccountGroup"	
				
		ReportCurrency
			if (CashAccountGroup		entered
			and GeneralLedgerCompany	!entered)
				required
					"ReportCurrencyRequiredForCashAccountGroup"
					
		ForecastDate
			required
			
		ForecastBy
			required
			if (ReportCurrency	!entered)
				if (CashManagementAccount 	entered
				and GeneralLedgerCompany	!entered)
					force default to ForecastBy.CashAccountCurrency
				if (GeneralLedgerCompany	entered
				and CashManagementAccount 	!entered
				and CashAccountGroup		!entered)
					force default to ForecastBy.CompanyCurrency
			if (CashAccountGroup		entered
			and GeneralLedgerCompany	!entered)
				force default to ForecastBy.ReportCurrency
			if (GeneralLedgerCompany !entered)
				constraint (!ForecastBy.CompanyCurrency)
					"CompanyMustBeEnteredToForecastByCompanyCurrency"
			if (CashManagementAccount	!entered)
				constraint (!ForecastBy.CashAccountCurrency)
					"CashAccountMustBeEnteredToForcastByCashAccountCurrency"

		PeriodDays1
			required
  
		PeriodDays2
			constraint (PeriodDays2	> PeriodDays1)
				"ForecastRangeDaysShouldBeInIncreasingOrder"

		PeriodDays3
			constraint (PeriodDays2 entered)
				"CannotHaveBlanksInForecastRangeDays"
				
			constraint (PeriodDays3	> PeriodDays2)
				"ForecastRangeDaysShouldBeInIncreasingOrder"

		PeriodDays4
			constraint (PeriodDays3 entered)
				"CannotHaveBlanksInForecastRangeDays"
				
  			constraint (PeriodDays4	> PeriodDays3)
				"ForecastRangeDaysShouldBeInIncreasingOrder"
  
		IncludePayables
			initial value is true

		IncludeReceivables 
			initial value is true

		IncludeProcurement 
			initial value is true

		IncludeDiscount		
			if (IncludeDiscount changed)
				if (IncludeDiscount = "Y")
					invoke FastUpdate CashManagementGroup
						invoked.ShowDiscountedAmount = true
				else
					invoke FastUpdate CashManagementGroup
						invoked.ShowDiscountedAmount = false

	Relations
		DirectForecastPeriodRel
			one-to-one relation to DirectForecastPeriod
			Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
				related.DirectForecast			= DirectForecast
				related.DirectForecastPeriod	= LocalForecastPeriod








		DisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "AP"
				related.Company				  	= GeneralLedgerCompany
				related.CashManagementAccount 	= CashManagementAccount

		DisbursementAccountGroupDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "AP"
				related.Company				  	= GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)

		CompanyDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "AP"
				related.Company	   				= GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount entered)

		CashManagementAccountDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses AccountVendorCompany
				related.SystemCode				= "AP"
				related.CashManagementAccount 	= CashManagementAccount

		CashAccountGroupDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses AccountVendorCompany
				related.SystemCode				= "AP"
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)

		ReceiptDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "AR"
				related.Company				  	= GeneralLedgerCompany
				related.CashManagementAccount 	= CashManagementAccount

		ReceiptAccountGroupDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "AR"
				related.Company				  	= GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)

		CompanyReceiptDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode 				= "AR"
				related.Company	   				= GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount entered)

		CashManagementAccountReceiptDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses AccountVendorCompany
				related.SystemCode				= "AR"
				related.CashManagementAccount 	= CashManagementAccount

		CashAccountGroupReceiptDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses AccountVendorCompany
				related.SystemCode				= "AR"
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)

		EncumbranceDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "PO"
				related.Company				  	= GeneralLedgerCompany
				related.CashManagementAccount 	= CashManagementAccount

		EncumbranceAccountGroupDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "PO"
				related.Company				  	= GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)

		CompanyEncumbranceDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanyAccountVendor
				related.SystemCode				= "PO"
				related.Company	   				= GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount entered)

		CashManagementAccountEncumbranceDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses AccountVendorCompany
				related.SystemCode				= "PO"
				related.CashManagementAccount 	= CashManagementAccount

		CashAccountGroupEncumbranceDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses AccountVendorCompany
				related.SystemCode				= "PO"
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)

		CompanyAccountTotalDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses DisbursementCompanyAccountVendor
				related.Company	 			  = GeneralLedgerCompany
				related.CashManagementAccount = CashManagementAccount
				
		CompanyAccountGroupTotalDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses DisbursementCompanyAccountVendor
				related.Company	 			  = GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)
				
		CompanyTotalDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses DisbursementCompanyAccountVendor
				related.Company = GeneralLedgerCompany
			Instance Selection
				where (related.CashManagementAccount entered)
		
		CashManagementAccountTotalDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses DisbursementAccountVendorCompany
				related.CashManagementAccount = CashManagementAccount

		CashAccountGroupTotalDisbursementDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses DisbursementAccountVendorCompany
			Instance Selection
				where (related.CashManagementAccount within CashAccountGroup)
		
				 
    Conditions
		ForecastLoadHasBeenRun
			restricted
			when (CashManagementGroup.LastDirectForecastLoad entered)

    	IncludePayablesOrProcurement
    		restricted
    		when (IncludePayables
    		or    IncludeProcurement)

    	IncludePayablesAndProcurement
    		restricted
    		when (IncludePayables
    		and   IncludeProcurement)

		InflowCompanyAccountEntered
			restricted
			when (GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludeReceivables)
			
		InflowCompanyAccountGroupEntered
			restricted
			when (GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludeReceivables)
			
		InflowCompanyOnlyEntered
			restricted
			when (GeneralLedgerCompany entered
			and   !CashManagementAccount entered
			and   !CashAccountGroup entered
			and   IncludeReceivables)
			
		InflowAccountOnlyEntered
			restricted
			when (!GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludeReceivables)
			
		InflowAccountGroupOnlyEntered
			restricted
			when (!GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludeReceivables)
			
		OutflowCompanyAccountEntered
			restricted
			when (GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludePayablesOrProcurement)
			
		OutflowCompanyAccountGroupEntered
			restricted
			when (GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludePayablesOrProcurement)
			
		OutflowCompanyOnlyEntered
			restricted
			when (GeneralLedgerCompany entered
			and   !CashManagementAccount entered
			and   !CashAccountGroup entered
			and   IncludePayablesOrProcurement)
			
		OutflowAccountOnlyEntered
			restricted
			when (!GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludePayablesOrProcurement)
			
		OutflowAccountGroupOnlyEntered
			restricted
			when (!GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludePayablesOrProcurement)
			
		OutflowCompanyAccountEnteredAPPO
			restricted
			when (GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludePayablesAndProcurement)
			
		OutflowCompanyAccountGroupEnteredAPPO
			restricted
			when (GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludePayablesAndProcurement)
			
		OutflowCompanyOnlyEnteredAPPO
			restricted
			when (GeneralLedgerCompany entered
			and   !CashManagementAccount entered
			and   !CashAccountGroup entered
			and   IncludePayablesAndProcurement)
			
		OutflowAccountOnlyEnteredAPPO
			restricted
			when (!GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludePayablesAndProcurement)
			
		OutflowAccountGroupOnlyEnteredAPPO
			restricted
			when (!GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludePayablesAndProcurement)
			
		OutflowCompanyAccountEnteredAP
			restricted
			when (GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludePayables
			and   !IncludeProcurement)
			
		OutflowCompanyAccountGroupEnteredAP
			restricted
			when (GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludePayables
			and   !IncludeProcurement)
			
		OutflowCompanyOnlyEnteredAP
			restricted
			when (GeneralLedgerCompany entered
			and   !CashManagementAccount entered
			and   !CashAccountGroup entered
			and   IncludePayables
			and   !IncludeProcurement)
			
		OutflowAccountOnlyEnteredAP
			restricted
			when (!GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludePayables
			and   !IncludeProcurement)
			
		OutflowAccountGroupOnlyEnteredAP
			restricted
			when (!GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludePayables
			and   !IncludeProcurement)
			
		OutflowCompanyAccountEnteredPO
			restricted
			when (GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludeProcurement
			and   !IncludePayables)
			
		OutflowCompanyAccountGroupEnteredPO
			restricted
			when (GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludeProcurement
			and   !IncludePayables)
			
		OutflowCompanyOnlyEnteredPO
			restricted
			when (GeneralLedgerCompany entered
			and   !CashManagementAccount entered
			and   !CashAccountGroup entered
			and   IncludeProcurement
			and   !IncludePayables)
			
		OutflowAccountOnlyEnteredPO
			restricted
			when (!GeneralLedgerCompany entered
			and   CashManagementAccount entered
			and   IncludeProcurement
			and   !IncludePayables)
			
		OutflowAccountGroupOnlyEnteredPO
			restricted
			when (!GeneralLedgerCompany entered
			and   CashAccountGroup entered
			and   IncludeProcurement
			and   !IncludePayables)
			
	Rule Blocks
		UpdateRangeCount
			RangeCount = 1
			if (PeriodDays2	entered)
				RangeCount += 1
			if (PeriodDays3	entered)
				RangeCount += 1
			if (PeriodDays4	entered)
				RangeCount += 1

		CreateDirectForecastPeriod
			LocalForecastPeriod	= 2
			if	(PeriodDays2 entered
			and	!DirectForecastPeriodRel exists)
				invoke Create DirectForecastPeriod
					invoked.CashManagementGroup		= CashManagementGroup
					invoked.DirectForecast			= DirectForecast
					invoked.DirectForecastPeriod	= 2
					invoked.PeriodDateRange.Begin	= PeriodFromDate 
					invoked.PeriodDateRange.End   	= PeriodToDate
					
			LocalForecastPeriod	= 3
			if	(PeriodDays3 entered
			and	!DirectForecastPeriodRel exists)
				invoke Create DirectForecastPeriod
					invoked.CashManagementGroup		= CashManagementGroup
					invoked.DirectForecast			= DirectForecast
					invoked.DirectForecastPeriod	= 3
					invoked.PeriodDateRange.Begin 	= PeriodFromDate 
					invoked.PeriodDateRange.End   	= PeriodToDate
					
			LocalForecastPeriod	= 4
			if	(PeriodDays4 entered
			and	!DirectForecastPeriodRel exists)
				invoke Create DirectForecastPeriod
					invoked.CashManagementGroup		= CashManagementGroup
					invoked.DirectForecast			= DirectForecast
					invoked.DirectForecastPeriod	= 4
					invoked.PeriodDateRange.Begin 	= PeriodFromDate 
					invoked.PeriodDateRange.End   	= PeriodToDate

  	Actions
  		Create is a Create Action
			Action Rules
				include UpdateRangeCount

			Exit Rules
				LocalForecastPeriod	= 1
				invoke Create DirectForecastPeriod
					invoked.CashManagementGroup		= CashManagementGroup
					invoked.DirectForecast			= DirectForecast
					invoked.DirectForecastPeriod	= 1
					invoked.PeriodDateRange.Begin	= PeriodFromDate 
					invoked.PeriodDateRange.End   	= PeriodToDate
					
				LocalForecastPeriod	= 5
				invoke Create DirectForecastPeriod
					invoked.CashManagementGroup		= CashManagementGroup
					invoked.DirectForecast			= DirectForecast
					invoked.DirectForecastPeriod	= 5
					invoked.PeriodDateRange.Begin 	= PeriodFromDate 
					invoked.PeriodDateRange.End   	= PeriodToDate
					
				include CreateDirectForecastPeriod

  		Update is an Update Action
			Action Rules
				include UpdateRangeCount

			Exit Rules
				LocalForecastPeriod	= 1
				if  (DirectForecastPeriodRel exists
				and (ForecastDate changed
				or	 PeriodDays1 changed))
					invoke Update DirectForecastPeriodRel
						invoked.PeriodDateRange.Begin	= PeriodFromDate 
						invoked.PeriodDateRange.End   	= PeriodToDate
				
				LocalForecastPeriod	= 2
				if (DirectForecastPeriodRel exists)
					if (ForecastDate changed
					or	PeriodDays2 changed)
						invoke Update DirectForecastPeriodRel
							invoked.PeriodDateRange.Begin	= PeriodFromDate 
							invoked.PeriodDateRange.End   	= PeriodToDate
					else
					if (!PeriodDays2 entered)
						invoke Delete DirectForecastPeriodRel

				LocalForecastPeriod	= 3
				if (DirectForecastPeriodRel exists)
					if (ForecastDate changed
					or	PeriodDays3 changed)
						invoke Update DirectForecastPeriodRel
							invoked.PeriodDateRange.Begin	= PeriodFromDate 
							invoked.PeriodDateRange.End   	= PeriodToDate
					else
					if (!PeriodDays3 entered)
						invoke Delete DirectForecastPeriodRel

				LocalForecastPeriod	= 4
				if (DirectForecastPeriodRel exists)
					if (ForecastDate changed
					or	PeriodDays4 changed)
						invoke Update DirectForecastPeriodRel
							invoked.PeriodDateRange.Begin	= PeriodFromDate 
							invoked.PeriodDateRange.End   	= PeriodToDate
					else
					if (!PeriodDays4 entered)
						invoke Delete DirectForecastPeriodRel

				LocalForecastPeriod	= 5
				if  (DirectForecastPeriodRel exists
				and (ForecastDate changed
				or	 PeriodDays4 changed))
					invoke Update DirectForecastPeriodRel
						invoked.PeriodDateRange.Begin	= PeriodFromDate 
						invoked.PeriodDateRange.End   	= PeriodToDate
				
				include CreateDirectForecastPeriod

  		Delete is a Delete Action
  		
	
