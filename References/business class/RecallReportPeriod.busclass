RecallReportPeriod is a BusinessClass
    owned by recall
    prefix is RMPer

    Ontology
    	symbolic key is RecallReportPeriod
    
    Patterns
    	disable Auditing		
        disable EffectiveDated	
    
    Persistent Fields
        
	Local Fields
		CurrDate			is Date
		TempDate			is Date
		NumberOfMonths		is Numeric 5
		AdjustMonth			is Numeric 2
		NumberOfDays		is Numeric 5
		AdjustDays			is Numeric 5
		PeriodRange			is a DateRange
		Month				is Numeric size 2
    		States
    			January			value is 1
    			February		value is 2
    			March			value is 3
    			April			value is 4
    			May				value is 5
    			June			value is 6
    			July			value is 7
    			August			value is 8
    			September		value is 9
    			October			value is 10
    			November		value is 11
    			December		value is 12
		BiMonth				is Numeric size 2
    		States
    			January			value is 1
    				default label is "Jan_/Feb"
    			February		value is 2
    				default label is "Jan_/Feb"
    			March			value is 3
    				default label is "Mar_/Apr"
    			April			value is 4
    				default label is "Mar_/Apr"
    			May				value is 5
    				default label is "May_/Jun"
    			June			value is 6
    				default label is "May_/Jun"
    			July			value is 7
    				default label is "Jul_/Aug"
    			August			value is 8
    				default label is "Jul_/Aug"
    			September		value is 9
    				default label is "Sep_/Oct"
    			October			value is 10
    				default label is "Sep_/Oct"
    			November		value is 11
    				default label is "Nov_/Dec"
    			December		value is 12
    				default label is "Nov_/Dec"

	    LocalRecallSource					is Numeric size 2
	    	States
	    		Internal					value is 1
	    		ThirdParty					value is 2
	    		FDA							value is 3
	    		Supplier					value is 4
	    		Manufacturer				value is 5

	Derived Fields
		ThruMsg is a MessageField
			restricted
			"Thru<ReportThruDate>"
	
		MsgMonth is a MessageField
			restricted
			"<Month>_<DerivedYear>"
	
		MsgBiMonth is a MessageField
			restricted
			"<BiMonth>_<DerivedYear>"
	
   		DerivedQuarterMessage is a MessageField	
   			restricted
			"<DerivedYear>_Quarter_<DerivedQuarter>"
   			
		ReportFromDate is a DerivedField
			type is Date
			if (RecallReport = 0)
				return current corporate date
			else
			if (RecallReport.FrequencyInDays)
				if (RecallReport.EndDate entered)
					CurrDate = RecallReport.EndDate
				else
					CurrDate = current corporate date

				NumberOfDays = (((RecallReport.NumberOfPeriods - RecallReportPeriod + 1) * RecallReport.Frequency) - 1)

				if (RecallReport.IncludeCurrent
				or  RecallReport.EndDate entered)
					NumberOfDays += CurrDate week day - 7
				else
					NumberOfDays += CurrDate week day

				return (CurrDate - NumberOfDays as days)
			else
				if (RecallReport.EndDate entered)
					CurrDate = RecallReport.EndDate
				else
					CurrDate = current corporate date

				if (RecallReport.Frequency.Monthly)
					AdjustMonth = 1
				else
					AdjustMonth = CurrDate month
					while (AdjustMonth > RecallReport.Frequency)
						AdjustMonth -= RecallReport.Frequency

				NumberOfDays = CurrDate day - 1
				NumberOfMonths = (((RecallReport.NumberOfPeriods - RecallReportPeriod + 1) * RecallReport.Frequency) + (AdjustMonth - 1))

				if (RecallReport.IncludeCurrent
				or  RecallReport.EndDate entered)
					NumberOfMonths -= RecallReport.Frequency
				
				TempDate = CurrDate  - NumberOfDays as days - NumberOfMonths as months
				return TempDate

		ReportThruDate is a DerivedField
			type is Date
			if (RecallReport = 0)
				return current corporate date
			else
			if (RecallReport.FrequencyInDays)
				if (RecallReport.EndDate entered)
					CurrDate = RecallReport.EndDate
				else
					CurrDate = current corporate date

				NumberOfDays = (((RecallReport.NumberOfPeriods - RecallReportPeriod) * RecallReport.Frequency))

				if (RecallReport.IncludeCurrent
				or  RecallReport.EndDate entered)
					AdjustDays = (7 - CurrDate week day)
					return CurrDate - NumberOfDays as days + AdjustDays as days
				else
					AdjustDays = CurrDate week day
					return CurrDate - NumberOfDays as days - AdjustDays as days

			else
				NumberOfMonths = (((RecallReport.NumberOfPeriods - RecallReportPeriod) * RecallReport.Frequency)) 

				if (RecallReport.EndDate entered)
					CurrDate = RecallReport.EndDate
				else
					CurrDate = current corporate date

				if (RecallReport.Frequency.Monthly)
					NumberOfMonths += 1
				else
					AdjustMonth = CurrDate month
					while (AdjustMonth > RecallReport.Frequency)
						AdjustMonth -= RecallReport.Frequency
					NumberOfMonths += AdjustMonth

				if (RecallReport.IncludeCurrent
				or  RecallReport.EndDate entered)
					NumberOfMonths -= RecallReport.Frequency
				
				TempDate = CurrDate - NumberOfMonths as months
				NumberOfDays = TempDate days in month - TempDate day
				return (TempDate + NumberOfDays as days)
			
		ReleaseDateNoticeCount is a DerivedField
			type is Numeric 5
			return (instance count of RecallNoticeReleasedDateRel)

		DerivedYear is a DerivedField
			type is Alpha 4
			restricted
			return (ReportFromDate year)
			
		DerivedQuarter is a DerivedField
			type is Alpha 1
			restricted
			if (ReportFromDate month > 9)
				return "4"
			else
			if (ReportFromDate month > 6)
				return "3"
			else
			if (ReportFromDate month > 3)
				return "2"
			else
				return "1"
			
		PeriodTitle is a DerivedField
			type is Alpha size 50
			if (RecallReport.Frequency.Weekly)
				return ThruMsg
			else
			if (RecallReport.Frequency.BiWeekly)
				return ThruMsg
			else
			if (RecallReport.Frequency.BiMonthly)
				BiMonth = ReportFromDate month
				return MsgBiMonth
			else
			if (RecallReport.Frequency.Quarterly)
				return DerivedQuarterMessage
			else
			if (RecallReport.Frequency.Monthly)
				Month = ReportFromDate month
				return MsgMonth

		RecallSourceCount1 is a DerivedField
			type is Numeric 5
			LocalRecallSource = 1
			return (instance count of LocalRecallNoticeSourceRel)
			
		RecallSourceCount2 is a DerivedField
			type is Numeric 5
			LocalRecallSource = 2
			return (instance count of LocalRecallNoticeSourceRel)
			
		RecallSourceCount3 is a DerivedField
			type is Numeric 5
			LocalRecallSource = 3
			return (instance count of LocalRecallNoticeSourceRel)
			
		RecallSourceCount4 is a DerivedField
			type is Numeric 5
			LocalRecallSource = 4
			return (instance count of LocalRecallNoticeSourceRel)
			
		RecallSourceCount5 is a DerivedField
			type is Numeric 5
			LocalRecallSource = 5
			return (instance count of LocalRecallNoticeSourceRel)
			
	Conditions

	Relations
		RecallNoticeCreateDateRel is a RecallNotice set
    		Instance Selection
				where (related.ApplicableRecallNotice
				and    related.create date >= ReportFromDate
				and    related.create date <= ReportThruDate)

		RecallNoticeReleasedDateRel is a RecallNotice set
    		Instance Selection
				where (related.ApplicableRecallNotice
				and    related.ReleasedTimestamp >= ReportFromDate
				and    related.ReleasedTimestamp <= ReportThruDate)

		ReleasedDateTaskRel is a RecallNoticeTask set
    		Instance Selection
				where (related.RecallNotice.ApplicableRecallNotice
				and    related.RecallNotice.ReleasedTimestamp >= ReportFromDate
				and    related.RecallNotice.ReleasedTimestamp <= ReportThruDate)

    	LocalRecallNoticeSourceRel
    		one-to-many relation to RecallNotice
    		Field Mapping uses ByRecallSource
    			related.RecallSource		= LocalRecallSource
    		Instance Selection
				where (related.ApplicableRecallNotice
				and    related.DerivedReleaseDate >= ReportFromDate
				and    related.DerivedReleaseDate <= ReportThruDate) 

	Actions
		Create is an Action
			restricted
		
		Update is an Action
			restricted
		
		Delete is an Action 
			restricted
		
		Purge is a Purge Action
			restricted
		

		
