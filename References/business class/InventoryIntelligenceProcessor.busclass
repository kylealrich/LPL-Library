InventoryIntelligenceProcessor is a BusinessClass
	owned by InventoryIntelligence
	sql name is IIProcessor
	prefix is IIPE
	representative image is InventoryIntelligenceProcessor.Employee.ResourcePicture.File
		missing image is random background
			foreground text is "<InventoryIntelligenceProcessor.Employee.Name.Initials>"
	
	Ontology
		symbolic key is InventoryIntelligenceProcessor
	
	Patterns
	
	Persistent Fields
		DefaultAccess	is an AccessType
		
	Local Fields
		LocalCompany 	is like InventoryCompany
		LocalLocation 	is like InventoryLocation
		LocalAccess		is like AccessType
		LocalProcessor  is like InventoryIntelligenceProcessor
		LocalHROrganization is like HROrganization
	
	Field Rules
		DefaultAccess
			initial value is AccessType.None
			if (DefaultAccess not entered
			and !RedundantLocationsWithNoneAccess)
				default to AccessType.None
			required
				"MustSpecifyDefaultAccessRight"
			constraint (!RedundantLocations)
				"RedundantCompanyLocationSpecificAccessExists"
			
	Conditions
		RedundantLocations
			when (first ProcessorLocationsWithSameAccessRel exists)
		
		RedundantLocationsWithNoneAccess
			when (first ProcessorLocationsWithNoneAccessRel exists)

		InventoryIntelligenceProcessorExists
			when (InventoryIntelligenceProcessor exists)

		IncludeConsignment					
			restricted
			when (IIHGlobalConfigurationRel.ConsignmentInValuation)

		DoNotIncludeConsignment				
			restricted
			when (!IIHGlobalConfigurationRel.ConsignmentInValuation)

	Derived Fields
		HROrganizationAndProcessor	is a StringField
			type is AlphaUpper 20
			HROrganization
			", "
			InventoryIntelligenceProcessor

		CreateMessage is a LabelField
			restricted
			"CreateInventoryIntelligenceProcessor"
		InventoryIntelligenceProcessorMessage is a LabelField
			restricted
			"<InventoryIntelligenceProcessor.Employee.Name.PreferredFirstAndLastName>'sInventoryIntelligenceProcessorProfile"

		DerivedFormTitle is a DerivedField
			type is MessageField
			if (InventoryIntelligenceProcessorExists)
				return InventoryIntelligenceProcessorMessage
			else
				return CreateMessage

		ReferenceInventoryValue is a DerivedField		
			type is like InternationalAmount
			return (sum IIProcessorWithViewOrEditAccessRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.ReferenceInventoryValue)
				
		TargetInventoryValue is a DerivedField			
			type is like InternationalAmount
			return (sum IIProcessorWithViewOrEditAccessRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.TargetInventoryValue)

		RemovalCandidatesValue is a DerivedField			
			type is like InternationalAmount
			return (sum IIProcessorWithViewOrEditAccessRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralRemovalCandidatesValue)

		PotentialChange is a DerivedField					
			type is like InternationalAmount
			return (sum IIProcessorWithViewOrEditAccessRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.UnderReviewValue) 

		PotentialIncrease is a DerivedField					
			type is like InternationalAmount
			return (sum IIProcessorWithViewOrEditAccessRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.IncreaseCostOfImprovedService)

		PotentialDecrease is a DerivedField					
			type is like InternationalAmount
			return (sum IIProcessorWithViewOrEditAccessRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.DecreasePotentialSavings)

		PerpetualItemValue is a DerivedField				
			type is like InternationalAmount
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralEligibleIemValue)

		PerpetualItemCount is a DerivedField				
			type is Numeric size 10
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralEligibleIemCount)

		PerpetualDecreaseItemCount is a DerivedField		
			type is Numeric size 10
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.DecreaseItemCount)

		PerpetualPotentialSavings is a DerivedField			
			type is like InternationalAmount
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.DecreasePotentialSavings)

		PerpetualIncreaseItemCount is a DerivedField			
			type is Numeric size 10
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.IncreaseItemCount)

		PerpetualRecommendedItemCount is a DerivedField			
			type is Numeric size 10
			return (PerpetualDecreaseItemCount + PerpetualIncreaseItemCount)

		PerpetualRecommendedItemValue is a DerivedField			
			type is like InternationalAmount
			return (PerpetualPotentialSavings + PerpetualCostOfImprovedService)

		PerpetualCostOfImprovedService is a DerivedField		
			type is like InternationalAmount
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.IncreaseCostOfImprovedService)

		PerpetualRemovalCount is a DerivedField				
			type is Numeric size 10
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralRemovalCandidatesCount)

		PerpetualRemovalValue is a DerivedField				
			type is like InternationalAmount
			return (sum IIProcessorPerpetualLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralRemovalCandidatesValue)

		ParItemValue is a DerivedField						
			type is like InternationalAmount
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralEligibleIemValue)

		ParItemCount is a DerivedField					
			type is Numeric size 10
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralEligibleIemCount)

		ParDecreaseItemCount is a DerivedField			
			type is Numeric size 10
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.DecreaseItemCount)
			
		ParPotentialSavings is a DerivedField			
			type is like InternationalAmount
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.DecreasePotentialSavings)
			
		ParIncreaseItemCount is a DerivedField			
			type is Numeric size 10
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.IncreaseItemCount)
			
		ParCostOfImprovedService is a DerivedField			
			type is like InternationalAmount
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.IncreaseCostOfImprovedService)
			
		ParRemovalCount is a DerivedField				
			type is Numeric size 10
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralRemovalCandidatesCount)
			
		ParRemovalValue is a DerivedField			
			type is like InternationalAmount
			return (sum IIProcessorParLocationsRel.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation.GeneralRemovalCandidatesValue)

		ParRecommendedItemCount is a DerivedField			
			type is Numeric size 10
			return (ParDecreaseItemCount + ParIncreaseItemCount)

		ParRecommendedItemValue is a DerivedField			
			type is like InternationalAmount
			return (ParPotentialSavings + ParCostOfImprovedService)

		AnalysisDateStr is a StringField					
            type is Alpha size 50
            first RecsProcessResultInvLocRel.AnalysisDateString

	Relations
		ProcessorLocationsWithSameAccessRel is a InventoryIntelligenceProcessorLocation set
			Instance Selection
				where (related.Access = DefaultAccess)
				
		ProcessorLocationsWithNoneAccessRel is a InventoryIntelligenceProcessorLocation set
			Instance Selection
				where (related.Access = AccessType.None)

		EmployeeRel
			one-to-one relation to Employee
			Field Mapping uses symbolic key
				related.HROrganization	= HROrganization
				related.Employee		= InventoryIntelligenceProcessor
				
		IIHInventoryLocationRel is a IIHInventoryLocation set			
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

		ProcessorIIHInventoryLocationsRel is a IIHInventoryLocation set			
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				and related.ProcessorWithViewOrEditAccess)

		IIHItemLocationRel is a IIHItemLocation set           
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				and related.ProcessorWithViewOrEditAccess)

		RemovalCandidatesRel
			one-to-many relation to IIHItemLocation
			Field Mapping uses ByRemovalCandidate
				related.RemovalCandidate	= true
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				and related.ProcessorWithViewOrEditAccess)
		InventoryIntelligenceProcessorCompanyRel is a InventoryIntelligenceProcessorLocation set				
			Instance Selection
				where (related.IsCompanyRule)
				
		IIProcessorAccessRightsRel												
			one-to-one relation to InventoryIntelligenceProcessorAccessRights
			delete cascades
			Field Mapping uses symbolic key
				related.HROrganization 						= HROrganization
				related.InventoryIntelligenceProcessor 		= InventoryIntelligenceProcessor
				related.InventoryIntelligenceProcessorAccessRights.InventoryCompany = LocalCompany
				related.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = LocalLocation
				
		IIProcessorAccessRightsForCompanyRel											
			one-to-many relation to InventoryIntelligenceProcessorAccessRights
			delete cascades
			Field Mapping uses ByCompany
				related.HROrganization 					= HROrganization
				related.InventoryIntelligenceProcessor	= InventoryIntelligenceProcessor
				related.InventoryIntelligenceProcessorAccessRights.InventoryCompany 	= LocalCompany
				
		IIHLocationsForCompanyRel							
			one-to-many relation to IIHInventoryLocation
			Field Mapping uses symbolic key
				related.Company		= LocalCompany
				
		InventoryIntelligenceProcessorLocationRel is a InventoryIntelligenceProcessorLocation set				
			Instance Selection
				where (related.IsLocationRule)
				
		IIProcessorWithViewOrEditAccessRel				
			one-to-many relation to InventoryIntelligenceProcessorAccessRights
			delete cascades
			Field Mapping uses symbolic key
				related.HROrganization 					= HROrganization
         		related.InventoryIntelligenceProcessor	= InventoryIntelligenceProcessor
				
		IIProcessorPerpetualLocationsRel					
			one-to-many relation to InventoryIntelligenceProcessorAccessRights
			delete cascades
			Field Mapping uses symbolic key
				related.HROrganization 											= HROrganization
				related.InventoryIntelligenceProcessor						 	= InventoryIntelligenceProcessor
			Instance Selection
				where (related.IsPerpetualLocation)
				
		IIProcessorParLocationsRel							
			one-to-many relation to InventoryIntelligenceProcessorAccessRights
			delete cascades
			Field Mapping uses symbolic key
				related.HROrganization 											= HROrganization
				related.InventoryIntelligenceProcessor						 	= InventoryIntelligenceProcessor
			Instance Selection
				where (related.ParOrExchangeCart)

		AccessRightsForProcessorRel								
			one-to-many relation to InventoryIntelligenceProcessorAccessRights
			delete cascades
			Field Mapping uses symbolic key
				related.HROrganization 											= LocalHROrganization
				related.InventoryIntelligenceProcessor						 	= LocalProcessor

		RecsProcessResultInvLocRel								
		    one-to-many relation to InventoryIntelligenceProcessResult
		    Field Mapping uses ByProcessStatusStartTime
		        related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
		        related.Process                = 2
		        related.Status                 = 2

		IIProcessResultRel					
		    one-to-many relation to InventoryIntelligenceProcessResult
		    Field Mapping uses ByProcessStatusStartTime
		        related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				related.Process                = 2
		        related.Status                 = 2
			Instance Selection
				where (related.RunsLimit)

		CumulativeHistoryChartRel
		    one-to-many relation to InventoryIntelligenceProcessResult
		    Field Mapping uses ByProcessStatusStartTime
		        related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				related.Process                = 2
		        related.Status                 = 2
			Instance Selection
				where (related.CumulativeChartRunsLimit)

		InventoryIntelligenceProcessorRel									
			one-to-one relation to InventoryIntelligenceProcessor
			Field Mapping uses symbolic key
				related.HROrganization 					= HROrganization
				related.InventoryIntelligenceProcessor	= InventoryIntelligenceProcessor

		IIHGlobalConfigurationRel						
			one-to-one relation to IIHGlobalConfiguration
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup
				
	Actions
		Create is a Create Action
			Exit Rules
				invoke UpdateAccessRightsForProcessor


				invoke Process ComputeCumulativeGraph in background
					invoked.FinanceEnterpriseGroup 				= actor.context.FinanceEnterpriseGroup
					invoked.PrmHROrganization				  	= HROrganization
					invoked.PrmInventoryIntelligenceProcessor 	= InventoryIntelligenceProcessor
					invoked.TimeOutInMinutes   					= 20
		
		Update is an Update Action
			Exit Rules
				invoke UpdateAccessRightsForProcessor


				invoke Process ComputeCumulativeGraph in background
					invoked.FinanceEnterpriseGroup 				= actor.context.FinanceEnterpriseGroup
					invoked.PrmHROrganization				  	= HROrganization
					invoked.PrmInventoryIntelligenceProcessor 	= InventoryIntelligenceProcessor
					invoked.TimeOutInMinutes   					= 20
		
		Delete is a Delete Action
			Entrance Rules
				LocalProcessor = InventoryIntelligenceProcessor
				LocalHROrganization = HROrganization
			Exit Rules
				for each AccessRightsForProcessorRel
					invoke Purge each

		UpdateAccessRights  is a Set Action
			restricted
			Action Rules
				Instance Rules
					invoke UpdateAccessRightsForProcessor

		
		UpdateAccessRightsForProcessor is an Instance Action
			Action Rules
				if (DefaultAccess.View or DefaultAccess.Edit)
					for each IIHInventoryLocationRel
						LocalCompany  = each.Company
						LocalLocation = each.IIHInventoryLocation
						LocalAccess   = DefaultAccess
						invoke Update IIProcessorAccessRightsRel
							invoked.HROrganization													= HROrganization
							invoked.InventoryIntelligenceProcessor									= InventoryIntelligenceProcessor
							invoked.InventoryIntelligenceProcessorAccessRights.InventoryCompany  	= LocalCompany
							invoked.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = LocalLocation
							invoked.Access 															= LocalAccess
							invoked.RuleLevel														= 1
				else
					for each IIProcessorWithViewOrEditAccessRel
						invoke Purge each

						
				for each InventoryIntelligenceProcessorCompanyRel
					LocalCompany = each.Company
					LocalAccess  = each.Access
					if (LocalAccess.None)
						for each IIProcessorAccessRightsForCompanyRel
							invoke Purge each
					else
						if (IIProcessorAccessRightsForCompanyRel exists)
							for each IIProcessorAccessRightsForCompanyRel
								LocalLocation = each.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation
								invoke Update each
									invoked.HROrganization													= HROrganization
									invoked.InventoryIntelligenceProcessor									= InventoryIntelligenceProcessor
									invoked.InventoryIntelligenceProcessorAccessRights.InventoryCompany  	= LocalCompany
									invoked.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = LocalLocation
									invoked.Access 															= LocalAccess
									invoked.RuleLevel														= 2
						else
							for each IIHLocationsForCompanyRel
								LocalLocation = each.IIHInventoryLocation
								invoke Create InventoryIntelligenceProcessorAccessRights
									invoked.HROrganization													= HROrganization
									invoked.InventoryIntelligenceProcessor									= InventoryIntelligenceProcessor
									invoked.InventoryIntelligenceProcessorAccessRights.InventoryCompany  	= LocalCompany
									invoked.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = LocalLocation
									invoked.Access 															= LocalAccess
									invoked.RuleLevel														= 2

							
				for each InventoryIntelligenceProcessorLocationRel
					LocalCompany  = each.Company
					LocalLocation = each.Location
					LocalAccess   = each.Access
					if (LocalAccess.None)
						if (IIProcessorAccessRightsRel exists)
							invoke Purge IIProcessorAccessRightsRel
					else
						if (IIProcessorAccessRightsRel exists)
								invoke Update IIProcessorAccessRightsRel
									invoked.HROrganization													= HROrganization
									invoked.InventoryIntelligenceProcessor									= InventoryIntelligenceProcessor
									invoked.InventoryIntelligenceProcessorAccessRights.InventoryCompany  	= LocalCompany
									invoked.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = LocalLocation
									invoked.Access 															= LocalAccess
									invoked.RuleLevel														= 3
						else
							invoke Create InventoryIntelligenceProcessorAccessRights
								invoked.HROrganization													= HROrganization
								invoked.InventoryIntelligenceProcessor									= InventoryIntelligenceProcessor
								invoked.InventoryIntelligenceProcessorAccessRights.InventoryCompany  	= LocalCompany
								invoked.InventoryIntelligenceProcessorAccessRights.IIHInventoryLocation = LocalLocation
								invoked.Access 															= LocalAccess
								invoked.RuleLevel														= 3
									

