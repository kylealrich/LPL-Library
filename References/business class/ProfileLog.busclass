ProfileLog is a BusinessClass
    owned by la
    prefix is PLOG
	contains environment data
	disable data area copy
		preserve target data
    
    Ontology
    	symbolic key is ProfileLog
    
    Patterns
    	implements CRUD
		disable Auditing
		disable EffectiveDated
	
	Persistent Fields
		Description is Alpha 60
		AutoTranCount is Numeric 10
			default label is"AutoTransactionCount"
		TranCount is Numeric 10
			default label is"TransactionCount"
		Active is Boolean
		LoadEntriesAutomatically is Boolean 
		PrintStdout is Boolean
				
	Transient Fields
		LogData is BinaryDocument

	Field Rules
		LoadEntriesAutomatically
			initial value is true
		
	Relations

		ActiveProfileLogSet
			one-to-many relation to ProfileLog
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Active)
				
	Actions
		Create is a Create Action
			Action Rules
				if (Active)
					for each ActiveProfileLogSet
						invoke MakeInactive each
			
			
		Delete is a Delete Action
		Update is an Update Action




























		
		SaveIntermediateEntry is an Update Action
			restricted
			Action Rules
				TranCount = TranCount + 1
				invoke Create ProfileLogIntermediateEntry
					invoked.ProfileLog = ProfileLog
					invoked.LogData    = LogData

		RemoveLogEntries is an Instance Action
			Action Rules
				initialize TranCount
				initialize AutoTranCount
				invoke Delete ProfileLogEntry set
				invoke Delete ProfileLogIntermediateEntry set
		
		MakeInactive is an Instance Action
			valid when (Active)
			Action Rules					
				invoke Update
					invoked.Active = false
		
		MakeActive is an Instance Action
			valid when (!Active)
			Action Rules					
				for each ActiveProfileLogSet
					invoke MakeInactive each
				
				invoke Update
					invoked.Active = true
					
