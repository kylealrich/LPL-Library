StructureRowValue is a BusinessClass
	owned by GeneralLedger
	prefix is Srval

	Ontology
		symbolic key is StructureRowValue
    Patterns


	Persistent Fields
        Value							is Boolean
        
    Local Fields
    	LocalFromField					is Numeric 2
    	LocalToField					is Numeric 2
        
    Derived Fields
    	DerivedFromText is a DerivedField
    		type is Alpha 25
    		restricted
    		return StructureRow.DisplayLabel
    	DerivedToText is a DerivedField
    		type is Alpha 25
    		restricted
    		return StructureDetail.DisplayLabel
    	DerivedDescription is a DerivedField
    		type is Alpha 250
    		restricted
    		return DerivedFromText + " To " + DerivedToText
    	DerivedRelationExists is a DerivedField
    		type is Boolean
    		restricted
    		if (CodeBlockRelExists)
    			return true
    		else
    			return false  		
        
    Conditions
    	FromLessThanTo
    		restricted
    		when (StructureRow.SortOrder < StructureDetail.SortOrder)
    	FromEqualTo
    		restricted
    		when (StructureRow.SortOrder = StructureDetail.SortOrder)
    	NoLongerSelected
    		restricted
    		when (Value = false)

    	CodeBlockRelExists
    		restricted
    		when (CodeBlockRelationRel exists)
    	LedgerToEntity
    		restricted
    		when (StructureRow.Type.Ledger
    		and   StructureDetail.Type.AccountingEntity)
    	EntityToAcctUnit
    		restricted
    		when (StructureRow.Type.AccountingEntity
    		and   StructureDetail.Type.AccountingUnit)
    	
    Relations
    	CodeBlockRelationRel
    		one-to-many relation to CodeBlockRelation
    		Field Mapping uses ByFromToAll
    			related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
    			related.FromField							= LocalFromField
    			related.ToField								= LocalToField
    		
    Field Rules
    	FinanceEnterpriseGroup
    		if (LedgerToEntity
    		or  EntityToAcctUnit)
    			constraint (Value = true)
    				"CannotUnselectSystemDefinedRelations"    		
    	Value
    		constraint (!FromEqualTo)
    			"InvalidSelection,FromAndToAreEqual"
    		constraint (FromLessThanTo)
    			"InvalidSelection,FromFieldOrderMustBeLessThanToFieldOrder"	
    			
    Actions
    	Create is a Create Action
    		Field Rules
    		
    		Action Rules
    		
    		Exit Rules
    			if (Value)
    		    	LocalFromField								= StructureRow.Type
    		    	LocalToField								= StructureDetail.Type
    				if (!CodeBlockRelationRel exists)    			
	    				invoke CreateFromMatrix CodeBlockRelation
	    					invoked.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
	    					invoked.Description						= DerivedDescription
	    					invoked.FromField						= StructureRow.Type
							invoked.ToField							= StructureDetail.Type
							invoked.Active							= true
							
		CreateFromRelation is a Create Action
			restricted
	    	
    	Update is an Update Action
    		Field Rules
    		
    		Action Rules
    		
    		Exit Rules
    		    LocalFromField								= StructureRow.Type
    		    LocalToField								= StructureDetail.Type
    			if (!Value)
    				if (CodeBlockRelExists)
    					invoke SystemDelete CodeBlockRelationRel
    		    if (Value)
    		    	if (!CodeBlockRelExists)
    					invoke CreateFromMatrix CodeBlockRelation
    						invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
    						invoked.Description					= DerivedDescription
    						invoked.FromField					= StructureRow.Type
							invoked.ToField						= StructureDetail.Type
							invoked.Active						= true
    	
    	Delete is a Delete Action
    		
    		Exit Rules
    			LocalFromField			= StructureRow.Type
    			LocalToField			= StructureDetail.Type

    			invoke SystemDelete CodeBlockRelationRel
    	
    	DeleteFromRelation is a Delete Action
    		restricted

    	RemoveValue is an Instance Action
    		restricted
    		Action Rules
    			Value = false
    
