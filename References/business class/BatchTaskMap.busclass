BatchTaskMap is a BusinessClass
    owned by la
    prefix is TSKM

    Ontology
		part of BatchTaskStep
    		relative key is StepID is Numeric size 9
    		
    Persistent Fields
    	BatchTaskMapData
    	TargetField	is a BatchTaskField

	Derived Fields
		TargetFieldType is a DerivedField
			type is Numeric size 1
			return TargetField.FieldType
		
		SourceFieldType is a DerivedField
			type is Numeric size 1
			return BatchTaskMapData.SourceField.FieldType

	Conditions
		IsComplete
			default label is "Complete"
			when (BatchTaskMapData.SourceStep entered
			and   BatchTaskMapData.SourceField.BusinessField entered
			and   TargetField.BusinessField entered)
 
	Rule Blocks
		ParentBatchTaskConfirmsAndConstraints
			constraint (not BatchTask.IsInProgress)
				"<BatchTask>CannotBeUpdated.ItOrAJobStreamWhichIncludesItIsInProgressState"

			if (BatchTask.CheckPointExists)
				confirmation required
					"<BatchTask>OrAJobStreamThatIncludesItHasUnfinishedTriggersAndRecoveryInformation_(Checkpoint)Exists.UpdatingAtThisTimeMayCauseCorruptionOrLossOfDataOrCompromiseTheAbilityToRecoverAFailedJobCorrectly.AreYouSureYouWantToUpdate?"
			else
				if (BatchTask.IsScheduled)
					confirmation required
						"<BatchTask>OrAJobStreamThatIncludesItIsPendingScheduling,CurrentlyInProcess,OrInAnIncompletedOrFailedState.UpdatingAtThisTimeMayImpactThePendingOrCurrentExecutionsOfItOrJobStreamsThatIncludeIt.AreYouSureYouWantToUpdate?"
		
 	Field Rules
 		StepID
			autosequence
				minimize contention
 		
 		BatchTaskMapData
 			required
 			
 		TargetField
 			required
 			
 			constraint (TargetField.BusinessView = Sequence.TaskStep.BusinessClass) 
 				"TargetBusinessClassMustMatchJobsBusinessClass"
 				
 			if (Sequence.TaskStep.TaskAction.IsParameterized)
 				constraint (TargetField.FieldType = 2) 
 					"TargetFieldMustBeAParameter"
 			else
 				constraint (TargetField.FieldType = 0 ) 
 					"TargetFieldMustBePersistent"

	Actions
		Create is a Create Action
			Action Rules
				include ParentBatchTaskConfirmsAndConstraints
				
				invoke MarkAsUsed BatchTaskMapData.SourceField


				
		Update is an Update Action
			restricted 
			
			Action Rules
				include ParentBatchTaskConfirmsAndConstraints
		
		Delete is a Delete Action			
			Action Rules
				include ParentBatchTaskConfirmsAndConstraints
			
			Exit Rules
				if (not BatchTaskMapData.SourceField.BatchTaskMap_ByBatchTaskMapDataSourceField_SetRel exists)
					invoke Delete BatchTaskMapData.SourceField 

		Copy is an Instance Action
            restricted

            Parameters
                NewJob is AlphaUpper size 30
                NewSequence is Numeric size 8

            Action Rules
                invoke Create BatchTaskMap
                    invoked.BatchTask       = NewJob
                    invoked.Sequence        = NewSequence
                    invoked.BatchTaskMapData = BatchTaskMapData
                    invoked.TargetField     = TargetField
		
	Sets	
		ByTaskSeqSourceStep
 			Sort Order
 				BatchTask
 				Sequence
 				BatchTaskMapData.SourceStep
 				BatchTaskMapData.SourceField.BusinessField
 				TargetField.BusinessField
 			Instance Selection
 				where (IsComplete)
 					
		ByTaskExecutionOrderSourceStepExecutionOrder
 			Sort Order
 				BatchTask
 				Sequence.ExecutionOrder
 				BatchTaskMapData.SourceStep.ExecutionOrder
 				BatchTaskMapData.SourceField.BusinessField
 				TargetField.BusinessField
 			Instance Selection
 				where (IsComplete)

		ByTaskSeqSourceField
 			Sort Order
 				BatchTask
 				Sequence
 				BatchTaskMapData.SourceField.BusinessField
 				TargetField.BusinessField
 				BatchTaskMapData.SourceStep 			
 			Instance Selection
 				where (IsComplete)
 				
		ByTaskSeqTargetField
 			Sort Order
 				BatchTask
 				Sequence
 				TargetField.BusinessField
 				BatchTaskMapData.SourceField.BusinessField
 				BatchTaskMapData.SourceStep 			 					
 			Instance Selection
 				where (IsComplete)	

		ByTaskExecutionOrderTargetField
 			Sort Order
 				BatchTask
 				Sequence.ExecutionOrder
 				TargetField.BusinessField
 				BatchTaskMapData.SourceField.BusinessField
 				BatchTaskMapData.SourceStep 			 					
 			Instance Selection
 				where (IsComplete)	
 				
