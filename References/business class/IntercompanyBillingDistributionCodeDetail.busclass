IntercompanyBillingDistributionCodeDetail is a BusinessClass
	owned by intercobilling
	prefix is ICBCD
	sql name is "ICBDistributionCodeDetail"

	Ontology
		symbolic key is IntercompanyBillingDistributionCodeDetail

	Patterns
		disable AuditIndex


	Persistent Fields
		Description
		DistributionPercent			is Percent size 6.3
		DistributionAccount			is a FinanceCodeBlock


	Field Rules
		Description
			default to DistributionAccount.GeneralLedgerChartAccount.AccountDescription


	Actions
		Create is a Create Action
			Action Rules
				constraint (DistributionAccount.GeneralLedgerChartAccount entered)
					"AtMinimum,MustEnterAChartAccount"

				if (DistributionPercent entered)
					invoke UpdateTotals IntercompanyBillingDistributionCode
						invoked.OldDistributionPercent = blank
						invoked.NewDistributionPercent = DistributionPercent


		Update is an Update Action
			Action Rules
				constraint (DistributionAccount.GeneralLedgerChartAccount entered)
					"AtMinimum,MustEnterAChartAccount"

				if (DistributionPercent changed)
					invoke UpdateTotals IntercompanyBillingDistributionCode
						invoked.OldDistributionPercent = old DistributionPercent
						invoked.NewDistributionPercent = DistributionPercent


		Delete is a Delete Action
			Action Rules
				invoke UpdateTotals IntercompanyBillingDistributionCode
					invoked.OldDistributionPercent = DistributionPercent
					invoked.NewDistributionPercent = blank

