VendorCompanyDefault is a BusinessClass
    owned by ap
    prefix is APX
    classic name is APCPYVND

    Ontology
        symbolic key is VendorCompanyDefault
            classic set name is APXSET1
            classic name for VendorLocation is LOCATION-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        TermsCode
            classic name is TERM-CODE
            default label is "PaymentTerms"
        TaxExemptCode             is AlphaUpper size 1
            classic name is TAX-EXEMPT-CD
        TaxCode
        DistributionCode          is a PayablesDistributionCode
            classic name is DIST-CODE
        AccrualCode               is a PayablesAccrualCode
            classic name is ACCR-CODE
        DiscountCode              is a PayablesDiscountCode
            classic name is DISC-CODE
        CashCode
        IncomeCode                is a PayablesIncomeCode
        AuthorityCode             is a PayablesAuthorityCode
            classic name is AUTH-CODE
        BankTransactionCode
            classic name is BANK-INST-CODE
            default label is "PaymentCode"
        PoEdiNumber               is AlphaUpper size 15
        	default label is "OriginalP\OIssue"
        RevisedEDINumber          is an EDINumber
            classic name is REV-EDI-NUMBER
            default label is "RevisedP\OIssue"
        TaxUsageCode
            classic name is TAX-USAGE-CD
        DiversityCode             is a PayablesDiversityCode
            classic name is DIVERSE-CODE
        RetainageAccrualCode      is a PayablesAccrualCode
            classic name is RET-ACCR-CODE
        SecondaryWithholdingCode1 is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE1
        SecondaryWithholdingCode2 is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE2
        SecondaryWithholdingCode3 is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE3
        Affiliate                 is Boolean
            classic name is AFFILIATE-FL
        InvoiceRoutingCategory
        Default					  is Boolean
        	default label is "ExpenseManagementDefault"
		AffiliateOwnership           	is AlphaUpper size 1
            States
                ParentEnterprise       value is "P"
                SubsidiaryEnterprise   value is "S"
                FellowEnterprise	   value is "F"
		ProcessLevel				is a PayablesProcessLevel


	Derived Fields
		AffiliateOwnershipRequiredMsg is a MessageField
			"AffiliateOwnershipIsRequired"
                
    Local Fields


    
    Field Rules





		TaxUsageCode
			constraint (TaxCode entered)
				"Tax_\Usage_\CodeNotValidIfNo_\Tax_\CodeIsEntered"         

			constraint (TaxUsageCodesRel exist)
				"Tax_\Usage_\CodeNotDefined;MustDefineIn_\T\X07.1"         

		AccrualCode
			constraint (AccrualCodesRel exist)
				"InvoiceAccrualCodeDoesNotHaveDetailsDefined"      
			






		RetainageAccrualCode
			constraint (ContractGroupsRel exist)
				"CannotAddRetainageAccrualCode,ReqsContractManagement"     
			



		CashCode
			constraint (!VendorLocation.LocationType.PurchaseFrom)
				"CannotDefineCashCodeForPurchaseFromLocation"              

		BankTransactionCode
			constraint (!VendorLocation.LocationType.PurchaseFrom)
				"CanootDefinePaymentCodeForPurchaseFromLocation"           

		SecondaryWithholdingCode1
			constraint (SecondaryWithholdingCode1DetailRel exists)
				"PayablesSecondaryWithholdingCodeDetailDoesNotExistForCompany<Company>"

		SecondaryWithholdingCode2
			constraint (SecondaryWithholdingCode2DetailRel exists)
				"PayablesSecondaryWithholdingCodeDetailDoesNotExistForCompany<Company>"

			constraint (SecondaryWithholdingCode2 != SecondaryWithholdingCode1)
				"SecondaryWithholdingCodeCannotBeDuplicated"               

		SecondaryWithholdingCode3
			constraint (SecondaryWithholdingCode3DetailRel exists)
				"PayablesSecondaryWithholdingCodeDetailDoesNotExistForCompany<Company>"

			constraint (SecondaryWithholdingCode3 != SecondaryWithholdingCode1
			and         SecondaryWithholdingCode3 != SecondaryWithholdingCode2)
				"SecondaryWithholdingCodeCannotBeDuplicated"               
    
 		AffiliateOwnership
			if (not Affiliate)
				initialize
			else
				if (Company.ClaimsAndLiabilitiesReporting
	 			and Company.AccountingEntity.AddressCode.PostalAddress.Country != Vendor.VendorAddressesRel.PostalAddress.Country)
	 				required			

	Conditions
    	RecordExists
    		restricted
    		when (VendorCompanyDefault.Company entered)

		DisplayAffiliateOwnershipRequiredMsg
			restricted
			when (Company.ClaimsAndLiabilitiesReporting
			and Company.AccountingEntity.AddressCode.PostalAddress.Country != Vendor.VendorAddressesRel.PostalAddress.Country
 			and Affiliate
 			and AffiliateOwnership not entered)
 
 		IsForeignVendor
 			restricted
 			when (Company.AccountingEntity.AddressCode.PostalAddress.Country != Vendor.VendorAddressesRel.PostalAddress.Country)


    Relations

















		AccrualCodesRel
			one-to-many relation to PayablesAccrualCodeDetail
			Field Mapping uses Set2
				related.VendorGroup			= VendorGroup
				related.PayablesAccrualCode	= AccrualCode
			Instance Selection
				where (related.Active)
	






		ContractGroupsRel
			one-to-many relation to ContractGroup
			Field Mapping uses symbolic key

		TaxUsageCodesRel
			one-to-many relation to TaxUsageCode
			Field Mapping uses Set2
				related.TaxUsageCode = TaxUsageCode

		SecondaryWithholdingCode1DetailRel
			one-to-one relation to PayablesSecondaryWithholdingCodeDetail
			Field Mapping uses symbolic key	
				related.VendorGroup 						= VendorGroup
				related.PayablesSecondaryWithholdingCode	= SecondaryWithholdingCode1
				related.Company								= Company 
 
		SecondaryWithholdingCode2DetailRel
			one-to-one relation to PayablesSecondaryWithholdingCodeDetail
			Field Mapping uses symbolic key
				related.VendorGroup 						= VendorGroup
				related.PayablesSecondaryWithholdingCode	= SecondaryWithholdingCode2
				related.Company								= Company 
 
		SecondaryWithholdingCode3DetailRel
			one-to-one relation to PayablesSecondaryWithholdingCodeDetail
			Field Mapping uses symbolic key
				related.VendorGroup 						= VendorGroup
				related.PayablesSecondaryWithholdingCode	= SecondaryWithholdingCode3
				related.Company								= Company 

		DefaultVendorCompanyDefaultRel
			one-to-many relation to VendorCompanyDefault
			Field Mapping uses symbolic key
				related.VendorGroup						= VendorGroup
				related.Vendor 							= Vendor
				related.VendorLocation					= blank
			Instance Selection
				where (related.Default
				and    related.Company					!= this instance.Company)

	Action Exit Rules
		if (Default)
			for each DefaultVendorCompanyDefaultRel
				invoke Update each
					invoked.Default = false
						
    Actions
		Create is a Create Action
			Exit Rules
				constraint (VendorCompanyDefault.Company.VendorGroup = VendorCompanyDefault.Vendor.VendorGroup)
					"Vendor<VendorCompanyDefault.Vendor>BelongsToVendorGroup<VendorCompanyDefault.Vendor.VendorGroup>.TheVendorGroupAssignedToCompany<VendorCompanyDefault.Company>Is<VendorCompanyDefault.Company.VendorGroup>."
				
		Update is an Update Action

		Delete is a Delete Action
		
		Purge is a Purge Action
    		restricted
    
    Sets

        Set2	
            indexed
            Sort Order
                Company
                Vendor
                VendorLocation
                VendorGroup
