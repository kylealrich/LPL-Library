UserSession is a BusinessClass
	owned by security
	prefix is USSE
	stored in environment

	Ontology
		symbolic key is SessionId

	Patterns
		implements CRUD
		implements SecurityCache	

    Persistent Fields
		UserName				is Alpha 200
		Actor					is Alpha 80
		Identity				is Alpha 200
		Service					is AlphaUpper 100
		SSODomain				is AlphaUpper 20
        IsAnonymous             is Boolean
            default label is "AnonymousUserSession"
		LoginTime				is TimeStamp
		LastAccessTime 			is TimeStamp
		ExpirationTime			is TimeStamp
		MaxInactiveInterval		is Numeric size 10

	Actions
		Create is a Create Action
		Update is an Update Action
		Delete is a Delete Action
		PurgeExpiredSessionsAsync is a Set Action
			run in background

			completion message is "PurgeExpiredSessionsAsyncSubmitted"						

			Instance Selection
				where ExpirationTime < system current timestamp
				
			Action Rules
				Instance Rules
						invoke Delete 
		PurgeExpiredSessionsNow is a Set Action
			run in foreground
			
			completion message is "PurgeExpiredSessionsNowCompleted"
			
			Instance Selection
				where ExpirationTime < system current timestamp
			
			Action Rules
				Instance Rules
						invoke Delete 	
		PurgeExpiredSessionsForUserName is a Set Action
			run in foreground
			
			Parameters
				ParamUserName is Alpha 200
				    default label is "UserName"
		
			Parameter Rules
				ParamUserName required
				
			completion message is "PurgeExpiredSessionsForUserNameCompleted"
			
			Instance Selection
				where (ExpirationTime < system current timestamp
				and    UserName = ParamUserName)
							
			Action Rules
				Instance Rules
					invoke Delete 						

	Relations
	
		AllExpiredUserSessions
			one-to-many relation to UserSession
			Field Mapping uses symbolic key	
			Instance Selection		
				where related.ExpirationTime < system current timestamp
			
	Sets
		SessionIdSet
			indexed
			no duplicates
			Sort Order
				SessionId
				UserName
				
		UserNameSet
			indexed
			duplicates
			Sort Order
				UserName		

		ByLoginTime
			indexed
			Sort Order	
				LoginTime descending
				SessionId
				
		ByLastAccessTime
			indexed
			Sort Order	
				LastAccessTime descending
				SessionId		

		ByExpirationTime
			indexed
			Sort Order	
				ExpirationTime descending
				SessionId					
