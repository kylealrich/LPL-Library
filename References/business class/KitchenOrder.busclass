KitchenOrder is a BusinessClass
	owned by recipe
	prefix is KTC
	
	Ontology
		symbolic key is KitchenOrder
		
	
	Patterns
		disable AuditIndex
		implements StaticJava
		
	Persistent Fields
		Recipe
		Item
		Description
		BatchYieldQuantity						is an UnsignedQuantity
			protected
			default label is "YieldQuantity"
			precision is Item.NumberOfDecimalsQuantity
		BatchYieldUOM							is a UnitOfMeasure
			default label is "YieldUOM"
			protected
		BatchYieldUOMMultiplier					is an UOMMultiplier
			protected
		BatchQuantity							is Numeric size 5
			default label is "NumberOfBatches"
		PlanTotalYieldQuantity					is an UnsignedQuantity
			protected
			default label is "PlannedTotalYield"
			precision is Item.NumberOfDecimalsQuantity

		SecondaryYieldQuantity					is an UnsignedQuantity
			protected
			precision is Item.NumberOfDecimalsQuantity
		SecondaryYieldUOM						is a UnitOfMeasure
			protected
		SecondaryYieldUOMMultiplier				is an UOMMultiplier
			protected
		PlanTotalSecondaryYieldQuantity			is an UnsignedQuantity
			protected
			precision is Item.NumberOfDecimalsQuantity

		StartDate				
		EndDate									is Date
		DateNeeded								is Date 
		Notes									is Text
		MakeCode								is Numeric size 1
			States
				MakeToOrder value is "1"
				MakeToStock value is "2"
		Status									is Numeric size 1
			States
				Open		value is 0
				Processing	value is 1


				Preparing	value is 2
				Completed	value is 3
				Cancelled	value is 4
		EstimatedIngredientTotal				is an InternationalAmount
		EstimatedComponentTotal					is an InternationalAmount
		EstimatedPackagingTotal					is an InternationalAmount
		EstimatedBurdenTotal					is an InternationalAmount
		EstimatedOrderTotal						is an InternationalAmount
			default label is "EstimatedBatchTotal"
		EstimatedPerYieldCost					is an InternationalCost
		EstimatedUnitCost						is an InternationalCost
		ActualBatchQuantity						is Numeric size 5
		ActualTotalYieldQuantity				is an UnsignedQuantity
			precision is Item.NumberOfDecimalsQuantity
				round to precision
		ActualTotalSecondaryYieldQuantity		is an UnsignedQuantity
			precision is Item.NumberOfDecimalsQuantity
				round to precision
		ActualIngredientTotal					is an InternationalAmount
		ActualComponentTotal					is an InternationalAmount
		ActualPackagingTotal					is an InternationalAmount
		ActualBurdenTotal						is an InternationalAmount
		ActualOrderTotal						is an InternationalAmount
			default label is "ActualBatchTotal"
		ActualUnitCost							is an InternationalCost
			precision is Item.NumberOfDecimalsCost
		KitchenOffsetAccount					is a FinanceCodeBlock
		BurdenAbsorptionAccount					is a FinanceCodeBlock
		WasteAccount							is a FinanceCodeBlock
		WasteQuantityInYieldUOM					is an UnsignedQuantity
			precision is Item.NumberOfDecimalsQuantity
		WasteQuantityInSecondaryUOM				is an UnsignedQuantity
			precision is Item.NumberOfDecimalsQuantity
		HeaderWasteAmount						is an InternationalAmount
		WasteIngredientTotal					is an InternationalAmount
		WasteComponentTotal						is an InternationalAmount
		WastePackagingTotal						is an InternationalAmount
		LineWasteTotal							is an InternationalAmount
		WasteTotal								is an InternationalAmount
		LineCount								is Numeric size 6
			disable Auditing
			protected
			restricted
		InstructionCount						is Numeric size 6
			disable Auditing
			protected
			restricted
		OriginatingTransaction					is BusinessObjectReference
		RequestingCompanyAndLocation			
		WaitingForIngredients					is Boolean						
		KitchenRequisitionsEnabled				is Boolean
			protected
		Bin
		MultipleBins							is Boolean
		ServingSize								is like UnsignedQuantity
			protected
			precision is Item.DerivedNumberOfDecimalsRecipe
		ServingSizeUOM							is a RecipeUnitOfMeasure
			protected
		ServingSizeUOMMultiplier				is a UOMMultiplier
			protected
		IDMUniqueID								is a IDMPID
			protected
		IDMJob									is like IDMJob
			protected		
		IDMPrinter	
			default label is "Printer"
	
	Local Fields
		UOMCalculation
		IDMXMLDefinition
		IDMItem
		LocalCompany			is like InventoryCompany
		LocalInventoryLocation 	is like InventoryLocation
		LocalKitchenOrder		is like KitchenOrder
		RoundedValue
		RoundToRecipeDecimals	is Boolean
		LocalInputQuantity		is an UnsignedQuantity
		LocalRoundedQuantity	is an UnsignedQuantity
		AttributeCtr			is Numeric 2
		LocalExecute			is Boolean
	
	Sets
		ByRequestingCompanyAndLocation
			Sort Order
				RequestingCompanyAndLocation
				Company
				InventoryLocation
				KitchenOrder

		ByOriginatingTransaction
			Sort Order
				OriginatingTransaction
				Company
				InventoryLocation
				KitchenOrder
		
		ByRecipeItem
			Sort Order
				Item
				Company
				InventoryLocation
				KitchenOrder
		
	Rule Blocks
		ValidateKitchenOrder
			constraint (Company.ItemGroup.RecipeManagementInUse)
				"ItemGroup<Company.ItemGroup>NotSetupFor_\Kitchen_\Management"
			constraint (Company.KitchenOrderInUse)
				"Kitchen_\OrderNotEnabledFor_\Inventory_\Company<Company>"
			constraint (InventoryLocation.RecipeEnabled)
				"Kitchen_\ManagementNotEnabledFor_\Inventory_\Location<InventoryLocation>"
			constraint (Recipe.Active)
				"MustUseActiveRecipeForKitchenOrders")
					
		CreateKitchenOrderLinesFromRecipe
			for each Recipe.RecipeLine set
				invoke Create KitchenOrderLine
					fill in fields from each
					invoked.Company				= Company
					invoked.InventoryLocation	= InventoryLocation
					invoked.KitchenOrder		= KitchenOrder
	
		CreateKitchenOrderInstructionsFromRecipe
			for each Recipe.RecipeInstruction set
				invoke Create KitchenOrderInstruction
					fill in fields from each
						except invoked.Attachment.FSMAttachmentIDM
					invoked.Company				= Company
					invoked.InventoryLocation	= InventoryLocation
					invoked.KitchenOrder		= KitchenOrder

					invoked.Attachment.File		= each.Attachment.File
					invoked.Attachment.MimeType = each.Attachment.MimeType
					invoked.Attachment.Title	= each.Attachment.Title			
	
		CreateKitchenOrderDetailForSingleBin
			if (Bin entered and not MultipleBins)
				if (KitchenOrderDetail set not exists)
					invoke Create KitchenOrderDetail
						invoked.Company				= Company
						invoked.InventoryLocation	= InventoryLocation
						invoked.KitchenOrder		= KitchenOrder
						invoked.Bin					= Bin
						invoked.Quantity			= ActualTotalYieldQuantity
						invoked.UnitOfMeasure		= BatchYieldUOM
						invoked.SecondaryQuantity	= ActualTotalSecondaryYieldQuantity

		CreateWarehouseSupplyHeaderRecord
			invoke Create WarehouseDemand
				invoked.Company									= Company
				invoked.WarehouseDemand.DemandSystemCode		= "KO"
				invoked.WarehouseDemand.DemandDocument			= KitchenOrder
				invoked.WarehouseDemand.DemandCompany			= Company
				invoked.DocumentNumberNumeric					= KitchenOrder
				invoked.Destination								= InventoryLocation
				if (InventoryLocation.WarehouseProcessType entered)
					invoked.RequirePickingFeedback				= InventoryLocation.WarehouseProcessType.RequirePickingFeedback
					invoked.RequirePackingFeedback				= InventoryLocation.WarehouseProcessType.RequirePackingFeedback
				invoked.DestinationName							= InventoryLocation.Name
				invoked.DestinationAddress						= InventoryLocation.PostalAddress
				invoked.RequireFreight							= "N"
			
		CreateOrUpdateWarehouseSupplyLineRecord
			if (WarehouseSupplyLineRel not exists)
				invoke CreateSupply WarehouseDemandLine
					invoked.Company									= Company
					invoked.InventoryLocation						= InventoryLocation
					invoked.WarehouseDemandLine.DemandDocumentType	= DemandDocumentType.KitchenOrder
					invoked.WarehouseDemand.DemandSystemCode		= DemandSystemCode.KitchenOrder
					invoked.WarehouseDemand.DemandCompany			= Company
					invoked.WarehouseDemand.DemandDocument			= KitchenOrder
					invoked.WarehouseDemandLine.LineNumber			= 1
					invoked.Item									= Item
					invoked.EstimatedTransactionDate				= DateNeeded
					invoked.LineType								= "I"
					if (Item.SecondaryUOM entered)
						invoked.TransactionUOM						= SecondaryYieldUOM
						invoked.TransactionUOMMultiplier			= SecondaryYieldUOMMultiplier
						invoked.Quantity							= PlanTotalSecondaryYieldQuantity
					else
						invoked.TransactionUOM						= BatchYieldUOM
						invoked.TransactionUOMMultiplier			= BatchYieldUOMMultiplier
						invoked.Quantity							= PlanTotalYieldQuantity
					invoked.DemandRecordType						= WarehouseDemandLine.DemandRecordType.Supply

			else
				if (WarehouseSupplyLineRel exist)
					if (PlanTotalYieldQuantity > 0)
	        			invoke Update WarehouseSupplyLineRel
	        				invoked.TransactionUOM							= BatchYieldUOM
	        				invoked.TransactionUOMMultiplier				= BatchYieldUOMMultiplier
	        				invoked.Quantity								= PlanTotalYieldQuantity
	        				invoked.EstimatedTransactionDate				= DateNeeded
	        				invoked.InventoryLocation						= InventoryLocation
        			else
        				invoke Delete WarehouseSupplyLineRel		

		RecalculateLineHeaderAndBurdens
			for each KitchenOrderLine set
				invoke RecalculateAmounts each
		
			invoke RecalculateHeaderTotals

			if (KitchenOrderBurden set exists)
				for each FinishedGoodBurdenTypeRel
					invoke RecalculateBurden each
				
				invoke RecalculateHeaderBurdenTotals
				
		RoundQuantity
			initialize RoundedValue
			RoundedValue.RoundInput = LocalInputQuantity
			if (RoundToRecipeDecimals)
				RoundedValue.RoundTo = (1/10^Item.DerivedNumberOfDecimalsRecipe)
			else
				RoundedValue.RoundTo = (1/10^Item.NumberOfDecimalsQuantity)
			RoundedValue.RoundingType = RoundingType.Normal
			
			LocalRoundedQuantity = RoundedValue.RoundResult
				
	Relations
		ZeroCostKitchenOrderLinesRel is a KitchenOrderLine set
			Instance Selection
				where (related.InventoryCost = 0) 
				
		OriginalKitchenOrderRel
			one-to-one relation to KitchenOrder
			Field Mapping uses symbolic key
				related.Company				= LocalCompany
				related.InventoryLocation	= LocalInventoryLocation
				related.KitchenOrder		= LocalKitchenOrder
				
		ItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.Item				= Item
	
		OriginatingRequisitionRel
			one-to-one relation to Requisition
			Field Mapping uses symbolic key
				related.Company					= DerivedRequestingCompany
				related.Requisition				= DerivedRequisition
		
		OriginatingRequisitionLineRel
			one-to-one relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company					= DerivedRequestingCompany
				related.Requisition				= DerivedRequisition
				related.RequisitionLine			= DerivedRequisitionLine
		
		OriginatingRequisitionLineDemandRel
			one-to-one relation to WarehouseDemandLine
			Field Mapping uses symbolic key
				related.Company											= Company
				related.WarehouseDemand.DemandSystemCode				= DemandSystemCode.Requisitions
				related.WarehouseDemand.DemandDocument					= DerivedRequisition
				related.WarehouseDemand.DemandCompany					= DerivedRequestingCompany
				related.InventoryLocation								= InventoryLocation
				related.Item											= Item
				related.WarehouseDemandLine.DemandDocumentType			= DemandDocumentType.Issue
				related.WarehouseDemandLine.LineNumber					= DerivedRequisitionLine
				related.WarehouseDemandLine.ComponentSequence   		= 0				
		

		RequisitionLinesRel
			one-to-many relation to RequisitionLine
			Field Mapping uses symbolic key
				related.Company					= DerivedRequestingCompany
				related.Requisition				= DerivedRequisition
		

		OpenInventoryTypeRequisitionLinesForItemRel
			one-to-many relation to RequisitionLine
			Field Mapping uses ByItem
				related.Item					= Item
			Instance Selection
				where  (related.FromCompanyLocationBin.FromCompany	= Company
				and		related.FromCompanyLocationBin.FromLocation	= InventoryLocation
				and		related.IsInventory
				and		related.DemandExists
				and		not related.CreatePurchaseOrder)
		
		ItemUOMRel
        	one-to-one relation to ItemUOM
        	Field Mapping uses symbolic key
        		related.ItemGroup         = Item.ItemGroup
        		related.Item              = Item
        		related.UnitOfMeasure     = BatchYieldUOM
		
		ServingItemRecipeUOMRel
			one-to-one relation to ItemRecipeUOM
			Field Mapping uses symbolic key
        		related.ItemGroup        				= Item.ItemGroup
        		related.Item              				= Item
        		related.RecipeUnitOfMeasure	 			= ServingSizeUOM				
		
		FinishedGoodBurdenTypeRel is a KitchenOrderBurden set
			Instance Selection
				where (related.IsAmountPerFinishedGoodsUnit
				or     related.IsPercentOfFinishedGoodsInventoryCost
				or     related.IsFlatAmountBurden
				or     related.IsAmountPerBurdenUnit)
		




















				
		WarehouseSupplyLineRel
			one-to-one relation to WarehouseDemandLine
			Field Mapping uses symbolic key
				related.Company											= Company
				related.WarehouseDemand.DemandSystemCode				= DemandSystemCode.KitchenOrder
				related.WarehouseDemand.DemandDocument					= KitchenOrder
				related.WarehouseDemand.DemandCompany					= Company
				related.InventoryLocation								= InventoryLocation
				related.Item											= Item
				related.WarehouseDemandLine.DemandDocumentType			= DemandDocumentType.KitchenOrder
				related.WarehouseDemandLine.LineNumber					= 1
				related.WarehouseDemandLine.ComponentSequence   		= 0
				
		IDMJobRel
			one-to-one relation to IDMJob
			Field Mapping uses symbolic key
				related.IDMJob = IDMJob
	
		KitchenOrderLineRel 
			one-to-many relation to KitchenOrderLine
			Field Mapping uses BySequence
				related.Company				= Company
				related.InventoryLocation	= InventoryLocation
				related.KitchenOrder		= KitchenOrder
			
		KitchenOrderInstructionRel 
			one-to-many relation to KitchenOrderInstruction
			Field Mapping uses BySequence
				related.InventoryLocation	= InventoryLocation
				related.KitchenOrder		= KitchenOrder

		IDMAdditionalAttributesLinesRel
			one-to-many relation to IDMAdditionalAttributesLines
			Field Mapping uses symbolic key
				related.IDMAdditionalAttributesHeader = "FSM_KitchenOrder"
			Instance Selection
				where(related.IDMAdditionalAttributesHeader.ActivateAdditionalAttributes
				and	  related.ActivateAdditionalAttributes.Active)
		
	Transient Fields
		TransientActualBatchQuantity				is like UnsignedQuantity
			derive value from DerivedActualBatchQuantity
		TransientActualTotalYieldQuantity			is like UnsignedQuantity
			precision is Item.NumberOfDecimalsQuantity
			derive value from DerivedActualTotalYieldQuantity
		TransientActualTotalSecondaryYieldQuantity	is like UnsignedQuantity
			precision is Item.NumberOfDecimalsQuantity
			derive value from DerivedActualTotalSecondaryYieldQuantity
		TransientCompleteAll	is Boolean
			default label is "PlannedQuantitiesComplete"
		
	Field Rules
		Item
			required
			constraint (ItemLocationRel exists)
				"Item<Item>DoesNotExistAtLocation<InventoryLocation>"
				
		Recipe
			force default to Item

		Notes
			default to Recipe.Notes
					
		Description
			required
			initial value is Item.Description
			force default to Item.Description
	
		EndDate
			if (StartDate entered)
				constraint (EndDate >= StartDate)
					"StartDateIsLessThanEndDate"
			else
				cannot be entered
					"MustEnterStartDateIfEndDateIsEntered"
					
		DateNeeded
			initial value is current corporate date
			default to current corporate date
			if (action type.Create
			or  IsOpen)
				constraint (DateNeeded >= current corporate date)
					"DateNeededCannotBeLessThanToday'sDate"
			
		MakeCode
			initial value is MakeCode.MakeToStock
			default to MakeCode.MakeToStock
	
		BatchYieldQuantity
			LocalInputQuantity = Recipe.BatchYieldQuantity
			RoundToRecipeDecimals = false
			include RoundQuantity
			default to LocalRoundedQuantity
			
		SecondaryYieldQuantity
			LocalInputQuantity = Recipe.SecondaryYieldQuantity
			RoundToRecipeDecimals = false
			include RoundQuantity
			default to LocalRoundedQuantity
			
		BatchYieldUOM
			required
			initial value is Recipe.BatchYieldUOM
			force default to Recipe.BatchYieldUOM

		SecondaryYieldUOM
			initial value is Recipe.SecondaryYieldUOM
			force default to Recipe.SecondaryYieldUOM

		BatchYieldUOMMultiplier
			default to ItemUOMRel.UOMConversion
		
		SecondaryYieldUOMMultiplier
			force default to Item.SecondaryUOMConversion
			
		BatchQuantity
			required
			
			constraint (BatchQuantity >= 0)
				"CannotEnterNegativeBatchQuantity"
				
			if (BatchQuantity changed)
				initialize PlanTotalYieldQuantity
				initialize PlanTotalSecondaryYieldQuantity
			
		PlanTotalYieldQuantity
			LocalInputQuantity = (BatchYieldQuantity * BatchQuantity)
			RoundToRecipeDecimals = false
			include RoundQuantity
			PlanTotalYieldQuantity = LocalRoundedQuantity
					
		PlanTotalSecondaryYieldQuantity
			LocalInputQuantity = (SecondaryYieldQuantity * BatchQuantity)
			RoundToRecipeDecimals = false
			include RoundQuantity
			PlanTotalSecondaryYieldQuantity = LocalRoundedQuantity

		ServingSize
			constraint (ServingSize decimals <= Item.DerivedNumberOfDecimalsRecipe)
				"TooManyDecimalDigitsEnteredForQuantity:MaxForItem<Item>Is<Item.DerivedNumberOfDecimalsRecipe>"
			
			LocalInputQuantity = Recipe.ServingSize
			RoundToRecipeDecimals = true
			include RoundQuantity
			default to LocalRoundedQuantity

		ServingSizeUOM
			force default to Recipe.ServingSizeUOM
			constraint (ServingItemRecipeUOMRel exists)
				"<ServingSizeUOM>IsAnInvalidUnitOfMeasureForItem<Item>"				
		
		ServingSizeUOMMultiplier
			force default to ServingItemRecipeUOMRel.UOMConversion
			
		EstimatedOrderTotal
			if (EstimatedOrderTotal changed)
				initialize EstimatedPerYieldCost
				
		EstimatedPerYieldCost
			default to EstimatedOrderTotal / BatchQuantity
		
		EstimatedUnitCost
			default to EstimatedPerYieldCost / BatchYieldQuantity
			
		KitchenOffsetAccount
			default to InventoryLocation.InventoryLocationExtension.KitchenOffsetAccount
		
		WasteAccount
			default to InventoryLocation.WasteAccount

		ActualBatchQuantity
			if (ActualBatchQuantity changed)
				initialize ActualTotalYieldQuantity
				initialize ActualTotalSecondaryYieldQuantity

		ActualTotalYieldQuantity
			if (ActualBatchQuantity entered)
				default to (BatchYieldQuantity * ActualBatchQuantity)
				
		ActualTotalSecondaryYieldQuantity
			if (ActualBatchQuantity entered)
				default to (SecondaryYieldQuantity * ActualBatchQuantity)
		
		HeaderWasteAmount
			force default to DerivedHeaderWasteAmount
		
		WasteTotal
			force default to HeaderWasteAmount + LineWasteTotal
			
		Bin
			if (not ItemLocationRel.BinTracked)
				cannot be entered
					"RecipeItem<Item>IsNotBinTrackedAtLocation<InventoryLocation>"
			else
				if (not MultipleBins)
					default to ItemLocationRel.PreferredBin

		MultipleBins
			if (not ItemLocationRel.BinTracked)
				cannot be entered
					"RecipeItem<Item>IsNotBinTrackedAtLocation<InventoryLocation>"			
			else
				if (MultipleBins)
					initialize Bin
				else
					if (MultipleBins changed)
						constraint (KitchenOrderDetail set not exists)
							"DeleteDetailsBeforeSettingMultipleBinsFlagToFalse"
		
		TransientActualBatchQuantity
			initial value is BatchQuantity
			
		TransientActualTotalYieldQuantity
			initial value is PlanTotalYieldQuantity
		
		TransientActualTotalSecondaryYieldQuantity
			initial value is PlanTotalSecondaryYieldQuantity
		
		TransientCompleteAll
			initial value is true
			
		IDMPrinter
			initial value is Company.UserDefaultPrinterRel.IDMPrinter
			default to Company.UserDefaultPrinterRel.IDMPrinter 
				
	Conditions
		HasZeroCostLinesExists
			restricted
			when (ZeroCostKitchenOrderLinesRel exists)

		IsRecordExists
			restricted
			when (KitchenOrder exists)
	
		IsOpen
			restricted
			when (Status.Open)
			
		IsWaiting
			restricted
			when (Status.Processing
			and   WaitingForIngredients)
			
		IsReady
			restricted
			when (Status.Processing
			and	  not WaitingForIngredients)
		
		IsPreparing
			restricted
			when (Status.Preparing)
			
		IsCompleted
			restricted
			when (Status.Completed)
		
		IsCancelled
			restricted
			when (Status.Cancelled)
		
		IsNotCompletedOrCancelled
			restricted
			when (Status.Open
			or	  Status.Processing
			or	  Status.Preparing)
		
		IsSingleBinTracked
			restricted
			when (ItemLocationRel.BinTracked and not MultipleBins)
		
		MultipleDetailsAllowed
			restricted
			when (MultipleBins
			or	  ItemLocationRel.LotTracked.LotRequiredAtReceipt
			or	  ItemLocationRel.SerialTracked.SerialRequiredAtReceipt)
		
		IsReadyForPreparation
			restricted
			when ((Status.Open and not KitchenRequisitionsEnabled)
			or    (Status.Processing and not WaitingForIngredients))
			
		HasIDMJobError
			restricted	
			when (HasIDMJob and (IDMJobRel.Status.TimedOut or IDMJobRel.Status.Failed))		
			
		HasIDMDocumentAndPrinter
			restricted	
			when (HasIDMDocument and IDMPrinter entered)
			
		HasIDMDocument
			restricted
			when (Company.UseIDM and IDMJobRel.Status.Finished and IDMUniqueID entered)			
			
		HasIDMJob
			restricted
			when (Company.UseIDM and IDMJobRel exists)		
			
		KitchenOrderInUseWithoutIDMTemplate
			restricted
			when (Company.KitchenOrderInUse 
			and not Company.UseIDM
			and Company.KitchenOrderTemplate not entered)
			
		ValidForGeneration
			restricted
			when (Company.KitchenOrderInUse 
			and Company.UseIDM
			and Company.KitchenOrderTemplate entered)	
			
		ValidForReGeneration
			restricted
			when (Company.KitchenOrderInUse 
			and Company.UseIDM
			and Company.KitchenOrderTemplate entered
			and IsPreparing)			
		
	Derived Fields
		DerivedKitchenOrderTitle is a DerivedField
			type is MessageField
			restricted
			if (KitchenOrder exists)
				return KitchenOrderTitleWithRecord
			else
				return KitchenOrderTitle
				
		KitchenOrderTitle is a LabelField
			restricted
			"KitchenOrder"

		KitchenOrderTitleWithRecord is a LabelField
			restricted
			"KitchenOrder_<KitchenOrder>"

		ItemDescriptionLabel is a LabelField
			"<Item.Description>"
		
		PlannedYieldAndUOMDisplay is a LabelField
			"<PlanTotalYieldQuantity>_<BatchYieldUOM>"
		
		PlannedYieldInSecondaryUOMDisplay is a LabelField
			"<PlanTotalSecondaryYieldQuantity>_<SecondaryYieldUOM>"
		
		ActualYieldAndUOMDisplay is a LabelField
			"<ActualTotalYieldQuantity>_<BatchYieldUOM>"
			
		ActualYieldInSecondaryUOMDisplay is a LabelField
			"<ActualTotalSecondaryYieldQuantity>_<SecondaryYieldUOM>"
		
		DerivedPlannedYield is a DerivedField
			type is MessageField
			if (Item.SecondaryUOM entered)
				return PlannedYieldAndUOMDisplay + " / " + PlannedYieldInSecondaryUOMDisplay
			else
				return PlannedYieldAndUOMDisplay
		
		DerivedActualYield is a DerivedField
			type is MessageField
			if (Item.SecondaryUOM entered)
				return ActualYieldAndUOMDisplay + " / " + ActualYieldInSecondaryUOMDisplay
			else
				return ActualYieldAndUOMDisplay
		
		DerivedEstimatedServingYieldQuantity is a DerivedField
			type is like UnsignedQuantity
			if (ServingSize entered)
				if (BatchYieldUOM != ServingSizeUOM)
					initialize UOMCalculation
					UOMCalculation.InputQuantity 		= PlanTotalYieldQuantity
					UOMCalculation.InputUOM      		= BatchYieldUOM
					UOMCalculation.InputToUOM	 		= ServingSizeUOM
					UOMCalculation.InputToUOMConversion = ServingSizeUOMMultiplier
					UOMCalculation.Method        		= UOMCalculation.Method.ConvertToAlternate
					return UOMCalculation.OutputQuantity / ServingSize
				else
				    return PlanTotalYieldQuantity / ServingSize
				
		DerivedActualServingYieldQuantity is a DerivedField
			type is like UnsignedQuantity
			if (ServingSize entered)
				if (BatchYieldUOM != ServingSizeUOM)
					initialize UOMCalculation
					UOMCalculation.InputQuantity 		= PlanTotalYieldQuantity
					UOMCalculation.InputUOM      		= BatchYieldUOM
					UOMCalculation.InputToUOM	 		= ServingSizeUOM
					UOMCalculation.InputToUOMConversion = ServingSizeUOMMultiplier
					UOMCalculation.Method       		= UOMCalculation.Method.ConvertToAlternate
				return UOMCalculation.OutputQuantity / ServingSize
			else
				return PlanTotalYieldQuantity / ServingSize
		
		DerivedInventoryCompanyLocationDisplay is a LabelField
			"<Company.DerivedCompanyDisplay>_|_<InventoryLocation.RepresentativeText>"
		
		IsOpenLabel is a LabelField
			"Open"

		IsWaitingLabel is a LabelField
			"WaitingForIngredients"

		IsReadyLabel is a LabelField
			"ReadyForPreparation"

		PreparingLabel is a LabelField
			"StartedPreparing"

		CompletedLabel is a LabelField
			"Completed"

		CancelledLabel is a LabelField
			"Cancelled"
		
		DerivedStatusMessage is a DerivedField
			type is MessageField
			default label is "Status"
			if (KitchenOrder exists)
				if (IsOpen)
					return IsOpenLabel
				else
				if (IsWaiting)
					return IsWaitingLabel
				else
				if (IsReady)
					return IsReadyLabel
				else
				if (Status.Preparing)
					return PreparingLabel
				else
				if (Status.Completed)
					return CompletedLabel
				else
				if (Status.Cancelled)
					return CancelledLabel
			return blank
			
		DerivedEstimatedIngredientTotal is a DerivedField
			type is like InternationalAmount
			return EstimatedIngredientTotal
		
		DerivedEstimatedComponentTotal is a DerivedField
			type is like InternationalAmount
			return EstimatedComponentTotal

		DerivedEstimatedPackagingTotal is a DerivedField
			type is like InternationalAmount
			return EstimatedPackagingTotal

		DerivedEstimatedBurdenTotal is a DerivedField
			type is like InternationalAmount
			return EstimatedBurdenTotal

		DerivedEstimatedOrderTotal is a DerivedField
			type is like InternationalAmount
			return EstimatedOrderTotal
		
		DerivedActualIngredientTotal is a DerivedField
			type is like InternationalAmount
			return ActualIngredientTotal
		
		DerivedActualComponentTotal is a DerivedField
			type is like InternationalAmount
			return ActualComponentTotal
		
		DerivedActualPackagingTotal is a DerivedField
			type is like InternationalAmount
			return ActualPackagingTotal
		
		DerivedActualBurdenTotal is a DerivedField
			type is like InternationalAmount
			return ActualBurdenTotal
		
		DerivedActualOrderTotal is a DerivedField
			type is like InternationalAmount
			return ActualOrderTotal

		DerivedWasteIngredientTotal is a DerivedField
			type is like InternationalAmount
			return WasteIngredientTotal
		
		DerivedWasteComponentTotal is a DerivedField
			type is like InternationalAmount
			return WasteComponentTotal
		
		DerivedWastePackagingTotal is a DerivedField
			type is like InternationalAmount
			return WastePackagingTotal
				
		DerivedWasteTotal is a DerivedField
			type is like InternationalAmount
			return WasteTotal
				
		DerivedEstimatedIngredientTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return EstimatedIngredientTotal / PlanTotalYieldQuantity
			
		DerivedEstimatedComponentTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return EstimatedComponentTotal / PlanTotalYieldQuantity
			
		DerivedEstimatedPackagingTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return EstimatedPackagingTotal / PlanTotalYieldQuantity
			
		DerivedEstimatedBurdenTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return EstimatedBurdenTotal / PlanTotalYieldQuantity
		
		DerivedEstimatedCostPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return EstimatedOrderTotal / PlanTotalYieldQuantity
			
		DerivedActualIngredientTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return ActualIngredientTotal / ActualTotalYieldQuantity
			
		DerivedActualComponentTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return ActualComponentTotal / ActualTotalYieldQuantity
			
		DerivedActualPackagingTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return ActualPackagingTotal / ActualTotalYieldQuantity
			
		DerivedActualBurdenTotalPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return ActualBurdenTotal / ActualTotalYieldQuantity
		
		DerivedActualCostPerYield is a DerivedField
			type is like UnsignedUnitCost
				precision is Item.NumberOfDecimalsCost
			return ActualOrderTotal / ActualTotalYieldQuantity
	
		DerivedHeaderWasteAmount is a DerivedField
			type is like InternationalAmount
			return ((ActualIngredientTotal / ActualTotalYieldQuantity) * WasteQuantityInYieldUOM)
			
		DerivedTotalDetailQuantity is a DerivedField
			type is like Quantity
			restricted
			return sum KitchenOrderDetail set.Quantity
			
		DerivedTotalDetailSecondaryQuantity is a DerivedField
			type is like Quantity
			restricted
			return sum KitchenOrderDetail set.SecondaryQuantity
		
		DerivedRequestingCompany is a DerivedField
			type is like Company
			if (OriginatingTransaction.BusinessClassName = "RequisitionLine")
				return OriginatingTransaction(RequisitionLine).RequisitionLine.Company
		
		DerivedRequisition is a DerivedField
			type is like Requisition
			if (OriginatingTransaction.BusinessClassName = "RequisitionLine")
				return OriginatingTransaction(RequisitionLine).RequisitionLine.Requisition
		
		DerivedRequisitionLine is a DerivedField
			type is like RequisitionLine
			if (OriginatingTransaction.BusinessClassName = "RequisitionLine")
				return OriginatingTransaction(RequisitionLine).RequisitionLine
						






	
		DerivedActualBatchQuantity					is a DerivedField
			type is like UnsignedQuantity
			restricted
			return BatchQuantity
				
		DerivedActualTotalYieldQuantity				is a DerivedField 
			type is like UnsignedQuantity
			restricted
			return PlanTotalYieldQuantity
				
		DerivedActualTotalSecondaryYieldQuantity	is a DerivedField
			type is like UnsignedQuantity
			restricted
			return PlanTotalSecondaryYieldQuantity
			
		DerivedIDMFileName					        is a DerivedField
			type is Alpha 100
			return "Company "+ Company +" Kitchen Order "+ KitchenOrder+ " - " + Item.Description
			
		FailedToCreateIDMJobMessage is a MessageField
			restricted
            "FailedToGenerate_Kitchen_Order_<KitchenOrder>_IDMDocument"
		
		IDMTemplateRequiredMessage is a MessageField
            "NoAssignedDocumentTemplateIn_Inventory_Company"
	
		IDMStatusMessage is a MessageField
            "IDM_Job_<IDMJobRel.Status>"
		
		ZeroCostLinesMessage is a MessageField
            "Unit_CostIsMissingFromOneOrMoreLineItems"
            
		DerivedIDMDocumentLink is a DerivedField
			type is Alpha 2083
			if (IDMUniqueID entered)	
				IDMItem.DocumentType = "FSM_KitchenOrder" 
				IDMItem.IDMUniqueId = IDMUniqueID
				return IDMItem.GetLink
			return blank
	
        DerivedIDMXML is a DerivedField
            type is Text
            restricted
            initialize IDMXMLDefinition

            IDMXMLDefinition.Busclass                                           = reference to this instance
            IDMXMLDefinition.ListName                                           = "KitchenOrderForIDMList"
            IDMXMLDefinition.DocumentName                                       = "KitchenOrder"
            IDMXMLDefinition.OutputFileName                                     = "KitchenOrder_" + KitchenOrder
            

			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName			= "KitchenOrderLineRel"
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName 			= "KitchenOrderLineForIDMList"
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1		= 1
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag				= "Lines"
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag				= "Line"
            

			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName			= "KitchenOrderInstructionRel"
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName 			= "KitchenOrderInstructionForIDMList"
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1		= 2
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag				= "Instructions"
			IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag				= "Instruction"
            
            constraint (IDMXMLDefinition.GenerateXML)
                "ErrorOccuredInIDMXMLDefinition.GenerateXML"
                
            return IDMXMLDefinition.OutputText	
	
	Create Rules
		KitchenRequisitionsEnabled = InventoryLocation.InventoryLocationExtension.KitchenRequisitionsEnabled
			
	Actions
		Create is a Create Action
			Action Rules
				include ValidateKitchenOrder
			Exit Rules
				include CreateKitchenOrderLinesFromRecipe
				include CreateKitchenOrderInstructionsFromRecipe

		CreateCopy is a Create Action
			restricted
			Action Rules
				include ValidateKitchenOrder

		CreateFromRecipe is a Create Action
			restricted
			Exit Rules
				include CreateKitchenOrderLinesFromRecipe
				include CreateKitchenOrderInstructionsFromRecipe
				invoke SendNotification

		CopyKitchenOrder is a Create Action
			Parameters
				PrmCompany				is an InventoryCompany
					default label is "Company"
				PrmInventoryLocation	is an InventoryLocation
					default label is "InventoryLocation"
				PrmKitchenOrder			is a KitchenOrder
					default label is "KitchenOrder"
				PrmDateNeeded			is Date
					default label is "DateNeeded"
			Parameter Rules
				PrmCompany
					required
				PrmInventoryLocation
					required
				PrmKitchenOrder
					required
				PrmDateNeeded
					initial value is PrmKitchenOrder.DateNeeded
					default to current corporate date
					constraint (PrmDateNeeded >= current corporate date)
						"DateNeededCannotBeLessThanToday'sDate"
			Action Rules
				LocalCompany			= PrmCompany
				LocalInventoryLocation	= PrmInventoryLocation
				LocalKitchenOrder		= PrmKitchenOrder
				constraint (OriginalKitchenOrderRel exists)
					"Kitchen_\OrderDoesNotExist"
				
				constraint (PrmCompany.KitchenOrderInUse)
					"Kitchen_\OrderNotEnabledFor_\Inventory_\Company<PrmCompany>"
				constraint (PrmInventoryLocation.RecipeEnabled)
					"Recipe_\ManagementNotEnabledFor_\Inventory_\Location<PrmInventoryLocation>"

				invoke CreateCopy this instance
					invoked.Company					= PrmCompany
					invoked.InventoryLocation		= PrmInventoryLocation
					invoked.DateNeeded				= PrmDateNeeded
					invoked.Item					= PrmKitchenOrder.Item
					invoked.BatchYieldQuantity		= PrmKitchenOrder.BatchYieldQuantity
					invoked.BatchQuantity			= PrmKitchenOrder.BatchQuantity
					invoked.Notes					= PrmKitchenOrder.Notes
					invoked.KitchenOffsetAccount	= PrmKitchenOrder.KitchenOffsetAccount
					invoked.BurdenAbsorptionAccount	= PrmKitchenOrder.BurdenAbsorptionAccount
					invoked.WasteAccount			= PrmKitchenOrder.WasteAccount
					
				for each PrmKitchenOrder.KitchenOrderLine set
					invoke AddIngredient each
						fill in fields from each
							except invoked.ActualQuantityInRecipeUOM
							except invoked.ActualQuantityInStockUOM
							except invoked.ActualLineAmount
							except invoked.WasteQuantity
							except invoked.WasteAmount
						invoked.KitchenOrder			= KitchenOrder
						
				for each PrmKitchenOrder.KitchenOrderInstruction set
					invoke Create each
						fill in fields from each
						invoked.KitchenOrder			= KitchenOrder
						
				for each PrmKitchenOrder.KitchenOrderBurden set
					invoke Create each
						fill in fields from each
							except invoked.ActualFlatAmount
							except invoked.ActualAmountPerUnit
							except invoked.ActualPerBurdenUnitQuantity
							except invoked.ActualPerBurdenUnitMultiplier
							except invoked.ActualPercentageOfInventoryCost
							except invoked.ActualBurdenCost
						invoked.KitchenOrder			= KitchenOrder
						
				for each PrmKitchenOrder.KitchenOrderDetail set
					invoke Create each
						fill in fields from each
						invoked.KitchenOrder			= KitchenOrder					
	
		CreateAndLinkToRequisition is a Create Action
			restricted
			Exit Rules
				include CreateKitchenOrderLinesFromRecipe
				include CreateKitchenOrderInstructionsFromRecipe
				invoke SendNotification
		
		CalculateHeaderTotals is an Instance Action
			restricted
			Parameters
				PrmEstimatedIngredientTotal			is an InternationalAmount
				PrmEstimatedComponentTotal			is an InternationalAmount
				PrmEstimatedPackagingTotal			is an InternationalAmount
				PrmEstimatedBurdenTotal				is an InternationalAmount
				PrmActualIngredientTotal			is an InternationalAmount
				PrmActualComponentTotal				is an InternationalAmount
				PrmActualPackagingTotal				is an InternationalAmount
				PrmActualBurdenTotal				is an InternationalAmount
				PrmWasteIngredientTotal				is an InternationalAmount
				PrmWasteComponentTotal				is an InternationalAmount
				PrmWastePackagingTotal				is an InternationalAmount
				PrmRecalculateBurdenCost			is Boolean
			Local Fields
				LocalOldEstimatedBurdenTotal		is an InternationalAmount
				LocalOldActualBurdenTotal			is an InternationalAmount
			Action Rules
				if (IsOpen)
					EstimatedIngredientTotal			+= PrmEstimatedIngredientTotal
					EstimatedComponentTotal				+= PrmEstimatedComponentTotal
					EstimatedPackagingTotal				+= PrmEstimatedPackagingTotal
					EstimatedOrderTotal 				 = EstimatedIngredientTotal + EstimatedComponentTotal + EstimatedPackagingTotal
				else
					if (IsPreparing)
						ActualIngredientTotal			+= PrmActualIngredientTotal
						ActualComponentTotal			+= PrmActualComponentTotal
						ActualPackagingTotal			+= PrmActualPackagingTotal
						
						WasteIngredientTotal			+= PrmWasteIngredientTotal
						WasteComponentTotal				+= PrmWasteComponentTotal
						WastePackagingTotal				+= PrmWastePackagingTotal
						
						ActualOrderTotal				= ActualIngredientTotal + ActualComponentTotal + ActualPackagingTotal
						LineWasteTotal					= WasteIngredientTotal + WasteComponentTotal + WastePackagingTotal
						HeaderWasteAmount				= DerivedHeaderWasteAmount
						
						WasteTotal						= HeaderWasteAmount + LineWasteTotal
						
				if (KitchenOrderBurden set exists
				and PrmRecalculateBurdenCost)
					for each KitchenOrderBurden set
						invoke RecalculateBurden each
					
					invoke RecalculateHeaderBurdenTotals
				
		CalculateHeaderBurdenTotal is an Instance Action
			restricted
			Parameters
				PrmEstimatedBurdenTotal				is an InternationalAmount
				PrmActualBurdenTotal				is an InternationalAmount
			Action Rules
				EstimatedBurdenTotal				+= PrmEstimatedBurdenTotal
				ActualBurdenTotal					+= PrmActualBurdenTotal
				EstimatedOrderTotal					+= PrmEstimatedBurdenTotal
				ActualOrderTotal					+= PrmActualBurdenTotal
		
		RecalculateHeaderTotals is an Instance Action
			restricted
			Action Rules
				if (IsOpen)
					initialize EstimatedIngredientTotal
					initialize EstimatedPackagingTotal
					initialize EstimatedComponentTotal
					
					for each KitchenOrderLine set
						if (each.IngredientType.Ingredient)
							EstimatedIngredientTotal 				+= each.EstimatedLineAmount
						else
							if (each.IngredientType.Packaging)
								EstimatedPackagingTotal				+= each.EstimatedLineAmount
							else
								if (each.IngredientType.Component)
									EstimatedComponentTotal			+= each.EstimatedLineAmount
									
					EstimatedOrderTotal = EstimatedIngredientTotal + EstimatedComponentTotal + EstimatedPackagingTotal
				else
					if (IsPreparing)
						initialize ActualIngredientTotal
						initialize ActualPackagingTotal
						initialize ActualComponentTotal
						
						for each KitchenOrderLine set
							if (each.IngredientType.Ingredient)
								ActualIngredientTotal 				+= each.ActualLineAmount
							else
								if (each.IngredientType.Packaging)
									ActualPackagingTotal				+= each.ActualLineAmount
								else
									if (each.IngredientType.Component)
										ActualComponentTotal			+= each.ActualLineAmount
						
						ActualOrderTotal = ActualIngredientTotal + ActualComponentTotal + ActualPackagingTotal
									
		RecalculateHeaderBurdenTotals is an Instance Action
			restricted
			Action Rules
				if (IsOpen)
					initialize EstimatedBurdenTotal
	
					for each KitchenOrderBurden set
						EstimatedBurdenTotal						+= each.EstimatedBurdenCost
						
					EstimatedOrderTotal	+= EstimatedBurdenTotal
				
				else
				if (IsPreparing)
					initialize ActualBurdenTotal
					
					for each KitchenOrderBurden set
						ActualBurdenTotal							+= each.ActualBurdenCost
					
					ActualOrderTotal	+= ActualBurdenTotal
				
		SystemUpdate is an Update Action
			restricted
			
		FastUpdate is an Update Action
			restricted
			bypass field rules
				
		SendNotification is an Instance Action
			restricted
			Local Fields
				LocalActor			is an Actor
			Action Rules
				LocalActor = actor
				send notification
					to LocalActor
					description is "KitchenOrder<KitchenOrder>CreatedFor<Recipe.Description>"
					priority is medium
					detail is "KitchenOrderCreationCompleted._\KitchenOrder<KitchenOrder>"
					linkback(webapp is ApplicationAdministrator navigation is ViewKitchenOrder text is "KitchenOrder<KitchenOrder>CreatedFor<Recipe.Description>")
					
		PrintKitchenOrder is an Instance Action
			valid when (HasIDMDocumentAndPrinter)
			Action Rules
				IDMItem.DocumentType = "FSM_KitchenOrder"
				IDMItem.IDMUniqueId	 = IDMUniqueID
				IDMItem.IDMPrinter	 = IDMPrinter

				constraint (IDMItem.GetItemDetails)
					"DocumentDoesNotExistInIDM"

				IDMItem.IDMPID       = IDMItem.IDMItemDetails.PID
				IDMItem.FileName     = IDMItem.IDMItemDetails.FileName

				invoke SendToPrinter IDMJob
					invoked.Description = "Print Kitchen Order"
					invoked.FileName	= IDMItem.FileName
					invoked.IDMItem		= IDMItem
					
		UpdatePrinter is an Instance Action
			valid when (ValidForReGeneration)
			Parameters
				Printer	is an IDMPrinter
			Action Rules
				IDMPrinter = Printer		
	
		UpdateIDMUniqueID is an Instance Action
			restricted
			Action Rules
				invoke FastUpdate
					invoked.IDMUniqueID = IDMJobRel.MDSID
					
		RegenerateKitchenOrderDocument is an Instance Action
			valid when (ValidForReGeneration)			
			Action Rules
				invoke GenerateIDMDocument
					
		GenerateIDMDocument is an Instance Action
			restricted
			Local Fields
				IDMAttributes
				IDMGenerateDocument
				LocalIDMJob 				is an IDMJob view
				CreateIDMJobError			is Boolean
				CreateIDMJobErrorMessage	is Text
				LocalActor	is an Actor
					
			Action Rules
				LocalActor = actor
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName  	= "KitchenOrder" 
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue  	= KitchenOrder
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName  	= "ItemGroup"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue  	= Item.ItemGroup
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName  	= "RecipeItem"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue  	= Item		
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName  	= "Company"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue  	= Company	
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName  	= "InventoryLocation"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue  	= InventoryLocation		
				
				if (IDMAdditionalAttributesLinesRel exists)
					AttributeCtr = 6
					include IDM.IDMAdditionalAttributes
				
				IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
				
				IDMGenerateDocument.XML 				 = DerivedIDMXML
				IDMGenerateDocument.IDMAttributes		 = IDMAttributes
				
				IDMGenerateDocument.DocumentType		 = "FSM_KitchenOrder"
				IDMGenerateDocument.FileName			 = DerivedIDMFileName + Company.KitchenOrderTemplate.DerivedOutputFormat
				
				IDMGenerateDocument.TemplateUniqueId     = Company.KitchenOrderTemplate.IDMUniqueId
				
				if (IDMPrinter entered)
					IDMGenerateDocument.IDMPrinter      = IDMPrinter
				
				invoke CreateFromGenerateDocument IDMJob
					assign result to LocalIDMJob
					resume on error
						CreateIDMJobError 		 = true
						CreateIDMJobErrorMessage = error message
					invoked.Actor				= actor
					invoked.Description			= "Prepare Kitchen Order"
					invoked.IDMGenerateDocument = IDMGenerateDocument
					
				if (CreateIDMJobError)
					send notification
						to LocalActor
						description is "<FailedToCreateIDMJobMessage>"
						priority is high
						detail is "<CreateIDMJobErrorMessage>"
					
				if (LocalIDMJob entered)
					IDMJob = LocalIDMJob.IDMJob	
					

					invoke UpdateIDMUniqueID in background
						run after LocalIDMJob.AsyncId			
	
	StateCycles
		KitchenOrderStateCycle is a StateCycle
			state field is Status
			
			Open is a State
				Update is an Update Action
					Exit Rules
						if (BatchQuantity changed)
							include RecalculateLineHeaderAndBurdens

				Process is an Instance Action
					valid when (KitchenRequisitionsEnabled)
					Action Rules
					Exit Rules
						invoke TransitionToProcessing

				Prepare is an Instance Action
					valid when (not KitchenRequisitionsEnabled)
					Action Rules
						constraint (DateNeeded >= current corporate date)
							"DateNeededCannotBeLessThanToday'sDate"
	
						constraint (KitchenOffsetAccount entered)
							"Missing_\Kitchen_\Offset_\AccountFor_\Kitchen_\Order<KitchenOrder>"
						
						constraint (WasteAccount entered)
							"MissingWasteAccountForKitchenOrder<KitchenOrder>"
					
						for each KitchenOrderLine set
							constraint (each.InventoryCost entered)
								"NoInventoryCostFoundForLine<each.Sequence>Item<each.IngredientItem>"
					Exit Rules
						include CreateWarehouseSupplyHeaderRecord
						include CreateOrUpdateWarehouseSupplyLineRecord
						invoke TransitionToPreparing
						if (ValidForGeneration)
							invoke GenerateIDMDocument

				TransitionToProcessing is an Instance Action
					restricted
					Action Rules
						make transition to Processing
				
				TransitionToPreparing is an Instance Action
					restricted
					Action Rules
						make transition to Preparing
												
				Delete is a Delete Action

			Processing is a State
				StartPreparing is an Instance Action
					valid when (not WaitingForIngredients)
					Action Rules
						WaitingForIngredients = false
						
					Exit Rules
						invoke TransitionToPreparing
						
				TransitionToPreparing is an Instance Action
					restricted
					Action Rules
						make transition to Preparing
			



						
			Preparing is a State
				
				Update is an Update Action
					Action Rules
					Exit Rules
						if (ActualBatchQuantity changed
						or  ActualTotalYieldQuantity changed)
							include RecalculateLineHeaderAndBurdens
				
				CopyPlannedQuantities is an Instance Action
					Action Rules
						ActualBatchQuantity							= BatchQuantity
						ActualTotalYieldQuantity					= PlanTotalYieldQuantity
						ActualTotalSecondaryYieldQuantity			= PlanTotalSecondaryYieldQuantity
						
						for each KitchenOrderLine set
							invoke Update each
								invoked.ActualQuantityInRecipeUOM	= each.PlanQuantityInRecipeUOM
						
				Complete is an Instance Action
					Local Fields
						NewInventoryTransaction					is an InventoryTransaction view
						NewInventoryTransactionLine				is an InventoryTransactionLine view
						LocalInventoryTransaction				is an InventoryTransaction
						LocalInventoryTransactionLine			is an InventoryTransactionLine
						LocalTransactionSystemCode				is a TransactionSystemCode
						
					Action Rules
						if (IsSingleBinTracked)
							include CreateKitchenOrderDetailForSingleBin

						if (ItemLocationRel.BinTracked
						or  ItemLocationRel.LotTracked.LotRequiredAtReceipt
						or  ItemLocationRel.SerialTracked.SerialRequiredAtReceipt)
							constraint (KitchenOrderDetail set exists)
								"Kitchen_\Order_\DetailIsRequiredFor_\Detail_\Tracked_\Recipe_\Items"
							constraint (ActualTotalYieldQuantity = DerivedTotalDetailQuantity)
								"DetailQuantityOutOfBalance"
								
							if (Item.SecondaryUOM entered)	
								constraint (ActualTotalSecondaryYieldQuantity = DerivedTotalDetailSecondaryQuantity)
									"DetailSecondaryQuantityOutOfBalance"
					
						for each KitchenOrderLine set
							invoke Complete each

						for each KitchenOrderBurden set
							invoke RecalculateBurden each
						
						if (ActualBatchQuantity entered)
							ActualUnitCost							= (ActualOrderTotal / ActualTotalYieldQuantity)
						
						EndDate = current corporate date

						invoke Unreleased.Create InventoryTransaction
							assign result to NewInventoryTransaction
							invoked.Company						= Company
							invoked.InventoryLocation			= InventoryLocation
							invoked.InventoryDocumentType		= "RC"
							invoked.OriginatingTransaction		= reference to this instance
							invoked.TransactionDate				= current corporate date
							invoked.GeneralLedgerDate			= current corporate date
							
						LocalInventoryTransaction = NewInventoryTransaction.InventoryTransaction
							
					Exit Rules
						invoke UpdateDemandQuantity WarehouseSupplyLineRel
							invoked.PrmQuantity	  = PlanTotalYieldQuantity * -1
					
						invoke Unreleased.Create InventoryTransactionLine
							assign result to NewInventoryTransactionLine
		    				invoked.OriginatingTransaction 					= reference to this instance
		    				invoked.Company									= Company
		    				invoked.InventoryLocation						= InventoryLocation
		    				invoked.InventoryTransaction					= LocalInventoryTransaction
		    				invoked.TransactionSystemCode					= TransactionSystemCode.InventoryControl
		    				invoked.UpdateDate								= current timestamp
		    				invoked.GeneralLedgerDate						= current corporate date
		    				invoked.Item									= Item
		    				invoked.OffsetAccount							= KitchenOffsetAccount
							invoked.FromCurrency							= Company.Currency
							invoked.ToCurrency								= Company.Currency
							invoked.Quantity								= ActualTotalYieldQuantity
							invoked.TransactionUOM							= Item.StockUOM 
							invoked.TransactionUOMMultiplier				= BatchYieldUOMMultiplier
							invoked.SecondaryQuantity						= ActualTotalSecondaryYieldQuantity
							invoked.SecondaryUOM							= SecondaryYieldUOM
							invoked.SecondaryUOMMultiplier					= SecondaryYieldUOMMultiplier
							invoked.UnitCost								= ActualUnitCost
							invoked.UnitCostInTransactionCurrency 			= ActualUnitCost
							invoked.Bin										= Bin
							invoked.MultipleBins							= MultipleBins
					
						LocalInventoryTransactionLine 	= NewInventoryTransactionLine.InventoryTransactionLine
						LocalTransactionSystemCode		= TransactionSystemCode.InventoryControl
						
						if (MultipleDetailsAllowed
						and KitchenOrderDetail set exists)
							for each KitchenOrderDetail set
								invoke Unreleased.Create InventoryTransactionLineDetail
									invoked.Company										= Company 
									invoked.InventoryLocation							= InventoryLocation
									invoked.TransactionSystemCode						= LocalTransactionSystemCode
									invoked.InventoryTransaction						= LocalInventoryTransaction
									invoked.InventoryTransactionLine					= LocalInventoryTransactionLine
									invoked.Item										= each.Item
									invoked.UnitOfMeasure								= each.UnitOfMeasure
									invoked.Serial										= each.Serial
									invoked.Lot											= each.Lot
									invoked.Sublot										= each.Sublot
									invoked.LotOnHold									= each.LotOnHold
									invoked.LotExpirationDate							= each.LotExpirationDate
									invoked.Bin											= each.Bin
									invoked.Quantity									= each.Quantity
									invoked.SecondaryQuantity							= each.SecondaryQuantity
						
						invoke FinalRelease Unreleased LocalInventoryTransaction
					
						invoke TransitionToCompleted
				



							
				TransitionToCompleted is an Instance Action
					restricted
					Action Rules
						make transition to Completed
				
				Cancel is an Instance Action
					Entrance Rules
						confirmation required
							"Kitchen_\OrderWillBeCanceled.Continue?"
					Action Rules
						invoke Delete WarehouseSupplyLineRel
						make transition to Cancelled
							
			Completed is a State



			
			Cancelled is a State
								
				
