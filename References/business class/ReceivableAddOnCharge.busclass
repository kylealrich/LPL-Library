ReceivableAddOnCharge is a BusinessClass
    owned by ar
	prefix is ARAOC

    Ontology
        symbolic key is ReceivableAddOnCharge

    Patterns
        disable AuditIndex

    Persistent Fields
        Description
        GeneralLedgerAccount                    is a FinanceCodeBlock
            default label is "GlobalLedgerAccount"
        ProcessLevelFinanceCodeBlockOverride    is Boolean
            default label is "AllowProcessLevelFinanceCodeBlockOverride"
        ChargeType                              is Numeric size 1
            States
                FlatAmount    value is 1
                Percentage    value is 2
        FlatAmount                              is an InternationalAmount
        Currency
        AmountPercent                           is a CostPercent
            default label is "Percent"
        Taxable                                 is Boolean
        TaxCode
        UNSPSCCodeGroup
        HSNSACCode
        ProductTaxCategory
		TaxUsageCode
        Active                                  is Boolean

    Transient Fields
        TransientFinanceCodeBlockFull           is a FinanceCodeBlockFull

    Rule Blocks
        CreateUpdateRules
            if (ChargeType not entered)
                ChargeType = 1

            if (ChargeType.FlatAmount)
                constraint (FlatAmount entered)
                    "Flat_\AmountIsRequiredIf_\Charge_\TypeIs_\Flat_\Amount"


                
                initialize AmountPercent

            if (ChargeType.Percentage)
                constraint (AmountPercent entered)
                    "PercentIsRequiredIf_\Charge_\TypeIs_\Percentage"

                initialize FlatAmount
                initialize Currency








            if (ProcessLevelFinanceCodeBlockOverride)
                constraint (GeneralLedgerAccount.GeneralLedgerChartAccount entered)
                    "AtAMinimum,MustEnterA_\Global_\Ledger_\Chart_\Account"
            else
                constraint (GeneralLedgerAccount entered)
                    "Global_\Ledger_\AccountIsRequired"
                TransientFinanceCodeBlockFull = GeneralLedgerAccount

            if (not Taxable)
                if (TaxCode entered
                or  HSNSACCode entered
                or  UNSPSCCodeGroup entered
                or  ProductTaxCategory entered
                or  TaxUsageCode entered)
                    constraint (false)
                        "TaxFieldsCannotBeEnteredIfAddOnChargeIsNot_\Taxable"

            if (UNSPSCCodeGroup.UNSPSCCode entered)
                constraint (UNSPSCCodeGroup.ItemGroup entered)
                    "Item_\GroupIsRequiredIfUNSPSC_\CodeIsEntered"
            else
                constraint (UNSPSCCodeGroup.ItemGroup not entered)
                    "Item_\GroupCannotBeEnteredIfUNSPSC_\CodeIsNotEntered"

            if (Description not entered)
                Description = ReceivableAddOnCharge

    Field Rules
        Description
            required
        
        GeneralLedgerAccount
            required

        ChargeType
            required
            initial value is 1

        Currency
            if (ChargeType.FlatAmount
            and FlatAmount entered)
                default to Company.Currency
                required         
               
                constraint (Currency = Company.Currency)
                    "CurrencyCannotBeDifferFrom_\Company_\Currency"
            else
                cannot be entered
                    "CurrencyCannotBeEnteredIf_\Flat_\AmountIsNotEntered"        
    
    Relations
	   	GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company

    Conditions
        IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)

    Actions
        Create is a Create Action
        	Action Rules
                include CreateUpdateRules
        		Active = true

		Update is an Update Action
            Action Rules
                include CreateUpdateRules

        Delete is a Delete Action
