MobileSupplyChainShipmentDetailTracking  is a BusinessClass
    owned by mscm
    prefix is MSSDT
    sql name is "MSCMShipmentDetailTracking"

    Ontology
        symbolic key is MobileSupplyChainShipmentDetailTracking
	
    Patterns
		implements ContextualParent
		implements StaticJava
		disable AuditIndex
		
    Persistent Fields
        TrackingNumber
        Quantity
            precision is WarehouseShipmentLine.DerivedNumberOfDecimalsQuantity
        UnitOfMeasure
        
    Field Rules
		UnitOfMeasure
			required
            constraint (ItemUOMRel exists)
				"<UnitOfMeasure>IsNotAValidUnitOfMeasureForItem<WarehouseShipmentLine.Item>"		

        Quantity
			required

    Local Fields
        UOMCalculation
        LocalDeliveryQuantity                           is a Quantity
        LocalDeliveryTicket		                        is like MobileSupplyChainDeliveryTicket
        LocalDeliveryUnit                               is like MobileSupplyChainDeliveryUnit
        LocalInforTrackingNumber                        is a TrackingNumber
        
    Derived Fields
    
        DerivedLineUOMQuantity is a DerivedField
            type is like Quantity

            initialize UOMCalculation

            UOMCalculation.InputUOM		 = UnitOfMeasure
			UOMCalculation.InputToUOM  	 = WarehouseShipmentLine.TransactionUOM
			UOMCalculation.InputQuantity = Quantity
			UOMCalculation.Method		 = UOMCalculation.Method.ConvertToAlternate
            
            return UOMCalculation.OutputQuantity
    
    Rule Blocks
        CreateDeliveryLine
            invoke Create MobileSupplyChainDeliveryLine
                invoked.Company                             = Company
                invoked.MobileSupplyChainLocation           = InventoryLocation
                invoked.MobileSupplyChainDeliveryTicket     = LocalMobileSupplyChainDeliveryTicket
                invoked.MobileSupplyChainDeliveryUnit       = LocalMobileSupplyChainDeliveryUnit
                invoked.OriginatingLineNumber				= WarehouseShipmentLine
                invoked.Quantity							= LocalDeliveryQuantity
                invoked.UnitOfMeasure						= UnitOfMeasure
                invoked.Item								= WarehouseShipmentLine.Item
                invoked.Bin									= WarehouseShipmentLine.DerivedBinTo
                invoked.Lot									= WarehouseShipmentLineDetail.WarehouseShipmentLineDetail.Lot
                invoked.Sublot                              = WarehouseShipmentLineDetail.WarehouseShipmentLineDetail.Sublot
                invoked.Serial						        = WarehouseShipmentLineDetail.WarehouseShipmentLineDetail.Serial
                invoked.ExpirationDate                      = WarehouseShipmentLineDetail.WarehouseShipmentLineDetail.Lot.LotExpirationDate
                invoked.OrderedQuantity						= WarehouseShipmentLine.PrintedQuantity
                invoked.OrderedUOM							= WarehouseShipmentLine.TransactionUOM
                invoked.BackorderedQuantity					= WarehouseShipmentLine.TotalBackorderQuantity
                invoked.Requisition							= WarehouseShipment.WarehouseDemand.DemandDocument
                invoked.Requester							= WarehouseShipment.DerivedRequester
            
    Conditions 
        HasDuplicateTrackingNumber
            when (DuplicateTrackingNumberRel exists)
    
    Sets
        ByTrackingNumber
            Sort Order
                Company
                InventoryLocation
                WarehouseShipment
                TrackingNumber
                WarehouseShipmentLine
                WarehouseShipmentLineDetail.Lot
                WarehouseShipmentLineDetail.Bin
                WarehouseShipmentLineDetail.Sublot
                WarehouseShipmentLineDetail.UnitOfMeasure
                WarehouseShipmentLineDetail.Serial
                WarehouseShipmentLineComponent
                MobileSupplyChainShipmentDetailTracking

    Relations

        DuplicateTrackingNumberRel
            one-to-many relation to MobileSupplyChainShipmentDetailTracking
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseShipment               = WarehouseShipment
                related.WarehouseShipmentLine           = WarehouseShipmentLine
                related.WarehouseShipmentLineComponent  = WarehouseShipmentLineComponent
                related.WarehouseShipmentLineDetail     = WarehouseShipmentLineDetail
            Instance Selection
                where (related.MobileSupplyChainShipmentDetailTracking != MobileSupplyChainShipmentDetailTracking
                and    related.TrackingNumber                           = TrackingNumber
                and    related.UnitOfMeasure                            = UnitOfMeasure)

        ItemUOMRel
            one-to-many relation to ItemUOM
            Field Mapping uses symbolic key
                related.ItemGroup     = Company.ItemGroup
                related.Item          = WarehouseShipmentLine.Item
                related.UnitOfMeasure = UnitOfMeasure
        
        MobileSupplyChainDeliveryTicketRel
			one-to-one relation to MobileSupplyChainDeliveryTicket
			Field Mapping uses symbolic key
				related.Company									= Company
				related.MobileSupplyChainLocation				= InventoryLocation
				related.MobileSupplyChainDeliveryTicket			= LocalDeliveryTicket
        
        MobileSupplyChainDeliveryLineRel
			one-to-many relation to MobileSupplyChainDeliveryLine
			Field Mapping uses symbolic key
				related.Company									= Company
				related.MobileSupplyChainLocation				= InventoryLocation
				related.MobileSupplyChainDeliveryTicket			= LocalDeliveryTicket
        
        MobileSupplyChainDeliveryUnitRel
			one-to-many relation to MobileSupplyChainDeliveryUnit
			Field Mapping uses symbolic key
				related.Company									= Company
				related.MobileSupplyChainLocation				= InventoryLocation
				related.MobileSupplyChainDeliveryTicket			= LocalDeliveryTicket
                related.MobileSupplyChainDeliveryUnit           = LocalDeliveryUnit
        
        MobileSupplyChainConfigurationRel
            one-to-one relation to MobileSupplyChainConfiguration
            Field Mapping uses symbolic key
                related.MobileSupplyChainConfiguration  = Company.FinanceEnterpriseGroup
        
    Actions

        Create is a Create Action
            valid when (WarehouseShipment.Status.Printed)

        Update is an Update Action
            valid when (WarehouseShipment.Status.Printed)
            
        Delete is a Delete Action
            valid when (WarehouseShipment.Status.Printed)
        
        ProcessDeliveryByContainer is a Set Action
			restricted
			Parameters
				PrmCompany                  is an InventoryCompany
                    default label is "Company"
                PrmInventoryLocation        is an InventoryLocation
                    default label is "InventoryLocation"
				PrmWarehouseShipment		is an WarehouseShipment
					default label is "WarehouseShipment"

			Sort Order is ByTrackingNumber
                
			Instance Selection
                where (Company					= PrmCompany
                and    InventoryLocation        = PrmInventoryLocation
                and    WarehouseShipment        = PrmWarehouseShipment)

			Local Fields
                LocalMobileSupplyChainDeliveryTicket		    is like MobileSupplyChainDeliveryTicket
                LocalMobileSupplyChainDeliveryUnit			    is like MobileSupplyChainDeliveryUnit
                LocalMobileSupplyChainDeliveryTicketView        is a MobileSupplyChainDeliveryTicket view
                LocalMobileSupplyChainDeliveryUnitView          is a MobileSupplyChainDeliveryUnit view
                LocalDeliveryLineCount							is Numeric size 6
                LocalSetDeliveryQuantity                        is a Quantity
                LocalReportPrinter                              is like MobileSupplyChainPrinter
                LocalLabelPrinter                               is like MobileSupplyChainPrinter
               
			Action Rules
                TrackingNumber Set Rules
                    Entrance Rules
                        initialize LocalDeliveryLineCount

                        invoke Create MobileSupplyChainDeliveryTicket 
                            assign result to LocalMobileSupplyChainDeliveryTicketView
                            invoked.Company                                             = Company
                            invoked.MobileSupplyChainLocation                           = InventoryLocation
                            invoked.DeliveryType                                        = DeliveryType.Shipment
                            invoked.InforTrackingNumber                                 = TrackingNumber
                            invoked.DeliverToCompanyLocation.MobileSupplyChainCompany   = WarehouseShipment.CompanyAndLocation.RequestingCompany
                            invoked.DeliverToCompanyLocation.MobileSupplyChainLocation  = WarehouseShipment.CompanyAndLocation.RequestingLocation
                            invoked.OriginatingTransaction                              = reference to WarehouseShipment
                            invoked.ReceivedBy                                          = WarehouseShipment.DerivedPickedBy
                            invoked.DocumentNumber      								= WarehouseShipment
                            invoked.DeliverTo                                           = WarehouseShipment.DerivedBillToName

                        LocalMobileSupplyChainDeliveryTicket = LocalMobileSupplyChainDeliveryTicketView.MobileSupplyChainDeliveryTicket
                        
                        invoke Create MobileSupplyChainDeliveryUnit
                            assign result to LocalMobileSupplyChainDeliveryUnitView
                            invoked.Company                             = Company
                            invoked.MobileSupplyChainLocation           = InventoryLocation
                            invoked.MobileSupplyChainDeliveryTicket     = LocalMobileSupplyChainDeliveryTicketView.MobileSupplyChainDeliveryTicket
                            invoked.Status								= MobileSupplyChainDeliveryUnit.Status.Picked

                        LocalMobileSupplyChainDeliveryUnit  = LocalMobileSupplyChainDeliveryUnitView.MobileSupplyChainDeliveryUnit

                    Exit Rules
                        LocalDeliveryTicket     = LocalMobileSupplyChainDeliveryTicket
                        LocalDeliveryUnit       = LocalMobileSupplyChainDeliveryUnit

                        invoke Update MobileSupplyChainDeliveryTicketRel
				            invoked.NumberOfLines			= LocalDeliveryLineCount
                        
                        if(LocalDeliveryLineCount > 0)
                            invoke CreateEventForAllLines MobileSupplyChainDeliveryTicketRel
                                invoked.PrmPerformedAction		= PerformedAction.Picked
                                invoked.PrmTimeSubmitted		= WarehouseShipment.DerivedLastTimeSubmitted
                            
                        if (WarehouseShipment.IsPrintShipment)
                            if (WarehouseShipment.IsPrintReport)
                                LocalReportPrinter              = WarehouseShipment.MSCMReportPrinter
                            if (WarehouseShipment.IsPrintLabel)
                                LocalLabelPrinter               = WarehouseShipment.MSCMLabelPrinter

                            invoke Print MobileSupplyChainDeliveryTicketRel
                                invoked.PrmReportPrinter 	= LocalReportPrinter
                                invoked.PrmLabelPrinter		= LocalLabelPrinter
                        
                        invoke BuildRequisitionTextSearch MobileSupplyChainDeliveryUnitRel

                WarehouseShipmentLine Set Rules
                    Entrance Rules
                        initialize LocalSetDeliveryQuantity
                    Exit Rules
                        if (WarehouseShipmentLine.IsBinTracked
                        or not WarehouseShipmentLine.IsDetailRequired) 
                            LocalDeliveryQuantity   = LocalSetDeliveryQuantity
                            LocalDeliveryTicket     = LocalMobileSupplyChainDeliveryTicket
                            LocalDeliveryUnit       = LocalMobileSupplyChainDeliveryUnit

                            include CreateDeliveryLine

                            LocalDeliveryLineCount += 1
                
                WarehouseShipmentLineDetail.Lot Set Rules
                    Entrance Rules
                        initialize LocalSetDeliveryQuantity
                    Exit Rules       
                        if (WarehouseShipmentLine.IsLotTracked) 
                            LocalDeliveryQuantity   = LocalSetDeliveryQuantity
                            LocalDeliveryTicket     = LocalMobileSupplyChainDeliveryTicket
                            LocalDeliveryUnit       = LocalMobileSupplyChainDeliveryUnit

                            include CreateDeliveryLine

                            LocalDeliveryLineCount += 1

                Instance Rules
                    if (WarehouseShipmentLine.IsLotTracked          
                    or WarehouseShipmentLine.IsBinTracked           
                    or not WarehouseShipmentLine.IsDetailRequired)  
                        LocalSetDeliveryQuantity += Quantity

                    else                                            
                        LocalDeliveryQuantity   = Quantity
                        LocalDeliveryTicket     = LocalMobileSupplyChainDeliveryTicket
                        LocalDeliveryUnit       = LocalMobileSupplyChainDeliveryUnit

                        include CreateDeliveryLine
                        
                        LocalDeliveryLineCount += 1
