SegregationOfDutiesObjectType is a BusinessClass
    owned by security
    prefix is SODOT

	Ontology
        symbolic key is SegregationOfDutiesObjectType

    Patterns
    	implements CreateStamp
    	implements UpdateStamp
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        disable Auditing

    Persistent Fields
    	ConditionGroup				is Numeric size 1
    		default label is "Condition"
    		States
    			LeftSide			value is 1
    			RightSide			value is 2
    			
        Conjunction                 is Alpha size up to 3
        	default label is "Operator"
            States
                And 		        value is "and"
                Or			        value is "or"
		
		ActionTypeName				is Alpha size up to 25
		
	Transient Fields
        BusinessViewName            is a BusinessView
            default label is "BusinessView"

        CommonAction                is Alpha size up to 25
            default label is "CommonAction"
            States
            	Inquire						value is "Inquire"
				AllActions					value is "all actions"
				ViewAuditLog				value is "ViewAuditLog"
				ViewFullAuditLog			value is "ViewFullAuditLog"
                AllInquiries        		value is "all inquiries"
                AllCreates          		value is "all creates"
                AllUpdates          		value is "all updates"
                AllDeletes          		value is "all deletes"
                AllFunctions				value is "all functions"
                AllImports					value is "all imports"
                AllAuditViews				value is "all audit views"
                DataExport					value is "data export"
                AllCryptoActions			value is "all crypto actions"
                AllCreate					value is "audit create"
                AuditUpdate					value is "audit update"
                AuditDelete					value is "audit delete"
                InsertHistory				value is "insert history"
                DeleteHistory				value is "delete history"
                PurgeAuditLogEntries		value is "purge audit log entries"
                ChangeEffectiveDate			value is "change effective date"
                ChangeCreateDate			value is "change create date"
                Invalidate					value is "invalidate"                       

        BusinessViewAction          is a BusinessAction
            default label is "BusinessViewAction"
    
    Local Fields
    	LocalSODObjectType			is a SegregationOfDutiesObjectType
    	    
    Field Rules
        BusinessViewName
        	if (SegregationOfDutiesObjectType.SecurableObjectName = blank)
	            required
	                "Field<SegregationOfDutiesObjectType.SecurableObjectType>IsRequired"
	                
		Conjunction
			required
		
		ConditionGroup	
			required
            
    Conditions
        IsBusinessTask
            when (SegregationOfDutiesObjectType.SecurableObjectType = SegregationOfDutiesObjectType.SecurableObjectType.BusinessTask) 
            
        IsBusinessClass
        	when (SegregationOfDutiesObjectType.SecurableObjectType = SegregationOfDutiesObjectType.SecurableObjectType.BusinessClass)   

    Sets
        ByCreateStamp
            indexed
            Sort Order
                create stamp 
                SegregationOfDutiesTemplate
                SegregationOfDutiesObjectType.SecurableObjectType
                SegregationOfDutiesObjectType.SecurableObjectName
                SegregationOfDutiesObjectType.SecurableObjectAction

        ByTemplate
            duplicates
            Sort Order
                SegregationOfDutiesTemplate
                create stamp 

    Relations
        BusinessTaskRel is a BusinessView set
            Instance Selection
                where (related.Type = BusinessView.Type.BusinessTask)

        BusinessClassRel is a BusinessView set
            Instance Selection
                where (related.Type = BusinessView.Type.BusinessClass)

        BusinessViewRel
            if (SegregationOfDutiesObjectType.SecurableObjectType.BusinessClass)
                BusinessClassRel
            else
                BusinessTaskRel

	Rule Blocks
		CreateRules
			SegregationOfDutiesObjectType.SecurableObjectName = BusinessViewName
			
			if (SegregationOfDutiesObjectType.SecurableObjectAction not entered) 
                if (BusinessViewAction entered and CommonAction not entered)
                    SegregationOfDutiesObjectType.SecurableObjectAction = BusinessViewAction
                    ActionTypeName = BusinessViewAction.ActionTypeName
                    
                if (BusinessViewAction not entered and CommonAction entered)
                    SegregationOfDutiesObjectType.SecurableObjectAction = CommonAction

                if (CommonAction entered and BusinessViewAction entered)
                    constraint (false)
                        "CannotSetBothCommonActionOrBusinessViewActionAtTheSameTime."

                if (CommonAction not entered and BusinessViewAction not entered)
                    constraint (false)
                        "MustSelectEitherCommonActionOrBusinessViewAction."
			
    Actions	
        Create is a Create Action
        	restricted
			Action Rules
				include CreateRules
        
        CreateLeftCondition is a Create Action
			Action Rules
                ConditionGroup = ConditionGroup.LeftSide
				include CreateRules
	                        
        CreateRightCondition is a Create Action
			Action Rules
                ConditionGroup = ConditionGroup.RightSide
				include CreateRules             
	                                        
        Update is an Update Action
		
		Delete is a Delete Action

        Purge is a Purge Action
        
        PurgeAll is a Set Action
			Action Rules
				Instance Rules
					invoke Purge
