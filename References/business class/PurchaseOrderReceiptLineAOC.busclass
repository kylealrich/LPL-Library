PurchaseOrderReceiptLineAOC is a BusinessClass
    owned by po
    prefix is REC
    classic name is RECAOCDTL

    Ontology
        symbolic key is PurchaseOrderReceiptLineAOC
            classic set name is RECSET1
            classic name for PurchaseOrderReceipt is REC-NUMBER
            classic name for PurchaseOrderReceiptLine is LINE-NBR
            classic name for AddOnCharge is AOC-CODE

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        PurchaseOrder
        Vendor
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
        ShipToLocation                 is an InventoryLocation
            classic name is LOCATION
        AddOnChargePercent
            classic name is AOC-RATE
        AddOnChargeEntryMethod
            classic name is ENTRY
        Quantity
        OriginalUnitCost               is an InternationalCost
            classic name is ORIG-UNIT-CST
        TotalAddOnChargeAmount         is an InternationalAmount
            classic name is TOTAL-AOC
        ReceivedQuantity               is a Quantity
            classic name is REC-QTY
        MatchedQuantity                is a Quantity
            classic name is MATCHED-QTY
        MiscellaneousAddOnChargeInvoicedAmount is an InternationalAmount
            classic name is MISC-MA-AMT
        ReceivedQuantityToMatch        is a Quantity
            classic name is RECQTY-TO-MA
        ReceivedAmountToMatch          is an InternationalAmount
            classic name is RECAMT-TO-MA
        TaxCode
        TaxableUnitCost                is an InternationalCost
            classic name is TAX-UNIT-CST
        Taxable                        is Boolean
            classic name is TAXABLE-FLAG
        AddOnChargeOrigin
            classic name is ORIGIN-CD
        PrintOnPO                      is Boolean
            classic name is AOC-ON-PO
        Summarize                      is Boolean
            classic name is SUMMARY-FLAG
        ZeroCost                       is Boolean
            classic name is ZERO-COST-FLG
        Canceled                       is Boolean
            classic name is CANCELLED-FL
        Closed
            classic name is CLOSED-FL
        CreatedDuringSpread            is Boolean
            classic name is INV-SPRD-CRET
        Issued                         is Boolean
            classic name is ISSUED-FLAG
        InvoicedTaxAmount              is an InternationalAmount
            classic name is INVC-TAX-AMT
        AccruedTaxAmount               is an InternationalAmount
            classic name is ACCR-TAX-AMT
        ExtendedTaxableAmount          is an InternationalAmount
            classic name is EXT-TAXBL-AMT
        LandedUnitCost                 is an InternationalCost
            classic name is LAND-UNIT-CST
        LandedAddOnCharge              is Boolean
            classic name is LANDED-FLAG
        MatchDetailKey
            classic name is MATCH-DTL-KEY
        CrossReferenceVendor
            classic name is XREF-VENDOR
		MatchReferenceNumber
        Currency
            classic name is CURRENCY-CODE
        ReceiptCurrencyConversionRate  is an EnteredCurrencyConversionRate
            classic name is REC-CNV-RATE
        AddOnChargeAmountPOCurrency    is an InternationalAmount
            classic name is TOTAL-AOC-PO
        Status                         is Numeric size 1
            States
                All  value is blank
                Open value is 1
                I    value is 2
                R    value is 3
                Paid value is 9
        OpenToMatchQuantity            is a Quantity
            classic name is OPEN-TO-MATCH
        ChargebackQuantity             is a Quantity
            classic name is CHARGEBACK-QTY
        MatchObjectID                  is an ObjId
            classic name is MATCH-OBJ-ID
        MatchSequence                  is a SeqNbr
            classic name is MATCH-SEQ-NBR
        TrackType
        	protected
        GlobalLineType
            classic name is GLBL-LINE-TYPE

	Transient Fields
		PorecuwsUpdFromPo38				is Boolean
		PopcrPaoLineNumber				is like LineNumber
        PorctRecCompany					is like PayablesCompany
        PorctRecRecNumber				is like PurchaseOrderReceipt
        PorctRecLineNumber				is like PurchaseOrderReceiptLine			
        PorctRecAOCCode					is like AddOnCharge
        Item 
        	derive value from PurchaseOrderReceiptLine.Item

	Context Fields
		PayablesInvoice
			
	Local Fields
		LocalRunRoutine					is Alpha 1
		LocalCompany					is like PayablesCompany
		CalculateTax
		
		LocalPurchaseOrder				is like PurchaseOrder
		LocalPurchaseOrderLine			is like PurchaseOrderLine
		LocalAddOnCharge				is like AddOnCharge
		
		LocalPAOCompany					is like PayablesCompany
		LocalPAOPurchaseOrder			is like PurchaseOrder
		LocalPAOPurchaseOrderLine		is like PurchaseOrderLine				
		LocalPAOAddOnCharge				is like AddOnCharge
		
		PoreccwsTotalRecAOC				is an InternationalAmount
		PoreccwsRemainUnitCost			is an InternationalCost
		LocalToalAddOnChargeAmount		is an InternationalAmount
		PoreccwsAllowanceAoc			is Boolean
		
		LocalRemainingAmountPurchaseOrderReceipt			is like PurchaseOrderReceipt
		LocalRemainingAmountPurchaseOrderLine				is like PurchaseOrderLine
		LocalRemainingAmountPurchaseOrderReceiptLine		is like PurchaseOrderReceiptLine
		UOMCalculation

		LocalOldAddOnChargeAmountPOCurrency					is an InternationalAmount
		CurrencyExchange
		LocalCurrencyTable				is a CurrencyTable	
		SavedTransactionAmount			is a CurrencyAmount
		SavedExchangeDate				is a ExchangeDate
		SavedFromCurrency				is a FromCurrency
		LocalFromCurrency				is like Currency
		LocalToCurrency 				is like Currency

	Derived Fields


		TotalTaxAmount is a DerivedField
			type is like InternationalCost
			restricted
			if (AddOnCharge entered
			and PorecuwsUpdFromPo38)
				constraint (AddOnCharge.SpreadMethod = "N")
					"MustUseAOCCodeWithNoSpreadForE/R/SA/O/C"
					
			if (TotalAddOnChargeAmount not entered)
				constraint (PurchaseOrderAndLineAddOnChargeRel.ZeroCost)
					"TotalAOCMustBeEntered"
			if (OriginalUnitCost not entered)
				if (PurchaseOrderReceiptLine.VendorPriceUOMQuantity entered)
					OriginalUnitCost = AddOnChargeAmountPOCurrency/PurchaseOrderReceiptLine.VendorPriceUOMQuantity
				else
					if (PurchaseOrderAndLineAddOnChargeRel.EnteredUnitCost entered)
						OriginalUnitCost = PurchaseOrderAndLineAddOnChargeRel.EnteredUnitCost
					else
						OriginalUnitCost = AddOnChargeAmountPOCurrency
			if (AddOnCharge entered
			and AddOnCharge.AddOnChargeType = "A")
				if (OriginalUnitCost > "0")
					OriginalUnitCost = OriginalUnitCost * -1
				if (AddOnChargeAmountPOCurrency > "0")
					AddOnChargeAmountPOCurrency = AddOnChargeAmountPOCurrency * -1			
			
			if (TaxCode entered
			and TotalAddOnChargeAmount entered)
				CalculateTax.InvoiceCompany			= Company
				CalculateTax.Function				= "C"
				CalculateTax.TaxCode				= TaxCode
				CalculateTax.TaxableAmount			= AddOnChargeAmountPOCurrency
				CalculateTax.TaxDate				= current corporate date
				CalculateTax.InvoiceDate			= current corporate date
				CalculateTax.NbrOfDecimals			= PurchaseOrderReceiptLine.Item.NumberOfDecimalsCost
				if (CalculateTax.OutputErrorMessage not entered)
					if (PurchaseOrderReceiptLine.VendorPriceUOMQuantity entered)
						TaxableUnitCost = CalculateTax.OutputTotalTaxAmount / PurchaseOrderReceiptLine.VendorPriceUOMQuantity
					else
						TaxableUnitCost = CalculateTax.OutputTotalTaxAmount	
			else
				initialize TaxableUnitCost
			return 

		DerivedOriginalUnitCost is a DerivedField
			type is like InternationalCost
			return OriginalUnitCost
								
		DerivedOpenToMatchExtendedCost is a DerivedField
			type is like InternationalAmount
			restricted
			return (OpenToMatchQuantity * OriginalUnitCost)
			
		DerivedReceivedQuantityInStockUOM is a DerivedField
			type is like Quantity
			if (PurchaseOrderReceiptLine.ReceivedUOM != Item.StockUOM)
				initialize UOMCalculation
	            UOMCalculation.Method				= UOMCalculation.Method.ConvertToStock
				UOMCalculation.InputUOM			= PurchaseOrderReceiptLine.ReceivedUOM
	            UOMCalculation.InputQuantity		= ReceivedQuantity
	            return UOMCalculation.OutputQuantity
			else
				return ReceivedQuantity

		DerivedLandedUnitCostInStockUOM is a DerivedField
			type is like InternationalCost
			if (PurchaseOrderReceiptLine.ReceivedUOM != Item.StockUOM)
				initialize UOMCalculation
	            UOMCalculation.Method				= UOMCalculation.Method.ConvertToStock
				UOMCalculation.InputUOM				= PurchaseOrderReceiptLine.ReceivedUOM
	            UOMCalculation.InputUnitCost		= LandedUnitCost
				return UOMCalculation.OutputUnitCost
			else
				return LandedUnitCost

		DerivedAddOnChargeAmountPOCurrency is a DerivedField
			type is like InternationalAmount
			restricted
			if (PurchaseOrder.Currency = Currency)
				return TotalAddOnChargeAmount
			else
				initialize CurrencyExchange
				SavedFromCurrency				= Company.Currency
				CurrencyExchange.ToCurrency		= PurchaseOrder.Currency
				LocalCurrencyTable				= PurchaseOrder.CurrencyTable
				SavedTransactionAmount			= TotalAddOnChargeAmount
				if (PurchaseOrder.CurrencyExchangeSetPoint.Receiving)
					SavedExchangeDate			= PurchaseOrderReceipt.ReceivedDate
				else
					SavedExchangeDate			= PurchaseOrder.PurchaseOrderDate
				
				LocalFromCurrency	      		= Company.Currency
				LocalToCurrency	      			= PurchaseOrder.Currency
				if (CurrencyRelationshipRel.MultDiv.Divide)
					return (TotalAddOnChargeAmount / CurrencyExchange.OutputCurrencyRate)
				else
					return (TotalAddOnChargeAmount * CurrencyExchange.OutputCurrencyRate)

		
    Conditions
    
    	ForCostAdjustment
    		restricted
    		when (LandedUnitCost entered)

        NotClosedAndNotCanceled
            classic name is INV-LOOKUP-SEL
            restricted
            when (Closed.No
            and   not Canceled)

		ReleasedReceiver
			restricted
			when (PurchaseOrderReceipt.IsOpen)

		LinkedToContextInvoice
			restricted
			when (ContextMatchPurchaseOrderInvoiceRel exists)
			
    Sets

        Set2
            indexed
            Instance Selection
                where (NotClosedAndNotCanceled)
            Sort Order
                Company
                Vendor
                PurchaseOrder
                ShipToLocation
                PurchaseOrderReceipt
                PurchaseOrderReceiptLine
                AddOnCharge

        Set3
            indexed
            Sort Order
                Company
                PurchaseOrder
                PurchaseOrderReceipt
                PurchaseOrderReceiptLine
                AddOnCharge
                Vendor
                ShipToLocation

        Set5
            indexed
            Sort Order
                Company
                MatchObjectID
                MatchSequence
                PurchaseOrderReceipt
                PurchaseOrderReceiptLine
                AddOnCharge

	Relations
	
        PurchaseOrderReceiptInvoiceLinkRel
            one-to-many relation to PurchaseOrderReceiptInvoiceLink
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt = PurchaseOrderReceipt
                and   related.PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine = PurchaseOrderReceiptLine
                and   related.PurchaseOrderReceiptInvoiceLink.MatchObjectID = MatchObjectID
                and   related.PurchaseOrderReceiptInvoiceLink.MatchSequence = MatchSequence)

		PoreclineAOCRel
            one-to-many relation to PurchaseOrderReceiptLineAOC
            Field Mapping uses symbolic key
                related.Company              = Company
                related.PurchaseOrderReceipt = PurchaseOrderReceipt
                related.PurchaseOrderReceiptLine = PurchaseOrderReceiptLine

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = LocalCompany

        PurchaseOrderAndLineAddOnChargeRel  
            one-to-one relation to PurchaseOrderAndLineAddOnCharge
            Field Mapping uses symbolic key
                related.Company 				= LocalCompany
                related.PurchaseOrder			= LocalPurchaseOrder
                related.PurchaseOrderLine 		= LocalPurchaseOrderLine
                related.AddOnCharge				= LocalAddOnCharge

        PurchaseOrderLineRel  
            one-to-one relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company 				= LocalPAOCompany
                related.PurchaseOrder			= LocalPAOPurchaseOrder
                related.PurchaseOrderLine 		= LocalPAOPurchaseOrderLine

        PoreccpdHeaderAOCRel 
            one-to-many relation to PurchaseOrderReceiptLineAOC
            Field Mapping uses Set3
                related.Company 					= LocalPAOCompany
                related.PurchaseOrder				= LocalPAOPurchaseOrder
            Instance Selection
            	where (related.PurchaseOrderReceipt != blank
            	or     related.AddOnCharge			= LocalPAOAddOnCharge)

        PoreclineRemainingAmountRel 
            one-to-many relation to PurchaseOrderReceiptLine
            Field Mapping uses Set6
                related.Company              		= LocalPAOCompany
                related.PurchaseOrderReceipt 		= LocalRemainingAmountPurchaseOrderReceipt 
                related.PurchaseOrderLine			= LocalRemainingAmountPurchaseOrderLine  
                related.PurchaseOrderReceiptLine	= LocalRemainingAmountPurchaseOrderReceiptLine 

		ContextMatchPurchaseOrderInvoiceRel
			one-to-one relation to MatchPurchaseOrderInvoice
			Field Mapping uses symbolic key
				related.Company			= Company
				related.PurchaseOrder	= PurchaseOrder
				related.PayablesInvoice	= PayablesInvoice
				related.Vendor			= Vendor
				
		ItemLocationRel 
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company 						= Company
				related.InventoryLocation 		= ShipToLocation
				related.Item								= PurchaseOrderReceiptLine.Item

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CurrencyRelationshipRel
			one-to-one relation to CurrencyRelationship
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				 = GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.CurrencyRelationship.FromCurrency	 = LocalFromCurrency
				related.CurrencyRelationship.ToCurrency		 = LocalToCurrency

    Field Rules
		Closed
			default to Closed.No
		
        Status
            default to 1
            
        AddOnChargeOrigin
        	default to PurchaseOrderAndLineAddOnChargeRel.AddOnChargeOrigin

        AddOnChargeAmountPOCurrency
        	default to DerivedAddOnChargeAmountPOCurrency

		TrackType
			default to PurchaseOrderReceiptLineAOC.AddOnCharge.TrackType
        	





	Rule Blocks
		CreateFromPoreccpdRules




			
			LocalCompany					= Company
			LocalPurchaseOrder				= PurchaseOrderReceipt.PurchaseOrder
			LocalPurchaseOrderLine			= PurchaseOrderReceiptLine
			LocalAddOnCharge				= AddOnCharge
			
			PurchaseOrder					= PurchaseOrderAndLineAddOnChargeRel.PurchaseOrder
			Vendor							= PurchaseOrderAndLineAddOnChargeRel.Vendor
			PurchaseFromLocation			= PurchaseOrderAndLineAddOnChargeRel.PurchaseFromLocation
			ShipToLocation					= PurchaseOrderAndLineAddOnChargeRel.ShipToLocation
			AddOnChargePercent				= PurchaseOrderAndLineAddOnChargeRel.AddOnChargePercent
			AddOnChargeEntryMethod			= PurchaseOrderAndLineAddOnChargeRel.AddOnChargeEntryMethod
			Quantity						= PurchaseOrderAndLineAddOnChargeRel.Quantity
			MiscellaneousAddOnChargeInvoicedAmount	= PurchaseOrderAndLineAddOnChargeRel.MiscellaneousAddOnChargeInvoicedAmount
			if (PurchaseOrderAndLineAddOnChargeRel.PurchaseOrderLine not entered)
				MiscellaneousAddOnChargeInvoicedAmount	= PurchaseOrderAndLineAddOnChargeRel.TotalAddOnChargeAmount
			else
				MatchDetailKey				= PurchaseOrderReceiptLine.MatchDetailKey
				ReceivedAmountToMatch		= PurchaseOrderAndLineAddOnChargeRel.TotalAddOnChargeAmount
				initialize MatchedQuantity
			TaxCode							= PurchaseOrderAndLineAddOnChargeRel.TaxCode
			initialize TaxableUnitCost
			Taxable							= PurchaseOrderAndLineAddOnChargeRel.Taxable

			PrintOnPO						= PurchaseOrderAndLineAddOnChargeRel.PrintOnPO
			Summarize						= PurchaseOrderAndLineAddOnChargeRel.Summarize
			ZeroCost						= PurchaseOrderAndLineAddOnChargeRel.ZeroCost
			Canceled						= PurchaseOrderAndLineAddOnChargeRel.Canceled
			Closed							= "N"
			CreatedDuringSpread				= PurchaseOrderAndLineAddOnChargeRel.CreatedDuringSpread
			Issued							= PurchaseOrderAndLineAddOnChargeRel.Issued
			InvoicedTaxAmount				= PurchaseOrderAndLineAddOnChargeRel.InvoicedTaxAmount
			AccruedTaxAmount				= PurchaseOrderAndLineAddOnChargeRel.AccruedTaxAmount
			ExtendedTaxableAmount			= PurchaseOrderAndLineAddOnChargeRel.ExtendedTaxableAmount
			LandedUnitCost					= PurchaseOrderAndLineAddOnChargeRel.LandedUnitCost
			LandedAddOnCharge				= PurchaseOrderAndLineAddOnChargeRel.LandedAddOnCharge
			CrossReferenceVendor			= PurchaseOrderAndLineAddOnChargeRel.CrossReferenceVendor

			Currency						= PurchaseOrderAndLineAddOnChargeRel.Currency
			ReceiptCurrencyConversionRate	= PurchaseOrderAndLineAddOnChargeRel.ReceiptCurrencyConversionRate
			Status							= "1"
			if (PurchaseOrderAndLineAddOnChargeRel.PurchaseOrderLine entered)
				if (PurchaseOrderReceiptLine.VendorBuyUOM = PurchaseOrderReceiptLine.VendorPriceUOM
				or  PurchaseOrderReceiptLine.VendorBuyUOMMultiplier entered)
					OriginalUnitCost		= PurchaseOrderAndLineAddOnChargeRel.EnteredUnitCost
				else
					OriginalUnitCost		= PurchaseOrderAndLineAddOnChargeRel.EnteredUnitCost * (PurchaseOrderReceiptLine.VendorPriceUOM/PurchaseOrderReceiptLine.VendorBuyUOM)
			else
				OriginalUnitCost = PurchaseOrderAndLineAddOnChargeRel.EnteredUnitCost
			if (PurchaseOrderReceiptLine entered)
				if (PurchaseOrderReceiptLine.EnteredReceivedQuantity = PurchaseOrderAndLineAddOnChargeRel.Quantity)
					TotalAddOnChargeAmount = PurchaseOrderAndLineAddOnChargeRel.TotalAddOnChargeAmount	
				else

					LocalPAOCompany						= PurchaseOrderAndLineAddOnChargeRel.Company
					LocalPAOPurchaseOrder 				= PurchaseOrderAndLineAddOnChargeRel.PurchaseOrder
					LocalPAOPurchaseOrderLine			= PurchaseOrderAndLineAddOnChargeRel.PurchaseOrderLine
					if (PurchaseOrderLineRel exist
					and (PurchaseOrderReceiptLine.BuyUOMQuantity < PurchaseOrderLineRel.ReceivedQuantity)
					and ((PurchaseOrderLineRel.ReceivedQuantity + PurchaseOrderReceiptLine.BuyUOMQuantity) = PurchaseOrderLineRel.Quantity))
						LocalPAOAddOnCharge					= PurchaseOrderAndLineAddOnChargeRel.AddOnCharge
						if (PoreccpdHeaderAOCRel exist)
							LocalRemainingAmountPurchaseOrderReceipt			= PoreccpdHeaderAOCRel.PurchaseOrderReceipt
							LocalRemainingAmountPurchaseOrderLine				= PurchaseOrderReceiptLine.PurchaseOrderLine
							LocalRemainingAmountPurchaseOrderReceiptLine		= PoreccpdHeaderAOCRel.PurchaseOrderReceiptLine
							if (PoreclineRemainingAmountRel exist)
								LocalToalAddOnChargeAmount 	= PoreccpdHeaderAOCRel.TotalAddOnChargeAmount
								PoreccwsTotalRecAOC 		+= LocalToalAddOnChargeAmount
					if (PoreccwsTotalRecAOC entered)
						TotalAddOnChargeAmount 			= PurchaseOrderAndLineAddOnChargeRel.TotalAddOnChargeAmount - PoreccwsTotalRecAOC
						PoreccwsRemainUnitCost			= TotalAddOnChargeAmount / PoreclineRemainingAmountRel.EnteredReceivedQuantity													

					if (TotalAddOnChargeAmount not entered)
						TotalAddOnChargeAmount = PurchaseOrderAndLineAddOnChargeRel.EnteredUnitCost * PurchaseOrderReceiptLine.BuyUOMQuantity
				ReceivedQuantity = PurchaseOrderReceiptLine.BuyUOMQuantity
				if (MatchedQuantity < 0)
					MatchedQuantity = 0
				ReceivedQuantityToMatch = ReceivedQuantity - MatchedQuantity
				if (ReceivedQuantityToMatch < 0)
					initialize ReceivedQuantityToMatch
				if (ReceivedQuantityToMatch = 0)
					Closed = "Y"
				if (PoreccwsRemainUnitCost not entered)
					ReceivedAmountToMatch = OriginalUnitCost * (PurchaseOrderReceiptLine.BuyUOMQuantity - MatchedQuantity * (PurchaseOrderReceiptLine.VendorBuyUOM/PurchaseOrderReceiptLine.VendorPriceUOM))
				else
					ReceivedAmountToMatch = PoreccwsRemainUnitCost * (PurchaseOrderReceiptLine.BuyUOMQuantity - MatchedQuantity * (PurchaseOrderReceiptLine.VendorBuyUOM/PurchaseOrderReceiptLine.VendorPriceUOM))
				if (ReceivedAmountToMatch !< 0
				or  PoreccwsAllowanceAoc)
					ReceivedAmountToMatch 		= ReceivedAmountToMatch
				else
					ReceivedAmountToMatch 		= 0
			else
				ReceivedQuantity 				= 1
				TotalAddOnChargeAmount 			= PurchaseOrderAndLineAddOnChargeRel.TotalAddOnChargeAmount
				MiscellaneousAddOnChargeInvoicedAmount	= PurchaseOrderAndLineAddOnChargeRel.TotalAddOnChargeAmount
				initialize ReceivedAmountToMatch
			
			OpenToMatchQuantity = ReceivedQuantityToMatch 

	Create Exit Rules
		if (AddOnChargeAmountPOCurrency entered)
			invoke UpdateHeaderAmountsFromAddOnCharge PurchaseOrderReceipt
				invoked.PrmTrackType				= TrackType
				invoked.PrmTotalAddOnChargeAmount	= AddOnChargeAmountPOCurrency

	Actions
		BatchCreateCostAdjustment is a Set Action 
			restricted
			Parameters
				PrmCompany							is a PurchasingCompany
				PrmPurchaseOrder					is a PurchaseOrder
				PrmPurchaseOrderLine			is a PurchaseOrderLine
				PrmAddOnCharge					is an AddOnCharge
				
			Instance Selection
				where (Company 																	= PrmCompany
				and PurchaseOrderReceipt.PurchaseOrder						= PrmPurchaseOrder
				and PurchaseOrderReceiptLine.PurchaseOrderLine 		= PrmPurchaseOrderLine
				and AddOnCharge																= PrmAddOnCharge
				and LandedUnitCost															> 0)

			Sort Order 
				Company
                PurchaseOrder
                PurchaseOrderReceipt
                PurchaseOrderReceiptLine
                AddOnCharge
                
			Action Rules
				Instance Rules
					invoke UpdateForCostAdjustment ItemLocationRel
						invoked.PrmCostDifference 			= DerivedLandedUnitCostInStockUOM * -1 
						invoked.PrmQuantity 					= DerivedReceivedQuantityInStockUOM
						invoked.PrmDocumentNumber 	= PurchaseOrderReceiptLine.DerivedInventoryTransaction 
						invoked.PrmLineNumber				= PurchaseOrderReceiptLine
						if (PurchaseOrderReceipt.PurchaseOrder.HasProcessLevel)
							invoked.PrmOffsetAccount 		= PurchaseOrderReceipt.PurchaseOrder.ProcessLevel.ReceiptAccrualAccount
						else
							invoked.PrmOffsetAccount 		= PurchaseOrderReceipt.PurchaseOrder.MatchCompanyRel.ReceiptAccrualAccount
							
		CreateCostAdjustment is an Instance Action
			restricted
			valid when (ForCostAdjustment)
			Action Rules
				invoke UpdateForCostAdjustment ItemLocationRel
					invoked.PrmCostDifference 	= DerivedLandedUnitCostInStockUOM * -1 
					invoked.PrmQuantity 		= DerivedReceivedQuantityInStockUOM
					invoked.PrmDocumentNumber 	= PurchaseOrderReceiptLine.DerivedInventoryTransaction 
					invoked.PrmLineNumber		= PurchaseOrderReceiptLine
					if (PurchaseOrderReceipt.PurchaseOrder.HasProcessLevel)
						invoked.PrmOffsetAccount = PurchaseOrderReceipt.PurchaseOrder.ProcessLevel.ReceiptAccrualAccount
					else
						invoked.PrmOffsetAccount = PurchaseOrderReceipt.PurchaseOrder.MatchCompanyRel.ReceiptAccrualAccount
				
		Create is an Action 
			valid when (!ReleasedReceiver)


		CreatefromPorecepd is a Create Action
			restricted

            Action Rules




				
				include CreateFromPoreccpdRules


				if (PurchaseOrderReceiptLine.Item not entered)
					if (PurchaseOrderReceiptLine.Company not entered)
						LocalCompany	= Company				
				
				if (PurchaseOrderReceiptLine entered)
					TaxCode = PurchaseOrderAndLineAddOnChargeRel.TaxCode

				LocalRunRoutine = TotalTaxAmount

				if (PorecuwsUpdFromPo38)
					if (AddOnCharge.LandedAddOnCharge)
						LandedAddOnCharge = true
					constraint (AddOnCharge.SpreadMethod = "N")
						"MustUseAOCCodeWithNoSpreadForE/R/SA/O/C"							
											 
		Update is an Action 
			valid when (!ReleasedReceiver)
			Entrance Rules
				LocalOldAddOnChargeAmountPOCurrency		= AddOnChargeAmountPOCurrency
			Action Rules









				ReceivedQuantityToMatch = PurchaseOrderReceiptLine.EnteredReceivedQuantity - PurchaseOrderReceiptLine.RejectedQuantity
				ReceivedQuantity = ReceivedQuantityToMatch
				Quantity = ReceivedQuantity
				ShipToLocation = PurchaseOrderReceiptLine.ShipToLocation
				ReceivedAmountToMatch =  TotalAddOnChargeAmount
				OriginalUnitCost = TotalAddOnChargeAmount / Quantity
				MatchDetailKey = PurchaseOrderReceiptLine.MatchDetailKey
				AddOnChargeAmountPOCurrency = DerivedAddOnChargeAmountPOCurrency
				AddOnChargeEntryMethod = AddOnChargeEntryMethod.UnitCost

			Exit Rules
				if (LocalOldAddOnChargeAmountPOCurrency not = AddOnChargeAmountPOCurrency)
					invoke UpdateHeaderAmountsFromAddOnCharge PurchaseOrderReceipt
						invoked.PrmTrackType				= TrackType
						invoked.PrmTotalAddOnChargeAmount	= LocalOldAddOnChargeAmountPOCurrency * -1
	
					invoke UpdateHeaderAmountsFromAddOnCharge PurchaseOrderReceipt
						invoked.PrmTrackType				= TrackType
						invoked.PrmTotalAddOnChargeAmount	= AddOnChargeAmountPOCurrency


		UpdatefromPorecepd is an Update Action
			restricted
	                      
            Action Rules
				LocalRunRoutine = TotalTaxAmount

				if (PorecuwsUpdFromPo38)
					if (AddOnCharge.LandedAddOnCharge)
						LandedAddOnCharge = true
					constraint (AddOnCharge.SpreadMethod = "N")
						"MustUseAOCCodeWithNoSpreadForE/R/SA/O/C"	
            				
		UpdateFromMatch is an Update Action
			restricted
			bypass field rules
		
		CloseFromLineAddOnCharge is an Instance Action
			restricted
			Action Rules
				Closed = Closed.Yes				
				
		Delete is an Action
			valid when (!ReleasedReceiver)
			Exit Rules
	    		invoke UpdateHeaderAmountsFromAddOnCharge PurchaseOrderReceipt
					invoked.PrmTrackType				= TrackType
					invoked.PrmTotalAddOnChargeAmount	= AddOnChargeAmountPOCurrency * -1
		
		UpdateReceiptLineCosts is an Instance Action
			restricted
			Parameters
				PrmOldEnteredUnitCost is an InternationalCost
			Action Rules
				if (OriginalUnitCost != PrmOldEnteredUnitCost)
					OriginalUnitCost = PrmOldEnteredUnitCost  
			 	
		Purge is a Purge Action
			restricted

					
