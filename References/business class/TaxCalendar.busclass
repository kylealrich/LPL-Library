TaxCalendar is a BusinessClass
    owned by tx
    prefix is TXcld


    Ontology
        symbolic key is TaxCalendar


    Patterns
        implements StaticJava
        disable AuditIndex

    Relations

        IntrastatCalendarPeriodsRel
            one-to-many relation to IntrastatCalendarPeriod
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
                related.TaxCalendar.TaxEntity 	= TaxCalendar.TaxEntity
                related.TaxCalendar.TaxYear 	= TaxCalendar.TaxYear

        ExistingIntrastatCalendarPeriodsRel
            one-to-many relation to IntrastatCalendarPeriod
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 	= LocalExistingFinEntGrp	
                related.TaxCalendar.TaxEntity 	= LocalTaxEntity
                related.TaxCalendar.TaxYear 	= LocalExistingTaxYear
                
        NewIntrastatCalendarPeriodRel
            one-to-many relation to IntrastatCalendarPeriod
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
                related.TaxCalendar.TaxEntity 	= TaxCalendar.TaxEntity
                related.TaxCalendar.TaxYear 	= LocalNewTaxYear
			Instance Selection
                where (related.IntrastatCalendarPeriod 	= LocalNewTaxPeriod)
                
       	TaxCalendarPeriodsRel
            one-to-many relation to TaxCalendarPeriod
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
                related.TaxCalendar.TaxEntity 	= TaxCalendar.TaxEntity
                related.TaxCalendar.TaxYear 	= TaxCalendar.TaxYear

   		ExistingTaxCalendarPeriodsRel
            one-to-many relation to TaxCalendarPeriod
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 		= LocalExistingFinEntGrp	
                related.TaxCalendar.TaxEntity 		= LocalTaxEntity			 
                related.TaxCalendar.TaxYear 		= LocalExistingTaxYear
                
       	NewTaxCalendarPeriodsRel
            one-to-many relation to TaxCalendarPeriod
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
                related.TaxCalendar.TaxEntity 		= TaxCalendar.TaxEntity
                related.TaxCalendar.TaxYear 		= LocalNewTaxYear
                related.TaxCalendarPeriod 			= LocalNewTaxPeriod
                                
      	ExistingTaxCalendarRel												
            one-to-one relation to TaxCalendar
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= LocalExistingFinEntGrp	
                related.TaxCalendar.TaxEntity 	= TaxCalendar.TaxEntity
                related.TaxCalendar.TaxYear		= LocalExistingTaxYear    

    	NewTaxCalendarRel													
            one-to-one relation to TaxCalendar
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup   
                related.TaxCalendar.TaxEntity 	= TaxCalendar.TaxEntity
                related.TaxCalendar.TaxYear		= LocalNewTaxYear   

       	TaxTransactionsRel
            one-to-many relation to TaxTransaction
            Field Mapping uses symbolic key	 
            	related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup 
                related.TaxEntity 							= TaxCalendar.TaxEntity
   			Instance Selection
                where 	(related.TaxPointDate year			= TaxCalendar.TaxYear)						  		 
                
	Local Fields										
		LocalExistingFinEntGrp	is like FinanceEnterpriseGroup
		LocalExistingTaxYear	is a TaxYear
		LocalNewTaxYear			is a TaxYear
		LocalNewTaxPeriod		is Numeric 2
		LocalTaxEntity			is a TaxEntity  
		LocalPeriodEndDate		is Date   

	Derived Fields										
		NewPerEndDateNoDashes is a StringField	 
			type is Alpha 10
			LocalNewTaxYear 
			DateAlpha8[5:6]
			DateAlpha8[7:8]	

		DateAlpha8 is a DerivedField
			type is Alpha 8
			return LocalPeriodEndDate					

		DerivedPeriodName is a DerivedField
			type is Alpha 10
			if (LocalNewTaxPeriod = 1)
				return "January"
			if (LocalNewTaxPeriod = 2)
				return "February"
			if (LocalNewTaxPeriod = 3)
				return "March"
			if (LocalNewTaxPeriod = 4)
				return "April"
			if (LocalNewTaxPeriod = 5)
				return "May"
			if (LocalNewTaxPeriod = 6)
				return "June"				
			if (LocalNewTaxPeriod = 7)
				return "July"
			if (LocalNewTaxPeriod = 8)
				return "August"
			if (LocalNewTaxPeriod = 9)
				return "September"
			if (LocalNewTaxPeriod = 10)
				return "October"
			if (LocalNewTaxPeriod = 11)
				return "November"
			if (LocalNewTaxPeriod = 12)
				return "December"																								
			return blank	 
						
    Conditions

        HasReportedVatPeriods
        	restricted
            when (any TaxCalendarPeriodsRel.Status.Reported)

        HasReportedIntrastatPeriods
        	restricted
            when (any IntrastatCalendarPeriodsRel.Status.Reported)
            

	Actions

        Create is a Create Action

   		Update is an Update Action
						
        Delete is a Delete Action
			Entrance Rules
				constraint (!HasReportedVatPeriods
				and			!HasReportedIntrastatPeriods)
					"TaxYearHasReportedPeriods;CannotBeDeleted"	
           	Action Rules


		CreateNewCalendarFromExisting is an Instance Action	 				
			Parameters
				PrmFinEntGrp				is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"				
				PrmTaxEntity				is a TaxEntity
					default label is "TaxEntity"	
				PrmExistingTaxYear			is a TaxYear
					default label is "ExistingTaxYear"
				NewTaxYear					is a TaxYear
				PrmCreateVatPeriods is AlphaUpper size 1
		            States
		                No				   	   value is ""
		                FromExistingVatPeriods value is "E"
		                FromIntrastatPeriods   value is "I"
		         	default label is "CreateNewVATPeriods?" 
				CreateNewIntrastatPeriods is AlphaUpper size 1
		            States
		                No			   	   	   			value is ""
		                FromExistingIntrastatPeriods    value is "E"
		                FromVatPeriods        			value is "V"
		         	default label is "CreateNewIntrastatPeriods?" 
		         	                
			Parameter Rules
				PrmFinEntGrp
					initial value is actor.context.FinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					required
				PrmTaxEntity
					initial value is TaxCalendar.TaxEntity
					required
				PrmExistingTaxYear
					initial value is TaxCalendar.TaxYear
					required
				NewTaxYear 	 
					default to TaxCalendar.TaxYear
					required
			Local Fields										 
				NewPeriodEndDate	is Date 
			Action Rules
				LocalExistingFinEntGrp = PrmFinEntGrp
				if (NewTaxYear not entered)
					NewTaxYear = TaxCalendar.TaxYear
				LocalExistingTaxYear	= PrmExistingTaxYear
				LocalTaxEntity			= PrmTaxEntity
				LocalNewTaxYear			= NewTaxYear
				if (ExistingTaxCalendarRel exist)
					if (NewTaxCalendarRel not exist)
						invoke Create TaxCalendar
							fill in fields from this instance
							invoked.TaxEntity	= PrmTaxEntity
							invoked.TaxYear		= NewTaxYear




					if (PrmCreateVatPeriods.FromExistingVatPeriods)
						for each ExistingTaxCalendarPeriodsRel	 
							initialize LocalPeriodEndDate
							LocalPeriodEndDate		= each.PeriodEndDate
							NewPeriodEndDate		= NewPerEndDateNoDashes	 
							if (NewTaxCalendarPeriodsRel not exist)
								invoke Create TaxCalendarPeriod
									fill in fields from this instance

									invoked.TaxCalendar.TaxEntity	= each.TaxCalendar.TaxEntity 
									invoked.TaxCalendar.TaxYear		= NewTaxYear
									invoked.TaxCalendarPeriod		= each.TaxCalendarPeriod
									invoked.PeriodEndDate  			= NewPeriodEndDate	 		
									invoked.Status					= 1			
					else						
					if (PrmCreateVatPeriods.FromIntrastatPeriods)  
						for each ExistingIntrastatCalendarPeriodsRel	 
							initialize LocalPeriodEndDate
							LocalPeriodEndDate		= each.PeriodEndDate
							LocalNewTaxPeriod		= each.IntrastatCalendarPeriod
							if (NewTaxCalendarPeriodsRel not exist)
								invoke Create TaxCalendarPeriod
									fill in fields from this instance
									invoked.TaxCalendar.TaxEntity	= each.TaxCalendar.TaxEntity 
									invoked.TaxCalendar.TaxYear		= each.TaxCalendar.TaxYear
									invoked.TaxCalendarPeriod		= LocalNewTaxPeriod		 
									invoked.PeriodEndDate  			= LocalPeriodEndDate	  		
									invoked.Status					= 1			




					if (CreateNewIntrastatPeriods.FromExistingIntrastatPeriods)					
						for each ExistingIntrastatCalendarPeriodsRel	 
							initialize LocalPeriodEndDate
							LocalNewTaxYear			= NewTaxYear
							LocalPeriodEndDate		= each.PeriodEndDate
							NewPeriodEndDate		= NewPerEndDateNoDashes	
							LocalNewTaxPeriod		= each.IntrastatCalendarPeriod  
							if (NewIntrastatCalendarPeriodRel not exist)
								invoke Create IntrastatCalendarPeriod
									fill in fields from this instance
									invoked.TaxCalendar.TaxEntity	= each.TaxCalendar.TaxEntity 
									invoked.TaxCalendar.TaxYear		= NewTaxYear
									invoked.IntrastatCalendarPeriod	= LocalNewTaxPeriod
									invoked.PeriodEndDate  			= NewPerEndDateNoDashes 
									if (each.PeriodName not entered)
										invoked.PeriodName			= DerivedPeriodName 
									else	
										invoked.PeriodName			= each.PeriodName
									invoked.Status					= 1			
					else											
					if (CreateNewIntrastatPeriods.FromVatPeriods)
						for each ExistingTaxCalendarPeriodsRel	 
							initialize LocalPeriodEndDate
							LocalNewTaxYear			= NewTaxYear
							LocalPeriodEndDate		= each.PeriodEndDate
							LocalNewTaxPeriod		= each.TaxCalendarPeriod 
							NewPeriodEndDate		= NewPerEndDateNoDashes	 
							if (NewIntrastatCalendarPeriodRel not exist)
								invoke Create IntrastatCalendarPeriod
									fill in fields from this instance
									invoked.TaxCalendar.TaxEntity	= each.TaxCalendar.TaxEntity 
									invoked.TaxCalendar.TaxYear		= NewTaxYear
									invoked.IntrastatCalendarPeriod	= LocalNewTaxPeriod
									invoked.PeriodEndDate  			= NewPerEndDateNoDashes  
									invoked.PeriodName				= DerivedPeriodName
									invoked.Status					= 1							                    
                                                                              
