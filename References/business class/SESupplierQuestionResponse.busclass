SESupplierQuestionResponse is a BusinessClass
    owned by ss
    prefix is ESRQ

    Ontology
    	part of SourcingEventResponse 
    		relative key is SourcingEventSupplierQuestion
    			
   	Patterns
        implements TemplateDriven by SourcingEventSupplierQuestion
        	create instance
        		when (SESupplierQuestionAnswer entered)

    Persistent Fields
    	SESupplierQuestionAnswer
    	Attachment
    	FromTechnicalProposal   is Boolean
    	CMAttachment            is Boolean	
    
	Local Fields
		LocalRepositoryQuestion		is a Question
		AnswerChanged				is Boolean
		LocalLastQuestion			is Numeric 2
		LocalNewConditionalCreated	is Boolean
		LocalNoNewConditionalQuestions	is Boolean

    Sets
    	ByQuestion
    		duplicates
    		Sort Order
    			Company
    			SourcingEvent
    			SourcingEventSupplierQuestion
    			SourcingEventResponse
    			
    	BySupplier
    		Sort Order
    			Company
    			SourcingEvent
    			NotifiedSupplier.SupplierGroup
    			NotifiedSupplier.Supplier
    			NotifiedSupplier.SupplierSourceId
    			SourcingEventResponse
    			SourcingEventSupplierQuestion
    
	Derived Fields
		ResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (SourcingEventSupplierQuestion.ResponseType.Text)
				SESupplierQuestionAnswer.TextValue
			else
			if (SourcingEventSupplierQuestion.ResponseType.Number)
				SESupplierQuestionAnswer.NumericValue
			else
			if (SourcingEventSupplierQuestion.ResponseType.Date)
				DerivedDate
			else
			if (SourcingEventSupplierQuestion.ResponseType.List)
				SESupplierQuestionAnswer.SESupplierQuestionValue
			else
			if (SourcingEventSupplierQuestion.ResponseType.YesNo)
				if (SESupplierQuestionAnswer.YesNoValue.Yes)
					YesText
				else
					NoText
			else
				YesNoTextValue
	
		SecondStepYesMessage is a MessageField
			restricted
			"Yes"
			
		SecondStepNoMessage is a MessageField
			restricted
			"No"
					
		TechnicalProposalYesNo is a DerivedField
			type is Alpha 10
			restricted
			if (SourcingEventResponse.TechnicalProposalAccepted)
				return SecondStepYesMessage
			else
				return SecondStepNoMessage
		
		BestAndFinalYesNo is a DerivedField
			type is Alpha 10
			restricted
			if (SourcingEventResponse.BestAndFinalSupplier)
				return SecondStepYesMessage
			else
				return SecondStepNoMessage
		
		TechnicalProposalAccepted is a DerivedField
			type is Alpha 10
			if (SourcingEvent.TwoStepBiddingUsed)
				return (TechnicalProposalYesNo)
			else
				return blank
				
		BestAndFinalSupplier is a DerivedField
			type is Alpha 10
			if (SourcingEvent.BestAndFinalOffer)
				return (BestAndFinalYesNo)
			else
				return blank	
		
		YesNoTextValue is a StringField
			type is Alpha size 260
			restricted
			YesNoText
			"-"
			SESupplierQuestionAnswer.YesNoText.Text

		YesNoText is a ConditionalField
			type is Text
			restricted
			if (SESupplierQuestionAnswer.YesNoText.YesNo = 1)
				YesText
			else
				NoText

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"

		DerivedDate is a StringField
			type is Alpha 10
			restricted
			SESupplierQuestionAnswer.DateValue month
			"/"
			SESupplierQuestionAnswer.DateValue day
			"/"
			SESupplierQuestionAnswer.DateValue year

		QuestionScore is a DerivedField
			type is like InternationalAmount
			if (!SourcingEventResponse.NoBid)
				if (SourcingEventSupplierQuestion.ResponseType.YesNoText)
					return (first AllocQuestionYesNoTextRel.ScoreAllocation * SourcingEventQuestionRel.QuestionWeighting)
				else
					return (first AllocQuestionAnswerRel.ScoreAllocation * SourcingEventQuestionRel.QuestionWeighting)
			else
				return 0 
				
		SetSendToCM is a ConditionalField
			type is Text
			restricted
			if (CMAttachment)
				SetToNoMessage
			else
				SetToYesMessage
		
		SetToNoMessage is a MessageField
			restricted
   			"Do_Not_Email_Attachment"
   			
   		SetToYesMessage is a MessageField
			restricted
   			"Email_Attachment_On_Contract"
   			
   		ResponseRequiredMessage is a MessageField
			restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
			restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
			restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsYes"
            
        NoAttachmentMessage is a MessageField
			restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsNo"
            
        YesOrNoAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsYes"
            
        NoAttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsNo"
            
        AttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsYes"
            
        NoAttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsNo"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
			restricted
			if (SourcingEventSupplierQuestion.SourcingEventQuestion entered)
	            if (SourcingEventQuestionRel.ResponseRules.ResponseRequired
    	        or  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequired)
        	        ResponseRequiredMessage        
            	else
	            if (SourcingEventQuestionRel.ResponseRules.ResponseRequiredAndAttachmentRequired
    	        or  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
        	        ResponseAndAttachmentRequiredMessage
            	else
	            if (SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
    	            YesAttachmentMessage
        	    else
            	if (SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                	NoAttachmentMessage
	            else
    	        if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
        	        YesOrNoAndTextMessage
            	else
	            if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
    	            AttachmentOrTextMessage
        	    else
            	if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                	YesAttachmentOrTextMessage
	            else
    	        if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
        	        NoAttachmentOrTextMessage
            	else 
	            if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
    	            AttachmentAndTextMessage
        	    else
            	if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                	YesAttachmentAndTextMessage
	            else
    	        if (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
        	        NoAttachmentAndTextMessage
            	else
                	blank
			else
			if (SourcingEventSupplierQuestion.Question entered)
	            if (QuestionRel.ResponseRules.ResponseRequired
    	        or  QuestionRel.YesNoResponseRules.ResponseRequired)
        	        ResponseRequiredMessage        
            	else
	            if (QuestionRel.ResponseRules.ResponseRequiredAndAttachmentRequired
    	        or  QuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
        	        ResponseAndAttachmentRequiredMessage
            	else
	            if (QuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
    	            YesAttachmentMessage
        	    else
            	if (QuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                	NoAttachmentMessage
	            else
    	        if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
        	        YesOrNoAndTextMessage
            	else
	            if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
    	            AttachmentOrTextMessage
        	    else
            	if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                	YesAttachmentOrTextMessage
	            else
    	        if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
        	        NoAttachmentOrTextMessage
            	else 
	            if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
    	            AttachmentAndTextMessage
        	    else
            	if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                	YesAttachmentAndTextMessage
	            else
    	        if (QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
        	        NoAttachmentAndTextMessage
            	else
                	blank
			else
				blank
   			
        IsQuestionRequired is a ConditionalField
            type is Alpha 125
			restricted
			if (SourcingEventSupplierQuestion.SourcingEventQuestion entered)
	            if (SourcingEventQuestionRel.ResponseRules.ResponseRequired
    	        or  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequired
	            or  SourcingEventQuestionRel.ResponseRules.ResponseRequiredAndAttachmentRequired
    	        or  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequired
	            or  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            	or  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo
    	        or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired
	            or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
            	or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
    	        or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo
	            or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            	or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes
    	        or  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
					"*"
            	else
                	blank
			else
			if (SourcingEventSupplierQuestion.Question entered)
	            if (QuestionRel.ResponseRules.ResponseRequired
    	        or  QuestionRel.YesNoResponseRules.ResponseRequired
	            or  QuestionRel.ResponseRules.ResponseRequiredAndAttachmentRequired
    	        or  QuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequired
	            or  QuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            	or  QuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo
    	        or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired
	            or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
            	or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
    	        or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo
	            or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            	or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes
    	        or  QuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
					"*"
            	else
                	blank
			else
				blank
   			
 		AnswerMessage is a MessageField
 			restricted
 			"Answer"

		AnswerTextWithRequired is a StringField
 			type is Text
			restricted
	   		IsQuestionRequired 
			" "
	   		AnswerMessage

	Conditions
		HasQuestionAttachment
			restricted
			when (SourcingEventQuestionRel.Attachment entered)
		HasQuestionAnswerAttachment
			when (Attachment entered)
		HasWeightedQuestion
			restricted
			when (SourcingEventQuestionRel.HasWeighting)
		
		CanSetCMToNo
			restricted
			when (SourcingEvent.ContractOutputExists 
			and   CMAttachment
			and   HasQuestionAnswerAttachment
			and   SourcingEventQuestionRel.CMQuestion)
			
		CanSetCMToYes
			restricted
			when (SourcingEvent.ContractOutputExists
			and   CMAttachment = false
			and   HasQuestionAnswerAttachment
			and   SourcingEventQuestionRel.CMQuestion)
			
		DisplayCMAttachmentButton
			restricted
			when (SourcingEvent.ContractOutputExists
			and   HasQuestionAnswerAttachment
			and   SourcingEvent.Status.Open
			and   SourcingEventQuestionRel.CMQuestion)
			
		HasResponseAttachment
			restricted
            when (Attachment entered)
        
        AttachmentAlwaysRequired   
			restricted
            when (SourcingEventQuestionRel.AlwaysRequireResponseAttachment
            and  (SourcingEventQuestionRel.NonYesNoResponseType
            or    SourcingEventQuestionRel.ResponseType.YesNo))

		ConditionalQuestionsExist 
			restricted
			when (((SourcingEventSupplierQuestion.ResponseType.YesNo
			or    SourcingEventSupplierQuestion.ResponseType.YesNoText)
			and   ConditionalYesNoQuestionRel exists)
			or   (SourcingEventSupplierQuestion.ResponseType.List
			and   ConditionalListQuestionRel exists))			

		SESupplierQuestionAnswerOnlyATextChange
			restricted
			when (SourcingEventSupplierQuestion.ResponseType.YesNoText
			and   SESupplierQuestionAnswer.YesNoText.YesNo not changed
			and   SESupplierQuestionAnswer.YesNoText.Text changed)

		SESupplierQuestionAnswerEntered 
			restricted
			when (SESupplierQuestionAnswer.TextValue				not = blank
			or    SESupplierQuestionAnswer.NumericValue				> 0
			or    SESupplierQuestionAnswer.DateValue				> 0
			or    SESupplierQuestionAnswer.SESupplierQuestionValue	not = blank
			or    SESupplierQuestionAnswer.YesNoValue				> 0
			or    SESupplierQuestionAnswer.YesNoText.YesNo			> 0
			or    SESupplierQuestionAnswer.YesNoText.Text			not = blank)

	Relations
		AllocQuestionAnswerRel 
			one-to-many relation to SourcingEventQuestionAllocation
			Field Mapping uses symbolic key
				related.Company														= Company
				related.SourcingEvent												= SourcingEvent
				related.SourcingEventQuestion 										= SourcingEventSupplierQuestion
				related.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer = SESupplierQuestionAnswer
			Instance Selection
				where (SourcingEventQuestionRel.EligibleForWeighting)

		AllocQuestionYesNoTextRel 
			one-to-many relation to SourcingEventQuestionAllocation
			Field Mapping uses symbolic key
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventQuestion 			= SourcingEventSupplierQuestion
			Instance Selection
				where (SourcingEventQuestionRel.EligibleForWeighting
				and    related.SourcingEventQuestionAllocation.SourcingEventQuestionAnswer.YesNoText.YesNo = SESupplierQuestionAnswer.YesNoText.YesNo)
		
		EventQuestionsAndResponsesRel	
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
			Instance Selection
				where (related.Company					= Company
				and related.SourcingEvent				= SourcingEvent
				and related.SourcingEventQuestion 		= SourcingEventSupplierQuestion			
    			and related.SourcingEventResponse		= SourcingEventResponse)

		SourcingEventQuestionRel 
			one-to-one relation to SourcingEventQuestion
			Field Mapping uses symbolic key
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventQuestion			= SourcingEventSupplierQuestion.SourcingEventQuestion

		SourcingEventSupplierQuestionRel 
			one-to-many relation to SourcingEventSupplierQuestion
			Field Mapping uses symbolic key
				related.NotifiedSupplier.SupplierGroup	= actor.agent(SupplierSourceId).SupplierGroup
	 			related.NotifiedSupplier.Supplier		= actor.agent(SupplierSourceId).Supplier
	 			related.NotifiedSupplier.SupplierSourceId = actor.agent(SupplierSourceId).SupplierSourceId
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion	= SourcingEventSupplierQuestion

		SameSourcingEventSupplierQuestionRel 
			one-to-many relation to SourcingEventSupplierQuestion 
			Field Mapping uses ByQuestion
				related.NotifiedSupplier.SupplierGroup	= NotifiedSupplier.SupplierGroup
	 			related.NotifiedSupplier.Supplier		= NotifiedSupplier.Supplier
	 			related.NotifiedSupplier.SupplierSourceId = NotifiedSupplier.SupplierSourceId
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.Question						= LocalRepositoryQuestion

		ConditionalListQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup				= Company.SourcingGroup 
				related.Question						= SourcingEventSupplierQuestion.Question 
				related.QuestionAnswer					= SESupplierQuestionAnswer.SESupplierQuestionValue 

		ConditionalYesNoQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup				= Company.SourcingGroup
				related.Question						= SourcingEventSupplierQuestion.Question 
			Instance Selection
				where ((SourcingEventSupplierQuestion.ResponseType.YesNo
				and	    related.ConditionalQuestion.QuestionAnswer.QuestionAnswerGroup.YesNoAnswer		= SESupplierQuestionAnswer.YesNoValue)		
				or     (SourcingEventSupplierQuestion.ResponseType.YesNoText
				and     related.ConditionalQuestion.QuestionAnswer.QuestionAnswerGroup.YesNoText.YesNo	= SESupplierQuestionAnswer.YesNoText.YesNo))

		LocalRepositoryQuestionRel 
			one-to-one relation to Question 
			Field Mapping uses symbolic key 
				related.ProcurementGroup				= Company.SourcingGroup
				related.Question						= LocalRepositoryQuestion

		QuestionRel 
			one-to-one relation to Question 
			Field Mapping uses symbolic key 
				related.ProcurementGroup				= Company.SourcingGroup
				related.Question						= SourcingEventSupplierQuestion.Question

		CreatedFromThisQuestionAnsweredRel
			one-to-many relation to SourcingEventSupplierQuestion 
			Field Mapping uses symbolic key  
				related.NotifiedSupplier				= NotifiedSupplier
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
			Instance Selection
				where (related.CreatedFromQuestion		= SourcingEventSupplierQuestion
				and    related.AnyQuestionAnswers)	

		CreatedFromThisQuestionRel 
			one-to-many relation to SourcingEventSupplierQuestion 
			Field Mapping uses symbolic key  
				related.NotifiedSupplier				= NotifiedSupplier
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
			Instance Selection 
				where (related.CreatedFromQuestion		= SourcingEventSupplierQuestion)			

		CreatedFromThisPreviousQuestionRel 
			one-to-one relation to SourcingEventSupplierQuestion 
			Field Mapping uses symbolic key  
				related.NotifiedSupplier				= NotifiedSupplier
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion	= SourcingEventSupplierQuestion.CreatedFromQuestion			

	Field Rules
        
        SESupplierQuestionAnswer
            if (!SourcingEventResponse.NoBid)
	            if (SourcingEventQuestionRel.ResponseRequired)
	                required
	                    "MustAnswerAllRequiredQuestions"
	                    
	            if (SourcingEventSupplierQuestion.ResponseType.YesNoText
	            and !HasResponseAttachment
	            and (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
	            or  (SESupplierQuestionAnswer.YesNoText.YesNo.Yes
	            and  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
	            or  (SESupplierQuestionAnswer.YesNoText.YesNo.No
	            and  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
	                constraint (SESupplierQuestionAnswer.YesNoText.Text entered)
	                    "MustEnterEitherTextOrAnAttachmentForQuestion<SourcingEventSupplierQuestion.QuestionText>"
	                    
	            if (SourcingEventSupplierQuestion.ResponseType.YesNo
	            and (SESupplierQuestionAnswer.YesNoValue.Yes
	            and  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
	            or  (SESupplierQuestionAnswer.YesNoValue.No
	            and  SourcingEventQuestionRel.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SourcingEventSupplierQuestion.QuestionText>"    
	            
	            if (AttachmentAlwaysRequired
	            and !SourcingEventSupplierQuestion.ResponseType.YesNoText)
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SourcingEventSupplierQuestion.QuestionText>"
	                       
	            if  (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
	            or   SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SESupplierQuestionAnswer.YesNoText.YesNo.Yes
	            and  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SESupplierQuestionAnswer.YesNoText.YesNo.No
	            and  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (SESupplierQuestionAnswer.YesNoText.Text entered)
	                    "MustEnterTextForQuestion<SourcingEventSupplierQuestion.QuestionText>"
	                    
	            if  (SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SESupplierQuestionAnswer.YesNoText.YesNo.Yes
	            and  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SESupplierQuestionAnswer.YesNoText.YesNo.No
	            and  SourcingEventQuestionRel.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SourcingEventQuestionRel.QuestionText>" 
    
        Attachment
            if (SourcingEventQuestionRel.AlwaysRequireResponseAttachment
            and !SourcingEventResponse.NoBid)
                required
                    "MustAttachADocumentForAllQuestionsRequiringAnAttachment"
            if (!SESupplierQuestionAnswer entered)
                cannot be entered
                    "MustAnswerQuestionToAttachADocument"   
                    
        CMAttachment
            if (!SourcingEvent.CreateByCopy
            and  HasQuestionAnswerAttachment
            and  SourcingEventQuestionRel.CMQuestion)
                default to true
        
	Rule Blocks

		CreateConditionalSESupplierQuestion

			if ((SourcingEventSupplierQuestion.ResponseType.YesNo
			or   SourcingEventSupplierQuestion.ResponseType.YesNoText)
			and  ConditionalYesNoQuestionRel exists)
				for each ConditionalYesNoQuestionRel
					LocalRepositoryQuestion = each.ConditionalQuestion
					if (SameSourcingEventSupplierQuestionRel !exists)
						invoke AttachToSourcingEventSupplierQuestion LocalRepositoryQuestionRel
							invoked.ParmCompany					= Company
							invoked.ParmSourcingEvent			= SourcingEvent
							invoked.ParmFromQuestion			= LocalRepositoryQuestion
							invoked.ParmCreatedFromQuestion		= SourcingEventSupplierQuestion
							invoked.ParmNotifiedSupplier		= NotifiedSupplier
							LocalNewConditionalCreated			= true

			if (SourcingEventSupplierQuestion.ResponseType.List
			and ConditionalListQuestionRel exists)
				for each ConditionalListQuestionRel
					LocalRepositoryQuestion = each.ConditionalQuestion
					if (SameSourcingEventSupplierQuestionRel !exists)
						invoke AttachToSourcingEventSupplierQuestion LocalRepositoryQuestionRel
							invoked.ParmCompany					= Company
							invoked.ParmSourcingEvent			= SourcingEvent
							invoked.ParmFromQuestion			= LocalRepositoryQuestion
							invoked.ParmCreatedFromQuestion		= SourcingEventSupplierQuestion
							invoked.ParmNotifiedSupplier		= NotifiedSupplier
							LocalNewConditionalCreated			= true

    Actions
     	Create is a Create Action
     		Action Rules
 				if (!SourcingEventResponse.HasCurrentModificationRequests)
	 				constraint (SourcingEventResponse.CanUpdateResponse)
 						"BuyerCannotAnswerQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
 					constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEvent.BestAndFinalStarted
	 				and !SourcingEvent.StepTwoStarted
	 				and !SourcingEventResponse.HasCurrentModificationRequests)
	 					constraint (SourcingEvent.CanRespond)  
	 						"CanOnlyRespondBetweenEventOpenDateAndTimeAndCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotCreateIfResponseHasBeenSubmitted"
				if (ConditionalQuestionsExist)
					confirmation required
						"AdditionalQuestionsWillBeCreatedAsAResultOfYourAnswers"
 				
   			Exit Rules
				if (ConditionalQuestionsExist)
					include CreateConditionalSESupplierQuestion

     	Update is an Update Action
     		Action Rules
				if (SourcingEventSupplierQuestion.YesNoOrList
				and	SESupplierQuestionAnswer changed
				and !SESupplierQuestionAnswerOnlyATextChange)
					AnswerChanged = true
					constraint (CreatedFromThisQuestionAnsweredRel !exists)
						"CannotChangeAnswer;ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichHaveBeenAnswered"
					if (CreatedFromThisQuestionRel exists)
						confirmation required 
							"ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichWillBeDeleted"	
						LocalLastQuestion = last CreatedFromThisQuestionRel.SourcingEventSupplierQuestion

					if (ConditionalQuestionsExist)
						confirmation required
							"AdditionalQuestionsWillBeCreatedAsAResultOfYourChangeToAnswers"

 				if (!SourcingEventResponse.HasCurrentModificationRequests)
 					constraint (SourcingEventResponse.CanUpdateResponse)
 						"BuyerCannotUpdateQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEvent.BestAndFinalStarted
	 				and !SourcingEvent.StepTwoStarted
	 				and !SourcingEventResponse.HasCurrentModificationRequests)
	 					constraint (SourcingEvent.CanRespond)  
	 						"CannotUpdateResponseAfterEventCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotUpdateIfResponseHasBeenSubmitted"
 				if (SESupplierQuestionAnswer changed
 				or  Attachment changed)
 					constraint (!FromTechnicalProposal)
	 					"CannotChangeAnswerWhichWasPartOfAnApprovedTechnicalProposal"

			Exit Rules
				if (SourcingEventSupplierQuestion.YesNoOrList
				and	AnswerChanged)
					if (ConditionalQuestionsExist)
						include CreateConditionalSESupplierQuestion
					else
						LocalNoNewConditionalQuestions		= true
				if (CreatedFromThisQuestionRel exists)
					if (LocalNewConditionalCreated
					or  LocalNoNewConditionalQuestions)
						invoke DeleteConditionalQuestions SourcingEventSupplierQuestion
							invoked.ParmNotifiedSupplier	= NotifiedSupplier
							invoked.ParmCompany				= Company
							invoked.ParmSourcingEvent		= SourcingEvent 
							invoked.ParmOriginalQuestion	= SourcingEventSupplierQuestion 
							invoked.ParmLastOldQuestion		= LocalLastQuestion 

     	Delete is a Delete Action
			Entrance Rules

				if (SourcingEventSupplierQuestion.YesNoOrList)
					constraint (CreatedFromThisQuestionAnsweredRel !exists)
						"CannotDeleteAnswer;ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichHaveBeenAnswered"
					constraint (CreatedFromThisQuestionRel !exists)
						"ConditionalQuestionsHaveBeenCreatedFromThisAnswer;ResponseCannotBeDeleted"	

    		Action Rules
 				constraint (SourcingEventResponse.CanUpdateResponse)
 					"BuyerCannotDeleteQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotDeleteIfResponseHasBeenSubmitted"
 				constraint (!FromTechnicalProposal)
	 				"CannotDeleteAnswerWhichWasPartOfAnApprovedTechnicalProposal"
	 				
	 	UpdateFromAcceptTechnicalProposal is an Update Action
	 		restricted
	 		
	 	UpdateCMAttachment is an Instance Action
	 		
	 		Action Rules
	 			if (CanSetCMToNo)
	 				CMAttachment = false
	 			else
	 			if (CanSetCMToYes)
	 				CMAttachment = true
