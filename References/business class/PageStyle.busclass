PageStyle is a BusinessClass
    owned by repository
    prefix is PSFW

    Ontology
        symbolic key is PageStyle
    
	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing

    Persistent Fields
    	Description 			is Alpha size 60
			translatable
    	FavoriteIconImage		is a UserImage
    	Logo					is a UserImage
		BackgroundColor			is Alpha size 10
			default label is "Color"
    	BackgroundImage			is a UserImage
		TextColor				is Alpha size 10
			default label is "Color"
		Default					is Boolean
		Active					is Boolean
		ColumnLayout			is Numeric size 2
	        States
	        	Three			value is 3
	        	Four			value is 4


	Transient Fields
	    		
	Field Groups

    Derived Fields
		BackgroundColorDisplay is a DerivedField
			type is RichText
			return "<div style='width:100px; height:25px; background-color:" + BackgroundColor + "'></div>"

		TextColorDisplay is a DerivedField
			type is RichText
			return "<div style='width:100px; height:25px; background-color:" + TextColor + "'></div>"
			
	Local Fields
		    		
    Field Rules
    	Description
    		required

		ColumnLayout
			initial value is ColumnLayout.Four
			default to ColumnLayout.Four	    	





	Attach Rules
		constraint (Active)
			"PageStyleIsInactive"

	Conditions
	
		BackgroundColorEntered
			when (BackgroundColor entered)
	
		TextColorEntered
			when (TextColor entered)

	Relations
		DefaultPageStyleRel
			one-to-many relation to PageStyle
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Default)
								
    Actions
    	Create is a Create Action
    	
    	Update is an Update Action

    	Delete is a Delete Action
    	
		RemoveDefault is an Instance Action
			restricted
			Action Rules
				Default = false
				
		MakeDefault is an Instance Action
			valid when(!Default)

			Entrance Rules
				invoke RemoveDefault DefaultPageStyleRel
				
			Action Rules
				Default = true
