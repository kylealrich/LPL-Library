PfiServiceDefinition is a BusinessClass
    owned by pfi
    prefix is PfiSD
    default label is "ServiceDefinition"
    framework type is ProcessFlow
    disable data area copy
		preserve target data

    Ontology
        symbolic key is PfiServiceDefinition

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements CompoundDocument
            Document Components
                PfiServiceVariable set
                PfiServiceFlowDefinition set
        implements DynamicCreation
        implements AuditLogEntryActions

    Persistent Fields

        Description               is a PfiDescription
        CriteriaNames             is a PfiCriteriaNames
        IsServiceEnabled          is Boolean
            default label is "ServiceIsEnabled"
        LastServiceFlowDefinition is Numeric size 10
            default label is "LastServiceProcessDefinition"

    Derived Fields

        ShortDesc is a StringField
            type is Alpha size 50
            default label is "ShortDescription"
            Description

        CanBeInitiated is a DerivedField
            type is Boolean
            default label is "ServiceCanBeInitiated"
            if (IsServiceEnabled = false)
                return false
            for each PfiServiceFlowDefinition set
                if (each.IsServiceFlowEnabled = true)
                    return true
            return false

        DefaultCriteriaName1 is a LabelField
            "CriteriaName1"

        DefaultCriteriaName2 is a LabelField
            "CriteriaName2"

        DefaultCriteriaName3 is a LabelField
            "CriteriaName3"

        DerivedCriteriaName1 is a DerivedField
            type is Alpha 40
            if (CriteriaNames.CriteriaName[1] = blank)
                return DefaultCriteriaName1
            else
                return CriteriaNames.CriteriaName[1]

        DerivedCriteriaName2 is a DerivedField
            type is Alpha 40
            if (CriteriaNames.CriteriaName[2] = blank)
                return DefaultCriteriaName2
            else
                return CriteriaNames.CriteriaName[2]

        DerivedCriteriaName3 is a DerivedField
            type is Alpha 40
            if (CriteriaNames.CriteriaName[3] = blank)
                return DefaultCriteriaName3
            else
                return CriteriaNames.CriteriaName[3]

        IsSystemDefined is a DerivedField
            type is Boolean
            for each PfiServiceFlowDefinition set
                if (each.FlowDefinition.FlowType = each.FlowDefinition.FlowType.SystemDefined)
                    return true
            return false

    Sets

        ByDescription
            sql name is PfiSD01
            not indexed
            Sort Order
                Description
                PfiServiceDefinition

        ByCriteriaNames1
            sql name is PfiSD02
            not indexed
            Sort Order
                CriteriaNames.CriteriaName[1]
                PfiServiceDefinition

        ByCriteriaNames2
            sql name is PfiSD03
            not indexed
            Sort Order
                CriteriaNames.CriteriaName[2]
                PfiServiceDefinition

        ByCriteriaNames3
            sql name is PfiSD04
            not indexed
            Sort Order
                CriteriaNames.CriteriaName[3]
                PfiServiceDefinition

        ByServiceDefinitionEnabled
            sql name is PfiSD05
            indexed
            Sort Order
                PfiServiceDefinition
            Instance Selection
                where (IsServiceEnabled)

    Field Rules

        IsServiceEnabled
            default to false
            initial value is false

    Actions

        Create is a Create Action

        Update is an Update Action

            Action Rules
                if (not PfiServiceFlowDefinition set.exists)
                    IsServiceEnabled = false

        Delete is a Delete Action
