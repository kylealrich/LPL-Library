ReceiptInterfaceInput is a BusinessClass
    owned by ma
    prefix is W8261

    Ontology
    	symbolic key is ReceiptInterfaceInput

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
    	RunGroup
        RecordType                 is AlphaUpper size 1
        	States
        		ReceiptHeader		value is "1"
        		AddOnCharge			value is "2"
        		ReceiptLine			value is "3"
        		ReceiptLineDetail	value is "4"
        Company                 is a PurchasingCompany
        ReferenceNumber         is like ReferenceNumber 
        LineNumber              is like LineNumber 
        PurchaseOrder           is like PurchaseOrder    
        InterfacedPurchaseOrder is like PurchaseOrderImport
        PoRelease               is like PORelease 
        PoCode                  is like POCode 
        Item                    is like Item
        Description             is like Description 
        ItemType                is like ItemType 
        Location                is like InventoryLocation
        ProcessLevel            is like ProcessLevel 
        OperatorID              is like Operator 
        	holds pii
        TransactionDate         is Date 
        EnteredReceivedQuantity is like Quantity 
        ReceivedUOM             is like UnitOfMeasure
        ReceivedUOMMultiplier   is like UOMMultiplier 
        CatchWeightQuantity     is like Quantity 
        OriginalUnitCost        is like InternationalCost
        CancelBackorder         is Boolean 
        Bin                     is like Bin 
        Status                  is Numeric size 1
        UpdateDate              is TimeStamp 
        Vendor                  is like Vendor
        InterfacedVendor        is a OldVendor
        VendorPriceUOMQuantity  is like Quantity 
        VendorPriceUOM          is like UnitOfMeasure 
        VendorPriceUOMMultiplier is like UOMMultiplier 
        BuyUOMQuantity          is like Quantity 
        VendorBuyUOM            is like UnitOfMeasure 
        VendorBuyUOMMultiplier  is like UOMMultiplier 
        TaxCode                 is like TaxCode 
        TaxUsageCode            is like TaxUsageCode 
        Buyer                   is like Buyer 
        BillOfLading            is like BillOfLading 
        MerchandiseCarrier      is like Vendor
        MatchReferenceNumber	is like MatchReferenceNumber
        Currency                is like Currency 

        Serial                  is like ItemSerialNumber 
        Lot                     is like ItemLot 
        Sublot                  is like Sublot 
        LotExpirationDate       is Date 
        DetailQuantity          is like Quantity 
        HoldFlag                is Boolean 
        AddOnCharge             is like AddOnCharge 
        TotalAddOnChargeAmount  is like InternationalAmount
        ReceivedDate            is Date 
        ErsComments             is Alpha size up to 200    
        ERSStatus               is Numeric size 1
        Invoice                 is like Invoice 
        InvoiceReference        is Alpha size 2
        PrintCode               is like PrintCode
        VendorItem              is like VendorItem 
        TermCode                is like TermsCode 
        SeqNbr                  is like SeqNbr 
        AcctUnit                is like AccountingUnit
        Account                 is like GeneralLedgerChartAccount 
        SubAcct                 is like SubAcct 
        MSDSRequired 			is Boolean 
        MSDSVersion             is like MSDSVersion 
        MSDSDocument            is like MSDSDocument 
        ReceiptComments         is like ErsComments 
        ShipToArriveDate        is Date 
        ReceiptCurrencyConversionRate is like CnvRate
        ItemGTIN                is like ItemGTIN

        CompanyGln              is like GlobalLocationNumber 
        LocationGln             is like GlobalLocationNumber 
        VendorGln               is like GlobalLocationNumber 
        BuyerGln                is like GlobalLocationNumber 
        BinGln                  is like GlobalLocationNumber 
        GlobalDocumentType      is like GlobalDocumentType 
        GlobalLineType          is like GlobalLineType 

		UseCompany					is like Company
		UseVendor					is like Vendor

        NewPurchaseOrderReceipt	is a PurchaseOrderReceipt
        Interfaced			     is Boolean
        HSNSACCode

	Transient Fields
		UseLocation 					is like InventoryLocation
		UseBuyer						is like Buyer
		UseBin							is like Bin
	Local Fields
		LocalPurchaseOrder				is a PurchaseOrder
    	InterfacedReceipt				is a PurchaseOrderReceipt view
		LocalInterfaceResult 			is a PurchaseOrderReceiptInterfaceResult

		LocalRunGroup					is like RunGroup
		LocalCompany					is like Company
		LocalVendor						is like Vendor
				
		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150


		



		LocalPurchaseOrderLine			is like PurchaseOrderLine
		LocalMatchDetailKey				is like MatchDetailKey

		NextSentence					is Boolean
		Counter							is Numeric 5

        LocalSequence                  	is like SeqNbr
		LastRecordType					is AlphaUpper size 1
		
		LocalGlobalLocationNumberType   is like GlobalLocationNumberType
		LocalGlobalLocationNumber		is like GlobalLocationNumber
		
		POLineLookup
		LocalTrigger					is Numeric 1
		LocalFinanceEnterpriseGroup		is like FinanceEnterpriseGroup	
        LocalBin                     	is like Bin 
        LocalSerial                  	is like ItemSerialNumber 
        LocalLot                     	is like ItemLot 

	Derived Fields
		NoReceiptLinesMsg is a MessageField
			restricted
			"NoLinesExistForReceipt"

		UnreleasedReceiptMsg is a MessageField
			restricted
			"UnreleasedReceiptExistsForPurchaseOrder<PurchaseOrder>"
		
		UnreleasedReceiptLineMsg is a MessageField
			restricted
			"UnreleasedReceiptExistsForPO<PurchaseOrder>AndPOLine<LocalPurchaseOrderLine>"

		CannotDeterminePurchaseOrderLineMsg is a MessageField
			restricted
			"CannotDeterminePurchaseOrderLine;MultipleLinesExistForValuesEntered"

		CannotFindMatchingPurchaseOrderLineMsg is a MessageField
			restricted
			"CannotFindMatchingPurchaseOrderLine"

		CannotDetermineCompanyUsingGLNMsg is a MessageField
			restricted
			"CannotDetermineCompanyUsingGlobalLocationNumber"
		
		CannotDetermineVendorUsingGLNMsg is a MessageField
			restricted
			"CannotDetermineVendorUsingGlobalLocationNumber"
		
		CannotDetermineLocationUsingGLNMsg is a MessageField
			restricted
			"CannotDetermineLocationUsingGlobalLocationNumber"
		
		CannotDetermineBuyerUsingGLNMsg is a MessageField
			restricted
			"CannotDetermineBuyerUsingGlobalLocationNumber"
		
		CannotDetermineBinUsingGLNMsg is a MessageField
			restricted
			"CannotDetermineBinUsingGlobalLocationNumber"

		LineMissingDataMsg is a MessageField
			restricted
			"Item,VendorItem,Description,OrLineNumberRequiredForDetailRecord"

		AddOnChargeRequiredMsg is a MessageField
			restricted
			"AddOnChargeCodeRequired"

		ReceiptLinesWithDifferentLocationsMsg is a MessageField
			restricted
			"CannotHaveMultipleLocationsOnReceiverLinesForPoolOptionCompanyVendorPurchaseOrderShipToLocation"
			
		CancelRemainingMustBeYesIfQuantityIsZeroMsg is a MessageField
			restricted
			"CancelRemainingMustBeYesIfQuantityIsZero"
		
		CannotEnterBinMessage is a MessageField
			restricted
			"CannotEnterBinIfNotBinTracked"
			
		CannotEnterLotMessage is a MessageField
			restricted
			"CannotEnterLotIfNotLotTracked"
		
		CannotEnterSerialMessage is a MessageField
			restricted
			"CannotEnterSerialIfNotSerialTracked"

		DerivedVendor is a DerivedField
			type is like Vendor
			if  (Vendor not entered
			and  InterfacedVendor entered)
				if (InterfaceMappingVendorRel exists)
					return first InterfaceMappingVendorRel.Vendor
				else
				if  (VendorForLegacyVendorRel exists)
					return first VendorForLegacyVendorRel.Vendor 
			else
				return Vendor

		DerivedVendorPoolOption is a DerivedField
			type is like PoolOption
			LocalVendor = DerivedVendor
			if (!LocalVendorRel.PoolOption.UseCompany)
				return LocalVendorRel.PoolOption
			return MatchCompanyRel.PoolOption
			
		LineCountForAReceipt is a DerivedField
			type is Numeric 10 
			return instance count of LinesForHeaderRel 
		
		AOCCountForAReceipt is a DerivedField
			type is Numeric 10
			return instance count of LineAddOnCostsRel

	Conditions

		IsPartiallyImported
			when (NewPurchaseOrderReceipt entered)

		SkipThisRecord
	        when  (ReceiptHeaderRel.DuplicateHeaderRel exists
			or     PurchaseOrderReceiptImportRel exists
			or     ReferenceNumber not entered)		

		HasDuplicateReceipt
			when (ReceiptHeaderRel.DuplicateHeaderRel exists)

		DuplicateLineExists
			when (RecordType.ReceiptLine
			and   DuplicateLineRel exists)

		DuplicateAOCExists
			when (RecordType.AddOnCharge
			and   DuplicateAOCRel exists)

		HasDuplicateReceiptImport
			when (PurchaseOrderReceiptImportRel exists)

		HasUnreleasedReceiptLine
			when (UnreleasedReceiptLineRel exists)

		ReferenceNotEntered
			when (ReferenceNumber not entered)
        	
        IsHSNSACCodeEnabled
			restricted
			when (Company.GeneralLedgerCompany.RequireHSNSACCode)

        IsCatchWeightItem
        	restricted
        	when (ItemRel.CatchWeightCode.AllTransactionsRequireBoth or ItemRel.CatchWeightCode.ReceiptRequiresBoth) 
			
    Relations

		ItemRel
            one-to-one relation to Item
            Field Mapping uses symbolic key
                related.ItemGroup 	= Company.InventoryCompany.ItemGroup
                related.Item		= Item

		PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
				related.Company								= Company
			
		VendorForLegacyVendorRel
            one-to-many relation to Vendor
            Field Mapping uses ByLegacyVendor
				related.VendorGroup							= PayablesCompanyRel.VendorGroup
				related.LegacyVendor						= InterfacedVendor
			
		InterfaceMappingVendorRel
            one-to-many relation to VendorMappingTableInterface
            Field Mapping uses symbolic key
				related.VendorGroup							= PayablesCompanyRel.VendorGroup
			Instance Selection
				where (related.VendorMappingTableInterface.OldVendor	= InterfacedVendor)
			
		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
                related.Company                    			= Company
                related.PurchaseOrderImport 				= InterfacedPurchaseOrder
                related.POCode								= PoCode
			
		LocalInterfaceResultsRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult	= LocalInterfaceResult
				
        LinesForHeaderRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptLine 

		LocalInterfaceResultRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult	= LocalInterfaceResult

        LocalReceiptsInRunGroupRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= LocalRunGroup

        LocalReceiptsInRunGroupCompanyRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= LocalRunGroup
                related.Company                     		= LocalCompany



		ReceiptLinesWithDifferentLocationsRel
			one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptLine 
			Instance Selection
				where (related.Location != Location)

		LocalVendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup							= PayablesCompanyRel.VendorGroup
				related.Vendor								= LocalVendor

		MatchCompanyRel
            one-to-one relation to MatchCompany
            Field Mapping uses symbolic key
				related.Company								= Company

        ReceiptHeaderRel
            one-to-one relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptHeader
                related.LineNumber							= blank
                related.SeqNbr								= blank
                related.AddOnCharge							= blank

        LineAddOnCostsRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.AddOnCharge
                related.LineNumber							= LineNumber
                related.SeqNbr								= SeqNbr

        LineDetailRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptLineDetail
                related.LineNumber							= LineNumber

        ItemLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           					= Company
                related.InventoryLocation 					= Location
                related.Item              					= Item

        PurchaseOrderLineLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses Set11
				related.Company								= Company
				related.PurchaseOrder						= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
				related.MatchDetailKey						= LocalMatchDetailKey
			Instance Selection
				where (related.Closed.No
				and   (Location not entered
				or     related.ShipToLocation				= Location))
        
        ItemAndVendorItemLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrder						= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
			Instance Selection
				where (related.Item							= Item
    			and    related.VendorItem					= VendorItem)
            
        ItemOnlyLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrder						= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
			Instance Selection
				where (related.Item							= Item)

        VendorItemOnlyLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrder						= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
			Instance Selection
				where (related.VendorItem					= VendorItem)

        LineNumberLookupRel
			one-to-one relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrder						= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
				related.PurchaseOrderLine					= LineNumber
				
		PurchaseOrderReceiptLineRel
			one-to-one relation to PurchaseOrderReceiptLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrderReceipt				= ReceiptHeaderRel.NewPurchaseOrderReceipt
				related.PurchaseOrderReceiptLine			= LocalPurchaseOrderLine

		UnreleasedReceiptRel
			one-to-many relation to PurchaseOrderReceipt
			Field Mapping uses Set3
				related.Company								= Company
				related.PurchaseOrder						= PurchaseOrder
			Instance Selection
				where (related.Status.Unreleased)
				
		UnreleasedReceiptLineRel
			one-to-many relation to PurchaseOrderReceiptLine
			Field Mapping uses ByPurchaseOrderLine
				related.Company								= Company
				related.PurchaseOrder						= PurchaseOrder
				related.PurchaseOrderLine					= LineNumber
			Instance Selection
				where (related.Status.Entered
				and    not related.PurchaseOrderReceipt.IsDropShip) 


        PurchaseOrderReceiptLineForAOCRel
            one-to-one relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptLine
                related.LineNumber							= LineNumber
                related.SeqNbr								= SeqNbr
                related.AddOnCharge							= blank 

        OtherAddOnChargesForSameLineRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.AddOnCharge
                related.LineNumber							= LineNumber
                related.SeqNbr								= SeqNbr
            Instance Selection
                where (related.AddOnCharge					!= AddOnCharge)

        DuplicateHeaderRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptHeader 
            Instance Selection
            	where (related.ReceiptInterfaceInput != ReceiptInterfaceInput)

        DuplicateLineRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.ReceiptLine 
                related.LineNumber							= LineNumber
                related.SeqNbr								= SeqNbr
            Instance Selection
            	where (related.ReceiptInterfaceInput != ReceiptInterfaceInput
            	and    related.Location						= Location)

        DuplicateAOCRel
            one-to-many relation to ReceiptInterfaceInput
            Field Mapping uses ByRunGroup
            	related.RunGroup							= RunGroup
                related.Company                     		= Company
                related.ReferenceNumber						= ReferenceNumber
                related.RecordType							= RecordType.AddOnCharge 
                related.LineNumber							= LineNumber
                related.SeqNbr								= SeqNbr
                related.AddOnCharge							= AddOnCharge 
            Instance Selection
            	where (related.ReceiptInterfaceInput != ReceiptInterfaceInput
            	and    related.Location						= Location)

        PurchaseOrderReceiptImportRel
            one-to-one relation to PurchaseOrderReceiptImport
            Field Mapping uses symbolic key
                related.Company                    								= Company
                related.PurchaseOrderReceiptImport 								= ReferenceNumber

		LocalPurchasingCompanyRel
			one-to-one relation to PurchasingCompany
			Field Mapping uses symbolic key
				related.Company                    		= LocalCompany
				
        GlobalLocationNumberDetailSet6Rel
            one-to-one relation to GlobalLocationNumberDetail
            Field Mapping uses Set6
                related.Company                                           	= LocalCompany

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company 											= UseCompany

        GlobalLocationNumberDetailSet2Rel
            one-to-many relation to GlobalLocationNumberDetail
            Field Mapping uses Set2
                related.ItemGroup                                           = LocalPurchasingCompanyRel.ProcurementGroup
                related.GlobalLocationNumberDetail.GlobalLocationNumberType = 2
            Instance Selection
                where (related.CompanyLocationBin.Company                   = LocalCompany)

        GlobalLocationNumberDetailSet4Rel
            one-to-many relation to GlobalLocationNumberDetail
            Field Mapping uses Set4
                related.ItemGroup                                           = LocalPurchasingCompanyRel.ProcurementGroup
                related.GlobalLocationNumberDetail.GlobalLocationNumberType = 7
                related.VendorAndVendorLocation.Vendor						= LocalVendor

        GlobalLocationNumberDetailSet7Rel
            one-to-many relation to GlobalLocationNumberDetail
            Field Mapping uses Set7
                related.GlobalLocationNumberDetail.GlobalLocationNumberType = LocalGlobalLocationNumberType
                related.GlobalLocationNumber								= LocalGlobalLocationNumber

        GlobalLocationNumberDetailSet1Rel
            one-to-many relation to GlobalLocationNumberDetail
            Field Mapping uses symbolic key
            	related.ItemGroup											= InventoryCompanyRel.ItemGroup
                related.GlobalLocationNumber								= LocalGlobalLocationNumber
            Instance Selection
                where (related.GlobalLocationNumberDetail.GlobalLocationNumberType = LocalGlobalLocationNumberType)

		ItemGTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup  											= LocalCompany
				related.ItemGTIN 											= ItemGTIN
		
        GLNPurchaseOrderReceiptRel
            one-to-one relation to PurchaseOrderReceipt
            Field Mapping uses symbolic key
                related.Company							= UseCompany
                related.PurchaseOrderReceipt			= NewPurchaseOrderReceipt

		SamePurchaseOrderTransactionDetailsRel
			one-to-many relation to PurchaseOrderTransactionDetail
	        Field Mapping uses ByLine
	        	related.Company															= Company
      			related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseTransactionDocumentType.Receiving
      			related.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= ReceiptHeaderRel.NewPurchaseOrderReceipt
      			related.PurchaseOrderTransactionDetail.LineNumber						= LocalPurchaseOrderLine
      		Instance Selection
      			where (related.Bin		= LocalBin
      			and    related.Lot		= LocalLot
      			and	   related.Serial	= LocalSerial)	

    Sets

        ByRunGroup
            Sort Order
            	RunGroup
            	Company
            	ReferenceNumber
            	RecordType
            	LineNumber
            	SeqNbr
            	AddOnCharge

        ByRunGroupUniqueID
            Sort Order
            	RunGroup
            	Company
            	ReferenceNumber
            	RecordType
            	LineNumber
            	SeqNbr
            	AddOnCharge
            	Location
            	ReceiptInterfaceInput

	Rule Blocks
		GetPurchaseOrderLine

			if  (Item entered
			and  VendorItem entered
			and  instance count of ItemAndVendorItemLookupRel = 1)
				LocalPurchaseOrderLine = first ItemAndVendorItemLookupRel.PurchaseOrderLine
				if  (ItemType not entered)
					ItemType = first ItemAndVendorItemLookupRel.ItemType
			else
			if  (Item not entered
			and  VendorItem entered
			and  instance count of VendorItemOnlyLookupRel = 1)
				LocalPurchaseOrderLine  = first VendorItemOnlyLookupRel.PurchaseOrderLine
				Item 					= first VendorItemOnlyLookupRel.Item
				if  (ItemType not entered)
					ItemType = first VendorItemOnlyLookupRel.ItemType
			else
			if  (Item entered
			and  VendorItem not entered
			and  instance count of ItemOnlyLookupRel = 1)
				LocalPurchaseOrderLine = first ItemOnlyLookupRel.PurchaseOrderLine
				if  (ItemType not entered)
					ItemType = first ItemOnlyLookupRel.ItemType
			else
				initialize LocalMatchDetailKey
				initialize LocalPurchaseOrderLine
	
				if  (ItemType.Inventoried 
				or   ItemType.NonStock)
					LocalMatchDetailKey 	= Item
				else
					LocalMatchDetailKey 	= VendorItem
					if  (LocalMatchDetailKey not entered)
						LocalMatchDetailKey = Description
	
					if  (LocalMatchDetailKey not entered)
						LocalMatchDetailKey = Item
	
				if  (LocalMatchDetailKey entered)
					for each PurchaseOrderLineLookupRel
						if  (!ItemType.Special
						or   ReceivedUOM not entered
						or   each.EnteredBuyUOM		= ReceivedUOM)
	
							if (LocalPurchaseOrderLine entered)
								ErrorOccurred		= true
								LocalErrorMessage	= CannotDeterminePurchaseOrderLineMsg
	
							LocalPurchaseOrderLine = each.PurchaseOrderLine
								
				if (LocalPurchaseOrderLine not entered)
					ErrorOccurred		= true
					LocalErrorMessage	= CannotFindMatchingPurchaseOrderLineMsg

		GLNProcessing

			LocalCompany							= PrmCompany
			UseCompany								= Company
			UseVendor								= Vendor
			initialize UseLocation
			initialize UseBuyer
			
			if  (UseCompany not entered)	
				if  (CompanyGln = SaveGlobalLocationNumber1)
					UseCompany						= SaveCompany1
				else
				if  (CompanyGln = SaveGlobalLocationNumber2)
					UseCompany						= SaveCompany2

			if  (UseCompany not entered)	
				LocalGlobalLocationNumberType 		= 1
				LocalGlobalLocationNumber			= CompanyGln
				UseCompany							= first GlobalLocationNumberDetailSet7Rel.Company

			if  (UseCompany not entered)	
				LocalGlobalLocationNumberType 		= 2
				UseCompany							= first GlobalLocationNumberDetailSet7Rel.Company
				
			if  (UseCompany not entered)	
				ErrorOccurred						= true
				LocalErrorMessage					= CannotDetermineCompanyUsingGLNMsg

			if  (!ErrorOccurred)
				Counter = 1
				if  (UseVendor not entered)					
					LocalGlobalLocationNumberType 		= 7
					LocalGlobalLocationNumber			= VendorGln
					initialize Counter
					for each GlobalLocationNumberDetailSet1Rel
						Counter += 1
						UseVendor						= first GlobalLocationNumberDetailSet1Rel.VendorAndVendorLocation.Vendor
							
					if  (UseVendor not entered	
					or   Counter != 1)
						ErrorOccurred					= true
						LocalErrorMessage				= CannotDetermineVendorUsingGLNMsg

			if  (!ErrorOccurred)
				if  (LocationGln entered)
					LocalGlobalLocationNumberType 		= 3
					LocalGlobalLocationNumber			= LocationGln
					initialize Counter
					for each GlobalLocationNumberDetailSet1Rel
						Counter += 1
						UseLocation						= first GlobalLocationNumberDetailSet1Rel.RequestingCompanyAndLocation.RequestingLocation

					if  (UseLocation not entered	
					or   Counter != 1)
						ErrorOccurred					= true
						LocalErrorMessage				= CannotDetermineLocationUsingGLNMsg
	
			if  (!ErrorOccurred)
				if  (BuyerGln entered)
					LocalGlobalLocationNumberType 		= 12
					LocalGlobalLocationNumber			= BuyerGln
					initialize Counter
					for each GlobalLocationNumberDetailSet1Rel
						Counter += 1
						UseBuyer						= first GlobalLocationNumberDetailSet1Rel.PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation

					if  (UseBuyer not entered	
					or   Counter != 1)
						ErrorOccurred					= true
						LocalErrorMessage				= CannotDetermineBuyerUsingGLNMsg
	
	
	Field Rules
		ReferenceNumber
			required
				"ReferenceNumberIsRequired"

		PurchaseOrder
			if  (InterfacedPurchaseOrder not entered)
				required
					"PurchaseOrderOrInterfacedPurchaseOrderRequired"
					
		InterfacedPurchaseOrder
			if  (PurchaseOrder not entered)
				required
					"PurchaseOrderOrInterfacedPurchaseOrderRequired"
				
		RunGroup
			required
			
		OperatorID
			default to actor

    Actions

        Create is a Create Action


		Update is an Update Action
        		
        Delete is a Delete Action
			Action Rules
				if (RecordType.ReceiptHeader
				and NewPurchaseOrderReceipt entered)
				    confirmation required
				    	"ThisRecordHasBeenPartiallyInterfaced.ItIsRecommendedToUseMoveToMaintenanceAction.DeletingThisRecordWillNotDeleteRelatedRecords.ContinueAnyway?"
				
        FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
			Entrance Rules

			
        
		DeleteAllTransactionsForRunGroup is a Set Action
			confirmation required
			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup 
				PrmRunGroup				  is AlphaUpper 30
					default label is "RunGroup"
			Instance Selection
				where (RunGroup	= PrmRunGroup
                and   (Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
                or     Company not entered))
				
			Parameter Rules 
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					
			Action Rules
				Instance Rules
					invoke FastDelete 

		MoveToMaintenance is an Instance Action
		    valid when (RecordType.ReceiptHeader)
		    Parameters
		        PrmRunGroup is like RunGroup
		    Parameter Rules
		        PrmRunGroup
		            initial value is RunGroup
		            required
		    Action Rules
		
		        constraint (RecordType.ReceiptHeader)
		            "ActionValidOnHeaderRecordOnly"
		
		        invoke CreateNoRules PurchaseOrderReceiptImport
		            fill in fields from ReceiptHeaderRel
		                except invoked.RunGroup
		            invoked.RunGroup                    = PrmRunGroup
		            invoked.PurchaseOrderReceiptImport  = ReferenceNumber
		                

		        for each LinesForHeaderRel

		            invoke CreateNoRules PurchaseOrderReceiptLineImport
		                fill in fields from each
		                    except invoked.RunGroup
		                invoked.RunGroup                                    = PrmRunGroup
		                invoked.PurchaseOrderReceiptImport                  = each.ReferenceNumber
		                invoked.PurchaseOrderReceiptLineImport.LineNumber    = each.LineNumber
		                if (each.LineNumber not entered)
		                    invoked.PurchaseOrderReceiptLineImport.LineNumber    = 1
		                invoked.PurchaseOrderReceiptLineImport.SeqNbr        = each.SeqNbr
		

		            for each each.LineAddOnCostsRel

		                invoke CreateNoRules PurchaseOrderReceiptLineAOCImport
		                    fill in fields from each
		                        except invoked.RunGroup
		                        except invoked.RecordType
		                    invoked.RunGroup                                        = PrmRunGroup
		                    invoked.PurchaseOrderReceiptImport                      = each.ReferenceNumber
		                    invoked.PurchaseOrderReceiptLineAOCImport.LineNumber    = each.LineNumber
		                    if (each.LineNumber not entered)
		                        invoked.PurchaseOrderReceiptLineAOCImport.LineNumber    = 1
		                    invoked.PurchaseOrderReceiptLineAOCImport.AddOnCharge     = each.AddOnCharge
		                    invoked.PurchaseOrderReceiptLineAOCImport.SeqNbr        = each.SeqNbr
		                    invoked.EnteredUnitCost                                    = each.OriginalUnitCost
		                    invoked.Quantity                                        = each.EnteredReceivedQuantity
		

		                invoke FastDelete each
		

		            for each each.LineDetailRel

		                invoke CreateNoRules POReceiptInventoryDetailImport
		                    fill in fields from each
		                        except invoked.RunGroup
		                    invoked.RunGroup                                    = PrmRunGroup
		                    invoked.PurchaseOrderReceiptImport                  = each.ReferenceNumber
		                    invoked.POReceiptInventoryDetailImport.LineNumber    = each.LineNumber
		                    if (each.LineNumber not entered)
		                        invoked.POReceiptInventoryDetailImport.LineNumber    = 1
		                    invoked.POReceiptInventoryDetailImport.Sequence        = each.SeqNbr
		                    if (IsCatchWeightItem)
		                    	invoked.Quantity								= each.CatchWeightQuantity
		                    	invoked.SecondaryQuantity						= each.DetailQuantity
		                    else
		                    	invoked.Quantity								= each.DetailQuantity
		

		                invoke FastDelete each
		

		            invoke FastDelete each  
		            
		        invoke FastDelete


		CreateErrorRecord is an Instance Action
 			restricted
 			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup	
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like PurchaseOrderReceiptInterfaceResult
 				PrmErrorLevel           is Numeric 2
				EnableGLNProcessing		is Boolean
 					
			Action Rules


		
				LocalInterfaceResult   		= PrmResult
				LocalCompany           		= Company
				LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				initialize LocalSequence

				invoke Update LocalInterfaceResultsRel
					invoked.Status = 2 

				if  (LocalInterfaceResultsRel.MoveErrorsToNewRunGroup)
				
					if  (LocalInterfaceResultsRel.ErrorRunGroup not entered)
						LocalRunGroup					= LocalInterfaceResultsRel.ErrorRunGroupPrefix + "ERRORS_" + RunGroup

						invoke Update LocalInterfaceResultsRel
							invoked.ErrorRunGroup		= LocalRunGroup
					else
						LocalRunGroup					= LocalInterfaceResultsRel.ErrorRunGroup
				else
					LocalRunGroup						= LocalInterfaceResultsRel.RunGroup

				if  (EnableGLNProcessing)
					LocalCompany						= UseCompany
					LocalVendor							= UseVendor


				invoke CreateNoRules PurchaseOrderReceiptImport
					fill in fields from ReceiptHeaderRel
						except invoked.RunGroup
					invoked.RunGroup					= LocalRunGroup
					invoked.PurchaseOrderReceiptImport  = ReferenceNumber
					invoked.RecordInError				= "Y"
					invoked.ErrorMessage				= PrmErrorMessage
					invoked.ErrorLevel					= PrmErrorLevel 
					invoked.InterfaceRun	            = PrmResult
					if  (EnableGLNProcessing)
						invoked.Company					= LocalCompany
						invoked.Vendor					= LocalVendor
						invoked.Location				= UseLocation
						invoked.Buyer					= UseBuyer
						

				for each LinesForHeaderRel

					LocalSequence += 1
					invoke CreateNoRules PurchaseOrderReceiptLineImport
						fill in fields from each
							except invoked.RunGroup
						invoked.RunGroup									= LocalRunGroup
						invoked.PurchaseOrderReceiptImport  				= each.ReferenceNumber
						invoked.PurchaseOrderReceiptLineImport.LineNumber	= each.LineNumber
						if (each.LineNumber not entered)
							invoked.PurchaseOrderReceiptLineImport.LineNumber	= 1
						invoked.PurchaseOrderReceiptLineImport.SeqNbr		= each.SeqNbr
						if  (EnableGLNProcessing)
							invoked.Company									= LocalCompany
						if  (PrmErrorLevel 		= 2
						and  each.LineNumber 	= this instance.LineNumber
						and  each.SeqNbr 		= this instance.SeqNbr)
							invoked.RecordInError							= true


					for each each.LineAddOnCostsRel

						invoke CreateNoRules PurchaseOrderReceiptLineAOCImport
							fill in fields from each
								except invoked.RunGroup
								except invoked.RecordType
							invoked.RunGroup										= LocalRunGroup
							invoked.PurchaseOrderReceiptImport  					= each.ReferenceNumber
							invoked.PurchaseOrderReceiptLineAOCImport.LineNumber	= each.LineNumber
							if (each.LineNumber not entered)
								invoked.PurchaseOrderReceiptLineAOCImport.LineNumber	= 1
							invoked.PurchaseOrderReceiptLineAOCImport.AddOnCharge 	= each.AddOnCharge
							invoked.PurchaseOrderReceiptLineAOCImport.SeqNbr		= each.SeqNbr
							invoked.EnteredUnitCost									= each.OriginalUnitCost
							invoked.Quantity										= each.EnteredReceivedQuantity

							if  (EnableGLNProcessing)
								invoked.Company										= LocalCompany
								invoked.Vendor										= LocalVendor

							if  (PrmErrorLevel 		= 3
							and  each.LineNumber 	= this instance.LineNumber
							and  each.SeqNbr 		= this instance.SeqNbr
							and  each.AddOnCharge 	= this instance.AddOnCharge)
								invoked.RecordInError							= true


						invoke FastDelete each


					for each each.LineDetailRel

						invoke CreateNoRules POReceiptInventoryDetailImport
							fill in fields from each
								except invoked.RunGroup
							invoked.RunGroup									= LocalRunGroup
							invoked.PurchaseOrderReceiptImport  				= each.ReferenceNumber
							invoked.POReceiptInventoryDetailImport.LineNumber	= each.LineNumber
		                    if (IsCatchWeightItem)
		                    	invoked.Quantity								= each.CatchWeightQuantity
		                    	invoked.SecondaryQuantity						= each.DetailQuantity
		                    else
		                    	invoked.Quantity								= each.DetailQuantity
							if (each.LineNumber not entered)
								invoked.POReceiptInventoryDetailImport.LineNumber	= 1
							invoked.POReceiptInventoryDetailImport.Sequence		= each.SeqNbr
							if  (EnableGLNProcessing)
								invoked.Company									= LocalCompany


						invoke FastDelete each


					invoke FastDelete each  


				invoke FastDelete ReceiptHeaderRel 

					
		InterfaceReceipts is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup   is an FinanceEnterpriseGroup
					default label is "PrmFinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				PrmLocation                 is an InventoryLocation 
 				ReleaseReceipts				is Boolean
				ReleaseOption
				AllowDropshipReceivers		is Boolean
				AllowMatchItemOrVendorItem	is Boolean
					default label is "AllowMatchOnEitherItemOrVendorItem"
				EnableGTINProcessing        is Boolean 
				EnableGLNProcessing         is Boolean 
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				RemoveExistingReceiptsInReceiptMaintenance is Boolean
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"

					if  (PrmCompany entered)
						for each LocalReceiptsInRunGroupCompanyRel
							constraint (each.RecordType.ReceiptHeader or each.RecordType.ReceiptLine or each.RecordType.ReceiptLineDetail or each.RecordType.AddOnCharge)
								"InvalidRecordType<RecordType>(SeeReferenceNumber<each.ReferenceNumber>)"
							if  (each.RecordType.ReceiptHeader)
								constraint (!each.RecordType = LastRecordType)
									"CannotHaveConsecutiveHeaderRecords(SeeReferenceNumber<each.ReferenceNumber>)"
							LastRecordType = RecordType
					else
						for each LocalReceiptsInRunGroupRel
							constraint (each.RecordType.ReceiptHeader or each.RecordType.ReceiptLine or each.RecordType.ReceiptLineDetail or each.RecordType.AddOnCharge)
								"InvalidRecordType<RecordType>(SeeReferenceNumber<each.ReferenceNumber>)"
							if  (each.RecordType.ReceiptHeader)
								constraint (!each.RecordType = LastRecordType)
									"CannotHaveConsecutiveHeaderRecords(SeeReferenceNumber<each.ReferenceNumber>)"
							LastRecordType = RecordType


				ReleaseReceipts
 					initial value is true
					
				EnableGLNProcessing
					if (EnableGLNProcessing)


						
						LocalCompany = PrmCompany
						
						initialize Counter

						if (PrmCompany entered)
							if  (GlobalLocationNumberDetailSet6Rel exists)
								SaveGlobalLocationNumber1	= GlobalLocationNumberDetailSet6Rel.GlobalLocationNumber
								SaveCompany1				= GlobalLocationNumberDetailSet6Rel.Company
								
							initialize Counter
							for each GlobalLocationNumberDetailSet2Rel
								Counter += 1
								if  (Counter = 1)
									SaveGlobalLocationNumber2	= each.GlobalLocationNumber
									SaveCompany2				= each.Company
								else
									constraint (instance count of GlobalLocationNumberDetailSet2Rel < 2)
										"MultipleRecordsFoundForCompanyGLN"
								
					
			Local Fields
				LocalInstanceCount				is Numeric 10
				LocalSetInterfaceResult			is a PurchaseOrderReceiptInterfaceResult view 	
				LocalLineCount					is Numeric 12
				LocalCount						is Numeric 12   
				LocalAOCCount					is Numeric 12
				SaveGlobalLocationNumber1		is like GlobalLocationNumber
				SaveCompany1					is like Company
				SaveGlobalLocationNumber2		is like GlobalLocationNumber
				SaveCompany2					is like Company


				
			Instance Selection			
				where (RunGroup								= PrmRunGroup
                and    Company.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and   (PrmCompany not entered
				or     Company								= PrmCompany)
				and (PrmLocation not entered
                or Location = PrmLocation))

			Sort Order
            	RunGroup
            	Company
            	ReferenceNumber
            	RecordType
            	LineNumber
            	SeqNbr
            	AddOnCharge

			Action Rules

				Empty Set Rules
					invoke Create PurchaseOrderReceiptInterfaceResult
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.RunTime						= current timestamp
						invoked.RunGroup					= PrmRunGroup
						invoked.RunType						= 1
						invoked.Company						= PrmCompany
				        invoked.ReleaseReceipts				= ReleaseReceipts
						invoked.ReleaseOption				= ReleaseOption
				        invoked.AllowDropshipReceivers		= AllowDropshipReceivers
				        invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
						invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
						invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix
						invoked.Status						= 1 
				
				RunGroup Set Rules
					Entrance Rules
						ErrorOccurred							= false
						
						invoke Create PurchaseOrderReceiptInterfaceResult
							assign result to LocalSetInterfaceResult
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.RunType						= 1
							invoked.Company						= PrmCompany
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.ReleaseOption				= ReleaseOption
					        invoked.AllowDropshipReceivers		= AllowDropshipReceivers
					        invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix

					Exit Rules
						invoke Update LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
							invoked.RecordsProcessed							= LocalInstanceCount
							invoked.InterfaceCounts.PassedHeaderCount          	= LocalCount         
							invoked.InterfaceCounts.FailedHeaderCount           = (LocalInstanceCount - LocalCount)
							invoked.InterfaceCounts.DetailCount          		= LocalLineCount
							invoked.InterfaceCounts.AddOnChargeCount     		= LocalAOCCount
							if (!LocalSetInterfaceResult.Status.Incomplete)
								invoked.Status        				= 1 

				Company Set Rules
					Entrance Rules
						LocalCompany	= Company

				ReferenceNumber Set Rules
					Entrance Rules
						
					Exit Rules
						invoke PostLoadInterface ReceiptInterfaceInput in foreground
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmReferenceNumber			= ReferenceNumber
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.ReleaseOption				= ReleaseOption
							invoked.AllowDropshipReceivers		= AllowDropshipReceivers
							invoked.EnableGLNProcessing			= EnableGLNProcessing
							invoked.EnableGTINProcessing		= EnableGTINProcessing
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
							invoked.PrmInterfaceRun				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
					
				Instance Rules
					if (RecordType.ReceiptHeader)
						LocalInstanceCount			+= 1
						if  (RemoveExistingReceiptsInReceiptMaintenance)
							invoke Delete PurchaseOrderReceiptImportRel 
																
		                if  (SkipThisRecord)
							NextSentence = true
						else
							ErrorOccurred									= false
							
							LocalLineCount									+= LineCountForAReceipt
	
							if (NewPurchaseOrderReceipt entered)
								invoke FastUpdate NewPurchaseOrderReceipt
									invoked.OriginatingInterfaceRun			= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
							else
							if  (UnreleasedReceiptRel exists
							and  not Company.AllowMultipleUnreleasedReceipts)
								ErrorOccurred								= true
								LocalErrorMessage							= UnreleasedReceiptMsg
							else
							if  (!LinesForHeaderRel exists)
								ErrorOccurred								= true
								LocalErrorMessage							= NoReceiptLinesMsg
							else
							if (LinesForHeaderRel.HasUnreleasedReceiptLine)
								LocalPurchaseOrderLine						= LinesForHeaderRel.LineNumber	
								ErrorOccurred								= true
								LocalErrorMessage							= UnreleasedReceiptLineMsg
							else
							if (ReceiptLinesWithDifferentLocationsRel exists
							and DerivedVendorPoolOption = 3)
								ErrorOccurred								= true
								LocalErrorMessage							= ReceiptLinesWithDifferentLocationsMsg
							else
							if  (EnableGLNProcessing)
								include GLNProcessing

						if  (!ErrorOccurred)

							invoke Unreleased.CreateFromInterface PurchaseOrderReceipt
								assign result to InterfacedReceipt
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
									except invoked.PurchaseOrderReceipt
									except invoked.Status
									
								invoked.Vendor								= DerivedVendor
	
								if  (EnableGLNProcessing)
									invoked.Company							= UseCompany
									invoked.Vendor							= UseVendor
									invoked.ShipToLocation					= UseLocation
									invoked.Buyer							= UseBuyer
	
								if (InterfacedPurchaseOrder entered
								and PurchaseOrder not entered)
									invoked.PurchaseOrder					= first InterfacedPurchaseOrderRel.PurchaseOrder
								else
									invoked.PurchaseOrder					= PurchaseOrder
	
								invoked.TransientInterfaceStatus			= Status
								invoked.InterfaceInProcess					= true
								invoked.OriginatingInterfaceRun				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
								if  (AllowDropshipReceivers)
									invoked.BypassDropshipEdit				= true
							
						if (ErrorOccurred)
							invoke CreateErrorRecord
								invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
								invoked.PrmErrorMessage			= LocalErrorMessage
								invoked.PrmErrorLevel			= 1
								invoked.PrmResult				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
								invoked.EnableGLNProcessing		= EnableGLNProcessing
						else
							LocalCount 									+= 1
							NewPurchaseOrderReceipt						= InterfacedReceipt.PurchaseOrderReceipt
					else
					if (RecordType.AddOnCharge)
			            if  (SkipThisRecord
			            or  ReceiptHeaderRel.NewPurchaseOrderReceipt not entered)
							NextSentence = true
						else
							ErrorOccurred				= false
							if  (AddOnCharge not entered)
								ErrorOccurred		= true
								LocalErrorMessage	= AddOnChargeRequiredMsg
			
							if (!ErrorOccurred)
								if  (LineNumber entered)  
									if  ((Item entered
									and   LineNumberLookupRel.Item != Item)
									or   (VendorItem entered
									and   LineNumberLookupRel.VendorItem != VendorItem))
										include GetPurchaseOrderLine
									else
										LocalPurchaseOrderLine = LineNumber 
										
									if  (PurchaseOrderReceiptLineForAOCRel exists
									and !PurchaseOrderReceiptLineForAOCRel.Interfaced) 
										invoke ReceiptLineLoad PurchaseOrderReceiptLineForAOCRel in foreground
											invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
											invoked.PrmRunGroup					= PrmRunGroup
											invoked.PrmCompany					= PrmCompany
											invoked.PrmReferenceNumber			= ReferenceNumber
									        invoked.ReleaseReceipts				= ReleaseReceipts
											invoked.AllowDropshipReceivers		= AllowDropshipReceivers
									        invoked.AllowMatchItemOrVendorItem  = AllowMatchItemOrVendorItem
											invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
											invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
											invoked.PrmInterfaceRun				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
										if (LocalSetInterfaceResult.Status.Incomplete)
											ErrorOccurred = true
			
							if (!LocalSetInterfaceResult.Status.Incomplete)	
								if (!ErrorOccurred)							
									invoke Create PurchaseOrderReceiptLineAOC
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										fill in fields from this instance
											
										invoked.PurchaseOrder				= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
										invoked.PurchaseOrderReceipt		= ReceiptHeaderRel.NewPurchaseOrderReceipt
										invoked.PurchaseOrderReceiptLine	= LocalPurchaseOrderLine
										invoked.AddOnCharge					= AddOnCharge
										invoked.ShipToLocation				= Location
										if  (TotalAddOnChargeAmount not entered)
											invoked.TotalAddOnChargeAmount	= EnteredReceivedQuantity * OriginalUnitCost
			
								if (ErrorOccurred)
									invoke CreateErrorRecord
										invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
										invoked.PrmErrorMessage			= LocalErrorMessage
										invoked.PrmErrorLevel			= 3
										invoked.PrmResult				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
										invoked.EnableGLNProcessing		= EnableGLNProcessing
								else
									LocalAOCCount += 1
									invoke FastDelete
									if  (PurchaseOrderReceiptLineForAOCRel exists
									and  !OtherAddOnChargesForSameLineRel exists
									and  !PurchaseOrderReceiptLineForAOCRel.LineDetailRel exists)
										invoke FastDelete PurchaseOrderReceiptLineForAOCRel
				
					else
					if (RecordType.ReceiptLine)
						invoke ReceiptLineLoad in foreground
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.PrmReferenceNumber			= ReferenceNumber
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.AllowDropshipReceivers		= AllowDropshipReceivers
					        invoked.AllowMatchItemOrVendorItem	= AllowMatchItemOrVendorItem
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
							invoked.PrmInterfaceRun				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
						if (LocalSetInterfaceResult.Status.Incomplete)
							ErrorOccurred = true

 		PostLoadInterface is a Set Action
 			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup	
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				PrmReferenceNumber			is like ReferenceNumber
				ReleaseReceipts				is Boolean
				ReleaseOption
				AllowDropshipReceivers		is Boolean
				EnableGLNProcessing			is Boolean
				EnableGTINProcessing		is Boolean
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PurchaseOrderReceiptInterfaceResult

			Parameter Rules
				PrmRunGroup
					required

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
				and   (PrmCompany            			  not entered
				or     Company	= PrmCompany)
				and	   ReferenceNumber = PrmReferenceNumber
        		and    RecordType.ReceiptHeader)

			Sort Order
            	RunGroup
            	Company
            	ReferenceNumber
            	RecordType
            	LineNumber
            	SeqNbr
            	AddOnCharge

			Action Rules
				Empty Set Rules

				RunGroup Set Rules
						
					Exit Rules

				Instance Rules
					
	                if  (SkipThisRecord
	                or  ReceiptHeaderRel.NewPurchaseOrderReceipt not entered)
						NextSentence = true
					else
					if  (EnableGLNProcessing)
						if (AllowDropshipReceivers
						and GLNPurchaseOrderReceiptRel.PurchaseOrder.IsDropship)
							invoke CreateInProgress.TransitionToDropship GLNPurchaseOrderReceiptRel
						else
							if (GLNPurchaseOrderReceiptRel.PurchaseOrderReceipt.Status.CreateInProgress)
								invoke CreateInProgress.TransitionToUnreleased GLNPurchaseOrderReceiptRel
					
							invoke Unreleased.FastUpdate GLNPurchaseOrderReceiptRel
								invoked.InterfaceInProcess			= false
							
							if (ReleaseReceipts)
								if  (ReleaseOption.ReleaseOnly)
									invoke Unreleased.Release GLNPurchaseOrderReceiptRel
										resume on error
								else
								if  (ReleaseOption.ReleaseAndDeliver)
									invoke Unreleased.ReleaseAndPrintLPL GLNPurchaseOrderReceiptRel
										resume on error
								else
								if  (ReleaseOption.ReleaseAndDeliverNoBackorders)
									invoke Unreleased.ReleaseAndPrintNoBackordersLPL GLNPurchaseOrderReceiptRel
										resume on error
					else
						if (AllowDropshipReceivers
						and NewPurchaseOrderReceipt.PurchaseOrder.IsDropship)
							invoke CreateInProgress.TransitionToDropship NewPurchaseOrderReceipt
						else
							if (NewPurchaseOrderReceipt.Status.CreateInProgress)
								invoke CreateInProgress.TransitionToUnreleased NewPurchaseOrderReceipt
						
							invoke Unreleased.FastUpdate NewPurchaseOrderReceipt
								invoked.InterfaceInProcess			= false
							if (ReleaseReceipts)
								if  (ReleaseOption.ReleaseOnly)
									invoke Unreleased.Release NewPurchaseOrderReceipt
										resume on error
								else
								if  (ReleaseOption.ReleaseAndDeliver)
									invoke Unreleased.ReleaseAndPrintLPL NewPurchaseOrderReceipt
										resume on error
								else
								if  (ReleaseOption.ReleaseAndDeliverNoBackorders)
									invoke Unreleased.ReleaseAndPrintNoBackordersLPL NewPurchaseOrderReceipt
										resume on error

					invoke FastDelete 
			
		ReceiptLineLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				PrmReferenceNumber			is like ReferenceNumber
				ReleaseReceipts				is Boolean
				AllowDropshipReceivers		is Boolean
				AllowMatchItemOrVendorItem  is Boolean
				EnableGTINProcessing        is Boolean 
				EnableGLNProcessing         is Boolean 
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PurchaseOrderReceiptInterfaceResult
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					
				PrmRunGroup
					required
						"RunGroupIsRequired"

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    ReferenceNumber					= PrmReferenceNumber
        		and    RecordType.ReceiptLine)

			Sort Order
            	RunGroup
            	Company
            	ReferenceNumber
            	RecordType
            	LineNumber
            	SeqNbr
            	AddOnCharge

			Action Rules

				Empty Set Rules

				RunGroup Set Rules
						
					Exit Rules

				Instance Rules










	                if  (SkipThisRecord
	                or  ReceiptHeaderRel.NewPurchaseOrderReceipt not entered)
						NextSentence = true
					else
					if (ReceiptHeaderRel exists)
						ErrorOccurred						= false
	
						if  (EnableGTINProcessing
						and  ItemGTIN entered)
							LocalCompany							= Company
							if  (EnableGLNProcessing)
								LocalCompany						= ReceiptHeaderRel.UseCompany
								
							Item									= first ItemGTINRel.Item
	
						if  (!Interfaced)
							if  (!ErrorOccurred)
								if  (Item not entered
								and  VendorItem not entered
								and  Description not entered
								and  LineNumber not entered)
									ErrorOccurred		= true
									LocalErrorMessage	= LineMissingDataMsg

							if  (!ErrorOccurred)
								if  (LineNumber not entered
								or	 LineNumberLookupRel.QuantityFullyCancelled
								or  (Item entered
								and  LineNumberLookupRel.Item != Item)
								or  (VendorItem entered
								and  LineNumberLookupRel.VendorItem != VendorItem)
								or  (DerivedVendorPoolOption.CompanyVendorPoShipTo 
								and  LineNumberLookupRel.ShipToLocation != Location))

									initialize POLineLookup
									POLineLookup.Company				= Company
									POLineLookup.PurchaseOrder			= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
									POLineLookup.PurchaseOrderLine		= LineNumber
									POLineLookup.Item					= Item
									POLineLookup.VendorItem				= VendorItem
									POLineLookup.Description			= Description
									POLineLookup.Location				= Location
									POLineLookup.AllowMatchItemOrVendorItem = AllowMatchItemOrVendorItem
									POLineLookup.InterfaceType 			= "R"
									LocalTrigger						= POLineLookup.ExecuteLookup
									if  (POLineLookup.OutputErrorNumber entered)
										ErrorOccurred					= true
										LocalErrorMessage				= POLineLookup.OutputErrorMessage
									else
										LocalPurchaseOrderLine 				= POLineLookup.PurchaseOrderLine
										if (LineNumber not entered)
											LineNumber                      = POLineLookup.PurchaseOrderLine
										Item							= POLineLookup.Item
										VendorItem						= POLineLookup.VendorItem
								else
									LocalPurchaseOrderLine = LineNumber

									if  (Item not entered
									and  LineNumberLookupRel.VendorItem = VendorItem)
										Item = LineNumberLookupRel.Item

									if  (VendorItem not entered
									and  LineNumberLookupRel.Item = Item)
										VendorItem = LineNumberLookupRel.VendorItem



							if (!ErrorOccurred)


								if (EnteredReceivedQuantity entered)
									if (PurchaseOrderReceiptLineRel exists)
										invoke Update PurchaseOrderReceiptLineRel
											invoked.EnteredReceivedQuantity += EnteredReceivedQuantity
											invoked.CatchWeightQuantity 	+= CatchWeightQuantity
											if (LineDetailRel exists)
											    invoked.InterfaceHasDetails	= true
									else								
										invoke CreateFromInterface PurchaseOrderReceiptLine
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											fill in fields from this instance
											invoked.Company						= ReceiptHeaderRel.Company
											invoked.PurchaseOrder				= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
											invoked.PurchaseOrderReceipt		= ReceiptHeaderRel.NewPurchaseOrderReceipt
											invoked.PurchaseOrderReceiptLine	= LocalPurchaseOrderLine
											invoked.PurchaseOrderLine 			= LocalPurchaseOrderLine
											invoked.ShipToLocation				= Location
											if (LineDetailRel exists)
											    invoked.InterfaceHasDetails		= true
								else
									if (CancelBackorder)

									 	LocalPurchaseOrder = ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder	
								 			
										invoke Create POReceiptLineTemplate
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											fill in fields from this instance

											invoked.Company						= ReceiptHeaderRel.Company
											invoked.PurchaseOrder				= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
											invoked.PurchaseOrderReceipt		= ReceiptHeaderRel.NewPurchaseOrderReceipt
											invoked.PurchaseOrderLine 			= LocalPurchaseOrderLine
											invoked.ShipToLocation				= Location

									else
										ErrorOccurred		= true
										LocalErrorMessage	= CancelRemainingMustBeYesIfQuantityIsZeroMsg
								

									
						if (!ErrorOccurred)
							Interfaced								= true
						
							if  (EnableGLNProcessing
							and  BinGln entered)
								LocalGlobalLocationNumberType 		= 11
								LocalGlobalLocationNumber			= BinGln
								initialize Counter
								for each GlobalLocationNumberDetailSet1Rel
									Counter += 1
									UseBin			= first GlobalLocationNumberDetailSet1Rel.CompanyLocationBin.Bin
	
							if (LineDetailRel exists)
								if (PurchaseOrderReceiptLineRel.IsDetailTracked) 
									for each LineDetailRel
										if (PurchaseOrderReceiptLineRel.IsBinTracked)
											if (each.Bin entered)
												LocalBin 	= each.Bin
											else
												LocalBin	= PurchaseOrderReceiptLineRel.Bin
											
										if (each.Lot entered and (PurchaseOrderReceiptLineRel.IsLotRequiredAtReceipt or PurchaseOrderReceiptLineRel.IsNonstockLotRequiredAtReceipt))
											LocalLot	= each.Lot
											
										if (each.Serial entered and (PurchaseOrderReceiptLineRel.IsSerialRequiredAtReceipt or PurchaseOrderReceiptLineRel.IsNonstockSerialRequiredAtReceipt))
											LocalSerial	= each.Serial						
											
										if (LocalBin entered or LocalLot entered or LocalSerial entered) 
											if (SamePurchaseOrderTransactionDetailsRel exists)
												invoke FastUpdate SamePurchaseOrderTransactionDetailsRel
								                    if (IsCatchWeightItem)
								                    	invoked.Quantity			+= each.CatchWeightQuantity
								                    	invoked.SecondaryQuantity	+= each.DetailQuantity
								                    else
								                    	invoked.Quantity			+= each.DetailQuantity			
											else
												invoke Create PurchaseOrderTransactionDetail
													resume on error
														ErrorOccurred		= true
														LocalErrorMessage	= error message
													fill in fields from each
														except invoked.Bin
														except invoked.Lot
														except invoked.Serial
					
													invoked.PurchaseOrderReceipt											= ReceiptHeaderRel.NewPurchaseOrderReceipt
													invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= "PO"
													invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= ReceiptHeaderRel.NewPurchaseOrderReceipt
													invoked.SourceDocumentAlpha												= ReceiptHeaderRel.NewPurchaseOrderReceipt
													invoked.PurchaseOrderTransactionDetail.LineNumber						= LocalPurchaseOrderLine
													invoked.PurchaseOrderTransactionDetail.TransactionSequence				= each.SeqNbr
													invoked.Bin																= LocalBin
													invoked.Lot																= LocalLot
													invoked.Serial															= LocalSerial
								                    if (IsCatchWeightItem)
								                    	invoked.Quantity								= each.CatchWeightQuantity
								                    	invoked.SecondaryQuantity						= each.DetailQuantity
								                    else
														if (each.DetailQuantity entered)
															invoked.Quantity							= each.DetailQuantity
														else
															if (each.Serial entered)
																invoked.Quantity						= 1
															
													if  (EnableGLNProcessing)
														invoked.Bin															= UseBin
														
											if (ErrorOccurred)
												end for each
											else
												invoke FastDelete each
										else 
											invoke FastDelete each
								else 
									for each LineDetailRel
										invoke FastDelete each
									
						if (ErrorOccurred)
							invoke CreateErrorRecord
								invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
								invoked.PrmErrorMessage			= LocalErrorMessage
								invoked.PrmErrorLevel			= 2 
								invoked.PrmResult				= PrmInterfaceRun
								invoked.EnableGLNProcessing		= EnableGLNProcessing
						else
						if  (!LineAddOnCostsRel exists
						and  !LineDetailRel exists)
							invoke FastDelete
						else
							Interfaced = true
	
		AddOnChargeLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				ReleaseReceipts				is Boolean
				ReleaseOption
				EnableGTINProcessing        is Boolean 
				EnableGLNProcessing         is Boolean 
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PurchaseOrderReceiptInterfaceResult
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
                and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    RecordType.AddOnCharge)

			Sort Order
            	RunGroup
            	Company
            	ReferenceNumber
            	RecordType
            	LineNumber
            	SeqNbr
            	AddOnCharge

			Action Rules

				Empty Set Rules
						

					invoke PostLoadInterface ReceiptInterfaceInput
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
				        invoked.ReleaseReceipts				= ReleaseReceipts
						invoked.ReleaseOption				= ReleaseOption
						invoked.EnableGLNProcessing			= EnableGLNProcessing
						invoked.EnableGTINProcessing		= EnableGTINProcessing
						invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
						invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
						invoked.PrmInterfaceRun				= PrmInterfaceRun

				RunGroup Set Rules
						
					Exit Rules

						invoke PostLoadInterface ReceiptInterfaceInput
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.ReleaseOption				= ReleaseOption
							invoked.EnableGLNProcessing			= EnableGLNProcessing
							invoked.EnableGTINProcessing		= EnableGTINProcessing
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
							invoked.PrmInterfaceRun				= PrmInterfaceRun

				Instance Rules

	                if  (SkipThisRecord
	                or  ReceiptHeaderRel.NewPurchaseOrderReceipt not entered)
						NextSentence = true
					else
					
						ErrorOccurred						= false
	
						if (!ErrorOccurred)
							if  (AddOnCharge not entered)
								ErrorOccurred		= true
								LocalErrorMessage	= AddOnChargeRequiredMsg
	
						if (!ErrorOccurred)
							if  (LineNumber entered)  
								if  ((Item entered
								and   LineNumberLookupRel.Item != Item)
								or   (VendorItem entered
								and   LineNumberLookupRel.VendorItem != VendorItem))
									include GetPurchaseOrderLine

								else
									LocalPurchaseOrderLine = LineNumber
	
						if (!ErrorOccurred)
							invoke Create PurchaseOrderReceiptLineAOC
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
									
								invoked.PurchaseOrder				= ReceiptHeaderRel.NewPurchaseOrderReceipt.PurchaseOrder
								invoked.PurchaseOrderReceipt		= ReceiptHeaderRel.NewPurchaseOrderReceipt
								invoked.PurchaseOrderReceiptLine	= LocalPurchaseOrderLine
								invoked.AddOnCharge					= AddOnCharge
								invoked.ShipToLocation				= Location
								if  (TotalAddOnChargeAmount not entered)
									invoked.TotalAddOnChargeAmount	= EnteredReceivedQuantity * OriginalUnitCost
	
						if (ErrorOccurred)

							invoke CreateErrorRecord
								invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
								invoked.PrmErrorMessage			= LocalErrorMessage
								invoked.PrmErrorLevel			= 3
								invoked.PrmResult				= PrmInterfaceRun
								invoked.EnableGLNProcessing		= EnableGLNProcessing
	
						else
							if  (PurchaseOrderReceiptLineForAOCRel exists
							and  !OtherAddOnChargesForSameLineRel exists
							and  !PurchaseOrderReceiptLineForAOCRel.LineDetailRel exists)

								invoke FastDelete PurchaseOrderReceiptLineForAOCRel
							

							invoke FastDelete


        PopulateMissingCompanyAndVendor is a Set Action
            Parameters
                PrmCompany                  is a PurchasingCompany           
                    default label is "Company"
 
 
            Sort Order is primary
                    
            Instance Selection
                where (Company not entered
                or Vendor not entered)
                
                
            Action Rules
                Instance Rules
                    if (Company not entered)
                        POLineLookup.Company = PrmCompany
                        POLineLookup.PurchaseOrder = PurchaseOrder
                        if (POLineLookup.PurchaseOrderRel exists)
                            Company = PrmCompany
                    if (Vendor not entered)
                        POLineLookup.Company = Company
                        POLineLookup.PurchaseOrder = PurchaseOrder
                        if (POLineLookup.PurchaseOrderRel exists)
                            Vendor = POLineLookup.PurchaseOrderRel.Vendor

































































                        
