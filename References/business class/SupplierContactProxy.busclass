SupplierContactProxy is a BusinessClass
    owned by procurement
    prefix is SCNG

    Ontology
    	part of SupplierSourceId
    		relative key is ContactId is Numeric size 4

    Persistent Fields
		PersonName
		EmailAddress 						
		ReceiveEmailNotification			is Boolean

	Local Fields
		LocalCompany                        is a SourcingCompany
	
	Conditions
	
		ActiveSupplierOrNotSupplier
			restricted
			when ((!actor.agent(SupplierSourceId).SupplierSourceId.InactiveAtSomeLevel
			and   actor.agent(SupplierSourceId).SupplierSourceId exists)
			or    actor.agent(SupplierSourceId).SupplierSourceId !exists)
			
	Field Rules
		ContactId
			autosequence 
		PersonName
			if (true)	
				required
		EmailAddress
			if (true)	
				required
		ReceiveEmailNotification
			if (SupplierSourceId.ReceiveEmailNotification = false)
                constraint (ReceiveEmailNotification = false)
                    "SupplierContactMustBeSetToReceiveEmailForProxyToReceiveEmail"
	
	Relations
	
		SourcingCompanyExtensionRel 
			one-to-one relation to SourcingCompanyExtension
			Field Mapping uses symbolic key
				related.Company = LocalCompany
	
	Derived Fields
		FullName is a MessageField
			"<PersonName.GivenName>_<PersonName.FamilyName>"
			
		Notify is a MessageField
			"Notify"
			
		DoNotNotify is a MessageField
			"Do_Not_Notify"
				
		NotificationTag is a DerivedField
			type is Alpha 30
			if (ReceiveEmailNotification)
				return Notify
			else
				return DoNotNotify
			
	Actions
		Create is a Create Action
			valid when (ActiveSupplierOrNotSupplier)
			Field Rules
                ReceiveEmailNotification
                    default to SupplierSourceId.ReceiveEmailNotification
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		
