CashCodeFundEdit is a BusinessClass
	owned by cb
	prefix is CCFE
	default label is "CashCodeFundEdit"
    
    Ontology
    	symbolic key is CashCodeFundEdit

    Patterns
        implements DynamicCreation
		disable AuditIndex
		disable Auditing 
		disable EffectiveDated
    	
    Persistent Fields
 		CashManagementGroup
  		OriginatingTransaction		is BusinessObjectReference
 		CashCode
 		AccountingEntity
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>"
		AccountingUnit
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>"
        FinanceDimension1
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>"
 		EditAmount					is an InternationalAmount
 		CommittedAmount				is an InternationalAmount
 		ExceedesFundBalance			is Boolean
			default label is "ExceedsFundBalance"
 		EditGroup					is like CashCodeFund
		CommittedAmountRelieved 	is Boolean
		RemainingCommittedAmount 	is an InternationalAmount
				    			
	Derived Fields
    	DerivedEditAmount is a DerivedField
			type is like InternationalAmount
			if (EditGroup entered)
				return (sum FundEditByEditGroupRel.EditAmount + EditAmount)
			else
				return EditAmount

		DerivedLocalRoundTo is a DerivedField
			type is Decimal 5.4
			restricted
			if (LocalNumberOfDecimals = 2)
				return .01
			else
			if (LocalNumberOfDecimals = 0)
				return 1
			else
			if (LocalNumberOfDecimals = 3)
				return .001
			else
			if (LocalNumberOfDecimals = 4)
				return .0001

		Msg01 is a MessageField
			restricted
			"DistributionsExceedCashFundAmountForCashCode<CashCode>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<AccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>_<AccountingUnit>"
			
		Msg02 is a MessageField
			restricted
			"DistributionsExceedCashFundAmountForCashCode<CashCode>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<AccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>_<FinanceDimension1>"
			
		Msg03 is a MessageField
			restricted
			"<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<AccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>_<AccountingUnit>IsNotDefinedAsAValidFundCombinationForCashCode<CashCode>"
			
		Msg04 is a MessageField
			restricted
			"<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<AccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>_<FinanceDimension1>IsNotDefinedAsAValidFundCombinationForCashCode<CashCode>"
			
		ErrorMessage is a DerivedField
			type is Alpha size up to 200
			if (EditAmount entered)
				if (AccountingUnit entered)
					return Msg01
				else
					return Msg02
			else
				if (AccountingUnit entered)
					return Msg03
				else
					return Msg04
				
		
	Local Fields
		LocalNumberOfDecimals					is Numeric 1
		
	Transient Fields
				
	Field Rules
		EditAmount
			EditGroup = CashCodeFundRel.ParentCashCodeFund
			if ((CashCode.EditAvailableFunds
			and  !CashCodeFundRel.ExcludeFromEdit							
			and  DerivedEditAmount > 0
			and  DerivedEditAmount > CashCodeFundRel.EditAvailableAmount)
			or   CashCodeFundRel !exist)
				ExceedesFundBalance = true
			else
				ExceedesFundBalance = false
	
	Conditions
		IncludedInEditGroup
			when (EditGroup entered)
		
		ViewPayablesInvoice
			when (OriginatingTransaction.BusinessClassName = "PayablesInvoice")

		ViewCashLedgerPayment
			when (OriginatingTransaction.BusinessClassName = "CashLedgerPayment")
						
	Relations
		CashCodeFundRel
			one-to-one relation to CashCodeFund
			Field Mapping uses ByAccountingEntityFund
				related.CashManagementGroup		= CashManagementGroup
				related.CashCode				= CashCode
				related.AccountingEntity		= AccountingEntity
				related.AccountingUnit			= AccountingUnit
				related.FinanceDimension1		= FinanceDimension1

		FundEditByEditGroupRel
			one-to-many relation to CashCodeFundEdit
			Field Mapping uses ByEditGroup
				related.CashManagementGroup		= CashManagementGroup
				related.OriginatingTransaction	= OriginatingTransaction
				related.EditGroup				= EditGroup
			Instance Selection
				where (related.CashCodeFundEdit != CashCodeFundEdit)
				
			
	Sets
		ByOriginatingTransaction
			Sort Order
		 		CashManagementGroup
		  		OriginatingTransaction
		 		CashCode
		 		AccountingEntity
				AccountingUnit
		 		FinanceDimension1

		ByExceedesFundBalance
			Sort Order
		 		CashManagementGroup
		  		OriginatingTransaction
		  		ExceedesFundBalance
		 		CashCode
		 		AccountingEntity
				AccountingUnit
		 		FinanceDimension1
		
		ByEditGroup
			Instance Selection
				where (IncludedInEditGroup)
			Sort Order
		 		CashManagementGroup
		  		OriginatingTransaction
		  		EditGroup
		 		CashCode
		 		AccountingEntity
				AccountingUnit
		 		FinanceDimension1
		
		ByCommittedAmountRelieved
			Sort Order
				CashManagementGroup
				CommittedAmountRelieved
				CashCode
				AccountingEntity
				AccountingUnit
				FinanceDimension1
				OriginatingTransaction
	
	Rule Blocks







	Actions
	
		Create is a Create Action


		
		Update is an Update Action


		
		Delete is a Delete Action
		
		UpdateCommittedFunds is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateCommittedAmount CashCodeFundRel
					invoked.PrmCommittedAmount = (EditAmount *-1)
				CommittedAmount += EditAmount
				RemainingCommittedAmount += EditAmount
				initialize EditAmount
		
		UpdateRemainingCommittedAmount is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				initialize RemainingCommittedAmount
				CommittedAmountRelieved = true

		InitializeEditAmount is an Instance Action		
			default label is untranslatable
			restricted
			Action Rules
				initialize EditAmount

		CancelCommittedFunds is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				invoke UpdateCommittedAmount CashCodeFundRel
					invoked.PrmCommittedAmount = CommittedAmount
				initialize CommittedAmount
				initialize RemainingCommittedAmount
				
		RelieveCommittedFunds is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmPaymentPercent		is a Percent
				PrmNumberOfDecimals		is Numeric 1
				PrmVoidReinstatement	is Boolean
				PrmPayGroup				is like PayGroup
			Local Fields
				LocalPaidAmount		is an InternationalAmount
			Action Rules
				if (PrmPaymentPercent != 1)
					LocalPaidAmount 		= CommittedAmount * PrmPaymentPercent
					LocalNumberOfDecimals	= PrmNumberOfDecimals	
					round LocalPaidAmount to nearest DerivedLocalRoundTo
				else
					LocalPaidAmount = CommittedAmount
				if (PrmVoidReinstatement)
					LocalPaidAmount = LocalPaidAmount * -1
				
				RemainingCommittedAmount -= LocalPaidAmount
				if (RemainingCommittedAmount !entered)
					CommittedAmountRelieved = true
				else 
					CommittedAmountRelieved = false

				if (PrmPayGroup entered)
					invoke Create CashCodeFundConsolidatedUpdate
						invoked.CashManagementGroup		= CashManagementGroup
						invoked.PayGroup				= PrmPayGroup
						invoked.CashCode				= CashCode
						invoked.AccountingEntity		= AccountingEntity
						invoked.AccountingUnit			= AccountingUnit
						invoked.FinanceDimension1		= FinanceDimension1
						invoked.CommittedAmount			= LocalPaidAmount * -1
				else
					invoke UpdateCommittedAmount CashCodeFundRel
						invoked.PrmCommittedAmount = LocalPaidAmount
				
		RelieveAmmendedCommittedFunds is an Instance Action		
			default label is untranslatable
			restricted
			Parameters
				PrmPaymentPercent		is a Percent
				PrmNumberOfDecimals		is Numeric 1
			Local Fields
				LocalPaidAmount		is an InternationalAmount
			Action Rules
				if (EditAmount	entered)			
					if (PrmPaymentPercent != 1)
						LocalPaidAmount			= EditAmount * PrmPaymentPercent
						LocalNumberOfDecimals	= PrmNumberOfDecimals	
						round LocalPaidAmount to nearest DerivedLocalRoundTo
					else
						LocalPaidAmount			= EditAmount
					
					RemainingCommittedAmount -= LocalPaidAmount
					if (RemainingCommittedAmount !entered)
						CommittedAmountRelieved = true
					else 
						CommittedAmountRelieved = false

					invoke UpdateCommittedAmount CashCodeFundRel
						invoked.PrmCommittedAmount = LocalPaidAmount
					
		
		UpdateRemainingCommittedAmountField is a Set Action
			restricted
			default label is untranslatable
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
					default label is "CashManagementGroup"
				PrmCashCode					is a CashCode
					default label is "CashCode"	

			Parameter Rules
				PrmCashManagementGroup
					required

			Local Fields
				LocalPaymentPercent		is a Percent
				
			Instance Selection
				where (CashManagementGroup	 	= PrmCashManagementGroup
				and    !CommittedAmountRelieved
				and   (CashCode					= PrmCashCode
				or     PrmCashCode				not entered))

			Sort Order is ByCommittedAmountRelieved

			Action Rules
				Empty Set Rules
					
				Set Rules
					Entrance Rules

					Exit Rules
				
				Instance Rules
					if (OriginatingTransaction.BusinessClassName = "CashLedgerPayment")
						if (OriginatingTransaction(CashLedgerPayment).Status.Historical)
							CommittedAmountRelieved = true
							initialize RemainingCommittedAmount
						else
							RemainingCommittedAmount = EditAmount
					else
						if (OriginatingTransaction(PayablesInvoice).Status.Historical)
							CommittedAmountRelieved = true
							initialize RemainingCommittedAmount
						else
							if (OriginatingTransaction(PayablesInvoice).AmountPaid entered)
								LocalPaymentPercent = (OriginatingTransaction(PayablesInvoice).AmountPaid / OriginatingTransaction(PayablesInvoice).InvoiceAmount.CurrencyAmount)
								RemainingCommittedAmount	= EditAmount * LocalPaymentPercent
								LocalNumberOfDecimals		= OriginatingTransaction(PayablesInvoice).InvoiceCurrency.NumberOfDecimals
								round RemainingCommittedAmount to nearest DerivedLocalRoundTo
							else
								RemainingCommittedAmount = EditAmount	
				
