AssetLocation is a BusinessClass
    owned by am
    prefix is LOC
    classic name is AMLOCATION

    Ontology
        symbolic key is AssetLocation
            classic set name is LOCSET1
            classic name is LOCATION-NAME

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        PostalAddress							is a PostalAddressV2	
        	holds pii
            classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
            classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
            classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
            classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
            classic name for PostalAddress.Municipality is ADDR5
            classic name for PostalAddress.StateProvince is STATE-PROV
        CountryTaxAuthority
            classic name is AUTH-COUNTRY
        DistrictTaxAuthority
            classic name is AUTH-DISTRICT
        LocalityTaxAuthority
            classic name is AUTH-CTY-LOCAL
        AddressContact
            classic name is ADDR-CONTACT
        PhoneNumber           is a TelephoneNumber 
        	holds pii
            classic name for PhoneNumber.InternationalPrefix is PHONE-COUNTRY
            classic name for PhoneNumber.SubscriberNumber is PHONE-NUMBER
            classic name for PhoneNumber.Extension is PHONE-EXT
        FaxNumber
            classic name for FaxNumber.InternationalPrefix is FAX-COUNTRY
            classic name for FaxNumber.SubscriberNumber is FAX-NUMBER
            classic name for FaxNumber.Extension is FAX-EXT
        TelexNumber
        DetailLevel1Size      is Numeric size 2
            classic name is DTL-SEG-1
        DetailLevel2Size      is Numeric size 2
            classic name is DTL-SEG-2
        DetailLevel3Size      is Numeric size 2
            classic name is DTL-SEG-3
        GeographicalType      is a GISType
            classic name is GEO-TYPE
        GeographicalLatitude  is a GISCoordinate
            classic name is GEO-LATITUDE
        GeographicalLongitude is a GISCoordinate
            classic name is GEO-LONGITUDE
        GeographicalAltitude  is a GISCoordinate
            classic name is GEO-ALTITUDE
        GeographicalDate      is TimeStamp
            classic name is GEO-DATE
		Picture						is an AlternateAttachment
        Status						is AlphaUpper size 1
			States
				Active					value is blank
				Inactive				value is 1

	Local Fields
		TotalOfLevelSizes				is Numeric 2
		LocalAssetTaxAuthorityType		is a AssetTaxAuthorityType
		LocalAssetTaxAuthority			is like AssetTaxAuthority 
        LocalAttributeCtr               is Numeric 2	    
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforAddress"

		ContextMessageText is a MessageField
			restricted
			"AssetLocation<AssetLocation>"
			

		AssetLocationName is a MessageField
			"<AssetLocation>-<Description>"


        AssetLocationActivatedMessage is a DerivedField
			type is MessageField 		
			restricted
			if (Status changed
            and Status = blank)
				return LocationActivatedMessage					 	
			return blank	
				
		LocationActivatedMessage is a MessageField
			restricted
			"AssetLocationActivatedSuccessfully"

        AssetLocationDeletedMessage is a DerivedField
			type is MessageField 		
			restricted
			if (AssetLocationDetailsRel exists)
				return LocationDeletedMessage					 	
			return DeletionMessage	
				
		LocationDeletedMessage is a MessageField
			restricted
			"Warning:ThisWillDeleteAssetLocationDetailRecords.WouldYouLikeToContinue?"

        DeletionMessage is a MessageField
            restricted
            "ConfirmAction:'Delete'"

    Conditions
		AssetLocationEntered
			restricted
			when (AssetLocation entered)
			
        AssetAdjustmentExists
        	restricted
            classic name is ADJUST-EXIST
            when (first AssetAdjustmentsRel exists)





        ReleasedAssetExists
            classic name is ASSET2-EXIST
            when (first ReleasedAssetsRel exists)





        HasCountryTaxAuthority
        	restricted
            classic name is COUNTRY
            when (CountryTaxAuthority entered)

        HasLocalityTaxAuthority
        	restricted
            classic name is CTY-LOCAL
            when (LocalityTaxAuthority entered)

        HasDistrictTaxAuthority
        	restricted
            classic name is DISTRICT
            when (DistrictTaxAuthority entered)





        AssetLocationDetailExists
        	restricted
            classic name is LOCATION-EXIST
            when (first AssetLocationDetailsRel exists)

        HasStateOrProvince
        	restricted
            classic name is STATE-PROV
            when (PostalAddress.StateProvince entered)

        AssetTransferExists
        	restricted
            classic name is TRF-EXIST
            when (first AssetTransfersRel exists)
            
		LocationDetailExists
			restricted
			when (AssetLocationDetailsRel exists)

        InUseLocationsExists
            restricted
            when (!UsedByATransaction)        

    Relations









        ReleasedAssetsRel
            classic name is AMASSET2
            one-to-many relation to Asset
            Field Mapping uses Set3
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
                related.Status        				= 2
                related.AssetLocation 				= AssetLocation









        ReleasedAndPostReleasedAssetsRel
            one-to-many relation to Asset
            Field Mapping uses Set3
                related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
            Instance Selection
                where (related.Status  		> 1
                and related.Status			< 9
                and related.AssetLocation 	= AssetLocation)

        AssetAdjustmentsRel
            classic name is AMASSETADJ
            one-to-many relation to AssetAdjustment
            Field Mapping uses Set3
                related.AssetLocation = AssetLocation









        AssetTransfersRel
            classic name is AMASSETTRF
            one-to-many relation to AssetTransfer
            Field Mapping uses Set3
                related.AssetLocation = AssetLocation

        AssetLocationDetailsRel
            classic name is AMLOCDTL
            one-to-many relation to AssetLocationDetail
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
                related.AssetLocation			= AssetLocation
                
		AnotherDistrictTaxAuthorityRel
			one-to-many relation to AssetLocation
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup 
				related.DistrictTaxAuthority	= LocalAssetTaxAuthority
			Instance Selection
				where (related.AssetLocation 	!= AssetLocation)

		AnotherLocalityTaxAuthorityRel
			one-to-many relation to AssetLocation
			Field Mapping uses Set3
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.LocalityTaxAuthority	= LocalAssetTaxAuthority
			Instance Selection
				where (related.AssetLocation 	!= AssetLocation)

		AnotherCountryTaxAuthorityRel
			one-to-many relation to AssetLocation
			Field Mapping uses Set5
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.CountryTaxAuthority		= LocalAssetTaxAuthority
			Instance Selection
				where (related.AssetLocation != AssetLocation)

		AssetTaxAuthorityRel
			one-to-one relation to AssetTaxAuthority
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
				related.AssetTaxAuthorityType	= LocalAssetTaxAuthorityType
				related.AssetTaxAuthority		= LocalAssetTaxAuthority

        AssetTemplateRel
            one-to-many relation to AssetTemplate
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        AssetImportGroupRel
            one-to-many relation to AssetImport
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        ProjectInterfaceControlRel
            one-to-many relation to AssetInterfaceOptions
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        PayableAssetInterfaceRel
            one-to-many relation to AssetInterface
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation
                and related.EditSystem = "AP")
        
        ProjectAssetInterfaceRel
            one-to-many relation to AssetInterface
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation
                and related.EditSystem = "PS")

        AssetRel
            one-to-many relation to Asset
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        AssetItemAdjustmentImportRel
            one-to-many relation to AssetItemAdjustmentImport
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        AssetItemInventoryRel
            one-to-many relation to AssetItemInventory
            Field Mapping uses symbolic key
            Instance Selection
                where (related.InventoryLocation = AssetLocation)

        MassSelectionRel
            one-to-many relation to MassSelection
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation
                or     related.ToLocation    = AssetLocation)

        AssetTransferRel
            one-to-many relation to AssetTransfer
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation
                or     related.AssetTransferTo.ToAssetLocation    = AssetLocation)

        PayablesAssetDetailRel
			one-to-many relation to PayablesAssetDetail
			Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        AssetRevaluationRel
            one-to-many relation to AssetRevaluation
            Field Mapping uses Set1
                related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        PayablesDistributionCodeDetailRel
            one-to-many relation to PayablesDistributionCodeDetail
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        PayablesInvoiceDistributionImportRel
            one-to-many relation to PayablesInvoiceDistributionImport
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        PurchaseOrderLineAssetDefaultsRel
            one-to-many relation to PurchaseOrderLineAssetDefaults
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        PurchaseOrderImportAssetDefaultsRel
            one-to-many relation to PurchaseOrderImportAssetDefaults
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation)

        PurchaseOrderInterfaceInputRel
            one-to-many relation to PurchaseOrderInterfaceInput
            Field Mapping uses symbolic key
            Instance Selection
                where (related.AssetLocation = AssetLocation)

    Attach Rules
        if (parentcontext.name = "AssetTemplate"
        or  parentcontext.name = "AssetImport"
        or  parentcontext.name = "AssetInterfaceOptions"
		or  parentcontext.name = "AssetInterface"
		or  parentcontext.name = "PayablesAssetDetail"
		or  parentcontext.name = "AssetTransfer"
		or  parentcontext.name = "Asset"
        or  parentcontext.name = "AssetItemAdjustmentImport"
        or  parentcontext.name = "AssetItemInventory"
		or  parentcontext.name = "MassSelection"
		or  parentcontext.name = "AssetRevaluation"
        or  parentcontext.name = "AssetBatchUpdateResult"
        or  parentcontext.name = "AssetCompany"
		or  parentcontext.name = "MassAssetDistributionSplit"
		or  parentcontext.name = "DisposedAssetPurge"
		or  parentcontext.name = "ReleaseAssets"
        or  parentcontext.name = "MassAssetAdditions"
		or  parentcontext.name = "AssetBarCodeLabels"
        or  parentcontext.name = "PurchaseOrderLineAssetDefaults"
        or  parentcontext.name = "PurchaseOrderLineAssetDetail"
        or  parentcontext.name = "PurchaseOrderImportAssetDefaults"
        or  parentcontext.name = "PurchaseOrderImportLine"
        or  parentcontext.name = "PurchaseOrderInterfaceInput"
        or  parentcontext.name = "PayablesInvoiceDistributionImport"
        or  parentcontext.name = "PayablesDistributionCodeDetail")
            constraint (Status = blank)
                "AssetLocationIsInactive"

    Sets

        Set2
            indexed
            Instance Selection
                where (HasDistrictTaxAuthority)
            Sort Order
                FinanceEnterpriseGroup
                DistrictTaxAuthority
                AssetLocation

        Set3
            indexed
            Instance Selection
                where (HasLocalityTaxAuthority)
            Sort Order
                FinanceEnterpriseGroup
                LocalityTaxAuthority
                AssetLocation

        Set4
            indexed
            Instance Selection
                where (HasStateOrProvince)
            Sort Order
                FinanceEnterpriseGroup
                PostalAddress.StateProvince
                AssetLocation

        Set5
            indexed
            Instance Selection
                where (HasCountryTaxAuthority)
            Sort Order
                FinanceEnterpriseGroup
                CountryTaxAuthority
                AssetLocation

    Field Rules

        Description
            required

		DetailLevel1Size
			TotalOfLevelSizes = DetailLevel1Size + DetailLevel2Size + DetailLevel3Size
			constraint (TotalOfLevelSizes <= 14)
				"TotalOfDetailFieldSizesMustNotBeGreaterThan14"

			if (LocationDetailExists)
				cannot be changed
					"AssetLocationDetailsExist;CannotChangeDetailFieldSize"

		DetailLevel2Size
			if (LocationDetailExists)
				cannot be changed
					"AssetLocationDetailsExist;CannotChangeDetailFieldSize"

		DetailLevel3Size
			if (LocationDetailExists)
				cannot be changed
					"AssetLocationDetailsExist;CannotChangeDetailFieldSize"

		GeographicalDate
			if (GeographicalLatitude changed
			or GeographicalLongitude changed
			or GeographicalAltitude changed)
				force default to current timestamp

        Status
            default to blank
            if (Status changed 
            and Status = 1)
                constraint (AssetTemplateRel not exists)
                    "AssetLocationIsUsedInAssetTemplate.AssetLocationCannotBeInactive"
                constraint (AssetImportGroupRel not exists)
                    "AssetLocationIsUsedInAssetImportGroup.AssetLocationCannotBeInactive"
                constraint (ProjectInterfaceControlRel not exists)
                    "AssetLocationIsUsedInProjectInterfaceControl.AssetLocationCannotBeInactive"
                constraint (PayableAssetInterfaceRel not exists)
                    "AssetLocationIsUsedInPayableAssetInterface.AssetLocationCannotBeInactive"
                constraint (ProjectAssetInterfaceRel not exists)
                    "AssetLocationIsUsedInProjectAssetInterface.AssetLocationCannotBeInactive"
                constraint (PayablesAssetDetailRel not exists)
                    "AssetLocationIsUsedInPayablesAssetDetail.AssetLocationCannotBeInactive"
                constraint (AssetTransferRel not exists)
                    "AssetLocationIsUsedInAssetTransfer.AssetLocationCannotBeInactive"
                constraint (AssetRel not exists)
                    "AssetLocationIsUsedInAsset.AssetLocationCannotBeInactive"
                constraint (AssetItemAdjustmentImportRel not exists)
                    "AssetLocationIsUsedInAssetItemAdjustmentImport.AssetLocationCannotBeInactive"
                constraint (AssetItemInventoryRel not exists)
                    "AssetLocationIsUsedInAssetItemInventory.AssetLocationCannotBeInactive"
                constraint (MassSelectionRel not exists)
                    "AssetLocationIsUsedInMassSelection.AssetLocationCannotBeActive"
                constraint (AssetRevaluationRel not exists)
                    "AssetLocationIsUsedInAssetRevaluation.AssetLocationCannotBeActive"
                constraint (PayablesDistributionCodeDetailRel not exists)
                    "AssetLocationIsUsedInPayablesDistributionCodeDetail.AssetLocationCannotBeActive"
                constraint (PayablesInvoiceDistributionImportRel not exists)
                    "AssetLocationIsUsedInPayablesInvoiceDistributionImport.AssetLocationCannotBeActive"
                constraint (PurchaseOrderLineAssetDefaultsRel not exists)
                    "AssetLocationIsUsedInPurchaseOrderLineAssetDefaults.AssetLocationCannotBeActive"
                constraint (PurchaseOrderImportAssetDefaultsRel not exists)
                    "AssetLocationIsUsedInPurchaseOrderImportAssetDefaults.AssetLocationCannotBeActive"
                constraint (PurchaseOrderInterfaceInputRel not exists)
                    "AssetLocationIsUsedInPurchaseOrderInterfaceInput.AssetLocationCannotBeActive"

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Picture
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Picture

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Picture
	
	Actions
		Create is a Create Action

		Update is an Update Action
            completion message is "<AssetLocationActivatedMessage>"
			Exit Rules
				if (DistrictTaxAuthority changed)
					LocalAssetTaxAuthority	= old DistrictTaxAuthority
					if (AnotherDistrictTaxAuthorityRel not exists)
						LocalAssetTaxAuthorityType	= "D"
						invoke Delete AssetTaxAuthorityRel

				if (LocalityTaxAuthority changed)
					LocalAssetTaxAuthority	= old LocalityTaxAuthority
					if (AnotherLocalityTaxAuthorityRel not exists)
						LocalAssetTaxAuthorityType	= "L"
						invoke Delete AssetTaxAuthorityRel

				if (CountryTaxAuthority changed)
					LocalAssetTaxAuthority	= old CountryTaxAuthority
					if (AnotherCountryTaxAuthorityRel not exists)
						LocalAssetTaxAuthorityType	= "C"
						invoke Delete AssetTaxAuthorityRel
						
		UploadToIDM is an Instance Action  
			valid when (Picture.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Picture	
													
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Picture.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Picture
	
    				commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop


		Delete is a Delete Action
            completion message is "AssetLocationDeletedSuccessfully"
            confirmation required
                "<AssetLocationDeletedMessage>"
            valid when (InUseLocationsExists)
			Entrance Rules
                constraint (!UsedByATransaction)
                    "CannotDelete;OnlyUnusedLocationsCanBeDeleted"

                invoke Delete AssetLocationDetailsRel

				LocalAssetTaxAuthority	= DistrictTaxAuthority
				if (AnotherDistrictTaxAuthorityRel not exists)
					LocalAssetTaxAuthorityType	= "D"
					invoke Delete AssetTaxAuthorityRel

				LocalAssetTaxAuthority	= LocalityTaxAuthority
				if (AnotherLocalityTaxAuthorityRel not exists)
					LocalAssetTaxAuthorityType	= "L"
					invoke Delete AssetTaxAuthorityRel

				LocalAssetTaxAuthority	= CountryTaxAuthority
				if (AnotherCountryTaxAuthorityRel not exists)
					LocalAssetTaxAuthorityType	= "C"
					invoke Delete AssetTaxAuthorityRel

				
