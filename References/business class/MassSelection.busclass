MassSelection is a BusinessClass
    owned by am
    prefix is AMS
    classic name is AMSELECT
	default label is "SelectionName"
	
    Ontology
        symbolic key is MassSelection
            classic set name is AMSSET1
            classic name is SELECT-NAME

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        SelectAll                  is Boolean
	    Released 				   is Boolean
        Company                    is an AssetCompany
			disable surrogates
        ToCompany                  is an AssetCompany
			disable surrogates
        AssetLocation
            classic name is LOCATION-NAME
        ToLocation                 is an AssetLocation
        AssetDivision
            classic name is DIVISION
        ToDivision                 is an AssetDivision
        AssetType
            classic name for AssetType.Type is ASSET-TYPE
            context of Company
        ToAssetType                is an AssetType
            classic name for ToAssetType.Type is TO-ASSET-TYPE
            classic name for ToAssetType.SubType is TO-SUB-TYPE
            context of ToCompany
        AssetAccountGroup
            classic name is ACCT-GRP
            context of Company
        ToAssetAccountGroup        is an AssetAccountGroup
            classic name is TO-ACCT-GRP
            context of ToCompany
		DistributionAccount			is a FinanceCodeBlockNoAccountFull
        	default label is "DefaultTransactionDimensions"
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT

		ToDistributionAccount		is a FinanceCodeBlockNoAccountFull
        	default label is "ToDefaultTransactionDimensions"		
            classic name for ToDistributionAccount.AccountingUnit is TO-ACCT-UNIT

        AssetAccountingUnitGroup
            classic name is AU-GROUP
            context of Company
        ToAssetAccountingUnitGroup is an AssetAccountingUnitGroup
            default label is "ToAssetDimensionGroup"
            classic name is TO-AU-GROUP
            context of ToCompany
      	AssetProject	 							is a FinanceCodeBlockProjectOnly
			default label is "Asset<Company.FinanceEnterpriseGroup.ProjectLabel>"
        	classic name for AssetProject.Project is ACTIVITY
        ToAssetProject 	 							is a FinanceCodeBlockProjectOnly
			default label is "ToAsset<Company.FinanceEnterpriseGroup.ProjectLabel>"
        	classic name for ToAssetProject.Project is TOACTIVITY
        AssetGroup
        ToAssetGroup               is an AssetGroup
        AssetLease
        DebitTotal                 is an InternationalAmount
        CreditTotal                is an InternationalAmount
        LocationDetail			   is an AssetLocationDetail					
            classic name is LOC-DTL
            context of AssetLocation
        ToLocationDetail		   is an AssetLocationDetail					
        	context of ToLocation
       	Property
        AssetCategory
        AssetOwner					is an Employee
        ToAssetOwner				is an Employee
		Simulated 				   is Boolean
		
		
	Local Fields
		LocalAmount 			is an InternationalAmount
		LocalAsset				is like Asset
		LocalAssetSummaryGroup	is like AssetSummaryGroup
		ErrorCount				is Numeric 1
				
	Derived Fields
		DerivedNumberOfAssets     			is a ConditionalField
			type is Numeric 10
			if (SelectAll)
				if (DerivedCountOfAssets entered)
					DerivedCountOfAssets
				else
					blank
			else
				instance count of MassSelectionDetailsRel
		
		DerivedCountOfAssets				is a ConditionalField
			type is Numeric 10
			restricted
			if (IsAdjustment)
				instance count of AssetsForAdjustmentRel - instance count of MassSelectionDetailsExcludeRel
			else
			if (IsTransfer)
				instance count of AssetsForTransferRel - instance count of MassSelectionDetailsExcludeRel
			else
			if (IsDisposal)
				instance count of AssetsForDisposalRel - instance count of MassSelectionDetailsExcludeRel
			else
				instance count of AssetsForRevalueRel - instance count of MassSelectionDetailsExcludeRel
				
		DetailExistMessage        			is a MessageField
			"NoDetailsExist;AtLeastOneDetailShouldBeSelected"				
		
		BookDetailsMessage					is a MessageField
			"BookMappingHasNotBeenDoneForIntercompanyTransfer"				
			
		DerivedItemTotalTransactionCost 	is a DerivedField		
			type is like InternationalAmount 			
			if (SelectAll)
				if (IsAdjustment)
					return DerivedAssetCostForAdjustment - ExcludedAssetCostForSelectAll
				else
				if (IsDisposal)
					return DerivedAssetCostForDisposal - ExcludedAssetCostForSelectAll
				else
				if (IsTransfer)
					return DerivedAssetCostForTransfer - ExcludedAssetCostForSelectAll
				else
					return DerivedAssetCostForRevalue - ExcludedAssetCostForSelectAll			
			else
				return DerivedAssetCostForDetails
		
		DerivedAssetCostForDetails 			is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalAmount
			for each MassSelectionDetailsRel
				LocalAmount += each.Asset.EnteredTransactionCost 
			return LocalAmount
		
		DerivedAssetCostForAdjustment 		is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalAmount
			for each AssetsForAdjustmentRel
				LocalAmount += each.EnteredTransactionCost 
			return LocalAmount
		
		DerivedAssetCostForTransfer 		is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalAmount
			for each AssetsForTransferRel
				LocalAmount += each.EnteredTransactionCost 
			return LocalAmount				
		
		DerivedAssetCostForDisposal 		is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalAmount
			for each AssetsForDisposalRel
				LocalAmount += each.EnteredTransactionCost 
			return LocalAmount
		
		DerivedAssetCostForRevalue 			is a DerivedField
			type is like InternationalAmount
			restricted
			initialize LocalAmount
			for each AssetsForRevalueRel
				LocalAmount += each.EnteredTransactionCost 
			return LocalAmount										
		
		ExcludedAssetCostForSelectAll		is a DerivedField
			type is like InternationalAmount
			restricted
			return sum MassSelectionDetailsExcludeRel.DerivedTransactionCost
			
		DerivedCurrencyValue is a DerivedField
			type is like Currency
			return first AssetExcludeRel.Currency
						
		ErrorFlag is a DerivedField
			type is Numeric 1
			if (ToCompany != Company)
				initialize ErrorCount
				initialize LocalAssetSummaryGroup
				for each MassSelectionDetailsRel
					if ((LocalAssetSummaryGroup entered
					and each.Asset.AssetSummaryGroup != LocalAssetSummaryGroup)
					or (LocalAssetSummaryGroup not entered
					and each.Asset.AssetSummaryGroup entered))
						LocalAsset				= each.Asset
						LocalAssetSummaryGroup	= each.Asset.AssetSummaryGroup
						if (instance count of SummaryGroupAssetsRel != instance count of LocalMassSelectionDetailRel)
							ErrorCount = 1
							end for each					
				return ErrorCount
			
				
    Conditions
        IsBooksExist
        	restricted
			when (first MassSelectionBookDetailsRel exists)

        IsDetailExists
        	restricted
			when (first MassSelectionDetailsRel exists)

		IsReleased
        	restricted
			when (Released)
		
		IsAdjustment
        	restricted
            when (MassSelectionType.Adjustment)
            
        IsDisposal
        	restricted
            when (MassSelectionType.Disposal)

        IsRevalue
        	restricted
            when (MassSelectionType.Revalue)

        IsTransfer
        	restricted
            when (MassSelectionType.Transfer)
				
		ShowMassSelectionAdjustment
        	restricted
            when (MassSelectionType.Adjustment
			and	  !SelectAll)
            
        ShowMassSelectionDisposal
        	restricted
            when (MassSelectionType.Disposal
			and	  !SelectAll)

        ShowMassSelectionRevalue
        	restricted
            when (MassSelectionType.Revalue
			and	  !SelectAll)

        ShowMassSelectionTransfer
        	restricted
            when (MassSelectionType.Transfer
			and	  !SelectAll)
        
        ShowMassSelectAllAdjustment
        	restricted
            when (MassSelectionType.Adjustment
			and	  SelectAll)
            
        ShowMassSelectAllDisposal
        	restricted
            when (MassSelectionType.Disposal
			and	  SelectAll)

        ShowMassSelectAllRevalue
        	restricted
            when (MassSelectionType.Revalue
			and	  SelectAll)

        ShowMassSelectAllTransfer
        	restricted
            when (MassSelectionType.Transfer
            and	  SelectAll)
            
        IsIntercompanyTransfer
        	restricted
        	when (IsTransfer
        	and   Company != ToCompany)
        
		IsAdjustmentTransferOrDisposal
			restricted					      
        	when (MassSelectionType.Adjustment
        	or    MassSelectionType.Transfer
        	or    MassSelectionType.Disposal)
    	
    	IsDetailNotExistWithoutSelectAll
    		restricted
    		when (MassSelectionDetailsRel not exists
    		and   !SelectAll)
    	
    	IsBookDetailsNotExist
    		restricted
    		when (MassSelectionBookDetailsRel not exists
    		and   IsIntercompanyTransfer)
    	
    	IsLeaseClosedOrTerminated
    		restricted
    		when (AssetLease.Lease.Status.Closed
    		or    AssetLease.Lease.Status.Terminated)
    	
		HasToDetailsEqualToFromDetails
			restricted
			when (ToCompany = Company
			and   ToLocation = AssetLocation
			and   ToDivision = AssetDivision
			and   ToAssetType = AssetType
			and   ToAssetGroup = AssetGroup
			and   ToLocationDetail = LocationDetail
			and   ToAssetAccountGroup = AssetAccountGroup
			and   ToAssetAccountingUnitGroup = AssetAccountingUnitGroup
			and   ToDistributionAccount = DistributionAccount
			and	  ToAssetOwner = AssetOwner)

		FromDetailsChanged
			restricted
			when (Company changed
			or  AssetLocation changed
			or  AssetDivision changed
			or  AssetType changed
			or  AssetGroup changed
			or  LocationDetail changed
			or  AssetAccountGroup changed
			or  AssetAccountingUnitGroup changed
			or  DistributionAccount changed
	   		or 	AssetProject changed
	        or  AssetLease changed
			or  AssetCategory changed
			or  Simulated changed)

		IsValidForSummaryGroupTransfer  
			restricted
			when (ErrorFlag = 0)

		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

						
	Relations
		SummaryGroupAssetsRel	
			one-to-many relation to Asset
			Field Mapping uses symbolic key 
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.AssetSummaryGroup = LocalAssetSummaryGroup)		
	
		LocalMassSelectionDetailRel
			one-to-many relation to MassSelectionDetail
			Field Mapping uses symbolic key 
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection 
				where (related.IncludeOrExcludeAsset.Include
				and	related.Asset.AssetSummaryGroup	= LocalAssetSummaryGroup)
					
		MassSelectionBookDetailsRel is a MassSelectionBookDetail set

		MassSelectionDetailsRel is a MassSelectionDetail set
        
        MassSelectionDetailsExcludeRel is a MassSelectionDetail set
        	Instance Selection
        		where (related.IncludeOrExcludeAsset.Exclude)
        		
        AssetLeaseRel
        	one-to-many relation to Asset
        	Field Mapping uses Set13
 				related.FinanceEnterpriseGroup 		  	= FinanceEnterpriseGroup
        		related.AssetLease.LeaseCompany 	  	= AssetLease.LeaseCompany
        		related.AssetLease.Lease			  	= AssetLease.Lease
				
		AssetsForAdjustmentRel
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup 	      	= FinanceEnterpriseGroup
				related.Company 				      	= Company
			Instance Selection
				where ((AssetLease not entered
				or 	    related.AssetLease			  	= AssetLease)							
				and    (AssetLocation not entered
				or 	    related.AssetLocation		  	= AssetLocation)
				and    (AssetDivision not entered
				or      related.AssetDivision		  	= AssetDivision)
				and    (AssetGroup not entered
				or      related.AssetGroup   			= AssetGroup) 
				and    (AssetType not entered
				or	    related.AssetType 				= AssetType)	
				and    (AssetAccountGroup not entered
				or		related.AssetAccountGroup	  	= AssetAccountGroup)
				and    (DistributionAccount not entered
				or		related.AssetAccountingUnit		= DistributionAccount)
				and    (AssetAccountingUnitGroup not entered
				or		related.AssetAccountingUnitGroup = AssetAccountingUnitGroup)
				and   ((related.Status.Unreleased				
				and     related.WorkInProcess		  	= true)
				or	    related.Status.Released)
				and     related.Simulated 				= Simulated)
		
		AssetsForDisposalRel
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup 		 = FinanceEnterpriseGroup
				related.Company 				 	 = Company
			Instance Selection
				where ((AssetLease not entered
				or 	   (related.AssetLease			  	= AssetLease))							
				and    (AssetLocation not entered
				or 	   (related.AssetLocation		  	= AssetLocation))
				and    (AssetDivision not entered
				or     (related.AssetDivision		  	= AssetDivision))
				and    (AssetGroup not entered
				or     (related.AssetGroup   			= AssetGroup)) 
				and    (AssetType not entered
				or	   (related.AssetType 				= AssetType))	
				and    (AssetAccountGroup not entered
				or	   (related.AssetAccountGroup	  	= AssetAccountGroup))
				and    (DistributionAccount not entered
				or	   (related.AssetAccountingUnit		= DistributionAccount))
				and    (AssetAccountingUnitGroup not entered
				or	   (related.AssetAccountingUnitGroup = AssetAccountingUnitGroup))				
				and    (related.Status.Released))	
		
		AssetsForTransferRel	
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup		 = FinanceEnterpriseGroup
				related.Company 				 	 = Company
			Instance Selection
				where ((AssetLease not entered
				or 		related.AssetLease			  	= AssetLease)							
				and    (AssetLocation not entered
				or 		related.AssetLocation		  	= AssetLocation)
				and    (AssetDivision not entered
				or      related.AssetDivision		  	= AssetDivision)
				and    (AssetGroup not entered
				or      related.AssetGroup   			= AssetGroup) 
				and    (AssetType not entered
				or	    related.AssetType 				= AssetType)	
				and    (AssetAccountGroup not entered
				or		related.AssetAccountGroup	  	= AssetAccountGroup)
				and    (DistributionAccount not entered
				or		related.AssetAccountingUnit		= DistributionAccount)
				and    (AssetAccountingUnitGroup not entered
				or		related.AssetAccountingUnitGroup = AssetAccountingUnitGroup)
				and    (AssetOwner not entered
				or		related.AssetOwner 				= AssetOwner)		
				and      related.Status.Released)					

		AssetsForRevalueRel
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.Company 				 	= Company
			Instance Selection
				where ((related.AllowRevaluation = true)
				and    (AssetType not entered
				or	    related.AssetType		 		= AssetType)					
				and    (AssetLocation not entered
				or 		related.AssetLocation		  	= AssetLocation)
				and    (AssetDivision not entered
				or      related.AssetDivision		  	= AssetDivision)
				and    (AssetGroup not entered
				or      related.AssetGroup   			= AssetGroup) 
				and    (AssetAccountGroup not entered
				or		related.AssetAccountGroup	  	= AssetAccountGroup)
				and    (DistributionAccount not entered
				or		related.AssetAccountingUnit		= DistributionAccount)
				and    (AssetAccountingUnitGroup not entered
				or		related.AssetAccountingUnitGroup = AssetAccountingUnitGroup)			
				and   ((related.Status.Unreleased				
				and     related.WorkInProcess		= true)
				or	    related.Status.Released)
				and		related.HasNonZeroBasisBooks)	
				
		AssetExcludeRel
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.Company 				 	= Company
			Instance Selection
				where ((related.AssetType.Type			= AssetType.Type
				and	    related.AssetType.SubType 		= AssetType.SubType)					
				and    (AssetLocation not entered
				or 		related.AssetLocation		  	= AssetLocation)
				and    (AssetDivision not entered
				or      related.AssetDivision		  	= AssetDivision)
				and    (AssetGroup not entered
				or      related.AssetGroup   			= AssetGroup) 
				and    (AssetAccountGroup not entered
				or		related.AssetAccountGroup	  	= AssetAccountGroup)
				and    (DistributionAccount not entered
				or		related.AssetAccountingUnit		= DistributionAccount)
				and    (AssetAccountingUnitGroup not entered
				or		related.AssetAccountingUnitGroup = AssetAccountingUnitGroup)			
				and   ((related.Status.Unreleased				
				and     related.WorkInProcess		= true)
				or	    related.Status.Released)
				and		(!related.IsExcludedFromSelectAll))

		GeneralLedgerCompanyRel
       		one-to-one relation to GeneralLedgerCompany
       		Field Mapping uses symbolic key
       			related.Company					= Company
				
											
							    
    Sets

        Set2
            indexed
            Instance Selection
                where (IsReleased)
            Sort Order
            	FinanceEnterpriseGroup
                Company
                MassSelectionType
                MassSelection
	
	Rule Blocks
		CreateAssetGroup
			if (ToAssetGroup entered
			and ToAssetGroup not exists)
				invoke Create AssetGroup
					invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.AssetGroup				= ToAssetGroup
					invoked.Description				= ToAssetGroup				
	
	Field Rules
		Company
			required
				
			if (IsRevalue)
				constraint (Company.Revalue)
					"CompanyRevalueShouldBeSetForRevaluationOfAssets"			
		
		ToCompany
			if (IsTransfer)
				required	
				constraint (Company.BalanceTransfers = ToCompany.BalanceTransfers)
					"TheFromAndToCompanyMustHaveTheSameTransferBalancingOptionSelected"			
		AssetLease
			if (IsAdjustmentTransferOrDisposal)
				constraint (AssetLeaseRel exists)
					"AssetLeaseDoesNotExistInAsset"				
				
				if (IsDisposal)
					constraint (!IsLeaseClosedOrTerminated)
						"LeaseShouldNotBeClosedOrTerminated"		
					
		AssetType
			if (IsRevalue)
				required 
					"AssetTypeIsRequiredForRevalue"				
				constraint (AssetType.Revalue)
					"AssetTypeDoesNotAllowRevalue"				
		
		ToDistributionAccount
			if (IsTransfer)
				if (DistributionAccount entered)
					required

		
		SelectAll
			initial value is true
		
	Actions
		Create is a Create Action
			Action Rules
				if (IsTransfer)
					constraint (!HasToDetailsEqualToFromDetails)			
						"AtLeastOneFromOrToDetailsShouldBeDifferent"				
					include CreateAssetGroup
							
		Update is an Update Action
			Action Rules
				if (SelectAll changed)
					if (IsDetailExists)
						confirmation required
							"DetailsWillBeDeleted;Proceed?"
						invoke Delete MassSelectionDetailsRel
				if (FromDetailsChanged)
					constraint (!IsDetailExists)				
						"CannotChangeSelectDetails;MustFirstRemoveSelectedAssets"					
				
				constraint (!Released)							
					"CannotChange;BatchAlreadyBeenUpdated"					
					
				if (IsTransfer)
					constraint (!HasToDetailsEqualToFromDetails)
						"AtLeastOneFromOrToDetailsShouldBeDifferent"									
					if (ToCompany changed)
						invoke Delete MassSelectionBookDetailsRel
					
					include CreateAssetGroup
					
		Delete is a Delete Action
							
