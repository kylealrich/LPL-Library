LedgerHierarchy is a BusinessClass
	owned by GeneralLedger
	
	prefix is GLLHy
	
	Ontology
		part of Ledger
			relative key is SubordinateLedger is a Ledger
			
	Patterns
		implements BODId
		
	Context Fields
		ReportingBasis
#ifdef module integration
		FSMInboundBODTracker
#endif


		
	Persistent Fields
		TopNode					is AlphaUpper 20

	Derived Fields
		DerivedAccountingEntityTopNode	is a DerivedField
			type is AlphaUpper 12
			restricted
			return ReportingBasisRel.AccountingEntityTopNode				



	

#ifdef module integration
		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
	
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (ActionCode = "C" or (BODVariationID < 1) or ActionCode.Create)
				return "Add"
			else
			if (ActionCode = "U")
				return "Replace"
			else
			if (ActionCode = "D")
				return "Replace"
				
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (ActionCode = "C" or ActionCode = "U" )
				return "Open"
			else
			if(ActionCode = "D")
				return "Deleted"		
		
		DerivedSubordinateLedgerName is a DerivedField
			type is Alpha 60
			restricted
			return SubordinateLedger.Ledger
					
		DerivedBODDocumentID is a DerivedField
			type is Alpha 60
			restricted
			return FinanceEnterpriseGroup + DerivedDelimiter + ReportingBasis + DerivedDelimiter + DerivedSubordinateLedgerName	
			
		DerivedReportLedger is a DerivedField
			type is Alpha 40
			restricted
			return ReportingBasis + DerivedDelimiter + DerivedSubordinateLedgerName
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
			
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return 	"Infor-nid:" + DerivedTenantID + ":" + ":"  + ":" + DerivedBODDocumentID + ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
		
		DerivedReportingBasisDescripion is a DerivedField
			type is Alpha 60
			restricted
			return ReportingBasisRel.ReportingBasis.Description
			
		DerivedSubordinateLedgerDescription is a DerivedField
			type is Alpha 60
			restricted
			return SubordinateLedger.Description	
			
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
		
		ReportingBasisXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	        ReportingBasisXMLBOD = template.IONSyncCodeDefination_LedgerHierarchy_ST document for this instance
	        
#endif	

	Local Fields
		LocalAccountingEntity			is a AccountingEntity
		LocalDeleteLedgerPeriod			is Boolean
		LocalCloseLedger				is a Ledger
		LocalKeyFromValue				is Alpha 200
		LocalKeyToValue					is Alpha 200
    	LocalEntityTopNode				is like AccountingEntity
    	LocalLedgerTopNode				is like Ledger
    	LocalReportingBasis				is a ReportingBasis
    	ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		LocalNativeLPLBODTrigger		is Boolean		
#ifdef module integration
		NewBODTracker  					is a FSMInboundBODTracker view
#endif
		LocalFSMInboundBODTracker		is Numeric 15
		Error            				is Boolean
		ErrorMessage     				is Alpha 300
		LocalConfigurationParameter		is Alpha size up to 200
		LocalBackgroundGroup			is Alpha 200
    Conditions
    	ValidForCurrencyLedger
    		restricted
    		when (SubordinateLedger.ForCurrencyLedger)
    	NotACurrencyLedger
    		restricted
    		when (!SubordinateLedger.CurrencyLedger)
    	AllCurrencyLedger
    		restricted
    		when (SubordinateLedger.CurrencyLedger)

	Relations
		ReportingBasisRel
			one-to-one relation to ReportingBasis
			Field Mapping uses ByLedgerTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.LedgerTopNode			= TopNode
				related.AccountAnalysisString	= blank
		ReportingBasisAccountingEntityRel
			one-to-many relation to AccountingEntityHierarchy
			Field Mapping uses SubordinateInTopNode
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TopNode					= DerivedAccountingEntityTopNode
		LedgerInOtherReportingBasisRel
			one-to-many relation to LedgerHierarchy
			Field Mapping uses ByTopNodeInSubordinate
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.SubordinateLedger		= SubordinateLedger
			Instance Selection
				where (related.TopNode			= LocalLedgerTopNode)
		EntityInOtherReportingBasisRel
			one-to-many relation to AccountingEntityHierarchy
			Field Mapping uses ByTopNodeInSubordinate
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.SubordinateAccountingEntity	= LocalAccountingEntity
			Instance Selection
				where (related.TopNode				!= LocalEntityTopNode)
		EntityLedgerPeriodRel
			one-to-many relation to EntityLedgerPeriod
			Field Mapping uses ByEntityLedger
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AccountingEntity			= LocalAccountingEntity
				related.Ledger						= SubordinateLedger
		CodeBlockRelationRel
			one-to-one relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.FromField				= 0
				related.ToField					= 1
		CodeBlockRelationDetailRel
			one-to-one relation to CodeBlockRelationDetail
			Field Mapping uses ByKeyValues
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.CodeBlockRelation		= CodeBlockRelationRel.CodeBlockRelation
				related.KeyFromValue			= LocalKeyFromValue
				related.KeyToValue				= LocalKeyToValue
		BudgetEntityLedgerRel
			one-to-one relation to BudgetEntityLedger
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup    
				related.AccountingEntity		= LocalAccountingEntity    
				related.Ledger					= SubordinateLedger    
		EntityInOtherBudgetReportingBasisRel
			one-to-many relation to AccountingEntityHierarchy
			Field Mapping uses ByTopNodeInSubordinate
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.SubordinateAccountingEntity	= LocalAccountingEntity
			Instance Selection
				where (related.TopNode				!= LocalEntityTopNode
				and    related.ReportingBasisRel.UseForBudgetEdit)

#ifdef module integration
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMInboundBODTrackerRel
			one-to-one relation to FSMInboundBODTracker
			Field Mapping uses symbolic key
       			related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
#endif

	Sets
		ByParent
			Sort Order
				FinanceEnterpriseGroup
				Ledger
				SubordinateLedger
		BySubordinate
			Sort Order
				FinanceEnterpriseGroup
				SubordinateLedger				
				Ledger
		SubordinatesByTopNode
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				SubordinateLedger
				Ledger
		SubordinateInTopNode
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				SubordinateLedger				
    	ByTopNodeInSubordinate
    		Sort Order
				FinanceEnterpriseGroup
    			SubordinateLedger
    			TopNode

	Rule Blocks
		DeleteRelatedData
			for each ReportingBasisAccountingEntityRel
				LocalDeleteLedgerPeriod		= true
				LocalAccountingEntity		= each.SubordinateAccountingEntity
				LocalEntityTopNode			= each.TopNode
				LocalBackgroundGroup = FinanceEnterpriseGroup + Ledger + "DeleteRelatedRecords"
				if (EntityInOtherReportingBasisRel exists)
					LocalDeleteLedgerPeriod		= false
					for each EntityInOtherReportingBasisRel
						LocalLedgerTopNode = each.DerivedLedgerTopNode
						if (LedgerInOtherReportingBasisRel exists)
							end for each
					if (!LedgerInOtherReportingBasisRel exists) 
						if (BudgetEntityLedgerRel exists)
							invoke PurgeInBackground BudgetEntityLedger in background group (LocalBackgroundGroup)
								invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity			= LocalAccountingEntity    
								invoked.PrmLedger					= SubordinateLedger    
						if (EntityLedgerPeriodRel exists)
							invoke DeleteAllSetLedgerPeriod EntityLedgerPeriod in background group (LocalBackgroundGroup)
								invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.PrmAccountingEntity			= LocalAccountingEntity
								invoked.PrmLedger 					= SubordinateLedger
						LocalKeyFromValue		= SubordinateLedger
						LocalKeyToValue			= LocalAccountingEntity
						if (CodeBlockRelationDetailRel exists)
							invoke SystemDelete CodeBlockRelationDetailRel
				if (LocalDeleteLedgerPeriod)
					if (BudgetEntityLedgerRel exists)
						invoke PurgeInBackground BudgetEntityLedger in background group (LocalBackgroundGroup)
							invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.PrmAccountingEntity			= LocalAccountingEntity    
							invoked.PrmLedger					= SubordinateLedger    
					if (EntityLedgerPeriodRel exists)
						invoke DeleteAllSetLedgerPeriod EntityLedgerPeriod in background group (LocalBackgroundGroup)
							invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
							invoked.PrmAccountingEntity				= LocalAccountingEntity
							invoked.PrmLedger 						= SubordinateLedger
					LocalKeyFromValue			= SubordinateLedger
					LocalKeyToValue				= LocalAccountingEntity
					if (CodeBlockRelationDetailRel exists)
						invoke SystemDelete CodeBlockRelationDetailRel
			
		TriggerReportingBasisLedgerServiceRules
			trigger "ReportingBasisLedgerService" PA service
				resume on error
				title is "EG:<FinanceEnterpriseGroup>RB:<ReportingBasis>"
				Criteria
					FinanceEnterpriseGroup
					ReportingBasis
				Variables
					ActionCode
					include persistent fields from FinanceEnterpriseGroup
					include persistent fields from ReportingBasisRel.ReportingBasis
					include persistent fields from SubordinateLedger
					bod id.VariationID
						variable name is VariationID
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp	
		
	Actions
		Create is a Create Action
			restricted
			Exit Rules
				LocalReportingBasis = ReportingBasis
				invoke BuildLedgerBusClassGroup LocalReportingBasis

				for each ReportingBasisAccountingEntityRel
					LocalCloseLedger			= LocalReportingBasis.DerivedCloseLedger
					LocalAccountingEntity		= each.SubordinateAccountingEntity
					if (SubordinateLedger		!= LocalCloseLedger)
						if  (LocalReportingBasis.UseForBudgetEdit
						and !BudgetEntityLedgerRel exists)
							invoke Create BudgetEntityLedger
								invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.AccountingEntity		= LocalAccountingEntity
								invoked.Ledger					= SubordinateLedger
						invoke CreateEntityLedgerPeriod AccountingEntityPeriod in background 	
							invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.PrmAccountingEntity	= LocalAccountingEntity
							invoked.PrmLedger			= SubordinateLedger
						LocalKeyFromValue		= SubordinateLedger
						LocalKeyToValue			= LocalAccountingEntity
						if (!CodeBlockRelationDetailRel exists)
							invoke SystemCreate CodeBlockRelationDetailRel
								resume on error
								invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.CodeBlockRelation		= CodeBlockRelationRel.CodeBlockRelation
								invoked.Ledger					= SubordinateLedger
								invoked.AccountingEntity		= LocalAccountingEntity
				if (LocalReportingBasis.UseForBudgetEdit)
					invoke CreateBudgetEntityLedger
				invoke TriggerReportingBasisLedger

		Update is an Update Action
			restricted	

		Delete is a Delete Action
			restricted
			Entrance Rules
				if (ReportingBasisRel.UseForBudgetEdit)
					invoke DeleteBudgetEntityLedger
				include DeleteRelatedData
				if (FinanceEnterpriseGroup.BODTrigger)
					ActionCode = ActionCode.Delete
					increment bod id.VariationID
					include TriggerReportingBasisLedgerServiceRules



			Exit Rules
				invoke BuildLedgerBusClassGroup ReportingBasisRel

		DeleteInBackground is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup 		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmLedger							is a Ledger
					default label is "DerivedTopNode"
				PrmSubordinateLedger				is a Ledger
					default label is "LocalLedger"
			Instance Selection
				where  (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and 	Ledger = PrmLedger
				and 	SubordinateLedger = PrmSubordinateLedger)
			Action Rules
				Instance Rules
					invoke Delete
		
		Purge is a Purge Action
			restricted
			Action Rules
				if (ReportingBasisRel.UseForBudgetEdit)
					invoke DeleteBudgetEntityLedger
				include DeleteRelatedData
			Exit Rules
				invoke BuildLedgerBusClassGroup ReportingBasisRel

		CreateBudgetEntityLedger is an Instance Action
			restricted
			Action Rules
				for each ReportingBasisAccountingEntityRel
					LocalCloseLedger			= ReportingBasisRel.ReportingBasis.DerivedCloseLedger
					LocalAccountingEntity		= each.SubordinateAccountingEntity
					if (SubordinateLedger		!= LocalCloseLedger)
						if  (ReportingBasisRel.UseForBudgetEdit
						and !BudgetEntityLedgerRel exists)
							invoke Create BudgetEntityLedger
								invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.AccountingEntity		= LocalAccountingEntity
								invoked.Ledger					= SubordinateLedger
#ifdef module integration

		SendReportingBasisLedgerBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ReportingBasisXMLBOD
					bod type is "Sync.CodeDefinition"
					document id is DerivedBODDocumentID 
					variation id is	BODVariationID

		TriggerReportingBasisLedgerNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "LedgerHierarchy"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_LedgerHierarchy_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				if(FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)	
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= ReportingBasis 
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= TopNode
							invoked.Reference2				= "LedgerHierarchy"
							invoked.Reference3				=SubordinateLedger
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.BODDocumentID			= ReportingBasis
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.Reference1				= TopNode
							invoked.Reference2				= "LedgerHierarchy"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendReportingBasisLedgerBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODFormattedId
							invoked.BODXML								= ReportingBasisXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODFormattedId
							invoked.BODXML									= ReportingBasisXMLBOD
#endif

		DeleteBudgetEntityLedger is an Instance Action
			restricted
			Action Rules
				for each ReportingBasisAccountingEntityRel
					LocalDeleteLedgerPeriod		= true
					LocalAccountingEntity		= each.SubordinateAccountingEntity
					LocalEntityTopNode			= each.TopNode
					if (EntityInOtherBudgetReportingBasisRel exists)
						LocalDeleteLedgerPeriod		= false
						for each EntityInOtherReportingBasisRel
							LocalLedgerTopNode = each.DerivedLedgerTopNode
							if (LedgerInOtherReportingBasisRel exists)
								end for each
						if (!LedgerInOtherReportingBasisRel exists) 
							invoke Purge BudgetEntityLedgerRel
					if (LocalDeleteLedgerPeriod)
						invoke Purge BudgetEntityLedgerRel

		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord		
		

		TriggerReportingBasisLedger is an Instance Action
        	restricted
            Action Rules
				if (FinanceEnterpriseGroup.BODTrigger)
					if (!action type.Delete)
						if (action != "UpdateBODIdFields") 
							if (bod id.VariationID < 1)
 								ActionCode      = ActionCode.Create   
                    		else
                        		ActionCode      = ActionCode.Update 
							increment bod id.VariationID
							include TriggerReportingBasisLedgerServiceRules



		BODDataInitialLoadReportingBasisLedgerCriteria1 is a Set Action
			restricted
			Parameters
				PrmFEG							is a FinanceEnterpriseGroup
				PrmReportingBasisLedgerFrom		is a Ledger
				PrmReportingBasisLedgerTo		is a Ledger				
			
			Parameter Rules
			
			Instance Selection
				where  (((PrmFEG entered
				and		  PrmFEG = FinanceEnterpriseGroup)
				or		  PrmFEG not entered)	
				and		((PrmReportingBasisLedgerFrom entered
				and		  PrmReportingBasisLedgerFrom <= SubordinateLedger)
				or		  PrmReportingBasisLedgerFrom not entered)				
				and    ((PrmReportingBasisLedgerTo entered
				and		 PrmReportingBasisLedgerTo >= SubordinateLedger)
				or		 PrmReportingBasisLedgerTo not entered))
				
			Sort Order
				Ledger
			Action Rules	
				Ledger Set Rules
					Entrance Rules
					
					Exit Rules				
						
				Instance Rules
					if(PrmFEG entered or actor.context.FinanceEnterpriseGroup = FinanceEnterpriseGroup)	
						if (FinanceEnterpriseGroup.BODTrigger)
								ActionCode = ActionCode.Create
								increment bod id.VariationID
								include TriggerReportingBasisLedgerServiceRules



