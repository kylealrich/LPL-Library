DefaultIDMTemplate is a BusinessClass
	owned by idm
	
	prefix is DITM
   	disable data area copy
    	preserve target data
    		
	Ontology
		symbolic key is DefaultIDMTemplate
	
	Patterns
	
	Persistent Fields
		Name
		Description	
		DocumentType				  is Numeric 3
			States
				US1099Form  				value is 1
				US1099XMLCopyA  			value is 2
				US1099XMLCopyBOrC 			value is 3
		TemplateType				  			  is Numeric 3
			States
				None					    	value is 0
				US1099DIVCopyAOriginalForm		value is 1		
				US1099DIVCopyBIndividualForm	value is 2	
				US1099DIVCopyCIndividualForm	value is 3	
						
				US1099GCopyAOriginalForm		value is 4			
				US1099GCopyBIndividualForm		value is 5		
				US1099GCopyCIndividualForm		value is 6		
				
				US1099INTCopyAOriginalForm		value is 7			
				US1099INTCopyBIndividualForm	value is 8		
				US1099INTCopyCIndividualForm	value is 9			
				
				US1099KCopyAOriginalForm		value is 10		
				US1099KCopyBIndividualForm		value is 11
				US1099KCopyCIndividualForm		value is 12
				
				US1099MISCCopyAOriginalForm		value is 13			
				US1099MISCCopyBIndividualForm	value is 14
				US1099MISCCopyCIndividualForm  	value is 15		
				
				US1099NECCopyAOriginalForm		value is 16
				US1099NECCopyBIndividualForm	value is 17
				US1099NECCopyCIndividualForm	value is 18
				
				US1099XML						value is 19
				
				US1099DIVCopyBConsolidatedForm	value is 20
				US1099GCopyBConsolidatedForm	value is 21	
				US1099INTCopyBConsolidatedForm	value is 22	
				US1099KCopyBConsolidatedForm	value is 23
				US1099MISCCopyBConsolidatedForm	value is 24
				US1099NECCopyBConsolidatedForm	value is 25
				
							
		Attachment
		Instructions              	  is Text
		Year
		Version						  is Decimal 3.2		
	
	Field Rules
		Description
			required
		DocumentType
			required
		TemplateType
			if (DocumentType.US1099Form)
				constraint (1099FormType)
					"InvalidTemplateType"
				required
		Attachment
			required
		Year
			required
		Version
			required

		Name
			initial value is "****_DIT"
			if (DocumentType.US1099Form)
				if (TemplateType.US1099DIVCopyAOriginalForm)
					force default to "US1099-DIV_COPY_A_ORIG_DIT"
				else
				if (TemplateType.US1099DIVCopyBIndividualForm)
					force default to "US1099-DIV_COPY_B_LASER_DIT"
				else
				if (TemplateType.US1099DIVCopyCIndividualForm)
					force default to "US1099-DIV_COPY_C_LASER_DIT"
				else
				if (TemplateType.US1099GCopyAOriginalForm)
					force default to "US1099-G_COPY_A_ORIG_DIT"
				else
				if (TemplateType.US1099GCopyBIndividualForm)
					force default to "US1099-G_COPY_B_LASER_DIT"
				else
				if (TemplateType.US1099GCopyCIndividualForm)
					force default to "US1099-G_COPY_C_LASER_DIT"
				else
				if (TemplateType.US1099INTCopyAOriginalForm)
					force default to "US1099-INT_COPY_A_ORIG_DIT"
				else
				if (TemplateType.US1099INTCopyBIndividualForm)
					force default to "US1099-INT_COPY_B_LASER_DIT"
				else
				if (TemplateType.US1099INTCopyCIndividualForm)
					force default to "US1099-INT_COPY_C_LASER_DIT"
				else
				if (TemplateType.US1099KCopyAOriginalForm)
					force default to "US1099-K_COPY_A_ORIG_DIT"
				else
				if (TemplateType.US1099KCopyBIndividualForm)
					force default to "US1099-K_COPY_B_LASER_DIT"
				else
				if (TemplateType.US1099KCopyCIndividualForm)
					force default to "US1099-K_COPY_C_LASER_DIT"
				else
				if (TemplateType.US1099MISCCopyAOriginalForm)
					force default to "US1099-MISC_COPY_A_ORIG_DIT"
				else
				if (TemplateType.US1099MISCCopyBIndividualForm)
					force default to "US1099-MISC_COPY_B_LASER_DIT"
				else
				if (TemplateType.US1099MISCCopyCIndividualForm)
					force default to "US1099-MISC_COPY_C_LASER_DIT"	
				else
				if (TemplateType.US1099NECCopyAOriginalForm)
					force default to "US1099-NEC_COPY_A_ORIG_DIT"
				else
				if (TemplateType.US1099NECCopyBIndividualForm)
					force default to "US1099-NEC_COPY_B_LASER_DIT"
				else
				if (TemplateType.US1099NECCopyCIndividualForm)
					force default to "US1099-NEC_COPY_C_LASER_DIT"
				else
				if (TemplateType.US1099DIVCopyBConsolidatedForm)
					force default to "US1099-DIV_COPY_B_CONSOLIDATED_DIT"
				else
				if (TemplateType.US1099GCopyBConsolidatedForm)
					force default to "US1099-G_COPY_B_CONSOLIDATED_DIT"
				else
				if (TemplateType.US1099INTCopyBConsolidatedForm)
					force default to "US1099-INT_COPY_B_CONSOLIDATED_DIT"
				else
				if (TemplateType.US1099KCopyBConsolidatedForm)
					force default to "US1099-K_COPY_B_CONSOLIDATED_DIT"
				else
				if (TemplateType.US1099MISCCopyBConsolidatedForm)
					force default to "US1099-MISC_COPY_B_CONSOLIDATED_DIT"
				else				
				if (TemplateType.US1099NECCopyBConsolidatedForm)
					force default to "US1099-NEC_COPY_B_CONSOLIDATED_DIT"
												
			constraint (com.lawson.rdtech.type.Field.endsWith(Name, "_DIT"))
                "DefaultIDMTemplateNameMustHaveDITSuffix"		

			required
			

	Local Fields
		LocalIDMTemplateYear 		is Numeric size 4
		LocalFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
		LocalVendorGroup			is a VendorGroup

	Derived Fields
		DerivedYearAndVersion is a StringField
			type is AlphaUpper 10
			restricted
			Year
			"_"
			"v"
			Version

		DerivedIDMDocumentType is a DerivedField
			type is Numeric size 2
			restricted
			if (TemplateType.US1099DIVCopyAOriginalForm)
				return 28 
			else
			if (TemplateType.US1099DIVCopyBIndividualForm)
				return 29 
			else
			if (TemplateType.US1099DIVCopyCIndividualForm)
				return 30 
			else
			if (TemplateType.US1099GCopyAOriginalForm)
				return 31 
			else
			if (TemplateType.US1099GCopyBIndividualForm)
				return 32 
			else
			if (TemplateType.US1099GCopyCIndividualForm)
				return 33 
			else
			if (TemplateType.US1099INTCopyAOriginalForm)
				return 34 
			else
			if (TemplateType.US1099INTCopyBIndividualForm)
				return 35 
			else
			if (TemplateType.US1099INTCopyCIndividualForm)
				return 36 
			else
			if (TemplateType.US1099KCopyAOriginalForm)
				return 37 
			else
			if (TemplateType.US1099KCopyBIndividualForm)
				return 38 
			else
			if (TemplateType.US1099KCopyCIndividualForm)
				return 39 
			else
			if (TemplateType.US1099MISCCopyAOriginalForm)
				return 40 
			else
			if (TemplateType.US1099MISCCopyBIndividualForm)
				return 41 
			else
			if (TemplateType.US1099MISCCopyCIndividualForm)
				return 42 
			else
			if (TemplateType.US1099NECCopyAOriginalForm)
				return 43 
			else
			if (TemplateType.US1099NECCopyBIndividualForm)
				return 44 
			else
			if (TemplateType.US1099NECCopyCIndividualForm)
				return 45 
			else
			if (TemplateType.US1099DIVCopyBConsolidatedForm)
				return 51 
			else
			if (TemplateType.US1099GCopyBConsolidatedForm)
				return 52 
			else
			if (TemplateType.US1099INTCopyBConsolidatedForm)
				return 53 
			else
			if (TemplateType.US1099KCopyBConsolidatedForm)
				return 54 
			else
			if (TemplateType.US1099MISCCopyBConsolidatedForm)
				return 55 
			else
			if (TemplateType.US1099NECCopyBConsolidatedForm)
				return 56 

		US1099IDMTemplateUploadBackgroundGroup is a MessageField
			restricted
			"UploadingUS1099IDMTemplatesForVendorGroup<LocalVendorGroup>"

	Conditions
		1099FormType
			restricted
			when (TemplateType.US1099DIVCopyAOriginalForm
				or TemplateType.US1099DIVCopyBIndividualForm
				or TemplateType.US1099DIVCopyBConsolidatedForm
				or TemplateType.US1099DIVCopyCIndividualForm
				or TemplateType.US1099GCopyAOriginalForm
				or TemplateType.US1099GCopyBIndividualForm
				or TemplateType.US1099GCopyBConsolidatedForm
				or TemplateType.US1099GCopyCIndividualForm
				or TemplateType.US1099INTCopyAOriginalForm
				or TemplateType.US1099INTCopyBIndividualForm
				or TemplateType.US1099INTCopyBConsolidatedForm
				or TemplateType.US1099INTCopyCIndividualForm
				or TemplateType.US1099KCopyAOriginalForm
				or TemplateType.US1099KCopyBIndividualForm
				or TemplateType.US1099KCopyBConsolidatedForm
				or TemplateType.US1099KCopyCIndividualForm
				or TemplateType.US1099MISCCopyAOriginalForm
				or TemplateType.US1099MISCCopyBIndividualForm
				or TemplateType.US1099MISCCopyBConsolidatedForm
				or TemplateType.US1099MISCCopyCIndividualForm
				or TemplateType.US1099NECCopyAOriginalForm
				or TemplateType.US1099NECCopyBIndividualForm
				or TemplateType.US1099NECCopyBConsolidatedForm
				or TemplateType.US1099NECCopyCIndividualForm)
			
		HasAttachment
			restricted
			when (Attachment entered)	

	Relations
		IDMTemplateRel
			one-to-many relation to IDMTemplate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = LocalFinanceEnterpriseGroup

			Instance Selection
				where (related.IDMDocumentType = DerivedIDMDocumentType
				and	   related.TemplateYear	   = LocalIDMTemplateYear)
			
   	Actions
    	Create is a Create Action

            	    			
		Update is an Update Action
    		
    		
    	Delete is a Delete Action

	
		UploadUS1099IDMTemplates is a Set Action					
			default label is untranslatable
			restricted
			Parameters
				ForceUpload					is Boolean
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmVendorGroup				is like VendorGroup
			
			Local Fields
				LocalAsyncId 				is an AsyncActionRequest

			Instance Selection
				where (1099FormType)

			Action Rules
				Instance Rules
					LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
					LocalVendorGroup = PrmVendorGroup
					LocalIDMTemplateYear = Year
					if (IDMTemplateRel not exists)
						if (LocalAsyncId entered)
							invoke IDMTemplateCreate in background group (US1099IDMTemplateUploadBackgroundGroup)
								run after LocalAsyncId
								assign async action request id to LocalAsyncId
								resume on error
								invoked.PrmFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup
						else
							invoke IDMTemplateCreate in background group (US1099IDMTemplateUploadBackgroundGroup)
								assign async action request id to LocalAsyncId
								resume on error
								invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
					else
					if (IDMTemplateRel.Description != DerivedYearAndVersion
					or  ForceUpload)			
						if (LocalAsyncId entered)
							invoke IDMTemplateUpdate in background group (US1099IDMTemplateUploadBackgroundGroup)
								run after LocalAsyncId
								assign async action request id to LocalAsyncId
								resume on error
								invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						else
							invoke IDMTemplateUpdate in background group (US1099IDMTemplateUploadBackgroundGroup)
								assign async action request id to LocalAsyncId
								resume on error
								invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
	
		IDMTemplateUpdate is an Instance Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				
			Action Rules
				LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				LocalIDMTemplateYear = Year
				if (IDMTemplateRel.TemplateExistsInIDM)
					invoke Update IDMTemplateRel
						resume on error
						invoked.Description					= DerivedYearAndVersion
						invoked.DocumentTemplate.Title		= Attachment.Title
						invoked.DocumentTemplate.File		= Attachment.File
						invoked.DocumentTemplate.MimeType	= Attachment.MimeType	
						invoked.TemplateYear				= Year
						invoked.IDMItem.OverrideUser		= true
				else
					invoke IDMUpload IDMTemplateRel
						resume on error
						invoked.Description					= DerivedYearAndVersion
						invoked.DocumentTemplate.Title		= Attachment.Title
						invoked.DocumentTemplate.File		= Attachment.File
						invoked.DocumentTemplate.MimeType	= Attachment.MimeType	
						invoked.TemplateYear				= Year
						invoked.IDMItem.OverrideUser		= true	

		IDMTemplateCreate is an Instance Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmVendorGroup				is like VendorGroup

			Local Fields
				LocalDefaultTemplateYear 	is Alpha size 4		

			Action Rules
				LocalDefaultTemplateYear				= Year
				invoke Create IDMTemplate
					resume on error
					invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
					invoked.Name 						= LocalDefaultTemplateYear + "_" + Name
					invoked.Description					= DerivedYearAndVersion
					invoked.Active						= true
					invoked.IDMDocumentType				= DerivedIDMDocumentType 
					invoked.DocumentTemplate.Title		= Attachment.Title
					invoked.DocumentTemplate.File		= Attachment.File
					invoked.DocumentTemplate.MimeType	= Attachment.MimeType					
					invoked.TemplateYear				= Year
					invoked.IDMItem.OverrideUser		= true


	Sets
		ByLatestVersion
			Sort Order
				DocumentType
				TemplateType
				Year				descending
				DefaultIDMTemplate  descending
				
