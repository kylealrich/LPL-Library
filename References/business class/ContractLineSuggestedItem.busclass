ContractLineSuggestedItem is a BusinessClass
	owned by po
	
	prefix is Consi
	
	Ontology
		symbolic key is ContractLineSuggestedItem
		
	Persistent Fields
		EnteredVendorItem				is a VendorItem
		EnteredManufacturer             is a Manufacturer
		EnteredManufacturerNumber       is a ManufacturerNumber
		SuggestedItem                   is an Item
		SuggestedVendorItem             is a VendorItem
		SuggestedManufacturer           is a Manufacturer
		SuggestedManufacturerNumber     is a ManufacturerNumber 
		MessageNumber                   is Numeric 3
		SuggestedItemResolution         is Numeric 1
			States
				KeepEnteredInformation  value is 1
				UseSuggestedItem        value is 2
					default label is "UseSuggestedLineChange"
				UseOtherSuggestedItem   value is 3
		Vendor
	
	Derived Fields
		
		DerivedContractLineItem is a DerivedField 
			type is AlphaUpper 32
			return ContractLine.ItemNumber

		DerivedManufacturerPartNumber is a DerivedField 
			type is AlphaUpper 50
			if (SuggestedManufacturerNumber entered)
				return SuggestedManufacturerNumber 
			else 
			if (EnteredManufacturerNumber entered)
				return EnteredManufacturerNumber

		DerivedManufacturerNumber is a DerivedField 
			type is like ManufacturerNumber 
			if (SuggestedItemResolution = 0)
				return EnteredManufacturerNumber 
			else
			if (SuggestedItemResolution = 2)
				return SuggestedManufacturerNumber 

		DerivedManufacturerCode is a DerivedField 
			type is like ManufacturerCode 
			if (SuggestedItemResolution = 0)
				return EnteredManufacturer.ManufacturerCode  
			else
			if (SuggestedItemResolution = 2)
				return SuggestedManufacturer.ManufacturerCode 

		DerivedManufacturerDivision is a DerivedField 
			type is like ManufacturerDivision 
			if (SuggestedItemResolution = 0)
				return EnteredManufacturer.ManufacturerDivision  
			else
			if (SuggestedItemResolution = 2)
				return SuggestedManufacturer.ManufacturerDivision 

		DerivedVendorItem is a DerivedField 
			type is like ManufacturerNumber 
			if (SuggestedItemResolution = 0)
				return EnteredVendorItem 
			else
			if (SuggestedItemResolution = 2)
				return SuggestedVendorItem  

		Message1 is a MessageField
			restricted
			"VendorItemAndItemFoundForEnteredManufacturerNumberBasedOnManufacturerAndManufacturerNumberOnVendorItem"
			
		Message2 is a MessageField
			restricted
			"ItemFoundForEnteredManufacturerNumberBasedOnManufacturerNumberOnItem"
			
		Message3 is a MessageField
			restricted
			"ItemFoundForEnteredVendorItem"
			
		Message4 is a MessageField
			restricted
			"ItemFoundForEnteredManufacturerNumberBasedOnManufacturerNumberOnVendorItemForADifferentVendor"
			
		Message5 is a MessageField
			restricted
			"VendorItemAndItemFoundForEnteredManufacturerNumberBasedOnManufacturerNumberOnlyOnVendorItem"
			
		Message6 is a MessageField 
			restricted 
			"ManufacturerInformationChangeSuggestedFromAttributeUpdate"

		Message7 is a MessageField 
			restricted 
			"VendorItemChangeSuggestedFromAttributeUpdate"	

		Message8 is a MessageField 
			restricted 
			"ItemFoundForSuggestedVendorItemFromAtttributeUpdate"	

		Message9 is a MessageField
			restricted
			"VendorItemAndItemFoundForSuggestedManufacturerOrManufacturerNumberBasedOnManufacturerAndManufacturerNumberOnVendorItemFromAttributeUpdate"			
		
		Message10 is a MessageField 
			restricted 
			"ManufacturerInformationAndVendorItemChangeSuggestedFromItemAttribute"

		Message11 is a MessageField 
			restricted 
			"ItemFoundForSuggestedManufacturerOrManufacturerNumberBasedOnManufacturerInformationOnItemFromAtttributeUpdate"	

		MessageDisplay is a ConditionalField
			type is Alpha 250
			if (MessageNumber = 1)
				Message1
			else
			if (MessageNumber = 2)	
				Message2
			else 
			if (MessageNumber = 3)
				Message3
			else
			if (MessageNumber = 4)
				Message4
			else
			if (MessageNumber = 5)	
				Message5
			else 
			if (MessageNumber = 6)
				Message6
			else 
			if (MessageNumber = 7)
				Message7
			else
			if (MessageNumber = 8)
				Message8 
			else 
			if (MessageNumber = 9)
				Message9
			else 
			if (MessageNumber = 10)
				Message10 
			else 
				Message11
	
	Conditions
	
		Resolved
			when (SuggestedItemResolution > 0)
	
		NotResolved
			when (SuggestedItemResolution = 0)

		ConsolidatedItemMasterExists 
			when (ConsolidatedItemMasterRel exists)
		
		NewItemConsolidatedItemMasterExists 
			when (ItemConsolidatedItemMasterRel exists)

		ContractLineHasErrors 
			when (ContractLine.ErrorsExist)	

		NotForVendorItem  
			restricted
			when (SuggestedVendorItem !entered)

		ForVendorItem  
			restricted
			when (SuggestedVendorItem entered)

		HasOtherLinesForManufacturer 
			restricted 
			when (NotForVendorItem
			and   OpenContractLinesRel exists)

		HasOtherLinesForVendorItem 
			restricted 
			when (ForVendorItem 
			and   OpenContractLinesForVendorItemRel exists)
		
		InactiveSuggestedVendorItem
			restricted 
			when (SuggestedVendorItem entered
			and   SuggestedVendorItem exists
			and  !SuggestedVendorItem.IsActive)
			
		VendorItemIsNew 
			restricted
			when (ContractLine.ItemNumber exists
			and   SuggestedVendorItem entered 
			and   !VendorItemExists)
		
		PotentialDuplicateVendorItem 
			restricted
			when ((VendorItemIsNew
			or     InactiveSuggestedVendorItem)
			and    DerivedManufacturerPartNumber entered
			and    POVendorItemBuyUOMDuplicateRel exists)
		
		DuplicateVendorItemNoManufacturerNumber
			restricted
			when ((VendorItemIsNew
			or     InactiveSuggestedVendorItem)
			and    EnteredVendorItemRel exists
			and    POVendorItemBuyUOMDuplicateRel !exists)

		MustDecideHowToHandleCurrentVendorItem
			restricted 
			when (PotentialDuplicateVendorItem
			or    DuplicateVendorItemNoManufacturerNumber)
		
		NotResolvedNoItem 
			when (NotResolved
			and   SuggestedItem !entered)
		
		FromAttributeUpdate 
			when (MessageNumber > 5)

		VendorItemExists 
			when (VendorItemRel exists)

		NotResolvedNewVendorItemForItemmast
			restricted 
			when (NotResolved
			and   MessageNumber = 7
			and   ContractLine.ItemType.Itemmast
			and   SuggestedVendorItem entered
			and   SuggestedManufacturer !entered
			and   SuggestedManufacturerNumber !entered
			and   SuggestedVendorItem !exists)
	Relations
		EnteredVendorItemRel 
			one-to-many relation to VendorItem 
			Field Mapping uses Set3
				related.ProcurementGroup 	= ContractGroup 
				related.Vendor              = Contract.Vendor 
				related.VendorItem			= EnteredVendorItem		
		
		ItemRel 
			one-to-one relation to Item	
			Field Mapping uses symbolic key 
				related.ItemGroup           = ContractGroup 
				related.Item                = SuggestedItem
		
		VendorItemRel 
			one-to-many relation to VendorItem 
			Field Mapping uses Set3
				related.ProcurementGroup 	= ContractGroup 
				related.Vendor              = Contract.Vendor 
				related.VendorItem			= SuggestedVendorItem
		
		
		ConsolidatedItemMasterRel
			one-to-many relation to ConsolidatedItemMaster 
			Field Mapping uses ByManufacturerInformation
				related.ContractGroup          = ContractGroup 
				related.Manufacturer           = EnteredManufacturer
				related.ManufacturerNumber     = EnteredManufacturerNumber

		ItemConsolidatedItemMasterRel
			one-to-many relation to ConsolidatedItemMaster   
			Field Mapping uses ByInternalItem 
				related.ContractGroup          = ContractGroup 
				related.InternalItem           = SuggestedItem 

		OpenContractLinesRel  
			one-to-many relation to ContractLine 
			Field Mapping uses ByMfgInfoVenItem
				related.ContractGroup 						= ContractGroup
				related.Manufacturer.ManufacturerCode		= DerivedManufacturerCode 
				related.Manufacturer.ManufacturerDivision   = DerivedManufacturerDivision
				related.ManufacturerNumber                  = DerivedManufacturerNumber 
			Instance Selection 
				where (related.LineNotClosed
				and    related.UniqueID != ContractLine.UniqueID)

		OpenContractLinesForVendorItemRel  
			one-to-many relation to ContractLine 
			Field Mapping uses ByVendorItemVendor 
				related.ContractGroup       				= ContractGroup
				related.VendorItem                          = DerivedVendorItem 
			Instance Selection 
				where (related.LineNotClosed
				and    related.UniqueID != ContractLine.UniqueID
				and    related.Vendor    = Vendor)

		OtherSuggestedItemRel 
			one-to-many relation to ContractLineSuggestedItem
			Field Mapping uses symbolic key
				related.ContractGroup	=	ContractGroup
  				related.Contract		=	Contract
  				related.ContractLine	= 	ContractLine
  			Instance Selection
  				where (related.UniqueID != UniqueID)

		OtherSuggestedItemForVendorItemRel 
			one-to-many relation to ContractLineSuggestedItem
			Field Mapping uses BySuggestedVendorItem
				related.ContractGroup			=	ContractGroup
  				related.SuggestedVendorItem		=	SuggestedVendorItem
  			Instance Selection
  				where (related.UniqueID != UniqueID
				and    related.Vendor    = Vendor)				

		OtherSuggestedItemForMfgNumberRel 
			one-to-many relation to ContractLineSuggestedItem
			Field Mapping uses BySuggestedVendorItem
				related.ContractGroup			=	ContractGroup
  			Instance Selection
  				where (related.UniqueID 		   			!= UniqueID
				and    related.Vendor                       = Vendor 
				and    related.EnteredVendorItem            = EnteredVendorItem
				and    related.SuggestedManufacturer        = SuggestedManufacturer
				and    related.SuggestedManufacturerNumber	= SuggestedManufacturerNumber)

		POVendorItemBuyUOMDuplicateRel
            one-to-many relation to VendorItem
            Field Mapping uses Set5
                related.ProcurementGroup    = ContractGroup
                related.Vendor              = Contract.Vendor
                related.Manufacturer        = EnteredManufacturer
                related.ManufacturerNumber  = DerivedManufacturerPartNumber
            Instance Selection
                where  (related.ManufacturerNumber entered
                and     related.Manufacturer entered
                and     related.VendorItem.Active
				and    (related.VendorBuyUOM !entered
				or      related.VendorBuyUOM = ContractLine.UOM.UnitOfMeasure))

	Sets
	
		BySuggestedVendorItem
			Sort Order
				ContractGroup
				SuggestedVendorItem
				Contract
				ContractLine
				ContractLineSuggestedItem
	
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		RecheckForSuggestedItems is an Instance Action
			valid when (NotResolved)
			Action Rules
				
				invoke CheckForSuggestedItems ContractLine
				
		KeepEnteredInformation is an Instance Action 
			valid when (NotResolved)

			Action Rules 
				SuggestedItemResolution = 1
		
		AcceptAllNonItemMasterChanges is a Set Action 
			default label is "AcceptAllChangesWithNoSuggestedItem"
			restricted 
			Parameters 
				ParmContractGroup is a ContractGroup 
					default label is "ContractGroup"

			Parameter Rules	
				ParmContractGroup 
					required 

			Instance Selection 
				where (ParmContractGroup = ContractGroup
				and    NotResolvedNoItem
				and   !ContractLine.SelectedForItemCreation)

			Action Rules 

				Instance Rules 
					invoke AcceptSuggestedItem

		AcceptSuggestedItem is an Instance Action
			valid when (NotResolved)
			default label is "AcceptSuggestion"		
			Parameters 
				ParmContractGroup is a ContractGroup 
					default label is "ContractGroup" 

			Parameter Rules 

				ParmContractGroup 
					initial value is ContractGroup 
					default to ContractGroup 
			
			Action Rules
				if (SuggestedItem entered)
					confirmation required
						"AcceptingSuggestedItemWillChangeSpecialItemOnContractLineToItemmastAndVendorItemWillBeUsedFromThisRecord;DoYouWantToContinue?"
				if (InactiveSuggestedVendorItem)
					confirmation required
						"SuggestedVendorItemExistsAndIsInactive;AcceptingSuggestionWillSetVendorItemToActive;DoYouWantToContinue?"
					
				constraint (!ContractLine.SelectedForItemCreation)
					"CannotAcceptSuggestedItem;ContractLineIsInAnotherItemCreationProcess"
					
				if (SuggestedVendorItem entered
				and SuggestedItem !entered)
						
					invoke FastUpdateWithoutEdits ContractLine 
						invoked.UpdateVendorItem               = true 
						invoked.NewVendorItem                  = SuggestedVendorItem
						if (SuggestedManufacturerNumber entered)
							invoked.NewManufacturerNumber      = SuggestedManufacturerNumber
						if (SuggestedManufacturer entered)
							invoked.NewManufacturer            = SuggestedManufacturer

					if (OtherSuggestedItemForVendorItemRel exists)
						for each OtherSuggestedItemForVendorItemRel

							invoke Update each 
								invoked.SuggestedItemResolution = 2										

				else 
				if ((SuggestedManufacturerNumber entered
				or   SuggestedManufacturer entered)
				and  SuggestedVendorItem !entered
				and  SuggestedItem !entered)
					invoke UpdateManufacturerInformation ContractLine 
						invoked.ParmManufacturerNumber          = SuggestedManufacturerNumber
						invoked.ParmManufacturer                = SuggestedManufacturer
						invoked.ParmUpdateAllLines              = true 

					if (OtherSuggestedItemForMfgNumberRel exists)
						for each OtherSuggestedItemForMfgNumberRel

							invoke Update each 
								invoked.SuggestedItemResolution = 2 

				else 
				if (SuggestedItem entered)
					invoke ChangeSpecialItemToItem ContractLine
						invoked.PrmNewItemNumber = SuggestedItem
						if (SuggestedVendorItem entered)
							invoked.PrmNewVendorItem 		= SuggestedVendorItem
							invoked.PrmVendor        		= Vendor 
						if (SuggestedManufacturer entered)
							invoked.PrmNewManufacturer      = SuggestedManufacturer
						if (SuggestedManufacturerNumber entered)
							invoked.PrmNewManufacturerNumber = SuggestedManufacturerNumber
						invoked.FromAcceptSuggestedItem 	= true 
				
				SuggestedItemResolution = 2 
				for each OtherSuggestedItemRel
					invoke Update each
						invoked.SuggestedItemResolution = 3 
	
			Exit Rules 

				if (SuggestedVendorItem entered)
					if (ConsolidatedItemMasterRel exists 
					and ItemConsolidatedItemMasterRel exists)
					 	if (first ConsolidatedItemMasterRel.ConsolidatedItemMaster != ItemConsolidatedItemMasterRel.ConsolidatedItemMaster)
							invoke UpdateAndConsolidate first ConsolidatedItemMasterRel
								invoked.TransientNewConsolidatedItemMaster      = ItemConsolidatedItemMasterRel.ConsolidatedItemMaster 
								invoked.TransientContract       				= Contract 
								invoked.TransientContractLine   				= ContractLine
								invoked.TransientNewItem    				    = SuggestedItem 	
								invoked.TransientFromSuggestedItem              = true 				
