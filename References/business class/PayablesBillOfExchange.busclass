PayablesBillOfExchange is a BusinessClass
    owned by ap
    prefix is APT
    classic name is APDRAFTS

    Ontology
        symbolic key is PayablesBillOfExchange
            classic set name is APTSET1
            classic name is TRANS-IDENT
            classic name for BankTransactionCode is BANK-INST-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        PayGroup
        Company                   is a PayablesCompany
			disable surrogates
        VendorGroup				  
			disable surrogates
        Vendor
        VendorName                
        	holds pii
            classic name is VENDOR-VNAME
        DraftNumber               is AlphaRight size 20
            classic name is DRAFT-NBR
        Suffix
        ProcessLevel              is a PayablesProcessLevel
            classic name is PROC-LEVEL
        RemitToCode               is a VendorLocation
        PaymentDocumentOrigin
            classic name is PMT-DOC-ORIGIN
        CashCode
        Currency
            classic name is CURRENCY-CODE
        DraftAmount               is an InternationalAmount
        AppliedAmount             is an InternationalAmount
            classic name is APPLIED-AMT
        NumberOfDecimals          is Numeric size 1
            classic name is TRAN-ND
        OriginalCurrencyRate      is an OrigRate
            classic name is ORIG-RATE
        BaseOriginalAmount        is an InternationalAmount
            classic name is BASE-ORIG-AMT
        RevaluedBaseAmount        is an InternationalAmount
            classic name is BASE-ACT-AMT
        BaseNumberOfDecimals
            classic name is BASE-ND
        ActualCurrencyRate        is an AcctRate
            classic name is ACT-RATE
        DraftDate                 is Date
        MaturityDate              is Date
        AcceptDate                is Date
        CashDate                  is Date
        DishonorDate              is Date
        CancelDate                is Date
        Status                    is Numeric size 1
            classic name is REC-STATUS
            States
                Unreleased value is 0
                Released   value is 1
                Accepted   value is 2
                Cashed     value is 3
                Dishonored value is 4
                Canceled   value is 5
        PrintBOE                  is Boolean
            classic name is PRINT-STATUS


        BillOfExchangeAccrualCode is a PayablesBillOfExchangeAccrualCode
            classic name is DRAFT-CODE
        UpdateGeneralLedger       is Boolean
            classic name is GL-UPDATE-FL
            default label is "UpdateGlobalLedger"
        PostDate                  is Date
        PayerReference            is a PayerRef
            classic name is PAYER-REF
        StatementNumber
            classic name is STATEMENT-NBR
        JournalBook
            classic name is JRNL-BOOK-NBR
            restricted
        AcceptanceAccount         is a FinanceCodeBlock
            classic name for AcceptanceAccount.ToAccountingEntity is CASH-COMPANY
            classic name for AcceptanceAccount.AccountingUnit is ACPT-ACCT-UNIT
            classic name for AcceptanceAccount.GeneralLedgerChartAccount is ACPT-ACCOUNT
		CompanyCashCode
     	EvidenceOfShipment 		  is Alpha size 100 
            classic name is EVID-OF-SHIP
     	SpecialInstruction 		  is Alpha size 100 			
            classic name is SPEC-INSTRUC
    Derived Fields

        StatusDescription  is a ConditionalField
            type is Alpha size 9
            restricted
            classic name is DERIVED-STATUS
            if (IsUnreleased)
                "Unrelease"
            else
            if (IsReleased)
                "Unaccept"
            else
            if (IsAccepted)
                "Accepted"
            else
            if (IsCashed)
                "Cashed"
            else
            if (IsCanceled)
                "Cancel"
            else
            if (IsDishonored)
                "Dishonor"
            else
                blank

        VendorGroupOrBlank is a ConditionalField
            type is AlphaUpper size 4
            restricted
            classic name is VGROUP
            if (IsCompanyExist)
                Company.VendorGroup
            else
                blank

        ShortDraftName     is a ConditionalField
            type is Alpha size 20
            restricted
            classic name is SHORT-DFT-NAME
            if (IsRemitExists)
                RemitToCode.VendorName
            else
                Vendor.VendorName

    Conditions

        IsAcceptAccount
        	restricted
            when (Company = 9)

        IsAcceptProgram
        	restricted
            when (Status.Released
            or    Status.Accepted)

        IsAccepted
        	restricted
            when (Status.Accepted)

        IsAcptDtNtEnt
        	restricted
            when (AcceptDate not entered)

        IsAmtEqlsAppld
        	restricted
            when (DraftAmount = AppliedAmount)

        IsApAcceptReqd
        	restricted
            when (BankTransactionCode.PayableAcceptanceAccounting)

        IsApOrigDraft
        	restricted
            when (PaymentDocumentOrigin.Payable)

        IsArOrigDraft
        	restricted
            when (PaymentDocumentOrigin.Receivable)

        IsAutoReconcile
        	restricted
            when (BankTransactionCode.AutomaticReconciliation
            and   CompanyCashCode.ReconcileGeneralLedgerEntry)

        IsBaseCurrency
        	restricted
            when (Currency = Company.Currency)	

        IsBnkinstNtChg
        	restricted
            when (old BankTransactionCode = BankTransactionCode)

        IsCanceled
        	restricted
            when (Status.Canceled)

        IsCashCurrency
        	restricted
            when (CashCode not entered
            or    Currency = CashCode.Currency)

        IsCashed
        	restricted
            when (Status.Cashed)

        IsCompanyExist
        	restricted
            when (Company entered)

        IsCurrNotChg
        	restricted
            when (old Currency = Currency)

        IsDishonored
        	restricted
            when (Status.Dishonored)

        IsDraftBal
        	restricted
            when (PayGroup.BillOfExchangeAcceptanceAccounting
            and   RemitToCode not entered
            and   (Status.Accepted
                or    Status.Cashed
                or    Status.Dishonored
                and   not UpdateGeneralLedger))

        IsDraftBalLoc
        	restricted
            when (PayGroup.BillOfExchangeAcceptanceAccounting
            and   RemitToCode entered
            and   (Status.Accepted
                or    Status.Cashed
                or    Status.Dishonored
                and   not UpdateGeneralLedger))

        IsDraftNbrEntr
        	restricted
            when (DraftNumber entered)

        IsGlNotUpdated
        	restricted
            when (not UpdateGeneralLedger)

        IsGlUpdated
        	restricted
            when (UpdateGeneralLedger)

        IsNoAmtChg
        	restricted
            when (old DraftAmount = DraftAmount)

        IsNoCashCdChg
        	restricted
            when (old CashCode = CashCode)

        IsNoGlUpdate
        	restricted
            when (not UpdateGeneralLedger
            and   Status >  1
            and   Status <  5)

        IsNonBaseCurr
        	restricted
            when (Currency != Company.Currency)	

        IsNonCanceled
        	restricted
            when (not Status.Canceled)

        IsNotPrinted
        	restricted
            when (not PrintBOE
            and   PaymentDocumentOrigin.Payable
            and   BankTransactionCode.PaymentOutputOption.PrintedDocument)

        IsRateEntrd
        	restricted
            when (OriginalCurrencyRate entered
            or    BaseOriginalAmount entered)

        IsReleased
        	restricted
            when (Status.Released)

        IsRemitExists
        	restricted
            when (RemitToCode entered)

        IsStatement
        	restricted
            when (StatementNumber entered)

        IsUnreleased
        	restricted
            when (Status.Unreleased)

        IsVenbalInBase
        	restricted
            when (VendorBalanceRel.BalanceCurrency not entered)

        IsVendorMaster
        	restricted
            when (RemitToCode not entered)

        IsVendorNotChg
        	restricted
            when (old Vendor = Vendor)

		WithinDraftDateRange
			restricted
			when (DraftDateRange not entered
			or   (DraftDateRange entered
			and   DraftDate within DraftDateRange))

		WithinMaturityDateRange
			restricted
			when (MaturityDateRange not entered
			or   (MaturityDateRange entered
			and   MaturityDate within MaturityDateRange))
		
		ShowInEntryList
			restricted
			when (Status.Unreleased)

		ShowInVendorAcceptanceList
			restricted
			when (WithinDraftDateRange
			and   WithinMaturityDateRange
			and  (Status.Released
			or    Status.Accepted))

		ShowInDateAcceptanceList
			restricted
			when (WithinDraftDateRange
			and   WithinMaturityDateRange)

 		HasDistributions
 			restricted
 			when (PayablesBillOfExchangeDistribution set exists)
 			
 			           
	Context Fields
		DraftDateRange				is a DateRange
		MaturityDateRange			is a DateRange

    Relations

        PayablesInvoicePaymentHistoryRel
            classic name is APAPPHIST
            one-to-many relation to PayablesInvoicePaymentHistory
            Field Mapping uses Set6
                related.PayGroup            = PayGroup
                related.BankTransactionCode = BankTransactionCode
                related.TransactionIDNumber = PayablesBillOfExchange

        PayablesBillOfExchangeDistributionsRel
            classic name is APDRFTDIST
            one-to-many relation to PayablesBillOfExchangeDistribution
            Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashManagementGroup	
                related.BankTransactionCode    	= BankTransactionCode
                related.PayablesBillOfExchange 	= PayablesBillOfExchange

        PayablesInvoicePaymentsRel
            classic name is APPAYMENT
            one-to-many relation to PayablesInvoicePayment
            Field Mapping uses Set6
                related.PayGroup            = PayGroup
                related.BankTransactionCode = BankTransactionCode
                related.TransactionIDNumber = PayablesBillOfExchange

        UnappliedPayablesInvoicePaymentsRel
            one-to-many relation to PayablesInvoicePayment
            Field Mapping uses symbolic key
                related.Company								= Company
            Instance Selection
                where (related.Vendor						= Vendor
                and   (related.Status.Released
				or     related.Status.SelectedForPayment)
                and    related.InvoiceCurrency				= Currency
                and    related.BankTransactionCode			= BankTransactionCode
                and    related.Company.VendorGroup 			= PayGroup.VendorGroup)

		DisplayHoldsRel
            one-to-many relation to PayablesInvoicePayment
            Field Mapping uses Set12
                related.PayGroup							= PayGroup
            Instance Selection
                where (related.Vendor						= Vendor
                and   (related.Status.Released
				or     related.Status.SelectedForPayment)
                and    related.InvoiceCurrency				= Currency
                and    related.BankTransactionCode			= BankTransactionCode
                and    related.Company.VendorGroup 			= PayGroup.VendorGroup
                and   (related.IsOnHold
                and    related.PayablesOnHoldRelMatches)
				and    related.OnHold)

		DisplayOpenRel
            one-to-many relation to PayablesInvoicePayment
            Field Mapping uses Set12
                related.PayGroup							= PayGroup
            Instance Selection
                where (related.Vendor						= Vendor
                and    related.InvoiceCurrency				= Currency
                and    related.BankTransactionCode			= BankTransactionCode
                and    related.Company.VendorGroup 			= PayGroup.VendorGroup
                and    related.Status.Released)








        CashLedgerTransactionRel
            classic name is CBTRANS
            one-to-one relation to CashLedgerTransaction
            Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashManagementGroup	
                related.BankTransactionCode   	= BankTransactionCode
                related.CashLedgerSourceRecord	= "CBT"
                related.CashLedgerTransaction 	= PayablesBillOfExchange

        VendorBalanceRel
            classic name is APVENBAL
            one-to-one relation to VendorBalance
            required
            Field Mapping uses symbolic key
                related.VendorGroup    = Company.VendorGroup
                related.Vendor         = Vendor
                related.VendorLocation = RemitToCode
                related.Company        = Company















		
		PayablesInvoicePaymentsOnHoldRel
            one-to-many relation to PayablesOnHold
            Field Mapping uses Set5
                related.HoldRecordType = "P"
                related.VendorGroup    			= Company.VendorGroup
                related.Company        			= Company
                related.Vendor         			= Vendor
				related.PayablesInvoice 		= LocalPayablesInvoice
            Instance Selection
                where (related.PayablesInvoicePayment.PaymentSequence	= LocalPaymentSequence
                and    related.PayablesInvoicePayment.VoidSequence 		= LocalNextVoidSeq)

		PayablesBillOfExchangeAccrualCodeDetailRel
			one-to-many relation to PayablesBillOfExchangeAccrualCodeDetail
			Field Mapping uses symbolic key
				related.VendorGroup							= VendorGroup
				related.PayablesBillOfExchangeAccrualCode	= BillOfExchangeAccrualCode
				related.Company								= Company

		PayGroupBOEAccrualCodeDetailRel
			one-to-many relation to PayablesBillOfExchangeAccrualCodeDetail
			Field Mapping uses symbolic key
				related.VendorGroup							= VendorGroup
				related.PayablesBillOfExchangeAccrualCode	= PayGroup.BillOfExchangeAccrualCode
				related.Company								= Company
		
		PayablesInvoiceRel							
			one-to-one relation to PayablesInvoice
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PayablesInvoice		= LocalPayablesInvoice









    Sets

        Set10
            indexed
            Instance Selection
                where (IsStatement)
            Sort Order
                CashCode
                PayGroup
                StatementNumber
                DraftNumber
                Suffix

        Set2
            indexed
            Sort Order
                CashCode
                Status
                BankTransactionCode
                DraftNumber
                Suffix
                PayablesBillOfExchange
                Company
                Vendor

        Set3
            indexed
            Instance Selection
                where (IsAcceptProgram)
            Sort Order
                PayGroup
                Status
                DraftDate
                BankTransactionCode
                DraftNumber
                Suffix
                PayablesBillOfExchange

        Set4
            indexed
            Sort Order
                PayGroup
                Vendor
                Status
                DraftNumber
                Suffix
                BankTransactionCode
                PayablesBillOfExchange

        Set5
            indexed
            Instance Selection
                where (IsNoGlUpdate)
            Sort Order
                CashCode
                MaturityDate
                PayGroup
                BankTransactionCode
                DraftNumber
                Suffix
                PayablesBillOfExchange

        Set6
            indexed
            Instance Selection
                where (IsNonCanceled)
            Sort Order
                BankTransactionCode
                DraftNumber
                Suffix
                Vendor
                PayGroup

        Set7
            indexed
            Sort Order
                PayGroup
                BankTransactionCode
                Vendor
                DraftNumber
                Suffix
                PayablesBillOfExchange

        Set8
            indexed
            Sort Order
                VendorGroup
                Vendor
                Company
                BankTransactionCode
                PayablesBillOfExchange

    Field Rules

		VendorGroup
			force default to PayGroup.VendorGroup
		
		Company
			force default to PayGroup.PostCompany
		
        DraftNumber
            required
			cannot be changed

        PayGroup
            required
			cannot be changed

			AccessCode = AccessCode.Change
			if (action type.Create)
				AccessCode = AccessCode.Add
			else
				if (action type.Delete)
					AccessCode = AccessCode.Delete
			
			initialize VerifyPayGroupAccess
			VerifyPayGroupAccess.VendorGroup		= CashManagementGroup
			VerifyPayGroupAccess.PayGroup 			= PayGroup
			VerifyPayGroupAccess.SystemCode 		= "AP" 
			VerifyPayGroupAccess.PayablesUserClass	= PayGroup.PayablesUserClass
			VerifyPayGroupAccess.PayablesUser 		= actor
			VerifyPayGroupAccess.AccessCode 		= AccessCode
			constraint (VerifyPayGroupAccess.PerformVerifyPayGroupAccess)
				"<VerifyPayGroupAccess.OutputErrorMessage>"

        Vendor
            required
			if (!Status.Unreleased)
				cannot be changed

		Currency
            required
			if (!Status.Unreleased)
				cannot be changed
			default to RemitToCode.InvoiceCurrency
			default to Vendor.InvoiceCurrency
			default to Vendor.VendorClass.Currency
			default to PayGroup.BaseCurrency
			constraint (Currency = CashCode.Currency)
				"BOECurrencyMustEqualCashCodeCurrency"
					
		CashCode
			required
			if (!Status.Unreleased)
				cannot be changed
			default to RemitToCode.CashCode
			default to Vendor.CashCode
			default to ProcessLevel.CashCode
			default to Company.CashCode
			default to PayGroup.CashCode
			
			constraint (Currency = CashCode.Currency)
				"CurrencyMustEqualCashCodeCurrency"
			
			constraint (CompanyCashCode exists)
				"CompanyCashCodeRelationshipDoesNotExist"

		Suffix
			cannot be changed

		DraftDate
			required
			if (!Status.Unreleased)
				cannot be changed
			if (PaymentDocumentOrigin.Payable)
				default to current corporate date
		
		DraftAmount
			if (!Status.Unreleased)
				if (PaymentDocumentOrigin.Payable)
					cannot be changed
						"CannotChangeDraftAmountForPayablesOriginatedBillOfExchange"
			if (Currency = PayGroup.BaseCurrency
			and BaseOriginalAmount entered)
				DraftAmount = BaseOriginalAmount
			constraint (DraftAmount >= 0)
				"BillOfExchangeAmountCannotBeNegative"

		MaturityDate
			required
			if (!Status.Unreleased)
				cannot be changed
			constraint (MaturityDate >= DraftDate)
				"MaturityDateCannotBeLessThanDraftDate"
		
		BankTransactionCode
			constraint (BankTransactionCode.BankTransactionType.BillOfExchangePayment)
				"PaymentCodeBustBeBillOfExchangePaymentType"

		PaymentDocumentOrigin
			default to BankTransactionCode.TransactionOrigin
		
		RevaluedBaseAmount
			default to BaseOriginalAmount
		



				
		BillOfExchangeAccrualCode
			if (PayGroup.BillOfExchangeAcceptanceAccounting)
				if (PayGroup.BillOfExchangeAccrualCode.Active)
					default to PayGroup.BillOfExchangeAccrualCode 
				required
			default to PayGroup.BillOfExchangeAccrualCode
		
		PrintBOE
			if (PaymentDocumentOrigin.Receivable)
				PrintBOE = true
			if (PaymentDocumentOrigin.Payable)
				PrintBOE = false
		
		AcceptDate
			constraint (AcceptDate <= current corporate date)
				"AcceptDateCannotBeGreaterThanSystemDate"
			
			
	Local Fields
		VerifyPayGroupAccess					
		AccessCode				is Alpha size 1
			States
				Add			value is "A"
				Change		value is "C"
				Delete		value is "D"
		LocalPayablesInvoice 	is like PayablesInvoice
		LocalPaymentSequence 	is a PaymentSequence
		LocalNextVoidSeq 		is a VoidSequence
		LocalSuffix				is like Suffix															
		LocalCancelSequence		is like CancelSequence   												

	Rule Blocks
		UnacceptRules
			invoke 704CancelDraft
				invoked.ActionCode = "U"
				invoked.PrmActionDate	= PostDate
				if (Vendor.BalanceCurrency not entered)
					invoked.PrmCurrency = PayGroup.BaseCurrency
				else
					invoked.PrmCurrency = Currency

		UpdateRules
			for each PayablesInvoicePaymentsRel
				invoke Update each
					invoked.CashCode = CashCode
			
	Actions
		
		Purge is a Purge Action
			restricted
		
		Create is a Create Action
			Field Rules
		        DraftNumber
		            required
					if (PayGroup.BillOfExchangeNumbering.Automatic)
						increment PayGroup.LastDraftNumber
		        		DraftNumber = PayGroup.LastDraftNumber
				
		704CancelDraft is an Instance Action	
			restricted
			Parameters
				ActionCode		is Alpha size 1
		            States
		            	Invoice		value is "I"
		            	Draft		value is "D"
		            	R			value is "R"
		            	U			value is "U"
		            	C			value is "C"
				PrmCurrency		is a Currency
				PrmActionDate	is Date
			Action Rules
				for each PayablesBillOfExchangeDistributionsRel 
					if (each.Status.Unposted)
						invoke Delete each
					else
						invoke CreateOffset each
							invoked.PrmActionDate = PrmActionDate
				
				for each PayablesInvoicePaymentsRel 
					if (!ActionCode.U)
						LocalPayablesInvoice	= each.PayablesInvoice
						LocalSuffix				= each.PayablesInvoicePayment.Suffix					
						LocalCancelSequence		= each.PayablesInvoicePayment.CancelSequence			
						LocalPaymentSequence 	= each.PayablesInvoicePayment.PaymentSequence	
						if (each.DraftPayablesInvoicePaymentRel exists)
							LocalNextVoidSeq 	= each.DraftPayablesInvoicePaymentRel.PayablesInvoicePayment.VoidSequence
							LocalNextVoidSeq   -= 1
							if (LocalNextVoidSeq not entered)
								LocalNextVoidSeq = 9999
						else
							LocalNextVoidSeq 	= 9999

						invoke Delete PayablesInvoicePaymentsOnHoldRel
						









						invoke FastUpdate each															
							invoked.OnHold								= false							
							invoked.PayablesInvoicePayment.VoidSequence = LocalNextVoidSeq				
							if (PrmActionDate entered)													
								invoked.VoidDate						= PrmActionDate					
							else																		
							    invoked.VoidDate                        = current corporate date          		
							invoked.Status								= 8								

						invoke FastUpdate PayablesInvoiceRel											
							invoked.PayGroup							= each.PayGroup					

						invoke CancelPayablesBillOfExchangeCreate PayablesInvoicePayment				
							fill in fields from each													
							initialize invoked.TransactionIDNumber										
							initialize invoked.Suffix													
							initialize invoked.TransactionNumber										
							initialize invoked.PayablesInvoicePayment.VoidSequence						
							initialize invoked.VoidDate													

					else
						invoke Update each
							invoked.Status								= 4
						invoke Create PayablesInvoicePayment
							fill in fields from each
							invoked.Status = 1
							invoked.BankTransactionCode 	= each.PayablesInvoice.BankTransactionCode
							invoked.CashCode				= each.PayablesInvoice.CashCode
							invoked.PayVendor				= each.PayablesInvoice.PayVendor
							invoked.RemitToCode				= each.PayablesInvoice.RemitToCode
							
				
				for each PayablesInvoicePaymentHistoryRel 
					invoke Unarchive each.PayablesInvoiceHistory
					
				if (Status > 1
				and PayGroup.BillOfExchangeAcceptanceAccounting)
					invoke UpdateVendorBalance VendorBalanceRel
						


		711DraftAccept is an Instance Action	
			restricted
			Parameters
				ActionCode		is Alpha size 1
		            States
		            	Invoice		value is "I"
		            	Draft		value is "D"
		            	R			value is "R"
				PrmPostDate		is Date
				PrmCurrency		is a Currency
			Action Rules
				if (ActionCode.Invoice)
					invoke Create PayablesBillOfExchangeDistribution
						fill in fields from this instance
						invoked.Company						= PayGroup.PostCompany
						invoked.OriginalCompany 			= PayGroup.PostCompany


						invoked.TransactionNumber			= DraftNumber
						invoked.DistributionType			= "A"
						invoked.Status						= 1
						if (PrmPostDate entered)
							invoked.PostDate				= PrmPostDate
						else
							invoked.PostDate				= PostDate
				else
					for each PayablesInvoicePaymentsRel
						invoke Create PayablesBillOfExchangeDistribution
							fill in fields from this instance
							invoked.Company						= PayGroup.PostCompany
							invoked.OriginalCompany				= PayGroup.PostCompany


							invoked.TransactionNumber			= DraftNumber
							invoked.DistributionType			= "A"
							invoked.Status						= 1
							if (PrmPostDate entered)
								invoked.PostDate				= PrmPostDate
							else
								invoked.PostDate				= PostDate
							invoked.InvVendor					= each.Vendor
							invoked.Invoice						= each.PayablesInvoice
							invoked.InvSeqNbr					= each.PayablesInvoicePayment.PaymentSequence
							invoked.InvVoidSeq												= each.PayablesInvoicePayment.VoidSequence
							invoked.GLTransactionAmount										= each.PaymentAmount.CurrencyAmount
							invoked.GLBaseAmount.FunctionalAmount.EnteredCurrencyAmount		= each.PaymentAmount.FunctionalAmount.EnteredCurrencyAmount
							invoked.GLBaseAmount.AlternateAmount.EnteredCurrencyAmount		= each.PaymentAmount.AlternateAmount.EnteredCurrencyAmount
							invoked.GLBaseAmount.AlternateAmount2.EnteredCurrencyAmount		= each.PaymentAmount.AlternateAmount2.EnteredCurrencyAmount
							invoked.GLBaseAmount.AlternateAmount3.EnteredCurrencyAmount		= each.PaymentAmount.AlternateAmount3.EnteredCurrencyAmount
							invoked.GLBaseAmount.ToFunctionalAmount.EnteredCurrencyAmount	= each.PaymentAmount.ToFunctionalAmount.EnteredCurrencyAmount
							invoked.GLBaseAmount.ToAlternateAmount.EnteredCurrencyAmount	= each.PaymentAmount.ToAlternateAmount.EnteredCurrencyAmount
							invoked.GLBaseAmount.ToAlternateAmount2.EnteredCurrencyAmount	= each.PaymentAmount.ToAlternateAmount2.EnteredCurrencyAmount
							invoked.GLBaseAmount.ToAlternateAmount3.EnteredCurrencyAmount	= each.PaymentAmount.ToAlternateAmount3.EnteredCurrencyAmount
							invoked.GLBaseAmount.ProjectAmount.EnteredCurrencyAmount		= each.PaymentAmount.ProjectAmount.EnteredCurrencyAmount
							invoked.GLBaseAmount.ReportAmount1.EnteredCurrencyAmount		= each.PaymentAmount.ReportAmount1.EnteredCurrencyAmount
							invoked.GLBaseAmount.ReportAmount2.EnteredCurrencyAmount		= each.PaymentAmount.ReportAmount2.EnteredCurrencyAmount
							invoked.GLBaseAmount.ReportAmount3.EnteredCurrencyAmount		= each.PaymentAmount.ReportAmount3.EnteredCurrencyAmount
							invoked.GLBaseAmount.ReportAmount4.EnteredCurrencyAmount		= each.PaymentAmount.ReportAmount4.EnteredCurrencyAmount
							invoked.GLBaseAmount.ReportAmount5.EnteredCurrencyAmount		= each.PaymentAmount.ReportAmount5.EnteredCurrencyAmount
							invoked.AccrualCode					= each.AccrualCode
							invoked.GLFinanceCodeBlock			= each.DiscountAccount
						if (PayGroup.BillOfExchangeAcceptanceAccounting)
							invoke RestrictedUpdate each
								invoked.PrmStatus = 5  
						else
							invoke RestrictedUpdate each
								invoked.PrmStatus = 4  
				
				invoke Create PayablesBillOfExchangeDistribution
					fill in fields from this instance
					invoked.Company						= PayGroup.PostCompany
					invoked.OriginalCompany				= PayGroup.PostCompany
					invoked.TransactionNumber			= DraftNumber
					invoked.GLTransactionAmount			= DraftAmount * -1


					if (BillOfExchangeAccrualCode not entered)
						invoked.BillOfExchangeAccrualCode	= PayGroup.BillOfExchangeAccrualCode
						invoked.GLFinanceCodeBlock			= PayGroupBOEAccrualCodeDetailRel.DistributionAccount
					else
						invoked.BillOfExchangeAccrualCode	= BillOfExchangeAccrualCode
						invoked.GLFinanceCodeBlock			= PayablesBillOfExchangeAccrualCodeDetailRel.DistributionAccount
					invoked.DistributionType				= "A"
					invoked.GLBaseAmount.FunctionalAmount.EnteredCurrencyAmount		= BaseOriginalAmount * -1
					invoked.GLBaseAmount.ToFunctionalAmount.EnteredCurrencyAmount	= BaseOriginalAmount * -1
					invoked.Status							= 1
					if (PrmPostDate entered)
						invoked.PostDate					= PrmPostDate
					else
						invoked.PostDate					= AcceptDate
					invoked.AccrualCode						= blank
					
		UpdateAppliedAmount is an Instance Action
			restricted
			Parameters
				PrmAppliedAmount 		is an InternationalAmount
		        PrmBaseOriginalAmount   is an InternationalAmount
		        PrmRevaluedBaseAmount   is an InternationalAmount
			Action Rules
				AppliedAmount 		= PrmAppliedAmount
				BaseOriginalAmount	= PrmBaseOriginalAmount
				RevaluedBaseAmount	= PrmRevaluedBaseAmount

	StateCycles
		InvoicePaymentLifeCycle is a StateCycle
			state field is Status
            Unreleased is a State  

				Field Rules
					Currency
						cannot be changed
					
				Update is an Update Action
					Action Rules
						include UpdateRules
				
				Delete is a Delete Action
					Entrance Rules
						invoke 704CancelDraft
							invoked.PrmActionDate	= PostDate
							invoked.ActionCode = "C"
					Action Rules
						for each PayablesInvoicePaymentsRel
							invoke UnapplyFromCheck each  

				Cancel is an Instance Action
					completion message is "CancelBillOfExchangeComplete"			
					Parameters														
						BillOfExchangeCancelDate	is Date							
					Parameter Rules													
						BillOfExchangeCancelDate									
							initial value is current corporate date							
							default to current corporate date									

					Entrance Rules													
						invoke 704CancelDraft										
							invoked.PrmActionDate	= BillOfExchangeCancelDate		
							invoked.ActionCode		= "C"							
					Action Rules
						initialize AcceptDate
						CancelDate = current corporate date
						make transition to Canceled
					
				Release is an Instance Action
					Action Rules
						constraint (IsAmtEqlsAppld)
							"CannotRelease.DraftAmountMustEqualAppliedAmount"
						constraint (DraftNumber entered)
							"DraftNumberIsRequired"

						UpdateGeneralLedger = false

						if (PayGroup.BillOfExchangeAcceptanceAccounting
						and !BankTransactionCode.PayableAcceptanceAccounting)
							invoke 711DraftAccept
								invoked.ActionCode = "D"
								invoked.PrmCurrency = Currency

						if (BankTransactionCode.ReceivableAcceptanceAccounting)
							make transition to Released
						else
							AcceptDate = DraftDate
							make transition to Accepted
					
            Released is a State  

				Update is an Update Action
					Field Rules
						MaturityDate
							required
						CashCode
							if (CashCode entered)
								constraint (Currency = CashCode.Currency)
									"CashCodeCurrencyMustEqualBOECurrency"
					Action Rules
						include UpdateRules
				
				Accept is an Instance Action
					Parameters
						PrmVendorGroup					is a VendorGroup
							default label is "VendorGroup"
						PrmAcceptDate					is Date
							default label is "AcceptDate"
						PrmPostDate						is Date
							default label is "PostDate"
						PrmCashCode						is a CashCode
							default label is "CashCode"
						PrmBillOfExchangeAccrualCode 	is a PayablesBillOfExchangeAccrualCode
							default label is "BillOfExchangeAccrualCode"
					Parameter Rules
						PrmVendorGroup
							initial value is VendorGroup
							default to VendorGroup
						PrmAcceptDate
							default to current corporate date
						PrmPostDate
							default to PrmAcceptDate
					Action Rules
						if (PayGroup.BillOfExchangeAcceptanceAccounting)
							constraint (BillOfExchangeAccrualCode entered)
								"BillOfExchangeAccrualCodeRequired"
						PostDate 					= PrmPostDate
						AcceptDate 					= PrmAcceptDate
						CashCode 					= PrmCashCode
						BillOfExchangeAccrualCode	= PrmBillOfExchangeAccrualCode
						
						if (PayGroup.BillOfExchangeAcceptanceAccounting)
							invoke 711DraftAccept
								invoked.ActionCode = "D"
								if (Vendor.BalanceCurrency not entered)
									invoked.PrmCurrency = PayGroup.BaseCurrency
								else
									invoked.PrmCurrency = Currency
						
						make transition to Accepted

				Cancel is an Instance Action
					completion message is "CancelBillOfExchangeComplete"			
					Parameters														
						BillOfExchangeCancelDate	is Date							
					Parameter Rules													
						BillOfExchangeCancelDate									
							initial value is current corporate date							
							default to current corporate date									

					Entrance Rules													
						invoke 704CancelDraft										
							invoked.PrmActionDate	= BillOfExchangeCancelDate		
							invoked.ActionCode		= "C"							
					Action Rules
						initialize AcceptDate
						CancelDate = current corporate date
						make transition to Canceled

            Accepted is a State  
            
				Update is an Update Action
					Field Rules
						BillOfExchangeAccrualCode
							cannot be changed
								"CannotChangeBillOfExchangeAccrualCodeOnAcceptedBillOfExchange"
					Action Rules
						include UpdateRules
				
				Cancel is an Instance Action
					completion message is "CancelBillOfExchangeComplete"			
					Parameters														
						BillOfExchangeCancelDate	is Date							
					Parameter Rules													
						BillOfExchangeCancelDate									
							initial value is current corporate date							
							default to current corporate date									

					Entrance Rules													
						invoke 704CancelDraft										
							invoked.PrmActionDate	= BillOfExchangeCancelDate		
							invoked.ActionCode		= "C"							
					Action Rules
						initialize AcceptDate
						CancelDate = current corporate date
						make transition to Canceled
				
				Cash is an Instance Action
					Parameters
						PrmRevaluedBaseAmount			is an InternationalAmount
							default label is "BaseAmount"
						PrmActualCurrencyRate			is an AcctRate
							default label is "Rate"
						PrmCashCode						is a CashCode
							default label is "CashCode"
						PrmCashDate						is Date
							default label is "CashDate"
						PrmPostDate						is Date
							default label is "PostDate"
					Parameter Rules
						PrmRevaluedBaseAmount
							initial value is RevaluedBaseAmount
							default to RevaluedBaseAmount
						PrmActualCurrencyRate
							initial value is ActualCurrencyRate
							default to ActualCurrencyRate
						PrmCashCode
							initial value is CashCode
							default to CashCode
						PrmCashDate
							initial value is current corporate date
							default to current corporate date
						PrmPostDate
							default to PrmCashDate
							
					Action Rules
						constraint (!UpdateGeneralLedger)
							"CannotCash;PostedToGlobalLedger"

						RevaluedBaseAmount 		= PrmRevaluedBaseAmount
						ActualCurrencyRate 		= PrmActualCurrencyRate
						CashCode				= PrmCashCode
						PostDate 				= PrmPostDate
						CashDate 				= PrmCashDate

						make transition to Cashed

				Dishonor is an Instance Action
					Parameters			
						PrmDishonorDate					is Date
							default label is "DishonorDate"
						PrmPostDate						is Date
							default label is "PostDate"
					Parameter Rules		
						PrmDishonorDate
							initial value is current corporate date
							default to current corporate date
						PrmPostDate
							default to PrmDishonorDate
					Action Rules
						DishonorDate 			= PrmDishonorDate		
						PostDate 				= PrmPostDate			

						make transition to Dishonored

            	Unaccept is an Instance Action
            		Action Rules
            			constraint (BankTransactionCode.PayableAcceptanceAccounting)
            				"CannotUnaccept.BOEDoesNotRequireAcceptance" 

            			initialize AcceptDate

						include UnacceptRules            			
            			
            			make transition to Released

				Print is an Instance Action
					Action Rules
						PrintBOE = true

            Cashed is a State  

				Cancel is an Instance Action
					completion message is "CancelBillOfExchangeComplete"			
					Parameters														
						BillOfExchangeCancelDate	is Date							
					Parameter Rules													
						BillOfExchangeCancelDate									
							initial value is current corporate date							
							default to current corporate date									

					Entrance Rules													
						invoke 704CancelDraft										
							invoked.PrmActionDate	= BillOfExchangeCancelDate		
							invoked.ActionCode		= "C"							
					Action Rules
						initialize AcceptDate
						CancelDate = current corporate date
						make transition to Canceled
				
				GLUpdate is an Instance Action
					default label is "GlobalLedgerUpdate"
					Action Rules
						UpdateGeneralLedger = true
				
				Undo is an Instance Action
					Action Rules
						constraint (!UpdateGeneralLedger)
							"CannotUndo;PostedToGlobalLedger"
						constraint (StatementNumber not entered)
							"CannotEnterStatementNumber"
						
						RevaluedBaseAmount = BaseOriginalAmount
						initialize ActualCurrencyRate
						initialize CashDate
						make transition to Accepted





            	Unaccept is an Instance Action
            		Action Rules
						initialize CashDate
						initialize DishonorDate

						include UnacceptRules            			

            			make transition to Accepted

            Dishonored is a State  

				Cancel is an Instance Action
					completion message is "CancelBillOfExchangeComplete"			
					Parameters														
						BillOfExchangeCancelDate	is Date							
					Parameter Rules													
						BillOfExchangeCancelDate									
							initial value is current corporate date							
							default to current corporate date									

					Entrance Rules													
						invoke 704CancelDraft										
							invoked.PrmActionDate	= BillOfExchangeCancelDate		
							invoked.ActionCode		= "C"							
					Action Rules
						initialize AcceptDate
						CancelDate = current corporate date
						make transition to Canceled
				
				GLUpdate is an Instance Action
					default label is "GlobalLedgerUpdate"
					Action Rules
						UpdateGeneralLedger = true
				
				Undo is an Instance Action
					Action Rules
						constraint (!UpdateGeneralLedger)
							"CannotUndo;PostedToGlobalLedger"

						RevaluedBaseAmount = BaseOriginalAmount
						initialize PostDate
						initialize DishonorDate
						make transition to Accepted

            	Unaccept is an Instance Action
            		Action Rules
						initialize CashDate
						initialize DishonorDate

						include UnacceptRules            			

            			make transition to Accepted

            Canceled is a State  

			

			
