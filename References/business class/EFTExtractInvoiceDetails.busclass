EFTExtractInvoiceDetails is a BusinessClass
	owned by ar
	
	prefix is EFTEI
	sql name is EFTExtractInvoiceDetails
	
	Ontology
		symbolic key is EFTExtractInvoiceDetails
		
	
	Patterns
	
	Persistent Fields
		FinanceEnterpriseGroup
		EFTExtractResult
			delete ignored
		EFTCompany						is like ReceivableCompany
		ProcessingCompany				is like ReceivableCompany
        Customer
        IsSearch						is AlphaUpper size 1
		NoteMethod   					is AlphaUpper size 1
            States
                AlternateEdi value is "A"
                    default label is "Alternate EDI"
                Edi          value is "E"
                    default label is "EDI"
                Fax          value is "F"
		EDINumber
        FaxNumber
        AltEdiType
        AltEdiNbr
        CreditAnalyst
		Contact
		TransactionDate				is Date
		DiscountDate				is Date
		DueDate
		DiscAmount					is like InternationalAmount
		TransType					is AlphaUpper size 1
		Invoice 					is AlphaUpper size 22
		OpenAmount					is like InternationalAmount
		CurrentBalance				is like InternationalAmount
		TransactionAmount			is like InternationalAmount
		EftAction					
		BankEntity					
		BankAccountNumber
		EFTOpenAmount					is like InternationalAmount
        DiscountAmount					is like InternationalAmount
        NetTotalAmount					is like InternationalAmount
        
	Local Fields
		LocalEFTExtractResult			is like EFTExtractResult
		LocalFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
		LocalCustomer					is like Customer
		LocalNewEFTExtractCompany		is like ReceivableCompany
		LocalNewEFTExtractOutput		is an EFTExtractOutput
		LocalEnteredCompany				is like ReceivableCompany
		
	Derived Fields
			
	Field Rules
		
	Relations
		EFTExtractInvoiceDetailsRel
			one-to-many relation to EFTExtractInvoiceDetails
			Field Mapping uses Set1
				related.FinanceEnterpriseGroup 		= LocalFinanceEnterpriseGroup
				related.EFTExtractResult			= LocalEFTExtractResult
			Instance Selection
				where (related.EftAction = "1"
				or    related.EftAction = "5"
				or    related.EftAction = "3"
				or    related.EftAction = "6")
				
		EFTExtractInvoiceDetailCustomerRel
			one-to-many relation to EFTExtractInvoiceDetails
			Field Mapping uses Set1
				related.FinanceEnterpriseGroup 		= LocalFinanceEnterpriseGroup
				related.EFTExtractResult			= LocalEFTExtractResult
				related.Customer					= LocalCustomer
			Instance Selection
				where (related.EftAction = "1"
				or     related.EftAction = "5"
				or     related.EftAction = "3"
				or     related.EftAction = "6")
				
		EFTExtractOutputRel
			one-to-one relation to EFTExtractOutput
			Field Mapping uses symbolic key
				related.Company						= LocalNewEFTExtractCompany
				related.EFTExtractOutput			= LocalNewEFTExtractOutput
				
		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company						= ProcessingCompany
				related.Customer					= LocalCustomer
				
		ReceivableInvoiceDetailRel
			one-to-many relation to ReceivableInvoiceDetail
			Field Mapping uses OpenByCustomer
				related.Customer					= LocalCustomer
			Instance Selection
				where (related.EFTExtractResult	= LocalEFTExtractResult
				and    related.EftExtract.Extracted)
				
		EFTExtractResultRel
			one-to-one relation to EFTExtractResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		= LocalFinanceEnterpriseGroup
				related.EFTExtractResult			= LocalEFTExtractResult

		ElectronicFundsTransferCustomerGroupsRel
			one-to-many relation to ElectronicFundsTransferCustomerGroup
			Field Mapping uses symbolic key
				related.CustomerGroup 									= Company.CustomerGroupField.CustomerGroup
			Instance Selection
            	where (related.ElectronicFundsTransferCustomerGroup.Company	= LocalEnteredCompany
				and    related.ElectronicFundsTransferCustomerGroup.CreditCustomer	= LocalCustomer)
		
	Sets
		ByCompany
			duplicates
			Sort Order
				ReceivableInvoiceDetail.Company
				
		Set1
			Sort Order
				FinanceEnterpriseGroup
				EFTExtractResult
				Customer
				Company
				ReceivableInvoiceDetail
		
		ByCreditAnalyst
			duplicates
			Sort Order
				CreditAnalyst
				Customer
		
	Rule Blocks
			
	Conditions
	
	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			restricted
			
		Purge is a Purge Action
			restricted
		
		ProcessEFTExtractOutput is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmDataFileName					is AlphaUpper size 50
				PrmProcessingCompany			is a ReceivableCompany	
				PrmEFTExtractResult				is like EFTExtractResult
				PrmEnteredCompany				is like ReceivableCompany
				
			Parameter Rules
			
			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    EFTExtractResult		  = PrmEFTExtractResult
				and   (EftAction              = "1" 
				or     EftAction              = "5"
				or     EftAction              = "3"
				or     EftAction              = "6"))
			
			Sort Order is Set1
			
			Local Fields
				LocalNoteMethod					is AlphaUpper size 1
				LocalEFTCustomer				is like Customer
				NewEFTExtractOutput 			is an EFTExtractOutput view
				LocalOutputDetailData	 		is Alpha size 1000
				LocalFormatedTransactionDate	is Alpha size 15
				LocalFormatedDiscountDate		is Alpha size 15	
				LocalFormatedDueDate			is Alpha size 15
				LocalAdjustTotal	           	is like InternationalAmount
				LocalCurrentBalance				is like InternationalAmount
				AdjustTotal						is like InternationalAmount
				LocalDetailTransactionAmount	is Alpha size 20
				LocalDetailDiscountAmount		is Alpha size 20
				LocalDetailNetAmount			is Alpha size 20
				LocalStatus						is Alpha size 20
				LocalServer						is Alpha size 5
				LocalHeaderNumber      			is Alpha size 95
				LocalFaxExtention				is Alpha size 50
				LocalFaxNumber					is Alpha size 50
				CurrentYear						is a Year
				LocalTransactionDate			is Date
				CurrentMonth					is Numeric 2
				CurrentDay						is Numeric 2
				LocalReceivableInvoiceDetail	is like ReceivableInvoiceDetail
			
			Rule Blocks
				CreateEFTOutputDetail
					include FormatDates
					include FormatAmounts
					include EftActionStatus
					if (TransType = "C")
						LocalOutputDetailData				= TransType + " " + Invoice using "%-22s" + " " + LocalFormatedTransactionDate using "%12s" + "   " + LocalFormatedDiscountDate using "%12s" + " " + LocalFormatedDueDate using "%12s" + "     " + LocalStatus using "%-22s" + " " + LocalDetailTransactionAmount using "%13s" + "      " + LocalDetailDiscountAmount using "%13s" + "      " + LocalDetailNetAmount using "%16s"
					else
						LocalOutputDetailData				= TransType + " " + Invoice using "%-22s" + " " + LocalFormatedTransactionDate using "%12s" + "   " + LocalFormatedDiscountDate using "%12s" + " " + LocalFormatedDueDate using "%12s" + "     " + LocalStatus using "%-22s" + " " + LocalDetailTransactionAmount using "%13s" + "       " + LocalDetailDiscountAmount using "%13s" + "      " + LocalDetailNetAmount using "%16s"
					include CreateEftExtractOutputDetail
						
				CreateEftExtractOutputDetail
					invoke Create EFTExtractOutputDetail
						invoked.Company						= NewEFTExtractOutput.Company
						invoked.EFTExtractOutput			= NewEFTExtractOutput.EFTExtractOutput
						invoked.Data						= LocalOutputDetailData
						
				FormatDates
					if (TransactionDate entered)
						LocalFormatedTransactionDate 		= TransactionDate month + "/" + TransactionDate day  + "/" + TransactionDate year 
					else
						LocalFormatedTransactionDate 		= "00" + "/" + "00" + "/" + "00"
					if (DiscountDate entered)
						LocalFormatedDiscountDate 			= DiscountDate month + "/" + DiscountDate day + "/" + DiscountDate year
					else
						LocalFormatedDiscountDate 			= "            "
					if (DueDate entered)
						LocalFormatedDueDate 				= DueDate month + "/" + DueDate day + "/" + DueDate year
					else
						LocalFormatedDueDate 				= "00" + "/" + "00" + "/" + "00"
					
				FormatAmounts
					if (TransType = "C")
						LocalDetailTransactionAmount 		= TransactionAmount using "%13.3f" + "-"
						LocalDetailDiscountAmount 			= DiscAmount using "%13.3f" + "-"
						LocalDetailNetAmount				= OpenAmount using "%13.3f" + "-"
						LocalAdjustTotal 					= LocalAdjustTotal - OpenAmount
					else
						LocalDetailTransactionAmount 		= TransactionAmount using "%13.3f"
						LocalDetailDiscountAmount 			= DiscAmount using "%13.3f"
						LocalDetailNetAmount				= OpenAmount using "%13.3f"
						LocalAdjustTotal 					= LocalAdjustTotal + OpenAmount
					
				EftActionStatus
					if (EftAction = "1")
						LocalStatus 	= "Added"
					else
					if (EftAction = "5")
						LocalStatus 	= "Postponed"
					else
					if (EftAction = "3")
						LocalStatus 	= "Deleted"
					else
					if (EftAction = "6")
						LocalStatus 	= "Hold"
					
				PrintTotals
					LocalOutputDetailData 					= ""
					include CreateEftExtractOutputDetail
					if (LocalAdjustTotal < 0)
						LocalAdjustTotal					= LocalAdjustTotal * -1
						LocalOutputDetailData 				= "                                         Adjusted Total      " + LocalAdjustTotal using "%16s" + "-"
					else
						LocalOutputDetailData 				= "                                         Adjusted Total      " + LocalAdjustTotal using "%16s"
					include CreateEftExtractOutputDetail
					LocalOutputDetailData 					= ""
					include CreateEftExtractOutputDetail
					LocalOutputDetailData 					= "                                         Electronic Funds Transfer for" 
					include CreateEftExtractOutputDetail
					LocalOutputDetailData					= "                                         Bank Entity   " + BankEntity + "       " + "Bank Acct Nbr   " + BankAccountNumber
					include CreateEftExtractOutputDetail
					if (LocalCurrentBalance < 0)
						LocalCurrentBalance 				= LocalCurrentBalance * -1
						LocalOutputDetailData				= "                                         Re-calculated final notification total   " + LocalCurrentBalance using "%16s" + "-"
					else
						LocalOutputDetailData				= "                                         Re-calculated final notification total   " + LocalCurrentBalance
					include CreateEftExtractOutputDetail
						
			Action Rules
				Set Rules
					Exit Rules
						LocalEFTExtractResult				= PrmEFTExtractResult
						LocalFinanceEnterpriseGroup 		= PrmFinanceEnterpriseGroup
						invoke Purge EFTExtractInvoiceDetailsRel
						invoke UpdateStatusOnResult EFTExtractResultRel
							invoked.PrmRecordsExists 		= true 
				
				Instance Rules
					LocalEFTExtractResult					= PrmEFTExtractResult
					LocalFinanceEnterpriseGroup 			= PrmFinanceEnterpriseGroup
					LocalTransactionDate 					= current corporate date
					CurrentYear 							= LocalTransactionDate year
					CurrentMonth 							= LocalTransactionDate month
					CurrentDay								= LocalTransactionDate day
					LocalReceivableInvoiceDetail	    	= ReceivableInvoiceDetail
					LocalEnteredCompany						= PrmEnteredCompany
					
					if (LocalEFTCustomer = Customer)
						include CreateEFTOutputDetail
						LocalCustomer = Customer
						if (LocalReceivableInvoiceDetail    = last EFTExtractInvoiceDetailCustomerRel.ReceivableInvoiceDetail)
							LocalNewEFTExtractCompany		= NewEFTExtractOutput.Company
							LocalNewEFTExtractOutput		= NewEFTExtractOutput.EFTExtractOutput
							include PrintTotals
							invoke CreateEFTExtractFile EFTExtractOutputRel
					else
						LocalAdjustTotal					= 0
						LocalEFTCustomer					= Customer
						LocalCustomer 						= Customer
						LocalCurrentBalance					= sum ReceivableInvoiceDetailRel.EFTExtractNetAmount
						
						for each ElectronicFundsTransferCustomerGroupsRel	
							LocalCustomer = each.ElectronicFundsTransferCustomerGroup.Customer
							LocalCurrentBalance = LocalCurrentBalance + sum ReceivableInvoiceDetailRel.EFTExtractNetAmount

						LocalCustomer 						= Customer

						invoke Create EFTExtractOutput
							assign result to NewEFTExtractOutput
							invoked.Company 		 		= PrmProcessingCompany
							invoked.Customer 		 		= LocalCustomer
							invoked.DataFileName 	 		= PrmDataFileName
							invoked.EFTExtractResult 		= LocalEFTExtractResult
							
						LocalNoteMethod						= first EFTExtractInvoiceDetailsRel.NoteMethod
						if (LocalNoteMethod = "E")
							LocalServer						= "EDI"
							LocalHeaderNumber				= EDINumber
						if (LocalNoteMethod = "A")
							LocalServer						= AltEdiType
							LocalHeaderNumber				= AltEdiNbr
						if (LocalNoteMethod = "F")
							LocalServer						= "FAX"
							LocalFaxExtention				= FaxNumber.Extension
							LocalFaxNumber					= FaxNumber.SubscriberNumber
							LocalHeaderNumber				= LocalFaxNumber using "%-15s" + LocalFaxExtention using "%-5s"
							
						LocalOutputDetailData 				= LocalHeaderNumber using "%-75s"+ LocalServer
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= CurrentMonth + "/" + CurrentDay + "/" + CurrentYear
						include CreateEftExtractOutputDetail
							
						LocalOutputDetailData 				= Customer.Name using "%-120s"+ "              " + "Company " + PrmProcessingCompany.Name 
						include CreateEftExtractOutputDetail
	
						LocalOutputDetailData 				= "Attention: " + Contact using "%-20s" + "             " + "Analyst " + CompanyCustomerRel.CreditAnalyst.Name using "%-30s"
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= "                                            Phone  " + CompanyCustomerRel.CreditAnalyst.PhoneNumber.SubscriberNumber using "%-15s" + " "+ CompanyCustomerRel.CreditAnalyst.PhoneNumber.Extension using "%-5s"
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= ""
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= "Transaction                Tran Date      Disc Date     Due Date      Status                Tran Amount        Disc Amount          Adjusted Amount"
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= "--------------------      --------       --------      --------      ------------------    ---------------    ---------------      -----------------"
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= "The following items have been adjusted"
						include CreateEftExtractOutputDetail
						
						LocalOutputDetailData 				= ""
						include CreateEftExtractOutputDetail
						
						include CreateEFTOutputDetail
						
						if (LocalReceivableInvoiceDetail = last EFTExtractInvoiceDetailCustomerRel.ReceivableInvoiceDetail)
							LocalNewEFTExtractCompany		= NewEFTExtractOutput.Company
							LocalNewEFTExtractOutput		= NewEFTExtractOutput.EFTExtractOutput
							include PrintTotals
							invoke CreateEFTExtractFile EFTExtractOutputRel
