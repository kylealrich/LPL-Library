ContractGPOItemDelta is a BusinessClass
	owned by po
	
	prefix is CIXD
	
	Ontology
		symbolic key is ContractGPOItemDelta
		
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
	
	Persistent Fields
		ContractNumber               	is a GPOContractNumber  	
		ContractVendorNumber         	is a GPOSupplierID  		
		ContractVendorName           	is Alpha 80      		   	
		ContractVendorLegalName      	is Alpha 80       		  	
		ContractVendorTaxIDNumber    	is Alpha 9                  
		TierNumber                   	is like ContractImportTier 	
		TierName                     	is Alpha size 192		
		HealthTrustUIN				 	is like Item         		
		ManufacturerNumber           	is a GPOSupplierID 
		ManufacturerName             	is Alpha 250         		
		ManufacturerCatalogNumber    	is a ManufacturerNumber  	
		DistributorNumber            	is a GPOSupplierID      	
		DistributorName              	is Alpha 250            	
		DistributorReorderNumber     	is like VendorItem      	
		ItemShortDescription         	is Alpha 35
		ItemLongDescription          	is a Description3  			
		ItemFullDescription          	is Alpha 255
		UNSPSCCode                   	is AlphaUpper 8 			
		DistributorMarkupType        	is Alpha size 50
		ApplyDistributorMarkupIndicator is Boolean   				
		ContractUOMCode                 is like UnitOfMeasure       
		ContractUOMQuantityOfEach       is Unsigned Decimal size 13.7   
		ContractUnitPrice               is Unsigned Decimal size 17.4   
		CurrencyCode                    is like Currency
		ContractPriceEffectiveDate      is Date 					
		ContractPriceExpireDate         is Date 					
		RecordModifiedTimestamp         is Alpha 20		
		OperationFlag                   is Alpha 10         		
		
	Local Fields
	 	LocalContractGroup           is like ContractGroup
	 	LocalGPOContractNumber       is a GPOContractNumber  
	 	LocalVendorPart              is like VendorItem  
	 	LocalTier                    is like ContractImportTier 
	 	LocalItemSupplier            is like GPOSupplierID  
	 	LocalManufacturerSupplier    is like GPOSupplierID 
	 	LocalUOM                     is like UnitOfMeasure  
		LocalEffectiveDate           is Date  
		LocalHealthTrustUIN          is like Item 

	Conditions 

		HasInterfaceContract 
			restricted
			when (ContractImportByGPOContractRel exists)
	
	Relations
	
		ContractGPOItemRel
			one-to-many relation to ContractGPOItem 
			Field Mapping uses BySupplier
				related.GPOContractNumber                          = LocalGPOContractNumber
				related.ItemSupplier                               = LocalItemSupplier
			Instance Selection 
				where (related.TierLevel            = LocalTier
				and    related.UniversalItemNumber  = LocalHealthTrustUIN
				and    related.UOM                  = LocalUOM
				and    related.VendorPart           = LocalVendorPart
				and    related.StartDate            = LocalEffectiveDate)

	 	ContractImportByGPOContractRel
	 		one-to-many relation to ContractImport
	 		Field Mapping uses ByGPOContractNumber
	 			related.GPOContractNumber                           = LocalGPOContractNumber
				related.ContractGroup                               = LocalContractGroup			

	Sets
	 	ByGPOContractNumber 
	 		Sort Order
	 			ContractNumber
	 			ManufacturerCatalogNumber
				ContractGPOItemDelta
				
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassItemDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Purge 

		Purge is a Purge Action 

		LoadItemRecords is a Set Action 
			restricted 
			Parameters 
				ParmContractGroup is a ContractGroup
			
			Sort Order
				ContractNumber
				ManufacturerCatalogNumber
				ContractGPOItemDelta
			
			Action Rules 

				ContractNumber Set Rules 
					Exit Rules
						LocalGPOContractNumber = ContractNumber 
						LocalContractGroup     = ParmContractGroup 
						if (HasInterfaceContract)
							invoke FastUpdate first ContractImportByGPOContractRel
								invoked.LinesUpdated = true 

				Instance Rules 

					LocalContractGroup              = ParmContractGroup
					LocalGPOContractNumber 			= ContractNumber
				    LocalItemSupplier				= DistributorNumber			
					LocalManufacturerSupplier       = ContractVendorNumber
				    LocalTier                       = TierNumber 
				    LocalHealthTrustUIN             = HealthTrustUIN
				    LocalUOM                        = ContractUOMCode
				    LocalVendorPart                 = DistributorReorderNumber
				    LocalEffectiveDate 				= ContractPriceEffectiveDate 

					if (OperationFlag = "Add")
						if (ContractGPOItemRel !exists)
							invoke Create ContractGPOItem 
								invoked.GPOContractNumber 			= ContractNumber
								invoked.ManufacturerSupplier 		= ContractVendorNumber    
								invoked.TierLevel            		= TierNumber
								invoked.TierName                    = TierName 		
								invoked.UniversalItemNumber         = HealthTrustUIN
								invoked.ManufacturerNumber          = ManufacturerNumber 
								invoked.ManufacturerPart            = ManufacturerCatalogNumber
								invoked.ItemSupplier                = DistributorNumber            	        
								invoked.VendorPart                  = DistributorReorderNumber     	
								invoked.ItemShortDescription        = ItemShortDescription
								invoked.ItemDescription             = ItemLongDescription  
								invoked.ItemFullDescription         = ItemFullDescription
								invoked.UNSPSCCode                  = UNSPSCCode	
								invoked.DistributorMarkupType       = DistributorMarkupType
								invoked.ApplyDistributorMarkup		= ApplyDistributorMarkupIndicator  
								invoked.UOM                         = ContractUOMCode
								invoked.QuantityOfEach              = ContractUOMQuantityOfEach        
								invoked.Price                       = ContractUnitPrice               
								invoked.CurrencyCode                = CurrencyCode
								invoked.StartDate                   = ContractPriceEffectiveDate                              
								invoked.EndDate                     = ContractPriceExpireDate                                 
								invoked.RecordModifiedTimestamp     = RecordModifiedTimestamp		
								if (HasInterfaceContract)
									invoked.OperationFlag           = OperationFlag    

					else 
					if (OperationFlag = "Update")
						if (ContractGPOItemRel exists)
							invoke Update ContractGPOItemRel 
								invoked.ManufacturerSupplier 		= ContractVendorNumber    
								invoked.ManufacturerNumber          = ManufacturerNumber 
								invoked.ManufacturerPart            = ManufacturerCatalogNumber
								invoked.ItemShortDescription        = ItemShortDescription
								invoked.ItemDescription             = ItemLongDescription  
								invoked.ItemFullDescription         = ItemFullDescription
								invoked.UNSPSCCode                  = UNSPSCCode	
								invoked.DistributorMarkupType       = DistributorMarkupType
								invoked.ApplyDistributorMarkup		= ApplyDistributorMarkupIndicator  
								invoked.QuantityOfEach              = ContractUOMQuantityOfEach        
								invoked.Price                       = ContractUnitPrice               
								invoked.CurrencyCode                = CurrencyCode
								invoked.EndDate                     = ContractPriceExpireDate                                 
								invoked.RecordModifiedTimestamp     = RecordModifiedTimestamp		
								if (HasInterfaceContract)
									invoked.OperationFlag           = OperationFlag    


					else 
					if (OperationFlag = "DEL")
						if (ContractGPOItemRel exists)
							invoke Delete ContractGPOItemRel 





		
	
