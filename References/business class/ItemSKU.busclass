ItemSKU is a BusinessClass
    owned by ic
    prefix is ISK
    classic name is ITEMSKU

    Ontology
        symbolic key is ItemSKU
            classic set name is ISKSET1
            classic name is SKU

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        UnitOfMeasure      is a UnitOfMeasureEdit
            classic name for UnitOfMeasure.UnitOfMeasure is UOM
        UnitOfMeasureCost  is an InternationalCost
            classic name is UOM-COST
                precision is Item.NumberOfDecimalsCost
        SaleByManufacturer is Boolean
            classic name is SALE-BY-MFG
        SaleByDistributor  is Boolean
            classic name is SALE-BY-DISTR
        Descriptor         is AlphaUpper size 12
        VendorCaseCode     is AlphaUpper size 11
            classic name is VEN-CASE-CD
        ProductCode        is AlphaUpper size 2
            classic name is PRODUCT-CD
        InvoiceBreak       is AlphaUpper size 1
        ShelfLabelCode     is AlphaUpper size 1
            classic name is SHELF-LBL-CD
        ColorCode          is AlphaUpper size 10
            classic name is COLOR-CD
        SizeCode           is AlphaUpper size 10
            classic name is SIZE-CD
        StyleCode          is AlphaUpper size 10
            classic name is STYLE-CD
        FromWorksheetResult is an ItemInformationWorksheetResult

    Derived Fields

        ItemTypeSKU is a ConditionalField
            type is Numeric size 1
            restricted
            if (HasItemSKU)
                6
            else
                blank

    Conditions

        HasItemSKU
            classic name is SKU-ENTERED
            restricted
            when (ItemSKU entered)

		SKUDuplicated
			restricted
			when (DuplicateSKURel exists)
			
		UOMDuplicated
			restricted
			when (DuplicateUOMRel exists)
 
 	Relations
		DuplicateSKURel
			one-to-many relation to ItemSKU
			Field Mapping uses Set5
				related.ItemGroup		= ItemGroup
				related.ItemSKU 		= ItemSKU
			Instance Selection
				where (related.Item	!= Item)

		DuplicateUOMRel
			one-to-many relation to ItemSKU
			Field Mapping uses Set6
				related.ItemGroup		= ItemGroup
				related.Item			= Item
				related.UnitOfMeasure	= UnitOfMeasure
			Instance Selection
				where (related.ItemSKU != ItemSKU)
 
    Sets

        Set2
            indexed
            Sort Order
                ItemSKU
                ItemGroup

        Set5
            indexed
            Instance Selection
                where (HasItemSKU)
            Sort Order
                ItemGroup
                ItemSKU

        Set6
            indexed
            Sort Order
                ItemGroup
                Item
                UnitOfMeasure
                ItemSKU
                
        FromWorksheet
        	Sort Order
        		FromWorksheetResult
        		ItemGroup
        		Item
        		ItemSKU 

	Field Rules
		
		ItemSKU
			constraint (!Item.DrugPharmacyOrNarcoticUsage)
				"CannotDefineSKUForDrugsOrPharmacyOrNarcoticUsage"
			constraint (!SKUDuplicated)
				"SKUAlreadyExistsForAnotherItem:<first DuplicateSKURel.Item>"

		UnitOfMeasure
			required
				"UnitOfMeasureIsRequired"
			constraint (!UOMDuplicated)
				"AnotherSKUExistsWithTheSameUnitOfMeasure"

		UnitOfMeasureCost
			constraint (UnitOfMeasureCost decimals <= Item.NumberOfDecimalsCost)
				"TooManyDecimalsEntered:MaxIs<Item.NumberOfDecimalsCost>"	

	Actions
		
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

	Action Exit Rules
		invoke ItemDetailChanged Item 
		
