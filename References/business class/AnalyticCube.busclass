AnalyticCube is a BusinessClass
    owned by la
    prefix is ANA
    
    Ontology
    	symbolic key is AnalyticCube
    
    Patterns
    	implements CRUD
    	implements DynamicCreation
		disable Auditing
		disable EffectiveDated
	
	Persistent Fields
		LastReloadDate       is TimeStamp                
		LastRefreshDate      is TimeStamp                
		RecoveryDate         is TimeStamp                
		LastReloadExecTime   is a ElapsedTime            
			default label is "LastReloadExecutionTime "
			sql prefix is RLD
		LastRefreshExecTime  is a ElapsedTime            
			default label is "LastRefreshExecutionTime"
			sql prefix is REF
		Pause                is Boolean                  
		Verbose              is Boolean                  
		Report               is LPL                      
		CreateExportFile     is Boolean
		ExportFileName       is Alpha size 100
		Mode                 is an ACMode                
		RecoveryAction       is an ACRecoveryAction      
		Checkpoint           is LPLName                  
		CheckpointDACName    is Alpha size 50            
		LogsMigrated         is Boolean                  
	
	Transient Fields
		CubeStatus is Numeric size 1
			States
				Unknown     value is 0 
				Created     value is 1
				NoCubeName  value is 2 
				NoClassSpec value is 3 
				NotACube    value is 4 
				NotCreated  value is 5 
			derive value from CubeBuildStatus
			
	Field Rules
		Pause
			initial value is false
		Verbose
			initial value is false
		CreateExportFile
			initial value is false
			
	Derived Fields
		IsCubeLocked is a NativeField
			type is Boolean
			default label is "CubeLocked"
	
		CubeBuildStatus is a NativeField
			type is Numeric 1
			


		EnableRestrictedActions is a NativeField
			type is Boolean
			
		CubeIsRecoverable is a NativeField
			type is Boolean
			
		MaxFactRowReportSize is a NativeField
			type is Numeric size 4
			
		PreserveDACubes is a NativeField
			type is Boolean
			
		ConnectionType is a DerivedField
	        type is Numeric size 1
			if (BusinessClass.CubeType.InforBI)
				return 1
			else
			if (BusinessClass.CubeType.HyperCalc)
				return 2
			else
				return 0

		AsyncInProgressMessage is a MessageField
			restricted
			"ReloadInProgress"
			
		AsyncErrorMessage is a MessageField
			restricted
			"ReloadFailed"	
			
		AsyncWaitingMessage is a MessageField
			restricted
			"ReloadWaiting"				
		
		AsyncPendingMessage is a MessageField
			restricted
			"ReloadPending"						
		
		CubeIsActiveMessage is a MessageField
			restricted
			"CubeIsActiveAndWillProcessAllIncomingRequests"
		
		CubeIsDeactivatedMessage is a MessageField
			restricted
			"CubeIsDeactivatedAndWillSilentlyIgnoreAllIncomingRequests" 
		
		CubeReloadIsRunningMessage is a MessageField
			restricted
			"CubeIsBeingReloaded._AccessToThisCubeIsDisabledUntilTheReloadIsComplete"
		
		CubeReloadFailedMessage is a MessageField
			restricted
			"<CubeNonrecoverableErrorMessage>_<CubeAccessDisabledUntilReloadMessage>_<CubeReloadRequiresFullReloadMessage>"
		
		CubeReloadFailedRecoverableMessage is a MessageField
			restricted
			"<CubeNonrecoverableErrorMessage>_<CubeAccessDisabledUntilReloadMessage>_<CubeReloadNotRequiresReloadMessage>"
		
		CubeReloadConnectionFailedMessage is a MessageField
			restricted
			"<CubeReloadFailedMessage>ThisErrorDoesNotPreventReadAccessToThisCube"
		
		CubeReloadConnectionFailedRecoverableMessage is a MessageField
			restricted
			"<CubeReloadFailedRecoverableMessage>ThisErrorDoesNotPreventReadAccessToThisCube"
		
		CubeReloadInterruptedMessage is a MessageField
			restricted
			"<CubeReloadFailedMessage>"
		
		CubeReloadInterruptedRecoverableMessage is a MessageField
			restricted
			"<CubeReloadFailedRecoverableMessage>"
		
		CubeRefreshIsRunningMessage is a MessageField
			restricted
			"CubeIsBeingRefreshed._AccessToThisCubeIsDisabledUntilTheRefreshIsComplete"
		
		CubeRefreshFailedMessage is a MessageField
			restricted
			"<CubeNonrecoverableErrorMessage>_<CubeAccessDisabledUntilReloadMessage>_<CubeRefreshRequiresReloadMessage>"
		
		CubeRefreshFailedRecoverableMessage is a MessageField
			restricted
			"<CubeRecoverableErrorMessage>_<CubeAccessDisabledUntilRefreshMessage>_<CubeRefreshNotRequiresReloadMessage>"

		CubeRefreshConnectionFailedMessage is a MessageField
			restricted
			"<CubeRefreshFailedMessage>ThisErrorDoesNotPreventReadAccessToThisCube."
		
		CubeRefreshConnectionFailedRecoverableMessage is a MessageField
			restricted
			"<CubeRefreshFailedRecoverableMessage>ThisErrorDoesNotPreventReadAccessToThisCube."
		
		CubeRefreshInterruptedMessage is a MessageField
			restricted
			"<CubeRefreshFailedMessage>"
		
		CubeRefreshInterruptedRecoverableMessage is a MessageField
			restricted
			"<CubeRefreshFailedRecoverableMessage>"
		
		CubeIsLockedMessage is a MessageField
			restricted
			"CubeConfigurationIsBeingDeployed._AccessToThisCubeIsDisabledUntilTheDeploymentIsComplete"

		CubeReloadWaitingOnSyncLockMessage is a MessageField
			restricted
			"CubeReloadIsWaitingToAcquireTheSynchronizeLock"

		CubeRefreshWaitingOnSyncLockMessage is a MessageField
			restricted
			"CubeRefreshIsWaitingToAcquireTheSynchronizeLock"


		CubeRecoverableErrorMessage is a MessageField
			restricted
			"FailedWithARecoverableError."
		CubeNonrecoverableErrorMessage is a MessageField
			restricted
			"FailedWithANonrecoverableError."

		CubeReloadRequiresFullReloadMessage is a MessageField
			restricted
			"TheNextTimeReloadIsRunItWillBeRunAsFullReload."
		CubeReloadNotRequiresReloadMessage is a MessageField
			restricted
			"TheNextTimeReloadIsRunItWillBeRecoveredFromThePointOfFailure."

		CubeRefreshRequiresReloadMessage is a MessageField
			restricted
			"TheNextTimeRefreshIsRunItWillBePromotedToAReload."
		CubeRefreshNotRequiresReloadMessage is a MessageField
			restricted
			"TheNextTimeRefreshIsRunItWillBeRecoveredFromThePointOfFailure."

		CubeAccessDisabledUntilReloadMessage is a MessageField
			restricted
			"AccessToThisCubeIsDisabledUntilReloadIsSuccessfullyRunOnThisCube."
		CubeAccessDisabledUntilRefreshMessage is a MessageField
			restricted
			"AccessToThisCubeIsDisabledUntilRefreshIsSuccessfullyRunOnThisCube."

		
		ModeStatusMessage is a DerivedField
			type is MessageField
    		if (Mode.Active)
    			return CubeIsActiveMessage
    			
    		if (Mode.Deactivated)
    			return CubeIsDeactivatedMessage
    			
    		if (Mode.ReloadRunning)
    			return CubeReloadIsRunningMessage
    			
    		if (Mode.ReloadFailed)
    			if (CubeIsRecoverable)
					return CubeReloadFailedRecoverableMessage
    			else
    				return CubeReloadFailedMessage
    			
    		if (Mode.ReloadConnectionFailed)
    			if (CubeIsRecoverable)
					return CubeReloadConnectionFailedRecoverableMessage
    			else
	    			return CubeReloadConnectionFailedMessage
    			
    		if (Mode.ReloadInterrupted)
    			if (CubeIsRecoverable)
					return CubeReloadInterruptedRecoverableMessage
    			else
	    			return CubeReloadInterruptedMessage
    			
    		if (Mode.RefreshRunning)
    			return CubeRefreshIsRunningMessage
    			
    		if (Mode.RefreshFailed)
    			if (CubeIsRecoverable)
					return CubeRefreshFailedRecoverableMessage
    			else
	    			return CubeRefreshFailedMessage
    			
    		if (Mode.RefreshConnectionFailed)
    			if (CubeIsRecoverable)
					return CubeRefreshConnectionFailedRecoverableMessage
    			else
	    			return CubeRefreshConnectionFailedMessage
    			
    		if (Mode.RefreshInterrupted)
    			if (CubeIsRecoverable)
					return CubeRefreshInterruptedRecoverableMessage
    			else
	    			return CubeRefreshInterruptedMessage
    			
    		if (Mode.ReloadWaitingOnSyncLock)
    			return CubeReloadWaitingOnSyncLockMessage
    			
    		if (Mode.RefreshWaitingOnSyncLock)
    			return CubeRefreshWaitingOnSyncLockMessage
    			
    		if (Mode.DeployingConfiguration)
	    		if (AsyncActionRequestInProgressRel exists)
	    			return AsyncInProgressMessage
				else    			
	    		if (AsyncActionRequestErrorRel exists)
	    			return AsyncErrorMessage
	    		else
	    		if (AsyncActionRequestUnfinishedRel exists)
	   				return AsyncWaitingMessage
	    		else	
	   			if (AsyncActionRequestPendingRel exists)
	   				return AsyncPendingMessage
	   				
	   			return CubeIsLockedMessage
	   				
		ActiveConfigurationWithPendingMessage is a MessageField
			restricted
			"ActiveConfigurationWithPendingConfigurationChanges"

		ActiveConfigurationWithPendingDeleteMessage is a MessageField
			restricted
			"ActiveConfigurationWithPendingDelete"

		ActiveConfigurationMessage is a MessageField
			restricted
			"ActiveConfiguration"

		PendingConfigurationMessage is a MessageField
			restricted
			"PendingConfigurationChanges"

		PendingConfigurationDeleteMessage is a MessageField
			restricted
			"ConfigurationPendingDelete"

		UserCubeDeploymentMessage  is a DerivedField
    		type is MessageField

    		if (ActiveDeployedConfiguration)
    			if (DeployedConfigurationPendingDeploy)
    				return ActiveConfigurationWithPendingMessage
    			else
				if (DeployedConfigurationPendingDelete)    			
					return ActiveConfigurationWithPendingDeleteMessage
				else
					return ActiveConfigurationMessage
			else
			if (DeployedConfigurationPendingDeploy)
    				return PendingConfigurationMessage
			else
			if (DeployedConfigurationPendingDelete)    			
				return PendingConfigurationDeleteMessage
	
			return ""				
		



























			
	Sets
		AnalyticCubeSet
			Sort Order
				BusinessClass
				
	Conditions
		IsRefreshAllowed
			default label is "RefreshAllowed"
			when (BusinessClass.ImplementsAuditing and not CubeIsDeployingConfiguration) 			

		CanMakeActive
			restricted
			when (EnableRestrictedActions and not Mode.Active)

		CanReloadInForeground
			restricted
			when (EnableRestrictedActions and not CubeIsDeployingConfiguration)
			
		CanRefreshInForeground
			restricted
			when (IsRefreshAllowed and EnableRestrictedActions)
			
		CanCreateMDXQuery
			restricted
			when (EnableRestrictedActions)
			
		IsConnectionHealthy
			default label is "ConnectionHealthy"
			when (OLAPConnectionRel.Status.Healthy)

		IsCubeError
			default label is "CubeError"
			when (not CubeStatus.Created and not CubeStatus.Unknown)

		IsInforBICube 
			default label is "InforBICube"
			when (BusinessClass.CubeType.InforBI)
	        
		CanRunCreateReportInForeground
			restricted
			when (EnableRestrictedActions)

		CanRunRefreshReportInForeground
			restricted
			when (EnableRestrictedActions and CanRunRefreshReport)

		CanRunRefreshReport
			restricted
			when (IsRefreshAllowed and IsInforBICube)

		HasUserCube
			default label is "ConfigurationExists"
			when (UserCubeRel exists)
			
		HasUserCubeDeployment
			default label is "DeployedConfigurationExists"
			when (DeployedUserCubeRel exists)
						
		HasPendingUserCubeDeployment
			default label is "PendingUserCubeDeploymentExists"			
			when (DeployedUserCubeRel.DeployStatus.PendingDeploy
			or   (UserCubeRel exists
			and   UserCubeRel.Status.Active))

		CubeIsDeployingConfiguration
			when (Mode.DeployingConfiguration)
						
		CubeIsLocked
			when (Mode.DeployingConfiguration
			or	Mode.RefreshRunning
			or	Mode.RefreshFailed
			or	Mode.RefreshInterrupted
			or	Mode.ReloadRunning
			or	Mode.ReloadFailed		
			or	Mode.ReloadInterrupted
			or  Mode.ReloadWaitingOnSyncLock
			or  Mode.RefreshWaitingOnSyncLock)
			
		CubeIsRunningRefresh
			when (Mode.RefreshRunning)
			
		CubeIsRunningReload
			when (Mode.ReloadRunning)	

		CubeIsActive
			when (Mode.Active)		
			
		CubeIsDeactivated
			when (Mode.Deactivated)		
			
		ActiveDeployedConfiguration
			when (DeployedUserCubeRel exists
			and   DeployedUserCubeRel.Status.Active)		
		
		DeployedConfigurationPendingDeploy
			when (DeployedUserCubeRel.DeployStatus.PendingDeploy)
			
		DeployedConfigurationPendingDelete
			when (DeployedUserCubeRel.DeployStatus.PendingDelete)	
			
		DeployedConfigurationPending
			when (DeployedConfigurationPendingDeploy
			or    DeployedConfigurationPendingDelete)
			
		ShowDeployedYellowAlert
			when (DeployedConfigurationPending
			and   not ActiveDeployedConfiguration)		
			
		ShowModeYellowAlert
			when (Mode.DeployingConfiguration
			or    Mode.ReloadRunning		
			or    Mode.RefreshRunning		
			or    Mode.ReloadWaitingOnSyncLock
			or    Mode.RefreshWaitingOnSyncLock)
			
		ShowModeRedAlert
			when (Mode.ReloadFailed		
			or    Mode.ReloadInterrupted
			or    Mode.ReloadConnectionFailed
			or    Mode.RefreshFailed
			or    Mode.RefreshInterrupted
			or    Mode.RefreshConnectionFailed
			or    Mode.Deactivated)	
			
		ShowIsCubeLockedRedAlert
			default label is "ShowCubeLockedRedAlert"
			when (not Mode.Active
			and   not Mode.ReloadRunning
			and   not Mode.RefreshRunning
			and   not Mode.ReloadWaitingOnSyncLock
			and   not Mode.RefreshWaitingOnSyncLock
			and   IsCubeLocked)		

		RefreshedInLastDay
            when ((current timestamp - LastRefreshDate <= 86400))  

		ReloadedInLastDay
			when ((current timestamp - LastReloadDate <= 86400))  

    Relations    

		ThisInstanceRel
			one-to-one relation to AnalyticCube
			Field Mapping uses symbolic key
				related.BusinessClass = BusinessClass

		CubeDimensionRel
			one-to-many relation to CubeDimension
			Field Mapping uses symbolic key
				related.BusinessClass = BusinessClass

		ACInvalidFactRowReportsRel
			one-to-many relation to ACInvalidFactRowReport
			Field Mapping uses ByRunDate
				related.BusinessClass = BusinessClass

		ACRefreshReportsRel
			one-to-many relation to ACRefreshReport
			Field Mapping uses ByRunDate
				related.BusinessClass = BusinessClass

		ACDefinitionReportsRel
			one-to-many relation to ACDefinitionReport
			Field Mapping uses ByRunDate
				related.BusinessClass = BusinessClass

		OLAPConnectionRel
			one-to-one relation to OLAPConnection
			Field Mapping uses symbolic key
				related.OLAPConnection = BusinessClass.CubeType
				
		DeployedUserCubeRel
			default label is "DeployedUserCube"
			one-to-one relation to UserCubeDeployed
			Field Mapping uses ByClass
				related.BusinessClass = BusinessClass

		UserCubeRel
			default label is "UserCube"
			one-to-one relation to UserCube
			Field Mapping uses ByClass
				related.BusinessClass = BusinessClass	
				
		AsyncActionRequestPendingRel
			default label is "PendingReload"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByClass
				related.ImplementingClass 	= "AnalyticCube"
				related.AsyncAction			= "Reload"
			Instance Selection
				where (related.DataArea like parentcontext.dataarea 
				and    related.MappingField1 = BusinessClass
				and    related.PendingScheduling)	
				
		AsyncActionRequestErrorRel
			default label is "ReloadErrors"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByClass
				related.ImplementingClass 	= "AnalyticCube"
				related.AsyncAction			= "Reload"
			Instance Selection
				where (related.DataArea like parentcontext.dataarea 
				and    related.MappingField1 = BusinessClass
				and    related.FailedTriggersExist)			
													
		AsyncActionRequestInProgressRel
			default label is "ReloadInProgress"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByClass
				related.ImplementingClass 	= "AnalyticCube"
				related.AsyncAction			= "Reload"
			Instance Selection
				where (related.DataArea like parentcontext.dataarea 
				and    related.MappingField1 = BusinessClass
				and    related.InProgress)	
				
		AsyncActionRequestUnfinishedRel
			default label is "ReloadUnfinished"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByClass
				related.ImplementingClass 	= "AnalyticCube"
				related.AsyncAction			= "Reload"
			Instance Selection
				where (related.DataArea like parentcontext.dataarea 
				and    related.MappingField1 = BusinessClass
				and    related.NonFinishedTriggersExist)						

	Actions
		Create is a Create Action
			Action Rules
				constraint (BusinessClass.ImplementsAnalyticCube or HasPendingUserCubeDeployment)
					"Business\Class<BusinessClass>DoesNotContainAnAnalyticCubeDefinition."
					
		Delete is a Delete Action 
			
		Update is an Update Action
		
		Import is an Import Action
			Action Rules




		
				if (ThisInstanceRel not exists)
					invoke Create this instance
						invoked.BusinessClass = BusinessClass
		
		ReloadInForeground is an Instance Action   
			valid when (CanReloadInForeground)

		RefreshInForeground is an Instance Action
			valid when (CanRefreshInForeground)	
			
		Reload is an Instance Action   
			valid when (not CubeIsDeployingConfiguration)
			run in background
			synchronized on "<BusinessClass>-Cube"
			Queue Mapping Fields
				BusinessClass
				
		Refresh is an Instance Action
			valid when (IsRefreshAllowed)
			run in background
			synchronized on "<BusinessClass>-Cube"
			Queue Mapping Fields
				BusinessClass

		ReloadToDeployConfiguration is an Instance Action   
			run in background
			restricted
			Action Rules
				invoke Reload
			Queue Mapping Fields
				BusinessClass
								
		ScheduledReload is an Instance Action   
			valid when (not CubeIsDeployingConfiguration)
			run in background
			synchronized on "<BusinessClass>-Cube"
			restricted
			Action Rules
				invoke Refresh
			Queue Mapping Fields
				BusinessClass
				
		ScheduledRefresh is an Instance Action  
			valid when (IsRefreshAllowed)		
			run in background
			synchronized on "<BusinessClass>-Cube"
			restricted
			Action Rules
				invoke Refresh
			Queue Mapping Fields
				BusinessClass
				
		ReloadWithOptions is an Instance Action   
			valid when (not CubeIsDeployingConfiguration)
			synchronized on "<BusinessClass>-Cube"
			restricted
			Parameters
				ExportFileName is Alpha size 1000
				IsExportOnly   is Boolean
					default label is "ExportOnly"
				Verbose        is Boolean
				
		RefreshWithOptions is an Instance Action
			valid when (IsRefreshAllowed)	
			synchronized on "<BusinessClass>-Cube"
			restricted
			Parameters
				ExportFileName is Alpha size 1000
				IsExportOnly   is Boolean
					default label is "ExportOnly"
				Verbose        is Boolean

		ReloadDimensionsWithOptions is an Instance Action   
			valid when (not CubeIsDeployingConfiguration)
			synchronized on "<BusinessClass>-Cube"
			restricted
			Parameters
				ExportFileName is Alpha size 1000
				IsExportOnly   is Boolean
					default label is "ExportOnly"
				Verbose        is Boolean
				
		RefreshDimensionsWithOptions is an Instance Action
			valid when (IsRefreshAllowed)	
			synchronized on "<BusinessClass>-Cube"
			restricted
			Parameters
				ExportFileName is Alpha size 1000
				IsExportOnly   is Boolean
					default label is "ExportOnly"
				Verbose        is Boolean

		RefreshOLAPConnections is an Instance Action

		TestOLAPBackgroundConnection is an Instance Action
			run in background
			Action Rules
				display "<OLAPConnectionRel.StatusMessage>"
			
		Deactivate is an Instance Action
			valid when (CubeIsActive)
			confirmation required
				"DeactivatingACubeWillCauseAllReload,RefreshAndReadRequestsToBeIgnoredAndReturnWithoutError.AreYouSure?"
			Action Rules
				Mode = Mode.Deactivated
				
				invoke FireEvent

		Activate is an Instance Action
			valid when (CubeIsDeactivated)
			confirmation required
				"ActivatingACubeWillCauseAllReload,RefreshAndReadRequestsToBeProcessedAsNormal.AreYouSure?"
			Action Rules
				Mode = Mode.Active
				
				invoke FireEvent

		LockForConfiguration is an Instance Action
			restricted

			confirmation required
				"LockingACubePreventsAccessAndCanBlockOtherProcesses.AreYouSure?"
			Action Rules
				Mode = Mode.DeployingConfiguration
				
				invoke FireEvent
				
		MakeActive is an Instance Action
			valid when (CanMakeActive)
			confirmation required
				"MakingACubeActiveOpensAccessAndShouldOnlyBeDoneWhenTheCubeUpdateHasCompleted.AreYouSure?"
			Action Rules
				if (CubeIsRunningRefresh)
					Mode = Mode.RefreshFailed
				else
				if (CubeIsRunningReload)
					Mode = Mode.ReloadFailed
				else
					Mode = Mode.Active
					Checkpoint = blank
				
				invoke FireEvent		
				
		FireEvent is an Instance Action 
			restricted 
			
		FireEventImmediate is an Instance Action 
			restricted 		
			manual transaction	

		DeleteCubeLogs is an Instance Action
			valid when (EnableRestrictedActions)	
			confirmation required
				"RunningThisActionWillDeleteAllOfTheReloadAndRefreshLogs.Continue?"
			Action Rules
				invoke Delete AnalyticCubeLog set 

		DeleteReloadLogs is an Instance Action
			valid when (EnableRestrictedActions)	
			confirmation required
				"RunningThisActionWillDeleteAllOfTheReload.Continue?"
			Action Rules
				invoke Delete ACReloadLog set 

		DeleteRefreshLogs is an Instance Action
			valid when (EnableRestrictedActions)	
			confirmation required
				"RunningThisActionWillDeleteAllOfTheRefreshLogs.Continue?"
			Action Rules
				invoke Delete ACRefreshLog set 

		CreateDACubes is an Instance Action
			manual update

			confirmation required
				"ThisActionWillCreateDACubesInTheOLAPServerIfTheyDoNotAlreadyExist.Continue?"
			Action Rules
				invoke CreateDACube CubeDimensionRel 

		DeleteDACubes is an Instance Action
			manual update
			valid when (PreserveDACubes)
			confirmation required
				"DeletingTheDACubeRecordsCanResultInLostSecuritySettings.Continue?"
			Action Rules
				invoke DeleteDACube CubeDimensionRel 

		ExportDACubes is an Instance Action
			manual update
			valid when (PreserveDACubes)
			confirmation required
				"ExportingTheDACubesWillReplaceTheCurrentDACubesWithTheArchivedData.Continue?"
			Action Rules
				invoke ExportDACube CubeDimensionRel 

		ExportDACubesFromBackup is an Instance Action
			manual update
			valid when (PreserveDACubes)
			confirmation required
				"ExportingTheDACubesWillReplaceTheCurrentDACubesWithTheArchivedData.Continue?"
			Action Rules
				invoke ExportDACubeFromBackup CubeDimensionRel 

		ImportDACubes is an Instance Action
			manual update
			valid when (PreserveDACubes)
			confirmation required
				"ImportingTheDACubesWillReplaceTheCurrentArchiveWithTheCurrentDACubes.Continue?"
			Action Rules
				invoke ImportDACube CubeDimensionRel 

		CreateDimensionSurrogatesInForeground is an Instance Action
			valid when (EnableRestrictedActions)	
			Parameters
				CreateAllDimensions is Boolean
				SelectedDimensions  is a ACDimensions
				
			Parameter Rules
				CreateAllDimensions
					initial value is true

					if (not CreateAllDimensions)                       // This rule does not fire because CreateAllDimensions is "not entered"
						constraint (SelectedDimensions entries > 0)    // and only "required" rules fire for "not entered" values.
							"MustIncludeAtLeastOneDimensionToCreate"
					
				SelectedDimensions
					if (not CreateAllDimensions)
						required
							"MustIncludeAtLeastOneDimensionToCreate"
		

		CreateDimensionSurrogates is an Instance Action
			run in background
			Queue Mapping Fields
				BusinessClass
			Parameters
				CreateAllDimensions is Boolean
				SelectedDimensions  is a ACDimensions
				
			Parameter Rules
				CreateAllDimensions
					initial value is true

					if (not CreateAllDimensions)                       // This rule does not fire because CreateAllDimensions is "not entered"
						constraint (SelectedDimensions entries > 0)    // and only "required" rules fire for "not entered" values.
							"MustIncludeAtLeastOneDimensionToCreate"
					
				SelectedDimensions
					if (not CreateAllDimensions)
						required
							"MustIncludeAtLeastOneDimensionToCreate"
		
		DeleteDimensionSurrogates is an Instance Action
			confirmation required
				"RunningThisActionWillDeleteAllOfTheDimensionSurrogates.Continue?"
			Action Rules

				invoke Delete ACDimensionSurrogateCreateReq set

		CreateInvalidFactRowsReportInForeground is an Instance Action
			valid when (EnableRestrictedActions)	
			Parameters
		    	Description                is Alpha size 30
				MaximumNumberOfReportLines is Numeric size 6
				ShowBlankDimensionValues   is Boolean
				CountInvalidFactRows       is Boolean
				UseDimensionSurrogates     is Boolean
				ReportOnAllDimensions      is Boolean
				SelectedDimensions         is a ACDimensions
				
			Parameter Rules
		    	Description
					initial value is BusinessClass
					
				ShowBlankDimensionValues 
					initial value is false
					
				CountInvalidFactRows
					initial value is false
					
					if (CountInvalidFactRows)
						constraint (not ReportOnAllDimensions)
							"TheReportCanOnlyHaveOneDimensionWhenCountInvalidFactRowsIsSet"
						constraint (SelectedDimensions entries > 0)
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				MaximumNumberOfReportLines 
					initial value is 100

					constraint (MaximumNumberOfReportLines <= MaxFactRowReportSize)
						"MaximumNumberOfReportLinesCanNotBeLargerThan<MaxFactRowReportSize>"
						
				UseDimensionSurrogates
					initial value is true
					
				ReportOnAllDimensions
					initial value is true

					if (not ReportOnAllDimensions)                     // This rule does not fire because ReportOnAllDimensions is "not entered"
						constraint (SelectedDimensions entries > 0)    // and only "required" rules fire for "not entered" values.
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				SelectedDimensions
					if (not ReportOnAllDimensions)
						required
							"MustIncludeAtLeastOneDimensionInTheReport"






	
		CreateInvalidFactRowsReport is an Instance Action
			run in background
			Queue Mapping Fields
				BusinessClass
			Parameters
		    	Description                is Alpha size 30
				ShowBlankDimensionValues   is Boolean
				MaximumNumberOfReportLines is Numeric size 6
				CountInvalidFactRows       is Boolean
				UseDimensionSurrogates     is Boolean
				ReportOnAllDimensions      is Boolean
				SelectedDimensions         is a ACDimensions

			Parameter Rules
		    	Description
					initial value is BusinessClass
					
				ShowBlankDimensionValues 
					initial value is false
					
				CountInvalidFactRows
					initial value is false
					
					if (CountInvalidFactRows)
						constraint (not ReportOnAllDimensions)
							"TheReportCanOnlyHaveOneDimensionWhenCountInvalidFactRowsIsSet"
						constraint (SelectedDimensions entries > 0)
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				MaximumNumberOfReportLines 
					initial value is 100

					constraint (MaximumNumberOfReportLines <= MaxFactRowReportSize)
						"MaximumNumberOfReportLinesCanNotBeLargerThan<MaxFactRowReportSize>"
						
				ReportOnAllDimensions
					initial value is true

					if (not ReportOnAllDimensions)                     // This rule does not fire because ReportOnAllDimensions is "not entered"
						constraint (SelectedDimensions entries > 0)    // and only "required" rules fire for "not entered" values.
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				SelectedDimensions
					if (not ReportOnAllDimensions)
						required
							"MustIncludeAtLeastOneDimensionInTheReport"
						
					if (CountInvalidFactRows)
						constraint (SelectedDimensions entries = 1)
							"TheReportCanOnlyHaveOneDimensionWhenCountInvalidFactRowsIsSet"
	
		DeleteInvalidFactRowReports is an Instance Action
			confirmation required
				"RunningThisActionWillDeleteAllOfTheFactRowReports.Continue?"
			Action Rules
				invoke Delete ACInvalidFactRowReport set 

		CreateInvalidDimensionsReportInForeground is an Instance Action
			valid when (EnableRestrictedActions)	
			Parameters
		    	Description                is Alpha size 30
				MaximumNumberOfReportLines is Numeric size 6
				ShowBlankDimensionValues   is Boolean
				CountInvalidDimensions     is Boolean
				ReportOnAllDimensions      is Boolean
				SelectedDimensions         is a ACDimensions
				
			Parameter Rules
		    	Description
					initial value is BusinessClass
					
				ShowBlankDimensionValues 
					initial value is false
					
				CountInvalidDimensions
					initial value is false
					
					if (CountInvalidDimensions)
						constraint (not ReportOnAllDimensions)
							"TheReportCanOnlyHaveOneDimensionWhenCountInvalidDimensionsIsSet"
						constraint (SelectedDimensions entries > 0)
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				MaximumNumberOfReportLines 
					initial value is 100

					constraint (MaximumNumberOfReportLines <= MaxFactRowReportSize)
						"MaximumNumberOfReportLinesCanNotBeLargerThan<MaxFactRowReportSize>"
						
				ReportOnAllDimensions
					initial value is true

					if (not ReportOnAllDimensions)                     // This rule does not fire because ReportOnAllDimensions is "not entered"
						constraint (SelectedDimensions entries > 0)    // and only "required" rules fire for "not entered" values.
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				SelectedDimensions
					if (not ReportOnAllDimensions)
						required
							"MustIncludeAtLeastOneDimensionInTheReport"






	
		CreateInvalidDimensionsReport is an Instance Action
			run in background
			Queue Mapping Fields
				BusinessClass
			Parameters
		    	Description                is Alpha size 30
				MaximumNumberOfReportLines is Numeric size 6
				CountInvalidDimensions     is Boolean
				ReportOnAllDimensions      is Boolean
				SelectedDimensions         is a ACDimensions

			Parameter Rules
		    	Description
					initial value is BusinessClass
					
				CountInvalidDimensions
					initial value is false
					
					if (CountInvalidDimensions)
						constraint (not ReportOnAllDimensions)
							"TheReportCanOnlyHaveOneDimensionWhenCountInvalidDimensionsIsSet"
						constraint (SelectedDimensions entries > 0)
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				MaximumNumberOfReportLines 
					initial value is 100

					constraint (MaximumNumberOfReportLines <= MaxFactRowReportSize)
						"MaximumNumberOfReportLinesCanNotBeLargerThan<MaxFactRowReportSize>"
						
				ReportOnAllDimensions
					initial value is true

					if (not ReportOnAllDimensions)                     // This rule does not fire because ReportOnAllDimensions is "not entered"
						constraint (SelectedDimensions entries > 0)    // and only "required" rules fire for "not entered" values.
							"MustIncludeAtLeastOneDimensionInTheReport"
					
				SelectedDimensions
					if (not ReportOnAllDimensions)
						required
							"MustIncludeAtLeastOneDimensionInTheReport"
						
					if (CountInvalidDimensions)
						constraint (SelectedDimensions entries = 1)
							"TheReportCanOnlyHaveOneDimensionWhenCountInvalidDimensionsIsSet"
	
		DeleteInvalidDimensionReports is an Instance Action
			confirmation required
				"RunningThisActionWillDeleteAllOfTheDimensionReports.Continue?"
			Action Rules
				invoke Delete ACInvalidDimensionReport set 

		CreateMDXQuery is an Instance Action
			valid when (CanCreateMDXQuery)	
			Parameters
				PrmQueryName   is like ACMDXQuery
				    default label is untranslatable
				PrmDescription is Alpha size 60
				    default label is untranslatable
				PrmMDXQuery    is LPL
				    default label is untranslatable
				
			Parameter Rules

			Local Fields
				
			Entrance Rules

            Action Rules
            	invoke Create ACMDXQuery
            		invoked.BusinessClass = BusinessClass
            		invoked.AnalyticCube  = AnalyticCube
            		invoked.ACMDXQuery    = PrmQueryName
            		invoked.Description   = PrmDescription
            		invoked.MDXQuery      = PrmMDXQuery
				
		DeleteMDXQueries is an Instance Action
			valid when (CanCreateMDXQuery)
			confirmation required
				"RunningThisActionWillDeleteAllOfTheQueries.Continue?"
			Action Rules
				invoke Delete ACMDXQuery set 

		CreateRefreshReport is an Instance Action
			run in background
			valid when (CanRunRefreshReport)
			Parameters
				ReportLastRefreshDate     is a RunDate
				ReportRefreshAsOfDate     is a RunDate
			Parameter Rules
				ReportLastRefreshDate.Type 
					initial value is 0
				ReportRefreshAsOfDate.Type 
					initial value is 0
				
		CreateRefreshReportInForeground is an Instance Action
			valid when (CanRunRefreshReportInForeground)	
			Parameters
				ReportLastRefreshDate     is a RunDate
				ReportRefreshAsOfDate     is a RunDate
			Parameter Rules
				ReportLastRefreshDate.Type 
					initial value is 0
				ReportRefreshAsOfDate.Type 
					initial value is 0
				
		DeleteRefreshReports is an Instance Action
			confirmation required
				"RunningThisActionWillDeleteAllOfTheRefreshReports.Continue?"
			Action Rules
				invoke Delete ACRefreshReport set 

		CreateDefinitionReportInForeground is an Instance Action
			valid when (CanRunCreateReportInForeground)
			Parameters
				ReportType is a CubeReportType
				AllCubes   is Boolean
				
		CreateDefinitionReport is an Instance Action
			run in background
			Parameters
				ReportType is a CubeReportType
				AllCubes   is Boolean
				
		DeleteDefinitionReports is an Instance Action
			confirmation required
				"RunningThisActionWillDeleteAllOfTheCubeDefinitionReports.Continue?"
			Action Rules
				invoke Delete ACDefinitionReport set 

				
