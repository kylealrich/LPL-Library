IONInboxBodProcessor is a BusinessClass
    owned by la
    prefix is IONP
    stored in environment
    contains environment data 
	disable data area copy
		preserve target data
    default label is "ION_InboxBOD_Processor"

    Ontology
    	symbolic key is IONInboxBodProcessor

	Patterns
		disable AsOfDateProcessing
		disable EffectiveDated
		
    Persistent Fields
    	DataArea
    	BusinessTask
    		data area is DataArea
    		default label is "Task"
    	BusinessAction
    		data area is DataArea
    		default label is "Action"
    	IONBodType is Alpha size 42
    		default label is "BOD_Type"
    	ToLogicalId is Alpha size 250
		Status 	is Numeric size 1
			States
				Active				value is 0
				Inactive			value is 1    	
    	
    Conditions 
    	Active
    		when (Status = Status.Active)
    		
  	Field Rules
  		IONBodType
  			constraint IONBodType = blank or IONBodType matches "([A-Za-z\*])+\.([A-Za-z\*])+"
				"InvalidBOD_TypeMustBeForm_'noun.\verb'"
        
        DataArea
    		initial value is parentcontext.dataarea
			default to parentcontext.dataarea
        	
		BusinessTask
			required
			
		BusinessAction
			required

	Sets
		ByBusinessTask
			no duplicates
			Sort Order
				BusinessTask
				BusinessAction
				IONBodType
			
				
	Actions
		Activate is an Instance Action
			valid when (not Status.Active)
			Action Rules
				Status = Status.Active
				
		Deactivate is an Instance Action
			valid when (Status.Active)
			Action Rules
				Status = Status.Inactive
	
		Create is a Create Action
		Update is an Update Action
		Delete is a Delete Action
