InvoicedNotReceivedResult is a BusinessClass
	owned by ma
	
	prefix is INRRs
	
	Ontology
		symbolic key is InvoicedNotReceivedResult
		
	Patterns
        disable AuditIndex
		disable Auditing   
       	disable EffectiveDated
		implements CreateStamp	
	
	Persistent Fields
		RunTime								is TimeStamp

        PrmCompany                 			is a PayablesCompany
        	default label is "Company"
        PrmCompanyGroup            			is a GeneralLedgerCompanyGroup
        	default label is "CompanyGroup"
		PrmVendorRange						is a VendorNumberRange
		PrmSkipDays            	    		is Numeric 3

		ProcessingComplete					is Boolean
		
	Transient Fields
		TransientBlank						is Alpha 1
			
	Derived Fields
	
		NoRecordsToProcessMsg is a MessageField
			restricted
			"NoRecordsToProcess"
			 
		InProcessMessage is a MessageField
        	restricted
        	"InProcess"
        	
		CompleteMessage is a MessageField
        	restricted
        	"Complete"
        	
		NoRecordsFoundForCriteriaMsg is a MessageField
			restricted
			"***NoRecordsFoundForCriteriaEntered***"

		DerivedVendorRange is a StringField
			type is Alpha 100
			PrmVendorRange.Begin 
			" - "
			PrmVendorRange.End

		DerivedRunDate is a DerivedField
			type is Date
			default label is "RunDate"
			return RunTime date
			
		DerivedRunTime is a DerivedField
			type is Time
			default label is "RunTime"
			return RunTime time

		DerivedTitleNoParameters is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>"
			
		DerivedTitleVendorRange is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_VendorRange<DerivedVendorRange>"

		DerivedTitleSkipDays is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_SkipDays<PrmSkipDays>"

		DerivedTitleVendorRangeSkipDays is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_VendorRange<DerivedVendorRange>_SkipDays<PrmSkipDays>"

		DerivedTitleCompany is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_Company<PrmCompany>"

		DerivedTitleCompanyVendorRange is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_Company<PrmCompany>_VendorRange<DerivedVendorRange>"

		DerivedTitleCompanySkipDays is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_Company<PrmCompany>_SkipDays<PrmSkipDays>"

		DerivedTitleCompanyVendorRangeSkipDays is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_Company<PrmCompany>_VendorRange<DerivedVendorRange>_SkipDays<PrmSkipDays>"

		DerivedTitleCompanyGroup is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_CompanyGroup<PrmCompanyGroup>"

		DerivedTitleCompanyGroupVendorRange is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_CompanyGroup<PrmCompanyGroup>_VendorRange<DerivedVendorRange>"

		DerivedTitleCompanyGroupSkipDays is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_CompanyGroup<PrmCompanyGroup>_SkipDays<PrmSkipDays>"

		DerivedTitleCompanyGroupVendorRangeSkipDays is a LabelField
			restricted
			"InvoicedNotReceived_<DerivedFormattedRunDate>_<DerivedRunTime>_CompanyGroup<PrmCompanyGroup>_VendorRange<DerivedVendorRange>_SkipDays<PrmSkipDays>"

		DerivedFormattedRunDate is a DerivedField
			type is Alpha 10
			restricted
			return RunTime month + "/" + RunTime day + "/" + RunTime year

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (PrmCompany entered)
				if (PrmVendorRange entered)
					if (PrmSkipDays entered)
						return DerivedTitleCompanyVendorRangeSkipDays
					else
						return DerivedTitleCompanyVendorRange
				else
					if (PrmSkipDays entered)
						return DerivedTitleCompanySkipDays
					else
						return DerivedTitleCompany
			else
			if (PrmCompanyGroup entered)
				if (PrmVendorRange entered)
					if (PrmSkipDays entered)
						return DerivedTitleCompanyGroupVendorRangeSkipDays
					else
						return DerivedTitleCompanyGroupVendorRange
				else
					if (PrmSkipDays entered)
						return DerivedTitleCompanyGroupSkipDays
					else
						return DerivedTitleCompanyGroup
			else
				if (PrmVendorRange entered)
					if (PrmSkipDays entered)
						return DerivedTitleVendorRangeSkipDays
					else
						return DerivedTitleVendorRange
				else
					if (PrmSkipDays entered)
						return DerivedTitleSkipDays
					else
						return DerivedTitleNoParameters


			
	Field Rules

	Conditions
		ProcessingCompleteHasRecords
			when (ProcessingComplete
			and   LineResultsRel exists)
					
		ProcessingCompleteNoRecords
			when (ProcessingComplete
			and   !LineResultsRel exists)

	Relations
		LineResultsRel 
			one-to-many relation to InvoicedNotReceivedResultLine
			Field Mapping uses ByResult
				related.InvoicedNotReceivedResult	= InvoicedNotReceivedResult
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup = FinanceEnterpriseGroup)

	Sets
		ByRunTime
			Sort Order
				RunTime descending
				InvoicedNotReceivedResult descending
				FinanceEnterpriseGroup
				UniqueID

	Actions
			
		Create is an Action
			restricted
			Action Rules
				RunTime = current timestamp
				
		Update is an Update Action
			restricted

		FastUpdate is an Update Action
			refresh and lock this instance
			restricted
			bypass field rules
			Action Rules

		Delete is an Action
			Entrance Rules
				constraint (ProcessingComplete)
					"CannotDelete;JobIsRunning"
					
				invoke FastDelete LineResultsRel

		RerunWithSameParameters is an Instance Action
			Action Rules
				invoke InvoicedNotReceivedReport PayablesInvoiceDetail
						invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
			        invoked.PrmCompany						= PrmCompany
			        invoked.PrmCompanyGroup					= PrmCompanyGroup
					invoked.PrmVendorRange					= PrmVendorRange
					invoked.PrmSkipDays						= PrmSkipDays
								
