RepSetBCClientField is a BusinessClass
	prefix is RSCF
	owned by la
	default label is "ReplicationSetClientField"
	framework type is ReplicationSet
	representative text is "<RelatedValue>(<ReplicateFieldName>)"
	
	Patterns
		implements LightweightAuditing
		disable AuditIndex
		implements AuditLogEntryActions
	
	Ontology
		symbolic key is RepSetBCClientField
		
	Persistent Fields
		RelatedValue       is Alpha size 300
			is related value for RepSetBC.BusinessClass
		ReplicateFieldName is Alpha size 127
			translatable
		OrderNumber		   is Numeric size 4
			default label is "SortOrder"
		SuppressFieldData is Boolean

	Transient Fields

		IsResident 	is Boolean
			default label is "Resident"
			derive value from DerivedResidence
		IsUserField	is Boolean
			default label is "UserField"
			derive value from DerivedIsUserField
		RepresentationType is a BusinessFieldRepresentationType
			derive value from DerivedRepresentationType
		Type 		is a BusinessFieldType
			derive value from DerivedType
		NewOrderNumber is Numeric size 4
			default label is "SortOrder"
			derive value from OrderNumber
		ReplacePeriodsWithUnderscores is Boolean
		SkipSchemaChangeConfirmation is Boolean
		UseSQLName is Boolean
		IncludeTransients is Boolean
		IncludeConditions is Boolean
    	IncludeDerivedField is Boolean
    	IncludeContextField is Boolean 
		
	Local Fields
		LocalsDerived 						is Boolean
		LocalResidence 						is Boolean			
		LocalIsUserField					is Boolean
		LocalRepresentationType 			is Numeric size 3
		LocalType							is Numeric size 1
		LocalIsPrimaryKey					is Boolean
		LocalIsPrimaryKeyMember				is Boolean
		LocalFieldIsValid					is Boolean
		LocalAllKeysPresentExcludingThis	is Boolean
		LocalDeleteMode						is Boolean
		LocalIsTextSearchable				is Boolean
		LocalSkipSchemaChangeCheckMessage   is Boolean
		LocalIsRepText		  			    is Boolean
		LocalIsRepTextMember				is Boolean
		LocalAllRepTextPresentExcludingThis is Boolean
		LocalIsRepTextParent				is Boolean
		LocalClearIncremental				is Boolean
		LocalBusView						is a BusinessView
		LocalBusField						is a BusinessField
		LocalBusFieldPrefix					is LPLName
		I1 									is Numeric 2
		
	Derived Fields
		DerivedResidence is a DerivedField
			type is Boolean
			restricted
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalResidence
			
		DerivedIsUserField is a DerivedField
			type is Boolean
			restricted			
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalIsUserField
			
		DerivedRepresentationType is a DerivedField
			type is Numeric size 3
			restricted	
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalRepresentationType		
			
		DerivedType is a DerivedField
			type is Numeric size 1
			restricted
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalType
			
		FieldIsValid is a DerivedField
			type is Boolean
			restricted	
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalFieldIsValid	
				
		DerivedIsPrimaryKey is a DerivedField
			type is Boolean
			restricted			
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalIsPrimaryKey
			
		DerivedIsPrimaryKeyMember is a DerivedField
			type is Boolean
			restricted			
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalIsPrimaryKeyMember		
			
		DerivedAllKeysPresentExcludingThis is a DerivedField 
			type is Boolean
			restricted			
			LocalsDerived = DeriveLocals
			return LocalAllKeysPresentExcludingThis	
			
		DerivedIsTextSearchable is a DerivedField
			type is Boolean
			restricted						
			LocalsDerived = DeriveLocals
			return LocalIsTextSearchable
			
		DerivedIsRepText is a DerivedField
			type is Boolean
			restricted						
			LocalsDerived = DeriveLocals
			return LocalIsRepText			
			
		DerivedIsRepTextMember is a DerivedField
			type is Boolean
			restricted						
			LocalsDerived = DeriveLocals
			return LocalIsRepTextMember
			
		DerivedIsRepTextParent is a DerivedField
			type is Boolean
			restricted						
			LocalsDerived = DeriveLocals
			return LocalIsRepTextParent			
			
		DerivedAllRepTextPresentExcludingThis is a DerivedField
			type is Boolean
			restricted						
			LocalsDerived = DeriveLocals
			return LocalAllRepTextPresentExcludingThis	
			
		DeriveLocals is a NativeField
			type is Boolean
			restricted		
			
		CleanedReplicateFieldName is a NativeField	
			type is Alpha size 127
			restricted			
			
		IsValidReplicateFieldName is a NativeField	
			type is Boolean
			restricted	
			
		DerivedBusView is a DerivedField
			type is LPLName
			restricted
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalBusView
			
		DerivedBusField is a DerivedField
			type is LPLName
			restricted
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalBusField	
			
		DerivedBusFieldPrefix is a DerivedField
			type is LPLName
			restricted
			if (not LocalsDerived)
				LocalsDerived = DeriveLocals
			return LocalBusFieldPrefix
			
	Conditions
		ReplicateFieldNameEntered
			when (ReplicateFieldName entered)
		
		FieldInvalidForColumnar	
			when ((ReplicationSet.ExportFormat.COLUMNAR or RepSetBC.AlsoRepToColumnar) and not BusinessFieldRel.IsValidForColumnar)
		
		IsNumber
			default label is untranslatable
			when (RepresentationType.Numeric or RepresentationType.Decimal or RepresentationType.Currency or RepresentationType.Percent or RepresentationType.DecVar
			or    RepresentationType.Double or RepresentationType.Integer)
		
	Field Rules
		RelatedValue
			required
			
			if (RelatedValue entered) 
				RelatedValue = com.lawson.rdtech.type.StringField.trim(RelatedValue, 17) 
			
			if (not action type.Delete)
				constraint (FieldIsValid)
					"<RelatedValue>isNotAValidRelatedValue"
					
			constraint (not DeliveredFieldValueRel exists)
				"RelatedValue<RelatedValue>AlreadyExistsInDeliveredFields"							
					
		ReplicateFieldName		
			initial value is RelatedValue	
			

			if (ReplicateFieldName changed
			and action type.Update
			and old ReplicateFieldName = old ReplicateFieldName lowercase)
				UseSQLName = true
					
			force default to CleanedReplicateFieldName	
			
			constraint(IsValidReplicateFieldName)
				"InvalidReplicateFieldName"
			
			if (not ReplicateFieldName entered)  
				required	
				
			constraint (not DeliveredFieldsNameRel exists)
				"ReplicateFieldName<ReplicateFieldName>AlreadyExistsInDeliveredFields"	
		
		ReplacePeriodsWithUnderscores
			initial value is true	
		
		UseSQLName
			initial value is true
			
	Create Rules
		constraint (not ReplicationSet.ExportFormat.COLUMNAR or BusinessFieldRel.IsValidForColumnar)
			"FieldIsNotAllowed"
			
	Sets
		ByRelatedValue
			indexed
			Sort Order
				ReplicationSet
				RepSetBC
				RelatedValue
				
		ByReplicateFieldName
			indexed
			Sort Order
				ReplicationSet
				RepSetBC
				ReplicateFieldName
				
		ByOrderNumber
			indexed
			Sort Order
				ReplicationSet
				RepSetBC
				OrderNumber
				RepSetBCClientField	
				
	Relations
		DeliveredFieldsNameRel
	  		one-to-one relation to RepSetBCField
			Field Mapping uses ByReplicateFieldName
				related.ReplicationSet		= ReplicationSet
				related.RepSetBC			= RepSetBC 
				related.ReplicateFieldName	= ReplicateFieldName
				
		DeliveredFieldValueRel
	  		one-to-one relation to RepSetBCField
			Field Mapping uses ByRelatedValue
				related.ReplicationSet		= ReplicationSet
				related.RepSetBC			= RepSetBC 
				related.RelatedValue		= RelatedValue	
				
		OtherFieldValueRel
	  		one-to-one relation to RepSetBCClientField
			Field Mapping uses ByRelatedValue
				related.ReplicationSet		= ReplicationSet
				related.RepSetBC			= RepSetBC 
				related.RelatedValue		= RelatedValue	
				








		RepSetBCIncludeDeleteKeyRel
 			one-to-many relation to RepSetBC
 			Field Mapping uses symbolic key
 				related.ReplicationSet = ReplicationSet
 			 	related.RepSetBC = RepSetBC		
 			Instance Selection
				include deleted records
				where (related.ReplicationSet = ReplicationSet)
				
		NextFieldsRel
			one-to-many relation to RepSetBCClientField
			Field Mapping uses ByOrderNumber
				related.ReplicationSet = ReplicationSet
				related.RepSetBC = RepSetBC
			Instance Selection
				where (related.OrderNumber > OrderNumber)
				
		BusinessFieldRel
			one-to-one relation to BusinessField
			Field Mapping uses symbolic key
				related.BusinessView = DerivedBusView
				related.BusinessField = DerivedBusField
								
	Rule Blocks
		SchemaChangeCheck				
			if (RepSetBC.LastRegistrationStamp entered)
				if (not SkipSchemaChangeConfirmation and not LocalSkipSchemaChangeCheckMessage)
					confirmation required
						"ThisChangeWillRequireANewSchemaRegistration.AreYouSure?"
						
				invoke SetRegistrationRequired RepSetBC
					invoked.ClearIncrementalFlag = LocalClearIncremental
	Actions
		Create is an Action
			valid when (not ReplicationSet.InProcess)
			Entrance Rules
				if (RepSetBC.DeveloperMode)
					confirmation required
						"DeveloperModeIsActive.AreYouSureYouWantToAddAClientField?"		
			Action Rules
				LocalClearIncremental = false 
				include SchemaChangeCheck

				if (NewOrderNumber not entered)
					if (OrderNumber entered)
						NewOrderNumber = OrderNumber
					else
					if (NextFieldsRel exists)
						NewOrderNumber = last NextFieldsRel.OrderNumber + 1
					else
						NewOrderNumber = 1
				
			Exit Rules
				if (NewOrderNumber entered)
					invoke Resequence
			




		
        AddExplodedCompoundField is an Import Action 
			valid when (not ReplicationSet.InProcess)
			completion message is "<LocCount>Of<LocTotalCount>FieldsCreated"
			
			Local Fields
				LocTotalCount is Numeric size 6
				LocCount is Numeric size 6
				LocalBusinessFieldRepType is a BusinessFieldRepresentationType
				LocalFieldType is a BusinessFieldType
				
			Entrance Rules
				include RepSetBCFieldRB.ExplodeEntrance
					replace RelatedRel with DeliveredFieldValueRel
									
			Action Rules
				include RepSetBCFieldRB.ExplodeAction
					replace RelatedRel with DeliveredFieldValueRel
					replace FieldBusClass with RepSetBCClientField
					
        CreateForCopy is a Create Action 
            restricted
        
		Update is an Action
			valid when (not ReplicationSet.InProcess)
			Entrance Rules
				initialize LocalsDerived
			
			Action Rules
				if (RelatedValue changed
				or  ReplicateFieldName changed)
					if (RepSetBC.IncrementalReplication)
						confirmation required
							"ThisChangeWillResetTheReplicationForThisBusinessClass.AreYouSureYouWantToContinue?"
						
					LocalClearIncremental = true 
					include SchemaChangeCheck

			Exit Rules
				if (NewOrderNumber entered)
					invoke Resequence
				initialize LocalsDerived
				
		Resequence is an Instance Action
			restricted
		
		Delete is an Action
			valid when (not ReplicationSet.InProcess)
			Local Fields
				LocRepSetBCClientField is a RepSetBCClientField
				LocSortOrder is Numeric size 4
			Entrance Rules
				LocalDeleteMode = true
				LocRepSetBCClientField = first NextFieldsRel.RepSetBCClientField
				LocSortOrder = OrderNumber
			Action Rules
				constraint ((not DerivedIsPrimaryKey and not DerivedIsPrimaryKeyMember) or DerivedAllKeysPresentExcludingThis) 
					"PrimaryKeysAreRequired"
				
				constraint (not ReplicationSet.EnableEnterpriseSearch or (not DerivedIsRepText and not DerivedIsRepTextMember and not DerivedIsRepTextParent) or DerivedAllRepTextPresentExcludingThis)
					"FieldIsRequiredFor_Enterprise_Search"
				
				constraint (RelatedValue != "audit entry id" or (not RepSetBC.DataToExtract.AuditLogEntries and not RepSetBC.DataToExtract.FutureEffectiveEntries and not RepSetBC.DataToExtract.AuditAndFutureEntries))
					"FieldIsRequiredForUniqueness"			
					
				LocalClearIncremental = false 
				include SchemaChangeCheck	
				
			Exit Rules
				if (LocRepSetBCClientField exists)
					invoke ChangeSortOrder LocRepSetBCClientField
						invoked.NewOrderNum = LocSortOrder
				LocalDeleteMode = false	
			
		Purge is a Purge Action
			valid when (not ReplicationSet.InProcess)
			confirmation required
				"ThisCannotBeUndone,AreYouSureYouWantToPurge?"
					
			Entrance Rules
				LocalDeleteMode = true
			Action Rules
				constraint ((not DerivedIsPrimaryKey and not DerivedIsPrimaryKeyMember) or DerivedAllKeysPresentExcludingThis) 
					"PrimaryKeysAreRequired"
				
				constraint (not ReplicationSet.EnableEnterpriseSearch or (not DerivedIsRepText and not DerivedIsRepTextMember and not DerivedIsRepTextParent) or DerivedAllRepTextPresentExcludingThis)
					"FieldIsRequiredFor_Enterprise_Search"
					
				LocalClearIncremental = false 
				include SchemaChangeCheck	
				
			Exit Rules
				LocalDeleteMode = false					
				
        Copy is an Instance Action
            restricted
            
            Parameters
                NewReplicationSet     is like ReplicationSet
                NewRepSetBC            is like RepSetBC
                	default label is "NewReplicationSetBC"
                
            Parameter Rules
                NewReplicationSet
                    required    
                
                NewRepSetBC
                    required
                    
            Action Rules
                invoke CreateForCopy RepSetBCField
                    invoked.ReplicationSet      = NewReplicationSet
                    invoked.RepSetBC            = NewRepSetBC                    
                    invoked.RelatedValue        = RelatedValue
                    invoked.ReplicateFieldName  = ReplicateFieldName
                    invoked.OrderNumber			= OrderNumber
                    
		ChangeSortOrder is an Instance Action
			disable multiple instance selection
			
			Parameters
				NewOrderNum is Numeric size 4
					default label is "NewSortOrder"
				
			Parameter Rules
				NewOrderNum
					required
			
			Action Rules
				NewOrderNumber = NewOrderNum
				
			Exit Rules
				invoke Resequence

		SetSuppressFieldDataToActive is an Instance Action
			Action Rules
				SuppressFieldData = true

		SetSuppressFieldDataToInactive is an Instance Action
			Action Rules
				SuppressFieldData = false
