VendorReturn is a BusinessClass
    owned by po
    prefix is PTH
    classic name is PORETURNHD

    Ontology
        symbolic key is VendorReturn
            classic set name is PTHSET0
            classic name is RETURN-NUMBER

    Patterns
    	implements AnalyticCube
	   		disable continuous update
			dynamically calculate totals
			write to base level only 
        implements StaticJava
        disable AuditIndex
        implements BODId
        implements Archivable

    Persistent Fields
        ReferenceNumber
            classic name is REFERENCE-NO
        ReturnFromLocation                         is an InventoryLocation
            classic name is RETURN-FRM-LOC
        UserId                                     is a User
        CreationDate                               is Date
        ReasonCode                                 is a ProcurementReasonCode
        Vendor										
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
            context of Vendor
        ReturnAuthorizationDate                    is Date
            classic name is AUTHORIZE-DATE
        ReturnMaterialAuthorizationNumber          is AlphaUpper size 64
            sql name is RMaterialAuthorizationNumber
            classic name is RMA-NUMBER
        ReturnMaterialAuthorizationPrinted         is Boolean
            sql name is RMaterialAuthorizationPrinted
            classic name is RMA-PRINTED-FL
        Currency
            classic name is CURRENCY-CODE
        Contact
        PhoneNumber                                 is a TelephoneNumber 
        	holds pii
            classic name for PhoneNumber.InternationalPrefix is PHONE-PREFIX
            classic name for PhoneNumber.SubscriberNumber is PHONE-NUM
            classic name for PhoneNumber.Extension is PHONE-EXT
        Dropship                                   is Boolean
            classic name is DROPSHIP-FL
        DropshipName                               is a Name	 
        	holds pii
            classic name is SH-NAME
        DropshipAddress								is a PostalAddressV2	
        	holds pii
            classic name for DropshipAddress.DeliveryAddress.AddressLine1 is SH-ADDR1
            classic name for DropshipAddress.DeliveryAddress.AddressLine2 is SH-ADDR2
            classic name for DropshipAddress.DeliveryAddress.AddressLine3 is SH-ADDR3
            classic name for DropshipAddress.DeliveryAddress.AddressLine4 is SH-ADDR4
            classic name for DropshipAddress.Municipality is SH-CITY-ADDR5
            classic name for DropshipAddress.StateProvince is SH-STATE-PROV
            classic name for DropshipAddress.PostalCode is SH-POST-CODE
            classic name for DropshipAddress.County is SH-COUNTY
            classic name for DropshipAddress.Region is DEST-REGION
        OriginalFreightCreditAmount                is an InternationalAmount
            classic name is ORIG-FRT-CR-AM



        ReturnToVendorFreightCreditAmount          is an InternationalAmount
            sql name is RToVendorFreightCreditAmount
            classic name is RTV-FRT-CR-AM



        HandlingAmount                             is an InternationalAmount
            classic name is HANDLING-AM


        ReplacementFreightAmount                   is an InternationalAmount
            classic name is REPL-FRT-AM
        AddOnCharge
            classic name is AOC-CODE
        VendorClaimType
            classic name is VEN-CLAIM-TYPE
        ReplaceGoods                               is Boolean
        ShipOrHold
        ReplacementPurchaseOrder                   is a PurchaseOrder								
            classic name is RET-PO-NUMBER
            delete ignored
        VendorReturnPOCode                         is a POCode
            classic name is RET-PO-CODE
        ReplacementPurchaseOrderBuyer              is a Buyer										
            classic name is RET-PO-BUYER
        ProcessLevel                               is a PayablesProcessLevel
        ReplacementPurchaseOrderShippingMethod     is a ShipVia
            sql name is RPurchaseOrderShippingMethod
            classic name is IVEND-SHIP-VIA
        VendorRequestedReplacementDate             is Date
            classic name is VEND-REQ-DATE
        VendorReturnShippingMethod                 is a ShipVia
            classic name is OVEND-SHIP-VIA
        Carrier						  			   is a Vendor
        ShipmentDate                               is Date
            classic name is OVEND-SHP-DATE
        ShipmentTime                               is Numeric size 6
            classic name is OVEND-SHP-TIME
        ShipmentBillOfLading                       is AlphaUpper size 10
            classic name is OVEND-BOL
        ShippedBy                                  is AlphaUpper size 10
            classic name is OVEND-USER-ID
        ReturnValue                                is an InternationalAmount
        CreditReceived                             is an InternationalAmount
            classic name is CREDIT-RECVD
        FullyCredited                              is Boolean
            classic name is CREDIT-CL-FL
        ReplacementPurchaseOrderClosed             is Boolean
            classic name is REPL-CL-FL
        LastVendorReturnLine                       is a LineNumber
            classic name is LAST-LINE-NBR
            disable Auditing
        Canceled                                   is Boolean
            classic name is CANCEL-OPTIONS
        CanceledBy                                 is AlphaUpper size 10
            classic name is CANCEL-USERID
        CloseDate                                  is Date
        Status                                     is Numeric size 1
            States
                Added                  value is 0
                AuthorizedByVendor     value is 1
                WaitingForVendorAction value is 2
                Closed                 value is 9
        JournalBook
            classic name is JRNL-BOOK-NBR
            restricted
        ShipTerm
            classic name is FOB-CODE
        LoadingPort
        NatureOfTransactionCode
            classic name is NOTC
        StatisticalProcedure					   is an IntrastatStatisticalProcedure
            classic name is STAT-PROC
        ReturnStockToInventoryOnVendorReturnCancel is Boolean
            sql name is RSTIOnVendorReturnCancel
            classic name is VR-CXL-UPD-SOH
        CreatedBy                                  is an Operator 
        	holds pii
        LastUpdateDate                             is TimeStamp
            classic name is LAST-UPDT-DATE
        LastUpdateBy                               is an Operator 
        	holds pii
        GlobalDocumentType
            classic name is GLBL-DOC-TYPE
        TemporaryHold                              is AlphaUpper size 1
            classic name is TEMP-HOLD
            States
                SentForApproval  value is "S"
                    default label is "SentForApproval"
                ApprovedByEngine value is "A"
                    default label is "ApprovedByEngine"
				RejectedByEngine value is "R"
					default label is "RejectedbyEngine"
        OriginalFreightCreditAccount               is a TransactionCodeBlock
            classic name for OriginalFreightCreditAccount.AccountingUnit is ORIG-FRT-CR-AU
            classic name for OriginalFreightCreditAccount.GeneralLedgerChartAccount is ORIG-FRT-CR-AC
            classic name for OriginalFreightCreditAccount.Project is ORIG-FRT-ACTV
        ReturnToVendorFreightCreditAccount         is a TransactionCodeBlock
            classic name for ReturnToVendorFreightCreditAccount.AccountingUnit is RTV-FRT-CR-AU
            classic name for ReturnToVendorFreightCreditAccount.GeneralLedgerChartAccount is RTV-FRT-CR-ACT
            classic name for ReturnToVendorFreightCreditAccount.Project is RTV-FRT-ACTV
        HandlingAccount                            is a TransactionCodeBlock
            classic name for HandlingAccount.AccountingUnit is HANDLING-AU
            classic name for HandlingAccount.GeneralLedgerChartAccount is HANDLING-AC
            classic name for HandlingAccount.Project is HANDLING-ACTV
		CreatedFromPurchaseOrder                   is a PurchaseOrder
			delete ignored
		RelatedInventoryTransaction				   is an InventoryTransaction
		RelatedPayablesInvoice					   is an PayablesInvoice
			delete ignored
		RMARequested                               is Boolean
				
	Context Fields
		PayablesInvoice
		FSMInboundBODTracker

	Local Fields
		LocalLocalizationFound				is Boolean
		LocalItem							is an Item
		LocalPurchaseOrder					is a PurchaseOrder
		NewVendorReturn						is a VendorReturn view
		LocalVendorReturn					is a VendorReturn
		NewPurchaseOrderView        		is a PurchaseOrder view	
		NewInventoryTransaction    			is an InventoryTransaction view
		LocalVendorReturnUntaxedAmount		is an InternationalAmount
		LocalPayablesInvoiceUntaxedAmount	is an InternationalAmount												
		LocalContractGroup					is AlphaUpper size 4
		LocalContract						is a Contract
		LocalQuantity						is Decimal size 13.4
		LocalMfgContract					is Numeric size 15
		LocalCmContract						is Numeric size 15
		LocalLineNbr						is like PurchaseOrderLine
		LocalOriginalContract				is Numeric size 15
		LocalLocation						is like InventoryLocation
    	LocalManufacturerInformation		is a ManufacturerInformation
    	LocalPoDate							is Date
		LocalManufacturerUnitCost			is an InternationalCost
		LocalReturnUnitCost					is an InternationalCost
		LocalUnitCost                       is an InternationalCost
		LocalReturnCreditAmount				is an InternationalAmount
		LocalCommodityCode					is a CommodityCode
		LocalPOLineCommodityCode			is a CommodityCode
		LocalPurchMajcl						is Alpha 4
		LocalPurchMincl						is Alpha 4
		LocalIcItemCode						is an UNSPSCCode
		LocalInvoiceNumber					is Alpha size 22
		LocalCurrencyCode					is a Currency
		LocalExchangeRate					is Decimal size 14.7
		LocalCommCodes						is Alpha size 35
		I1									is Numeric 2
		I2									is Numeric 2
		LocalPayablesInvoice				is like PayablesInvoice
		LocalPayablesInvoiceDetail			is like PayablesInvoiceDetail
		LocalPurchaseOrderLine				is like PurchaseOrderLine
		LocalTitle 				            is Alpha size 255
		ActionCode							is Alpha size 1
			States
				Create  value is "C"
				Update	value is "U"
				Delete	value is "D"
		LocalBODCurrentTimeStamp			is a BODCurrentTimeStamp
		IntrastatProcessing
		LocalPurgeCount						is Numeric 10
		LocalPurgeRecords					is Boolean
		LocalActor							is an Actor
		LocalSupplierEmail                  is an EmailAddress
		LocalBuyerEmail                     is an EmailAddress
		LocalSupplierSourceId               is like SupplierSourceId
		LocalSupplier                   	is like Supplier
		LocalSupplierContact            	is like SupplierSourceId
		CompletionMessage					is Alpha 150
				

		LocalNativeLPLBODTrigger	        is Boolean
		NewBODTracker  						is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker			is Numeric 15
		Error            					is Boolean
		ErrorMessage     					is Alpha 300
		LocalConfigurationParameter			is Alpha size up to 200
		LocalCrossAE						is Alpha size 100
	    LocalAEAlreadyExecuted				is Boolean	

	Derived Fields
		DerivedReplaceGoods				is a DerivedField
			type is Numeric size 1
			if (VendorReturn entered)
				if (ReplaceGoods)
					return 1
				else
					return 2
			return 0

    	DerivedTotalLineCount			is a ComputeField
			type is Numeric 6
			restricted
			(instance count of InventoriedItemVedorReturnLinesRel)
    	
    	ReverseCreditReceived is a DerivedField
	        type is like InternationalAmount
	        restricted
	        return (CreditReceived * -1)
	        		
		DerivedSupplierSourceId is a DerivedField
			type is like SupplierSourceId
			restricted
			if (Vendor.SingleSupplier
			and PurchaseFromLocation entered
            and PurchaseFromLocation.SupplierSourceIdRel exists
            and PurchaseFromLocation.NumberOfSupplierContacts = 1)
   				return PurchaseFromLocation.SupplierSourceIdRel.SupplierSourceId         					 
            else
				return blank 	

    	CompanyAndName is a DerivedField
    		type is Text
    		return Company.RepresentativeText
    	
    	BuyerAndName is a DerivedField
    		type is Text
    		return ReplacementPurchaseOrderBuyer.RepresentativeText
    	
		PurchaseFromName is a DerivedField
			type is Text
			return PurchaseFromLocation.VendorLocation.VendorName

    	VendorReturnUntaxedAmount 		is a DerivedField
    		type is like InternationalAmount
    		restricted
			for each VendorReturnLinesRel
				LocalVendorReturnUntaxedAmount	+= each.ReturnQuantity * each.UnitCost  
    		return LocalVendorReturnUntaxedAmount	
    					
		PayablesInvoiceUntaxedAmount 	is a DerivedField
    		type is like InternationalAmount
    		restricted
    		if (DerivedTotalLineCount entered
 			and ReturnValue		= CreditReceived) 
				if (CreditReceived entered)
					LocalPayablesInvoiceUntaxedAmount	= first PayablesInvoicesRel.InvoiceAmount.CurrencyAmount - first PayablesInvoicesRel.PayablesInvoiceTaxRel.TotalInvoicedTaxAmount	
				if (LocalPayablesInvoiceUntaxedAmount < 0)
					LocalPayablesInvoiceUntaxedAmount  *= -1 
    		return LocalPayablesInvoiceUntaxedAmount
    	Affiliate is a DerivedField
			type is Boolean
			restricted
    		return first Vendor.VendorCompanyDefault set.Affiliate


		VendorReturnCount is a DerivedField
			type is Numeric 1
			restricted
			return 1

		ReturnLossCost is a DerivedField
			type is like InternationalAmount
			restricted
			return ReturnValue - CreditReceived

		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforSupplierRMA"
			
		ContextMessageText is a MessageField
			restricted
			"Vendor<Vendor>VendorReturn<VendorReturn>"
			

		MobileVendorReturnCompany is a LabelField
			"<Company>_-_<Company.Name>"
			
		MobileVendorReturnLocation is a LabelField
			"<ReturnFromLocation>_-_<ReturnFromLocation.Name>"
			
		MobileReturnValue is a LabelField
			"<ReturnValue>_<Currency>"
			
		MobileVendorReturnStatus is a DerivedField
			type is Alpha 25
			default label is "Status"
			if (Status.Added)
				return "Added"
			if (Status.AuthorizedByVendor)
				return "Authorized"
			if (Status.WaitingForVendorAction)
				return "Waiting"
			if (Status.Closed)
				return "Closed"
		
		MobileVendorReturn	is a DerivedField
			type is Alpha 10
			restricted
			if (VendorReturnExists)
				return VendorReturn
			else
				return blank

		MobileCreatedBy is a LabelField
			"<ActorRel.PersonName.PreferredFirstAndLastName>"



		VendorReturnCompanyDisplay is a LabelField
			"<Company>_-_<Company.Name>"
		VendorReturnnDisplay			is a LabelField
			"Return:<VendorReturn>|PO:<CreatedFromPurchaseOrder>"
		VendorReturnTitle is a LabelField
			"Return<VendorReturn>"
		VendorReturnLocationDisplay 	is a LabelField
			"<ReturnFromLocation>_-_<ReturnFromLocation.Name>"
		ReturnValueDisplay				is a LabelField
			"<ReturnValue>_<Currency>"		
	


		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedRequiredCleanDocId is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "RequiredCleanDocumentID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedConfigFEG is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "IsMultipleFEG"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
							
		DerivedTenantID is a DerivedField 
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value

		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODCurrentTimeStamp = current timestamp
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + 	DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
			
		DerivedBODID is a DerivedField
			type is Alpha size 100
			restricted
			return "infor-nid:" + DerivedTenantID + ":" + DerivedBODCrossAccountingEntity + ":" + DerivedLocation + ":" + DerivedDocID + ":?Shipment&verb=Process&TrackerID=" + LocalFSMInboundBODTracker
			
		DerivedBODVariationID is a DerivedField
			type is Alpha size 60
			restricted
			return bod id.VariationID
		
		DerivedFinanceEnterpriseGroup is a DerivedField	
			type is Alpha size 4
			restricted
			return Company.FinanceEnterpriseGroup
			
		DerivedAE is a DerivedField	
			type is Alpha size 12
			restricted
			if(DerivedRequiredCleanDocId = "Y")
		 		return DerivedBODCrossAccountingEntity
		 	else
				return Company.GeneralLedgerCompany.AccountingEntity
			
		DerivedAccountingEntity is a DerivedField
			type is Alpha size 100
			restricted
			return DerivedFinanceEnterpriseGroup + DerivedDelimiter + DerivedAE
		
		DerivedBODCrossAccountingEntity is a DerivedField
			type is Alpha size 100
			restricted
			if (DerivedRequiredCleanDocId = "Y")
				return DerivedGLCCrossAccountingEntity
			else
				return DerivedAccountingEntity
				
		DerivedGLCCrossAccountingEntity is a DerivedField
			type is Alpha size 100
			restricted
			if(LocalAEAlreadyExecuted != true)
				LocalAEAlreadyExecuted = true
				if(FSMBODCleanDocCrossReferenceRel.DestinationValue entered)
					LocalCrossAE = FSMBODCleanDocCrossReferenceRel.DestinationValue
				else
					LocalCrossAE = DerivedCrossGLC
				return LocalCrossAE
			else
				return LocalCrossAE
				
		DerivedCrossGLC is a DerivedField
			type is Alpha size 50
			restricted
			if(DerivedConfigFEG = "Y")
				return DerivedFinanceEnterpriseGroup + DerivedDelimiter + DerivedCompany
			else
				return DerivedCompany	
		
		DerivedCompany is a DerivedField
			type is Alpha size 4
			restricted
			return Company using "%d"
		
		DerivedVendorReturn is a DerivedField
			type is Alpha size 10
			restricted
			return VendorReturn using "%d"
				
		DerivedLocation is a DerivedField
			type is Alpha size 100
			restricted
			if(DerivedRequiredCleanDocId = "Y")
				return ReturnFromLocation
			else
				return DerivedCompany + DerivedDelimiter + ReturnFromLocation
		
		DerivedDocID is a DerivedField
			type is Alpha size 100
			restricted
			if(DerivedRequiredCleanDocId = "Y")
				return DerivedVendorReturn
			else
				return DerivedCompany + DerivedDelimiter + DerivedVendorReturn	
			
		DerivedActionCode is a DerivedField
			type is Alpha size 10
			restricted
			return "Add"
			
		DerivedBODStatus is a DerivedField
			type is Alpha size 10
			restricted
			return "Released"
			
		DerivedDocDateTime is a DerivedField
			type is Alpha size 30
			restricted
			if (ShipmentDate not entered)
				DerivedDocDateTime = CreationDate
				return DerivedDocDateTime[1:4] + "-" + DerivedDocDateTime[5:6] + "-" + DerivedDocDateTime[7:8] + "T00:00:00"
			else
				DerivedDocDateTime = ShipmentDate
				return DerivedDocDateTime[1:4] + "-" + DerivedDocDateTime[5:6] + "-" + DerivedDocDateTime[7:8] + "T00:00:00"
				
		DerivedPurchaseOrderName is a DerivedField
			type is Alpha size 80
			restricted
			return ReturnFromLocation.NameForPurchaseOrder
			
		DerivedPONameAddressLine1 is a DerivedField	
			type is Alpha size 40
			restricted
			return ReturnFromLocation.PostalAddress.DeliveryAddress.AddressLine1
			
		DerivedPONameAddressLine2 is a DerivedField	
			type is Alpha size 40
			restricted
			return ReturnFromLocation.PostalAddress.DeliveryAddress.AddressLine2
			
		DerivedPONameAddressLine3 is a DerivedField	
			type is Alpha size 40
			restricted
			return ReturnFromLocation.PostalAddress.DeliveryAddress.AddressLine3
			
		DerivedPONameAddressLine4 is a DerivedField	
			type is Alpha size 40
			restricted
			return ReturnFromLocation.PostalAddress.DeliveryAddress.AddressLine4
			
		DerivedPONameMunicipality is a DerivedField
			type is Alpha size 58
			restricted
			return ReturnFromLocation.PostalAddress.Municipality
			
		DerivedPONameStateProvince is a DerivedField
			type is Alpha size 3
			restricted
			return ReturnFromLocation.PostalAddress.StateProvince
			
		DerivedPONameCountry is a DerivedField
			type is Alpha size 3
			restricted
			return ReturnFromLocation.PostalAddress.Country
			
		DerivedPONamePostalCode is a DerivedField
			type is Alpha size 12
			restricted
			return ReturnFromLocation.PostalAddress.PostalCode
		
		DerivedAddressLine1 is a DerivedField
			type is Alpha size 40
			restricted
			return DropshipAddress.DeliveryAddress.AddressLine1
			
		DerivedAddressLine2 is a DerivedField
			type is Alpha size 40
			restricted
			return DropshipAddress.DeliveryAddress.AddressLine2
			
		DerivedAddressLine3 is a DerivedField
			type is Alpha size 40
			restricted
			return DropshipAddress.DeliveryAddress.AddressLine3
				
		DerivedAddressLine4 is a DerivedField
			type is Alpha size 40
			restricted
			return DropshipAddress.DeliveryAddress.AddressLine4
		
		DerivedMunicipality is a DerivedField
			type is Alpha size 58
			restricted
			return DropshipAddress.Municipality
			
		DerivedStateProvince is a DerivedField
			type is Alpha size 3
			restricted
			return  DropshipAddress.StateProvince
		
		DerivedCountry is a DerivedField	
			type is Alpha size 3
			restricted
			if (DropshipAddress.Country entered)
				return DropshipAddress.Country
			else
				return "US"
				
		DerivedPostalCode is a DerivedField
			type is Alpha size 12
			restricted
			return DropshipAddress.PostalCode
		
		VendorReturnLineXML is a DerivedField
			type is XMLDocument
			restricted
			if(VendorReturnLinesRel exists)
				for each VendorReturnLinesRel
					each.LocalDelimiter = DerivedDelimiter
					each.LocalAccountingEntity = DerivedAccountingEntity
					VendorReturnLineXML += template.IONProcessVendorReturn_VendorReturnLine_ST document for each
				return VendorReturnLineXML
			else
				return ""
				
		ProcessVendorReturnXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			ProcessVendorReturnXMLBOD =  template.IONProcessVendorReturn_VendorReturn_ST document for this instance
			return ProcessVendorReturnXMLBOD
		

		SentToEngineCompletionMessage is a MessageField
			restricted
			"ReturnSentToTaxEngineForApproval"

		ShipCompletionMessage is a MessageField
			restricted
			"TheFollowingAction_\'\Ship\'_\completed"

		DerivedCompletionDetailText	is a DerivedField
			type is RichText
			if (LocalPurgeRecords)
				return DerivedPurgeReturnText
			else
				return DerivedReportPurgeReturnText

		DerivedPurgeReturnText is a MessageField
			"VendorReturnPurgeHasBeenCompletedFor<LocalPurgeCount>Records."
			
		DerivedReportPurgeReturnText is a MessageField
			"ThereAre<LocalPurgeCount>VendorReturnRecordsWhichCanBePurged."


		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (ActionCode = "D" or ActionCode.Delete) 
				return "Deleted"
			else 
				return "Open"
	
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (ActionCode = "C" or ActionCode.Create)
				return "Add"
			else
				return "Replace"
				
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
			
		DerivedReturnAuthDate is a DerivedField
			type is Alpha size 25
			restricted
			DerivedReturnAuthDate = ReturnAuthorizationDate
			return DerivedReturnAuthDate[1:4] + "-" + DerivedReturnAuthDate[5:6] + "-" + DerivedReturnAuthDate[7:8]	
					
		DerivedSupplierRMABODID is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedAccountingEntity + ":"+ ReturnFromLocation +" :"  +DerivedDocID + ":" + DerivedBODRevision + ":" +"?SupplierRMA&verb=Sync&TrackerID=" + LocalFSMInboundBODTracker
			
		DerivedBusinessGroup is a DerivedField
			type is Alpha 5
			restricted
			return Company.GeneralLedgerCompany.BusinessGroup
			
		DerivedVendorGroup is a DerivedField
			type is Alpha 5
			restricted
			return Vendor.VendorGroup
			
		VendorReturnBODXML is a DerivedField
			type is XMLDocument
			restricted
			if (VendorReturnLinesRel exist)
				VendorReturnBODXML = template.IONSyncSupplierRMA_VendorReturn_ST document for this instance


 	Transient Fields
 		FromCurrency
 			derive value from Currency
 		TransientReplaceGoods				is Numeric size 1
 			derive value from DerivedReplaceGoods
 			default label is "ReplaceGoods"
 			States
 				UseDefaultValue				value is 0
 				Yes							value is 1
 				No							value is 2

		TransientGLCalendarPeriod 			is a GeneralLedgerCalendarPeriod	
			derive value from GeneralLedgerCalendarPeriodXrefRel.GeneralLedgerCalendarPeriod

 	Dimensions	
		ReplacementPurchaseOrderBuyer
			dimension name is Buyer

		TransientGLCalendarPeriod
			dimension name is Calendar
			caption is "<AlternateCaption>"
			Attributes
				YTDCrossReference
				SamePeriodLastYear
				YTDCrossRefSPLY
				PeriodType
				PeriodTypeName
				NumberOfDays
				
		Currency
		PurchaseFromLocation
		
		CreatedFromPurchaseOrder
			dimension name is PONumber
			
		ReasonCode
		
		Status
			dimension name is ReturnStatus
			
		Vendor
			caption is representative text
		VendorReturn
	Measures
		ReturnValue
		CreditReceived
			measure name is ReturnCreditReceived
		ReturnLossCost			
		VendorReturnCount
			measure name is ReturnCount
		
    Conditions
    	IsHSNSACCodeEnabled
			restricted
			when (Company.GeneralLedgerCompany.RequireHSNSACCode)
			
        CreditNeeded
        	restricted
            when (VendorClaimType.CreditMemo
            and   not Status.Closed)

        HasReferenceNumber
            classic name is REFERENCE-NO
            restricted
            when (ReferenceNumber entered)

        HasReplacementPurchaseOrder
            classic name is REPLACEMENT-PO
            restricted
            when (ReplacementPurchaseOrder entered)

        HasReturnMaterialAuthorizationNumber
            classic name is RMA-NO-EXISTS
            restricted
            when (ReturnMaterialAuthorizationNumber entered)

		HasDialogForAVendorReturnExists
			when (BuyerSupplierContactMessageRel exists)

        CanRequestRMA
        	restricted
        	when (Status.Added
        	and   ReturnMaterialAuthorizationNumber !entered
        	and   Vendor.SupplierRel exists
        	and   HasLines
        	and   RMARequested = false)
        
		NeedsSupplierRMA
			restricted
        	when (Status.Added
        	and   ReturnMaterialAuthorizationNumber !entered 
			and   RMARequested)			

        HasLines
        	restricted
        	when (VendorReturnLinesRel exists)
        
        ShippedStatus
        	restricted
            when (Status.Closed
            or    Status.WaitingForVendorAction)

		ShippedNotClosed
			restricted
            when (Status.WaitingForVendorAction)
		
		ValidForInvoice
			restricted
			when (Vendor not entered
			or	  PayablesInvoice.Vendor not entered
			or	  PayablesInvoice.Vendor = Vendor)
			
		CreditMemoOrChargeback
			restricted
        	when (VendorClaimType.CreditMemo
			or VendorClaimType.Chargeback)
		
		CreditMemoOrNoCharge
			restricted
        	when (VendorClaimType.CreditMemo
			or VendorClaimType.NoCharge)
		
		VendorReturnExists
			restricted
			when (VendorReturn exists)
			
		CreditMemoOrChargeBackExists
			restricted
			when (CreditMemoPayablesInvoiceRel exists)
			
		SupplierShowInvoice
			restricted
			when (CreditMemoOrChargeBackExists
			and	  !actor.agent(SupplierSourceId).SupplierGroup.SPDisablePortalInvoices)
			
		SupplierShowReplacementPO
			restricted
			when (HasReplacementPurchaseOrder 
			and	  !actor.agent(SupplierSourceId).SupplierGroup.SPDisablePortalPOs)

		SupplierShowOriginalPO
			restricted
			when (CreatedFromPurchaseOrder entered 
			and	  !actor.agent(SupplierSourceId).SupplierGroup.SPDisablePortalPOs)

		LineCostDiffersFromMatchCost
			restricted
			when (VendorReturnLinesWithDifferentMatchCostRel exists)
			
		HasCatchWeightItem
			restricted
			when (VendorReturnLinesWithCatchWeightRel exists)

		PurchaseOrderHasCatchWeightItem
			restricted
			when (CreatedFromPurchaseOrderLinesWithCatchWeightRel exists)
			

		IsCreatedFromPO
			when (CreatedFromPurchaseOrder entered)



		HasSupplier
			restricted
			when (Vendor.HasSupplier)

		HasSupplierContactForPurchaseFrom
			restricted
			when (DerivedSupplierSourceId entered)

		HasMessageDialog
			restricted
			when (SupplierContactMessageRel exists)

		HasMessageDialogOther
			restricted
			when (SupplierMessageDialogRel exists)

		BuyerCanCreateDialog
			restricted
			when (HasSupplier
			and   SupplierGroupRel.CanCreateDialogWithSupplier)

		SupplierContactHasReturnsAccess
			restricted
			when (SupplierSourceIdValidRel.HasReturnsAccess)

		SupplierCanView
			restricted
			when (Company.VendorGroup       = actor.agent(SupplierSourceId).SupplierGroup
 			and   Vendor			 		= actor.agent(SupplierSourceId).Supplier.Vendor			
			and ((actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.AllDocuments)
			or  (!actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.AllDocuments
			and   actor.agent(SupplierSourceId).Supplier.PrimaryContactViewAllDocuments = true
			and   actor.agent(SupplierSourceId).PrimaryContact)
			or  ((actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.OnlyLocationDocuments
			or    actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.LocationAndNoLocationDocuments)
			and  (actor.agent(SupplierSourceId).Supplier.PrimaryContactViewAllDocuments = false
			or   !actor.agent(SupplierSourceId).PrimaryContact)
			and   PurchaseFromLocation entered
			and   actor.agent(SupplierSourceId).VendorLocation = PurchaseFromLocation)
			or   (actor.agent(SupplierSourceId).Supplier.ContactLocationDisplay.LocationAndNoLocationDocuments
			and   PurchaseFromLocation !entered)))      

		ForSupplier
			restricted
			when (Company.VendorGroup       = actor.agent(SupplierSourceId).SupplierGroup
 			and   Vendor			 		= actor.agent(SupplierSourceId).Supplier.Vendor)

		PurgeReplacementPurchaseOrder
			when (PurgeReplacementPurchaseOrderRel exists
			and  (not PurgeReplacementPurchaseOrderRel.Closed.No or PurchaseOrderWithReceiptRel not exists))

		ReplacementFreightAmountRequired
			restricted
			when (AddOnCharge entered)

		AddOnChargeRequired
			restricted
			when (ReplacementFreightAmount entered)
															
		DropshipNameRequired
			restricted
			when (Dropship
			and   CreatedFromPurchaseOrder.DropshipName not entered)

		DropshipAddressRequired	
   			restricted
			when (Dropship
			and   CreatedFromPurchaseOrder.DropshipAddress not entered)

		VendorRequired
			restricted
			when (CreatedFromPurchaseOrder.Vendor not entered)

		ReturnFromLocationRequired
			restricted
			when (CreatedFromPurchaseOrder.ShipToLocation not entered)
			
		IsOnHold
			restricted
			when (TemporaryHold.SentForApproval)
		
		IsValidForCancel
			restricted
			when ((VendorClaimType.Chargeback
			and    not CreatedFromPurchaseOrder.HasReleasedAdjustments
			and    not CreatedFromPurchaseOrder.BuyerMessagesExist)
			or     CreditReceived not entered)

    Relations			    
    	PurchasingCompanyRel
			one-to-one relation to PurchasingCompany
			Field Mapping uses symbolic key
				related.Company = Company
		
		BuyerSupplierContactMessageRel
			one-to-many relation to SupplierContactMessage
			Field Mapping uses ByVendorReturn
				related.OriginatingCompany			= Company
				related.OriginatingReturn			= VendorReturn
		PayablesProcessLevelRel
            one-to-many relation to PayablesProcessLevel
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
            	where (related.DefaultProcessLevel)
            	
		SupplierCommentRel
			one-to-many relation to VendorReturnComment
			Field Mapping uses symbolic key
				related.Company			= Company
				related.VendorReturn	= VendorReturn
			Instance Selection
				where (related.Type != "O")

		SupplierSourceIdPrimaryRel
			one-to-many relation to SupplierSourceId
			Field Mapping uses symbolic key
				related.SupplierGroup   = Company.ProcurementGroup
				related.Supplier        = Vendor.SupplierRel.Supplier
			Instance Selection
				where (related.PrimaryContact)

		PurchaseFromSupplierSourceIdRel   
			one-to-one relation to SupplierSourceId
			Field Mapping uses symbolic key
				related.SupplierGroup    = SupplierGroupRel.SupplierGroup
				related.Supplier         = SupplierRel.Supplier
				related.SupplierSourceId = DerivedSupplierSourceId  

		SupplierGroupPortalContactsPORel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses symbolic key
				related.SupplierGroup   = Company.ProcurementGroup
			Instance Selection
				where (related.PODefaultDialogContact = true)

		GeneralLedgerCalendarPeriodXrefRel								
			one-to-one relation to GeneralLedgerCalendarPeriodXref
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Company.FinanceEnterpriseGroup
				related.GeneralLedgerCalendarPeriodXref	= CreationDate

        CreatedFromPurchaseOrderLinesRel
        	one-to-many relation to PurchaseOrderLine
        	Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder			= CreatedFromPurchaseOrder
        	Instance Selection
        		where (not related.ItemType.Service)

        CreatedFromPurchaseOrderLinesWithCatchWeightRel
        	one-to-many relation to PurchaseOrderLine
        	Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder			= CreatedFromPurchaseOrder
        	Instance Selection
        		where (related.IsCatchWeightItem)
        		
        PurchaseOrderLinesWithMatchQuantityRel												
        	one-to-many relation to PurchaseOrderLine
        	Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder			= CreatedFromPurchaseOrder
        	Instance Selection
        		where (related.MatchedQuantity entered)
        
        PurchaseOrderLinesWithReceivedQuantityRel
        	one-to-many relation to PurchaseOrderLine
        	Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder			= CreatedFromPurchaseOrder
        	Instance Selection
        		where (related.ReceivedQuantity entered)	   
        	        

		VendorReturnLinesRel is a VendorReturnLine set

		VendorReturnLinesForCancelRel is a VendorReturnLine set
			Instance Selection
				where (not related.Canceled)
		
		ReturnLinesForPurchaseOrderLinesWithCostRel is a VendorReturnLine set
			Instance Selection
				where (related.OriginalPurchaseOrderLine not entered
				or	   related.IsCreatedFromPOWithCost)
				
		VendorReturnLinesWithDifferentMatchCostRel is a VendorReturnLine set
			Instance Selection
				where (related.LineCostDiffersMatchCost)

		VendorReturnLinesWithCatchWeightRel is a VendorReturnLine set
			Instance Selection
				where (related.IsCatchWeightItem)
		VendorReturnLinesForAdjustmentRel is a VendorReturnLine set
			Instance Selection
				where (related.HasQuantityForAdjustment)
		
		InventoriedItemVedorReturnLinesRel is a VendorReturnLine set
			Instance Selection
				where (related.ItemType.Inventoried)
		
		PayablesInvoicesRel
        	one-to-many relation to PayablesInvoice
            Field Mapping uses ByReturnNumber
            	related.Company               = Company
                related.VendorReturn          = VendorReturn
                related.Vendor			      = Vendor        
		MatchCompanyRel
			one-to-one relation to MatchCompany
            Field Mapping uses symbolic key
                related.Company					= Company
     	






       
       	PurchasingVendorRel
       		one-to-one relation to PurchasingVendor
            Field Mapping uses symbolic key
            	related.VendorGroup				= Company.VendorGroup					
                related.Vendor					= Vendor
        
        VendorLocationRel										
        	one-to-one relation to VendorLocation
        	Field Mapping uses symbolic key
        		related.VendorGroup			  = Company.VendorGroup
        		related.Vendor        		  = Vendor
        		related.VendorLocation		  = PurchaseFromLocation
     	
    	ContractRebateQualifierItemsRel
			one-to-many relation to ContractRebateQualifierItem
			Field Mapping uses ByItemNumber
				related.ItemNumber				= LocalItem
			Instance Selection
				where (!related.Contract.ContractStatus.Closed)
				
		QualifierItemsForContractRel
			one-to-many relation to ContractRebateQualifierItem
			Field Mapping uses ByItemNumber
				related.ItemNumber					= LocalItem
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
		
		VendorReturnLineForOrigRel is a VendorReturnLine set
			Instance Selection
		 		where (related.OriginalPurchaseOrder entered)
		 		
		QualifierCodesManufInfoLevel2ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByManufInfo
				related.ManufacturerInfo.ManufacturerCode		= LocalManufacturerInformation.Manufacturer.ManufacturerCode
				related.ManufacturerInfo.ManufacturerDivision	= LocalManufacturerInformation.Manufacturer.ManufacturerDivision
				related.ContractGroup					= LocalContractGroup
				related.Contract						= LocalContract
			Instance Selection
				where (related.ItemCategory.ManufacturerInformation)

		QualifierCodesManufInfoLevel1ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByManufInfo
				related.ManufacturerInfo.ManufacturerCode		= LocalManufacturerInformation.Manufacturer.ManufacturerCode
				related.ManufacturerInfo.ManufacturerDivision	= blank
				related.ContractGroup					= LocalContractGroup
				related.Contract						= LocalContract
			Instance Selection
				where (related.ItemCategory.ManufacturerInformation)

		QualifierCodesPurchClassLevel2ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByPurchClass
				related.MajorPurchasingClass		= LocalPurchMajcl
				related.MinorPurchasingClass		= LocalPurchMincl
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
			Instance Selection
	    		where (related.ItemCategory.PurchasingClass)
    											
		QualifierCodesPurchClassLevel1ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByPurchClass
				related.MajorPurchasingClass		= LocalPurchMajcl
				related.MinorPurchasingClass		= blank
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
			Instance Selection
	    		where (related.ItemCategory.PurchasingClass)
    											
		QualifierCodesUnspscCodeLevel4ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= LocalIcItemCode.UNSPSCFamily 
				related.UnspscCode.UNSPSCClass			= LocalIcItemCode.UNSPSCClass
				related.UnspscCode.UNSPSCCommodity		= LocalIcItemCode.UNSPSCCommodity
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode)
    											
		QualifierCodesUnspscCodeLevel3ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= LocalIcItemCode.UNSPSCFamily 
				related.UnspscCode.UNSPSCClass			= LocalIcItemCode.UNSPSCClass
				related.UnspscCode.UNSPSCCommodity		= blank
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode)
    											
		QualifierCodesUnspscCodeLevel2ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= LocalIcItemCode.UNSPSCFamily 
				related.UnspscCode.UNSPSCClass			= blank
				related.UnspscCode.UNSPSCCommodity		= blank
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode)
    											
		QualifierCodesUnspscCodeLevel1ForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= blank
				related.UnspscCode.UNSPSCClass			= blank
				related.UnspscCode.UNSPSCCommodity		= blank
				related.ContractGroup				= LocalContractGroup
				related.Contract					= LocalContract
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode)
		
		QualifierCodesLocalCommodityCodeForContractRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByCommCode
				related.CommodityCode		= LocalPOLineCommodityCode.CommodityCode
				related.ContractGroup		= LocalContractGroup
				related.Contract			= LocalContract
			Instance Selection
				where (related.ItemCategory.CommodityCode)
		
		ContractRebateQualifierCodesCommodityCodeLevels1Thru5Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByCommCode
				related.CommodityCode		= LocalCommodityCode
			Instance Selection
				where (related.ItemCategory.CommodityCode
				and   !related.Contract.ContractStatus.Closed)
				
		ContractRebateQualifierCodesCommodityCodeLevel6Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByCommCode
				related.CommodityCode		= LocalPOLineCommodityCode
			Instance Selection
				where (related.ItemCategory.CommodityCode
				and   !related.Contract.ContractStatus.Closed)
		ContractRebateQualifierCodesManufInfoLevel1Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByManufInfo
				related.ManufacturerInfo.ManufacturerCode		= LocalManufacturerInformation.Manufacturer.ManufacturerCode
				related.ManufacturerInfo.ManufacturerDivision	= blank
			Instance Selection
				where (related.ItemCategory.ManufacturerInformation
				and   !related.Contract.ContractStatus.Closed)

		ContractRebateQualifierCodesManufInfoLevel2Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByManufInfo
				related.ManufacturerInfo.ManufacturerCode		= LocalManufacturerInformation.Manufacturer.ManufacturerCode
				related.ManufacturerInfo.ManufacturerDivision	= LocalManufacturerInformation.Manufacturer.ManufacturerDivision
			Instance Selection
				where (related.ItemCategory.ManufacturerInformation
				and   !related.Contract.ContractStatus.Closed)

		ContractRebateQualifierCodesPurchClassLevel1Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByPurchClass
				related.MajorPurchasingClass		= LocalPurchMajcl
				related.MinorPurchasingClass		= blank
			Instance Selection
	    		where (related.ItemCategory.PurchasingClass
				and   !related.Contract.ContractStatus.Closed)
    											
		ContractRebateQualifierCodesPurchClassLevel2Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByPurchClass
				related.MajorPurchasingClass		= LocalPurchMajcl
				related.MinorPurchasingClass		= LocalPurchMincl
			Instance Selection
	    		where (related.ItemCategory.PurchasingClass
				and   !related.Contract.ContractStatus.Closed)
    											
		ContractRebateQualifierCodesUnspscCodeLevel4Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode		= LocalIcItemCode
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode
				and   !related.Contract.ContractStatus.Closed)
    											
		ContractRebateQualifierCodesUnspscCodeLevel3Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= LocalIcItemCode.UNSPSCFamily 
				related.UnspscCode.UNSPSCClass			= LocalIcItemCode.UNSPSCClass
				related.UnspscCode.UNSPSCCommodity		= blank
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode
				and   !related.Contract.ContractStatus.Closed)
	    		
		ContractRebateQualifierCodesUnspscCodeLevel2Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= LocalIcItemCode.UNSPSCFamily 
				related.UnspscCode.UNSPSCClass			= blank
				related.UnspscCode.UNSPSCCommodity		= blank
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode
				and   !related.Contract.ContractStatus.Closed)
    											
		ContractRebateQualifierCodesUnspscCodeLevel1Rel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode.UNSPSCSegment		= LocalIcItemCode.UNSPSCSegment
				related.UnspscCode.UNSPSCFamily			= blank
				related.UnspscCode.UNSPSCClass			= blank
				related.UnspscCode.UNSPSCCommodity		= blank
			Instance Selection
	    		where (related.ItemCategory.UNSPSCCode
				and   !related.Contract.ContractStatus.Closed)
		
		CreditMemoPayablesInvoiceRel
        	one-to-many relation to PayablesInvoice
            Field Mapping uses ByReturnNumber           	
				related.Company               	= Company                
                related.VendorReturn          	= VendorReturn
				related.Vendor         	  		= Vendor
            Instance Selection
                where (related.InvoiceType     	= "C")    
		
		PayablesInvoiceRel
        	one-to-one relation to PayablesInvoice
            Field Mapping uses symbolic key           	
				related.Company               = Company                
                related.PayablesInvoice       = LocalPayablesInvoice
		
		IntrastatHeaderRel
			one-to-many relation to IntrastatHeader
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.TaxEntity				= Company.AccountingEntity
			Instance Selection
				where (related.PurchaseOrder  = ReplacementPurchaseOrder
				and    related.DocumentNumber = VendorReturn
				and	   related.Vendor		  = Vendor)
				
		TaxEntityRel
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.TaxEntity				= Company.AccountingEntity
				
		PurgeReplacementPurchaseOrderRel
			one-to-many relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company					= Company
				related.PurchaseOrder			= ReplacementPurchaseOrder
			Instance Selection
				include deleted records
				
		PurchaseOrderWithReceiptRel
			one-to-many relation to PurchaseOrderReceipt
			Field Mapping uses Set3
				related.Company					= Company
				related.PurchaseOrder			= ReplacementPurchaseOrder
				
		SupplierContactMessageRel
			one-to-many relation to SupplierContactMessage
			Field Mapping uses symbolic key
				related.SupplierGroup				= actor.agent(SupplierSourceId).SupplierGroup
 				related.Supplier  			 		= actor.agent(SupplierSourceId).Supplier
				related.SupplierSourceId            = actor.agent(SupplierSourceId).SupplierSourceId
			Instance Selection
				where (related.OriginatingCompany			= Company
				and    related.OriginatingReturn            = VendorReturn) 

		SupplierGroupRel
			one-to-one relation to SupplierGroup
			Field Mapping uses symbolic key
				related.SupplierGroup = Company.VendorGroup

		SupplierRel
			one-to-many relation to Supplier
			Field Mapping uses ByVendor
				related.SupplierGroup				= SupplierGroupRel.SupplierGroup
 				related.Vendor						= Vendor

		SupplierMessageDialogRel
			one-to-many relation to SupplierMessageDialog

			Field Mapping uses symbolic key
				related.SupplierGroup								= Company.ProcurementGroup
			Instance Selection
				where (related.SupplierContactMessage.OriginatingCompany	= Company
				and    related.SupplierContactMessage.OriginatingReturn    = VendorReturn)








		SupplierSourceIdValidRel
			one-to-one relation to SupplierSourceId
			Field Mapping uses symbolic key
				related.SupplierGroup    = SupplierGroupRel.SupplierGroup
				related.Supplier         = LocalSupplier
				related.SupplierSourceId = LocalSupplierContact 

		ActorRel
			one-to-one relation to Actor
			Field Mapping uses symbolic key
				related.Actor					= UserId
				

		PurgeProcurementTransactionDistributionRel
			one-to-many relation to ProcurementTransactionDistribution
			Field Mapping uses Set1
				related.Company								= Company
			Instance Selection
				include deleted records
				where (related.ProcurementDocumentNumber	= blank
				and   related.POCode						= blank
				and   related.Location						= ReturnFromLocation
				and   related.DocumentNumberNumeric			= VendorReturn
				and	  related.SystemCode					= "PO"
				and	  related.DocumentType					= "VR")
				
		PurgePurchaseOrderTransactionDetailRel
			one-to-many relation to PurchaseOrderTransactionDetail
			Field Mapping uses symbolic key
				related.Company = Company
			Instance Selection
				include deleted records
				where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.VendorReturn
				and	  related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = VendorReturn)
		
		PurgeVendorReturnCommentRel is a VendorReturnComment set
			Instance Selection
				include deleted records
				
		PurgeVendorReturnLineRel is a VendorReturnLine set
			Instance Selection
				include deleted records		


		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter

		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "VendorReturn"
            	related.FSMBODConfiguration.Direction 	= 1
				
		FSMBODCleanDocCrossReferenceRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
            	related.FSMBODCrossReference.SourceField 		= "CSFGLC"
            	related.FSMBODCrossReference.DestinationField 	= "DESTAE"
			Instance Selection
				where (related.SourceValue	= DerivedCrossGLC)




		GeneralLedgerCompanyRel 
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company	= Company
		
		FSMBODConfigRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "SupplierRMA"
            	related.FSMBODConfiguration.Direction 	= 1

		FSMInboundBODTrackerRel
			one-to-one relation to FSMInboundBODTracker
			Field Mapping uses symbolic key
       			related.FSMInboundBODTracker	= LocalFSMInboundBODTracker	

    Sets

        Set2
            indexed
            Sort Order
                Company
                Vendor
                CreationDate descending
                VendorReturn

        Set3
            indexed
            Instance Selection
                where (HasReferenceNumber)
            Sort Order
                Company
                ReferenceNumber
                VendorReturn

        Set1
            not indexed
            Sort Order
                Company
                VendorReturn descending
                
 	Rule Blocks
 		CreatePurchaseOrderForReplaceGoods
	 		if (ReplaceGoods)
				invoke Unreleased.Create PurchaseOrder
					assign result to NewPurchaseOrderView
					invoked.Company					= Company
					invoked.POCode 					= VendorReturnPOCode
					initialize invoked.PORelease
					invoked.Vendor 					= Vendor
					invoked.PurchaseFromLocation 	= PurchaseFromLocation				
					invoked.Buyer 					= ReplacementPurchaseOrderBuyer 
					invoked.VendorReturn 			= VendorReturn
					invoked.ProcessLevel 			= ProcessLevel
					invoked.NatureOfTransactionCode = NatureOfTransactionCode
					invoked.ShipToLocation 			= ReturnFromLocation
	
				ReplacementPurchaseOrder = NewPurchaseOrderView.PurchaseOrder
		
		UpdateReturnStockToInventoryOnVendorReturnCancel
			if (ReturnStockToInventoryOnVendorReturnCancel changed)								
				invoke UpdateReturnStockToInventoryOnVendorReturnCancel VendorReturnLinesRel
					invoked.PrmReturnStockToInventoryOnVendorReturnCancel = ReturnStockToInventoryOnVendorReturnCancel
 							
		
		ProcessContractRebateQualifiers				
			invoke UpdateContractRebateDueInvoiceLine each
				invoked.ParameterManufacturerContract		= LocalOriginalContract
		    	invoked.ParameterInvoiceDte					= PayablesInvoiceRel.InvoiceDate
		    	invoked.ParameterCompany					= Company
		    	invoked.ParameterVendor						= Vendor
		    	invoked.ParameterInvoice 					= LocalInvoiceNumber
		    	initialize invoked.ParameterSuffix						
		        invoked.ParameterPoNumber  					= LocalPurchaseOrder
		        invoked.ParameterLineNbr   					= LocalLineNbr
    			invoked.ParameterManufacturerUnitCost		= LocalManufacturerUnitCost
    			invoked.ParameterMatchUnitCost				= LocalReturnUnitCost
    			invoked.ParameterMatchedQty					= LocalQuantity
    			invoked.ParameterEntUnitCst					= LocalUnitCost
    			invoked.ParameterCmContract					= LocalCmContract
    			invoked.ParameterProcureGroup				= LocalContractGroup
    			invoked.ParameterMfgContract				= LocalMfgContract
    			invoked.ParameterLocation					= LocalLocation
    			invoked.ParameterManufCode					= LocalManufacturerInformation.Manufacturer.ManufacturerCode
    			invoked.ParameterManufDivision				= LocalManufacturerInformation.Manufacturer.ManufacturerDivision
    			invoked.ParameterServiceTypeContract		= false
    			invoked.ParameterPoDate						= LocalPoDate
    			invoked.ParameterVendorReturn				= true
				invoked.ParameterCurrencyCode				= LocalCurrencyCode
				invoked.ParameterExchangeRate				= LocalExchangeRate
				invoked.ParameterCurrencyTable				= Company.CurrencyTable
				invoked.ParameterPurchFrLocation			= PurchaseFromLocation
				initialize invoked.ParameterInvoiceStatus
				invoked.ParameterPayablesCompany			= Company
				invoked.ParameterPayablesInvoice			= LocalPayablesInvoice			
				invoked.ParameterPurchaseOrder				= LocalPurchaseOrder
				invoked.ParameterPurchaseOrderLine			= LocalPurchaseOrderLine
				invoked.ParameterPayablesInvoiceDetail		= LocalPayablesInvoiceDetail
										 
 	Field Rules
 		VendorRequestedReplacementDate
 			if (not ReplaceGoods)
 				cannot be entered
 					"ReplacementDateOnlyValidIfReplaceGoodsIsYes"
 	
 		Company
 			constraint (MatchCompanyRel exists)
 				"MatchCompanyDoesNotExist"	
 		
 		PurchaseFromLocation
 			default to CreatedFromPurchaseOrder.PurchaseFromLocation
 			
 		VendorReturnPOCode
 			default to CreatedFromPurchaseOrder.POCode																		
 				
 		ReturnFromLocation
 			default to CreatedFromPurchaseOrder.ShipToLocation
			
			required																								
 				"ReturnFromLocationIsRequired"																		

			if (VendorReturnLineForOrigRel exists)																	
				cannot be changed
					"LocationCannotBeChanged,Line<first VendorReturnLinesRel.VendorReturnLine>HasPurchaseOrderReference"															
 		
 		Vendor																										
 			if (CreatedFromPurchaseOrder entered)
 				force default to CreatedFromPurchaseOrder.Vendor
 				
 			required						
 				"VendorIsRequired"																					
 
 			if (VendorReturnLineForOrigRel exists)
 				cannot be changed
 					"VendorCannotBeChanged,Line<first VendorReturnLinesRel.VendorReturnLine>HasPurchaseOrderReference"										
 		
 		VendorClaimType
 			initial value is CreatedFromPurchaseOrder.Vendor.VendorClaimType
 			default to Vendor.VendorClaimType
 			default to MatchCompanyRel.VendorClaimType
 			required
 			
 		ReplaceGoods
			if (ReplaceGoods not changed)
				if (TransientReplaceGoods.Yes)
					ReplaceGoods = true
				else
				if (TransientReplaceGoods.No)
					ReplaceGoods = false
				else
					if (Vendor.ReplaceGoods.UseCompanyValue)
						ReplaceGoods = MatchCompanyRel.ReplaceGoods
					else
					if (Vendor.ReplaceGoods.Yes)
						ReplaceGoods = true
					else
						ReplaceGoods = false
						
			if (not ReplaceGoods)
				initialize VendorRequestedReplacementDate
			








 			
 		ShipOrHold
 			initial value is CreatedFromPurchaseOrder.Vendor.ShipOrHold
 			default to Vendor.ShipOrHold
 			default to MatchCompanyRel.ShipOrHold
 			
 		UserId
 			default to actor
 		
 		CreatedFromPurchaseOrder
 			constraint (!CreatedFromPurchaseOrder.Closed.Historical)
 				"CannotCreateVendorReturnForHistoricalPurchaseOrder"                								
 				
		Dropship
			default to CreatedFromPurchaseOrder.Dropship																

		DropshipName
 			if (Dropship)
 				default to CreatedFromPurchaseOrder.DropshipName
 				required
 			else
 				cannot be entered
 				
		DropshipAddress
 			if (Dropship)
 				default to CreatedFromPurchaseOrder.DropshipAddress
 				required
 			else
 				cannot be entered
 				
 		ReplacementPurchaseOrderBuyer	
 			default to CreatedFromPurchaseOrder.Buyer
 					
 			if (ReplaceGoods)
 				required
 				
 		ReplacementPurchaseOrderShippingMethod
 			if (!ReplaceGoods)
 				cannot be entered     																				
 					"ShipViaOnlyValidWhenReplaceGoodsIsSelected"														
				
 		Currency
			default to CreatedFromPurchaseOrder.Currency
			default to VendorLocationRel.InvoiceCurrency
			default to Vendor.InvoiceCurrency
			default to Company.Currency
 				







 					
 		NatureOfTransactionCode
 			if (ReplaceGoods)
 				default to CreatedFromPurchaseOrder.NatureOfTransactionCode
		
		ShipTerm
			default to PurchasingVendorRel.ShipTerm
			
		ReturnStockToInventoryOnVendorReturnCancel
			default to Company.ReturnStockToInventoryOnVendorReturnCancel
 			
 		VendorReturnShippingMethod
 			if (ShipOrHold.Ship)
 				default to PurchasingVendorRel.VendorReturnShippingMethod
 			else
 				cannot be entered			
 					"ShipViaOnlyValidIfVendorRequiresShipment" 														
 		
 		ReplacementFreightAmount
 			if (AddOnCharge entered)	
 				required							
 					"ReplacementFreightAmountIsRequiredIfAddOnChargeIsEntered"										
 			
 		AddOnCharge
 			if (ReplacementFreightAmount entered)
 				required							
 					"AddOnChargeIsRequiredIfReplacementFreightAmountIsEntered"										
 				
 		CreationDate
 			default to current corporate date   																				
 		
 		LastUpdateDate
 			default to current timestamp
 			
 		LastUpdateBy
 			default to actor		
 		
 		OriginalFreightCreditAmount
 			if (OriginalFreightCreditAmount changed)
 				ReturnValue += OriginalFreightCreditAmount - old OriginalFreightCreditAmount
 			
 		ReturnToVendorFreightCreditAmount
 			if (ReturnToVendorFreightCreditAmount changed)
 				ReturnValue += ReturnToVendorFreightCreditAmount - old ReturnToVendorFreightCreditAmount
 			
 		HandlingAmount
 			if (HandlingAmount changed)
 				ReturnValue += old HandlingAmount - HandlingAmount
 		
 		ReturnValue
 			if (VendorClaimType.NoCharge)
 				initialize ReturnValue
		
		OriginalFreightCreditAccount
			if (OriginalFreightCreditAmount entered)
				default to ReturnFromLocation.FreightAccount
				required
					"FreightAccountIsRequiredIfFreightAmountEntered"
			else
				initialize OriginalFreightCreditAccount
				
		ReturnToVendorFreightCreditAccount
			if (ReturnToVendorFreightCreditAmount entered)
				default to ReturnFromLocation.FreightAccount
				required
					"ReturnFreightAccountIsRequiredIfReturnFreightAmountEntered"
			else
				initialize ReturnToVendorFreightCreditAccount
				
		HandlingAccount
			if (HandlingAmount entered)
				default to ReturnFromLocation.HandlingAccount
				required
					"HandlingAccountIsRequiredIfHandlingAmountEntered"
			else
				initialize HandlingAccount
		
		ProcessLevel
			default to CreatedFromPurchaseOrder.ProcessLevel
			
			if (ReplaceGoods)
 				default to ReturnFromLocation.ProcessLevel
			
			if (VendorClaimType.Chargeback)
				default to first PayablesProcessLevelRel.PayablesProcessLevel
	
	Create Rules
	
	StateCycles
	
		VendorReturnCycle is a StateCycle
			state field is Status
			
			Added is a State
				
				Create is a Create Action

				CreateFromPurchaseOrder is a Create Action
					Field Rules
						CreatedFromPurchaseOrder
						 	required
						 	
		 			Action Rules
		 				if (CreatedFromPurchaseOrder entered)
		 					if (Dropship)
		 						constraint (first PurchaseOrderLinesWithMatchQuantityRel exists)
		 							"SelectedPurchaseOrderHasNoLineWithReceiptsOrMatches"								
		 					else
		 						constraint (first PurchaseOrderLinesWithReceivedQuantityRel exists)
		 							"SelectedPurchaseOrderHasNoLineWithReceiptsOrMatches"								
		 									
		 				constraint (CreatedFromPurchaseOrder entered)
		 					"PurchaseOrderIsRequired"
					Exit Rules	
				 		invoke UpdateFromVendorReturn CreatedFromPurchaseOrder
				 			invoked.PrmVendorReturn = VendorReturn
				 		
				 		
 				Authorize is an Instance Action
		 			Action Rules
		 				if (VendorClaimType.NoCharge)

							if (CreatedFromPurchaseOrder not entered or ReturnLinesForPurchaseOrderLinesWithCostRel exist)
								constraint (ReplaceGoods)
	 								"GoodsMustBeReplacedIfVendorClaimTypeIsNoCharge" 
	 								
 						if (Company.RequireReasonCodeOnVendorReturn)
 							constraint (ReasonCode entered)
								"ReasonCodeIsRequired"						
		 				if (ReplaceGoods)
				 			constraint (VendorRequestedReplacementDate entered)
								"ReplacementDateIsRequiredForReplaceGoods"											
				 		else
				 			constraint (VendorRequestedReplacementDate not entered)		
 								"ReplacementDateCannotBeEnteredIfNotReplaceGoods"												

 						
 						
 						if ((VendorClaimType.Chargeback)
 						or  (ReplaceGoods 
 						and  (PurchasingCompanyRel.RequireProcessLevel
 						or    PurchasingCompanyRel.DefaultProcessLevelFromHeaderLocation)))
 							constraint(ProcessLevel entered)
 								"ProcessLevelIsRequired"

		 				constraint (VendorReturnLinesRel exist)
		 					"CannotAuthorizeReturnWithNoLines"															
		 				
		 				constraint (HandlingAmount <= ReturnValue)			
		 					"HandlingChargeAmountCannotExceedReturnTotal"												
						
						if (!VendorClaimType.NoCharge)
							constraint (Vendor.WriteOffAmount <= ReturnValue)
								"CannotAuthorize;ReturnAmountIsLessThanMinimumVendorReturnAmount"							
						
		 				if (PurchaseFromLocation entered)
			 				if (PurchaseFromLocation.ReturnMaterialAuthorizationRequired)
		 						constraint (ReturnMaterialAuthorizationNumber entered)
		 							"ReturnMaterialAuthorizationNumberIsRequired"											
		 				else
		 					if (Vendor entered
		 					and PurchasingVendorRel.ReturnMaterialAuthorizationRequired)
		 						constraint (ReturnMaterialAuthorizationNumber entered)
		 							"ReturnMaterialAuthorizationNumberIsRequired"										
		 				
		 				if (LineCostDiffersFromMatchCost)
		 					confirmation required
		 						"LineCostDiffersFromMatchCost.DoYouWantToContinue?"
		 						
						invoke Authorize VendorReturnLinesRel
						
						ReturnAuthorizationDate = current corporate date
						LastUpdateDate 			= current timestamp
						LastUpdateBy   			= actor	 
						
						make transition to AuthorizedByVendor
													
 				Update is an Update Action
 					Action Rules
	 					include UpdateReturnStockToInventoryOnVendorReturnCancel
						
						if (TaxEntityRel.ThirdParty.TaxEngine)
							constraint (!TemporaryHold.SentForApproval)
								"CannotUpdateWhileAwaitingTaxApproval"
							constraint (!TemporaryHold.ApprovedByEngine)
								"CannotUpdateOnceApprovedByTaxEngine"
	 					
	 					if (IsHSNSACCodeEnabled
	 					and VendorReturnLinesRel exists)
		 					if ((ReturnFromLocation changed
		 					or   PurchaseFromLocation changed)
		 					or  (PurchaseFromLocation not entered
		 					and  Vendor changed))
		 						confirmation required
		        					"ChangingReturnFromLocationOrPurchaseFromLocationOrVendorChangesTaxCodeInEachLine,Continue?"
	        					invoke Update VendorReturnLinesRel
	        						initialize invoked.TaxCode	
	 					
 				Delete is a Delete Action
					Action Rules	
						if (TaxEntityRel.ThirdParty.TaxEngine)
							constraint (!TemporaryHold.SentForApproval)
								"CannotDeleteWhileAwaitingTaxApproval"
							constraint (!TemporaryHold.ApprovedByEngine)
								"CannotDeleteOnceApprovedByTaxEngine"

			AuthorizedByVendor is a State
				Entrance Rules
					if (ShipOrHold.Hold)  	
						invoke AuthorizedByVendor.Ship
						
				Update is an Update Action															
	 				Action Rules
						if (TaxEntityRel.ThirdParty.TaxEngine)
							constraint (!TemporaryHold.SentForApproval)
								"CannotUpdateWhileAwaitingTaxApproval"
							constraint (!TemporaryHold.ApprovedByEngine)
								"CannotUpdateOnceApprovedByTaxEngine"
	 					include UpdateReturnStockToInventoryOnVendorReturnCancel	

				TaxEngineApprove is an Instance Action 
					restricted
					Action Rules
						TemporaryHold = TemporaryHold.ApprovedByEngine

 				Delete is a Delete Action
					Action Rules	
						if (TaxEntityRel.ThirdParty.TaxEngine)
							constraint (!TemporaryHold.SentForApproval)
								"CannotDeleteWhileAwaitingTaxApproval"
							constraint (!TemporaryHold.ApprovedByEngine)
								"CannotDeleteOnceApprovedByTaxEngine"
							
					
 				Ship is an Instance Action 

					completion message is "<CompletionMessage>"
 					Local Fields
 						LocalTrigger 						is Alpha 1
 						LocalDestinationCountry				is like Country
						LocalDestinationRegion				is like Region
		 			Action Rules

		 				if (ReplaceGoods)
				 			constraint (VendorRequestedReplacementDate entered)
								"ReplacementDateIsRequiredForReplaceGoods"											
				 		else
				 			constraint (VendorRequestedReplacementDate not entered)		
 								"ReplacementDateCannotBeEnteredIfNotReplaceGoods"	
								 						
						constraint (!TaxEntityRel.ThirdParty.TaxEngine
						or !TemporaryHold.SentForApproval)
							"ShipmentAlreadySentForApproval"								

						if (TaxEntityRel.ThirdParty.TaxEngine
						and !TemporaryHold.ApprovedByEngine)
							invoke PutOnHold		
 							invoke TriggerBROrderVendorReturn
		 				else
							ShippedBy = actor
							
							if (TaxEntityRel.ThirdParty.TaxEngine)
								for each VendorReturnLinesRel
									invoke TaxEngineApprove each
							
							if (Dropship	
							and ReplaceGoods)
								ReplacementPurchaseOrderClosed	= true
											
							if (ShipmentDate not entered)
								ShipmentDate	= current corporate date
							
							if (ShipmentTime not entered)
								ShipmentTime	= current timestamp
							
							if (InventoriedItemVedorReturnLinesRel exists)
								invoke Unreleased.Create InventoryTransaction
									assign result to NewInventoryTransaction
									fill in fields from this instance
									invoked.InventoryLocation			 	= ReturnFromLocation
									invoked.InventoryDocumentType 			= "VR" 
									invoked.RelatedVendorReturn				= VendorReturn
									invoked.OriginatingTransaction			= reference to this instance
							
							RelatedInventoryTransaction = NewInventoryTransaction.InventoryTransaction	
							
							include CreatePurchaseOrderForReplaceGoods							
						
							if (ReplacementFreightAmount entered)
								invoke Create PurchaseOrderAndLineAddOnCharge
									fill in fields from this instance
									invoked.PurchaseOrderAndLineAddOnCharge.Company			= Company
									invoked.PurchaseOrderAndLineAddOnCharge.PurchaseOrder	= ReplacementPurchaseOrder
									invoked.PurchaseOrderAndLineAddOnCharge.AddOnCharge		= AddOnCharge
									invoked.ShipToLocation									= ReturnFromLocation
									invoked.AddOnChargeOrigin								= "T"
									invoked.TotalAddOnChargeAmount						= ReplacementFreightAmount
									invoked.AddOnChargeOrigin								= "PO"								

							if (ReplacementPurchaseOrder entered)
								invoke UpdateReplacementPurchaseOrderLines VendorReturnLinesRel

					Exit Rules
						if (TaxEntityRel.ThirdParty.TaxEngine
						and TemporaryHold.SentForApproval)
							CompletionMessage = SentToEngineCompletionMessage
						else
							CompletionMessage = ShipCompletionMessage

						if (!IsOnHold)

							if (TaxEntityRel.IntrastatUsed)
								initialize IntrastatProcessing
								IntrastatProcessing.InFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
								IntrastatProcessing.TaxEntity					= Company.AccountingEntity
								
								if (PurchaseFromLocation entered)
									LocalDestinationCountry						= VendorLocationRel.VendorAddress.Country
									LocalDestinationRegion						= VendorLocationRel.VendorAddress.Region
								else
									LocalDestinationCountry						= Vendor.VendorAddress.Country
									LocalDestinationRegion						= Vendor.VendorAddress.Region
									
								IntrastatProcessing.DestinationCountry			= LocalDestinationCountry
								IntrastatProcessing.DestinationRegion			= LocalDestinationRegion
								IntrastatProcessing.OriginCountry				= ReturnFromLocation.PostalAddressForPurchaseOrder.Country
								IntrastatProcessing.OriginRegion				= ReturnFromLocation.PostalAddressForPurchaseOrder.Region
								LocalTrigger									= IntrastatProcessing.CheckCountries
					
								if (IntrastatProcessing.OutputErrorNumber entered)
									constraint (IntrastatProcessing.OutputErrorMessage not entered)
										"<IntrastatProcessing.OutputErrorMessage>"
							

							if (IntrastatProcessing.DoIntrastatReporting)		
							
								IntrastatProcessing.HeaderFc					= IntrastatProcessing.HeaderFc.Create
								IntrastatProcessing.FiscalYear					= current year
								IntrastatProcessing.InvoiceDate					= CreationDate
								IntrastatProcessing.TransactionType				= IntrastatProcessing.TransactionType.Purchase
								IntrastatProcessing.SystemCode					= "PO"
								IntrastatProcessing.Vendor						= Vendor
								IntrastatProcessing.VendorGrp					= Vendor.VendorGroup
								IntrastatProcessing.PurchaseOrder				= ReplacementPurchaseOrder
								IntrastatProcessing.DocumentNumber				= VendorReturn
								IntrastatProcessing.NatureOfTransactionCode		= NatureOfTransactionCode
								IntrastatProcessing.VendorCurrency				= Currency
								IntrastatProcessing.DestinationCountry			= LocalDestinationCountry
								IntrastatProcessing.DestinationRegion			= LocalDestinationRegion
								IntrastatProcessing.OriginCountry				= ReturnFromLocation.PostalAddressForPurchaseOrder.Country
								IntrastatProcessing.OriginRegion				= ReturnFromLocation.PostalAddressForPurchaseOrder.Region
								IntrastatProcessing.LoadingPort					= LoadingPort
								IntrastatProcessing.TransportMode				= VendorReturnShippingMethod
								IntrastatProcessing.DeliveryTerms				= ShipTerm
								
								if (Dropship) 
									IntrastatProcessing.DropShip				= DropShipInd.Yes
								else
									IntrastatProcessing.DropShip				= DropShipInd.No
								
								IntrastatProcessing.StatisticalProcedure 		= StatisticalProcedure
								LocalTrigger 									= IntrastatProcessing.MainTrigger

							

							invoke Ship VendorReturnLinesRel
								if (TaxEntityRel.IntrastatUsed)
									invoked.PrmCreateIntrastatDetail	= IntrastatProcessing.DoIntrastatReporting
									invoked.PrmIntrastatNumber			= first IntrastatHeaderRel.IntrastatHeader.IntrastatNumber
									invoked.PrmDestinationCountry		= LocalDestinationCountry
									invoked.PrmDestinationRegion		= LocalDestinationRegion
									invoked.PrmOriginCountry			= ReturnFromLocation.PostalAddressForPurchaseOrder.Country
									invoked.PrmOriginRegion				= ReturnFromLocation.PostalAddressForPurchaseOrder.Region
							




							

							if (VendorClaimType.Chargeback)															
								if (HandlingAmount entered)															
									invoke Create PayablesInvoiceDistribution										
										fill in fields from RelatedPayablesInvoice									
											except invoked.TaxCode													
											except invoked.TaxableAmount											
										invoked.PayablesInvoice 		 			= RelatedPayablesInvoice		
										invoked.GLFinanceCodeBlock					= HandlingAccount				
										invoked.DistributionAmount.CurrencyAmount	= HandlingAmount				
										invoked.DistributionAmount.ExchangeDate		= ReturnAuthorizationDate		
										invoked.GLTransactionAmount 	 			= HandlingAmount				
										invoked.Description 	 					= "VendorReturnHandlingCharge"	

							

								if (OriginalFreightCreditAmount entered)													
									invoke Create PayablesInvoiceDistribution												
										fill in fields from RelatedPayablesInvoice											
											except invoked.TaxCode															
											except invoked.TaxableAmount													
										invoked.PayablesInvoice 		 				= RelatedPayablesInvoice			
										invoked.GLFinanceCodeBlock						= HandlingAccount					
										invoked.DistributionAmount.CurrencyAmount	 	= OriginalFreightCreditAmount * -1	
										invoked.DistributionAmount.ExchangeDate		 	= ReturnAuthorizationDate			
										invoked.GLTransactionAmount 	 				= OriginalFreightCreditAmount * -1	
										invoked.Description 	 						= "VendorReturnOriginalFreight"		

								

								if (ReturnToVendorFreightCreditAmount entered)														
									invoke Create PayablesInvoiceDistribution														
										fill in fields from RelatedPayablesInvoice													
											except invoked.TaxCode																	
											except invoked.TaxableAmount															
										invoked.PayablesInvoice 		 				= RelatedPayablesInvoice					
										invoked.GLFinanceCodeBlock						= HandlingAccount							
										invoked.DistributionAmount.CurrencyAmount	 	= ReturnToVendorFreightCreditAmount * -1 	
										invoked.DistributionAmount.ExchangeDate		 	= ReturnAuthorizationDate					
										invoked.GLTransactionAmount 	 				= ReturnToVendorFreightCreditAmount	* -1 	
										invoked.Description 	 						= "VendorReturnFreightCredit"				

							
							
							if (ReplacementPurchaseOrder entered)
								invoke Unreleased.Release ReplacementPurchaseOrder
								
							invoke TransitionToReleased RelatedInventoryTransaction
							

							if (VendorClaimType.Chargeback)

								invoke Unreleased.Release RelatedPayablesInvoice

								
							if (VendorClaimType.CreditMemo
							or  ReplaceGoods)
								if (LocalLocalizationFound)
									constraint (!TemporaryHold.SentForApproval)
										"SentToEngine"															
								else																 
									make transition to WaitingForVendorAction
							else
								make transition to Closed

							if (Company.FinanceEnterpriseGroup.BODTrigger)
								LocalTitle = "EG:"+Company.FinanceEnterpriseGroup+" CO:"+Company+" VR:"+VendorReturn	
									increment bod id.VariationID
									ActionCode = ActionCode.Update
									trigger PayablesService.VendorReturnService PA service
										resume on error
										title is "<LocalTitle>"
										Criteria
											Company.FinanceEnterpriseGroup
											Company
										Variables
											ActionCode
											include persistent fields from VendorReturn
											include persistent fields from Vendor
											bod id.VariationID
												variable name is VariationId
											Company.FinanceEnterpriseGroup
												variable name is FinanceEnterpriseGroup	
											Company.GeneralLedgerCompany.AccountingEntity
												variable name is AccountingEntity






 									
			WaitingForVendorAction is a State
				UpdateReasonCode is an Instance Action
					Parameters
						PrmReasonCode is a ProcurementReasonCode
						
					Parameter Rules
						PrmReasonCode
							required
						
					Action Rules
						ReasonCode = PrmReasonCode
				
				UpdateReturnStockToInventoryOnVendorReturnCancel is an Instance Action
					Parameters
						PrmReturnStockToInventoryOnVendorReturnCancel is Boolean
							default label is "ReturnStockToInventoryOnVendorReturnCancel"
						
					Action Rules
						ReturnStockToInventoryOnVendorReturnCancel = PrmReturnStockToInventoryOnVendorReturnCancel
						include UpdateReturnStockToInventoryOnVendorReturnCancel

				Cancel is an Instance Action
					Action Rules
						invoke CancelVendorReturn
					Exit Rules
						if (ReplacementPurchaseOrder entered)
							invoke CancelFromVendorReturn ReplacementPurchaseOrder	 
								invoked.PrmCancelPOFromVendorReturn	= true


						
		 		Close is an Instance Action
		 			Action Rules
		 				LastUpdateDate		 = current timestamp
		 				LastUpdateBy		 = actor
		 				
		 				if ((CreditMemoOrChargeback
                        and ReturnValue = CreditReceived)
                        or ReverseCreditReceived >= ReturnValue)	
							FullyCredited     = true
							
		 				if (ReplacementPurchaseOrderClosed
                        or (CreditMemoOrChargeback
                        and FullyCredited)
                        or CreditMemoOrNoCharge)		
		 					CloseDate         = current corporate date
                            make transition to Closed		

							if (VendorClaimType.CreditMemo)
								if (InventoriedItemVedorReturnLinesRel exists)
									if (VendorReturnLinesForAdjustmentRel exists
									and ReturnLossCost > 0)
										invoke Unreleased.Create InventoryTransaction
											assign result to NewInventoryTransaction
											fill in fields from this instance
											invoked.InventoryLocation							= ReturnFromLocation
											invoked.InventoryDocumentType  						= "VA" 
											invoked.RelatedVendorReturn							= VendorReturn
											invoked.OriginatingTransaction						= reference to this instance

									invoke Close VendorReturnLinesRel
										invoked.PrmInventoryTransaction = NewInventoryTransaction.InventoryTransaction

								if (NewInventoryTransaction.InventoryTransaction entered)
									invoke FinalRelease NewInventoryTransaction.InventoryTransaction

		 				ReplacementPurchaseOrderClosed = true	
						
				AutoClose is an Instance Action													
					restricted
		 			Parameters
		 				PrmCreditAmount			is an InternationalAmount
		 				
		 			Action Rules
		 				invoke AutoClose  VendorReturnLinesRel					
		 					invoked.PrmCreditAmount = PrmCreditAmount							
		 					
		 				if (ReplacementPurchaseOrderClosed)
		 					make transition to Closed											
		 				else
			 				if (VendorClaimType.CreditMemo
			 				and ReturnValue = CreditReceived)
			 					FullyCredited	= true
			 					make transition to Closed
			 				else
			 					if (CreditReceived >= ReturnValue)
			 						FullyCredited	= true
			 					if ((VendorClaimType.CreditMemo
			 					or  VendorClaimType.Chargeback)
			 					and FullyCredited)
			 						make transition to Closed
			 					if (VendorClaimType.NoCharge)
			 						make transition to Closed	
		 				
		 				ReplacementPurchaseOrderClosed = true	
			
			Closed is a State
				Entrance Rules
					if (!VendorClaimType.CreditMemo)
						invoke Close  VendorReturnLinesRel

					if (TaxEntityRel.ThirdParty.TaxEngine)
						TemporaryHold = TemporaryHold.ApprovedByEngine	

					ReplacementPurchaseOrderClosed = true
				
				Cancel is an Instance Action
					valid when (not Canceled)
					Action Rules	
						
						invoke CancelVendorReturn		
						
	Actions
		ReopenFromInvoice is an Instance Action 
			restricted
			Action Rules
				LastUpdateDate		 = current timestamp
				LastUpdateBy		 = actor
				
				if (CreditMemoOrChargeback
				and FullyCredited)	
					FullyCredited    = false

				if (Status.Closed)
					initialize CloseDate
					make transition to WaitingForVendorAction		

				invoke ReopenFromInvoice VendorReturnLinesRel

		CloseFromPOReceipt is an Instance Action
			restricted
			Action Rules
				LastUpdateDate		 = current timestamp
				LastUpdateBy		 = actor
				
				if (ReturnValue = CreditReceived
				or ReverseCreditReceived >= ReturnValue)	
					FullyCredited     = true
					
				if (ReplacementPurchaseOrderClosed
				or  FullyCredited
				or  VendorClaimType.NoCharge)		
					CloseDate         = current corporate date
					if (not Status.Closed)
						make transition to Closed		

				ReplacementPurchaseOrderClosed = true	

		CopyTo is an Instance Action
			Parameters
				ToReturnFromLocation 				is an InventoryLocation
				ToVendor 							is a Vendor
				
			Parameter Rules
				ToReturnFromLocation
					initial value is ReturnFromLocation
					required
				ToVendor
					initial value is Vendor
					required
	
			Action Rules
				invoke Added.Create VendorReturn
					assign result to NewVendorReturn
					fill in fields from this instance
						except invoked.LastVendorReturnLine
						except invoked.ReplacementPurchaseOrder
					invoked.ReturnFromLocation 	= ToReturnFromLocation
					invoked.Vendor				= ToVendor
					
				LocalVendorReturn 				= NewVendorReturn.VendorReturn
					
				for each VendorReturnLinesRel
					invoke Create VendorReturnLine 
						fill in fields from each
							except invoked.ReturnNumberAlpha
							except invoked.CancelQuantity
							except invoked.ReplacementPurchaseOrderLine
						invoked.Company				= Company
						invoked.VendorReturn		= LocalVendorReturn
						if (IsHSNSACCodeEnabled)
							if (ToReturnFromLocation entered
							or  ToVendor entered)
								initialize invoked.TaxCode
 		
 		UpdateReturnValue is an Instance Action
 			restricted
 			refresh and lock this instance
 			Parameters
				PrmReturnValue 				is an InternationalAmount
 			
 			Action Rules
 				if (PrmReturnValue entered)
					ReturnValue += PrmReturnValue							
		
		UpdateCreditReceived is an Instance Action
 			restricted
 			Parameters
				PrmCreditReceived 				is an InternationalAmount
 			
 			Action Rules
 				if (PrmCreditReceived entered)
					CreditReceived += PrmCreditReceived
					
		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location" 
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
				PrmVariationID       is Alpha size 22
					default label is "VariationID"	
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
				if (bod id.VariationID != PrmVariationID)
					bod id.VariationID			= PrmVariationID				
		
		UpdatePayablesInvoice is an Instance Action
 			restricted
 			Parameters
				PrmRelatedPayablesInvoice 				is an PayablesInvoice
 			
 			Action Rules
 				if (PrmRelatedPayablesInvoice entered)
					RelatedPayablesInvoice = PrmRelatedPayablesInvoice
						
		BuildReturnRebateInvoiceLines is a Set Action
			restricted			
			completion message is "BuildOfReturnRebateInvoiceLinesIsComplete"
			Parameters
				PrmProcurementGroup     	is a ProcurementGroup
				PrmCompany					is Numeric size 4
				PrmVendor					is like Vendor
			    PrmContract					is Numeric size 15		
				PrmBeginInvoiceDate 		is Date
				PrmEndInvoiceDate   		is Date

			Instance Selection
				where ((PrmProcurementGroup	= Company.ProcurementGroup)
				and    (PrmCompany			= Company)
				and    (PrmVendor			= Vendor)
				and    (CreditReceived			entered)
				and    (VendorClaimType.Chargeback
				or      VendorClaimType.CreditMemo)
				and    (((CloseDate			entered)
				and      (CloseDate		 	>= PrmBeginInvoiceDate	
				and       CloseDate		  	<= PrmEndInvoiceDate)
				and      (Status.Closed))
				or      ((CloseDate			not entered)
				and      (ReturnAuthorizationDate		> PrmBeginInvoiceDate - 6 months))))
				
			Sort Order
				VendorClaimType
				CloseDate
				Status
				ReturnAuthorizationDate
				CreditReceived

			Action Rules
				Instance Rules
					if (VendorClaimType.CreditMemo)
						LocalInvoiceNumber				= first PayablesInvoicesRel.Invoice
						LocalReturnCreditAmount			= (first PayablesInvoicesRel.InvoiceAmount * -1)
					else
						LocalInvoiceNumber				= VendorReturn
						LocalReturnCreditAmount			= CreditReceived
						
					for each VendorReturnLineForOrigRel
						LocalContractGroup				= each.OriginalPurchaseOrderLineRel.ProcurementGroup
						LocalMfgContract				= each.OriginalPurchaseOrderLineRel.ManufacturerContract
						LocalCmContract					= each.OriginalPurchaseOrderLineRel.Contract
						LocalOriginalContract			= each.OriginalPurchaseOrderLineRel.DerivedManufacturerContract
						LocalPurchaseOrder				= each.OriginalPurchaseOrder
						LocalPurchaseOrderLine			= each.OriginalPurchaseOrderLine
						if (CreditMemoPayablesInvoiceRel exists)
							LocalPayablesInvoice		= first CreditMemoPayablesInvoiceRel.PayablesInvoice
						else
							LocalPayablesInvoice		= first PayablesInvoicesRel.PayablesInvoice
						LocalPayablesInvoiceDetail		= each.first ReturnLinePayablesInvoiceDetailsRel.PayablesInvoiceDetail
						LocalUnitCost					= each.DerivedManufacturerUnitCost * -1
						if (each.VendorPriceUOMMultiplier			entered)
							LocalQuantity				= each.ReturnQuantity * each.EnteredUOMMultiplier / each.VendorPriceUOMMultiplier
						else
							LocalQuantity				= each.ReturnQuantity
						if (LocalReturnCreditAmount		!= ReturnValue)
							LocalQuantity				= (LocalReturnCreditAmount / (each.UnitCost * each.VendorPriceUOMMultiplier))							
						LocalLineNbr					= each.OriginalPurchaseOrderLine
						LocalLocation					= each.OriginalPurchaseOrderLineRel.ShipToLocation
						LocalManufacturerInformation.Manufacturer	= each.OriginalPurchaseOrderLineRel.Manufacturer
						LocalManufacturerInformation.ManufacturerNumber	= each.OriginalPurchaseOrderLineRel.ManufacturerNumber
						LocalManufacturerUnitCost		= each.DerivedManufacturerUnitCost * -1
						LocalReturnUnitCost				= each.DerivedReturnUnitCost * -1
						LocalItem						= each.Item
						LocalPOLineCommodityCode		= each.OriginalPurchaseOrderLineRel.CommodityCode
						LocalPurchMajcl					= each.OriginalPurchaseOrderLineRel.MajorPurchasingClass
						LocalPurchMincl					= each.OriginalPurchaseOrderLineRel.MinorPurchasingClass
						LocalIcItemCode					= each.OriginalPurchaseOrderLineRel.UNSPSCCode
    					LocalPoDate						= each.OriginalPurchaseOrderLineRel.PurchaseOrder.PurchaseOrderDate
	    				LocalCurrencyCode				= each.OriginalPurchaseOrderLineRel.PurchaseOrder.Currency
		    			LocalExchangeRate				= each.OriginalPurchaseOrderLineRel.PurchaseOrder.ReceiptCurrencyConversionRate
		    			if (PrmContract entered)
							for each QualifierItemsForContractRel
								include ProcessContractRebateQualifiers
								
							if (LocalCommCodes entered)
								I1 = 1
								while (I1 < 7)
									if (LocalPOLineCommodityCode.Segment[I1] entered)
										initialize LocalCommodityCode
										I2 = 1
										while (I2 <= I1)
											LocalCommodityCode.Segment[I2]  	= LocalPOLineCommodityCode.Segment[I2]
											I2									+= 1
										for each QualifierCodesLocalCommodityCodeForContractRel
											include ProcessContractRebateQualifiers
									I1	+= 1
																			
							if (LocalManufacturerInformation.Manufacturer.ManufacturerCode		entered)
								for each QualifierCodesManufInfoLevel1ForContractRel
									include ProcessContractRebateQualifiers
									
								if (LocalManufacturerInformation.Manufacturer.ManufacturerDivision		entered)
									for each QualifierCodesManufInfoLevel2ForContractRel
										include ProcessContractRebateQualifiers
										
							if (LocalPurchMajcl 	entered)
								for each QualifierCodesPurchClassLevel1ForContractRel
									include ProcessContractRebateQualifiers
									
								if (LocalPurchMincl	entered)
									for each QualifierCodesPurchClassLevel2ForContractRel
										include ProcessContractRebateQualifiers
										
							if (LocalIcItemCode.UNSPSCSegment 		entered)
								for each QualifierCodesUnspscCodeLevel1ForContractRel
									include ProcessContractRebateQualifiers
									
								if (LocalIcItemCode.UNSPSCFamily		entered)
									for each QualifierCodesUnspscCodeLevel2ForContractRel
										include ProcessContractRebateQualifiers
										
									if (LocalIcItemCode.UNSPSCClass		entered)
										for each QualifierCodesUnspscCodeLevel3ForContractRel
											include ProcessContractRebateQualifiers
											
										if (LocalIcItemCode.UNSPSCCommodity		entered)
											for each QualifierCodesUnspscCodeLevel4ForContractRel
												include ProcessContractRebateQualifiers
						else
							for each ContractRebateQualifierItemsRel
								include ProcessContractRebateQualifiers
								
							if (LocalPOLineCommodityCode entered)
								initialize LocalCommodityCode
								LocalCommodityCode.Segment[1]    = LocalPOLineCommodityCode.Segment[1]
								for each ContractRebateQualifierCodesCommodityCodeLevels1Thru5Rel
									include ProcessContractRebateQualifiers
		
								if (LocalPOLineCommodityCode.Segment[2]		entered)
									initialize LocalCommodityCode
									LocalCommodityCode.Segment[1]    = LocalPOLineCommodityCode.Segment[1]
									LocalCommodityCode.Segment[2]    = LocalPOLineCommodityCode.Segment[2]
									for each ContractRebateQualifierCodesCommodityCodeLevels1Thru5Rel
										include ProcessContractRebateQualifiers
										
									if (LocalPOLineCommodityCode.Segment[3]		entered)
										initialize LocalCommodityCode
										LocalCommodityCode.Segment[1]    = LocalPOLineCommodityCode.Segment[1]
										LocalCommodityCode.Segment[2]    = LocalPOLineCommodityCode.Segment[2]
										LocalCommodityCode.Segment[3]    = LocalPOLineCommodityCode.Segment[3]
										for each ContractRebateQualifierCodesCommodityCodeLevels1Thru5Rel
											include ProcessContractRebateQualifiers
											
										if (LocalPOLineCommodityCode.Segment[4]		entered)
											initialize LocalCommodityCode
											LocalCommodityCode.Segment[1]    = LocalPOLineCommodityCode.Segment[1]
											LocalCommodityCode.Segment[2]    = LocalPOLineCommodityCode.Segment[2]
											LocalCommodityCode.Segment[3]    = LocalPOLineCommodityCode.Segment[3]
											LocalCommodityCode.Segment[4]    = LocalPOLineCommodityCode.Segment[4]
											for each ContractRebateQualifierCodesCommodityCodeLevels1Thru5Rel
												include ProcessContractRebateQualifiers
												
											if (LocalPOLineCommodityCode.Segment[5]		entered)
												initialize LocalCommodityCode
												LocalCommodityCode.Segment[1]    = LocalPOLineCommodityCode.Segment[1]
												LocalCommodityCode.Segment[2]    = LocalPOLineCommodityCode.Segment[2]
												LocalCommodityCode.Segment[3]    = LocalPOLineCommodityCode.Segment[3]
												LocalCommodityCode.Segment[4]    = LocalPOLineCommodityCode.Segment[4]
												LocalCommodityCode.Segment[5]    = LocalPOLineCommodityCode.Segment[5]
												for each ContractRebateQualifierCodesCommodityCodeLevels1Thru5Rel
													include ProcessContractRebateQualifiers
			
												if (LocalPOLineCommodityCode.Segment[6]		entered)
													for each ContractRebateQualifierCodesCommodityCodeLevel6Rel
														include ProcessContractRebateQualifiers
							if (LocalManufacturerInformation.Manufacturer.ManufacturerCode	entered)
								for each ContractRebateQualifierCodesManufInfoLevel1Rel
									include ProcessContractRebateQualifiers
									
								if (LocalManufacturerInformation.Manufacturer.ManufacturerDivision	entered)
									for each ContractRebateQualifierCodesManufInfoLevel2Rel
										include ProcessContractRebateQualifiers
										
							if (LocalPurchMajcl 	entered)
								for each ContractRebateQualifierCodesPurchClassLevel1Rel
									include ProcessContractRebateQualifiers
									
								if (LocalPurchMincl	entered)
									for each ContractRebateQualifierCodesPurchClassLevel2Rel
										include ProcessContractRebateQualifiers
										
							if (LocalIcItemCode.UNSPSCSegment 		entered)
								for each ContractRebateQualifierCodesUnspscCodeLevel1Rel
									include ProcessContractRebateQualifiers
									
								if (LocalIcItemCode.UNSPSCFamily		entered)
									for each ContractRebateQualifierCodesUnspscCodeLevel2Rel
										include ProcessContractRebateQualifiers
										
									if (LocalIcItemCode.UNSPSCClass		entered)
										for each ContractRebateQualifierCodesUnspscCodeLevel3Rel
											include ProcessContractRebateQualifiers
											
										if (LocalIcItemCode.UNSPSCCommodity		entered)
											for each ContractRebateQualifierCodesUnspscCodeLevel4Rel
												include ProcessContractRebateQualifiers
										
		RequestRMA is an Instance Action
			valid when (CanRequestRMA)
			
			Action Rules
				RMARequested = true
				if (PurchaseFromLocation entered
				and PurchaseFromLocation.SupplierSourceIdRel exists)
					LocalSupplierEmail	  = PurchaseFromLocation.SupplierSourceIdRel.EmailAddress
					LocalSupplierSourceId = PurchaseFromLocation.SupplierSourceIdRel.SupplierSourceId
				else
					LocalSupplierEmail    = SupplierSourceIdPrimaryRel.EmailAddress  
					LocalSupplierSourceId = SupplierSourceIdPrimaryRel.SupplierSourceId
				
				if (CreatedFromPurchaseOrder entered)
					LocalBuyerEmail = CreatedFromPurchaseOrder.Buyer.EmailAddress
				else
				if (ReplacementPurchaseOrder entered)
					LocalBuyerEmail = ReplacementPurchaseOrder.Buyer.EmailAddress
				else
					LocalBuyerEmail = first SupplierGroupPortalContactsPORel.ContactEmail
				
				send email
					to LocalSupplierEmail
					from LocalBuyerEmail
					subject "AVendorReturnExistsToEnterAReturnMaterialAuthorizationNumberAgainst"
					Contents
						"VendorReturn<VendorReturn>HasBeenCreated"
						"ReturnInformationCanBeViewedInMessagesAndDialogInSupplierPortal"
						"ToIndicateAcceptanceOfTheReturnPleaseChooseTheEnterReturnMaterialAuthorizationNumberAction"
				invoke Create SupplierContactMessage
					invoked.SupplierGroup 					= Vendor.VendorGroup
					invoked.Supplier 						= Vendor.SupplierRel.Supplier
					invoked.SupplierSourceId				= LocalSupplierSourceId
					invoked.CreationDateTime				= current timestamp
					invoked.MessageTitle					= "Vendor Return request to enter RMA number"
					invoked.MessageText                     = "Enter RMA number for Vendor Return: " + VendorReturn 
					invoked.Status							= 1
					invoked.Priority						= 2
					invoked.SystemGenerated					= true
					invoked.ReleaseStatus					= 2
					if (CreatedFromPurchaseOrder entered)
						invoked.MessageOwner                = CreatedFromPurchaseOrder.Buyer
					else
						invoked.MessageOwner                = ReplacementPurchaseOrder.Buyer 	
					invoked.OriginatingCompany              = Company
					invoked.OriginatingReturn               = VendorReturn							
			
		EnterRMA is an Instance Action
			valid when (NeedsSupplierRMA)
			default label is "EnterReturnMaterialAuthorizationNumber"
			Parameters
				ParmRMA					is AlphaUpper size 64 
					default label is "ReturnMaterialAuthorizationNumber"
				ParmDialogMessage		is Alpha size 1000
					default label is "Message"
				ParmDialogAttachment    is an Attachment
					default label is "Attachment"

			Action Rules
				ReturnMaterialAuthorizationNumber = ParmRMA
				invoke Create SupplierMessageDialog
					invoked.SupplierGroup  					= actor.agent(SupplierSourceId).SupplierGroup
 					invoked.Supplier    			 		= actor.agent(SupplierSourceId).Supplier
					invoked.SupplierSourceId                = actor.agent(SupplierSourceId).SupplierSourceId
					invoked.SupplierMessage 				= first SupplierContactMessageRel.SupplierMessage
					invoked.SupplierContactMessage 			= first SupplierContactMessageRel.SupplierContactMessage
					invoked.Message                 		= ParmDialogMessage
					invoked.MessageAttachment       		= ParmDialogAttachment
				send email
					to first SupplierContactMessageRel.MessageOwner.EmployeeWorkEmailAddress
					from first SupplierContactMessageRel.SupplierSourceId.EmailAddress
					subject "AReturnMaterialAuthorizationNumberHasBeenEnteredByASupplier"
					Contents
						"VendorReturn<VendorReturn>HasHadAnRMANumberEntered"
						"ReturnInformationCanBeViewedInMessagesAndDialog"

		CreateSupplierMessageAndDialog is an Instance Action  
			valid when (ForSupplier)
			Parameters
				ParmDialogMessage       is Alpha size 1000
					default label is "Message"
				ParmDialogAttachment	is an Attachment
					default label is "Attachment"	
				ParmPriority            is Numeric 1
					States
						Low				value is 1
						Normal			value is 2
						High			value is 3					
					default label is "Priority"
				ParmResponseRequested   is Boolean
					default label is "ResponseRequired"
				
			Parameter Rules
				ParmDialogMessage
					required
					
				ParmPriority
					initial value is 2
						
			Local Fields
				LocalSupplierContactMessage        is a SupplierContactMessage view
			
			Action Rules	
					
				invoke Create SupplierContactMessage
					assign result to LocalSupplierContactMessage
					invoked.SupplierGroup  					= actor.agent(SupplierSourceId).SupplierGroup
 					invoked.Supplier    			 		= actor.agent(SupplierSourceId).Supplier
					invoked.SupplierSourceId                = actor.agent(SupplierSourceId).SupplierSourceId
					invoked.MessageTitle					= "Dialog for Return: " + VendorReturn
					invoked.MessageText                     = "User Created Message and Dialog for Return: " + VendorReturn
					invoked.Status							= 1
					invoked.Priority						= ParmPriority
					invoked.ReleaseStatus					= 2
					invoked.MessageOwner                    = CreatedFromPurchaseOrder.Buyer
					invoked.OriginatingCompany              = Company
					invoked.OriginatingPO                   = CreatedFromPurchaseOrder
					invoked.OriginatingReturn               = VendorReturn
					
				invoke Create SupplierMessageDialog
					invoked.SupplierGroup  					= actor.agent(SupplierSourceId).SupplierGroup
 					invoked.Supplier    			 		= actor.agent(SupplierSourceId).Supplier
					invoked.SupplierSourceId                = actor.agent(SupplierSourceId).SupplierSourceId
					invoked.SupplierContactMessage 			= LocalSupplierContactMessage.SupplierContactMessage
					invoked.Message                 		= ParmDialogMessage
					invoked.MessageAttachment       		= ParmDialogAttachment
					invoked.ResponseRequested       		= ParmResponseRequested					

		CreateMessageAndDialog is an Instance Action  
			default label is "StartASupplierDialog"
			valid when (BuyerCanCreateDialog)
			Parameters
				ParmSupplierGroup       is a SupplierGroup
				ParmSupplier            is a Supplier
					default label is "Supplier"
				ParmSupplierContact     is a SupplierSourceId
					default label is "SupplierContact"
				ParmDialogMessage       is Alpha size 1000
					default label is "Message"
				ParmDialogAttachment	is an Attachment
					default label is "Attachment"
				ParmPriority            is Numeric 1
					States
						Low				value is 1
						Normal			value is 2
						High			value is 3
					default label is "Priority"
				ParmResponseRequested   is Boolean
					default label is "ResponseRequired"

			Parameter Rules
				
				ParmSupplierGroup
					initial value is SupplierGroupRel.SupplierGroup
					
				ParmSupplier
					if (Vendor.SingleSupplier)
						default to Vendor.first SupplierRel.Supplier
				
				ParmSupplierContact
					if (Vendor.SingleSupplier
					and DerivedSupplierSourceId entered)
						default to DerivedSupplierSourceId
					else
					if (Vendor.SingleSupplierAndContact)
						default to Vendor.SupplierRel.PrimaryContact.SupplierSourceId
				
				ParmDialogMessage
					required

				ParmPriority
					initial value is 2

			Local Fields
				LocalSupplierContactMessage        is a SupplierContactMessage view

			Action Rules

				if (!Vendor.SingleSupplier)
					constraint (ParmSupplier entered)
						"MustEnterASupplier"

				if (!Vendor.SingleSupplierAndContact)
					constraint (ParmSupplierContact entered)
						"MustEnterASupplierContact"		

				constraint (ParmSupplier.Vendor = Vendor)
					"SupplierNotValidForVendor"
				
				if (ParmSupplier entered)
					LocalSupplier        = ParmSupplier
				else
					LocalSupplier 		 = Vendor.SupplierRel.Supplier
				LocalSupplierContact = ParmSupplierContact					
				
				constraint (SupplierSourceIdValidRel exists)
					"SupplierContactNotValidForSupplier"
				
				constraint (SupplierContactHasReturnsAccess)
					"SupplierContactDoesNotHaveAccessToVendorReturns"
				
				invoke Create SupplierContactMessage
					assign result to LocalSupplierContactMessage
					assign result to LocalSupplierContactMessage
					invoked.SupplierGroup  					= ParmSupplierGroup					
 					invoked.Supplier    			 		= ParmSupplier
					invoked.SupplierSourceId                = ParmSupplierContact 
					invoked.MessageTitle					= "Dialog for Vendor Return: " + VendorReturn
					invoked.MessageText                     = "User Created Message and Dialog for Vendor Return: " + VendorReturn
					invoked.Status							= 1
					invoked.Priority						= ParmPriority
					invoked.ReleaseStatus					= 2
					invoked.MessageOwner                    = actor.agent(Employee).Employee
					invoked.OriginatingCompany              = Company
					invoked.OriginatingReturn               = VendorReturn

				invoke Create SupplierMessageDialog
					assign result to LocalSupplierContactMessage
					invoked.SupplierGroup  					= ParmSupplierGroup					
 					invoked.Supplier    			 		= ParmSupplier
					invoked.SupplierSourceId                = ParmSupplierContact 
					invoked.SupplierContactMessage 			= LocalSupplierContactMessage.SupplierContactMessage
					invoked.Message                 		= ParmDialogMessage
					invoked.MessageAttachment       		= ParmDialogAttachment
					invoked.ResponseRequested       		= ParmResponseRequested
					invoked.Origin                          = 1

		TriggerSupplierRMA is an Instance Action  
	  		restricted
			Action Rules
				if (Company.FinanceEnterpriseGroup.BODTrigger)
					if (Status.WaitingForVendorAction)
						LocalTitle = "EG:"+Company.FinanceEnterpriseGroup+" CO:"+Company+" VR:"+VendorReturn	
							increment bod id.VariationID
							ActionCode = ActionCode.Update
							trigger "SupplierRMAService" PA service
								resume on error
								title is "<LocalTitle>"
								Criteria
									Company.FinanceEnterpriseGroup
									Company
								Variables
									ActionCode
									include persistent fields from VendorReturn
									bod id.VariationID
										variable name is VariationId
									LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
										variable name is CurrentTimeStamp
									Vendor.VendorGroup
										variable name is VendorGroup
									Company.FinanceEnterpriseGroup
										variable name is FinanceEnterpriseGroup	
									Company.GeneralLedgerCompany.AccountingEntity
										variable name is AccountingEntity	


		CancelVendorReturn is an Instance Action
			restricted
			Parameters
				PrmIsCancelFromPO is Boolean
			Action Rules
				if (not PrmIsCancelFromPO)
					constraint (IsValidForCancel)
						"CannotCancelReturn;CreditHasBeenReceived"	
				Canceled 				= true
				FullyCredited 			= false
				CanceledBy 				= actor	
			Exit Rules
				invoke Cancel VendorReturnLinesForCancelRel
					invoked.PrmIsCancelFromPOLine	= PrmIsCancelFromPO	
				
				if (Status.WaitingForVendorAction)			
					invoke WaitingForVendorAction.AutoClose
	
		PurgeClosedVendorReturn is a Set Action
			restricted
			
			Parameters
				PrmCompany						is a PurchasingCompany
				PrmPurgeVendorReturns			is AlphaUpper size 1
					States
						PurgeVendorReturns		value is "Y"
						ReportOnlyDoNotPurge	value is "R"
				PrmPurgeCutOffDate				is Date
				PrmFromPurgeAll					is Boolean

			Local Fields
				LocalSetPurgeCount				is Numeric 10
			
			Parameter Rules
				PrmCompany
					required
					constraint (PrmCompany.FinanceEnterpriseGroup.PurgeCutOffDate entered)
						"PurgeCutOffDateMustBeSetFor_Finance_Enterprise_Group<PrmCompany.FinanceEnterpriseGroup>"
				PrmPurgeVendorReturns
					initial value is "R"
					default to "R"
				PrmPurgeCutOffDate
					default to PrmCompany.FinanceEnterpriseGroup.PurgeCutOffDate
					
			
			Instance Selection
				include deleted records
				where (Company    = PrmCompany
				and    CloseDate <= PrmPurgeCutOffDate
				and    Status.Closed)
			
			Set Is
				PrmCompany
			
			Action Rules
				Empty Set Rules
					if (not PrmFromPurgeAll)
						LocalActor = actor
						send notification
							to LocalActor
							description is "NoVendorReturnRecordsFoundToPurge"
							priority is high
							detail is "ThereAreNoVendorReturnRecordsPurged"
				Set Rules
					Exit Rules
						if (not PrmFromPurgeAll)
							invoke SendPurgeNotification
								invoked.PrmPurgeCount			= LocalSetPurgeCount
								if (PrmPurgeVendorReturns.PurgeVendorReturns)
									invoked.PrmPurgeRecords 	= true
				Instance Rules
					if (ReplaceGoods)
						if (PurgeReplacementPurchaseOrder)
							LocalSetPurgeCount +=1
							if (PrmPurgeVendorReturns.PurgeVendorReturns)
								invoke Purge PurgeReplacementPurchaseOrderRel
								invoke Purge
					else
						LocalSetPurgeCount +=1
						if (PrmPurgeVendorReturns.PurgeVendorReturns)
							invoke Purge

		Purge is a Purge Action
			restricted
			bypass relational integrity rules
			Entrance Rules
				invoke Purge PurgeProcurementTransactionDistributionRel
				invoke Purge PurgePurchaseOrderTransactionDetailRel
				invoke Purge PurgeVendorReturnCommentRel
				invoke Purge PurgeVendorReturnLineRel
				
		SendPurgeNotification is an Instance Action
			restricted
			Parameters
				PrmPurgeCount		is Numeric 10
				PrmPurgeRecords		is Boolean				
			Local Fields
				LocalActor			is an Actor
			Action Rules
				LocalActor 			= actor
				LocalPurgeCount		= PrmPurgeCount
				LocalPurgeRecords	= PrmPurgeRecords
				send notification
					to LocalActor
					description is "VendorReturnPurgeHasBeenCompleted"
					priority is high
					detail is "<DerivedCompletionDetailText>"

		PutOnHold is an Instance Action
			restricted
			Action Rules
				TemporaryHold = TemporaryHold.SentForApproval
				for each VendorReturnLinesRel
					invoke PutOnHold each

		SendProcessVendorReturnBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ProcessVendorReturnXMLBOD
					bod type is "Process.Shipment"
					accounting entity is DerivedBODCrossAccountingEntity
					document id is VendorReturn
					variation id is	DerivedBODVariationID

		TriggerProcessVendorReturnNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 2
					invoked.PrmNoun						= "VendorReturn"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "VendorReturn"
					invoked.PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
					invoked.PrmCompany					= Company
					invoked.PrmMainUserTemplate 		= "IONProcessVendorReturn_VendorReturn_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger 
				if(Company.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)	
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 2
							invoked.Noun 					= "VendorReturn"					
							invoked.BODDocumentID			= DerivedDocID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODAccountingEntity		= DerivedBODCrossAccountingEntity
							invoked.Direction				= 1
							invoked.Reference1				= Company 
							invoked.Reference2				= VendorReturn
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= DerivedDocID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedBODCrossAccountingEntity
							invoked.Reference1				= Company
							invoked.Reference2				= VendorReturn
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendProcessVendorReturnBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID					            = DerivedBODID
							invoked.BODXML								= ProcessVendorReturnXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODID
							invoked.BODXML									= ProcessVendorReturnXMLBOD
		
		TriggerBROrderVendorReturn is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				if (Company.FinanceEnterpriseGroup.BODTrigger)
					if (!action type.Delete)
						if (action != "UpdateBODIdFields") 
							ActionCode = ActionCode.Update
						if (action type.Create)
							ActionCode = ActionCode.Create
					increment bod id.VariationID
					LocalTitle = "EG:"+Company.FinanceEnterpriseGroup+" CO:"+Company+" VN:"+Vendor+" VR:"+VendorReturn
					trigger "BROrderVendorReturnService" PA service
						resume on error
						title is "<LocalTitle>"
						Criteria
							Company.FinanceEnterpriseGroup
							Company
						Variables
							ActionCode
							include persistent fields from VendorReturn
							include persistent fields from VendorReturnLinesRel
							Company.FinanceEnterpriseGroup
								variable name is FinanceEnterpriseGroup	
							Company.GeneralLedgerCompany.AccountingEntity
								variable name is Accountingentity
							bod id.VariationID
								variable name is VariationId
							LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
								variable name is CurrentTimeStamp


		SendSupplierRMABODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is VendorReturnBODXML
					bod type is "Sync.SupplierRMA"
					accounting entity is DerivedAccountingEntity
					document id is DerivedDocID
					variation id is DerivedBODVariationID

		TriggerSupplierRMANativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "SupplierRMA"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "VendorReturn"
					invoked.PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup 
					invoked.PrmCompany					= Company
					invoked.PrmMainUserTemplate			= "IONSyncSupplierRMA_VendorReturn_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigRel.NativeLPLBODTrigger
				if(Company.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "SupplierRMA"					
							invoked.BODDocumentID			= DerivedVendorReturn
							invoked.BODVariationID			= DerivedBODVariationID							
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= DerivedCompany
							invoked.Reference2				= DerivedVendorReturn
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= DerivedVendorReturn
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference1				= DerivedCompany
							invoked.Reference2				= DerivedVendorReturn
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendSupplierRMABODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.BODID								= DerivedSupplierRMABODID
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= VendorReturnBODXML
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.BODID									= DerivedSupplierRMABODID
							invoked.CloseDate								= system current timestamp
							invoked.BODXML									= VendorReturnBODXML
				
