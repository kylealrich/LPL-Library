ProcurementCardStatementInput is a BusinessClass
	owned by po
	
	prefix is PCSI
	
	Ontology
        symbolic key is ProcurementCardStatementInput

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations
	
	Persistent Fields
		ProcurementGroup
		RecordType						is Numeric 2
			States
				Header					value is 0
				Transaction				value is 1
				Distribution			value is 2
		ResultProcessedBy               is a ProcurementCardStatementInterfaceResult
		ProcurementCardProgram 			is like ProcurementCardProgram
		ProcurementCardStatementImport	is like ProcurementCardStatementImport
		Company                         is like PurchasingCompany
		StatementFromDate 				is Date
        StatementToDate   				is Date

        ProcurementCardNumber           is like ProcurementCardNumber
        TransactionNumber				is a ProcurementCardTransaction
        MerchantCategoryCode            
        Merchant                        is like Merchant
		TransactionDate                 is Date
		PostDate                        is Date
		CardUserName                    
		PurchaseOrder                   is like PurchaseOrder
		PurchaseOrderLine               is like PurchaseOrderLine
		Item                            is like Item
		VendorItem                      is like VendorItem
		ItemDescription                 is a Description
		Quantity       
		VendorBuyUOM                    is like UnitOfMeasure
		ItemCharge                      is an InternationalAmount
		TaxAmount                 		is an InternationalAmount
        TotalAddOnChargeCost      		is an InternationalAmount
        TaxIndicator              		is like TaxIndicator

		ProcurementChargeDistributionType
        DistributionAmount              is an InternationalAmount
        DistributionAccount             is like TransactionCodeBlock
		
	Local Fields
    	
		LocalInterfaceResult             is like ProcurementCardStatementInterfaceResult
		LocalProcurementGroup            is like ProcurementGroup
		LocalProgram                     is like ProcurementCardProgram
		LocalProcurementCardStatement    is like ProcurementCardStatementImport
    	LocalErrorMessage                is Alpha 150
		LocalDuplicateStatement1         is like ProcurementCardStatementImport
		LocalDuplicateStatement2         is like ProcurementCardStatementImport
    
    Derived Fields
    
    	NoImportTransactionsMessage is a MessageField
    		restricted
    		"CannotAddStatement;MustHaveTransactionRecordsToProcess"
    		
    	DuplicateStatementMessage is a MessageField
    		restricted
    		"StatementAlreadyExists"
    		
    	DuplicateTransactionMessage is a MessageField
    		restricted
    		"TransactionAlreadyExists"
    	
    	DistributionAmountMessage is a MessageField
			restricted
			"TotalOfDistributionAmountsDoesNotMatchItemChargeAmount"
			
		TaxDistributionAmountMessage is a MessageField
			restricted
			"TotalOfTaxDistributionAmountsDoesNotMatchTaxAmount"
    
    Conditions
		
		ImportTransactionsExist
			restricted
			when (ProcurementCardStatementImportTransactionRel exists)
			
		StatementExists
			restricted
			when (ProcurementCardStatementRel exists)
			
		HasDistributions
			restricted
			when (AllImportTransactionDistributionsRel exists)
			
		NoDuplicateTransactions
			restricted
			when (DuplicateTransactionFromStatementRel !exists)
		
		DuplicateTransactions
			restricted
			when (DuplicateTransactionFromStatementRel exists)

		DuplicateTransactionInterface
			restricted
			when (RecordType = 1
			and   ProcurementCardStatementTransactionRel exists)
			
		DuplicateTransactionInput
			restricted
			when (RecordType = 1
			and   DuplicateTransactionInputRel exists)
			
		NoDuplicateTransactionInput
			restricted
			when (RecordType = 1
			and   DuplicateTransactionInputRel !exists)
			
		CompanySecurityGroupAllowsAccess
			restricted
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)
			
		SecurityGroupAllowsAccess
			restricted
			when (Company !entered
			or   (Company entered
			and   CompanySecurityGroupAllowsAccess))
			
    Relations
        
        AllImportTransactionDistributionsRel
            one-to-many relation to ProcurementCardStatementInput
            Field Mapping uses ByStatement
            	related.ProcurementGroup											= ProcurementGroup
                related.RecordType                   								= 2
                related.ProcurementCardProgram         								= ProcurementCardProgram
                related.ProcurementCardStatementImport 								= ProcurementCardStatementImport
                related.Company 													= Company
                related.TransactionNumber 											= TransactionNumber
        
        ImportTransactionDistributionsRel
            one-to-many relation to ProcurementCardStatementInput
            Field Mapping uses ByStatement
            	related.ProcurementGroup											= ProcurementGroup
                related.RecordType                   								= 2
                related.ProcurementCardProgram         								= ProcurementCardProgram
                related.ProcurementCardStatementImport 								= ProcurementCardStatementImport
                related.Company 													= Company
                related.TransactionNumber 											= TransactionNumber
			Instance Selection
				where (related.ProcurementChargeDistributionType = "G")

        TaxImportTransactionDistributionsRel
            one-to-many relation to ProcurementCardStatementInput
            Field Mapping uses ByStatement
            	related.ProcurementGroup											= ProcurementGroup
                related.RecordType                   								= 2
                related.ProcurementCardProgram         								= ProcurementCardProgram
                related.ProcurementCardStatementImport 								= ProcurementCardStatementImport
                related.Company 													= Company
                related.TransactionNumber 											= TransactionNumber
        	Instance Selection
				where (related.ProcurementChargeDistributionType = "T")	
    
    	StatementImportByProcurementGroupRel
    		one-to-many relation to ProcurementCardStatementImport
    	  	Field Mapping uses symbolic key
    	  		related.ProcurementGroup      = LocalProcurementGroup

		StatementImportRel
			one-to-one relation to ProcurementCardStatementImport
			Field Mapping uses symbolic key
				related.ProcurementGroup      			= LocalProcurementGroup
				related.ProcurementCardProgram 			= LocalProgram
				related.ProcurementCardStatementImport 	= LocalProcurementCardStatement
		
		LocalInterfaceResultsRel
			one-to-one relation to ProcurementCardStatementInterfaceResult
			Field Mapping uses symbolic key
				related.ProcurementGroup						= LocalProcurementGroup
				related.ProcurementCardStatementInterfaceResult	= LocalInterfaceResult 

		DuplicateTransactionFromStatementRel
			one-to-many relation to ProcurementCardStatementInput
			Field Mapping uses ByStatement
				related.ProcurementGroup             	= ProcurementGroup
				related.RecordType                   	= 1
				related.ProcurementCardProgram       	= ProcurementCardProgram
				related.ProcurementCardStatementImport 	= ProcurementCardStatementImport
			Instance Selection
				where (related.DuplicateTransactionInput)				
		
		DuplicateTransactionInputRel
			one-to-many relation to ProcurementCardStatementInput
			Field Mapping uses ByStatement
				related.ProcurementGroup             	= ProcurementGroup
				related.RecordType                   	= 1
				related.ProcurementCardProgram       	= ProcurementCardProgram
				related.ProcurementCardStatementImport 	= ProcurementCardStatementImport
				related.Company                         = Company
				related.TransactionNumber               = TransactionNumber
			Instance Selection
				where (related.ProcurementCardStatementInput != ProcurementCardStatementInput)
				
		ProcurementCardStatementsForCleanupRel
			one-to-many relation to ProcurementCardStatementInput
			Field Mapping uses ByStatement
				related.ProcurementGroup             	= ProcurementGroup
				related.RecordType                   	= 0
				related.ProcurementCardProgram       	= ProcurementCardProgram
				related.ProcurementCardStatementImport 	= ProcurementCardStatementImport
		
		ProcurementCardStatementTransactionsForCleanupRel
			one-to-many relation to ProcurementCardStatementInput
			Field Mapping uses ByStatement
				related.ProcurementGroup             	= ProcurementGroup
				related.RecordType                   	= 1
				related.ProcurementCardProgram       	= ProcurementCardProgram
				related.ProcurementCardStatementImport 	= ProcurementCardStatementImport
		
		ProcurementCardStatementTransactionDistributionsForCleanupRel
			one-to-many relation to ProcurementCardStatementInput
			Field Mapping uses ByStatement
				related.ProcurementGroup             	= ProcurementGroup
				related.RecordType                   	= 2
				related.ProcurementCardProgram       	= ProcurementCardProgram
				related.ProcurementCardStatementImport 	= ProcurementCardStatementImport
		
		MerchantRel
			one-to-one relation to Merchant
			Field Mapping uses symbolic key
				related.ProcurementGroup             	= ProcurementGroup
				related.ProcurementCardProgram       	= ProcurementCardProgram
				related.Merchant                        = Merchant
		
		ProcurementCardStatementImportTransactionRel
			one-to-many relation to ProcurementCardStatementInput
			delete cascades
			Field Mapping uses ByStatement
				related.ProcurementGroup                        = ProcurementGroup
				related.RecordType                              = RecordType.Transaction
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatementImport          = ProcurementCardStatementImport
				
		ProcurementCardStatementRel
			one-to-one relation to ProcurementCardStatement
			Field Mapping uses symbolic key
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatementImport
	
		ProcurementCardStatementTransactionRel
			one-to-one relation to ProcurementCardStatementTransaction
			Field Mapping uses symbolic key
				related.ProcurementGroup                        				= ProcurementGroup
				related.ProcurementCardProgram                  				= ProcurementCardProgram
				related.ProcurementCardStatement                				= ProcurementCardStatementImport
				related.ProcurementCardStatementTransaction.Company 			= Company 
				related.ProcurementCardStatementTransaction.TransactionNumber 	= TransactionNumber
		
		ProcurementCardProgramRel
        	one-to-one relation to ProcurementCardProgram
        	Field Mapping uses symbolic key
        		related.ProcurementGroup				= ProcurementGroup
        		related.ProcurementCardProgram			= ProcurementCardProgram
        		
        CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = Company
	
	Sets

        ByStatement
			Sort Order
				ProcurementGroup
				RecordType
				ProcurementCardProgram
				ProcurementCardStatementImport
				Company
				TransactionNumber
				ProcurementCardStatementInput
	
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
			
		DeleteStatement is a Set Action
			Parameters
				Statement                           is like ProcurementCardStatementImport
				
			Parameter Rules
				
				Statement
					required
			
			Instance Selection			
				where (ProcurementCardStatementImport   = Statement)
				
			Action Rules
				Instance Rules
					invoke FastDelete
		
		InterfaceProcurementCardStatements is a Set Action
			Parameters
				PrmProcurementGroup					is a ProcurementGroup
					default label is "ProcurementGroup"
				Program                             is a ProcurementCardProgram 
				Statement                           is like ProcurementCardStatementImport
				PrmPostingDate						is a PostingDate
					default label is "PostingDate"
				
			Parameter Rules
				
				Statement
					LocalProcurementGroup      		= PrmProcurementGroup
					LocalProgram               		= Program
					LocalProcurementCardStatement   = Statement
					if (Statement entered)
						constraint (StatementImportRel !exists)
							"StatementAlreadyExistsInProcurementCardStatementInterface;MustRunWithADifferentStatementNumberOrClearRecordsFromStatementInterface"
			
			Local Fields
				LocalInterfaceResultView		is a ProcurementCardStatementInterfaceResult view				
				LocalErrorOccurred              is Boolean
			
			Instance Selection			
				where (ProcurementGroup					= PrmProcurementGroup
				and    RecordType                   	= 0
				and   (ProcurementCardProgram 			= Program
				or     Program !entered)
				and   ((ProcurementCardStatementImport 	= Statement
				and    StatementImportRel !exists)
				or     Statement !entered))

			Sort Order
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatementImport
						
			Action Rules
			
				Empty Set Rules
				
					invoke Create ProcurementCardStatementInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.RunTime							= current timestamp
						invoked.ProcurementGroup				= PrmProcurementGroup
						invoked.ProcurementCardProgram	 		= Program
						invoked.ProcurementCardStatement	 	= Statement
				
				ProcurementGroup Set Rules
					Entrance Rules
			
						LocalErrorOccurred							= false
						
						invoke Create ProcurementCardStatementInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.RunTime							= current timestamp
							invoked.ProcurementGroup				= PrmProcurementGroup
							invoked.ProcurementCardProgram	 		= Program
							invoked.ProcurementCardStatement	 	= Statement
							
					Exit Rules
					
						invoke InterfaceProcurementCardStatementTransactions  
							invoked.PrmProcurementGroup  		= PrmProcurementGroup
							invoked.Program                     = Program
							invoked.Statement                   = Statement
							invoked.PrmResult                   = LocalInterfaceResult
							invoked.ParmDuplicateStatement1     = LocalDuplicateStatement1
							invoked.ParmDuplicateStatement2     = LocalDuplicateStatement2
							
				Instance Rules
				
					if (ProcurementCardStatementRel exists)
						LocalErrorOccurred	= true
						LocalErrorMessage 	= DuplicateStatementMessage
					else
						LocalErrorOccurred	= false
					if (ProcurementCardStatementRel !exists)
						if  (NoDuplicateTransactions)
							invoke Create ProcurementCardStatement
								resume on error
									LocalErrorOccurred	= true
									LocalErrorMessage	= error message
								fill in fields from this instance
								invoked.ProcurementCardStatement = ProcurementCardStatementImport
								invoked.InterfaceInProcess		 = 1
								invoked.OriginatingInterfaceRun  = LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
								invoked.PostingDate			 	 = PrmPostingDate
								
							if (!ImportTransactionsExist)
								LocalErrorOccurred	= true
								LocalErrorMessage 	= NoImportTransactionsMessage
						else
						if (DuplicateTransactions)
							if (LocalDuplicateStatement1 !entered)
								LocalDuplicateStatement1 = ProcurementCardStatementImport
							else
								LocalDuplicateStatement2 = ProcurementCardStatementImport
					if (LocalErrorOccurred)
						invoke SetError
							invoked.PrmErrorMessage 		= LocalErrorMessage
							invoked.PrmResult       		= LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
							invoked.PrmErrorLevel   		= 1	
							
					ResultProcessedBy                       = LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
					LocalInterfaceResult                    = LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
							
		InterfaceProcurementCardStatementTransactions is a Set Action    
			restricted
			Parameters
				PrmProcurementGroup					is a ProcurementGroup
				Program                             is a ProcurementCardProgram 
				Statement                           is like ProcurementCardStatement
				PrmResult                           is like ProcurementCardStatementInterfaceResult
				ParmDuplicateStatement1             is like ProcurementCardStatementImport
				ParmDuplicateStatement2             is like ProcurementCardStatementImport
				
			Local Fields
				LocalHasError                          		is Boolean
				LocalErrorOccurred              			is Boolean
			
			Instance Selection			
				where (ProcurementGroup				= PrmProcurementGroup
				and    RecordType                   = 1
				and   (ProcurementCardProgram 		= Program
				or     Program !entered)
				and   (ProcurementCardStatementImport = Statement
				or     Statement !entered))

			Sort Order
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatementImport
				TransactionNumber
				
			Action Rules
					
				Empty Set Rules
				
					invoke FinishLoadInterface 
						invoked.PrmProcurementGroup  	= PrmProcurementGroup
						invoked.PrmResult    			= PrmResult
				
				ProcurementGroup Set Rules
					Exit Rules
						
						invoke FinishLoadInterface 
							invoked.PrmProcurementGroup  	= PrmProcurementGroup
							invoked.PrmResult    			= PrmResult
			
				Instance Rules
					LocalInterfaceResult   	= PrmResult
					LocalHasError 			= false

					LocalErrorOccurred	= false
					if (ParmDuplicateStatement1 != ProcurementCardStatementImport
					and ParmDuplicateStatement2 != ProcurementCardStatementImport)
					
						invoke Unreleased.Create ProcurementCardStatementTransaction
							resume on error
								LocalHasError  		= true
								LocalErrorMessage	= error message
							fill in fields from this instance
							if (HasDistributions)
								invoked.TransientDistributionsExist = true
							invoked.ProcurementCardStatement   	        					= ProcurementCardStatementImport
							invoked.ProcurementCardStatementTransaction.TransactionNumber 	= TransactionNumber
							if (Company entered)
								invoked.ProcurementCardStatementTransaction.Company		 	= Company
							else
								invoked.ProcurementCardStatementTransaction.Company         = ProcurementCardStatementRel.Company 
						if (LocalHasError)
							invoke SetError 
								invoked.PrmErrorMessage = LocalErrorMessage
								invoked.PrmResult       = PrmResult
								invoked.PrmErrorLevel   = 2
						else
						if (!LocalHasError)
							if (MerchantRel !exists
							and ProcurementCardProgramRel.AutomaticallyAddMerchant)
								invoke CreateFromInterface Merchant
									invoked.ProcurementGroup 	   = ProcurementGroup
									invoked.ProcurementCardProgram = ProcurementCardProgram
									invoked.Merchant               = Merchant
									invoked.Active                 = true
							if (HasDistributions)
								if (ItemCharge != sum ImportTransactionDistributionsRel.DistributionAmount)
									LocalHasError 		= true
									LocalErrorMessage 	= DistributionAmountMessage
								if (TaxAmount != sum TaxImportTransactionDistributionsRel.DistributionAmount)
									LocalHasError       = true
									LocalErrorMessage   = TaxDistributionAmountMessage
								for each AllImportTransactionDistributionsRel
									if (!LocalHasError)
										invoke CreateFromInterface ProcurementCardStatementTransactionDistribution
											resume on error
												LocalHasError  		= true
												LocalErrorMessage	= error message
											fill in fields from each
											invoked.ProcurementCardStatement   	        					= ProcurementCardStatementImport
											invoked.ProcurementCardStatementTransaction.Company 			= Company
											invoked.ProcurementCardStatementTransaction.TransactionNumber 	= TransactionNumber
										if (!LocalHasError)
											invoke FastDelete each
										else
										if (LocalHasError)
											invoke SetError 
												invoked.PrmErrorMessage = LocalErrorMessage
												invoked.PrmResult       = PrmResult
												invoked.PrmErrorLevel   = 3
									else
									if (LocalHasError)
										invoke SetError 
											invoked.PrmErrorMessage = LocalErrorMessage
											invoked.PrmResult       = LocalInterfaceResult
											invoked.PrmErrorLevel   = 3
							
							if (not LocalHasError)
								invoke FastDelete
		
		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like ProcurementCardStatementInterfaceResult
 				PrmErrorLevel           is Numeric size 2
 					
			Action Rules
						
				if (PrmErrorLevel = 1)
					invoke CreateNoRules ProcurementCardStatementImport
						fill in fields from this instance
						invoked.ProcurementGroup                = ProcurementGroup
						invoked.ProcurementCardProgram          = ProcurementCardProgram
						invoked.ProcurementCardStatementImport  = ProcurementCardStatementImport
						invoked.RecordInError					= true
						invoked.ErrorMessage					= PrmErrorMessage
						invoked.ErrorLevel                      = PrmErrorLevel
						invoked.ResultProcessedBy               = PrmResult
				else 
					invoke CreateNoRules ProcurementCardStatementImport
						fill in fields from first ProcurementCardStatementsForCleanupRel
						invoked.ProcurementGroup                = first ProcurementCardStatementsForCleanupRel.ProcurementGroup
						invoked.ProcurementCardProgram          = first ProcurementCardStatementsForCleanupRel.ProcurementCardProgram
						invoked.ProcurementCardStatementImport  = first ProcurementCardStatementsForCleanupRel.ProcurementCardStatementImport
						invoked.RecordInError					= true
						invoked.ErrorMessage					= PrmErrorMessage
						invoked.ErrorLevel                      = PrmErrorLevel
						invoked.ResultProcessedBy               = PrmResult
				
				if (ProcurementCardStatementTransactionsForCleanupRel exists)
					for each ProcurementCardStatementTransactionsForCleanupRel
						if (each.DuplicateTransactionInputRel !exists)
							invoke CreateNoRules ProcurementCardStatementImportTransaction
								fill in fields from each
								if (each.ProcurementCardStatementTransactionRel exists)
									invoked.TransactionCreated										= true
								invoked.ProcurementCardStatementImportTransaction.Company  			= each.Company
								invoked.ProcurementCardStatementImportTransaction.TransactionNumber = each.TransactionNumber

							invoke FastDelete each
				
				if (ProcurementCardStatementTransactionDistributionsForCleanupRel exists)
					for each ProcurementCardStatementTransactionDistributionsForCleanupRel
						invoke CreateNoRules ProcurementCardStatementImportTransactionDistribution
							fill in fields from each
							invoked.ProcurementCardStatementImportTransaction.Company  			= each.Company
							invoked.ProcurementCardStatementImportTransaction.TransactionNumber = each.TransactionNumber
						invoke FastDelete each
				
				invoke FastDelete first ProcurementCardStatementsForCleanupRel
				LocalProcurementGroup 	= ProcurementGroup
				LocalInterfaceResult 	= PrmResult
				invoke UpdateErrorInformation LocalInterfaceResultsRel
		
		FinishLoadInterface is a Set Action
			restricted
			Parameters
				PrmProcurementGroup     is like ProcurementGroup
				PrmResult               is like ProcurementCardStatementInterfaceResult
						       
			Instance Selection
				where (ResultProcessedBy = PrmResult
				and    RecordType        = 0)
				
			Sort Order
				ProcurementGroup
				RecordType
				ProcurementCardProgram
				ProcurementCardStatementImport
				
			Action Rules
				Empty Set Rules
					LocalProcurementGroup      = PrmProcurementGroup
					LocalInterfaceResult       = PrmResult
					invoke Finish LocalInterfaceResultsRel
				
				ProcurementGroup Set Rules
								
					Exit Rules
						LocalProcurementGroup      = PrmProcurementGroup
						LocalInterfaceResult       = PrmResult
						invoke Finish LocalInterfaceResultsRel
						
				Instance Rules
					invoke UpdateFromInterface ProcurementCardStatementRel
						invoked.SetInterfaceInProcessFalse   = true
					if (NoDuplicateTransactions)
						if (ProcurementCardStatementTransactionDistributionsForCleanupRel exists)
							for each ProcurementCardStatementTransactionDistributionsForCleanupRel
								invoke FastDelete each
						if (ProcurementCardStatementTransactionsForCleanupRel exists)
							for each ProcurementCardStatementTransactionsForCleanupRel
								invoke FastDelete each
						invoke FastDelete
					
		
