AssetItemInventory is a BusinessClass
    owned by am
    prefix is IIV
    classic name is AMASTITINV

    Ontology
        symbolic key is AssetItemInventory
            classic set name is IIVSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        ItemCondition           is AlphaUpper size 1
            States
                Fair      value is "F"
	            Good      value is "G"
	            Excellent value is "E"
	            Poor      value is "P"
	            Damaged   value is "D"
        Company                 is an AssetCompany
        InventoryLocation       is an AssetLocation
            classic name is INV-LOC-NAME
        InventoryLocationDetail is an AssetLocationDetail
            classic name is INV-LOC-DTL
        InventoryQuantity       is Numeric size 8
            classic name is INV-QTY
        CreatedBy               is an Operator 
        	holds pii
            classic name is CREATOR-ID
        ItemStatus              is Numeric size 1
		InventorySerialNumber   is an SerialNumber

	Local Fields	
		LocalBaseAmount			is a CurrencyExchangeDateGroup
		LocalFromCurrency		is a FromCurrency
		LocalCurrencyTable		is a CurrencyTable
		LocalItemBaseAmount		is like InternationalAmount
		LocalAssetInventoryDate is Date
	
	Context Fields
		InventoryDateRange		is a DateRange 
		
	Transient Fields
		TransientInventoryDate 	is Date	
			derive value from AssetItemInventory.CreationDate date
		TransientAssetOwner		is an Employee
			derive value from AssetRel.AssetOwner
		TransientAssetDivision	is an AssetDivision
			derive value from AssetRel.AssetDivision
		TransientMatchStatus	is Numeric 1
			derive value from DerivedMatchStatus
			States
				Match 						value is 1
				NoMatch 					value is 2
				Disposed 					value is 3
				DifferentSerial 			value is 4
				DifferentLocation 			value is 5
				DifferentLocationDetail 	value is 6
				DifferentLocationAndSerial 	value is 7
				
	Derived Fields			
		BaseAmount is a DerivedField
			type is like InternationalAmount
			
			LocalCurrencyTable						= first AssetItemRel.Asset.CurrencyTable
			LocalBaseAmount.TransactionAmount		= first AssetItemRel.TransactionItemCost
			LocalFromCurrency						= first AssetItemRel.Asset.Currency
			LocalBaseAmount.BaseAmount.ToCurrency	= first AssetItemRel.Asset.Company.GeneralLedgerCompany.Currency
			LocalBaseAmount.ExchangeDate 			= first AssetItemRel.HighPurchaseDate
			LocalItemBaseAmount						= LocalBaseAmount.BaseAmount.OutputCurrencyAmount
			LocalAssetInventoryDate					= first AssetItemRel.InventoryDate date
			return LocalItemBaseAmount
			
		TransactionAmount is a DerivedField
			type is like InternationalAmount
			return first AssetItemRel.TransactionItemCost
		
		AssetItemInventoryDate is a DerivedField
			type is Date
			return first AssetItemRel.InventoryDate date
			
		DifferenceQuantity is a DerivedField
			type is like ItemQuantity
			return (InventoryQuantity - DerivedItemQuantity)

		DerivedAssetItem is a DerivedField
			type is like AssetItem
			return first AssetItemRel.AssetItem
		
		DerivedAssetItemNumber is a DerivedField
			type is like AssetItemNumber
			return first AssetItemRel.ItemNumber
		
		DerivedAssetItemDescription is a DerivedField
			type is like Description
			return first AssetItemRel.Description
			
		DerivedItemQuantity is a DerivedField
			type is like ItemQuantity
			return first AssetItemRel.ItemQuantity       	
        	
		DerivedAssetCurrency is a DerivedField
			type is like Currency	
			return first AssetItemRel.Asset.Currency
        
		DerivedAsset is a DerivedField
			type is like Asset
			return first AssetItemRel.Asset
        
		DerivedDivision is a DerivedField
			type is like AssetDivision
			return AssetRel.AssetDivision

		DerivedAssetLocationDetail is a DerivedField
			type is like AssetLocationDetail
			return first AssetItemRel.LocationDetail

		DerivedAssetLocation is a DerivedField
			type is like AssetLocation
			return AssetRel.AssetLocation

		DerivedItemSerialNumber is a DerivedField
			type is like SerialNumber
			return first AssetItemRel.SerialNumber

		DerivedMatchStatus						is a DerivedField
			type is Numeric 1
			if(IsDisposedAsset)
				return 3
			else
				if(DerivedAsset not entered
				or InventoryQuantity != DerivedItemQuantity)
					return 2
				else
					if(InventoryLocation = DerivedAssetLocation				
					and InventorySerialNumber = DerivedItemSerialNumber
					and InventoryLocationDetail = DerivedAssetLocationDetail)
						return 1
					else
					if(InventoryLocation = DerivedAssetLocation				
					and InventoryLocationDetail != DerivedAssetLocationDetail)
						return 6
					else
					if (InventoryLocation != DerivedAssetLocation				
					and InventorySerialNumber != DerivedItemSerialNumber)
						return 7
					else
					if(InventoryLocation != DerivedAssetLocation)			
						return 5
					else
					if (InventorySerialNumber != DerivedItemSerialNumber)
						return 4

	Conditions
		InFilterDateRange
			restricted
			when (InventoryDateRange not entered
			or AssetItemInventory.CreationDate within InventoryDateRange)

		IsDisposedAsset
			restricted
			when (AssetRel.Status.Disposed)

		IsUnreleasedAsset
			restricted
			when (AssetRel.Status.Unreleased)
			
	Relations
		AssetItemRel
        	one-to-many relation to AssetItem
        	Field Mapping uses Set4
        		related.FinanceEnterpriseGroup	= AssetItemInventory.BarCode.FinanceEnterpriseGroup
				related.BarCode					= AssetItemInventory.BarCode
		
		AssetRel
			one-to-one relation to Asset
			Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup	= AssetItemInventory.BarCode.FinanceEnterpriseGroup
        		related.Asset					= DerivedAsset
        
	Actions
		Create is a Create Action
		
		Delete is a Delete Action

		Purge is a Purge Action
			restricted
		
		AssetItemInventoryPurge is a Set Action
			restricted
			Parameters
				PrmRunGroup							is like RunGroup
				PrmDateRange			        	is a DateRange

			Local Fields
				LocalActor						is an Actor
				LocalNotification				is LPLText
				LocalNewLine					is LPLText
				LocalAssetItemInventoryCount	is Numeric 10

			Parameter Rules
				PrmDateRange
					if(PrmDateRange.Begin entered)
						constraint (PrmDateRange.End entered)
							"To-_DateRequiredWhen_From_-_DateEntered"

			Instance Selection
				include deleted records
				where((PrmRunGroup entered
				and PrmDateRange entered
				and AssetItemInventory.RunGroup				  = PrmRunGroup
				and AssetItemInventory.CreationDate date within PrmDateRange)
			 	or (PrmRunGroup entered
				and PrmDateRange not entered
				and AssetItemInventory.RunGroup			      = PrmRunGroup)
				or (PrmRunGroup not entered
				and	PrmDateRange entered
				and AssetItemInventory.CreationDate date within PrmDateRange))

			Action Rules
				Empty Set Rules
					LocalActor = actor
					send notification
						to LocalActor
						description is "AssetItemInventoryPurgeDetails"
						priority is high
						detail is "ThereAreNoAssetItemInventoryRecordsToPurge"
				Set Rules
					Exit Rules
						LocalNewLine		= "\u000a"
						LocalNotification   = "Number of records that have been purged " + LocalNewLine
						LocalNotification	+= "AssetItemInventory: " + LocalAssetItemInventoryCount + LocalNewLine

						LocalActor = actor
						send notification
							to LocalActor
							description is "AssetItemInventoryPurgeDetails"
							priority is high
							detail is "<LocalNotification>"
				Instance Rules
					LocalAssetItemInventoryCount += 1
					invoke Purge
