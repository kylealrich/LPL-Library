WorkPreNotificationPrint is a BusinessClass
	owned by ar
	
	prefix is EFT1
	
	Ontology
		symbolic key is WorkPreNotificationPrint
	
	Patterns
		disable AuditIndex
		disable Auditing 
		disable EffectiveDated
		disable DataTranslations
		disable StaticTranslations
	
	Persistent Fields
		FinanceEnterpriseGroup
		EFTNotificationPrintResult  is like EFTNotificationPrintResult
		CustomerGroupEntered		is Boolean
		DataFileName				is AlphaUpper size 50
		RecKey                  	is a AR105Ar1051workRecKeyGroup
		BankEntity              	is like BankEntity
        BankAccountNumber          	is like BankAccountNumber 
        	holds pii
        EftType                 	is Alpha size 1
        EftSeq                  	is like Sequence		
        DueDate                 	is Date      
        TransactionDate            	is Date      
        TransactionAmount           is like InternationalAmount
        AppliedAmount               is like InternationalAmount
        AdjustmentAmount            is like InternationalAmount
        DiscountAmount              is like InternationalAmount
        ProcCompanyDiscountAmount	is like InternationalAmount
        DiscDate                	is Date      
        OriginalAmount              is like InternationalAmount
        OriginalAppliedAmount       is like InternationalAmount
        OriginalAdjustmentAmount    is like InternationalAmount
        NoteMethod                	is Alpha size 1
        PreNote                 	is Alpha size 1
        OriginalCurrency           	is like Currency
        CompanyCurrency				is like Currency
        OrigNd                  	is like NumberOfDecimals				
        CustPoNbr               	is like CustomerPurchaseOrder		
        OrigRate                	is like OrigRate		
        CurrMudv                	is like CurrencyMultiplicationOrDivision	
        GroupRate               	is like CurrencyRateToCustomerGroupCurrency	
        GroupMudv               	is like CustomerGroupCurrencyMultiplicationOrDivision	
        BankInstCode            	is like BankTransactionCode
        BankInstType            	is like BankInstType 
        XrefNbr                 	is like XrefNbr		
        XrefType                	is Alpha size 1
        Vendor                  	is like Vendor
        CudName                 	is like VendorName 
        	holds pii
        CudAddress					is a PostalAddressV2	
        	holds pii
        CudContact              	is like Contact
        Contact                 	is like Contact
        EdiNbr                  	is like EDINumber	
        FaxNumber					is like FaxNumber
        AltEdiType              	is like AltEdiType		
        AltEdiNbr               	is like AltEdiNbr		
        CreditAnlyst            	is like CreditAnalyst
        SumLine                 	is Boolean		

	Local Fields
		LocalEFTNotificationOutputCompany		is like Company
		LocalEFTNotificationOutput				is like EFTNotificationOutput
		
		LocalFinanceEnterpriseGroup				is a FinanceEnterpriseGroup
		LocalEFTNotificationPrintResult			is like EFTNotificationPrintResult

		LocalCustomerGroup						is like CustomerGroup
		LocalCustomerBankCompany				is like Company
		LocalCustomer							is like Customer
		LocalCompany							is like Company
		LocalBankCompany						is like Company
		LocalBankCustomer						is like Customer
		LocalBankDate							is Date
		LocalPreNotifyCreditAnlyst				is like CreditAnalyst
		LocalCustomerGroupEntered				is Boolean
				
	Relations
		WorkPreNotificationPrintRel
			one-to-many relation to WorkPreNotificationPrint
			Field Mapping uses RecKey
				related.FinanceEnterpriseGroup 								= LocalFinanceEnterpriseGroup
				related.EFTNotificationPrintResult							= LocalEFTNotificationPrintResult
			Instance Selection
				where (related.RecKey.CustGroup = LocalCustomerGroup
				and    !(related.PreNote = "N" 
				and      related.RecKey.EftSort = "A"))	        

		WorkFinalNotiPrintRel
			one-to-one relation to WorkFinalNotificationPrint
			Field Mapping uses ByCustomer
				related.Customer 											= RecKey.CrCustomer

		WorkFinalNotificationPrintRel
			one-to-many relation to WorkFinalNotificationPrint
			Field Mapping uses ByEFTNotificationPrintResult
				related.FinanceEnterpriseGroup 								= LocalFinanceEnterpriseGroup
				related.EFTNotificationPrintResult							= LocalEFTNotificationPrintResult
								
		CstDraftBankRel
			one-to-many relation to CustomerDraftBank
			Field Mapping uses Set4
				related.CustomerGroup 										= RecKey.CustGroup
			Instance Selection
				where (related.Customer = RecKey.CrCustomer
				and    ((LocalCompany entered 
				and      related.Company = LocalCustomerBankCompany) 
				or      (CustomerGroupEntered 
				and      related.Company not entered))
				and    (current date >= related.BankDateRange.StartDate 
				and 	LocalBankDate <= related.BankDateRange.EndDate))

		WorkPreNotiPrintRel
			one-to-many relation to WorkPreNotificationPrint
			Field Mapping uses RecKey
				related.FinanceEnterpriseGroup 								= LocalFinanceEnterpriseGroup
				related.EFTNotificationPrintResult							= LocalEFTNotificationPrintResult
			Instance Selection
				where (related.RecKey.CustGroup = LocalCustomerGroup
				and    related.RecKey.CrCustomer = LocalCustomer)				

		EFTNotificationOutputRel
			one-to-one relation to EFTNotificationOutput
			Field Mapping uses symbolic key
				related.Company												= LocalEFTNotificationOutputCompany
				related.EFTNotificationOutput								= LocalEFTNotificationOutput
				
		CstDraftBnkRel
			one-to-many relation to CustomerDraftBank
			Field Mapping uses Set4
				related.CustomerGroup 										= LocalCustomerGroup
			Instance Selection
				where (((LocalCompany entered 
				and      related.Company = LocalCompany)
				or      (LocalCustomerGroupEntered 
				and      related.Company not entered))
				and    (related.PrenotificationRequired = true 
				and     related.PrenoteStatus != 9))
				
		EFTNotifyRel
			one-to-one relation to ReceivableElectronicFundsTransferNotify
			Field Mapping uses symbolic key
				related.CustomerGroup										= LocalCustomerGroup
				related.Company												= LocalBankCompany
				related.Customer											= LocalBankCustomer

		InvoiceDetailRel
			one-to-one relation to ReceivableInvoiceDetail
			Field Mapping uses symbolic key
				related.Company 											= RecKey.Company
				related.ReceivableInvoiceDetail.ReceivableInvoiceDetailType = RecKey.TransType
				related.ReceivableInvoiceDetail.Invoice 					= RecKey.Invoice
				related.ReceivableInvoiceDetail.PaymentSeq 					= RecKey.PaymentSeq				

		EFTTransactionRel
			one-to-one relation to ElectronicFundsTransferTransaction
			Field Mapping uses Set1
				related.CustomerGroup										= RecKey.CustGroup
                related.ProcessingCompany									= RecKey.CrCompany
                related.Company												= RecKey.Company
                related.ReceivableInvoiceDetail.ReceivableInvoiceDetailType	= RecKey.TransType
                related.ReceivableInvoiceDetail.Invoice						= RecKey.Invoice
                related.ReceivableInvoiceDetail.PaymentSeq 					= RecKey.PaymentSeq
                related.ElectronicFundsTransferTransaction.EftType			= EftType
                related.ElectronicFundsTransferTransaction.EftSeq			= EftSeq
                
		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company												= RecKey.CrCompany
				related.Customer 											= RecKey.CrCustomer
				                																				
	Sets
		RecKey
			indexed
			no duplicates
			Sort Order
				FinanceEnterpriseGroup
				EFTNotificationPrintResult
				RecKey

	Rule Blocks
					
	Actions
	
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

  		Purge is a Purge Action
  			restricted
			bypass relational integrity rules
			
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules


		ProcessEFTNotificationPrint is a Set Action
			restricted
			Parameters
				PrmCustGroup							is a CustomerGroup
				PrmLCompany								is a ReceivableCompany
				PrmFinanceEnterpriseGroup				is a FinanceEnterpriseGroup
				PrmCashManagementGroup					is a FinanceEnterpriseGroup
				PrmBankTransactionCode					is a BankTransactionCode
					context of PrmFinanceEnterpriseGroup
				PrmDataFileName							is AlphaUpper size 50
				PrmEFTNotiPrintResult					is an EFTNotificationPrintResult
					context of PrmFinanceEnterpriseGroup
				PrmCustomerBankCompany					is a ReceivableCompany
				PrmCustomerGroup						is a CustomerGroup
				PrmCustomerGroupEntered					is Boolean						
				PrmProcessingCompany					is a ReceivableCompany
				PrmDueType								is Alpha size 1
				PrmDueDays								is like DueDays
				PrmEFTCalendar							is like SystemCalendar
				PrmDerivedBankDate						is Date			

			Parameter Rules
			
			Local Fields
				LocalCrCustomer							is like Customer
				LocalLastRecKey							is a AR105Ar1051workRecKeyGroup
				NewEFTNotificationOutput 				is a EFTNotificationOutput view
				
				LocalBankSwitch							is Boolean
				LocalDueType							is Alpha size 1
				LocalDetailTransactionAmount			is Alpha size 20
				LocalDetailDiscountAmount				is Alpha size 20
				LocalDetailOpenAmount					is Alpha size 20
				LocalTotalPreTransactionAmount			is Alpha size 20
				LocalTotalPreDiscountAmount				is Alpha size 20
				LocalTotalPreNetAmount					is Alpha size 20
				LocalTotalFinalTransactionAmount		is Alpha size 20
				LocalTotalFinalDiscountAmount			is Alpha size 20
				LocalTotalFinalNetAmount				is Alpha size 20		

		        LocalSummarizeTransactionAmount			is like InternationalAmount
				LocalSummarizeDiscountAmount			is like InternationalAmount
				LocalSummarizeNetAmount					is like InternationalAmount
				LocalSummarizeCompany					is like Company 
				LocalSummarizeTransactionType			is like ReceivableInvoiceDetailType				
				LocalSummarizeInvoice					is like InvoiceNum
				LocalSummarizeCustomerPoNumber			is like CustomerPurchaseOrder
				LocalSummarizeDetailTranAmount			is Alpha size 20
				LocalSummarizeDetailDiscAmount			is Alpha size 20
				LocalSummarizeDetailOpenAmount			is Alpha size 20
				LocalSummarizeLine                 		is Boolean
				LocalFormatedDiscountDate				is Alpha size 15
				LocalFormatedTransactionDate			is Alpha size 15
				LocalFormatedDueDate					is Alpha size 15
				LocalOutputDetailData	 				is Alpha size 1000
				LocalPreCount							is Numeric size 10
				LocalFinalCount							is Numeric size 10
				LocalPreNetAmount						is like InternationalAmount
				LocalPreAmount							is like InternationalAmount
				LocalPreDiscount						is like InternationalAmount
				LocalFinalAmount						is like InternationalAmount
				LocalFinDiscount						is like InternationalAmount
				LocalOpenAmount							is like InternationalAmount
				LocalFinalNetAmount						is like InternationalAmount
				LocalEftTransactionNetAmount			is like InternationalAmount
				LocalPreHeader							is Boolean
				LocalFinalHeader						is Boolean

		        RecordCount								is Numeric 4
				LocalServer								is Alpha size 5
				LocalHeaderNumber      					is Alpha size 95
				LocalFaxExtention						is Alpha size 50
				LocalFaxNumber							is Alpha size 50
				LocalDraftBankCustomer					is like Customer
				IsHoldEFTProcessing						is Boolean

		        LocalCashManagementGroup				is like CashManagementGroup
		        LocalBankTransactionCode				is like BankTransactionCode
		        LocalDataFileName						is AlphaUpper size 50

				LocalDueDays							is like DueDays
				EFTCalendar								is like SystemCalendar


				LocalPreCreditCount						is Numeric 12
				LocalPreDebitOrInvoiceCount				is Numeric 12
				LocalFinalCreditCount					is Numeric 12
				LocalFinalDebitOrInvoiceCount			is Numeric 12
				
				LocalPreCreditNetAmount					is like InternationalAmount
				LocalPreDebitOrInvoiceNetAmount			is like InternationalAmount
				LocalFinalCreditNetAmount				is like InternationalAmount
				LocalFinalDebitOrInvoiceNetAmount		is like InternationalAmount
								
				
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and EFTNotificationPrintResult	= PrmEFTNotiPrintResult
				and RecKey.CustGroup			= PrmCustomerGroup
				and !(PreNote = "N"
				and  RecKey.EftSort = "A"))

			Sort Order is RecKey 
			
			Rule Blocks
				CurrencyConversion
					CurrencyWork.FinanceEnterpriseGroup								= LocalFinanceEnterpriseGroup
					CurrencyWork.ExchangeDate										= LocalExchangeDate
					CurrencyWork.TransactionAmount									= LocalTransactionAmount
					CurrencyWork.FromCurrency										= LocalFromCurrency
					CurrencyWork.BaseAmount.ToCurrency								= LocalToCurrency
					LocalOutputCurrencyAmount										= CurrencyWork.BaseAmount.OutputCurrencyAmount
		
				CreateEftNotificationOutputDetail
					invoke Create EFTNotificationOutputDetail
						invoked.Company												= NewEFTNotificationOutput.Company
						invoked.EFTNotificationOutput								= NewEFTNotificationOutput.EFTNotificationOutput
						invoked.Data												= LocalOutputDetailData
		
				CreateEFTOutputDetail
					if (RecKey.Company   != LocalSummarizeCompany
					or  RecKey.TransType != LocalSummarizeTransactionType
					or  RecKey.Invoice   != LocalSummarizeInvoice)
						LocalSummarizeTransactionAmount								= 0
						LocalSummarizeDiscountAmount								= 0
						LocalSummarizeNetAmount										= 0
						LocalSummarizeCompany										= RecKey.Company 
						LocalSummarizeTransactionType								= RecKey.TransType
						LocalSummarizeInvoice										= RecKey.Invoice
						LocalSummarizeLine 											= SumLine
						if (LocalSummarizeLine)

							include FormatDates
							if (CustPoNbr entered)
								LocalSummarizeCustomerPoNumber						= CustPoNbr
							else
								LocalSummarizeCustomerPoNumber						= XrefNbr
							if (EftType = "P")
								LocalPreCount 									   += 1
							else
								LocalFinalCount 								   += 1
					
					LocalOpenAmount 												= OriginalAmount - (OriginalAppliedAmount + OriginalAdjustmentAmount + DiscountAmount)
					if (PrmCustGroup entered)
						LocalEftTransactionNetAmount 								= OriginalAmount - (OriginalAppliedAmount + OriginalAdjustmentAmount + ProcCompanyDiscountAmount)
					if (EftType = "P")


						if (!LocalSummarizeLine)
							LocalPreCount 										   += 1
						if (RecKey.TransType = "C")
							LocalPreCreditCount									   += 1
							LocalPreCreditNetAmount								   += LocalOpenAmount
							LocalPreNetAmount									   -= LocalOpenAmount
							LocalPreAmount										   -= OriginalAmount
							LocalPreDiscount									   -= DiscountAmount
						else
							LocalPreDebitOrInvoiceCount							   += 1
							LocalPreDebitOrInvoiceNetAmount						   += LocalOpenAmount
							LocalPreNetAmount									   += LocalOpenAmount
							LocalPreAmount										   += OriginalAmount
							LocalPreDiscount									   += DiscountAmount
							
						if (!LocalPreHeader)
							LocalOutputDetailData 									= "The Following Are Prenotification Items For Electronic Funds Transfer"
							include CreateEftNotificationOutputDetail
							LocalOutputDetailData 									= ""
							include CreateEftNotificationOutputDetail
							LocalPreHeader 											= true

						if (LocalSummarizeLine)	
							include SummarizeInvoiceDetails
						if (!LocalSummarizeLine)

							include FormatDates
							include FormatAmounts
							if (RecKey.TransType = "C")
								LocalOutputDetailData 								= RecKey.TransType + " " + RecKey.Invoice using "%-22s" + " " + LocalFormatedTransactionDate using "%12s" + "  " + LocalFormatedDiscountDate using "%12s" + "  " + LocalFormatedDueDate using "%12s" + "  " + CustPoNbr using "%22s"+ " " + LocalDetailTransactionAmount using "%20s" + LocalDetailDiscountAmount using "%20s" + LocalDetailOpenAmount using "%20s"
							else
								LocalOutputDetailData 								= RecKey.TransType + " " + RecKey.Invoice using "%-22s" + " " + LocalFormatedTransactionDate using "%12s" + "  " + LocalFormatedDiscountDate using "%12s" + "  " + LocalFormatedDueDate using "%12s" + "  " + CustPoNbr using "%22s"+ " " + OriginalAmount using "%19.3f" + " " + DiscountAmount using "%19.3f" + " " + LocalOpenAmount using "%19.3f"
							include CreateEftNotificationOutputDetail
					else

						if (!LocalSummarizeLine)
							LocalFinalCount 									   += 1
						if (RecKey.TransType = "C")
							LocalFinalCreditCount								   += 1
							LocalFinalCreditNetAmount							   += LocalOpenAmount
							LocalFinalNetAmount									   -= LocalOpenAmount
							LocalFinalAmount									   -= OriginalAmount
							LocalFinDiscount									   -= DiscountAmount
						else
							LocalFinalDebitOrInvoiceCount						   += 1
							LocalFinalDebitOrInvoiceNetAmount					   += LocalOpenAmount
							LocalFinalNetAmount									   += LocalOpenAmount
							LocalFinalAmount									   += OriginalAmount
							LocalFinDiscount									   += DiscountAmount
						if (LocalPreCount != 0)

							if (PreNote != "N")
								include PreTotals
		
						invoke EFTUpdate InvoiceDetailRel
							invoked.EFTNotification 								= "9"
						




						
						if (!LocalFinalHeader)
							LocalOutputDetailData 									= "The Following Are Final Notification Items For Electronic Funds Transfer"
							include CreateEftNotificationOutputDetail
							LocalOutputDetailData 									= "Bank Entity  " + CstDraftBankRel.CustomerDraftBank.BOEEFTBank + "  " + "Bank Acct Nbr  " + CstDraftBankRel.CustomerDraftBank.BOEEFTBankAccount
							include CreateEftNotificationOutputDetail
							LocalOutputDetailData 									= ""
							include CreateEftNotificationOutputDetail
							LocalFinalHeader 										= true

						if (LocalSummarizeLine)	
							include SummarizeInvoiceDetails
						if (!LocalSummarizeLine)

							include FormatDates
							include FormatAmounts
							if (RecKey.TransType = "C")
								LocalOutputDetailData 								= RecKey.TransType + " " + RecKey.Invoice using "%-22s" + " " + LocalFormatedTransactionDate using "%12s" + "  " + LocalFormatedDiscountDate using "%12s" + "  " + LocalFormatedDueDate using "%12s" + "  " + CustPoNbr using "%22s"+ " " + LocalDetailTransactionAmount using "%20s" + LocalDetailDiscountAmount using "%20s" + LocalDetailOpenAmount using "%20s"
							else
								LocalOutputDetailData 								= RecKey.TransType + " " + RecKey.Invoice using "%-22s" + " " + LocalFormatedTransactionDate using "%12s" + "  " + LocalFormatedDiscountDate using "%12s" + "  " + LocalFormatedDueDate using "%12s" + "  " + CustPoNbr using "%22s"+ " " + OriginalAmount using "%19.3f" + " " + DiscountAmount using "%19.3f" + " " + LocalOpenAmount using "%19.3f"
							include CreateEftNotificationOutputDetail



					if (EFTTransactionRel exists)
						invoke FastUpdate EFTTransactionRel
							invoked.EftNote 										= "9"
							invoked.EFTNotificationPrintResult						= PrmEFTNotiPrintResult
							invoked.EFTNPrintDiscountDate							= DiscDate
							if (RecKey.TransType = "C")
								invoked.EFTNPrintDiscountAmount							= DiscountAmount * (-1)
								invoked.EFTNPrintInvoiceAmount							= OriginalAmount * (-1)
								invoked.EFTNPrintNetAmount								= LocalOpenAmount * (-1)
							else
								invoked.EFTNPrintDiscountAmount							= DiscountAmount
								invoked.EFTNPrintInvoiceAmount							= OriginalAmount
								invoked.EFTNPrintNetAmount								= LocalOpenAmount								 
				
				SummarizeInvoiceDetails
					if (RecKey.TransType = "C")
						LocalSummarizeTransactionAmount							   += OriginalAmount * (-1)
						LocalSummarizeDiscountAmount							   += DiscountAmount * (-1)
						LocalSummarizeNetAmount									   += LocalOpenAmount * (-1)
					else
						LocalSummarizeTransactionAmount							   += OriginalAmount
						LocalSummarizeDiscountAmount							   += DiscountAmount
						LocalSummarizeNetAmount									   += LocalOpenAmount
						
				PrintTotals
					if (LocalPreCount != 0)
						include PreTotals
					if (LocalFinalCount != 0)
						LocalOutputDetailData 										= ""
						include CreateEftNotificationOutputDetail
						LocalTotalFinalTransactionAmount							= LocalFinalAmount
						if (LocalFinalAmount < 0)
							LocalFinalAmount 										= LocalFinalAmount * (-1)
							LocalTotalFinalTransactionAmount 						= LocalFinalAmount using "%19.3f" + "-"
						LocalTotalFinalDiscountAmount								= LocalFinDiscount
						if (LocalFinDiscount < 0)
							LocalFinDiscount										= LocalFinDiscount * (-1)
							LocalTotalFinalDiscountAmount							= LocalFinDiscount using "%19.3f" + "-" 
						LocalTotalFinalNetAmount									= LocalFinalNetAmount
						if (LocalFinalNetAmount < 0)
							LocalFinalNetAmount 									= LocalFinalNetAmount * (-1)
							LocalTotalFinalNetAmount 								= LocalFinalNetAmount using "%19.3f" + "-" 
						LocalOutputDetailData 										= "                                 Final Notification Totals: Count/Amounts" + LocalFinalCount using "%10d" + "       " + LocalTotalFinalTransactionAmount using "%20s" + LocalTotalFinalDiscountAmount using "%20s" + LocalTotalFinalNetAmount using "%20s"
						include CreateEftNotificationOutputDetail
						LocalOutputDetailData 										= ""
						include CreateEftNotificationOutputDetail
						LocalOutputDetailData 										= ""
						include CreateEftNotificationOutputDetail
						
					invoke FastUpdate PrmEFTNotiPrintResult						
						invoked.FinalNoteCreditsCount				 = LocalFinalCreditCount
						invoked.FinalNoteDebitsOrInvoicesCount		 = LocalFinalDebitOrInvoiceCount

						
						invoked.FinalNoteCreditsAmount				 = LocalFinalCreditNetAmount * -1
						invoked.FinalNoteDebitsOrInvoicesAmount		 = LocalFinalDebitOrInvoiceNetAmount

						















										
						LocalPreHeader 												= false
						LocalFinalHeader 											= false
						
				PreTotals
					LocalOutputDetailData 											= ""
					include CreateEftNotificationOutputDetail
					LocalTotalPreTransactionAmount									= LocalPreAmount
					if (LocalPreAmount < 0)
						LocalPreAmount												= LocalPreAmount * (-1)
						LocalTotalPreTransactionAmount								= LocalPreAmount using "%19.3f" + "-"
					LocalTotalPreDiscountAmount										= LocalPreDiscount
					if (LocalTotalPreDiscountAmount < 0)
						LocalPreDiscount 											= LocalPreDiscount * (-1)
						LocalTotalPreDiscountAmount									= LocalPreDiscount using "%19.3f" + "-"
					LocalTotalPreNetAmount											= LocalPreNetAmount
					if (LocalPreNetAmount < 0)
						LocalPreNetAmount 											= LocalPreNetAmount * (-1)
						LocalTotalPreNetAmount										= LocalPreNetAmount using "%19.3f" + "-"
					LocalOutputDetailData 											= "                                     Pre-Notification Totals: Count/Amounts" + LocalPreCount using "%10d" + "     " + LocalTotalPreTransactionAmount using "%20s" + LocalTotalPreDiscountAmount using "%20s" + LocalTotalPreNetAmount using "%20s"
					include CreateEftNotificationOutputDetail
					
					LocalOutputDetailData 											= ""
					include CreateEftNotificationOutputDetail
					LocalOutputDetailData 											= ""
					include CreateEftNotificationOutputDetail

					invoke FastUpdate PrmEFTNotiPrintResult							
						invoked.PreNoteCreditsCount					 = LocalPreCreditCount
						invoked.PreNoteDebitsOrInvoicesCount		 = LocalPreDebitOrInvoiceCount

						
						invoked.PreNoteCreditsAmount				 = LocalPreCreditNetAmount * -1
						invoked.PreNoteDebitsOrInvoicesAmount		 = LocalPreDebitOrInvoiceNetAmount

					LocalPreCount 													= 0											

				FormatDates
					if (TransactionDate entered)
						LocalFormatedTransactionDate 								= TransactionDate month + "/" + TransactionDate day  + "/" + TransactionDate year 
					else
						LocalFormatedTransactionDate 								= "            "
					if (DiscDate entered)
						LocalFormatedDiscountDate 									= DiscDate month + "/" + DiscDate day + "/" + DiscDate year
					else
						LocalFormatedDiscountDate 									= "            "
					if (DueDate entered)
						LocalFormatedDueDate 										= DueDate month + "/" + DueDate day + "/" + DueDate year
					else
						LocalFormatedDueDate 										= "            "
				
				FormatAmounts
					if (RecKey.TransType = "C")
						LocalDetailTransactionAmount 								= OriginalAmount using "%19.3f" + "-"
						LocalDetailDiscountAmount 									= DiscountAmount using "%19.3f" + "-"
						LocalDetailOpenAmount 										= LocalOpenAmount using "%19.3f" + "-"
			
			Accumulators
			
			Action Rules
				Empty Set Rules
				
				Set Rules
					Entrance Rules

				        LocalCompany										= PrmLCompany
				        LocalFinanceEnterpriseGroup							= PrmFinanceEnterpriseGroup



				        LocalEFTNotificationPrintResult						= PrmEFTNotiPrintResult
				        
				        CustomerGroupEntered								= PrmCustomerGroupEntered
			        	LocalCustomerGroup									= PrmCustomerGroup

			        	LocalDueType										= PrmDueType
			        	LocalDueDays										= PrmDueDays
			        	EFTCalendar											= PrmEFTCalendar
				        if (!PrmCustomerGroupEntered entered)
				        	LocalCustomerBankCompany						= PrmCustomerBankCompany

						LocalPreHeader 										= false
						LocalFinalHeader 									= false
					
					Exit Rules
						LocalCustomerGroup									= PrmCustomerGroup
						LocalCustomerGroupEntered							= PrmCustomerGroupEntered
						LocalCompany										= PrmLCompany
						LocalFinanceEnterpriseGroup							= PrmFinanceEnterpriseGroup
						LocalEFTNotificationPrintResult						= PrmEFTNotiPrintResult
						LocalCompany										= PrmLCompany
			        	LocalDueType										= PrmDueType
			        	LocalDueDays										= PrmDueDays
			        	EFTCalendar											= PrmEFTCalendar
				        if (!PrmCustomerGroupEntered entered)
				        	LocalCustomerBankCompany						= PrmCustomerBankCompany
						LocalDraftBankCustomer	= 0
						if (LocalBankSwitch)
							for each CstDraftBnkRel

								IsHoldEFTProcessing 							= false
								LocalBankCustomer								= each.Customer
								LocalBankCompany								= each.Company
								if  (EFTNotifyRel exists
								and !EFTNotifyRel.HoldAllProcessing)		

									if (LocalDueType = "M")

										LocalBankDate 							= PrmDerivedBankDate
									else
										LocalBankDate 							= current date + PrmDueDays
									
									if (current date >= each.BankDateRange.StartDate
									and LocalBankDate <= each.BankDateRange.EndDate)

										if (LocalDraftBankCustomer != each.Customer)


											if (EFTNotifyRel.EFTNotificationMethod = "E")
												LocalServer						= "EDI"
												LocalHeaderNumber				= last WorkPreNotificationPrintRel.EdiNbr
											
											if (EFTNotifyRel.EFTNotificationMethod  = "A")
												LocalServer						= last WorkPreNotificationPrintRel.AltEdiType
												LocalHeaderNumber				= last WorkPreNotificationPrintRel.AltEdiNbr
											
											if (EFTNotifyRel.EFTNotificationMethod 	= "F")
												LocalServer						= "FAX"
												LocalFaxExtention				= last WorkPreNotificationPrintRel.FaxNumber.Extension
												LocalFaxNumber					= last WorkPreNotificationPrintRel.FaxNumber.SubscriberNumber
												LocalHeaderNumber				= LocalFaxNumber using "%-15s" + LocalFaxExtention using "%-5s"
											
											LocalOutputDetailData = LocalHeaderNumber using "%-95s"+ LocalServer
											include CreateEftNotificationOutputDetail
											
											LocalOutputDetailData 				= "The Following Bank Account Will Be Prenotified For Electronic Funds Transfer"
											include CreateEftNotificationOutputDetail
											
											LocalDraftBankCustomer 				= each.Customer
										LocalOutputDetailData 					= "Bank ID Number: " + each.CustomerDraftBank.BOEEFTBank + "  " + "Bank Account Number: " + each.CustomerDraftBank.BOEEFTBankAccount
										include CreateEftNotificationOutputDetail
										invoke FastUpdate each
											invoked.PrenoteStatus 				= 1
								
						invoke FastUpdate PrmEFTNotiPrintResult
							invoked.ProcessingCompany	= PrmProcessingCompany
						invoke FastDelete WorkFinalNotificationPrintRel
						invoke FastDelete WorkPreNotificationPrintRel
						invoke UpdateStatusOnResult	PrmEFTNotiPrintResult
										
				Instance Rules	



					LocalCompany									= PrmLCompany
					LocalFinanceEnterpriseGroup						= FinanceEnterpriseGroup
					LocalEFTNotificationPrintResult					= EFTNotificationPrintResult
					LocalCustomerGroup								= RecKey.CustGroup
					LocalPreNotifyCreditAnlyst 						= CreditAnlyst
					LocalBankSwitch									= false
			        if (!PrmCustomerGroupEntered entered)
			        	LocalCustomerBankCompany					= PrmCustomerBankCompany					

					
					if (WorkFinalNotiPrintRel exists)
						if (LocalDueType = "M")

							LocalBankDate 							= PrmDerivedBankDate
						else
							LocalBankDate 							= current corporate date + PrmDueDays
						
						for each CstDraftBankRel
							if (each.EFTBankAccountType not entered)
								LocalBankSwitch						= true
								end for each
							if (WorkFinalNotiPrintRel.CurrBal 		< 0
							and each.EFTBankAccountType = "C")
								LocalBankSwitch						= true
								end for each
							if (WorkFinalNotiPrintRel.CurrBal 		> 0
							and each.EFTBankAccountType = "D")
								LocalBankSwitch						= true
								end for each

					if (LocalBankSwitch
					or RecKey.EftSort != "Z")
						if (LocalCrCustomer = RecKey.CrCustomer)
							if (LocalSummarizeLine)
								if (RecKey.Company   != LocalSummarizeCompany
								or  RecKey.TransType != LocalSummarizeTransactionType
								or  RecKey.Invoice   != LocalSummarizeInvoice)

									if (RecKey.TransType = "C")
										LocalSummarizeDetailTranAmount 	= LocalSummarizeTransactionAmount using "%19.3f" + "-"
										LocalSummarizeDetailDiscAmount 	= LocalSummarizeDiscountAmount using "%19.3f" + "-"
										LocalSummarizeDetailOpenAmount 	= LocalSummarizeNetAmount  using "%19.3f" + "-"												
										LocalOutputDetailData 			= LocalSummarizeTransactionType + " " + LocalSummarizeInvoice using "%-22s" + " " + LocalFormatedTransactionDate  using "%12s" + "  " + LocalFormatedDiscountDate  using "%12s" + "  " + LocalFormatedDueDate  using "%12s" + "  " + LocalSummarizeCustomerPoNumber using "%22s"+ " " + LocalSummarizeDetailTranAmount using "%20s" + LocalSummarizeDetailDiscAmount using "%20s" + LocalSummarizeDetailOpenAmount using "%20s"
									else
										LocalOutputDetailData 			= LocalSummarizeTransactionType + " " + LocalSummarizeInvoice using "%-22s" + " " + LocalFormatedTransactionDate  using "%12s" + "  " + LocalFormatedDiscountDate  using "%12s" + "  " + LocalFormatedDueDate  using "%12s" + "  " + LocalSummarizeCustomerPoNumber using "%22s"+ " " + LocalSummarizeTransactionAmount using "%19.3f" + " " + LocalSummarizeDiscountAmount using "%19.3f" + " " + LocalSummarizeNetAmount using "%19.3f"
									include CreateEftNotificationOutputDetail			
									LocalSummarizeLine 					= false
							include CreateEFTOutputDetail
							if (RecKey = LocalLastRecKey)
								LocalEFTNotificationOutputCompany		= NewEFTNotificationOutput.Company
								LocalEFTNotificationOutput				= NewEFTNotificationOutput.EFTNotificationOutput
								include PrintTotals
								invoke CreateEFTNotificationFile EFTNotificationOutputRel




						else
							LocalPreCount							= 0
							LocalPreAmount 							= 0
							LocalPreDiscount						= 0
							LocalPreNetAmount						= 0
							LocalTotalPreTransactionAmount 			= 0
							LocalTotalPreDiscountAmount				= 0
							LocalTotalPreNetAmount					= 0

							LocalFinalCount							= 0
							LocalFinalAmount						= 0
							LocalFinDiscount						= 0				
							LocalFinalNetAmount						= 0
							LocalTotalFinalTransactionAmount		= 0
							LocalTotalFinalDiscountAmount			= 0
							LocalTotalFinalNetAmount				= 0
							invoke Create EFTNotificationOutput
								assign result to NewEFTNotificationOutput
								invoked.Company							= RecKey.CrCompany
								invoked.Customer						= RecKey.CrCustomer
								invoked.DataFileName					= PrmDataFileName
								invoked.EFTNotificationPrintResult 		= PrmEFTNotiPrintResult
						
							if (first WorkPreNotificationPrintRel.NoteMethod = "E")
								LocalServer								= "EDI"
								LocalHeaderNumber						= EdiNbr
							if (first WorkPreNotificationPrintRel.NoteMethod = "A")
								LocalServer								= AltEdiType
								LocalHeaderNumber						= AltEdiNbr
							if (first WorkPreNotificationPrintRel.NoteMethod = "F")
								LocalServer								= "FAX"
								LocalFaxExtention						= FaxNumber.Extension
								LocalFaxNumber							= FaxNumber.SubscriberNumber
								LocalHeaderNumber						= LocalFaxNumber using "%-15s" + LocalFaxExtention using "%-5s"
							
							LocalOutputDetailData 						= LocalHeaderNumber using "%-95s"+ LocalServer
							include CreateEftNotificationOutputDetail
							
							LocalOutputDetailData 						= DueDate month + "/" + DueDate day + "/" + DueDate year
							include CreateEftNotificationOutputDetail
							
							LocalOutputDetailData 						= CudName using "%-120s"+ "              " + "Company " + PrmProcessingCompany.Name 
							include CreateEftNotificationOutputDetail
							
							if (CustomerGroupEntered)
								LocalOutputDetailData 					= "Attention: " + CudContact using "%-20s"+ "             " + "Analyst " + CompanyCustomerRel.CreditAnalyst.Name using "%-30s"
							else
								LocalOutputDetailData 					= "Attention: " + Contact using "%-20s" + "             " + "Analyst " + CompanyCustomerRel.CreditAnalyst.Name  using "%-30s"
							include CreateEftNotificationOutputDetail
							LocalOutputDetailData 						= "                                            Phone  " + CompanyCustomerRel.CreditAnalyst.PhoneNumber.InternationalPrefix using "%-6s" + CompanyCustomerRel.CreditAnalyst.PhoneNumber.SubscriberNumber using "%-15s" + " "+ CompanyCustomerRel.CreditAnalyst.PhoneNumber.Extension using "%-5s"
							include CreateEftNotificationOutputDetail
							
							LocalOutputDetailData 						= ""
							include CreateEftNotificationOutputDetail
							
							LocalOutputDetailData 						= "Transaction                Tran Date      Disc Date     Due Date      Customer PO                Tran Amount        Disc Amount          Net Amount"
							include CreateEftNotificationOutputDetail
							
							LocalOutputDetailData 						= "----------------------      --------       --------     --------    ----------------------  -----------------   -----------------  -----------------"
							include CreateEftNotificationOutputDetail
							

							LocalCustomer								= RecKey.CrCustomer
							LocalCrCustomer								= RecKey.CrCustomer
							LocalLastRecKey								= last WorkPreNotiPrintRel.RecKey 
							include CreateEFTOutputDetail
							if (RecKey = LocalLastRecKey)
								LocalEFTNotificationOutputCompany		= NewEFTNotificationOutput.Company
								LocalEFTNotificationOutput				= NewEFTNotificationOutput.EFTNotificationOutput							
								include PrintTotals
								invoke CreateEFTNotificationFile EFTNotificationOutputRel
					else
						if (!LocalBankSwitch
						and RecKey.EftSort = "Z"
						and LocalPreCount entered)	
							include PreTotals
						invoke FastUpdate PrmEFTNotiPrintResult
							invoked.RecordsExists 						= false
					RecordCount += 1
					if (RecordCount = 500)
						commit transaction
						RecordCount = 0					
			







				
