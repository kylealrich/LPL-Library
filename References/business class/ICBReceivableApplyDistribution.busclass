ICBReceivableApplyDistribution is a BusinessClass
	owned by intercobilling
	prefix is IBRAD
	sql name is "ICBRApplyDistribution"

	Ontology
		symbolic key is ICBReceivableApplyDistribution

	Persistent Fields
        IntercompanyBillingDocumentHeader
        ReceivableEntity
        InvoiceLine                         is a Sequence
		ApplyFundDistributionAccount        is a FinanceCodeBlock
        OriginalCurrency					is a FromCurrency
        DistributionAmount                  is a FinanceCurrencyAmountGroup
        Description
        OriginalLastDistributionSequence    is a DistSeq
        OriginalReceivableGLDistribution    is like ReceivableGLDistribution 

    Transient Fields
        TransientSupplyOldFundAppliedAmount is Boolean
        TransientOldFundAppliedAmount       is an InternationalAmount
        TransientAccountingEntity           is an AccountingEntity
            derive value from ReceivableEntity.Company.AccountingEntity
        BypassNegativeRateEdit
            derive value from true

    Conditions
        DistributionWasReleased
            restricted
            when (ReceivableGLDistributionRel.Status.Released
            or    ReceivableGLDistributionRel.Status.Posted)

    Relations		
        
        ReceivableGLDistributionRel
			one-to-one relation to ReceivableGLDistribution
			Field Mapping uses Set2
				related.ReceivableCompanyDataGroup.TransactionCompany				 = ReceivableEntity.Company
				related.BatchNumber													 = ReceivableEntity.ReceivableInvoice.BatchNumber
				related.TransType													 = ReceivableEntity.ReceivableInvoiceType
				related.Invoice														 = ReceivableEntity.ReceivableInvoice	
				related.ReceivableCompanyDataGroup.Customer							 = ReceivableEntity.Customer	
                related.LastDistributionSequence                                     = OriginalLastDistributionSequence
                related.ReceivableGLDistribution                                     = OriginalReceivableGLDistribution

    Sets

		ByReceivableGLDistribution
			Sort Order
                IntercompanyBillingGroup
                IntercompanyBillingSettlementHeader.SettlementID
                IntercompanyBillingDocumentHeader 
                InvoiceLine
                ReceivableEntity.Company
                ReceivableEntity.ReceivableInvoiceType
                ReceivableEntity.ReceivableInvoice
                OriginalLastDistributionSequence
                OriginalReceivableGLDistribution

        ICBReceivableApplyDistributionDescending
			Sort Order
                IntercompanyBillingGroup
                IntercompanyBillingSettlementHeader.SettlementID
                IntercompanyBillingDocumentHeader 
                InvoiceLine
                ReceivableEntity.Company
                ReceivableEntity.ReceivableInvoiceType
                ReceivableEntity.ReceivableInvoice
                ICBReceivableApplyDistribution descending

	
    Delete Rules
        invoke AddToFundAppliedAmount ReceivableGLDistributionRel
            invoked.PrmAmount = -1 * DistributionAmount.CurrencyAmount
    
    
    Actions
        
        Create is a Create Action
			restricted
            Entrance Rules
                if (not IntercompanyBillingSettlementHeader.BypassReleasedSelectionsValidation)
                    constraint (DistributionWasReleased)
                        "ReceivableDistributionHasNotBeenReleased"

            Action Rules
                invoke AddToFundAppliedAmount ReceivableGLDistributionRel
                    invoked.PrmAmount = DistributionAmount.CurrencyAmount
        				
		Update is an Update Action
			restricted
            Action Rules
                if (TransientSupplyOldFundAppliedAmount)
                    invoke AddToFundAppliedAmount ReceivableGLDistributionRel
                        invoked.PrmAmount = DistributionAmount.CurrencyAmount - TransientOldFundAppliedAmount
                    
                    TransientSupplyOldFundAppliedAmount = false
                else
                    invoke AddToFundAppliedAmount ReceivableGLDistributionRel
                        invoked.PrmAmount = DistributionAmount.CurrencyAmount - old DistributionAmount.CurrencyAmount
                     
        Delete is a Delete Action
			restricted
