TrueCostPracticalCapacityInterface is a BusinessClass
	owned by truecost
	sql name is PracticalCapacityInterface
	prefix is TCPIN
	default label is "PracticalCapacityVolumeInterface"
	
	Ontology
		symbolic key is TrueCostPracticalCapacityInterface
		
	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		TrueCostCapacity					is like TrueCostCapacity
			default label is "PracticalCapacity"
		TrueCostResourceIntensityFactor		is like TrueCostResourceIntensityFactor
			default label is "Driver_Weight"
		TrueCostResourceUtilizationDriver	is like TrueCostResourceUtilizationDriver
			default label is "Driver"
		CapacityVolume						is Unsigned Decimal 19.4 
		EffectiveDate						is Date
		AccountingEntity					is like AccountingEntity
			default label is "<TrueCostConfiguration.AccountingEntityLabel>"
		AccountingUnit						is like AccountingUnit
			default label is "<TrueCostConfiguration.AccountingUnitLabel>"
		GeneralLedgerChartAccount			is like GeneralLedgerChartAccount
			restricted
			default label is "<TrueCostConfiguration.AccountLabel>"
		Project								is like Project
			default label is "<TrueCostConfiguration.ProjectLabel>"
		FinanceDimension1					is like FinanceDimension1
			default label is "<TrueCostConfiguration.FinanceDimension1Label>"
		FinanceDimension2					is like FinanceDimension2
			default label is "<TrueCostConfiguration.FinanceDimension2Label>"
		FinanceDimension3					is like FinanceDimension3
			default label is "<TrueCostConfiguration.FinanceDimension3Label>"
		FinanceDimension4					is like FinanceDimension4
			default label is "<TrueCostConfiguration.FinanceDimension4Label>"
		FinanceDimension5					is like FinanceDimension5
			default label is "<TrueCostConfiguration.FinanceDimension5Label>"
		FinanceDimension6					is like FinanceDimension6
			default label is "<TrueCostConfiguration.FinanceDimension6Label>"
		FinanceDimension7					is like FinanceDimension7
			default label is "<TrueCostConfiguration.FinanceDimension7Label>"
		FinanceDimension8					is like FinanceDimension8
			default label is "<TrueCostConfiguration.FinanceDimension8Label>"
		FinanceDimension9					is like FinanceDimension9
			default label is "<TrueCostConfiguration.FinanceDimension9Label>"
		FinanceDimension10					is like FinanceDimension10
			default label is "<TrueCostConfiguration.FinanceDimension10Label>"
		ErrorDescription					is Alpha 150

	Local Fields
		LocalTrueCostConfiguration								is like TrueCostConfiguration
		LocalRunGroup											is like RunGroup

	Field Rules
		TrueCostCapacity
			required

		TrueCostResourceUtilizationDriver
			required
		
		AccountingEntity
			required
		
		EffectiveDate
			required

	Conditions
		HasError
			restricted
			when (ErrorDescription entered)
 
	Derived Fields
			
	Relations
		RunGroupParamRel
			one-to-many relation to TrueCostPracticalCapacityInterface
			Field Mapping uses symbolic key
				related.TrueCostConfiguration							= 	LocalTrueCostConfiguration
			Instance Selection
				where(related.TrueCostPracticalCapacityInterface.RunGroup 		= 	LocalRunGroup)
	
	
	Actions
		Create is a Create Action
		
		Delete is a Delete Action
		
		Update is an Update Action


		InterfacePracticalCapacityVolumes is a Set Action
			Parameters
				PrmTrueCostConfiguration		is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroup						is a RunGroup
					default label is "RunGroup" 
			Parameter Rules		
				PrmTrueCostConfiguration
					required					
				PrmRunGroup
					required
					LocalTrueCostConfiguration	=	PrmTrueCostConfiguration
					LocalRunGroup				=	PrmRunGroup					
					constraint(RunGroupParamRel exists)
						"RunGroupDoesNotExist"
			Instance Selection				
				where(TrueCostConfiguration 						= PrmTrueCostConfiguration
				and	TrueCostPracticalCapacityInterface.RunGroup		= PrmRunGroup)
			Local Fields
				LocalTotalCount			is Numeric 12
				LocalSuccessCount		is Numeric 12
				LocalFailedCount		is Numeric 12
				LocalActor				is an Actor									
			Action Rules
				Empty Set Rules
				
				Set Rules
					Exit Rules
						LocalFailedCount = LocalTotalCount - LocalSuccessCount
						LocalActor 		 = actor

						send notification
							to LocalActor
							description is "ImportTo_True\Cost_Practical_CapacityHasCompleted"
							priority is high
							detail is "NumberOfSuccessfulRecords<LocalTotalCount>.___NumberOfFailedRecords<LocalFailedCount>.___TotalRecordsProcessed<LocalSuccessCount>"
					
				Instance Rules
					LocalTotalCount += 1
					initialize ErrorDescription
						
					invoke Create TrueCostCapacityDetail
						resume on error
							ErrorDescription = error message
						invoked.TrueCostConfiguration   									= TrueCostConfiguration
						invoked.TrueCostCapacity        									= TrueCostCapacity
						invoked.TrueCostCapacityDetail.TrueCostResourceIntensityFactor		= TrueCostResourceIntensityFactor
						invoked.TrueCostCapacityDetail.TrueCostResourceUtilizationDriver	= TrueCostResourceUtilizationDriver
						invoked.CapacityVolume          									= CapacityVolume
						invoked.EffectiveDate												= EffectiveDate
						invoked.AccountingEntity											= AccountingEntity
						invoked.AccountingUnit												= AccountingUnit
						invoked.GeneralLedgerChartAccount									= GeneralLedgerChartAccount
						invoked.Project														= Project
						invoked.FinanceDimension1	  										= FinanceDimension1
						invoked.FinanceDimension2     										= FinanceDimension2
						invoked.FinanceDimension3     										= FinanceDimension3
						invoked.FinanceDimension4    										= FinanceDimension4
						invoked.FinanceDimension5	 										= FinanceDimension5
						invoked.FinanceDimension6	  										= FinanceDimension6
						invoked.FinanceDimension7	 										= FinanceDimension7
						invoked.FinanceDimension8	  						  				= FinanceDimension8
						invoked.FinanceDimension9  	  						    			= FinanceDimension9
						invoked.FinanceDimension10    										= FinanceDimension10
								
					if (ErrorDescription not entered)
						invoke Delete
						LocalSuccessCount +=1

		DeleteAllTransactionsForRunGroup is a Set Action
			default label is "DeleteAllTransactionsForRunGroup"
			Parameters
				PrmTrueCostConfiguration				is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroup						is a RunGroup
					default label is "RunGroup"
			Parameter Rules
				PrmTrueCostConfiguration
					required
				PrmRunGroup
					required
					LocalTrueCostConfiguration	=	PrmTrueCostConfiguration
					LocalRunGroup				=	PrmRunGroup				
					constraint(RunGroupParamRel exists)
						"RunGroupDoesNotExist"
			Instance Selection				
				where(TrueCostConfiguration 			 			= PrmTrueCostConfiguration
				and   TrueCostPracticalCapacityInterface.RunGroup 	= PrmRunGroup)
			Action Rules
				Instance Rules
					invoke Delete

		CopyRunGroup					is a Set Action
			Parameters
				PrmTrueCostConfiguration				is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroupFrom							is a RunGroup
					default label is "CopyFromRunGroup"
				PrmRunGroupTo							is a RunGroup
					default label is "CopyToRunGroup"
					
			Parameter Rules
				PrmTrueCostConfiguration
					required
				PrmRunGroupFrom
					required
					LocalTrueCostConfiguration	= PrmTrueCostConfiguration
					LocalRunGroup 				= PrmRunGroupFrom
					constraint (RunGroupParamRel exists)
						"SourceRunGroupDoesNotExist"
					constraint (PrmRunGroupFrom != PrmRunGroupTo)
						"CopyFromRunGroupCannotBeTheSameAsCopyToRunGroup"
				PrmRunGroupTo
					required
					
			Instance Selection
				where (TrueCostConfiguration					= PrmTrueCostConfiguration
				and TrueCostPracticalCapacityInterface.RunGroup	= PrmRunGroupFrom)
				
			Action Rules
				
				Instance Rules
					invoke Create TrueCostPracticalCapacityInterface
						fill in fields from this instance
							except invoked.RunGroup
						invoked.RunGroup				= PrmRunGroupTo



