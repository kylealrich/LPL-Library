EDIDataExchange is a BusinessClass
    owned by edi
    prefix is EDIDX

    Ontology
        symbolic key is EDIDataExchange

    Patterns
        implements DynamicCreation
        implements StaticJava
        disable Auditing

    Persistent Fields
        CarrierName             is a PfiEDICarrier 
        Direction
        ProcessStatus
        Data                    is Text
        LastUpdatedTimeStamp    is TimeStamp
        StatusMessage           is Text
        FileName                

	Local Fields
		LocalCommstat                is Numeric size 1
		
		
	Derived Fields
        ExchangeData is a DerivedField
        	type is BinaryDocument
        	return Data
			
    Field Rules
        ProcessStatus
            default to ProcessStatus.New
        
        Direction
            required
        Data
            required

        LastUpdatedTimeStamp
            default to current timestamp
            initial value is current timestamp

    Sets
        ByLastUpdated
            duplicates
            Sort Order
                LastUpdatedTimeStamp
                CarrierName
                Direction
    
    
    Relations
    	EDITransactionHistoryRel 	
    		one-to-many relation to EDITransactionHistory
    		Field Mapping uses Set5
    			related.DataExchangeId = EDIDataExchange

        EDIPOStatusUpdateRel 	                              
    		one-to-many relation to EDIPOStatusUpdate
    		Field Mapping uses Set1
    			related.DataExchangeId = EDIDataExchange
   								                                    
    Actions
        Create is a Create Action
            Exit Rules
                LastUpdatedTimeStamp = current timestamp
        Delete is a Delete Action
        Update is an Update Action
            Action Rules
                LastUpdatedTimeStamp = current timestamp
                

                if (ProcessStatus.Success)
                	LocalCommstat = EDITransactionHistory.Commstat.SUCCESS
                else 
	               	if (ProcessStatus.Error)
	               		LocalCommstat = EDITransactionHistory.Commstat.ERROR
	               	else
	               		LocalCommstat = EDITransactionHistory.Commstat.WAITING
                	 	
                for each EDITransactionHistoryRel
        			if(Direction.Out)
                        invoke Update each 
        				    invoked.Commstat				= LocalCommstat


                for each EDIPOStatusUpdateRel 
        			if(Direction.Out)
                        invoke Update each 
        				    invoked.Commstat    = ProcessStatus

        DeleteSuccessfulRecords is a Set Action
            Action Rules
                Instance Rules
                    if (ProcessStatus.Success)
                        invoke Delete EDIDataExchange

           
    Conditions
        IsInboundData
            when ( Direction = 2)
        IsOutboundData
            when ( Direction = 1)
        IsNewInboundData
            when ( Direction = 2
            and ProcessStatus = 1)
        IsNewOutboundData
            when ( Direction = 1
            and ProcessStatus = 1)
        IsOutboundDataError
            when ( Direction = 1
            and ProcessStatus = 4)
        IsInboundDataError
            when ( Direction = 2
            and (ProcessStatus = 4
            or ProcessStatus = 5))
            

