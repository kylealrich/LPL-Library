SubscriptionUpdates is a BusinessClass
	owned by sharedfinance
	
	prefix is FGSUp
	
	Ontology
		symbolic key is SubscriptionUpdates
		
	Patterns
			
	Persistent Fields
		ThisChanged							is BusinessObjectReference 
		Label								is Alpha 30
		UpdatedByResource					is a FinanceResource  
		HowItChanged						is Numeric 03
			States
				SomethingChanged			value is 000

				TaskAccepted 				value is 401  
				TaskInProcress    			value is 402 
				TaskComplete				value is 403 
				TaskInTrouble				value is 404 
				TaskEstimateChanged			value is 405 
				TaskPendingApproval			value is 406
				TaskLateStart           	value is 407		
				TaskApproved				value is 408	
				TaskReassigned				value is 409
				TaskAssigned				value is 410
				TaskVoided					value is 411
				TaskApprovalRejected		value is 412	
				TaskApprovalReturned   		value is 413	
				ManualApproval				value is 414
				ManaulApprovalReject		value is 415			
				ManualApprovalReturn		value is 416
				TaskReopened				value is 417
				UpdatedDueDate				value is 418
				PeriodOpened				value is 419
				PeriodClosed				value is 420
				PeriodReopened				value is 421
				PeriodResetToScheduled 		value is 422
				WorkHasBeenScheduled		value is 423
				TaskReadyAndNeedsAssignment	value is 424
				OverDue						value is 425		
		WhenItChanged						is TimeStamp
		Description							is Alpha up to 60
		Published							is Boolean	
								
	Field Rules


		HowItChanged
			required
		WhenItChanged
			initial value is current timestamp
			force default to current timestamp
		Published
			initial value is false
				
	Conditions
		PeriodSubscription					
			restricted			
			when (ThisChanged.BusinessClassName = "FinancePeriod")
		CloseTaskSubscription					
			restricted			
			when (ThisChanged.BusinessClassName = "CloseTask")		

	Relations
		SubscribedUsersRel			
			one-to-many relation to FinanceResourceSubscription
			Field Mapping uses BySubscription
				related.HROrganization = FinanceGroup.HROrganization
				related.Subscription = ThisChanged
		FollowingUsersRel
			one-to-many relation to FinanceResourceSubscription
			Field Mapping uses ByResource
				related.HROrganization = FinanceGroup.HROrganization
				related.FollowResource = UpdatedByResource

	Sets
		ByPublished
			Sort Order
				FinanceGroup
				Published
				SubscriptionUpdates
		
		ByWhenItChanged
			Sort Order			
				FinanceGroup
				WhenItChanged
				ThisChanged.BusinessClassName 
				ThisChanged.BusinessObjectKey 
				UpdatedByResource
				SubscriptionUpdates
		
		ByThisChanged
			Sort Order
				FinanceGroup
				ThisChanged
				SubscriptionUpdates
				
		ByRecentUpdatedByResource
			Sort Order
				FinanceGroup
				UpdatedByResource
				WhenItChanged
						
	Rule Blocks
				
	Actions
		Create is a Create Action
			restricted
		Update is an Update Action
			restricted
		Delete is a Delete Action
			restricted
		Purge is a Purge Action
			restricted

		Publish is an Instance Action			
			restricted
			Action Rules
				if (SubscribedUsersRel exist)
					invoke NotifyUser SubscribedUsersRel
						invoked.Subject = Label
						invoked.Body = Description
					Published = true
				else
					if (FollowingUsersRel exists)
						invoke NotifyUser FollowingUsersRel
							invoked.Subject = Label
							invoked.Body = Description
						Published = true
					else	
						invoke Purge
				
