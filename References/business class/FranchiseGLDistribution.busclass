FranchiseGLDistribution is a BusinessClass
    owned by fr
    prefix is FDS
    classic name is FRDIST
    default label is "FranchiseDistribution"

    Ontology
        symbolic key is FranchiseGLDistribution
            classic set name is FDSNEWPRIMARYINDEX






    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        BillingCompany
	    FranchiseContract
        GeneralLedgerDate   	is Date
        	default label is "GlobalLedgerDate"
        Sequence3
        DistSeq             	is a Sequence3
		DistributionAccount		is a FinanceCodeBlock
        GltObjID                is an ObjId
        Customer
        CustomerShipTo
            classic name is SHIP-TO
        FranchiseStandardCharge
            classic name for FranchiseStandardCharge.Currency is CURRENCY-CODE
        Type					is AlphaUpper 2
        	States
        		Accrual				value is "A"
        		Prepayment			value is "P"
        TransactionAmount       is an InternationalAmount
            classic name is TRAN-AMOUNT
        BaseAmount              is an InternationalAmount
        Project
            classic name is ACTIVITY
        AtnObjID                is an ObjId
        CompanyCustomer
	
	Derived Fields


		CustomerAndName is a StringField
			type is Alpha 130 
			restricted
			Customer
			", "
			Customer.Name


    Conditions

        IsProject
        	restricted
            when (AtnObjID entered)

	Relations
		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 
				related.OriginatingTransaction = reference to this instance   
		
    Sets

        Set2
            indexed
            Instance Selection
                where (IsProject)
            Sort Order
                AtnObjID
                GltObjID

		ByCompanyContract
			indexed
			Sort Order
				BillingCompany
				FranchiseContract
				GeneralLedgerDate
        		Sequence3
        		DistSeq  	
        		FranchiseGLDistribution			   	

	Rule Blocks
		CreateGLTransactionDetail
			invoke Create GLTransactionDetail
				fill in fields from this instance
				invoked.JournalizeGroup						= PrmJournalizeGroup	
				invoked.OriginatingTransaction 				= reference to this instance
				invoked.FinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup 
				invoked.System								= "FR"	
				invoked.Reference							= "FranchiseGLDistribution"	
				invoked.AccountingEntity					= PrmAccountingEntity		
				invoked.TransactionAmount					= PrmTransactionAmount 		 
				invoked.CurrencyCode						= PrmCurrencyCode			
				invoked.TransactionDate						= PrmTransactionDate    	
				invoked.PostingDate							= PrmPostingDate        	
				invoked.FinanceCodeBlock					= PrmDistributionAccount		 
				invoked.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount            	= PrmReportCurrencyAmount          	
				invoked.ReportCurrencyAmount.KeepRateOnly 	= true
				invoked.ControlDocumentNumber 				= PrmControlDocumentNumber   
				invoked.DocumentNumber		  				= PrmDocumentNumber			
	        	invoked.GeneralLedgerEvent 					= "AD"
				invoked.Description			  				= "Description"		
				invoked.AutoReverse 						= false		

		ReleaseGLTransactionDetail				 
			if (GLTransactionDetailRel exist)
				invoke Release GLTransactionDetailRel

    Actions
		CreateFromBatch is a Create Action
			restricted
			Parameters
				PrmJournalizeGroup      	is a JournalizeGroup
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmAccountingEntity			is a AccountingEntity
				PrmTransactionAmount		is an InternationalAmount
				PrmReportCurrencyAmount		is an InternationalAmount
				PrmDistributionAccount		is a FinanceCodeBlock
				PrmTransactionDate			is Date
				PrmPostingDate				is Date
				PrmCurrencyCode				is AlphaUpper size 5
				PrmDocumentNumber			is AlphaUpper size 8
				PrmControlDocumentNumber	is AlphaUpper size 8
				
        	Action Rules       		

		   		include CreateGLTransactionDetail
				include ReleaseGLTransactionDetail	
						   		
		JournalizeDistributionsForBatch is an Instance Action 
			restricted
			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFranchiseCompany	is a BillingCompany
					default label is "FranchiseCompany"
				PrmPostThruDate			is Date
					default label is "PostThruDate"
				PrmJournalizeGroup       is like JournalizeGroup
			Action Rules
				invoke JournalizeDistributions FranchiseGLDistribution
					invoked.PrmEnterpriseGroup	  = PrmEnterpriseGroup
					invoked.PrmFranchiseCompany   = PrmFranchiseCompany
					invoked.PrmPostThruDate		  = PrmPostThruDate
					invoked.PrmJournalizeGroup	  = PrmJournalizeGroup
					
		JournalizeDistributions is a Set Action		
			restricted
			completion message is "FranchiseDistributionPostingCompleteForJournalizeGroup<MyJournalizeGroup>"
			run in background

			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFranchiseCompany	is a BillingCompany
					default label is "FranchiseCompany"
				PrmPostThruDate			is Date
					default label is "PostThruDate"
				PrmJournalizeGroup       is like JournalizeGroup
			Parameter Rules
				PrmEnterpriseGroup
					initial value is BillingCompany.CustomerBusinessGroup.FinanceEnterpriseGroup
					required
				PrmFranchiseCompany 	 
					required
				PrmPostThruDate		 


			Local Fields	
    			LocalSystemCode            	is a GeneralLedgerSystemCode
    				context of PrmEnterpriseGroup
    			MyJournalizeGroup 			is like JournalizeGroup

			Instance Selection
				where (BillingCompany = PrmFranchiseCompany
				and    GLTransactionDetailRel.JournalizeGroup = PrmJournalizeGroup
				and    FranchiseGLDistribution.GeneralLedgerDate <= PrmPostThruDate)

			Sort Order  
				BillingCompany
				FranchiseGLDistribution.GeneralLedgerDate 

			Action Rules	
				Set Rules
					Entrance Rules
    					LocalSystemCode = "FR"
						if (PrmJournalizeGroup entered)
							MyJournalizeGroup = PrmJournalizeGroup
						else
							increment LocalSystemCode.LastJournalizeGroup
							MyJournalizeGroup = LocalSystemCode.DerivedJournalizeGroup

					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmEnterpriseGroup in background
							invoked.PrmJournalizeGroup = MyJournalizeGroup
							invoked.PrmInterfaceInDetail		= true			

				Instance Rules
					display "FranchiseGLDistribution=<FranchiseGLDistribution>"
					display "MyJournalizeGroup=<MyJournalizeGroup>"
					if (GLTransactionDetailRel exist)
						invoke UpdateJournalizeGroup GLTransactionDetailRel	 
							invoked.PrmJournalizeGroup = MyJournalizeGroup
