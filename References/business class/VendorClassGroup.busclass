VendorClassGroup is a BusinessClass
	owned by ap
    prefix is VCG1
	
	Ontology
		symbolic key is VendorClassGroup
		
    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Description
	
	Derived Fields

		ActorSecurityGroup is a DerivedField
			type is like VendorClassSecurityGroup
			return actor.context.VendorClassSecurityGroup

		CreateMessage is a LabelField
			restricted
			"CreateVendorClassGroup"
		
		FormLabel is a LabelField
			restricted
			"VendorClassGroup:_"

		RepresentativeText is a StringField
			type is Text
			default label is "VendorClassGroup"
			VendorClassGroup " - " Description
		
		DerivedVendorClassGroupTitle	is a DerivedField
			type is Alpha size up to 132
			default label is "VendorClassGroup"
			return RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return FormLabel + DerivedVendorClassGroupTitle
			else
				return CreateMessage

	Field Rules
	
		Description
			required

    Relations

        VendorClassGroupDetailsRel
            one-to-many relation to VendorClassGroupDetail
            delete cascades
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup	
                related.VendorClassGroup			= VendorClassGroup

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = actor.context.FinanceEnterpriseGroup
				related.DimensionGroupType			  = 16
				related.FinanceDimensionStructure	  = VendorClassGroup	

	Conditions
		RecordExists
			restricted
			when (VendorClassGroup exists)

		HasVendorClassGroupDetails
			when (VendorClassGroupDetailsRel exists)
			
		UsedInASecurityGroup
			when (SecurityGroupRel exists)

		SecurityGroupAllowsAccess
			when  ((actor.context.VendorClassSecurityGroup = "")
			or     (actor.context.VendorClassSecurityGroup != ""
			and     actor.context.VendorClassSecurityGroup.FinanceDimensionStructure = VendorClassGroup))

	Actions
		Create is a Create Action
			Action Rules

		Update is an Update Action
			Action Rules

		Delete is a Delete Action
			Entrance Rules
				for each VendorClassGroupDetailsRel
					invoke Delete each
			
			Action Rules
			
				constraint (!UsedInASecurityGroup)
					"CannotDelete;VendorClassGroupIsUsedInASecurityGroup"
			
		CopyMembers is an Instance Action
			Parameters
				CopyToGroup is an VendorClassGroup
				
			Parameter Rules
			
				CopyToGroup
					constraint (CopyToGroup != VendorClassGroup)
						"CannotCopyAGroupToItself"
						
			Action Rules
				for each VendorClassGroupDetail set
					invoke Create VendorClassGroupDetail
						resume on error
						invoked.FinanceEnterpriseGroup = each.FinanceEnterpriseGroup
						invoked.VendorGroup            = each.VendorGroup
						invoked.VendorClass            = each.VendorClass
						invoked.VendorClassGroup       = CopyToGroup
					commit transaction				
