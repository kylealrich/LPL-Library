FinanceDimension9Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD9UP   
    
    Ontology
    	symbolic key is FinanceDimension9Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension9			is like FinanceDimension9
    	FinanceDimension9				is like FinanceDimension9
		SequenceDimension9				is like FinanceDimension9
		ParentSequenceDimension9		is like FinanceDimension9
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension9Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension9 is a DerivedField
			type is like FinanceDimension9
			restricted
			if (ParentFinanceDimension9 entered)
				return DerivedParentFinanceDimension9String
			else
				return TopNodeString

		DerivedSequenceParentDimension9 is a DerivedField
			type is like FinanceDimension9
			restricted
			if (ParentSequenceDimension9 entered)
				return ParentSequenceDimension9
			else
				return DerivedParentFinanceDimension9 
		
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension9Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension9String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension9Structure.StructureSequence
			"_"
			ParentFinanceDimension9
		
		DerivedSummaryFinanceDimension9 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension9Structure.StructureSequence
			"_"
			FinanceDimension9

		DerivedSummarySequenceDimension9 is a DerivedField
			type is like FinanceDimension9
			restricted
			if (SequenceDimension9 entered)
				return SequenceDimension9
			else 
				return DerivedSummaryFinanceDimension9

		
	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction	
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
 
 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension9Structure
				FinanceDimension9
				FinanceDimension9Upload.RunGroup
				FinanceDimension9Upload.Sequence
 	
	Relations
		FinanceDimension9HierarchyRel
			one-to-one relation to FinanceDimension9Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension9Structure = FinanceDimension9Structure
				related.FinanceDimension9		   = FinanceDimension9
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension9Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension9Structure = FinanceDimension9Structure
			Instance Selection
				where (related.FinanceDimension9Upload.RunGroup = FinanceDimension9Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 14
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 14
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension9
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"
							
    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension9Upload.Sequence entered)
					FinanceDimension9Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension9Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action

		Purge is a Purge Action
			restricted
			
		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension9Structure		is a FinanceDimension9Structure
					default label is "FinanceDimension9Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension9Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension9	is a FinanceDimension9
				DynamicRulesExist		is Boolean		
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension9Structure	    = PrmFinanceDimension9Structure
				and    FinanceDimension9Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension9Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension9Structure
				FinanceDimension9Upload.RunGroup
				FinanceDimension9Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
										
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension9 = FinanceDimension9
					else
						LocalFinanceDimension9 = DerivedSummarySequenceDimension9
					if (LocalFinanceDimension9 exists)
						if  (RecordType.Posting
						and !FinanceDimension9HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension9
								resume on error
									LocalErrorMessage		= error message
								invoked.PrmStructure		= FinanceDimension9Structure					
								invoked.PrmParentDimension  = DerivedSequenceParentDimension9
						else					
							if (DerivedSequenceParentDimension9 != LocalFinanceDimension9.DerivedParentDimension)
								invoke Move LocalFinanceDimension9
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension9
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension9
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension9 
										except invoked.ParentFinanceDimension9
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension9
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension9
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension9
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension9 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension9
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension9Structure		is a FinanceDimension9Structure
					default label is "FinanceDimension9Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension9Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension9Structure	    = PrmFinanceDimension9Structure
				and    FinanceDimension9Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension9Structure   is a FinanceDimension9Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension9Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension9Structure	    = PrmFinanceDimension9Structure
				and    FinanceDimension9Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
