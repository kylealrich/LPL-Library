PurchaseOrderAcknowledgementResult is a BusinessClass
	owned by po
	
	prefix is POAR
	sql name is POAcknowledgementRes
	
	Ontology
		symbolic key is PurchaseOrderAcknowledgementResult
	
	Patterns
		implements StaticJava
        disable AuditIndex
       	disable EffectiveDated
	
	Persistent Fields
		RunGroup				
		RunTime					is TimeStamp
		Company					is like PurchasingCompany
		Vendor                  is like Vendor
		EnableGLNProcessing     is Boolean
		
	Local Fields
		LocalRunGroup           is a RunGroup

	Derived Fields
	
		GLNProcessingText 			is a DerivedField
			type is Alpha 3
			if (EnableGLNProcessing = true)
				GLNProcessingText = "Yes"
			else
				GLNProcessingText = "No"
	
		NoRecordsFound 				is a MessageField
			"NoRecordsFound"
			
		TotalRecords 				is a DerivedField
			type is Numeric 6
			return instance count of PurchaseOrderRevisionByResultRel
	
	Conditions
	
		NoResultsForRunGroup
			restricted
			when (PurchaseOrderRevisionByResultRel !exists
			and   PurchaseOrderMessageRel !exists)
		
		MessagesExist
			restricted
			when (PurchaseOrderMessageRel exists)
			
		VendorExists
			when (Vendor entered)
			
		CompanyOnly
			when (Company entered
			and   Vendor !entered)
			
		NoVendorNoCompany
			when (Company !entered
			and   Vendor !entered)

		CompanySecurityGroupAllowsAccess
			restricted
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)
			
		SecurityGroupAllowsAccess
			restricted
			when (Company !entered
			or   (Company entered
			and   CompanySecurityGroupAllowsAccess))
	
	Relations
		
		PurchaseOrderRevisionByResultRel
			one-to-many relation to PurchaseOrderRevision
			Field Mapping uses ByAcknowledgementResult
				related.AcknowledgementResult    = PurchaseOrderAcknowledgementResult
				
		PurchaseOrderMessageRel
			one-to-many relation to PurchaseOrderAcknowledgementMessage
			Field Mapping uses ByAcknowledgementResult
				related.AcknowledgementResult    = PurchaseOrderAcknowledgementResult
				
		AcknowledgementsByRunGroupRel
    		one-to-many relation to PurchaseOrderEDIAcknowledgement
    		Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
				
		EmptyAcknowledgementMessageRel
			one-to-many relation to PurchaseOrderAcknowledgementMessage
    		Field Mapping uses ByAcknowledgementResult
				related.AcknowledgementResult = PurchaseOrderAcknowledgementResult
			Instance Selection
				where (related.MessageNumber = 10
				or     related.MessageNumber = 13)
				
		FactSheetRel
			one-to-one relation to SampleDocumentTemplate
			Field Mapping uses symbolic key
				related.SampleDocumentTemplate      = "PurchaseOrderAcknowledgement_ST"
				
		PurchaseOrderCompanyRel
			one-to-one relation to PurchasingCompany
			Field Mapping uses symbolic key
				related.Company             = Company
				
		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup = PurchaseOrderCompanyRel.ProcurementGroup
				related.Vendor = Vendor
		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = Company
	Sets
	
		ByRunTime
			Sort Order
				RunTime descending
				PurchaseOrderAcknowledgementResult
				FinanceEnterpriseGroup
				UniqueID
		
		ByRunGroup
			Sort Order
				RunGroup
				PurchaseOrderAcknowledgementResult
				FinanceEnterpriseGroup
				UniqueID
	
	Field Rules
	
	Actions
	
		Create is an Action
			restricted
			
		Update is an Action
			restricted
			
		Delete is an Action
		
			Entrance Rules
			
				for each EmptyAcknowledgementMessageRel
					invoke Delete each
		
		Purge is a Purge Action
			restricted
			
		ProcessAcknowledgements is an Instance Action
		
			Parameters
                PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany                  is a PurchasingCompany
					default label is "Company"
				PrmVendor                   is a Vendor
					default label is "Vendor"
				EnableGLNProcessing         is Boolean
				SendEmailToBuyers           is Boolean
				
			Parameter Rules
				
				PrmRunGroup
					LocalRunGroup     = PrmRunGroup
					required
						"RunGroupIsRequired"
						
			Action Rules
				invoke ProcessPurchaseOrderEDIAcknowledgement PurchaseOrderEDIAcknowledgement
					invoked.PrmRunGroup      	= PrmRunGroup
					invoked.PrmCompany       	= PrmCompany
					invoked.PrmVendor        	= PrmVendor
					invoked.EnableGLNProcessing = EnableGLNProcessing
					invoked.SendEmailToBuyers   = SendEmailToBuyers
			
	
