BusinessClassCompute is a BusinessClass
    owned by la
    prefix is Bccm

	Ontology
		symbolic key is BusinessClassCompute

	Patterns

		implements EffectiveDated
  

	Persistent Fields
		Description          is Alpha up to 100
        	translatable
		Compute       is LPLText
		Used          is Boolean
		Active        is Boolean
		Delivered 	  is Boolean
		
	Rule Blocks
		DeliveredDevModeCannotBeChanged
			if (Delivered
			and not DeveloperMode) 
				cannot be changed
					"ChangesAreNotAllowedForADeliveredBusinessClassCompute"
        
	Field Rules
    	Active
        	initial value is true
			default to true
			
			include DeliveredDevModeCannotBeChanged
			
		Used
        	default to false        
       	
       	Compute
	    	required
	    	
	    	include DeliveredDevModeCannotBeChanged
	    	
	    Delivered
			initial value is DeveloperMode
			
			if (not DeveloperMode)
				cannot be changed
					"DeliveredFlagCannotBeChangedOutsideOfDeveloperMode"
			else
			if (not Delivered)
				confirmation required
					"DeveloperModeIsEnabledAndBusinessClassComputeIsNotMarkedAsDelivered.IsThisCorrect?"
	    	
	Actions
    	Inactivate is an Instance Action
        	valid when (CanInactivate)  
            Action Rules
            	Active = false
                     
		Activate is an Instance Action
        	valid when (!Active)
        	Entrance Rules
        		if (Delivered and not DeveloperMode)
        			confirmation required
        				"ThisIsADeliveredBusinessClassCompute.OnceActivatedYouWillNotBeAbleToDeactivate.AreYouSureYouWantToActivate?"
        				
            Action Rules
            	Active = true

        Delete is an Action
        	valid when (CanDelete)
                                                
		Create is an Action
			Entrance Rules
				if (Delivered) 
					constraint (DeveloperMode)
						"DeliveredFlagCannotBeChangedOutsideOfDeveloperMode"
			
        		constraint (ParseText = blank)
					"Compute_L\P\LIsInvalid"
		
        Update is an Action
        	valid when (not RestrictUpdate)
        	Entrance Rules
        		constraint (ParseText = blank)
					"Compute_L\P\LIsInvalid"
					



		MarkAsDelivered is an Instance Action
			restricted
			Action Rules
				Delivered = true
	
	Attach Rules
    	constraint (Active)
    		"ThisBusinessClassComputeDefinitionIsInactiveAndCannotBeUsed"   		
    	Used = true       
    	
    Derived Fields
    	ParseText is a NativeField
			type is Text	
			
		DeveloperMode is a NativeField
			type is Boolean
			restricted
			
	Conditions
		RestrictUpdate
			default label is "UpdateIsRestricted"
			restricted
			when (Delivered and not DeveloperMode)	 
		
		CanInactivate
			restricted
			when (Active and not RestrictUpdate)
		
		CanDelete
			restricted
			when ((not Used) and not RestrictUpdate)
