OrderEntryStandardOrder is a BusinessClass
    owned by oe
    prefix is OSO
    classic name is OESTDORDER

    Ontology
        symbolic key is OrderEntryStandardOrder
            classic set name is OSOSET1
            classic name is STD-ORDER-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        BegDate     is Date
            default label is "BeginningDate"
        EndDate     is Date
            default label is "EndingDate"
        LastLineNbr is a LineNumber
        TopComms    is a TopCommX3InOestdorder
            classic name is TOP-COMM
            default label is "TopComments"
        BottomComms is a BottomCommX3InOestdorder
            classic name is BOTTOM-COMM
            default label is "BottomComments"
      
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforSalesOrder"   
			
		ContextMessageText is a MessageField
			restricted
			"OrderEntryStandardOrder<OrderEntryStandardOrder>ForCompany<Company>AndCustomer<Customer>"  

        RepresentativeText is a StringField
            type is Text
			default label is "Vendor"
            OrderEntryStandardOrder " - " Description

    Conditions

        IsCustSpecific
        	restricted
            when (Customer entered)

        IsShared
        	restricted
            when (Customer not entered)

        RecordExists
            when (OrderEntryStandardOrder exists)


    Relations

        StandardOrderLinesRel
            one-to-many relation to OrderEntryStandardOrderLine
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.Customer                = Customer
                related.OrderEntryStandardOrder = OrderEntryStandardOrder

		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company		= Company
				related.Customer	= Customer

    Field Rules

        Description
            required

	Actions
		Create is a Create Action
			Entrance Rules
				if (Customer entered)
					constraint (CompanyCustomerRel exists)
						"CompanyCustomerDoesNotExist"
		
		Update is an Update Action
		
		Delete is a Delete Action
