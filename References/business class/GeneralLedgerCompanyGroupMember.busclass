GeneralLedgerCompanyGroupMember is a BusinessClass
	default label is "GlobalLedgerCompanyGroupMember"
    owned by glif
    prefix is GCC
    sql name is GLedgerCompanyGroupMember
    classic name is GLCGCPY

    Ontology
        symbolic key is GeneralLedgerCompanyGroupMember
			classic set name is GCCSET3
			classic name for GeneralLedgerCompanyGroup is COMPANY-GROUP

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Sequence is Numeric size 4
            classic name is SEQ-NBR

	Relations
		OtherCompaniesInGroupRel
			one-to-many relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup = GeneralLedgerCompanyGroup
			Instance Selection
				where (related.Company != Company)

		AllForThisMemberRel
			one-to-many relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses Set1
				related.Company  = Company
				
    Sets

        Set2
            indexed
            Sort Order
                GeneralLedgerCompanyGroup
                Sequence
                Company

        Set1
            indexed
            Sort Order
                Company
                GeneralLedgerCompanyGroup

	Actions
		Create is a Create Action
			Field Rules
				Sequence
					increment GeneralLedgerCompanyGroup.LastSequence
					Sequence = GeneralLedgerCompanyGroup.LastSequence
			Action Rules
				if (OtherCompaniesInGroupRel exists)
					constraint (Company.FinanceEnterpriseGroup = first OtherCompaniesInGroupRel.Company.FinanceEnterpriseGroup)
						"CanOnlyIncludeCompaniesAssociatedWith_Finance_Enterprise_Group<first OtherCompaniesInGroupRel.Company.FinanceEnterpriseGroup>"
				
		Update is an Update Action

		Delete is a Delete Action

        CreateNewMemberWhereThisMemberExists is an Instance Action
        	Parameters
        		NewMember is a GeneralLedgerCompany
        		
        	Parameter Rules
        		NewMember
        			required
        			constraint (NewMember != Company)
        				"NewMemberCannotBeTheSameAsThisMember"
        				
        	Action Rules
        		for each AllForThisMemberRel
					invoke Create GeneralLedgerCompanyGroupMember
						resume on error
						invoked.GeneralLedgerCompanyGroup	= each.GeneralLedgerCompanyGroup
						invoked.Company				       	= NewMember
					commit transaction        			        		
		
