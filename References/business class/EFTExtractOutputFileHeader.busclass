EFTExtractOutputFileHeader is a BusinessClass
    owned by cb
    prefix is EOFH

    Ontology
        symbolic key is EFTExtractOutputFileHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
    	FileName					is AlphaUpper 50
		CreateDate					is TimeStamp
    	CashCode
    	BankTransactionCode
    	HeaderFile					is an Attachment
    	DetailFile					is an Attachment
    	FlatFile					is an Attachment
    	ProcessedByThirdParty		is Boolean
    	TotalNumberOfRecords 		is Numeric 10	
    	TotalPaymentAmount 			is an InternationalAmount	

	Transient Fields
		TransientFromCurrency		is a FromCurrency
			derive value from CashManagementAccount.Currency

    Local Fields
		    
    Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 10	
			(instance count of EFTExtractOutputFileDetail set)

		TotalAmount is a DerivedField
			type is like InternationalAmount
			return (sum EFTExtractOutputFileDetail set.ReceiptAmount)

        CsvOutput is a StringField
            type is Alpha 2048
            restricted
			TotalRecordCount
			","
			TotalAmount
			","
			CreateDate
			","
			BankTransactionCode

        HeaderOutputFileName is a StringField
            type is Alpha 100
			FileName
			"-"
			CashCode
			"ExtractHeader"

        DetailOutputFileName is a StringField
            type is Alpha 100
			FileName
			"-"
			CashCode
			"ExtractDetail"

        FlatFileOutputFileName is a StringField
            type is Alpha 100
			FileName
			"-"
			CashCode
			"FlatFile"

    Conditions
		HasHeaderAttachment
			restricted
			when (HeaderFile entered)
		HasDetailAttachment
			restricted
			when (DetailFile entered)
		HasFlatFileAttachment
			restricted
			when (FlatFile entered)

    Relations


    Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			CashManagementAccount
    			CashCode
    			FileName
    			EFTExtractOutputFileHeader

    	ByProcessedByThirdParty
    		Sort Order
    			ProcessedByThirdParty				descending
    			CashManagementGroup
    			CashManagementAccount
    			EFTExtractOutputFileHeader


    Field Rules
        CreateDate
            default to current timestamp 

		FileName
			default to BankTransactionCode            


	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

		CreateExtractFiles is an Instance Action
			Action Rules
				trigger "CBExtractFiles" PA service
					resume on error
					title is "CreateExtractFiles"
					Variables
						CashManagementGroup
						CashManagementAccount
						EFTExtractOutputFileHeader

		EFTExtractOutputFileHeaderPurge is a Set Action
			Parameters
				PrmCashManagementGroup   is a CashManagementGroup
				PrmCashManagementAccount is a CashManagementAccount
				PrmCashCode              is a CashCode
				PrmCreateDateRange       is a DateRange
			Parameter Rules
				PrmCashManagementAccount
					if (PrmCashManagementAccount entered)
						constraint (PrmCashCode not entered)
							"CannotEnterBothCashCodeAndCashManagementAccount"
				PrmCreateDateRange
					required
			Instance Selection
				where (CreateDate date within PrmCreateDateRange
				and  ((PrmCashCode entered
				and    CashCode = PrmCashCode)
				or     PrmCashCode not entered)
				and  ((PrmCashManagementAccount entered
				and    CashManagementAccount = PrmCashManagementAccount)
				or     PrmCashManagementAccount not entered))
			Action Rules
				Instance Rules
					invoke Purge EFTExtractOutputFileDetail set
					invoke Purge

		Purge is a Purge Action
			restricted
