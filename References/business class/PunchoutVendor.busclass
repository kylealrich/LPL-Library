PunchoutVendor is a BusinessClass
	owned by edi
	prefix is PVen
	
	Ontology
        symbolic key is PunchoutVendor
	
	Patterns
        disable AuditIndex
	
	Persistent Fields
		PunchoutVendorName				is a Name	 
			holds pii
			classic name is TLNAME
		PurchaseFromLocation			is a VendorLocation
			classic name is PURCH-FR-LOC
		URL
		FromDunsNumber					is a DunsNumberAlpha
		FromNetworkID					is a NetworkID
		ToNetworkID						is a NetworkID
		ToDunsNumber					is a DunsNumberAlpha
		SenderDunsNumber				is a DunsNumberAlpha
		SenderNetworkID					is a NetworkID
		DeploymentMode
		SharedSecret
		Punchin							is Boolean
		EnhancedPunchoutSetupRequest	is Boolean
		PunchoutVendorDescription		is a Description3
		PunchoutVendorCategory
		
	Field Rules
		DeploymentMode
			default to "test" 	
			initial value is "test" 

	Relations
		RequesterRel
			one-to-many relation to Requester
			Field Mapping uses symbolic key
				related.HROrganization = VendorGroup.BusinessGroup.FinanceEnterpriseGroup.HROrganization
		
		RequestersForPunchoutVendorRel
			one-to-many relation to RequesterPunchoutVendor
			Field Mapping uses ByVendor
			Instance Selection
				where (related.VendorGroup		= VendorGroup
				and	   related.Vendor			= Vendor
				and	   related.PunchoutVendor	= PunchoutVendor)

		BuyingOrganizationRel
			one-to-one relation to BuyingOrganization
			Field Mapping uses symbolic key
				related.BuyingOrganization	= VendorGroup
			
	Actions
		Create is a Create Action
			Entrance Rules
				constraint(BuyingOrganizationRel exists)
					"BuyingOrganization<VendorGroup>DoesNotExist"

		Update is an Update Action

		Delete is a Delete Action
			Action Rules
				invoke Delete RequestersForPunchoutVendorRel
					
	Derived Fields
		DerivedPunchoutVendorLogo is a DerivedField
			type is BinaryDocument
			if (PurchaseFromLocation entered
			and PurchaseFromLocation.Logo entered)
				return PurchaseFromLocation.Logo.File
			else
				return Vendor.Logo.File
