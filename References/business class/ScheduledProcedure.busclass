ScheduledProcedure is a BusinessClass
    owned by ic
    prefix is SP2

    Ontology
        symbolic key is ScheduledProcedure

    Patterns

        implements StaticJava
        implements DynamicCreation
        disable AuditIndex

    Persistent Fields
        ItemGroup
        ProcedureDate           is Date             
        ProcedureTime           is Time             
        OriginalProcedureDate   is Date
            protected
        OriginalProcedureTime   is Time
            protected
        Case                    is Alpha size 10
        PatientID               is a Patient        
            restricted
            protected
        RequestingLocation
        Provider                                    
            restricted
            protected
        PreferenceCard
        FromCompanyLocation
        Room
        FromCompanyParLocationBin
        Status                  is Numeric size 1
            protected
            States
                Open                value is 0
                FullyReserved       value is 1
                InProcess           value is 2
                Closed              value is 3
                Rejected            value is 4
                Canceled            value is 5
                WithError           value is 6
        RQCreationErrorMessage      is Text
            restricted
            protected
        ErrorType                   is Numeric size 1
            protected
            States
                NoError                value is 0
                CreateRequisition      value is 1
                ReleaseRequisition     value is 2
        ErrorMessage                is Alpha size 200
            protected
        Requisition             is like Requisition
        PatientProcedure        is like PatientProcedure

    Transient Fields

		TransientCaseNumber				is Alpha size 10    
			derive value from DerivedCase
		TransientPatientID				is a Patient
			derive value from DerivedPatientID
    	TransientPatientLastName        is a LastName
	   		derive value from DerivedPatientLastName
		TransientPatientFirstName       is a FirstName	 
			holds pii
			derive value from DerivedPatientFirstName
		TransientPatientMiddleName      is a MiddleName
			derive value from DerivedPatientMiddleName
		TransientProvider				is a Provider
			derive value from DerivedProvider
        TransientUpdatePatientProcedure is AlphaUpper size 1
            derive value from DerivedPatientProcedureUpdate
        TransientPickDate           is TimeStamp
            derive value from CurrentTimestamp

    Derived Fields
        ContextMessageEntityType is a MessageField
            restricted
            "Scheduled_Procedure"

		ContextMessageText is a MessageField
			restricted
			"Scheduled_Procedure<ScheduledProcedure>"

        DaysPastProcedureDateMessageText is a MessageField
            restricted
            "EnterTheNumberOfDaysToDetermineScheduledProceduresToBeCanceledFromCurrentDate."

		CurrentTimestamp is a DerivedField
			type is TimeStamp
			restricted
			return current timestamp

        DerivedPatientName is a DerivedField
            type is MessageField
            if (PatientID exists)
                return PatientID.DisplayPatientFullName

        DerivedProcedureDateAndTime is a DerivedField
            type is TimeStamp
            return ProcedureDate + " " + ProcedureTime

        DerivedProcedureDemandForecast is a ComputeField
            type is Numeric 1
            (ProcedureDate - current corporate date)

        DerivedProviderName is a DerivedField
            type is MessageField
            if (ItemGroup entered 
            and Provider entered)
                return Provider.DisplayProviderFullName

		DerivedScheduledProcedureTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return ContextMessageText
			else
				return ContextMessageEntityType

        DerivedRequisition is a DerivedField
			type is like Requisition
			return first ScheduledProcedureLinesRel.Requisition

        DerivedPatientProcedureUpdate is a DerivedField
            type is AlphaUpper size 1
            if (action type.Update)
                return blank
            else
                return "Y"

        DerivedProcedure is a DerivedField
            type is like Procedure
            restricted
            if (action type.Update)
                return blank
            else
                return PatientProcedureRel.Procedure

		DerivedCase is a DerivedField       
			type is like CaseNumber
            restricted
            if (action type.Update)
                return blank
            else
                return Case 

		DerivedPatientID is a DerivedField
			type is AlphaUpper size 20
            if (action type.Update)
                return blank
            else
                return PatientProcedureRel.PatientID

		DerivedPatientLastName	is a DerivedField 
			type is like LastName
			holds pii
			restricted
            if (action type.Update)
                return blank
            else
                return PatientProcedureRel.PatientLastName

		DerivedPatientFirstName	is a DerivedField 
			type is like FirstName	 
			holds pii
			restricted
            if (action type.Update)
                return blank
            else
                return PatientProcedureRel.PatientFirstName
                
		DerivedPatientMiddleName is a DerivedField 
			type is like MiddleName
			holds pii
            restricted
            if (action type.Update)
                return blank
            else
                return PatientProcedureRel.PatientMiddleName

		DerivedProvider is a DerivedField 
			type is like PhysName
			holds pii
            if (action type.Update)
                return blank
            else
                return PatientProcedureRel.PhysicianName

		DerivedPatientPrint is a DerivedField
			type is MessageField
			holds pii
            if (PatientProcedureRel.HasPatientName)
                if (InventoryCompanyRel.PrintPatientNameOnPickList)
                    return DerivedPatientFullName
                else
                    return "***************"

        DerivedPatientFullName is a DerivedField
            type is like Name
			holds pii
            restricted
            return PatientProcedureRel.DerivedPatientFullName

        DerivedProviderFullName is a DerivedField
            type is Text
            restricted
            if (ProviderRel.Name entered)
                return ProviderRel.DisplayProviderFullName

        DerivedCurrentCorporateTimeInSeconds is a DerivedField
            type is Numeric 5
            restricted
            return current corporate time

        DerivedProcedureTimeInSeconds is a DerivedField
            type is Numeric 5
            restricted
            return ProcedureTime

        DerivedProcedureDateAndTimeGreaterThanOrEqual24Hours is a ConditionalField
            type is Boolean
            restricted
            if (DerivedProcedureDemandForecast > 1                                      
            or (DerivedProcedureDemandForecast = 1                                      
            and DerivedCurrentCorporateTimeInSeconds <= DerivedProcedureTimeInSeconds)) 
                true
            else
                false

        RequisitionIsUnreleasedMessage is a MessageField
            "RequisitionIsUnreleased"

    Local Fields
        LocalPatientProcedureView       is a PatientProcedure view
        LocalProcedureDemandForecast    is Numeric size 1
        LocalRequisition                is a Requisition

    Context Fields

    Relations
        CanceledScheduledProcedureLinesRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.Status.Canceled)
        
        ComponentItemRel
            one-to-many relation to ComponentItem
            Field Mapping uses symbolic key
                related.ItemGroup           = PreferenceCard.ItemGroup
                related.PreferenceCard      = PreferenceCard

		InventoryCompanyRel
			one-to-one relation to InventoryCompany
			Field Mapping uses symbolic key
				related.Company                                 = Company

		PatientProcedureRel
			one-to-one relation to PatientProcedure
			Field Mapping uses symbolic key
				related.Company             = Company
				related.PatientProcedure	= PatientProcedure

        ProviderRel
            one-to-one relation to Provider
            Field Mapping uses symbolic key
                related.ItemGroup           = ItemGroup
                related.Provider            = PatientProcedureRel.PhysicianName

        RequestedRequisitionLineRel
            one-to-many relation to RequisitionLine
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.Requisition             = Requisition
            Instance Selection 
				where (related.ScheduledProcedure = ScheduledProcedure)	

        ScheduledProcedureLinesRel is a ScheduledProcedureLine set

        ScheduledProcedureLinesFullyPickedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsNonStock
                and    related.IsFullyPicked)

        ScheduledProcedureLinesInventoriedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsInventoried)

        ScheduledProcedureLinesNonInventoriedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsNonInventoried)

        ScheduledProcedureLinesNonStockRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsNonStock)

        ScheduledProcedureLinesNotFullyPickedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsNonStock
                and    not related.IsFullyPicked
                and    not related.IsCanceled)

        ScheduledProcedureLinesOpenAndFullRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsOpenOrFullyReserved)

        ScheduledProcedureLinesClosedOrProcessedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsClosedOrProcessed)

        ScheduledProcedureLinesPickedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsNonStock
                and    related.IsPicked)

        ScheduledProcedureLineForRequisitionRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsOpenOrFullyReserved
                and    related.IsInventoried)
                
        ScheduledProcedureLinesWithErrorRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.HasParLocationError
                or     related.HasParLocationGroupError)

        ScheduledProcedureLinesOpenNonStockRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsOpen
                and    related.IsNonStock)

        ScheduledProcedureLinesOpenRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsOpen)

        ScheduledProcedureLineWithReservedRel is a ScheduledProcedureLine set
			Instance Selection
				where (related.CanUndoReservation)

        ScheduledProcedureLineWithRemainingRel is a ScheduledProcedureLine set
			Instance Selection
				where (related.CanReserveRemaining)

        ScheduledProcedureLinesExceptRejectRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.Status.Open
                or     related.Status.FullyReserved
                or     related.Status.Requested
                or     related.Status.Closed            
                or     related.Status.Canceled           
                or     related.Status.WithError)

        ScheduledProcedureLinesFullyReservedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.IsFullyReserved)

        ScheduledProcedureLinesRejectRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.Status.Rejected)

        ScheduledProcedureLinesRequestedRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.Status.Requested)

        ScheduledProcedureLinesWithPurchaseOrderOrShipmentRel is a ScheduledProcedureLine set
            Instance Selection
                where (related.RequisitionLinePurchaseOrderOrShipmentLineExists)

        ScheduledProcedureLineDetailsRel is a ScheduledProcedureLineDetail set

        ScheduledProcedureLineDetailsUniqueParLocationRel is a ScheduledProcedureLineDetail set
            Instance Selection
                where (related.UniqueParLocation)

        ScheduledProcedureRequisitionLineRel 		
			one-to-many relation to RequisitionLine 
			Field Mapping uses symbolic key 
				related.Company					= Company
				related.Requisition             = LocalRequisition
			Instance Selection 
				where (related.ScheduledProcedure = ScheduledProcedure)	

        ScheduledProcedureRequisitionRel 		
			one-to-one relation to Requisition 
			Field Mapping uses symbolic key 
				related.Company					= Company
				related.Requisition             = Requisition

        UnreleasedRequisitionLinesRel 		
			one-to-many relation to RequisitionLine 
			Field Mapping uses symbolic key 
				related.Company					= Company
				related.Requisition             = Requisition
            Instance Selection
                where (related.IsUnreleased)

        UnreleasedRequisitionRel
            one-to-many relation to Requisition
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.Requisition             = Requisition
            Instance Selection
                where (related.Status.Unreleased)

    Conditions
        CanFinishAll
            restricted
            when (IsInProcess
            and   HasNonStockLines
            and   ScheduledProcedureLinesOpenNonStockRel exists
            and   ScheduledProcedureLinesNotFullyPickedRel not exists
            and   ScheduledProcedureLinesWithErrorRel not exists)

        CanPickAll
            restricted
            when (IsInProcess
            and   HasNonStockLines
            and   ScheduledProcedureLinesNotFullyPickedRel exists)

        CanReserveRemaining
            restricted
            when (IsInProcess
            and   ScheduledProcedureLineWithRemainingRel exists)

        CanUndoReservation
            restricted
            when (IsInProcess
            and   ScheduledProcedureLineWithReservedRel exists)

        HasNonStockLines
            restricted
            when (ScheduledProcedureLinesNonStockRel exists)

        HasInventoriedLines
            restricted
            when (ScheduledProcedureLinesInventoriedRel exists)

		HasPatientProcedureRecord
            restricted
			when (PatientProcedure entered)

		HasPatientProcedureInfo
			restricted
			when (Case 			                entered
			or 	  TransientPatientID 		    entered
			or 	  TransientProvider 	        entered
            or 	  TransientPatientLastName 		entered
			or 	  TransientPatientFirstName 	entered
			or 	  TransientPatientMiddleName 	entered)

        IsFullyReserved
            restricted
            when (Status.FullyReserved)

        IsInProcess
            restricted
            when (IsOpenOrFullyReserved
            or    IsRequested)
        IsOpen
            restricted
            when (Status.Open)

        IsOpenOrFullyReserved
            restricted
            when (Status.Open
            or    Status.FullyReserved)

        IsRequested
            restricted
            when (Status.InProcess 
            and   WithRequisition)

		RecordExists
			restricted
			when (ScheduledProcedure exists)

        RequestedWithUnreleasedRequisition
            restricted
            when (IsRequested
            and   UnreleasedRequisitionRel exists)

        WithErrorInRequisitionCreation  
            restricted
            when (RQCreationErrorMessage entered)

        WithErrorInMassRequisition
            restricted
            when (not ErrorType.NoError
            and   ErrorMessage entered)

        WithInProcedureDemandForecast
            restricted
            when (DerivedProcedureDemandForecast >= 0
            and   DerivedProcedureDemandForecast <= LocalProcedureDemandForecast)

        WithRequisition 
            restricted
            when (ScheduledProcedureRequisitionRel exists)

        HasRequestedRequisitionLine
            restricted
            when (RequestedRequisitionLineRel exists)

        RescheduledProcedureDate
            restricted
            when (ProcedureDate != OriginalProcedureDate)

        RescheduledProcedureTime
            restricted
            when (ProcedureTime != OriginalProcedureTime)
        
        RescheduledProcedure
            restricted
            when (RescheduledProcedureDate
            or    RescheduledProcedureTime)

        ScheduledProcedureItemsAreInProcess
            restricted
            when (DerivedProcedureDateAndTimeGreaterThanOrEqual24Hours
            and   ScheduledProcedureLinesWithPurchaseOrderOrShipmentRel exists)

        ShowRescheduledProcedureAlert
            restricted
            when (RescheduledProcedure
            and   ScheduledProcedureItemsAreInProcess)

        ValidForRequisitionCreate
            restricted
            when ((HasInventoriedLines
            and  IsOpenOrFullyReserved)
            or   (ScheduledProcedureLineForRequisitionRel exists
            and   Status.InProcess
            and   not WithRequisition))

        ValidForUpdate
            restricted
            when (IsOpenOrFullyReserved
            or   (Status.InProcess
            and   not WithRequisition))

        HasRequestedProcessedClosedLines
            restricted
            when (ScheduledProcedureLinesRequestedRel exists
            or    ScheduledProcedureLinesClosedOrProcessedRel exists)

    Field Groups
        ScheduledProcedurePreferenceCard
            ItemGroup
            PreferenceCard

    Sets
        ByProcedureDate
            Sort Order
                ItemGroup                
                ProcedureDate
                Case
                Company
                RequestingLocation
                FromCompanyLocation
                ScheduledProcedure

    Field Rules
        FromCompanyLocation
			initial value is RequestingLocation.FromCompanyLocation
			default to RequestingLocation.FromCompanyLocation		
				default individual fields
            required
            if (FromCompanyLocation.FromCompany changed)
                constraint (ScheduledProcedureLineWithReservedRel not exists)
                    "CannotChangeFromCompany;QuantityHasBeenReservedForAComponentItem"

            if (FromCompanyLocation.FromLocation changed)
                constraint (ScheduledProcedureLineWithReservedRel not exists)
                    "CannotChangeFromLocation;QuantityHasBeenReservedForAComponentItem"

        FromCompanyParLocationBin
			initial value is RequestingLocation.FromCompanyLocation
			default to RequestingLocation.FromCompanyLocation		
            required
            if (FromCompanyLocation.FromCompany changed)
                constraint (ScheduledProcedureLinesPickedRel not exists)
                    "CannotChangeFromCompany;Non-StockItemsWerePicked"

        PreferenceCard
            cannot be changed
            required

            constraint (PreferenceCard.Active)
                "PreferenceCard<PreferenceCard>IsNotActive"
            constraint (not PreferenceCard.ErrorExists)
                "CannotUsePreferenceCard<PreferenceCard>;ErrorExists"

        ProcedureDate
            required
            constraint (ProcedureDate >= current corporate date)
                "ProcedureDateCannotBeLessThanToday'sDate"

        ProcedureTime
            required
            if (ProcedureDate = current corporate date)
                constraint (ProcedureTime >= current corporate time)
                    "ProcedureTimeCannotBeLessThanToday'sTime"

        OriginalProcedureDate
            if (action type.Create)
                force default to ProcedureDate
            else
                cannot be changed
        OriginalProcedureTime
            if (action type.Create)
                force default to ProcedureTime
            else
                cannot be changed

        RequestingLocation
            constraint (RequestingLocation.PreferenceCardOption.PreferenceCardsInUse
            or         (RequestingLocation.PreferenceCardOption.DefaultToInventoryCompany and Company.PreferenceCardEnabled))
                "RequestingLocationIsNotUsingPreferenceCard"

    Rule Blocks
        CreateRequisitionHeader 
            initialize LocalErrorMessage
            invoke Create Requisition
                assign result to LocalRequisitionHeader
                resume on error
                    LocalErrorMessage	= error message
                fill in fields from this instance
                    except invoked.Status
                    except invoked.PatientProcedure
                invoked.Requester					= RequestingLocation.FromLocationRel.ReplenishmentRequester
                invoked.RequestedDeliveryDate 		= ProcedureDate
                invoked.RequisitionSource			= RqSource.ScheduledProcedure
                invoked.AllocationPriority          = 1
            if (LocalErrorMessage entered)
                ErrorType = ErrorType.CreateRequisition
                ErrorMessage = "Error in creation of Requisition Header: " + LocalErrorMessage

                include CreateInventoryControlResultDetail
        
        
        CreateRequisitionLine
            initialize LocalErrorMessage
            for each ScheduledProcedureLineForRequisitionRel
                initialize LocalRequisitionLine
                invoke Create RequisitionLine
                    assign result to LocalRequisitionLine
                    resume on error
                        LocalErrorMessage	= error message
                    fill in fields from each
                        except invoked.Status
                        except invoked.ItemType
                        except invoked.PatientProcedure
                    invoked.Requisition                 = LocalRequisitionHeader
                    invoked.Description                 = each.Item.Description
                    invoked.Quantity                    = each.DerivedQuantityToProcess
                    invoked.EnteredUOM                  = each.ComponentUOM
                    invoked.EnteredUOMMultiplier        = each.ComponentUOMMultiplier
                    invoked.PreferenceCardTransaction   = true
                    invoked.AllocationPriority          = 1
                    invoked.ScheduledProcedureLine      = each.ScheduledProcedureLine

                if (LocalErrorMessage entered)
                    LocalRequisition = LocalRequisitionHeader
                    
                    invoke Delete ScheduledProcedureRequisitionLineRel
                    ErrorType = ErrorType.CreateRequisition
                    ErrorMessage = "Error in creation of Requisition Line: " + LocalErrorMessage

                    include CreateInventoryControlResultDetail

                    end for each    
                    
                else
                    invoke FastUpdate each
                        invoked.RequisitionLine     = LocalRequisitionLine
                        invoked.Status              = each.Status.Requested

            if (LocalErrorMessage not entered)
                Requisition = LocalRequisitionHeader
                Status = Status.InProcess

                include CreateInventoryControlResultDetail

        ProcessPatientProcedure
			if (not HasPatientProcedureRecord) 
				if (HasPatientProcedureInfo)
					invoke Create PatientProcedure
						assign result to LocalPatientProcedureView
						invoked.Company                 = Company
						invoked.PatientID				= TransientPatientID
						invoked.ProcedureDate			= ProcedureDate
						invoked.PhysicianName			= TransientProvider
						invoked.PatientLastName			= TransientPatientLastName
						invoked.PatientFirstName		= TransientPatientFirstName
						invoked.PatientMiddleName		= TransientPatientMiddleName
                        invoked.RecordType		 		= 1

				PatientProcedure = LocalPatientProcedureView.PatientProcedure

			else 
			if (action type.Update
			and TransientUpdatePatientProcedure entered)	
				invoke Update PatientProcedureRel
					invoked.PatientID				= TransientPatientID
					invoked.ProcedureDate			= ProcedureDate
					invoked.PhysicianName			= TransientProvider
					invoked.PatientLastName			= TransientPatientLastName
					invoked.PatientFirstName		= TransientPatientFirstName
					invoked.PatientMiddleName		= TransientPatientMiddleName


        ProcessPreviousRequisition
            initialize LocalErrorMessage
            if (LocalRequisitionHeader.NumberOfLines > 0)
                if (PrmReleaseRequisition)
                    invoke Unreleased.Release LocalRequisitionHeader
                        resume on error
                            LocalErrorMessage = error message
                
                    if (LocalErrorMessage entered)
                        for each LocalRequisitionHeader.ScheduledProcedureRel
                            invoke FastUpdate each
                                invoked.ErrorType = ErrorType.ReleaseRequisition
                                invoked.ErrorMessage = "Cannot release requisition " + LocalRequisitionHeader + ": " + LocalErrorMessage
            else
                invoke Delete LocalRequisitionHeader

            initialize LocalRequisitionHeader

        CreateInventoryControlResult
            invoke Create InventoryControlResult
                assign result to LocalSetInventoryControlResult
                invoked.FinanceEnterpriseGroup      = PrmCompany.FinanceEnterpriseGroup
                invoked.ReportType                  = 9 
                invoked.ItemGroup                   = PrmItemGroup
                invoked.InventoryCompany            = PrmCompany
                invoked.RequestingLocation          = PrmRequestingLocation
                invoked.ReleaseRequisition          = PrmReleaseRequisition
                invoked.ForecastDay                 = PrmProcedureDemandForecast
                invoked.ProcedureDate               = PrmProcedureDate   

        CreateInventoryControlResultDetail
            invoke Create InventoryControlResultDetail
                invoked.FinanceEnterpriseGroup      = PrmCompany.FinanceEnterpriseGroup
                invoked.InventoryControlResult      = LocalSetInventoryControlResult.InventoryControlResult
                invoked.ReportType                  = 1 
                invoked.ScheduledProcedure          = ScheduledProcedure
                if (LocalErrorMessage entered)
                    invoked.Status                  = 1 
                    invoked.ErrorMessage            = ErrorMessage
                else
                    invoked.Requisition             = Requisition


    Actions
        CancelScheduledProcedures is a Set Action
            Parameters
                PrmItemGroup                        is an ItemGroup
                    default label is "ItemGroup"
                PrmCompany                          is an InventoryCompany
                    default label is "Company"
                PrmRequestingLocation               is a RequestingLocation
                    default label is "RequestingLocation"
                PrmDaysPastProcedureDate            is Numeric size 2
                    default label is "DaysPastProcedureDate"
            
            Parameter Rules
                PrmItemGroup
                    required

                PrmCompany
                    if (PrmRequestingLocation entered)
                        required
                            "Requesting_LocationEntered;_CompanyIsRequired"
                
                PrmDaysPastProcedureDate
                    required

                    constraint (PrmDaysPastProcedureDate > 0)
                        "Procedure_Cutoff_Number_Of_DaysShouldBeGreaterThan0"
            Local Fields
				LocalActor 						is an Actor
				LocalNotificationMessage		is LPLText
				LocalCutOffDate					is Date

            Instance Selection
                where (IsOpenOrFullyReserved
                and    ItemGroup                = PrmItemGroup
                and   (PrmCompany not entered 
                or    (Company                  = PrmCompany))
                and   (PrmRequestingLocation not entered 
                or    (RequestingLocation       = PrmRequestingLocation))
                and    ProcedureDate <= (current corporate date - PrmDaysPastProcedureDate))

            Sort Order

            Action Rules
                Empty Set Rules
                    LocalNotificationMessage = "No Scheduled Procedure to cancel for Item Group:" + PrmItemGroup 
                    

                    if (PrmCompany entered)
                        LocalNotificationMessage    += ", Company: " + PrmCompany
                    if (PrmRequestingLocation entered)
                        LocalNotificationMessage    += ", Requesting Location: " + PrmRequestingLocation

                    LocalCutOffDate = current corporate date - PrmDaysPastProcedureDate
                    LocalNotificationMessage        += " dated on or before " + LocalCutOffDate + "."
                    
					LocalActor = actor
					send notification
						to LocalActor
						description is "NoScheduledProceduresToCancel"
						priority is high
						detail is "<LocalNotificationMessage>"

                Set Rules
					Entrance Rules
					
					Exit Rules
						LocalNotificationMessage = "Scheduled Procedures from Item Group: " + PrmItemGroup


                        if (PrmCompany entered)
                            LocalNotificationMessage    += ", Company: " + PrmCompany
                        if (PrmRequestingLocation entered)
                            LocalNotificationMessage    += ", Requesting Location: " + PrmRequestingLocation

						LocalCutOffDate = current corporate date - PrmDaysPastProcedureDate
                        LocalNotificationMessage        += " dated on or before " + LocalCutOffDate + " are canceled."
						
						LocalActor = actor
						send notification
							to LocalActor
							description is "ScheduledProceduresPastProcedureDateAreNowCanceled"
							priority is high
							detail is "<LocalNotificationMessage>"

                Instance Rules
                    invoke Cancel

        Create is a Create Action
            Entrance Rules
                constraint (ComponentItemRel exists)
                    "PreferenceCard<PreferenceCard>DoesNotHaveAnyComponentItems"
            Exit Rules
                for each ComponentItemRel
                    invoke Create ScheduledProcedureLine
                        fill in fields from this instance
                        invoked.Item                                = each.Item
                        invoked.ComponentUOM                        = each.ComponentUOM
                        invoked.ComponentOpenQuantity               = each.OpenQuantity
                        invoked.ComponentHoldQuantity               = each.HoldQuantity
                invoke StatusChange
                include ProcessPatientProcedure

        CreateRequisition is an Instance Action
            valid when (ValidForRequisitionCreate)
            completion message is "Requisition<LocalRequisitionHeader>Created"
            Parameters
            
            Local Fields
                LocalRequisitionHeader      is a Requisition
                LocalRequisitionLine        is a RequisitionLine
            Action Rules

                invoke Create Requisition
                    assign result to LocalRequisitionHeader
                    fill in fields from this instance
                        except invoked.Status
                        except invoked.PatientProcedure
                    invoked.Requester					= RequestingLocation.FromLocationRel.ReplenishmentRequester
                    invoked.RequestedDeliveryDate 		= ProcedureDate
                    invoked.RequisitionSource			= RqSource.ScheduledProcedure
                    invoked.AllocationPriority          = 1
            
                for each ScheduledProcedureLineForRequisitionRel
                    invoke Create RequisitionLine
                        assign result to LocalRequisitionLine
                        fill in fields from each
                            except invoked.Status
                            except invoked.ItemType
                            except invoked.PatientProcedure
                        invoked.Requisition                 = LocalRequisitionHeader
                        invoked.Description                 = each.Item.Description
                        invoked.Quantity                    = each.DerivedQuantityToProcess
                        invoked.EnteredUOM                  = each.ComponentUOM
                        invoked.EnteredUOMMultiplier        = each.ComponentUOMMultiplier
                        invoked.PreferenceCardTransaction   = true
                        invoked.AllocationPriority          = 1
                        invoked.ScheduledProcedureLine      = each.ScheduledProcedureLine

                    invoke FastUpdate each
                        invoked.RequisitionLine     = LocalRequisitionLine
                        invoked.Status              = each.Status.Requested

                Requisition = LocalRequisitionHeader
                Status = Status.InProcess


        MassRequisitionFromScheduledProcedure is a Set Action
            Parameters
                PrmItemGroup                    is an ItemGroup
                    default label is "ItemGroup"
                PrmCompany                      is a InventoryCompany
                    default label is "Company"
                PrmRequestingLocation           is a RequestingLocation
                    default label is "RequestingLocation"
                PrmReleaseRequisition           is Boolean
                    default label is "ReleaseRequisition"
                PrmProcedureDemandForecast      is Numeric size 1
                    default label is "ForecastDay"
                    States
                        Today                   value is 1
                        1Day                    value is 2
                        2Day            	    value is 3
                        3Day               		value is 4
                        4Day                	value is 5
                        5Day               		value is 6
                PrmProcedureDate                is Date
                    default label is "ProcedureDate"
            Parameter Rules
                PrmItemGroup
                    required
                PrmCompany
                    required
                PrmProcedureDemandForecast
                    if (PrmProcedureDate not entered)
                        required
                    else
                        cannot be entered
                            "FieldMustBeBlankIf_Procedure_DateIsSelected"
                    LocalProcedureDemandForecast = PrmProcedureDemandForecast - 1
                PrmProcedureDate
                    constraint (PrmProcedureDate >= current corporate date)
                        "Procedure_DateCannotBeLessThanToday'sDate"
                   
            
            Local Fields
                LocalProcedureDate                  is Date
                LocalProcedureTime                  is Time
                LocalCase                           is Alpha size 10
                LocalPatientID                      is a Patient
                LocalPatientFullName                is a Name
                LocalRequestingLocation             is a RequestingLocation
                LocalFromCompanyLocation            is a FromCompanyLocation
                LocalRoom                           is AlphaUpper size 20
                LocalRequisitionHeader              is a Requisition
                LocalRequisitionLine                is a RequisitionLine
                LocalErrorMessage                   is Text
                LocalActor					        is an Actor
                LocalSetInventoryControlResult      is a InventoryControlResult view
                
            Instance Selection
				where (ItemGroup                = PrmItemGroup
                and    Company                  = PrmCompany
                and   (PrmRequestingLocation not entered 
                or     PrmRequestingLocation    = RequestingLocation)
                and  ((PrmProcedureDate entered 
                and    PrmProcedureDate         = ProcedureDate)
                or     WithInProcedureDemandForecast)
                and    ValidForRequisitionCreate)

            Accumulators
				ProcessedCount	
                ErrorCount
            
            Sort Order is ByProcedureDate
            Action Rules
                Set Rules
					Entrance Rules
                        include CreateInventoryControlResult
					
					Exit Rules
                        include ProcessPreviousRequisition
                            
                        invoke FinishMassRequisition LocalSetInventoryControlResult.InventoryControlResult
                            invoked.PrmProcessedCount   = ProcessedCount
                            invoked.PrmErrorCount       = ErrorCount
                            
				Empty Set Rules
                    include CreateInventoryControlResult

                    invoke FinishMassRequisition LocalSetInventoryControlResult.InventoryControlResult

                Instance Rules
                    increment ProcessedCount

                    initialize ErrorType
                    initialize ErrorMessage

                    if (ProcedureDate                           = LocalProcedureDate
                    and ProcedureTime                           = LocalProcedureTime            
                    and RequestingLocation                      = LocalRequestingLocation     
                    and Case                                    = LocalCase 
                    and DerivedPatientID                        = LocalPatientID
                    and DerivedPatientFullName                  = LocalPatientFullName                   
                    and FromCompanyLocation.FromCompany         = LocalFromCompanyLocation.FromCompany
                    and FromCompanyLocation.FromLocation        = LocalFromCompanyLocation.FromLocation
                    and Room                                    = LocalRoom)
                        include CreateRequisitionLine
                    else
                        if (LocalRequisitionHeader entered)
                            include ProcessPreviousRequisition
                            
                        include CreateRequisitionHeader
                        if (LocalErrorMessage not entered)
                            include CreateRequisitionLine

                            LocalProcedureDate                      = ProcedureDate          
                            LocalProcedureTime                      = ProcedureTime  
                            LocalCase                               = Case       
                            LocalPatientID                          = DerivedPatientID
                            LocalPatientFullName                    = DerivedPatientFullName              
                            LocalRequestingLocation                 = RequestingLocation   
                            LocalFromCompanyLocation.FromCompany    = FromCompanyLocation.FromCompany
                            LocalFromCompanyLocation.FromLocation   = FromCompanyLocation.FromLocation
                            LocalRoom                               = Room

                    if (LocalErrorMessage entered)
                        increment ErrorCount

        Cancel is an Instance Action
            default label is "CancelSchedule"
        	valid when (IsOpenOrFullyReserved)
			Action Rules
                for each ScheduledProcedureLinesOpenAndFullRel
                    invoke CancelLine each
                        invoked.PrmCancelQuantity = each.DerivedTotalQuantity
            Exit Rules
                if (ScheduledProcedureLinesClosedOrProcessedRel exists)
                    Status = Status.Closed
                else
                    Status = Status.Canceled

        Close is an Instance Action
            restricted
            Action Rules
                Status = Status.Closed

        Update is an Update Action
            valid when (ValidForUpdate)
            Action Rules
                if (ProcedureDate changed)
                    for each ScheduledProcedureLinesRel
                        invoke FastUpdate each
                            invoked.ProcedureDate = ProcedureDate
                include ProcessPatientProcedure

        StatusChange is an Instance Action
            restricted
            Action Rules
                if (not HasRequestedProcessedClosedLines)
                    if (ScheduledProcedureLinesOpenRel exists)
                        Status = Status.Open
                    else 
                    if (ScheduledProcedureLinesFullyReservedRel exists)
                            Status = Status.FullyReserved
                    else
                    if (ScheduledProcedureLinesRejectRel exists)
                        Status = Status.Rejected
                    else
                        Status = Status.Canceled
                else
                    if (ScheduledProcedureLinesRequestedRel exists)
                        Status = Status.InProcess
                    else
                        if (ScheduledProcedureLinesOpenAndFullRel exists)
                            Status = Status.InProcess
                        else
                            Status = Status.Closed

        FastUpdate is an Update Action
			restricted
			bypass field rules

        FinishParItemPicking is an Instance Action
            valid when (CanFinishAll)
            Action Rules
                for each ScheduledProcedureLinesFullyPickedRel
                    invoke FinishLine each

        PickAllParItems is an Instance Action
            valid when (CanPickAll)
            Action Rules
                invoke PickAll ScheduledProcedureLine
                    invoked.PrmCompany                  = Company
                    invoked.PrmScheduledProcedure       = ScheduledProcedure

        Delete is a Delete Action
            valid when (IsOpenOrFullyReserved)
            Entrance Rules
                invoke Delete ScheduledProcedureLinesRel

                if (HasPatientProcedureRecord)
                    invoke DeletePatientProcedure

        DeletePatientProcedure is an Instance Action
            restricted
            Entrance Rules
                invoke Delete PatientProcedureRel
                initialize PatientProcedure
            
        UndoReservation is an Instance Action
        	valid when (CanUndoReservation)
			Action Rules
                for each ScheduledProcedureLineWithReservedRel
                    invoke UndoReservation each
                if (IsFullyReserved)
                    Status = Status.Open

        ReserveRemaining is an Instance Action
        	valid when (CanReserveRemaining)
			Action Rules
                for each ScheduledProcedureLineWithRemainingRel
                    invoke ReserveRemaining each

        RemoveRequisition is an Instance Action
            restricted
            Action Rules
                initialize Requisition

        SetRequisition is a Set Action
            restricted
            Instance Selection
                where (Requisition not entered
                and    DerivedRequisition entered)
                
            Action Rules
                Instance Rules
                    Requisition = DerivedRequisition

        UpdateToCanceled is an Instance Action
            restricted
            Action Rules
                Status = Status.Canceled

        SetOriginalProcedureDateAndTime is a Set Action
            restricted
            Instance Selection
                where (OriginalProcedureDate not entered
                and    OriginalProcedureTime not entered)
                
            Action Rules
                Instance Rules
                    OriginalProcedureDate = ProcedureDate
                    OriginalProcedureTime = ProcedureTime

        Reschedule is an Instance Action
            valid when (IsInProcess)

            Parameters
                PrmNewProcedureDate        is Date
                    default label is "NewDate"
                PrmNewProcedureTime        is Time
                    default label is "NewTime"

            Parameter Rules
                PrmNewProcedureDate
                    required
                    constraint (PrmNewProcedureDate >= current corporate date)
                        "ProcedureDateCannotBeLessThanToday'sDate"
                    constraint (PrmNewProcedureDate != ProcedureDate or PrmNewProcedureTime != ProcedureTime)
                        "New_\Procedure_\DateAnd_\TimeCannotBeTheSameAsTheOld_\Procedure_\DateAnd_\Time"
                PrmNewProcedureTime
                    required
                    if (PrmNewProcedureDate = current corporate date)
                        constraint (PrmNewProcedureTime >= current corporate time)
                            "New_\Procedure_\TimeCannotBeLessThanToday'sTime"

            Action Rules
                ProcedureDate = PrmNewProcedureDate
                ProcedureTime = PrmNewProcedureTime

                if (WithRequisition and UnreleasedRequisitionLinesRel exists)
                    invoke Update UnreleasedRequisitionLinesRel
					    invoked.RequestedDeliveryDate	=	PrmNewProcedureDate

