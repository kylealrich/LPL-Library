FinanceDimension5Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD5UP   
    
    Ontology
    	symbolic key is FinanceDimension5Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension5			is like FinanceDimension5
    	FinanceDimension5				is like FinanceDimension5
		SequenceDimension5				is like FinanceDimension5
		ParentSequenceDimension5		is like FinanceDimension5
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description	
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension5Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension5 is a DerivedField
			type is like FinanceDimension5
			restricted
			if (ParentFinanceDimension5 entered)
				return DerivedParentFinanceDimension5String
			else
				return TopNodeString
		
		DerivedSequenceParentDimension5 is a DerivedField
			type is like FinanceDimension5
			restricted
			if (ParentSequenceDimension5 entered)
				return ParentSequenceDimension5
			else
				return DerivedParentFinanceDimension5
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension5Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension5String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension5Structure.StructureSequence
			"_"
			ParentFinanceDimension5
		
		DerivedSummaryFinanceDimension5 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension5Structure.StructureSequence
			"_"
			FinanceDimension5
		
		DerivedSummarySequenceDimension5 is a DerivedField
			type is like FinanceDimension5
			restricted
			if (SequenceDimension5 entered)
				return SequenceDimension5
			else 
				return DerivedSummaryFinanceDimension5
			
	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
 
 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension5Structure
				FinanceDimension5
				FinanceDimension5Upload.RunGroup
				FinanceDimension5Upload.Sequence
 	
	Relations
		FinanceDimension5HierarchyRel
			one-to-one relation to FinanceDimension5Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension5Structure = FinanceDimension5Structure
				related.FinanceDimension5		   = FinanceDimension5
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension5Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension5Structure = FinanceDimension5Structure
			Instance Selection
				where (related.FinanceDimension5Upload.RunGroup = FinanceDimension5Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 10
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 10
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension5
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"
							
    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension5Upload.Sequence entered)
					FinanceDimension5Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension5Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action

		Purge is a Purge Action
			restricted
			
		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension5Structure		is a FinanceDimension5Structure
					default label is "FinanceDimension5Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension5Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension5	is a FinanceDimension5
				DynamicRulesExist		is Boolean		
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension5Structure	    = PrmFinanceDimension5Structure
				and    FinanceDimension5Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension5Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension5Structure
				FinanceDimension5Upload.RunGroup
				FinanceDimension5Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
										
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension5 = FinanceDimension5
					else
						LocalFinanceDimension5 = DerivedSummarySequenceDimension5
					if (LocalFinanceDimension5 exists)
						if  (RecordType.Posting
						and !FinanceDimension5HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension5
								resume on error
									LocalErrorMessage		= error message	
								invoked.PrmStructure		= FinanceDimension5Structure					
								invoked.PrmParentDimension  = DerivedSequenceParentDimension5
						else					
							if (DerivedSequenceParentDimension5 != LocalFinanceDimension5.DerivedParentDimension)
								invoke Move LocalFinanceDimension5
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension5
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension5
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension5 
										except invoked.ParentFinanceDimension5
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension5
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension5
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension5
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension5 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension5
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension5Structure		is a FinanceDimension5Structure
					default label is "FinanceDimension5Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension5Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension5Structure	    = PrmFinanceDimension5Structure
				and    FinanceDimension5Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension5Structure   is a FinanceDimension5Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension5Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension5Structure	    = PrmFinanceDimension5Structure
				and    FinanceDimension5Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
