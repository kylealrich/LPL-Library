IntrastatHeader is a BusinessClass
    owned by tx
    prefix is INH
    classic name is INSTHEADER

    Ontology
        symbolic key is IntrastatHeader
            classic set name is INHSET1
            classic name for IntrastatHeader.IntrastatNumber is INTRASTAT-NBR

    Patterns
        implements StaticJava
        disable AuditIndex
		implements ContextualParent
		implements BODId
		
    Persistent Fields

        ReportYear                       is a Year
        ReportPeriod                     is a Period
        TransactionType                  is AlphaUpper size 1
            classic name is TRANS-TYPE
            States
                PurchasesArrivals value is "P"
                SalesDispatches   value is "S"
        SystemCode       				 is a GeneralLedgerSystemCode 
            classic name is SYSTEM-CD
		CustomerGroup
        Customer
 		VendorGroup	
        Vendor
        DocumentNumber
            classic name is DOCUMENT-NBR
        NatureOfTransactionCode			 is a IntrastatNatureOfTransactionCode	
            sql name is INatureOfTransactionCode
            classic name is NOTC
        TaxPointDate                     is Date
            classic name is TAX-PNT-DATE
        Status                           is Numeric size 1
            States
                Open 			value is 1
                Reported        value is 2
        CustomerVATCountry               is a Country
            classic name is CUST-VAT-CTRY
        CustomerVATRegistrationNumber    is a VATRegistrationNumber 
            classic name is CUST-VAT-REG-N
        CustomerCurrencyCode             is a Currency
            classic name is CUST-CURR-CD
        VendorVATCountry                 is a Country
            classic name is VEND-VAT-CTRY
        VendorVATRegistrationNumber      is a VATRegistrationNumber 
            classic name is VEND-VAT-REG-N
        VendorCurrencyCode               is a Currency
            classic name is VENDOR-CURR-CD
        CurrencyRate
            classic name is CURR-RATE
        OriginCountry                    is a Country
        OriginRegion                     is a Region
            context of OriginCountry
        LoadingPort                      is an IntrastatPortCode
            context of OriginCountry
        DestinationCountry               is a Country
            classic name is DEST-COUNTRY
        DestinationRegion                is a Region
            classic name is DEST-REGION
            context of DestinationCountry
        UnloadingPort                    is an IntrastatPortCode
            context of DestinationCountry
        TransportMode
        DeliveryTerms                    is a ShipTerm
            classic name is DLVRY-TRMS
        StatisticalProcedure			 is a IntrastatStatisticalProcedure	
            classic name is STAT-PROC
        DropShipIndicator                is a DropShipInd
            classic name is DROP-SHIP-IND
        DeliveryDate                     is Date
        PurchasingCompany
        PurchaseOrder
		RelatedObjectReference			is BusinessObjectReference
	
	Context Fields

		FSMInboundBODTracker
	
    Derived Fields

        CustomerVATNumbber is a StringField
            type is Alpha size 14
            classic name is CUST-VAT-NBR
            restricted
            CustomerVATCountry
            CustomerVATRegistrationNumber

        VendorVATNumber    is a StringField
            type is Alpha size 14
            classic name is VEND-VAT-NBR
            restricted
            VendorVATCountry
            VendorVATRegistrationNumber

		DisplayCompletionMessage	is a DerivedField
   			type is MessageField
   			restricted
   			if (CompletionMessage entered)
				return CompletionMessage
   			else	
   			if (NoDetailRecordsExist)
   				return NoDetailRecordsMessage
   			else
   				return blank	 
 
 		NoDetailRecordsMessage is a MessageField
 			restricted
			"<SuccessfullMessage>WARNING:NoDetailRecordsExist"

 		SuccessfullMessage is a MessageField
 			restricted
			"RecordSuccessfullyProcessed;"
							
		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
			
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedDisplayID is a DerivedField
			type is Alpha size 30
			restricted
			if (TaxEntity.VATRegistrationCountry = "IT")
				return TaxEntity + LocalPrmReportYear + LocalPrmReportPeriod
			else
				return TaxEntity +"-"+LocalPrmReportYear +"-"+ LocalPrmReportPeriod
				
		DerivedTaxID is a DerivedField
			type is AlphaUpper size 25
			restricted
			return TaxEntity.TaxID
			
		DerivedName is a DerivedField
			type is Alpha size up to 30
			restricted
			return TaxEntity.Name
					
		DerivedBODStatus is a DerivedField
			type is Alpha size 25
			restricted
			if(Status.Open)
				return "Open"
			if(Status.Reported)
				return "Reported"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create)
				return "Add"
			if (action type.Update)
				return "Replace"
			else 
				return "Delete"					
										
		LCLIntrastatServiceXMLBOD is a DerivedField
			type is XMLDocument	
			restricted
			LCLIntrastatServiceXMLBOD = template.IONSyncLCLTradeStatistics_IntrastatHeader_ST document for this instance
			

		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		 		
		DerivedAccountingEntity is a DerivedField
			type is Alpha 20
			restricted
			return FinanceEnterpriseGroup+DerivedDelimiter+TaxEntity
			
		DerivedDocumentID is a DerivedField
			type is Alpha 40
			restricted
			return FinanceEnterpriseGroup + DerivedDelimiter + TaxEntity + DerivedDelimiter + LocalPrmReportYear + DerivedDelimiter + LocalPrmReportPeriod
			
		DerivedIntrastatVatRegNumber is a DerivedField
			type is Alpha 20
			restricted
			return TaxEntity.VATRegistrationNumber
		
		DerivedIntrastatVatRegCountry 	is a DerivedField
			type is Alpha 20
			restricted 
			return last TaxEntity.VATRegistrationCountry
			
		DerivedAddressCode is a DerivedField
			type is Alpha 20
			restricted 
			return BODAccountingEntityRel.AddressCode
		
		DerivedPostalAddressCountry is a DerivedField
			type is Alpha 20
			restricted
			if (BODAddressCodeRel.PostalAddress.Country = "US")
				return "DE"
			else
				return BODAddressCodeRel.PostalAddress.Country  
				
		DerivedDefaultBODLocation is a DerivedField
			type is AlphaUpper size 20
			return ""
				
		URIData is a DerivedField
			type is XMLDocument
			restricted
			URIData = template.IONSyncLCLTradeStatistics_IntrastatHeader_URIData_ST document for this instance

		FinalURIData is a DerivedField
			type is XMLDocument
			restricted 
			if (BODAddressCodeRel.PostalAddress.Country = "IE")
				return URIData
		
		ContainerIndicator is a DerivedField
			type is XMLDocument
			restricted
			ContainerIndicator = template.IONSyncLCLTradeStatistics_IntrastatHeader_ContainerIndicator_ST document for this instance
	    
	    FinalContainerIndicator is a DerivedField
			type is XMLDocument
			restricted
			if (DerivedIntrastatVatRegCountry = "NL")
	    		return ContainerIndicator
	    	else
	    		return ""
			
		TradeStatisticsDeclarationDispatchLine is a DerivedField
			type is XMLDocument
			restricted
			return LocalTradeStatisticsDeclarationDispatchLine
			
		PROPERTIESXML is a DerivedField
			type is XMLDocument
			restricted
			PROPERTIESXML = template.IONSyncLCLTradeStatistics_IntrastatHeader_Properties_ST document for this instance
			
		TradeStatisticsDeclarationDispatch is a DerivedField
			type is XMLDocument
			restricted
			TradeStatisticsDeclarationDispatch = template.IONSyncLCLTradeStatistics_IntrastatHeader_DeclarationDispatch_ST document for this instance
			
		DerivedCountryName is a DerivedField
			type is Alpha 20
			restricted 
			if (BODAddressCodeRel.PostalAddress.CountryName = "USA")
				return " Franklin"
			else
				return BODAddressCodeRel.PostalAddress.CountryName
			
		DerivedAddressLine1 is a DerivedField
			type is Alpha 30
			restricted 
			return BODAddressCodeRel.PostalAddress.DeliveryAddress.AddressLine1
			
		DerivedAddressLine2 is a DerivedField
			type is Alpha 30
			restricted 
			return BODAddressCodeRel.PostalAddress.DeliveryAddress.AddressLine2
			
		DerivedAddressLine3 is a DerivedField
			type is Alpha 30
			restricted 
			return BODAddressCodeRel.PostalAddress.DeliveryAddress.AddressLine3
			
		DerivedAddressLine4 is a DerivedField
			type is Alpha 30
			restricted 
			return BODAddressCodeRel.PostalAddress.DeliveryAddress.AddressLine4
			
		DerivedPostalCode is a DerivedField
			type is Alpha 20
			restricted 
			return BODAddressCodeRel.PostalAddress.PostalCode
			
		DerivedRegion is a DerivedField
			type is Alpha 20
			restricted 
			return BODAddressCodeRel.PostalAddress.Region
			
		DerivedStateProvince is a DerivedField
			type is Alpha 20
			restricted 
			return BODAddressCodeRel.PostalAddress.StateProvince
			
		DerivedEmailAddress is a DerivedField
			type is Alpha 20
			restricted 
			return BODAddressCodeRel.EmailAddress
				
		DerivedVendorCustomerVatRegistrationNumber is a DerivedField
			type is Alpha 20
			restricted
			if (Vendor != 0)
				return Vendor.VATRegistrationNumber
			if (Customer != 0)
				return BODCompanyCustomerRel.VATRegistrationNumber
				
		DerivedVendorCustomerAddressLine1 is a DerivedField
			type is Alpha 40
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.DeliveryAddress.AddressLine1
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.DeliveryAddress.AddressLine1
				
		DerivedVendorCustomerAddressLine2 is a DerivedField
			type is Alpha 40
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.DeliveryAddress.AddressLine2
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.DeliveryAddress.AddressLine2
				
		DerivedVendorCustomerAddressLine3 is a DerivedField
			type is Alpha 40
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.DeliveryAddress.AddressLine3
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.DeliveryAddress.AddressLine3
				
		DerivedVendorCustomerAddressLine4 is a DerivedField
			type is Alpha 40
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.DeliveryAddress.AddressLine4
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.DeliveryAddress.AddressLine4
				
		DerivedVendorCustomerCountrySDCode is a DerivedField
			type is Alpha 20
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.StateProvince
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.StateProvince
				
		DerivedVendorCustomerPostalCode is a DerivedField
			type is Alpha 20
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.PostalCode
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.PostalCode
		
		DerivedVendorCustomerMunicipality is a DerivedField
			type is Alpha 20
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.Municipality
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.Municipality
				
		DerivedVendorCustomerCountry is a DerivedField
			type is Alpha 20
			restricted
			if (Vendor != 0)
				return Vendor.VendorAddress.Country
			if (Customer != 0)
				return BODCompanyCustomerRel.Customer.PostalAddress.Country
				
		DerivedVendorTaxId is a DerivedField
			type is Alpha 25
			restricted
			return Vendor.TaxID
		
		DerivedFinalTotalBaseAmountArrival is a DerivedField
			type is Decimal 19.3
			restricted
			return LocalTotalBaseAmountArrival
			
		DerivedFinalTotalStatisticalValueAmountArrival is a DerivedField
			type is Decimal 19.3
			restricted
			return LocalTotalStatisticalValueAmountArrival
			
		DerivedFinalTotalNetWeightMeasureArrival is a DerivedField
			type is Decimal 19.3
			restricted
			return LocalTotalNetWeightMeasureArrival
			
		DerivedTransportMethodCode is a DerivedField
			type is Alpha 10
			restricted
			if (TransportMode = "1")
				return "Sea"
			else
			if (TransportMode = "2")
				return "Rail"
			else
			if (TransportMode = "3")
				return "Road"
			else
			if (TransportMode = "4")
				return "Air"
			else
			if (TransportMode = "5")
				return "Post"
			else
			if (TransportMode = "7")
				return "Fixed"
			else
			if (TransportMode = "8")
				return "Inland Waterway"
			else
			if (TransportMode = "9")
				return "Own Propulsion"
				
		DerivedDirectionCode is a DerivedField
			type is Alpha 10
			restricted
			if (DerivedPostalAddressCountry = "NL" and TransactionType = "P")
				return "Arrival"
			if (DerivedPostalAddressCountry = "NL" and TransactionType = "S")
				return "Dispatch"
				
		DerivedRoleValue is a DerivedField
			type is Alpha 10
			restricted
			if (TransactionType = "P")
				return "Supplier"
			if (TransactionType = "S")
				return "Dispatch"
		
		DerivedDTLDirectionCode is a DerivedField
			type is Alpha 10
			restricted
			if (TransactionType = "P")
				return "Arrival"
			if (TransactionType = "S")
				return "Dispatch"
				
		DerivedIntrastatHeaderCount is a DerivedField 	
	    	type is Numeric 4
	    	restricted
	    	if (IntrastatDtlRel exists)
	    		return (instance count of IntrastatDtlRel)
	    	else
	    		return 0
	    		
	    DerivedSupplementaryUOM is a DerivedField
	    	type is AlphaUpper size 4
	    	restricted
	    	return IntrastatDtlRel.SupplementaryUOM
	    	
	    DerivedIntrastatDetail is a DerivedField
	    	type is Numeric size 4
	    	restricted
	    	return LocalIntrastatDetail
	    	
	    DerivedICNCode is a DerivedField
	    	type is like ICNCode
	    	restricted
	    	return LocalICNCode
	    	
	    DerivedICNCodeSupplementaryUOM is a DerivedField
	    	type is AlphaUpper size 8
	    	restricted
	    	return BODICNCodeRel.SupplementaryUOM
	    	
	    DerivedReceivedAmount is a DerivedField
	    	type is Decimal size 19.3
	    	restricted
	    	return LocalReceivedAmount
	   	    
	    DerivedCustomerCurrencyCodeBOD is a DerivedField
	    	type is AlphaUpper size 5
	    	restricted
	    	if (VendorCurrencyCode != blank)
				return VendorCurrencyCode
			else
			if (CustomerCurrencyCode != blank)
				return CustomerCurrencyCode
	    	
	    DerivedStockWeight is a DerivedField
	    	type is Decimal size 9.3
	    	restricted
	    	return LocalStockWeight
	    	
	    DerivedSupplementaryQuantity is a DerivedField
	    	type is Decimal size 13.4
	    	restricted
	    	return LocalSupplementaryQuantity 
	    	
	    DerivedDeclarationID is a DerivedField
			type is Alpha 40
			restricted
			if (TransactionType = "P")
				return ReportYear using "%d" + DerivedDelimiter + LocalPrmReportPeriod + DerivedDelimiter + "Arrival"
			if (TransactionType = "S")
				return ReportYear using "%d" + DerivedDelimiter + LocalPrmReportPeriod + DerivedDelimiter + "Dispatch"
	    	
	    DerivedDeclarationTypeCode is a DerivedField
			type is Alpha 10
			restricted
			if (DerivedIntrastatVatRegCountry = "DE")
				return "Simplified"
			else
			if (DerivedIntrastatVatRegCountry = "SE" or DerivedIntrastatVatRegCountry = "LU" )
				return "Detailed"
			else
			if (DerivedIntrastatVatRegCountry = "DK" )
				return "Fiscal"  
			else
			if (DerivedIntrastatVatRegCountry = "FR" )
				return "Simplified"
			else
			if (DerivedIntrastatVatRegCountry = "BE" )
				return "Extended"


		DerivedDeclarationFunction is a DerivedField
			type is Alpha 10
			restricted
			if (DerivedIntrastatVatRegCountry = "BE")
				return "Replaced"
			else
				return "Original"
				
		DerivedBODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
				
		DerivedNativeBODID is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID + ":" + DerivedAccountingEntity + ":" + DerivedDefaultBODLocation + ":" + DerivedDocumentID + ":" + "Intrastat:?IntrastatNumber&verb=Sync&TrackerID="+ LocalFSMInboundBODTracker		
						  				
    Relations

        IntrastatDtlRel
            one-to-many relation to IntrastatDetail
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup
                related.TaxEntity						= TaxEntity
            Instance Selection
                where (related.IntrastatHeader.FiscalYear = IntrastatHeader.FiscalYear
                and   related.IntrastatHeader.IntrastatNumber = IntrastatHeader.IntrastatNumber)

		IntrastatNumberEntityAndYearRel				 
            one-to-one relation to IntrastatSequenceNumber
  			Field Mapping uses symbolic key
  				related.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup

                related.TaxEntity 						= TaxEntity
				related.FiscalYear						= IntrastatHeader.FiscalYear   
						
		IncrementIntrastatSeqRel				 
            one-to-many relation to IntrastatSequenceNumber
  			Field Mapping uses symbolic key
  				related.FinanceEnterpriseGroup			= TaxEntity.FinanceEnterpriseGroup
 			Instance Selection
                where (related.TaxEntity 				= TaxEntity)
                
        FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "LCLTradeStatistics"
            	related.FSMBODConfiguration.Direction 	= 1            	
				
		BODAccountingEntityRel
			one-to-many relation to AccountingEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AccountingEntity                       = TaxEntity
				
		BODAddressCodeRel
			one-to-many relation to AddressCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AddressCode                       = DerivedAddressCode
				
		BODCompanyCustomerRel
			one-to-many relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company			= TaxEntity
				related.Customer              = Customer
				
		BODICNCodeRel
			one-to-many relation to ICNCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ICNCode              = DerivedICNCode
				
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
				   
    Conditions
    	NoDetailRecordsExist
    		restricted
    		when (IntrastatDtlRel not exists)

		HasTaxEntity
			restricted
			when (TaxEntity entered)

		VendorRequired                               
			restricted
			when (TransactionType.PurchasesArrivals
			and Vendor not entered)

		CustomerRequired                             
			restricted
			when (TransactionType.SalesDispatches
			and Customer not entered)
    Sets

        Set2
            indexed
            Sort Order
                TaxEntity
                IntrastatHeader.FiscalYear
                ReportYear
                ReportPeriod
                IntrastatHeader.IntrastatNumber

        Set3
            indexed
            Sort Order
                TaxEntity
                IntrastatHeader.FiscalYear
                IntrastatHeader.IntrastatNumber

	Transient Fields			 
 		BypassCheckCountries  			is Boolean
 		BypassNumberGeneration			is Boolean	
		 		
	Local Fields
		LocalTrigger					is Alpha 1
		IntrastatProcessing
		CompletionMessage				is Alpha size 250	
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		LocalPrmReportPeriod		     is AlphaUpper size 3
		LocalPrmReportYear		     is Numeric size 10
		LocalPrmIntrastatHeaderNumber is Numeric size 10
		TotalBaseAmountArrival is Decimal size 19.3
		TotalStatisticalValueAmountArrival is Decimal size 19.3
		TotalNetWeightMeasureArrival is Decimal size 19.3
		LocalTotalBaseAmountArrival is Decimal size 19.3
		LocalTotalStatisticalValueAmountArrival is Decimal size 19.3
		LocalTotalNetWeightMeasureArrival is Decimal size 19.3
		LocalTradeStatisticsDeclarationDispatchLine is XMLDocument
		LocalIntrastatDetail is Numeric size 4
		LocalICNCode is like ICNCode
		LocalStockWeight is Decimal size 9.3
		LocalSupplementaryQuantity is Decimal size 13.4
		LocalReceivedAmount is Decimal size 19.3
		NativeLPLBODTrigger							is Boolean				
		NewBODTracker  					is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker		is Numeric 15
		Error            				is Boolean
	    ErrorMessage     				is Alpha 300
	    LocalConfigurationParameter		is Alpha size up to 200
	    
    Field Rules
    	TaxEntity
    		constraint (TaxEntity.IntrastatUsed)
    			"EntityDoesNotUseIntrastat"	

		TaxPointDate
			required												
		OriginCountry												
			required  
		DestinationCountry											
			required 
		TransactionType												
			required	 							
		NatureOfTransactionCode
			required												
		Vendor				
			if (TransactionType.PurchasesArrivals)					
				required				
		Customer
			if (TransactionType.SalesDispatches)					
				required	
        DropShipIndicator
            default to "N"
        Status
        	default to 1    
 		
	Rule Blocks
				
		NativeLPLRuleblock
			if (TransactionType = "P" or TransactionType = "S")
				if (IntrastatDtlRel exists)		
					for each IntrastatDtlRel
						initialize TotalBaseAmountArrival 
						initialize TotalStatisticalValueAmountArrival 
						initialize TotalNetWeightMeasureArrival 
						TotalBaseAmountArrival  = each.BaseAmount
						LocalTotalBaseAmountArrival += TotalBaseAmountArrival 
						TotalStatisticalValueAmountArrival  = each.ReceivedAmount
						LocalTotalStatisticalValueAmountArrival += TotalStatisticalValueAmountArrival  
						TotalNetWeightMeasureArrival  = each.StockWeight
						LocalTotalNetWeightMeasureArrival  += TotalNetWeightMeasureArrival 
						LocalIntrastatDetail = each.IntrastatDetail
						LocalICNCode = each.ICNCode
						LocalStockWeight =  each.StockWeight
						LocalSupplementaryQuantity = each.SupplementaryQuantity
						LocalReceivedAmount = each.ReceivedAmount
						LocalTradeStatisticsDeclarationDispatchLine += template.IONSyncLCLTradeStatistics_IntrastatHeader_DeclarationDispatchLine_ST document for this instance
						
						
						
		CheckCountries

			if (OriginCountry entered
			and DestinationCountry entered)
				IntrastatProcessing.TaxEntity			= TaxEntity
				IntrastatProcessing.FiscalYear			= IntrastatHeader.FiscalYear	
				IntrastatProcessing.OriginCountry		= OriginCountry	
				IntrastatProcessing.OriginRegion 		= OriginRegion				 	
				IntrastatProcessing.DestinationCountry	= DestinationCountry
				IntrastatProcessing.DestinationRegion 	= DestinationRegion
				IntrastatProcessing.SystemCode			= "IN"	
				LocalTrigger							= IntrastatProcessing.CheckCountries
				constraint (IntrastatProcessing.OutputErrorNumber not entered)
					"<IntrastatProcessing.OutputErrorMessage>"
				
				if (IntrastatProcessing.SoftMessageNumber entered)
					CompletionMessage = IntrastatProcessing.OutputErrorMessage
						
    Actions
        Create is a Create Action
        	completion message is "<DisplayCompletionMessage>"		
			Entrance Rules
				if (BypassNumberGeneration
				and IntrastatHeader.IntrastatNumber not entered)
					BypassNumberGeneration = false

				if (not BypassNumberGeneration)
	     			constraint (IntrastatHeader.IntrastatNumber not entered)
	      				"CannotEnterAnIntrastatNumberForCreateAction;NumberWillBeAutoGenerated"
					if (IntrastatHeader.FiscalYear not entered)
						if (ReportYear entered)
							IntrastatHeader.FiscalYear = ReportYear		
						else
							IntrastatHeader.FiscalYear = TaxEntity.FiscalYear


					if (IntrastatNumberEntityAndYearRel not exist)

						invoke Create IntrastatSequenceNumber
							invoked.FinanceEnterpriseGroup	= TaxEntity.FinanceEnterpriseGroup
							invoked.TaxEntity				= TaxEntity
							invoked.FiscalYear				= IntrastatHeader.FiscalYear	

	
					increment TaxEntity.LastIntrastatNumber by 1		       				       				
			       	IntrastatHeader.IntrastatNumber = TaxEntity.LastIntrastatNumber
			       		


			       				       		



			Action Rules
				if (!BypassCheckCountries)
					include CheckCountries			
												
		CreateFromBatch is a Create Action
			bypass field rules
			restricted
			Entrance Rules
				increment TaxEntity.LastIntrastatNumber by 1		       				       				
				IntrastatHeader.IntrastatNumber = TaxEntity.LastIntrastatNumber
      
        
        Update is an Update Action
        	completion message is "<DisplayCompletionMessage>"		
			Entrance Rules
      			constraint (!Status.Reported)
      				"IntrastateRecordHasBeenReported;CannotBeChanged"	
                																		
        Delete is a Delete Action
			Entrance Rules
      			constraint (!Status.Reported)
      				"IntrastatHasBeenReported;CannotDelete"				
      				 				




        TriggerIntrastatService is an Instance Action  
	  	 	restricted
	  	 	Parameters
	  	 		PrmTaxEntity         is a TaxEntity
				PrmReportYear	     is Numeric size 10
				PrmReportPeriod		 is AlphaUpper size 3
				PrmIntrastatHeaderNumber   is Numeric size 10

	  	 	Local Fields
				ActionCode			 is Alpha size 1
					States
						Create  value is "C"
						Update	value is "U"
						Delete	value is "D"
				LocalFinanceEnterpriseGroup  is like FinanceEnterpriseGroup 
					  	 		
			Action Rules
				if (PrmTaxEntity.FinanceEnterpriseGroup.BODTrigger)
					ActionCode = ActionCode.Create
					LocalFinanceEnterpriseGroup = PrmTaxEntity.FinanceEnterpriseGroup
					increment bod id.VariationID

					trigger "IntrastatService" PA service
						resume on error
						title is "EG:<PrmTaxEntity.FinanceEnterpriseGroup>AE:<PrmTaxEntity>"
						Criteria
							LocalFinanceEnterpriseGroup
							PrmTaxEntity

						Variables
							ActionCode
							bod id.VariationID
								variable name is BODVariationId
							LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
								variable name is CurrentTimeStamp
							PrmTaxEntity
								variable name is PrmTaxEntity
							LocalFinanceEnterpriseGroup
								variable name is FinanceEnterpriseGroup	
							PrmReportYear
								variable name is ReportYear
							PrmReportPeriod
								variable name is ReportPeriod
							PrmIntrastatHeaderNumber
								variable name is IntrastatHeaderNumber
								





		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location" 
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"	
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
				PrmVariationID       is Alpha size 22
					default label is "VariationID"	
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
				if (bod id.VariationID != PrmVariationID)
					bod id.VariationID			= PrmVariationID				
								
		SendLCLTradeStatisticsNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is LCLIntrastatServiceXMLBOD
					bod type is "Sync.LCLTradeStatistics"	
					accounting entity is DerivedAccountingEntity
					document id is DerivedDocumentID
					variation id is DerivedBODVariationID
							
		TriggerIntrastatNativeLPLBOD is an Instance Action
			restricted
			Parameters
				PrmReportPeriod		     is AlphaUpper size 3
				PrmReportYear		     is Numeric size 10
				PrmIntrastatHeaderNumber is Numeric size 10
			Action Rules
				LocalPrmReportPeriod 		  = PrmReportPeriod
				LocalPrmReportYear 			  = PrmReportYear
				LocalPrmIntrastatHeaderNumber = PrmIntrastatHeaderNumber	
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "LCLTradeStatistics"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "IntrastatHeader"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmAccountingEntity			= TaxEntity
					invoked.PrmMainUserTemplate			= "IONSyncLCLTradeStatistics_IntrastatHeader_ST"
				NativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				if (FinanceEnterpriseGroup.BODTrigger and NativeLPLBODTrigger)
					include NativeLPLRuleblock
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "LCLTradeStatistics"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.RunGroup				= TaxEntity
							invoked.Invoice					= IntrastatHeader.IntrastatNumber
							invoked.Reference1				= IntrastatHeader.FiscalYear
							invoked.Reference2              = "IntrastatHeader" 
							invoked.Reference3				= LocalPrmReportPeriod 	
							invoked.Reference4				= LocalPrmReportYear 
							invoked.Reference5				= LocalPrmIntrastatHeaderNumber 
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.RunGroup				= TaxEntity
							invoked.Invoice					= IntrastatHeader.IntrastatNumber
							invoked.Reference1				= IntrastatHeader.FiscalYear
							invoked.Reference2              = "IntrastatHeader" 
							invoked.Reference3				= LocalPrmReportPeriod 	
							invoked.Reference4				= LocalPrmReportYear 
							invoked.Reference5				= LocalPrmIntrastatHeaderNumber 
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendLCLTradeStatisticsNativeLPLBOD
						resume on error
	                   		Error            							= true
	                        ErrorMessage     							= error message
	                if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedNativeBODID
							invoked.BODXML								= LCLIntrastatServiceXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedNativeBODID
							invoked.BODXML									= LCLIntrastatServiceXMLBOD
				
      				
