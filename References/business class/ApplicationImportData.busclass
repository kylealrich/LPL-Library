ApplicationImportData is a BusinessClass
    owned by la
    prefix is PLID
    stored in product line

    Patterns
        disable Auditing
        disable EffectiveDated
        disable UniqueID

    Persistent Fields

        Stamp        is SpecReposStamp
        Group        is Alpha size up to 20
        Mode		 is a DataImportMode
        Name         is Alpha size up to 100
        Hash         is Alpha size up to 64
        Data         is BinaryDocument

	Conditions
		PreviouslyLoaded
			when (ImportedDataRel.Hash = Hash)

	Derived Fields
		Valid is a NativeField
			type is Boolean


	Relations
		ImportedDataRel
            one-to-one relation to ImportedData
            Field Mapping uses Set1
				related.Group = Group
                related.Name = Name

    Sets

        Set1
            primary
            indexed
            Sort Order
                Stamp
                Group
                Name

	Actions
				
		Create is a Create Action
			restricted
		Delete is a Delete Action
			restricted
		Update is an Update Action
			restricted
				
		LoadData is an Instance Action
			valid when Valid
			
        	Action Rules
				invoke LoadDataWithMode
					invoked.ImportMode = Mode
				invoke SetDataLoaded
				
		LoadDataIgnoreDuplicates is an Instance Action
			valid when Valid
			
        	Action Rules
				invoke LoadDataWithMode
					invoked.ImportMode = DataImportMode.Ignore
				invoke SetDataLoaded
				
		LoadDataOverwriteDuplicates is an Instance Action
			valid when Valid
			
        	Action Rules
				invoke LoadDataWithMode
					invoked.ImportMode = DataImportMode.Overwrite
				invoke SetDataLoaded
				
		LoadDataUpdateDuplicates is an Instance Action
			valid when Valid
			
        	Action Rules
				invoke LoadDataWithMode
					invoked.ImportMode = DataImportMode.Update
				invoke SetDataLoaded
				
		LoadDataWithMode is an Instance Action
			restricted
			
			Parameters
		    	ImportMode          is a DataImportMode
				
		ClearDataLoaded is an Instance Action
			
        	Action Rules
				invoke Update ImportedDataRel
					invoked.Hash = blank
				
		SetDataLoaded is an Instance Action
			restricted
			
        	Action Rules
				invoke Update ImportedDataRel
					invoked.Hash = Hash
		    	
		LoadGroup is a Set Action
			disable checkpoint

			Parameters
				TargetGroup 		is Alpha size up to 20
				TargetStamp     	is Numeric size 12

			Parameter Rules
				TargetStamp
					TargetStamp = com.lawson.apps.la.ImportDataIsValid.getPLStamp(parentcontext.dataarea as String)

			Instance Selection
				where (Group = TargetGroup 
				and not PreviouslyLoaded
				and Stamp = TargetStamp)  

			Action Rules
				Empty Set Rules
					display "LoadGroupWithTargetGroup_'<TargetGroup>'_AndTargetStamp_'<TargetStamp>'_MatchedNoRows"
				Set Rules
					Entrance Rules
						display "LoadGroupWithTargetGroup'<TargetGroup>'AndTargetStamp'<TargetStamp>'"
				Instance Rules
					invoke LoadData

		LoadGroupAll is a Set Action
			disable checkpoint

			Parameters
				TargetGroup 		is Alpha size up to 20
				TargetStamp     	is Numeric size 12

			Parameter Rules
				TargetStamp
					TargetStamp = com.lawson.apps.la.ImportDataIsValid.getPLStamp(parentcontext.dataarea as String)

			Instance Selection
				where (Group = TargetGroup 
				and Stamp = TargetStamp)

			Action Rules
				Empty Set Rules
					display "LoadGroupAllWithTargetGroup_'<TargetGroup>'_AndTargetStamp_'<TargetStamp>'_MatchedNoRows"
				Set Rules
					Entrance Rules
						display "LoadGroupAllWithTargetGroup'<TargetGroup>'AndTargetStamp'<TargetStamp>'"
				Instance Rules
					invoke LoadData
