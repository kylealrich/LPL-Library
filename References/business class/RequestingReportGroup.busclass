RequestingReportGroup is a BusinessClass
    owned by ic
    prefix is RRGP

    Ontology
        symbolic key is RequestingReportGroup

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Name
        AddNewRequestingLocations is Boolean 

    Local Fields 
        LocalProcurementGroup   is like ProcurementGroup 
        LocalParticipantGroup   is like ParticipantGroup
        LocalCompany            is like InventoryCompany
        LocalRequestingLocation is like RequestingLocation 

	Conditions	
		RecordExist
			restricted
			when (RequestingReportGroup exist)

        HasParticipantGroups
            restricted 
            when (ParticipantGroupReportGroupRel exists)

	Relations 

        ParticipantGroupRel 
            one-to-one relation to ParticipantGroup 
            Field Mapping uses symbolic key
                related.ProcurementGroup    = LocalProcurementGroup
                related.ParticipantGroup    = LocalParticipantGroup 

        ParticipantGroupLineRel 
            one-to-many relation to ParticipantGroupLine  
            Field Mapping uses ByLocReqLocSet
                related.ProcurementGroup                                            = LocalProcurementGroup
                related.ParticipantGroup                                            = LocalParticipantGroup 
                related.ParticipantGroupLine.ParticipantLocation.Company            = LocalCompany 
                related.ParticipantGroupLine.ParticipantLocation.Location           = LocalRequestingLocation
                related.ParticipantGroupLine.ParticipantLocation.RequestingLocation = blank

        ParticipantGroupLineReqLocation
            one-to-many relation to ParticipantGroupLine  
            Field Mapping uses ByLocReqLocSet
                related.ProcurementGroup                                            = LocalProcurementGroup
                related.ParticipantGroup                                            = LocalParticipantGroup 
                related.ParticipantGroupLine.ParticipantLocation.Company            = LocalCompany 
                related.ParticipantGroupLine.ParticipantLocation.Location           = blank 
                related.ParticipantGroupLine.ParticipantLocation.RequestingLocation = LocalRequestingLocation

        RequestingReportGroupLocationRel 
            one-to-many relation to RequestingReportGroupLocation
            delete cascades
            Field Mapping uses symbolic key 
                related.Company                 = Company 
                related.RequestingReportGroup   = RequestingReportGroup 

        LocalParticipantGroupReportGroupRel 
            one-to-many relation to ParticipantGroupReportGroup 
            Field Mapping uses ByRequestingLocationReportGroup
                related.Company                  = Company 
                related.RequestingReportGroup    = RequestingReportGroup
                related.ProcurementGroup         = LocalProcurementGroup
                related.ParticipantGroup         = LocalParticipantGroup

        ParticipantGroupReportGroupRel 
            one-to-many relation to ParticipantGroupReportGroup 
            Field Mapping uses ByRequestingLocationReportGroup
                related.Company                  = Company 
                related.RequestingReportGroup    = RequestingReportGroup
	Field Rules
		Name
			required
			
	Actions
		Create is a Create Action
	
		Update is an Update Action
	
        Delete is a Delete Action

        CreateOrUpdateParticipantGroup is an Instance Action 
            Parameters 
                ParmProcurementGroup        is a ProcurementGroup  
                    default label is "ProcurementGroup"
                ParmParticipantGroup        is a ParticipantGroup  
                    default label is "SelectAParticipantGroupHereIfAddingToAnExistingOne"
                ParmNewParticipantGroup     is like ParticipantGroup 
                    default label is "EnterAParticipantGroupHereIfCreatingANewOne"
                ParmNewGroupTitle           is a Description 
                    default label is "EnterATitleHereIfANewGroup"
                ParmNewGroupDescription     is a Description4 
                    default label is "EnterADescriptionHereIfANewGroup"

            Parameter Rules 

                ParmProcurementGroup 
                    initial value is Company.PurchasingCompanyRel.ProcurementGroup

                ParmNewGroupTitle 
                    initial value is Name  

            Action Rules 

                LocalProcurementGroup               = ParmProcurementGroup
                LocalCompany                        = Company 
                if (ParmNewParticipantGroup entered)
                    LocalParticipantGroup           = ParmNewParticipantGroup
                else 
                    LocalParticipantGroup           = ParmParticipantGroup
                if (ParmParticipantGroup entered)
                    constraint (ParmNewParticipantGroup !entered)
                        "CannotEnterBothANewAndExistingParticipantGroup"
                    constraint (ParticipantGroupRel.LocationType)
                        "MustUseAParticipantGroupThatHasLocations"
                    constraint (!ParticipantGroupRel.MemberGroup)
                        "CannotUseAMemberGroup"
                if (ParmParticipantGroup !entered)
                    constraint (ParmNewParticipantGroup entered)
                        "MustEnterEitherANewOrExistingParticipantGroup"
                if (ParmNewParticipantGroup !entered)
                    constraint (ParmParticipantGroup entered)
                        "MustEitherEnterANewOrExistingParticipantGroup"
                if (ParmNewParticipantGroup entered)
                    constraint (ParmNewGroupTitle entered)
                        "MustEnterATitleIfCreatingANewParticipantGroup"
                    constraint (ParmNewGroupDescription entered)
                        "MustEnterADescriptionIfCreatingANewParticipantGroup"
                    constraint (ParticipantGroupRel !exists)
                        "ParticipantGroupAlreadyExists" 
                    invoke Create ParticipantGroup 
                        invoked.ProcurementGroup        = ParmProcurementGroup
                        invoked.ParticipantGroup        = ParmNewParticipantGroup
                        invoked.ParticipantGroupTitle   = ParmNewGroupTitle
                        invoked.Description             = ParmNewGroupDescription
                    invoke Create ParticipantGroupReportGroup 
                        invoked.ProcurementGroup        = ParmProcurementGroup
                        invoked.ParticipantGroup        = ParmNewParticipantGroup
                        invoked.Company                 = Company
                        invoked.RequestingReportGroup   = RequestingReportGroup

                    for each RequestingReportGroupLocationRel
						invoke Create ParticipantGroupLine 
                            invoked.ProcurementGroup													= ParmProcurementGroup
							invoked.ParticipantGroup													= ParmNewParticipantGroup
							invoked.ParticipantGroupLine.ParticipantLocation.Company					= Company	
							invoked.ParticipantGroupLine.ParticipantLocation.RequestingLocation  		= each.RequestingLocation	
                
                else 
                if (ParmParticipantGroup entered)
                    LocalParticipantGroup       = ParmParticipantGroup
                    if (LocalParticipantGroupReportGroupRel !exists)
                        invoke Create ParticipantGroupReportGroup 
                            invoked.ProcurementGroup        = ParmProcurementGroup
                            invoked.ParticipantGroup        = ParmParticipantGroup
                            invoked.Company                 = Company 
                            invoked.RequestingReportGroup   = RequestingReportGroup
                    for each RequestingReportGroupLocationRel                    
                        LocalRequestingLocation = each.RequestingLocation 
                        if (ParticipantGroupLineRel !exists
                        and ParticipantGroupLineReqLocation !exists)
                            invoke Create ParticipantGroupLine 
                                invoked.ProcurementGroup													= ParmProcurementGroup
                                invoked.ParticipantGroup													= ParmParticipantGroup
                                invoked.ParticipantGroupLine.ParticipantLocation.Company					= Company	
                                invoked.ParticipantGroupLine.ParticipantLocation.RequestingLocation 		= each.RequestingLocation	


                 
                        
