AssetManagementInterfaceResult is a BusinessClass
	owned by am
	prefix is AMIR
	Ontology
		symbolic key is AssetManagementInterfaceResult
		
	Persistent Fields
		ComputeOption               is a Compute
		RunGroup
		RunTime						is TimeStamp
		Status						is Numeric 1
			States
				Processing	value is 1
				Completed	value is 2


        Company						is an AssetCompany
        	classic name is PRM-COMPANY
        CompanyGroup				is a GeneralLedgerCompanyGroup
        	classic name is PRM-COMPANY-GROUP
        LeaseCompany				is a GeneralLedgerCompany 
        	classic name is PRM-LSE-COMPANY
        Lease
        	context of LeaseCompany
        	classic name is PRM-LSE-LEASE
        ErrorCount					is Numeric size 6
        ErrorLimit					is Numeric size 6
        	classic name is PRM-ERROR-LIMIT
        FilesToConvert				is AlphaUpper size 1
        	classic name is PRM-FILES-TO-CONVERT
            States
                All					value is "1"
                AssetItemBook		value is "2"
                    default label is "Asset/Item/Book"
                TransactionsOnly	value is "4"
                CommentsOnly		value is "5"
        FromRange					is AlphaUpper size 22
        	classic name is PRM-FR-RANGE
        ToRange						is AlphaUpper size 22
        	classic name is PRM-TO-RANGE
        AttributeBy					is AlphaUpper size 1
        	classic name is PRM-ATTRIBUTE-BY
            States
                ByAsset				value is "A"
                    default label is "by Asset"
                ByConversionNumber	value is "C"
                    default label is "by Conversion Number"

	Field Rules
		ComputeOption
			constraint(!ComputeOption = "P")
				"ComputeOptionCanNotBeProspectiveForImportedAssets"

		FinanceEnterpriseGroup
			required
			
		RunGroup
			required
			
        RunTime
            default to current timestamp 

		Status
			initial value is Status.Processing
			default to Status.Processing

        ErrorLimit
        	initial value is 000100
            default to 000100
        FilesToConvert
        	initial value is FilesToConvert.All
            default to FilesToConvert.All

		LeaseCompany
			if (LeaseCompany entered)
				constraint (Lease entered)
					"IfLeaseCompanyIsEnteredThenLeaseIsAlsoRequired" 

		AttributeBy
			if (FilesToConvert.All)
				required 
				constraint (AttributeBy.ByConversionNumber)
					"ConversionMethodMustBeByConversionNumberIfSelectAllFiles" 
		
			if (FilesToConvert.TransactionsOnly)
				required
				
			if (FilesToConvert.CommentsOnly)
				required
				constraint (AttributeBy.ByAsset)
					"ConversionMethodMustBeByAssetIfCommentsOnly"

			if (FilesToConvert.AssetItemBook
			and AttributeBy entered)
				cannot be entered
					"AttributeByCannotBeEnteredWhenFilesToConvertIsAssetItemBook"

		FromRange
			if (ToRange entered)
				required
			constraint (FromRange <= ToRange)
				"FromRangeMustBeLessThanOrEqualToToRange"
				
		ToRange
			if (FromRange entered)
				required
			
	Relations
		AssetRel
			one-to-many relation to Asset
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult
				
		AssetItemRel
			one-to-many relation to AssetItem
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult

		AssetBookRel        
			one-to-many relation to AssetBook
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult

    	AssetTransactionRel
			one-to-many relation to AssetTransaction
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult

		AssetBookTransactionRel
			one-to-many relation to AssetBookTransaction
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult

		AssetCommentRel
			one-to-many relation to AssetComment
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult
		
		AssetFundRel
			one-to-many relation to AssetFund
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult
		
		AssetItemFundRel
			one-to-many relation to AssetItemFund
			Field Mapping uses ByAssetManagementInterfaceResult	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.AssetManagementInterfaceResult	= AssetManagementInterfaceResult
		
		GeneralLedgerCompanyGroupMemberRel
			one-to-many relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup		= CompanyGroup

		RunningDuplicateRel
			one-to-many relation to AssetManagementInterfaceResult
			Field Mapping uses ByRunGroupStatus
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.RunGroup						= RunGroup
				related.Status							= Status.Processing
		
	Conditions
		Incomplete
			when (!Status.Completed)
		ShowErrorLimitExceeded
			when (ErrorCount > ErrorLimit)
		IsNoRunningDuplicate
			when (RunningDuplicateRel not exists)

		FundAccounting
			when (FinanceEnterpriseGroup.FundAccounting)
			
		ShowAssetsPanel
			when (FilesToConvert.All
			or    FilesToConvert.AssetItemBook)
		ShowItemsPanel
			when (FilesToConvert.All
			or    FilesToConvert.AssetItemBook)
		ShowBooksPanel
			when (FilesToConvert.All
			or    FilesToConvert.AssetItemBook)
		ShowCommentsPanel
			when (FilesToConvert.All
			or    FilesToConvert.CommentsOnly)
		ShowAssetTransactionsPanel
			when (FilesToConvert.All
			or    FilesToConvert.TransactionsOnly)
		ShowAssetBookTransactionsPanel
			when (FilesToConvert.All
			or    FilesToConvert.TransactionsOnly)
				
	Derived Fields
		ErrorLimitExceeded is a MessageField 
        	"ErrorLimit<ErrorLimit>Exceeded"

		NumAssetRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetRel
			
		NumAssetItemRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetItemRel
			
		NumAssetBookRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetBookRel
			
		NumAssetTransactionRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetTransactionRel
			
		NumAssetBookTransactionRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetBookTransactionRel
		
		NumAssetCommentRel is a DerivedField
			type is Numeric 9
			restricted
			return instance count of AssetCommentRel
		
		NumAssetFundRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetFundRel
		
		NumAssetItemFundRel is a DerivedField
			type is Numeric 9
        	restricted
			return instance count of AssetItemFundRel
		
		ConcatenatedKey is a StringField
			type is Alpha 50
			FinanceEnterpriseGroup
			"-"
			RunGroup
			"-"
			AssetManagementInterfaceResult

		BackgroundGroupKey is a StringField
			type is Alpha 150
			"AssetManagementInterfaceResult-AssetInterface"
			"-"
			ConcatenatedKey
			"-"
			
	Sets
		ByRunGroupStatus
			indexed
			Sort Order
				FinanceEnterpriseGroup
				RunGroup
				Status
				AssetManagementInterfaceResult
		
	Local Fields
		AssetBackgroundGroup		is AlphaUpper up to 200
		LastBackgroundGroup			is AlphaUpper up to 200

	Transient Fields	
		TransientAllowZeroLifeRemaining		is Boolean

	Actions
		AssetInterface is a Create Action
			completion message is "AssetInterfaceSubmitted"
			Entrance Rules
				constraint (IsNoRunningDuplicate)
					"AssetInterfaceAlreadyRunningForRunGroup"
			Action Rules
				if (Company not entered
				and CompanyGroup not entered
				and LeaseCompany not entered)
					constraint (false)
						"MustEnterCompanyOrCompanyGroupOrLeaseCompany" 
				
				if ((Company entered
				and  CompanyGroup entered)
				or  (Company entered
				and  LeaseCompany entered)
				or  (CompanyGroup entered
				and  LeaseCompany entered))
					constraint (false)
						"CanOnlyEnterOneOfCompany,CompanyGroup,OrLease" 
				
			Exit Rules
				Status = Status.Processing

				if (FilesToConvert.All
				or  FilesToConvert.AssetItemBook)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase1"

					if (LeaseCompany entered)
						invoke InterfaceAssetsByLeaseCompany AssetImport in background group(AssetBackgroundGroup)
							invoked.PrmRunGroup 						= RunGroup
							invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
							invoked.PrmLeaseCompany						= LeaseCompany
							invoked.PrmLease							= Lease
							invoked.PrmFromRange						= FromRange
							invoked.PrmToRange							= ToRange
							invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					else
						if (Company entered)
							invoke InterfaceAssetsByCompany AssetImport in background group(AssetBackgroundGroup)
								invoked.PrmRunGroup 						= RunGroup
								invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
								invoked.PrmCompany							= Company
								invoked.PrmFromRange						= FromRange
								invoked.PrmToRange							= ToRange
								invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
						else
							for each GeneralLedgerCompanyGroupMemberRel
								invoke InterfaceAssetsByCompany AssetImport in background group(AssetBackgroundGroup)
									invoked.PrmRunGroup 						= RunGroup
									invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
									invoked.PrmCompany							= each.Company
									invoked.PrmFromRange						= FromRange
									invoked.PrmToRange							= ToRange
									invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult

					LastBackgroundGroup						= AssetBackgroundGroup


					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase1B"
					invoke FixAssetItems AssetItemImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					invoke FixAssetBooks AssetBookImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					invoke FixAssetComments AssetCommentImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					invoke FixAssetTransactions AssetTransactionImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
						
					LastBackgroundGroup						= AssetBackgroundGroup



					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase2"

					invoke InterfaceAssetItems AssetItemImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmFromRange						= FromRange
						invoked.PrmToRange							= ToRange
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult

					LastBackgroundGroup						= AssetBackgroundGroup

					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase3"

					invoke InterfaceAssetBooks AssetBookImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmFromRange						= FromRange
						invoked.PrmToRange							= ToRange
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
						invoked.PrmComputeOption                    = ComputeOption
						invoked.PrmAllowZeroLifeRemaining    	    = TransientAllowZeroLifeRemaining

					LastBackgroundGroup						= AssetBackgroundGroup


					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase4"
					invoke InterfaceAssetComments AssetCommentImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmFromRange						= FromRange
						invoked.PrmToRange							= ToRange
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult

					LastBackgroundGroup						= AssetBackgroundGroup


				if (FilesToConvert.All
				or  FilesToConvert.TransactionsOnly)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase5"
					if (LeaseCompany entered)
						invoke InterfaceAssetTransactionsByLeaseCompany AssetTransactionImport in background group(AssetBackgroundGroup)
							run after background group (LastBackgroundGroup)
							invoked.PrmRunGroup 						= RunGroup
							invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
							invoked.PrmLeaseCompany						= LeaseCompany
							invoked.PrmLease							= Lease
							invoked.PrmFromRange						= FromRange
							invoked.PrmToRange							= ToRange
							invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					else
						if (Company entered)
							invoke InterfaceAssetTransactionsByCompany AssetTransactionImport in background group(AssetBackgroundGroup)
								run after background group (LastBackgroundGroup)
								invoked.PrmRunGroup 						= RunGroup
								invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
								invoked.PrmCompany							= Company
								invoked.PrmFromRange						= FromRange
								invoked.PrmToRange							= ToRange
								invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
						else
							for each GeneralLedgerCompanyGroupMemberRel
								invoke InterfaceAssetTransactionsByCompany AssetTransactionImport in background group(AssetBackgroundGroup)
									run after background group (LastBackgroundGroup)
									invoked.PrmRunGroup 						= RunGroup
									invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
									invoked.PrmCompany							= each.Company
									invoked.PrmFromRange						= FromRange
									invoked.PrmToRange							= ToRange
									invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
						
					LastBackgroundGroup						= AssetBackgroundGroup

		
				if (FilesToConvert.CommentsOnly)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase6"
					if (LeaseCompany entered)
						invoke InterfaceAssetCommentsByLeaseCompany AssetCommentImport in background group(AssetBackgroundGroup)
							run after background group (LastBackgroundGroup)
							invoked.PrmRunGroup 						= RunGroup
							invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
							invoked.PrmLeaseCompany						= LeaseCompany
							invoked.PrmLease							= Lease
							invoked.PrmFromRange						= FromRange
							invoked.PrmToRange							= ToRange
							invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					else
						if (Company entered
						or  CompanyGroup entered)
							invoke InterfaceAssetCommentsByCompany AssetCommentImport in background group(AssetBackgroundGroup)
								run after background group (LastBackgroundGroup)
								invoked.PrmRunGroup 						= RunGroup
								invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
								invoked.PrmCompany							= Company
								invoked.PrmCompanyGroup						= CompanyGroup
								invoked.PrmFromRange						= FromRange
								invoked.PrmToRange							= ToRange
								invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
						
					LastBackgroundGroup						= AssetBackgroundGroup

				if (FilesToConvert.All
				or  FilesToConvert.AssetItemBook)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase7"	

					invoke DeleteImportedRecords AssetItemImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
				
					invoke DeleteImportedRecords AssetBookImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult






								
					if (FilesToConvert.All)
						invoke DeleteImportedRecords AssetTransactionImport in background group(AssetBackgroundGroup)
							run after background group (LastBackgroundGroup)
							invoked.PrmRunGroup 						= RunGroup
							invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
							invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
					
						invoke DeleteImportedRecords AssetCommentImport in background group(AssetBackgroundGroup)
							run after background group (LastBackgroundGroup)
							invoked.PrmRunGroup 						= RunGroup
							invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
							invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult

					LastBackgroundGroup						= AssetBackgroundGroup


				if (FilesToConvert.TransactionsOnly)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase8"

					invoke DeleteImportedRecords AssetTransactionImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult

					LastBackgroundGroup						= AssetBackgroundGroup
					
				if (FilesToConvert.CommentsOnly)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase9"

					invoke DeleteImportedRecords AssetCommentImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult

					LastBackgroundGroup						= AssetBackgroundGroup

				if (FilesToConvert.All
				or  FilesToConvert.AssetItemBook)
					AssetBackgroundGroup	 				= BackgroundGroupKey + "Phase10"

					invoke DeleteImportedRecords AssetImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
				
					invoke DeleteRecordsInError AssetImport in background group(AssetBackgroundGroup)
						run after background group (LastBackgroundGroup)
						invoked.PrmRunGroup 						= RunGroup
						invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
						invoked.PrmAssetManagementInterfaceResult	= AssetManagementInterfaceResult
				
					LastBackgroundGroup						= AssetBackgroundGroup


				invoke MakeTransitionToCompleted in background
					run after background group (LastBackgroundGroup)


		
		MakeTransitionToCompleted is an Instance Action
			restricted
			run in background
			Action Rules
				Status = Status.Completed
		
		IncrementErrorCount is an Instance Action
			restricted
			Action Rules
				ErrorCount += 1
		
		Delete is a Delete Action
			valid when (!Incomplete)
		
		Purge is a Purge Action
			default label is "Delete"
			valid when (!Incomplete)
