POInterfaceResultDetail is a BusinessClass
	owned by po
	prefix is POIRD
	
	Ontology
		symbolic key is POInterfaceResultDetail	
	Patterns
        disable AuditIndex
       	disable EffectiveDated
       	disable Auditing
	
	Persistent Fields
		Company						is a GeneralLedgerCompany

        SourceDocumentOrigin
        SourceDocumentNumeric
        SourceDocumentAlpha
        OperatingCompany 			is like InventoryCompany
        RequestingLocation
        LineNumber
		Quantity					is an UnsignedQuantity
		SourceUOM					is a UnitOfMeasure
		DeliverTo
		RecordType					is Numeric size 1
			States
				Processed					value is 0
				Rejected					value is 1
				Error						value is 2
				Summary						value is 3

		PurchaseOrder				is like PurchaseOrder
		PurchaseOrderLine			is like PurchaseOrderLine
		Buyer						is like Buyer
		Vendor
		PurchaseFromLocation		is a VendorLocation
		Dropship					is Boolean
		BlanketContract				is a Contract
		Item
        ItemType
        ShipToLocation				is an InventoryLocation
        OrderedQuantity				is an UnsignedQuantity
        EnteredUOM					is a UnitOfMeasure
        EnteredUnitCost				is an UnsignedUnitCost
        	precision is DerivedNumberOfDecimalsCost
        VendorItem
        Currency
        EarlyDeliveryDate			is Date
        LateDeliveryDate			is Date

		ProcessedCount				is Numeric 9
			default label is "ProcessedRequests"
		RejectedCount				is Numeric 9
			default label is "RejectedRequests"
		PurchaseOrderCount			is Numeric 9
			default label is "CreatedPurchaseOrders"        
		ErrorMessage				is Alpha 90

	Transient Fields

		TransientPurchaseOrder is a PurchaseOrder
			derive value from DerivedPurchaseOrder	

		TransientPurchaseOrderLine is a PurchaseOrderLine
			derive value from DerivedPurchaseOrderLine	

		TransientPurchaseOrderInterface is a PurchaseOrderInterface
			derive value from PurchaseOrderInterfaceRel.PurchaseOrderInterface


    Derived Fields
        DerivedSourceDocument       	is a DerivedField
            type is AlphaUpper size 10
            classic name is SOURCE-DOC
            if (SourceDocumentOrigin.InventoryControl)
                return SourceDocumentAlpha
            else
	            return SourceDocumentNumeric

        DerivedCompanyPurchaseOrdeCount	is a DerivedField
			type is Numeric size 9
			default label is "PurchaseOrderCount"
			return instance count of POInterfaceResultDetailCompanyPORel
			
        DerivedCompanyRejectCount		is a DerivedField
			type is Numeric size 9
			default label is "RejectedRequests"
			return instance count of POInterfaceResultDetailCompanyRejectRel

		DerivedNumberOfDecimalsCost is a ConditionalField
			type is Numeric size 1
			restricted
			if (ItemType.NonStock
			or  ItemType.Inventoried)
				Item.NumberOfDecimalsCost
			else
			if (ItemType.Special
			or  ItemType.Service)
				InventoryCompanyRel.NumberOfDecimalsCost
			else
				8


		DerivedPurchaseOrder is a DerivedField
			type is Numeric size 13
			return PurchaseOrderRel.PurchaseOrder
		DerivedPurchaseOrderLine is a DerivedField
			type is Numeric size 9
			return PurchaseOrderLineRel.PurchaseOrderLine


	Field Rules

	Sets
		ByCompany
			Sort Order
				FinanceEnterpriseGroup
				POInterfaceResult
				Company
				Buyer		
				Vendor
				PurchaseFromLocation
				PurchaseOrder
				PurchaseOrderLine
				POInterfaceResultDetail

		ByBuyer
			Sort Order
				FinanceEnterpriseGroup
				POInterfaceResult
				Buyer		
				Company
				Vendor
				PurchaseFromLocation
				PurchaseOrder
				PurchaseOrderLine
				POInterfaceResultDetail

		ByVendor
			Sort Order
				FinanceEnterpriseGroup
				POInterfaceResult
				Vendor
				PurchaseFromLocation
				Buyer		
				Company
				PurchaseOrder
				PurchaseOrderLine
				POInterfaceResultDetail

	Relations
		POInterfaceResultDetailCompanyPORel
            one-to-many relation to POInterfaceResultDetail
            Field Mapping uses ByCompany
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.POInterfaceResult			= POInterfaceResult
				related.Company						= Company
			Instance Selection
				where (related.IsPurchaseOrderHeader)

		POInterfaceResultDetailCompanyRejectRel
            one-to-many relation to POInterfaceResultDetail
            Field Mapping uses ByCompany
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.POInterfaceResult			= POInterfaceResult
				related.Company						= Company
			Instance Selection
				where (related.IsRejected)

		PurchaseOrderRel
            one-to-many relation to PurchaseOrder
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder      		= PurchaseOrder

		PurchaseOrderLineRel
            one-to-many relation to PurchaseOrderLine
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder      		= PurchaseOrder
                related.PurchaseOrderLine   	= PurchaseOrderLine

		RequisitionRel
			one-to-one relation to Requisition
			valid when (FromRequisitions)
			Field Mapping uses symbolic key
				related.Company			= OperatingCompany
				related.Requisition		= SourceDocumentNumeric

		SourcePOInterfaceResultDetailRel
            one-to-many relation to POInterfaceResultDetail
            Field Mapping uses ByCompany
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.POInterfaceResult			= POInterfaceResult
				related.Company						= Company
			Instance Selection
				where (related.RecordType.Processed
				and    related.PurchaseOrder		= PurchaseOrder
				and	   related.PurchaseOrderLine	= PurchaseOrderLine)

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = Company


		ProcessedPOInterfaceResultDetailRel
            one-to-many relation to POInterfaceResultDetail
            Field Mapping uses symbolic key




		PurchaseOrderInterfaceRel
        	one-to-one relation to PurchaseOrderInterface
        	Field Mapping uses symbolic key
        		related.Company										 = Company
        		related.PurchaseOrderInterface.SourceDocumentOrigin  = SourceDocumentOrigin
        		related.PurchaseOrderInterface.SourceDocumentNumeric = SourceDocumentNumeric
        		related.PurchaseOrderInterface.SourceDocumentAlpha	 = SourceDocumentAlpha
        		related.PurchaseOrderInterface.OperatingCompany		 = OperatingCompany
        		related.PurchaseOrderInterface.RequestingLocation	 = RequestingLocation
        		related.PurchaseOrderInterface.LineNumber			 = LineNumber

	
	Conditions
		HasException
			when (POInterfaceResult.RejectedCount entered)

		HasRequisition
			restricted
			when (RequisitionRel exists)

        FromRequisitions
            classic name is REQUISITIONS
            restricted
            when (SourceDocumentOrigin.Requisitions)

		HasPurchaseOrderLineDetails
			restricted
			when (RecordType.Processed
			and	  Item entered)

		IsProcessed
			restricted
			when (RecordType.Processed)

		IsSummary
			restricted
			when (RecordType.Summary)

		IsRejected
			restricted
			when (RecordType.Rejected)

		IsPurchaseOrderHeader
			restricted
			when (RecordType.Processed
			and	  PurchaseOrderLine	= 1)

	Actions	
		Create is a Create Action
			restricted
			Action Rules

		Update is an Update Action
			restricted
				
		Delete is a Delete Action
			restricted			
