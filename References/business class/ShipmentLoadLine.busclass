ShipmentLoadLine is a BusinessClass 
    owned by wh
    prefix is SHLL

    Ontology
        symbolic key is ShipmentLoadLine

    Patterns 
        disable AuditIndex
    
    Persistent Fields
        ContainerNumber                     is Numeric size 6  
            protected
        ShipmentLoadContainer
        Item
        Quantity                            
            precision is Item.NumberOfDecimalsQuantity
        UnitOfMeasure
        TransactionUOMMultiplier         is a UOMMultiplier
        StockVolume
        StockWeight

    Derived Fields
        ShipmentLoadContainerFilledCount          is a DerivedField
            type is like ShipmentLoadContainer
            restricted
            return instance count of ShipmentLoadContainerFilledCountRel
        
        ShipmentLoadContainerCount                 is a DerivedField
            type is like ShipmentLoadContainer
            restricted
            return instance count of UpdateShipmentLoadContainerRel
        
        ShipmentLoadLineCount                 is a DerivedField
            type is like ShipmentLoadLine
            restricted
            return instance count of ShipmentLoadLineRel
        
        DerivedQuantityInTransUOM            is a DerivedField
            type is like Quantity
                precision is Item.NumberOfDecimalsQuantity
            default label is "Quantity"
            return Quantity / TransactionUOMMultiplier
        
        DerivedStockVolumetInTransUOM            is a DerivedField
            type is like StockVolume
                precision is Item.NumberOfDecimalsQuantity
            default label is "Volume"
            return StockVolume / TransactionUOMMultiplier
        
        DerivedStockWeightInTransUOM            is a DerivedField
            type is like StockWeight
                precision is Item.NumberOfDecimalsQuantity
            default label is "Weight"
            return StockWeight / TransactionUOMMultiplier

    Local Fields
        LocalCompany                    is like InventoryCompany
        LocalInventoryLocation          is like InventoryLocation
        LocalShipmentLoadConsolidation  is like ShipmentLoadConsolidation
        LocalWarehouseShipment          is like WarehouseShipment
        LocalWarehouseShipmentLine      is like WarehouseShipmentLine
        LocalShipmentLoadContainer      is a ShipmentLoadContainer
    Conditions
        RecordExists
            restricted
            when (ShipmentLoadLine exists)

        NoStockWeightAndVolume
            restricted
            when (StockVolume not entered or StockWeight not entered)
    
    Field Groups
        ShipmentLoadLineKeyFields
            Company
            InventoryLocation
            ShipmentLoadConsolidation
            WarehouseShipment
            
    Relations  
        ShipmentLoadLineTemplateRel
            one-to-one relation to ShipmentLoadLineTemplate
            Field Mapping uses ByWarehouseShipmentLine
                related.Company               = Company
                related.InventoryLocation     = InventoryLocation
                related.WarehouseShipment     = WarehouseShipment
                related.WarehouseShipmentLine = WarehouseShipmentLine
        
        UpdateShipmentLoadLineTemplateRel
            one-to-one relation to ShipmentLoadLineTemplate
            Field Mapping uses ByWarehouseShipmentLine
                related.Company               = LocalCompany
                related.InventoryLocation     = LocalInventoryLocation
                related.WarehouseShipment     = LocalWarehouseShipment
                related.WarehouseShipmentLine = LocalWarehouseShipmentLine
              
        ShipmentLoadLineRel
            one-to-many relation to ShipmentLoadLine
            Field Mapping uses ByShipmentLine
                related.Company                        = Company
                related.InventoryLocation              = InventoryLocation
                related.ShipmentLoadConsolidation      = ShipmentLoadConsolidation
                related.WarehouseShipment              = WarehouseShipment
                related.WarehouseShipmentLine          = WarehouseShipmentLine

        ShipmentLoadContainerFilledCountRel
            one-to-many relation to ShipmentLoadContainer
            Field Mapping uses symbolic key
                related.Company                        = LocalCompany
                related.InventoryLocation              = LocalInventoryLocation
                related.ShipmentLoadConsolidation      = LocalShipmentLoadConsolidation
            Instance Selection
                where (related.LoadedContainer)
        
        UpdateShipmentLoadContainerRel
            one-to-many relation to ShipmentLoadContainer
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.InventoryLocation            = InventoryLocation
                related.ShipmentLoadConsolidation    = ShipmentLoadConsolidation 
        
        DeleteShipmentLoadContainerRel
            one-to-one relation to ShipmentLoadContainer
            Field Mapping uses symbolic key
                related.Company                      = LocalCompany
                related.InventoryLocation            = LocalInventoryLocation
                related.ShipmentLoadConsolidation    = LocalShipmentLoadConsolidation 
                related.ShipmentLoadContainer        = LocalShipmentLoadContainer
        
        UpdateShipmentLoadConsolidationRel
            one-to-one relation to  ShipmentLoadConsolidation
            Field Mapping uses symbolic key
                related.Company                          = LocalCompany
                related.InventoryLocation                = LocalInventoryLocation
                related.ShipmentLoadConsolidation        = LocalShipmentLoadConsolidation
    
    Sets
        ByShipmentLoadConsolidation
            indexed
            Sort Order
                Company
                InventoryLocation
                ShipmentLoadConsolidation
                WarehouseShipment
                ShipmentLoadContainer
                WarehouseShipmentLine
                ShipmentLoadLine

        ByShipmentLine
            indexed
            Sort Order
                Company
                InventoryLocation
                ShipmentLoadConsolidation
                WarehouseShipment
                WarehouseShipmentLine
                ShipmentLoadLine

        ByWarehouseShipmentLine
            indexed
            Sort Order
                Company
                InventoryLocation
                WarehouseShipment
                WarehouseShipmentLine
                ShipmentLoadLine

	Field Rules
        WarehouseShipment
            required

        WarehouseShipmentLine
            required

        Item
            required

        Quantity
            required
                "CannotAdd_Line<WarehouseShipmentLine>;_CannotSplitQuantityCleanlyByContainerCapacity"

        UnitOfMeasure
            required
    
    Rule Blocks
        SaveCurrentShipmentLoadLine
            initialize LocalCompany
            initialize LocalInventoryLocation
            initialize LocalWarehouseShipment
            initialize LocalWarehouseShipmentLine
            LocalCompany                = Company
            LocalInventoryLocation      = InventoryLocation
            LocalWarehouseShipment      = WarehouseShipment
            LocalWarehouseShipmentLine  = WarehouseShipmentLine


	Actions		
        Create is a Create Action

        Update is an Update Action
            valid when (ShipmentLoadConsolidation.Status.Unreleased)

        Delete is a Delete Action
           
        RemoveShipmentLines is an Instance Action 
            default label is "Remove_Shipment_Lines"
            valid when (ShipmentLoadConsolidation.Status.Unreleased)
            completion message is "Removing_Shipment_LinesFrom_Load_Number<ShipmentLoadConsolidation>"
            confirmation required
                "ThisActionWillRemoveTheShipmentLineFromAllContainers;Proceed?"
            Action Rules
                include SaveCurrentShipmentLoadLine

                if (ShipmentLoadLineCount > 50)
                    invoke DeleteShipmentLoadLine ShipmentLoadLineRel
                        invoked.PrmCompany                   = Company
                        invoked.PrmInventoryLocation         = InventoryLocation
                        invoked.PrmShipmentLoadConsolidation = ShipmentLoadConsolidation
                        invoked.PrmWarehouseShipment         = WarehouseShipment
                        invoked.PrmWarehouseShipmentLine     = WarehouseShipmentLine

                else
                    invoke DeleteShipmentLoadLine ShipmentLoadLineRel in foreground
                        invoked.PrmCompany                   = Company
                        invoked.PrmInventoryLocation         = InventoryLocation
                        invoked.PrmShipmentLoadConsolidation = ShipmentLoadConsolidation
                        invoked.PrmWarehouseShipment         = WarehouseShipment
                        invoked.PrmWarehouseShipmentLine     = WarehouseShipmentLine
               

        DeleteShipmentLoadLine is a Set Action
            restricted
            Parameters
                PrmCompany                   is like Company
				PrmInventoryLocation         is like InventoryLocation
				PrmShipmentLoadConsolidation is like ShipmentLoadConsolidation
                PrmWarehouseShipment         is like WarehouseShipment
                PrmWarehouseShipmentLine     is like WarehouseShipmentLine
            Instance Selection
                where (Company                   = PrmCompany
                and    InventoryLocation         = PrmInventoryLocation
                and    ShipmentLoadConsolidation = PrmShipmentLoadConsolidation
                and    WarehouseShipment         = PrmWarehouseShipment
                and    WarehouseShipmentLine     = PrmWarehouseShipmentLine)
            Action Rules
                Set Rules
                    Exit Rules
                        LocalCompany                        = PrmCompany
                        LocalInventoryLocation              = PrmInventoryLocation
                        LocalShipmentLoadConsolidation      = PrmShipmentLoadConsolidation
                        LocalWarehouseShipment              = PrmWarehouseShipment
                        LocalWarehouseShipmentLine          = PrmWarehouseShipmentLine
                        invoke Update UpdateShipmentLoadLineTemplateRel
                            initialize invoked.ShipmentLoadConsolidation   
                            initialize invoked.AddShipmentLine       

                        invoke Update UpdateShipmentLoadConsolidationRel
                            invoked.NumberOfFilledContainerUnits = ShipmentLoadContainerFilledCount
                Instance Rules
                    
                    LocalShipmentLoadContainer = ShipmentLoadContainer
                    invoke Delete
                    invoke FastUpdate LocalShipmentLoadContainer
                        invoked.RemainingWeightCapacity += StockWeight
                        invoked.RemainingVolumeCapacity += StockVolume
        
        DeleteShipmentLoadLineContainer is a Set Action
            restricted
            Parameters
                PrmCompany                   is like Company
				PrmInventoryLocation         is like InventoryLocation
				PrmShipmentLoadConsolidation is like ShipmentLoadConsolidation
                PrmWarehouseShipment         is like WarehouseShipment
                PrmWarehouseShipmentLine     is like WarehouseShipmentLine
                PrmShipmentLoadContainer     is like ShipmentLoadContainer
            Instance Selection
                where (Company                   = PrmCompany
                and    InventoryLocation         = PrmInventoryLocation
                and    ShipmentLoadConsolidation = PrmShipmentLoadConsolidation
                and    WarehouseShipment         = PrmWarehouseShipment
                and    WarehouseShipmentLine     = PrmWarehouseShipmentLine)
            Action Rules
                Set Rules
                    Exit Rules  
                        LocalCompany                    = PrmCompany
                        LocalInventoryLocation          = PrmInventoryLocation
                        LocalShipmentLoadConsolidation  = PrmShipmentLoadConsolidation
                        LocalShipmentLoadContainer      = PrmShipmentLoadContainer
                        LocalWarehouseShipment          = PrmWarehouseShipment
                        LocalWarehouseShipmentLine      = PrmWarehouseShipmentLine

                        invoke Delete DeleteShipmentLoadContainerRel
                        invoke Update UpdateShipmentLoadLineTemplateRel
                            initialize invoked.ShipmentLoadConsolidation   
                            initialize invoked.AddShipmentLine       

                        invoke Update UpdateShipmentLoadConsolidationRel
                            invoked.NumberOfFilledContainerUnits = ShipmentLoadContainerFilledCount
                Instance Rules
                    invoke Delete 
                    invoke FastUpdate LocalShipmentLoadContainer
                        invoked.RemainingWeightCapacity += StockWeight
                        invoked.RemainingVolumeCapacity += StockVolume

        DeleteShipmentLoadLineWithContainer is an Instance Action 
            restricted
            Parameters
                PrmShipmentLoadContainer        is like ShipmentLoadContainer
            Action Rules
                
                include SaveCurrentShipmentLoadLine
                if (ShipmentLoadLineCount > 50)
                    invoke DeleteShipmentLoadLineContainer ShipmentLoadLineRel 
                        invoked.PrmCompany                   = Company
                        invoked.PrmInventoryLocation         = InventoryLocation
                        invoked.PrmShipmentLoadConsolidation = ShipmentLoadConsolidation
                        invoked.PrmWarehouseShipment         = WarehouseShipment
                        invoked.PrmWarehouseShipmentLine     = WarehouseShipmentLine
                        invoked.PrmShipmentLoadContainer     = PrmShipmentLoadContainer
                else
                    invoke DeleteShipmentLoadLineContainer ShipmentLoadLineRel in foreground
                        invoked.PrmCompany                   = Company
                        invoked.PrmInventoryLocation         = InventoryLocation
                        invoked.PrmShipmentLoadConsolidation = ShipmentLoadConsolidation
                        invoked.PrmWarehouseShipment         = WarehouseShipment
                        invoked.PrmWarehouseShipmentLine     = WarehouseShipmentLine
                        invoked.PrmShipmentLoadContainer     = PrmShipmentLoadContainer
