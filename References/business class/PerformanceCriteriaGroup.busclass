PerformanceCriteriaGroup is a BusinessClass
    owned by procurement
    prefix is PCGRP
    sql name is PFCRITGP

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Ontology
    	symbolic key is PerformanceCriteriaGroup
    	
	Persistent Fields
		Category					 			is a Description
		CategoryWeighting				 	 	is a Percent
		DisplayOrder
		Active									is Boolean
		LastPerformanceCriteriaDisplayOrder  	is a DisplayOrder
		
	Transient Fields
		NewDisplayOrder			is a DisplayOrder

	Local Fields

	Sets
 		ByDisplayOrder
 			duplicates
 			Sort Order
 				SupplierGroup
 				PerformanceEvaluationTemplate
 				DisplayOrder
 				PerformanceCriteriaGroup
 	
 	Conditions
		CategoryWeightingShouldBeEntered
			restricted
			when (PerformanceEvaluationTemplate.TotalCategoryWeighting > 0
			and	  !CategoryWeighting entered)

		PerformanceCriteriaExist
			restricted
			when (CriteriaByDisplayRel exists)

		HasActiveCriteria
			restricted
			when (ActivePerformanceCriteriaRel exists)
			
		TemplateHasBeenUsedForEvaluations
			restricted
			when (PerformanceEvaluationTemplate.SupplierPerformanceEvaluationRel exists)

		AllowCreation
			restricted
			when (PerformanceEvaluationTemplate.Active
			and	  !TemplateHasBeenUsedForEvaluations)

 	Field Rules
 		Category
 			required
 		CategoryWeighting
			constraint (CategoryWeighting <= 100%)
				"CategoryWeightingCannotBeGreaterThan100%"
			constraint (CategoryWeighting > 0)
				"CategoryWeightingCannotBeLessThanZero"
 		DisplayOrder
	 		autosequence using PerformanceEvaluationTemplate.LastPerformanceCriteriaGroupDisplayOrder
		Active
			initial value is true

	Relations
		CriteriaByDisplayRel
			one-to-many relation to PerformanceCriteria
			Field Mapping uses ByDisplayOrder 
				related.SupplierGroup 					= SupplierGroup
				related.PerformanceEvaluationTemplate	= PerformanceEvaluationTemplate
				related.PerformanceCriteriaGroup 		= PerformanceCriteriaGroup

		ActivePerformanceCriteriaRel
			one-to-many relation to PerformanceCriteria
			Field Mapping uses ByDisplayOrder 
				related.SupplierGroup 					= SupplierGroup
				related.PerformanceEvaluationTemplate	= PerformanceEvaluationTemplate
				related.PerformanceCriteriaGroup 		= PerformanceCriteriaGroup
			Instance Selection
				where (related.Active)

	Actions
		Create is a Create Action 	
			valid when (AllowCreation)
			
	 		Exit Rules
				if (PerformanceEvaluationTemplate.TotalCategoryWeighting > 100%)
					confirmation required
						"TotalCategoryWeightingIsGreaterThan100%;DoYouWantToContinue?"
						
		Update is an Update Action
			Exit Rules
				if (PerformanceEvaluationTemplate.TotalCategoryWeighting > 100%)
					confirmation required
						"TotalCategoryWeightingIsGreaterThan100%;DoYouWantToContinue?"
		
		Delete is a Delete Action
			valid when (!TemplateHasBeenUsedForEvaluations)


























