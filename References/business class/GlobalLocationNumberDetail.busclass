GlobalLocationNumberDetail is a BusinessClass
    owned by ic
    prefix is GLX
    classic name is GLNLOCDTL

    Ontology
        symbolic key is GlobalLocationNumberDetail
            classic set name is GLXSET1
            classic name for GlobalLocationNumberDetail.GlobalLocationNumberType is GLN-TYPE
            classic name for GlobalLocationNumber is GLN-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

		Company                      is a GeneralLedgerCompany
			disable surrogates
        CompanyLocationBin
            classic name for CompanyLocationBin.InventoryLocation is LOCATION
        RequestingCompanyAndLocation             
        VendorAndVendorLocation
        PurchasingVendorAndPurchaseFromLocation
        Buyer
            classic name is BUYER-CODE
        Manufacturer
            classic name for Manufacturer.ManufacturerCode is MANUF-CODE
            classic name for Manufacturer.ManufacturerDivision is MANUF-DIVISION


	Local Fields
		LocalSequence    is  Numeric size 6

	Derived Fields
	
		GlobalLocationNumberID  	is a DerivedField
			type is Alpha size 18	
			if (GlobalLocationNumberDetail.GlobalLocationNumberType.GeneralLedgerCompany)
				return Company
			else
			if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryCompany	
			or  GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryLocation
			or  GlobalLocationNumberDetail.GlobalLocationNumberType.Bin)	
				return CompanyLocationBin
			else
			if (GlobalLocationNumberDetail.GlobalLocationNumberType.RequestingLocation)	
				return RequestingCompanyAndLocation
			else
			if (GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerCode	
			or  GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerDivision)	
				return Manufacturer
			else
			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Buyer)
			 	return Buyer					
		    else		    
		    if (GlobalLocationNumberDetail.GlobalLocationNumberType.Vendor
		    or  GlobalLocationNumberDetail.GlobalLocationNumberType.VendorLocation)
		    	return VendorAndVendorLocation
		    else
		    if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchasingVendor
		    or  GlobalLocationNumberDetail.GlobalLocationNumberType.PurchaseFromLocation)
		 	    return PurchasingVendorAndPurchaseFromLocation

	
    Conditions













        IsBuyerAssigned
        	restricted
            when (Buyer entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.Buyer)

        IsGLCompanyAssigned
            classic name is GLCO-ASSIGNED
        	restricted
            when (Company entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.GeneralLedgerCompany)

        HasCompany
        	restricted
            when (CompanyLocationBin.Company entered
            and   not GlobalLocationNumberDetail.GlobalLocationNumberType.GeneralLedgerCompany)

        HasManufCode
        	restricted
            when (Manufacturer.ManufacturerCode entered)

        HasVendor
        	restricted
            when (VendorAndVendorLocation.Vendor entered)

        IsBinAssigned
            classic name is ICBIN-ASSIGNED
        	restricted
            when (CompanyLocationBin.Company entered
            and   CompanyLocationBin.InventoryLocation entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.Bin)

        IsICCompanyAssigned
            classic name is ICCO-ASSIGNED
        	restricted
            when (CompanyLocationBin.Company entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryCompany)

        IsInventoryLocationAssigned
            classic name is ICLOC-ASSIGNED
        	restricted
            when (CompanyLocationBin.Company entered
            and   CompanyLocationBin.InventoryLocation entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryLocation)

        IsManufacturerCodeAssigned
            classic name is MANF-CODE-ASGN
        	restricted
            when (ItemGroup entered
            and   Manufacturer.ManufacturerCode entered
            and   Manufacturer.ManufacturerDivision not entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerCode)

        IsManufacturerDivisionAssigned
            classic name is MANF-DIV-ASSGN
            restricted
            when (ItemGroup entered
            and   Manufacturer.ManufacturerCode entered
            and   Manufacturer.ManufacturerDivision entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerDivision)







        IsPurchaseFromLocationAssigned
            classic name is POVENLOC-ASSGN
            restricted
            when (PurchasingVendorAndPurchaseFromLocation.PurchasingVendor entered
            and   PurchasingVendorAndPurchaseFromLocation.PurchasingVendor entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.PurchaseFromLocation)

        IsRequestingLocationAssigned
            classic name is RQLOC-ASSIGNED
            restricted
            when (CompanyLocationBin.Company entered
            and   CompanyLocationBin.InventoryLocation entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.RequestingLocation)
            
        HasAssignedRequestingCompanyAndLocation
            restricted
            when (RequestingCompanyAndLocation.RequestingCompany entered
            and   RequestingCompanyAndLocation.RequestingLocation entered
            and   GlobalLocationNumberDetail.GlobalLocationNumberType.RequestingLocation)
            
        SecurityGroupAllowsAccess
    		when((Company not entered and CompanyLocationBin.Company not entered)
    		or  (Company entered and Company.SecurityGroupAllowsAccess)
    		or	(CompanyLocationBin.Company entered and CompanyLocationBin.Company.SecurityGroupAllowsAccess))

		GlobalLocationNumberTypeEntered
			restricted
			when (GlobalLocationNumberDetail.GlobalLocationNumberType entered)
		
    Relations

      	GlobalLocationNumberSequencesRel
      		one-to-many relation to GlobalLocationNumberDetail
      		Field Mapping uses symbolic key
                related.ItemGroup            = ItemGroup
                related.GlobalLocationNumber = GlobalLocationNumber
			Instance Selection	
				where	(related.GlobalLocationNumberDetail.GlobalLocationNumberType 	= GlobalLocationNumberDetail.GlobalLocationNumberType)				
    	
     
        GlobalLocationNumberDetailRel
            classic name is GLNLOCDTL
            one-to-one relation to GlobalLocationNumberDetail
            valid when (IsGLCompanyAssigned)
            Field Mapping uses Set6
                related.Company			 = Company







      
        BinRel
            classic name is ICBIN
            one-to-one relation to Bin
            valid when (IsBinAssigned)
            Field Mapping uses symbolic key
                related.Company           				= old CompanyLocationBin.Company
                related.InventoryLocation 				= old CompanyLocationBin.InventoryLocation
                related.Bin               				= old CompanyLocationBin.Bin

        InventoryCompanyRel
            classic name is ICCOMPANY
            one-to-one relation to InventoryCompany
            valid when (IsICCompanyAssigned)
            Field Mapping uses symbolic key
                related.Company 						= old CompanyLocationBin.Company

        InventoryLocationRel
            classic name is ICLOCATION
            one-to-one relation to InventoryLocation
            valid when (IsInventoryLocationAssigned)
            Field Mapping uses symbolic key
                related.Company           				= old CompanyLocationBin.Company
                related.InventoryLocation 				= old CompanyLocationBin.InventoryLocation

        ManufacturerRel
            classic name is ICMANFCODE
            one-to-one relation to Manufacturer
            valid when (HasManufCode)
            Field Mapping uses symbolic key
                related.ItemGroup                         = old ItemGroup
                related.Manufacturer.ManufacturerCode     = old Manufacturer.ManufacturerCode
                related.Manufacturer.ManufacturerDivision = old Manufacturer.ManufacturerDivision

        RequestingLocationRel
            classic name is RQLOC
            one-to-one relation to RequestingLocation
            valid when (IsRequestingLocationAssigned)
            Field Mapping uses symbolic key
                related.Company            				  = old RequestingCompanyAndLocation.RequestingCompany
                related.RequestingLocation 				  = old RequestingCompanyAndLocation.RequestingLocation

        AssignedRequestingCompanyAndLocationRel
            one-to-one relation to RequestingLocation
            valid when (HasAssignedRequestingCompanyAndLocation)
            Field Mapping uses symbolic key
                related.Company            				  =  RequestingCompanyAndLocation.RequestingCompany
                related.RequestingLocation 				  =  RequestingCompanyAndLocation.RequestingLocation

        BuyerRel
            one-to-one relation to Buyer
            valid when (IsBuyerAssigned)
            Field Mapping uses symbolic key
                related.HROrganization	 				  = old ItemGroup.BusinessGroup.FinanceEnterpriseGroup.HROrganization
                related.Buyer            				  = old Buyer

        PurchaseFromLocationRel
            classic name is POVENDLOC
            one-to-one relation to PurchaseFromLocation

            Field Mapping uses symbolic key
                related.VendorGroup          			   = old ItemGroup
                related.Vendor              			   = old PurchasingVendorAndPurchaseFromLocation.PurchasingVendor
                related.PurchaseFromLocation 			   = old PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation

        PurchasingVendorRel
            classic name is POVENDOR
            one-to-one relation to PurchasingVendor

            Field Mapping uses symbolic key
                related.VendorGroup 					  = old ItemGroup
                related.Vendor     						  = old PurchasingVendorAndPurchaseFromLocation.PurchasingVendor
		
		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key 
				related.VendorGroup 					 = old ItemGroup
                related.Vendor							 = old VendorAndVendorLocation.Vendor
		
		VendorLocationRel
			one-to-one relation to VendorLocation
			Field Mapping uses symbolic key
				related.VendorGroup         		 	  = old ItemGroup
                related.Vendor        			   		  = old VendorAndVendorLocation.Vendor
                related.VendorLocation	 				  = old VendorAndVendorLocation.VendorLocation
						
		
		
    Sets

        Set2
            indexed
            Instance Selection
                where (HasCompany)
            Sort Order
                ItemGroup
                GlobalLocationNumberDetail.GlobalLocationNumberType
                CompanyLocationBin
                GlobalLocationNumber

        Set3
            indexed
            Instance Selection
                where (IsBuyerAssigned)
            Sort Order
                ItemGroup
                GlobalLocationNumberDetail.GlobalLocationNumberType
                Buyer
                GlobalLocationNumber

        Set4
            indexed
            Instance Selection
                where (HasVendor)
            Sort Order
                ItemGroup
                GlobalLocationNumberDetail.GlobalLocationNumberType
                VendorAndVendorLocation.Vendor
                VendorAndVendorLocation.VendorLocation
                GlobalLocationNumber

        Set5
            indexed
            Instance Selection
                where (HasManufCode)
            Sort Order
                ItemGroup
                GlobalLocationNumberDetail.GlobalLocationNumberType
                Manufacturer
                GlobalLocationNumber

        Set6
            indexed
            Instance Selection
                where (IsGLCompanyAssigned)
            Sort Order

				Company
        
        Set7
            indexed
            Sort Order
                GlobalLocationNumberDetail.GlobalLocationNumberType
                GlobalLocationNumber
                GlobalLocationNumberDetail.Sequence
                ItemGroup

 		Set8
            indexed
            Instance Selection
            	where (IsPurchaseFromLocationAssigned)
            Sort Order
                ItemGroup
                GlobalLocationNumberDetail.GlobalLocationNumberType
                PurchasingVendorAndPurchaseFromLocation.PurchasingVendor
                PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation
                GlobalLocationNumber
 		
 		
 	Rule Blocks
 		GlobalLocationNumberDetailRecordExists
 		 	for each GlobalLocationNumberSequencesRel
		   		if (each.GlobalLocationNumberDetail.GlobalLocationNumberType = GlobalLocationNumberDetail.GlobalLocationNumberType) 
		   			constraint (each.GlobalLocationNumberID != GlobalLocationNumberID)
		   				"GlobalLocationNumberDetailAlreadyExists"
 				
 			if (GlobalLocationNumberDetailRel.GlobalLocationNumber entered)
 				constraint (GlobalLocationNumberDetailRel.GlobalLocationNumber = GlobalLocationNumber)
 					"GlobalLedgerCompany<Company>AlreadyHasGlobalLocationNumber<GlobalLocationNumberDetailRel.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 		
 			if (CompanyLocationBin.Company.GlobalLocationNumber entered)	
 	   			constraint (CompanyLocationBin.Company.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 	   				"InventoryCompany<CompanyLocationBin.Company>AlreadyHasGlobalLocationNumber<CompanyLocationBin.Company.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"	
 	   					
 			if (CompanyLocationBin.InventoryLocation.GlobalLocationNumber entered)
 				constraint (CompanyLocationBin.InventoryLocation.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"InventoryLocation<CompanyLocationBin.InventoryLocation>AlreadyHasGlobalLocationNumber<CompanyLocationBin.InventoryLocation.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"	
 			
 			if (RequestingCompanyAndLocation.RequestingLocation.GlobalLocationNumber entered)
 				constraint (RequestingCompanyAndLocation.RequestingLocation.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"RequestingLocation<RequestingCompanyAndLocation.RequestingLocation>AlreadyHasGlobalLocationNumber<RequestingCompanyAndLocation.RequestingLocation.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (Manufacturer.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerCode)
 				constraint (Manufacturer.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"ManufacturerCode<Manufacturer.ManufacturerCode>AlreadyHasGlobalLocationNumber<Manufacturer.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (Manufacturer.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerDivision)
 				constraint (Manufacturer.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"ManufacturerDivision<Manufacturer.ManufacturerDivision>AlreadyHasGlobalLocationNumber<Manufacturer.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (VendorAndVendorLocation.Vendor.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.Vendor)
 				constraint (VendorAndVendorLocation.Vendor.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"Vendor<VendorAndVendorLocation.Vendor>AlreadyHasGlobalLocationNumber<VendorAndVendorLocation.Vendor.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (VendorAndVendorLocation.VendorLocation.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.VendorLocation)
 				constraint (VendorAndVendorLocation.VendorLocation.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"VendorLocation<VendorAndVendorLocation.VendorLocation>AlreadyHasGlobalLocationNumber<VendorAndVendorLocation.VendorLocation.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (PurchasingVendorAndPurchaseFromLocation.PurchasingVendor.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.PurchasingVendor)
 				constraint (PurchasingVendorAndPurchaseFromLocation.PurchasingVendor.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)	
 					"PurchasingVendor<PurchasingVendorAndPurchaseFromLocation.PurchasingVendor>AlreadyHasGlobalLocationNumber<PurchasingVendorAndPurchaseFromLocation.PurchasingVendor.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.PurchaseFromLocation)
 				constraint (PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)	
 					"PurchasingFromLocation<PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation>AlreadyHasGlobalLocationNumber<PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			if (CompanyLocationBin.Bin.GlobalLocationNumber entered 
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.Bin)
 				constraint(CompanyLocationBin.Bin.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"Bin<CompanyLocationBin.Bin>AlreadyHasGlobalLocationNumber<CompanyLocationBin.Bin.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 			
 			if (Buyer.GlobalLocationNumber entered
 			and GlobalLocationNumberDetail.GlobalLocationNumberType.Buyer)
 				constraint(Buyer.GlobalLocationNumber = GlobalLocationNumberDetail.GlobalLocationNumber)
 					"Buyer<Buyer>AlreadyHasGlobalLocationNumber<Buyer.GlobalLocationNumber>,UnassignThisGlobalLocationNumberFirst"
 			
 						
 		GlobalLocationNumberUpdate
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryCompany)
 				invoke UpdateGlobalLocationNumber InventoryCompanyRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber CompanyLocationBin.Company
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryLocation)
 				invoke UpdateGlobalLocationNumber InventoryLocationRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber 	CompanyLocationBin.InventoryLocation
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.RequestingLocation)
 				invoke UpdateGlobalLocationNumber RequestingLocationRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber 	RequestingCompanyAndLocation.RequestingLocation	
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerCode
 			or  GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerDivision)	
 				invoke UpdateGlobalLocationNumber ManufacturerRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber 	Manufacturer
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Vendor)
 				invoke UpdateGlobalLocationNumber VendorRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber 	VendorAndVendorLocation.Vendor
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.VendorLocation)
 				invoke UpdateGlobalLocationNumber VendorLocationRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber 	VendorAndVendorLocation.VendorLocation	
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchasingVendor)
 				invoke UpdateGlobalLocationNumber PurchasingVendorRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber PurchasingVendorAndPurchaseFromLocation.PurchasingVendor	
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 					
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchaseFromLocation)		
 				invoke UpdateGlobalLocationNumber PurchaseFromLocationRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Bin)
 				invoke UpdateGlobalLocationNumber BinRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber CompanyLocationBin.Bin	
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 			
 			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Buyer)
 				invoke UpdateGlobalLocationNumber BuyerRel
 					initialize invoked.PrmGlobalLocationNumber
 				invoke UpdateGlobalLocationNumber Buyer	
 					invoked.PrmGlobalLocationNumber	= GlobalLocationNumberDetail.GlobalLocationNumber
 					invoked.PrmItemGroup	= GlobalLocationNumberDetail.GlobalLocationNumber.ItemGroup
 		
 		DeleteGlobalLocationNumber	
			
			if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryCompany)
 				invoke UpdateGlobalLocationNumber InventoryCompanyRel
 					initialize invoked.PrmGlobalLocationNumber


			if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryLocation)
 				invoke UpdateGlobalLocationNumber InventoryLocationRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.RequestingLocation)
 				invoke UpdateGlobalLocationNumber AssignedRequestingCompanyAndLocationRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerCode
 			or  GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerDivision)	
 				invoke UpdateGlobalLocationNumber ManufacturerRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Vendor)
 				invoke UpdateGlobalLocationNumber VendorRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.VendorLocation)
 				invoke UpdateGlobalLocationNumber VendorLocationRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchasingVendor)
 				invoke UpdateGlobalLocationNumber PurchasingVendorRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchaseFromLocation)		
 				invoke UpdateGlobalLocationNumber PurchaseFromLocationRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Bin)
 				invoke UpdateGlobalLocationNumber BinRel
 					initialize invoked.PrmGlobalLocationNumber

			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Buyer)
 				invoke UpdateGlobalLocationNumber BuyerRel
 					initialize invoked.PrmGlobalLocationNumber

 	Field Rules
 		
 		GlobalLocationNumberDetail
 			constraint (GlobalLocationNumberDetail.GlobalLocationNumberType entered)
 				"GlobalLocationNumberTypeIsRequired"
 	   
 	   	Company
 	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.GeneralLedgerCompany)
 	   			required
 	   
 	   	CompanyLocationBin
	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryCompany)
	   			required
	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.InventoryLocation)
   				required
   				constraint (CompanyLocationBin.InventoryLocation entered)
   					"InventoryLocationIsRequired"		
   				
   			if (GlobalLocationNumberDetail.GlobalLocationNumberType.Bin)
   				required
   				constraint (CompanyLocationBin.InventoryLocation entered)
   					"InventoryLocationIsRequired"
   				constraint (CompanyLocationBin.Bin entered)
   					"BinIsRequired"	
   			
 	   	RequestingCompanyAndLocation
 	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.RequestingLocation)
 	   			required
 	   			constraint (RequestingCompanyAndLocation.RequestingLocation entered)
 	   				"RequestingLocationIsRequired"
 	   
 	   	Manufacturer
 	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerCode)
 	   			required
 	   		
 	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.ManufacturerDivision)
 	   			required
 	   			constraint (Manufacturer.ManufacturerDivision entered)
 	   				"ManufacturerDivisionIsRequired"
 	   				
 	   	VendorAndVendorLocation
 	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.Vendor)
 	   			required
 	   		
 	   		if (GlobalLocationNumberDetail.GlobalLocationNumberType.VendorLocation)
 	   			required
 	   			constraint (VendorAndVendorLocation.VendorLocation entered)		
 	   				"VendorLocationIsRequired"
 	    PurchasingVendorAndPurchaseFromLocation
 	    	if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchasingVendor)
 	    		required
 	    
 	    	if (GlobalLocationNumberDetail.GlobalLocationNumberType.PurchaseFromLocation)
 	    		required
 	    		constraint (PurchasingVendorAndPurchaseFromLocation.PurchaseFromLocation entered)
 	    			"PurchaseFromLocationIsRequired"
 	    Buyer
	  		if (GlobalLocationNumberDetail.GlobalLocationNumberType.Buyer)
	  			required		
 	   			
 	Actions		
	    Create is a Create Action
	    	Action Rules
	    		include GlobalLocationNumberDetailRecordExists
	    		if (GlobalLocationNumberSequencesRel exists)
	    			LocalSequence = last GlobalLocationNumberSequencesRel.GlobalLocationNumberDetail.Sequence
	    			increment  LocalSequence
	    			GlobalLocationNumberDetail.Sequence	= LocalSequence
	    		else
					GlobalLocationNumberDetail.Sequence = 1
				
			Exit Rules
				include GlobalLocationNumberUpdate    
	    
	    Update is an Update Action
			Action Rules
				include GlobalLocationNumberDetailRecordExists
	 	
	 		Exit Rules
	 			include GlobalLocationNumberUpdate
	   			
	    Delete is a Delete Action
 			Exit Rules
 				include DeleteGlobalLocationNumber
