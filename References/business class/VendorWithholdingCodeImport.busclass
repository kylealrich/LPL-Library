VendorWithholdingCodeImport is a BusinessClass
	owned by ap
	prefix is CVWCD
	
	Ontology
		symbolic key is VendorWithholdingCodeImport
		
	
	Patterns
		implements StaticJava
		disable AuditIndex
		
	Persistent Fields
		VendorWithholdingType is Numeric size 1
			States
     			VendorWithholdingDefault value is 1
     			VendorWithholdingCode value is 2
		IncomeWithholdingPercent is a Percent
		DefaultWithholdingCode is Boolean

	Field Rules
		Company
			if (VendorWithholdingType.VendorWithholdingCode)
				required
		VendorWithholdingType
			constraint(VendorWithholdingType.VendorWithholdingCode
			or         VendorGroup.GlobalWithholding)
				"Vendor_\Withholding_\TypeCannotBe_\Vendor_\Withholding_\DefaultIf_\Global_\WithholdingNotFlaggedOn_\Vendor_\Group"

			constraint(VendorWithholdingType.VendorWithholdingDefault
			or		   VendorImport.AllowWithholdingCodeOverride)
				"Vendor_\Withholding_\TypeCannotBe_\Vendor_\Withholding_\CodeIf_\Allow_\Withholding_\Code_\OverrideNotFlaggedOn_\Default_\Tab"
	Rule Blocks
		ClearWithholdingCodeFields
			if (!VendorWithholdingType.VendorWithholdingCode)
				initialize Company
				initialize IncomeWithholdingPercent
				initialize DefaultWithholdingCode

	Conditions
		CompanyRequired
			restricted
			when (VendorWithholdingType.VendorWithholdingCode)

	Actions
		Create is a Create Action
			Entrance Rules
				include ClearWithholdingCodeFields
		Update is an Update Action
			Entrance Rules
				include ClearWithholdingCodeFields
		Delete is a Delete Action
