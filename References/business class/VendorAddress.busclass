VendorAddress is a BusinessClass
	owned by ap
	prefix is VDRSS


	Ontology
		symbolic key is VendorAddress

			classic name for VendorLocation is LOCATION-CODE
			classic name for VendorContact.VendorContactCode is RESP-CODE
			classic name for VendorContact.ContactLevel is CONTACT-LVL

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		PostalAddress	is a PostalAddressV2	
			holds pii
			classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
			classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
			classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
			classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
			classic name for PostalAddress.Municipality is CITY-ADDR5
			classic name for PostalAddress.StateProvince is STATE-PROV
			classic name for PostalAddress.Country is COUNTRY-CODE


		CreatedBy		is an Operator 
			holds pii
		LastUpdateDate	is TimeStamp
			classic name is LAST-UPDT-DATE
		LastUpdateBy	is an Operator 
			holds pii

		VendorConversionResult

	Local Fields
		ActionType					is AlphaUpper size 1
			States
				CreateType value is "C"
				UpdateType value is "U"
				DeleteType value is "D"
		CalculateTax
		LocalTaxCode				is a TaxCode
		OFACWebService
		ActionCode					is Alpha size 1
			States
				Create value is "C"
				Update value is "U"
				Delete value is "D"
		LocalVendorLocation			is a VendorLocation	

		LocalText		is Text	
		LocalText2		is Text	

		LocalTriggerFrom			is Alpha size 100
		NativeLPLBODTrigger			is Boolean
		NativeLPLRemitToBODTrigger 	is Boolean
		NativeLPLShipFromBODTrigger is Boolean

		LocalTriggerVerb			is Numeric size 5
		LocalMainUserTemplate		is Alpha size 250
	Transient Fields
		MultipleTaxAreaIdsExist		is Boolean
		TransientUpdatePrimeContact	is Boolean			
			default label is "UpdatePrimeContactAddress"

	Context Fields
		AuditDateRange		is a DateRange

	Field Rules








	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforAddress"

		ContextMessageText is a MessageField
			restricted
			"VendorAddress<VendorAddress>"

		DerivedTaxCode is a DerivedField
			type is like TaxCode
			restricted
			initialize CalculateTax

			CalculateTax.RelatedObjectReference		= reference to this instance
			CalculateTax.InFinanceEnterpriseGroup	= Vendor.VendorGroup.FinanceEnterpriseGroup
			CalculateTax.CallingModule				= "VendorAddress"
			CalculateTax.VertexQuantumOption		= "G"
			CalculateTax.TaxEntity					= blank
			CalculateTax.VendorGrp					= Vendor.VendorGroup
			CalculateTax.PostalAddress				= PostalAddress
			CalculateTax.DontCreateResultFiles		= true
			LocalTaxCode							= CalculateTax.ReturnedGeoCode
			MultipleTaxAreaIdsExist					= CalculateTax.MultipleTaxAreaIds
			if (CalculateTax.OutputErrorNumber not entered)
				if (PostalAddress.County not entered)
					PostalAddress.County 					= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County
				if (PostalAddress.PostalCode not entered)
					PostalAddress.PostalCode 				= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
			return LocalTaxCode

		CityStateZIPDisplay is a DerivedField	
			type is Text
			restricted
			initialize LocalText2

			if (PostalAddress.Municipality entered)
				LocalText2 += PostalAddress.Municipality
				if (PostalAddress.StateProvince entered
					or PostalAddress.PostalCode entered)
					LocalText2 += ", "

			if (PostalAddress.StateProvince entered
				and PostalAddress.PostalCode entered)
				LocalText2 += PostalAddress.StateProvince + " " + PostalAddress.PostalCode
			else
				LocalText2 += PostalAddress.StateProvince + PostalAddress.PostalCode
			return LocalText2

		FullAddressDisplay is a DerivedField	
			type is Text
			default label is "VendorAddress"
			initialize LocalText

			if (PostalAddress.DeliveryAddress.AddressLine1 entered)
				LocalText += PostalAddress.DeliveryAddress.AddressLine1
				if (PostalAddress.DeliveryAddress.AddressLine2 entered
					or PostalAddress.DeliveryAddress.AddressLine3 entered
					or PostalAddress.DeliveryAddress.AddressLine4 entered
					or CityStateZIPDisplayEntered)
					LocalText += VendorGroup.NewLine

			if (PostalAddress.DeliveryAddress.AddressLine2 entered)
				LocalText += PostalAddress.DeliveryAddress.AddressLine2
				if (PostalAddress.DeliveryAddress.AddressLine3 entered
					or PostalAddress.DeliveryAddress.AddressLine4 entered
					or CityStateZIPDisplayEntered)
					LocalText += VendorGroup.NewLine

			if (PostalAddress.DeliveryAddress.AddressLine3 entered)
				LocalText += PostalAddress.DeliveryAddress.AddressLine3
				if (PostalAddress.DeliveryAddress.AddressLine4 entered
					or CityStateZIPDisplayEntered)
					LocalText += VendorGroup.NewLine

			if (PostalAddress.DeliveryAddress.AddressLine4 entered)
				LocalText += PostalAddress.DeliveryAddress.AddressLine4
				if (CityStateZIPDisplayEntered)
					LocalText += VendorGroup.NewLine

			if (CityStateZIPDisplayEntered)
				LocalText += CityStateZIPDisplay
			return LocalText

	Conditions





		IsVendorLocation
			restricted
			when (VendorLocation entered)

		HasVendorContact
			restricted
			when (VendorContact entered)

		IsLastVendorAddress
			restricted
			when (first Vendor.VendorAddressesRel.UniqueID = last Vendor.VendorAddressesRel.UniqueID)
			
		IsLastVendorLocationAddress
			restricted
			when (first VendorLocation.VendorAddressRel.UniqueID = last VendorLocation.VendorAddressRel.UniqueID)

		CanDelete
			restricted
			when (!IsLastVendorAddress
			and   !IsLastVendorLocationAddress)

		RecordExists
			restricted
			when (VendorAddress exists)

		IsMainAddress		
			when ((VendorLocation !entered
				and VendorContact !entered)
			or    (VendorLocation entered
				and VendorContact !entered))

		CityStateZIPDisplayEntered	
			when(PostalAddress.Municipality entered
				or PostalAddress.StateProvince entered
				or PostalAddress.PostalCode entered)

	Relations































		PrimeVendorLocAddressRel		
			one-to-one relation to VendorAddress
			Field Mapping uses symbolic key
				related.VendorGroup						= VendorGroup
				related.Vendor							= Vendor
				related.VendorLocation					= LocalVendorLocation
				related.VendorContact.VendorContactCode	= "PRIME"
				related.VendorContact.ContactLevel		= blank
				

		FSMBODConfigurationRel
			one-to-many relation to FSMBODConfiguration
			Field Mapping uses symbolic key
				related.FSMBODConfiguration.Verb		= LocalTriggerVerb
				related.FSMBODConfiguration.Noun		= "SupplierPartyMaster"
				related.FSMBODConfiguration.Direction	= 1
			Instance Selection
				where (related.ProcessingMethod.NativeLPL)
		
	Rule Blocks


















		OFACValidation
			if (VendorGroup.ValidateOFAC)
				if (PostalAddress.DeliveryAddress.AddressLine1 changed
				or  PostalAddress.Municipality changed
				or  PostalAddress.StateProvince changed
				or  PostalAddress.Country changed
				or  PostalAddress.PostalCode changed)
					OFACWebService.Reference			= UniqueID
					OFACWebService.Name					= Vendor.VendorName
					OFACWebService.Addressess			= PostalAddress.DeliveryAddress.AddressLine1
					OFACWebService.City					= PostalAddress.Municipality
					OFACWebService.State				= PostalAddress.StateProvince
					OFACWebService.Country				= PostalAddress.Country
					OFACWebService.PostalCode			= PostalAddress.PostalCode
					OFACWebService.CompareThreshold		= VendorGroup.OFACCompareThreshold
					constraint (!OFACWebService.HasOFACResults)
						"OFACResultsExceedThreshold"








		NativeLPLBODTriggerCheck
			if (LocalTriggerVerb = 1)
				LocalMainUserTemplate = "IONSupplierPartyMasterXML_ST"
			else
				LocalMainUserTemplate = "IONProcessSupplierPartyMasterMainXML_ST"
			invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
				invoked.PrmVerb 					= LocalTriggerVerb
				invoked.PrmNoun						= "SupplierPartyMaster"
				invoked.PrmDirection				= 1
				invoked.PrmTriggerFrom				= LocalTriggerFrom
				invoked.PrmFinanceEnterpriseGroup	= VendorGroup.BusinessGroup.FinanceEnterpriseGroup
				invoked.PrmBusinessGroup			= VendorGroup
				invoked.PrmMainUserTemplate 		= LocalMainUserTemplate
			NativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger

	Delete Rules
		ActionType = ActionType.DeleteType



	Actions
		Create is a Create Action
			Action Rules
				include OFACValidation
				
		Update is an Update Action
			completion message is "AddressUpdated:LocationsWithSameAddressNeedToBeUpdatedIndividually"	
			Action Rules
				include OFACValidation
			Exit Rules
				if (TransientUpdatePrimeContact)		
					LocalVendorLocation = VendorLocation
					invoke Update PrimeVendorLocAddressRel
						invoked.PostalAddress	= PostalAddress
						invoked.LastUpdateDate	= current timestamp
						invoked.LastUpdateBy	= actor

		Delete is a Delete Action
			Entrance Rules
				constraint (CanDelete)
					"CannotDeleteLastAvailableAddressForVendor/Location"
					
		Purge is a Purge Action
    		restricted

		DeleteAddress is a Delete Action
			restricted

		Anonymize is an Instance Action	
			restricted
			Action Rules
				initialize PostalAddress
			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup	= VendorGroup.FinanceEnterpriseGroup
					invoked.Status					= 1
					invoked.AffectedBusinessClass	= "AP2"
					invoked.VendorGroup				= VendorGroup
					invoked.Vendor					= Vendor
					invoked.VendorLocation			= VendorLocation
					invoked.VendorContactCode		= VendorContact.VendorContactCode
					invoked.ContactLevel			= VendorContact.ContactLevel

		PurgeAuditLog is an Instance Action	
			restricted
			Action Rules
				invoke purge audit log entries 
			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup	= VendorGroup.FinanceEnterpriseGroup
					invoked.Status					= 2
					invoked.AffectedBusinessClass	= "AP2"
					invoked.VendorGroup				= VendorGroup
					invoked.Vendor					= Vendor
					invoked.VendorLocation			= VendorLocation
					invoked.VendorContactCode		= VendorContact.VendorContactCode
					invoked.ContactLevel			= VendorContact.ContactLevel

	Sets





















		ByVendorConversionResult
			Sort Order
				VendorConversionResult
				VendorGroup
				Vendor
				VendorLocation
				VendorContact

	Apply Pending Effective Rules
		if (!action type.Delete)
			if (PostalAddress changed)
				ActionCode = ActionCode.Update
				if(VendorLocation exists)
					invoke TriggerRemitToLocation VendorLocation
					invoke TriggerVendorLocation VendorLocation






				else
					increment Vendor.bod id.VariationID
					if(!Vendor.LocalVendorAddressTrigger and !Vendor.LocalFutureVendorAddressTrigger)
						invoke TriggerSupplierPartyMaster VendorGroup
							invoked.PrmVendor		= Vendor
							invoked.ActionCode		= ActionCode








	Audit Entry Rules
		if(not audit period.Future)
			if (!action type.Delete)
				if (PostalAddress changed)
					ActionCode = ActionCode.Update
					if(VendorLocation exists)
						invoke TriggerRemitToLocation VendorLocation
						invoke TriggerVendorLocation VendorLocation






					else
					if (!HasVendorContact and !Vendor.LocalVendorAddressTrigger and !Vendor.LocalFutureVendorAddressTrigger)
						invoke TriggerSupplierPartyMaster VendorGroup
							invoked.PrmVendor		= Vendor
							invoked.ActionCode		= ActionCode
							






	
	Action Exit Rules	
		if (PostalAddress changed)
			increment Vendor.bod id.VariationID














