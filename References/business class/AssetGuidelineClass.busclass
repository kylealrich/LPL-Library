AssetGuidelineClass is a BusinessClass
	owned by am
	
	prefix is AGCl
	
	Ontology
		symbolic key is AssetGuidelineClass
		
	Patterns
		implements StaticJava
	
	Persistent Fields
		Description         is Alpha up to 200
		Active
	
	Field Rules
		Description
			required
		Active
			initial value is true 
			if (Active changed
			and Active = false)
				confirmation required
					"InactivatingAGuidelineClassWillInactivateRelatedGuidelineClassRegulations;Continue?"
				invoke Update AssetGuidelineClassRegulation set 
					invoked.Active = false

	Conditions
		IsActive 
			restricted
			when (Active) 	

		HasActiveRegulations
			restricted
			when (Active
			and ActiveGuidelineClassRegulationsRel exists)				

	Relations
		ActiveGuidelineClassRegulationsRel is an AssetGuidelineClassRegulation set
			Instance Selection	
				where (IsActive)


	Actions
		Create is an Action

		CopyGuidelineClass is an Instance Action
			Parameters
				ToGuidelineClass				is like AssetGuidelineClass
					default label is "NewGuidelineClass"
				ToDescription					is Alpha up to 200
					default label is "Description"
				ToActive						is Boolean
					default label is "Active"
				CopyClassRegulations			is Boolean


			Parameter Rules
				ToGuidelineClass
					initial value is AssetGuidelineClass
					required
				ToDescription
					initial value is Description 
					required
				ToActive
					initial value is true

			Action Rules	
				invoke Create AssetGuidelineClass
					invoked.AssetGuidelineClass			= ToGuidelineClass
					invoked.Description					= ToDescription
					invoked.Active 						= ToActive
				if (CopyClassRegulations)
					for each AssetGuidelineClassRegulation set
						invoke Create AssetGuidelineClassRegulation
							invoked.AssetGuidelineClass	= ToGuidelineClass
							fill in fields from each

		Update is an Action
		
		Delete is an Action
			Entrance Rules
				if (AssetGuidelineClassRegulation set exists)
					confirmation required
						"AllRelatedAssetGuidelineClassRegulationsWillBeDeleted;SelectOKToContinue"
	
