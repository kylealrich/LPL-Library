BankStatementPosition is a BusinessClass
	owned by cashmgmt
	prefix is CMBSP
	
	Ontology
		symbolic key is BankStatementPosition
		
	Patterns
		implements DynamicCreation
		
	Persistent Fields
		ForecastAmount			  	is an InternationalAmount
		TransactionAmount		  	is an InternationalAmount
		PositionAmount			  	is an InternationalAmount
		SelectedForecastAmount	  	is Boolean
		SelectedTransactionAmount 	is Boolean
		StatementDate				is a snapshot of BankStatement.StatementDate
		NaturalBalance			  	is a snapshot of CashTransactionCategory.NaturalBalance
		Comment						is Text
		
	Derived Fields
		DerivedCommentLinkName is a DerivedField
			type is Alpha 12
			restricted
			if (Comment entered)
				return ViewCommentMessage
			else
			if (!IsFinalized)
				return AddCommentMessage
				
		AddCommentMessage is a MessageField
			restricted
			"AddComment"		

		ViewCommentMessage is a MessageField
			restricted
			"ViewComment"
		
		CorporateBaseForecastAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (BankStatement.MultiplyBaseCurrencyRate)
				return (ForecastAmount * BankStatement.CorporateBaseEndingBalance.EnteredCurrencyRate)
			else
				return (ForecastAmount / BankStatement.CorporateBaseEndingBalance.EnteredCurrencyRate)
		
		LocationForecastAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (BankStatement.MultiplyLocationCurrencyRate)
				return (ForecastAmount * BankStatement.LocationCurrencyEndingBalance.EnteredCurrencyRate)
			else
				return (ForecastAmount / BankStatement.LocationCurrencyEndingBalance.EnteredCurrencyRate)
				
		CorporateBaseTransactionAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (BankStatement.MultiplyBaseCurrencyRate)
				return (TransactionAmount * BankStatement.CorporateBaseEndingBalance.EnteredCurrencyRate)
			else
				return (TransactionAmount / BankStatement.CorporateBaseEndingBalance.EnteredCurrencyRate)
		
		LocationTransactionAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (BankStatement.MultiplyLocationCurrencyRate)
				return (TransactionAmount * BankStatement.LocationCurrencyEndingBalance.EnteredCurrencyRate)
			else
				return (TransactionAmount / BankStatement.LocationCurrencyEndingBalance.EnteredCurrencyRate)
				
		CorporateBasePositionAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (BankStatement.MultiplyBaseCurrencyRate)
				return (PositionAmount * BankStatement.CorporateBaseEndingBalance.EnteredCurrencyRate)
			else
				return (PositionAmount / BankStatement.CorporateBaseEndingBalance.EnteredCurrencyRate)
						
		LocationPositionAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (BankStatement.MultiplyLocationCurrencyRate)
				return (PositionAmount * BankStatement.LocationCurrencyEndingBalance.EnteredCurrencyRate)
			else
				return (PositionAmount / BankStatement.LocationCurrencyEndingBalance.EnteredCurrencyRate)
		
	Transient Fields
		TransientFromCurrency			is a FromCurrency
			derive value from BankStatement.StatementCurrency

	Field Rules
		PositionAmount
			if (SelectedForecastAmount)
				cannot be changed
					"PositionAmountCannotBeChangedWhenTheForecastAmountHasBeenSelected"
					
			if (SelectedTransactionAmount)
				cannot be changed
					"PositionAmountCannotBeChangedWhenTheTransactionAmountHasBeenSelected"
	
	Conditions
		IsFinalized
			restricted
			when (BankStatement.PositionStatus.Finalized)
		
		HasForecastDetails
			restricted
			when (ForecastAmount entered)
		
		HasTransactionDetails
			restricted
			when (TransactionAmount entered)
			
		CanViewBankStatementPositionSelect
			restricted
			when (!ViewerSelectRel exist)
				
	Relations
		CashForecastDetailPositionRel
			one-to-many relation to CashForecastDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup							 = CashManagementGroup
				related.CashForecast							  	 = BankStatement.last ActiveCashForecastAccountRel.CashForecast
				related.CashForecastAccount.CashManagementAccount 	 = CashManagementAccount
				related.CashForecastPeriod						  	 = StatementDate
				related.CashForecastCategory.CashTransactionCategory = CashTransactionCategory
				
		BankStatementLinePositionRel 
			one-to-many relation to BankStatementLine
			Field Mapping uses ByCategory
				related.CashManagementGroup		= CashManagementGroup
				related.CashManagementAccount	= CashManagementAccount
				related.BankStatement			= BankStatement
				related.CashTransactionCategory	= CashTransactionCategory
				
		ViewerSelectRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor					= actor
 				related.ActorRole.Role			= "CTMViewer_ST"
				
	Sets
		ByNaturalBalance
			Sort Order
				CashManagementGroup
				CashManagementAccount
				BankStatement
				NaturalBalance
				CashTransactionCategory	
				
	Actions
		Create is a Create Action
			restricted
			
		Update is an Update Action
			valid when (!IsFinalized)
					
		CopyForecastAmount is an Instance Action
			default label is untranslatable
			restricted
			valid when (!IsFinalized)
			Action Rules
				PositionAmount = ForecastAmount
				SelectedForecastAmount = true
				initialize SelectedTransactionAmount
				
		CopyTransactionAmount is an Instance Action
			default label is untranslatable
			restricted
			valid when (!IsFinalized)
			Action Rules
				PositionAmount = TransactionAmount
				SelectedTransactionAmount = true
				initialize SelectedForecastAmount
				
		InitializePositionAmount is an Instance Action
			default label is untranslatable
			restricted
			valid when (!IsFinalized)
			Action Rules
				initialize PositionAmount
				initialize SelectedForecastAmount
				initialize SelectedTransactionAmount
				
		Delete is a Delete Action
			valid when (!IsFinalized)
			
		Purge is a Purge Action
			restricted
