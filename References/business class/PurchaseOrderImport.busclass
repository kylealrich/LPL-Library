PurchaseOrderImport is a BusinessClass
    owned by po
    prefix is CAR
    classic name is POCHEADER

    Ontology
        symbolic key is PurchaseOrderImport
            classic set name is CARSET0
            classic name for PurchaseOrderImportRecordType is REC-TYP

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
		RunGroup
        RecordInError                    is Boolean
        ErrorMessage					 is Alpha 150
        ResultProcessedBy                is a PurchaseOrderInterfaceResult
        PurchaseOrder           		is like PurchaseOrder						
        PORelease
        Vendor
        InterfacedVendor is a OldVendor    
        CrossReferenceVendor is like CrossReferenceVendor           
        	classic name is XREF-VENDOR
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
        Buyer
            classic name is BUYER-CODE
        PurchaseOrderDate                is Date
            classic name is PO-DATE
        Currency
            classic name is CURRENCY-CODE
        EnteredCurrencyConversionRate
            classic name is ENT-CNV-RATE
        CurrencyExchangeSetPoint
            classic name is CURR-SET-FLAG
        Revalue                          is Boolean
            classic name is REVALUE-FLAG
        DefaultDeliveryDate              is Date
            classic name is DFLT-DL-DATE
        ShipToLocation                   is an InventoryLocation
            classic name is LOCATION
        DropshipName                     is a Name	 
        	holds pii
            classic name is SH-NAME
        DropshipAddress                  is a PostalAddressV2	
        	holds pii
            classic name for DropshipAddress.DeliveryAddress.AddressLine1 is SH-ADDR1
            classic name for DropshipAddress.DeliveryAddress.AddressLine2 is SH-ADDR2
            classic name for DropshipAddress.DeliveryAddress.AddressLine3 is SH-ADDR3
            classic name for DropshipAddress.DeliveryAddress.AddressLine4 is SH-ADDR4
            classic name for DropshipAddress.Municipality is SH-CITY-ADDR5
            classic name for DropshipAddress.StateProvince is SH-STATE-PROV
            classic name for DropshipAddress.PostalCode is SH-POST-CODE
            classic name for DropshipAddress.County is SH-COUNTY
            classic name for DropshipAddress.Country is SH-COUNTRY-CD
            classic name for DropshipAddress.Region is DEST-REGION
        DropshipPhoneNumber              is a TelephoneNumber 
        	holds pii
            classic name for DropshipPhoneNumber.InternationalPrefix is SH-PHONE-PREF
            classic name for DropshipPhoneNumber.SubscriberNumber is SH-PHONE
            classic name for DropshipPhoneNumber.Extension is SH-PHONE-EXT
        DropshipContact                  is an AddressContact
            classic name is SH-CONTACT
        Dropship                         is Boolean
            classic name is DROPSHIP-FL
        PrintReceipt                     is Boolean
            classic name is PRT-REC-FLAG
        FaxNumber
            classic name for FaxNumber.InternationalPrefix is FAX-PREFIX
            classic name for FaxNumber.SubscriberNumber is FAX-NUMBER
            classic name for FaxNumber.Extension is FAX-EXT
        ProcessLevel                     is a PayablesProcessLevel
        FreightTerm
            classic name is FREIGHT-TERMS
        ShipTerm
            classic name is FOB-CODE
        ShippingMethod					is a ShipVia
            classic name is SHIP-VIA
        TermCode						is a TermsCode
        TaxCode
        LetterOfCredit
            classic name is LETTER-OF-CR
        DefaultTaxable                   is Boolean
            classic name is DFLT-TAXBL-FL
        DefaultAssetTemplate             is an AssetTemplate
            classic name is DFLT-ASSET-TEM
        DefaultAsset                     is an Asset
            classic name is DFLT-ASSET-NBR
        DefaultSourceInformation
        Closed
            classic name is CLOSED-FL
        InvoiceMethod
            classic name is INVC-MTHD-CODE
        IssueMethod                      is a PurchaseOrderIssueMethod
            classic name is ISSUE-METH
        RevisionsInclude                 is AlphaUpper size 1
            classic name is REVISIONS-INCL
            States
                RevisionsOnly     value is "O"
                RevisionsComplete value is "C"
                Reissue           value is "I"
                NotApplicable     value is "N"
                NotRevised        value is blank
        UnloadingPort
        NatureOfTransactionCode
            classic name is NOTC
        StatisticalProcedure			 is an IntrastatStatisticalProcedure
            classic name is STAT-PROC
        ShipToArriveDate                 is Date
            classic name is SHIP-TO-ARRIVE
        SubcontractorPurchaseOrder       is Boolean
            classic name is SUBCONTRACT-PO
        WarehouseNumber
            classic name is WRHS-NUMBER
        MatchPrepayment                  is Boolean
            classic name is MTCH-PRPY-FL
        ProcurementCardNumber
            classic name is PCARD-NBR
        UseProcurementCard               is Boolean
            classic name is P-CARD-FLAG
        RetainagePercentCompleteTracking is Numeric size 1
            sql name is RPercentCompleteTracking
            classic name is RET-TRK-PCT-CP
            States
                NoRetainage        value is 0
                PurchaseOrderLevel value is 1
                LineLevel          value is 2
        RetainageFirstPercent            is a Pct
            classic name is RET-PCT-1
        RetainageSecondPercent           is a Pct
            classic name is RET-PCT-2
        RetainageUpToPercentComplete     is a Pct
            classic name is RET-UP-TO-PCT
        RetainageDueDate                 is Date
            classic name is RET-DUE-DATE
        RetainageOverridePercentComplete is a Pct
            sql name is ROverridePercentComplete
            classic name is RET-OVRD-PCT
        PurchaseOrderBillToName          is a Name	 
        	holds pii
            classic name is POB-NAME
        PurchaseOrderBillToAddress       is a PostalAddressV2	
        	holds pii
            classic name for PurchaseOrderBillToAddress.DeliveryAddress.AddressLine1 is POB-ADDR1
            classic name for PurchaseOrderBillToAddress.DeliveryAddress.AddressLine2 is POB-ADDR2
            classic name for PurchaseOrderBillToAddress.DeliveryAddress.AddressLine3 is POB-ADDR3
            classic name for PurchaseOrderBillToAddress.DeliveryAddress.AddressLine4 is POB-ADDR4
            classic name for PurchaseOrderBillToAddress.Municipality is POB-CITY-ADDR5
            classic name for PurchaseOrderBillToAddress.StateProvince is POB-STATE-PROV
            classic name for PurchaseOrderBillToAddress.PostalCode is POB-POSTAL-CD
            classic name for PurchaseOrderBillToAddress.County is POB-COUNTY
            classic name for PurchaseOrderBillToAddress.Country is POB-COUNTRY-CD
        PurchaseOrderBillToContact       is an AddressContact
            classic name is POB-CONTACT
        PurchaseOrderBillToPhoneNumber   is a TelephoneNumber 
        	holds pii
            classic name for PurchaseOrderBillToPhoneNumber.InternationalPrefix is POB-PHONE-PREF
            classic name for PurchaseOrderBillToPhoneNumber.SubscriberNumber is POB-PHONE
            classic name for PurchaseOrderBillToPhoneNumber.Extension is POB-PHONE-EXT
        PurchaseOrderBillToEmailAddress  is an EmailAddressMulti 
        	holds pii
            sql name is POrderBillToEmailAddress
            classic name is POB-EMAIL-ADDR
        GlobalDocumentType
            classic name is GLBL-DOC-TYPE
        DefaultDistributionAccount       is a FinanceCodeBlock
            classic name for DefaultDistributionAccount.ToAccountingEntity is DFLT-DIST-CO
            classic name for DefaultDistributionAccount.AccountingUnit is DFLT-ACCT-UNIT
            classic name for DefaultDistributionAccount.GeneralLedgerChartAccount is DFLT-ACCOUNT
            classic name for DefaultDistributionAccount.Project is DFLT-ACTIVITY
        DefaultProcedureInformation 
        ErrorLevel                       is Numeric 2
        	States
        		Header                   value is 1
        		Line                     value is 2
        		HeaderAOC                value is 3
        		SpreadAOC                value is 4
        		LineAOC                  value is 5
        		HeaderComment            value is 6
        		LineComment              value is 7
        		LineDistribution         value is 8
        		LineAssetDefault         value is 9
		LetterOfGuarantee
			classic name is LTR-OF-GUARAN
		UserDate1                        is Date
        UserDate2                        is Date
		PurchaseOrderUserField1
        PurchaseOrderUserField3          is a UserField2
        PurchaseOrderUserField5          is a UserFld2
		PurchaseOrderUserField7          is a UserFld7
		EAMWorkOrder
		EAMWorkOrderActivity

	Transient Fields
		TransientReceivingOptions						is Numeric size 1
			default label is "DropshipReceivingOptions"
			derive value from Dropship
			States
				ReceivingRequired			value is 0
				ReceivingNotAllowed			value is 1

    Local Fields

		LocalErrorMessage				is Alpha 150
		ErrorOccurred                   is Boolean
		InterfacedPurchaseOrderNumber   is a PurchaseOrder view
		LocalInterfaceResult            is like PurchaseOrderInterfaceResult
		LocalRunGroup                   is like RunGroup
		LocalCompany                    is like PurchasingCompany
		LocalPurchaseOrder              is like PurchaseOrder
		LocalType                       is like PurchaseOrderImportRecordType
		LocalFinanceEnterpriseGroup 	is like FinanceEnterpriseGroup 
		
    Derived Fields
    	
    	NotEligibleToCancelMessage is a MessageField
    		restricted
    		"PurchaseOrderIsNotEligibleToCancel"
    		
    	AddOnChargeNotEligibleToCancelMessage is a MessageField
    		restricted
    		"AddOnChargeIsNotEligibleToCancel"
    		
    	NotFoundToCancelMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderCannotBeFoundToCancel"
    		
    	PurchaseOrderAlreadyExistsMessage is a MessageField
    		restricted
    		"CannotAddNewPurchaseOrder;InterfacedPurchaseOrderAlreadyCreatedPurchaseOrder"
    
    	CannotUpdateClosedMessage is a MessageField
    		restricted
    		"CannotUpdate;PurchaseOrderIsClosed"
    		
    	NotFoundToUpdateMessage is a MessageField
    		restricted
    		"ReferencedPurchaseOrderCannotBeFoundToUpdate"
    		
    	AddOnChargeNotFoundToUpdateMessage is a MessageField
    		restricted
    		"AddOnChargeCannotBeFoundToUpdate"
    		
    	CannotSetToClosedMessage is a MessageField
    		restricted
    		"CannotSetHeaderOrLineToClosed;MustProcessToGetToClosedStatus"

		NameAndAddressRequiredMessage is a MessageField
			restricted
			"NameAndAddressIsRequiredForAlternateShipToAddress"
    		
		DerivedTransactionCount is a DerivedField
			type is Numeric 7
			return 1

		LineCount 				is a DerivedField
			type is Numeric 6
			return instance count of PurchaseOrderImportLinesRel 
				
		LineDistributionCount		is a DerivedField
			type is Numeric 6
			return instance count of PurchaseOrderImportLineDistributionsRel 
			
		LineCountForAPO is a DerivedField
			type is Numeric 10 
			return instance count of LinesForCountRel 
		
		DistributionCountForAPO is a DerivedField
			type is Numeric 10
			return instance count of LineDistributionsForCountRel
		
		CommentCountForAPO is a DerivedField
			type is Numeric 10
			return instance count of CommentsForCountRel		
		
		AOCCountForAPO is a DerivedField
			type is Numeric 10
			default label is "AddOnChargeCountForAPurchaseOrder"
			return instance count of AOCsForCountRel
			
    Sets
		ByPurchaseOrderImport
			Sort Order
				Company
				PurchaseOrderImport
				POCode
				PurchaseOrderImportRecordType
				
		ByRunGroup
			Sort Order
				RunGroup
				Company
				PurchaseOrderImportRecordType
				PurchaseOrderImport
				POCode
				
		ByRunGroup2
			Sort Order
				RunGroup
				Company
				PurchaseOrderImport
				POCode
				PurchaseOrderImportRecordType

	Relations

		PurchaseOrderImportLinesOldRunGroupRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses ByRunGroup
				related.RunGroup                  = LocalRunGroup
				related.Company              	  = Company
				related.PurchaseOrderImport       = PurchaseOrderImport
				related.POCode                    = POCode

		PurchaseOrderImportAOCsOldRunGroupRel
			one-to-many relation to PurchaseOrderImportAddOnCharge
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
			Instance Selection
				where (related.LineNumber not entered)

		LinesForCountRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode			
		
		LineDistributionsForCountRel
			one-to-many relation to PurchaseOrderImportLineDistribution
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
						
		AOCsForCountRel
			one-to-many relation to PurchaseOrderImportAddOnCharge
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
						
		CommentsForCountRel
			one-to-many relation to PurchaseOrderImportComment
			Field Mapping uses Set1
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
				
		PurchaseOrderRel
			one-to-many relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company              = Company
				related.PurchaseOrder        = PurchaseOrder
		
		InterfacedPurchaseOrderRel
			one-to-many relation to InterfacedPurchaseOrder
			Field Mapping uses ByImport
				related.Company              = Company
				related.PurchaseOrderImport  = PurchaseOrderImport
				related.POCode               = POCode
		
		LocalPurchaseOrderRel
			one-to-many relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company              = LocalCompany
				related.PurchaseOrder        = LocalPurchaseOrder
		
		PurchaseOrderImportLinesByRunGroupRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup

		PurchaseOrderImportStandaloneLinesByRunGroupRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
			Instance Selection
				where (related.PurchaseOrder entered)
				
		PurchaseOrderImportLinesRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses ByRunGroup
				related.RunGroup                  = RunGroup
				related.Company              	  = Company
				related.PurchaseOrderImport       = PurchaseOrderImport
				related.POCode                    = POCode
		
		PurchaseOrderImportLineDistributionsRel
			one-to-many relation to PurchaseOrderImportLineDistribution
			Field Mapping uses ByRunGroup
				related.RunGroup                  = RunGroup
				related.Company              	  = Company
				related.PurchaseOrderImport       = PurchaseOrderImport
				related.POCode                    = POCode

		PurchaseOrderImportByRunGroupRel
			one-to-many relation to PurchaseOrderImport
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
		
		PurchaseOrderImportCommentsRel
			one-to-many relation to PurchaseOrderImportComment
			Field Mapping uses Set1
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
			Instance Selection
				where (related.LineNumber not entered)

		PurchaseOrderImportAOCsRel
			one-to-many relation to PurchaseOrderImportAddOnCharge
			Field Mapping uses ByRunGroup
				related.RunGroup                    = RunGroup
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
			Instance Selection
				where (related.LineNumber not entered)
				
		PurchaseOrderImportAOCsByRunGroupRel
			one-to-many relation to PurchaseOrderImportAddOnCharge
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
		
		PurchaseOrderImportStandaloneAOCsByRunGroupRel
			one-to-many relation to PurchaseOrderImportAddOnCharge
			Field Mapping uses ByRunGroup
				related.RunGroup					= LocalRunGroup
			Instance Selection
				where (related.PurchaseOrder entered)
		
		LocalInterfaceResultsRel
			one-to-one relation to PurchaseOrderInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.PurchaseOrderInterfaceResult		= LocalInterfaceResult 
				
		InterfaceMappingVendorRel
            one-to-many relation to VendorMappingTableInterface
            Field Mapping uses symbolic key
				related.VendorGroup								= Company.VendorGroup
			Instance Selection
				where (related.VendorMappingTableInterface.OldVendor	= InterfacedVendor)
				
		VendorForLegacyVendorRel
            one-to-many relation to Vendor
            Field Mapping uses ByLegacyVendor
				related.VendorGroup						= Company.VendorGroup
				related.LegacyVendor					= InterfacedVendor
		
		CrossReferenceVendorRel
            one-to-many relation to CrossReferenceVendor
            Field Mapping uses Set2
				related.VendorGroup								= Company.VendorGroup
				related.CrossReferenceVendor                    = CrossReferenceVendor
				
		UploadSpreadsheetRel
			one-to-many relation to SampleDocumentTemplate
			Field Mapping uses ByType
				related.Type        = 108 
		
		FactSheetRel
			one-to-one relation to SampleDocumentTemplate
			Field Mapping uses symbolic key
				related.SampleDocumentTemplate                  = "PurchaseOrderInterface_ST"

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		BudgetEditConfigurationRel
			one-to-one relation to BudgetEditConfiguration
			Field Mapping uses symbolic key
				related.BudgetEditConfiguration = Company.FinanceEnterpriseGroup

		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= Company.FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode		= "PO"
				
    Conditions
    
    	RecordExists
			restricted
			when (PurchaseOrderImport exists)
			
		VendorMappingTableExists
			restricted
			when (InterfaceMappingVendorRel exists)
		
		LegacyVendorExists
			restricted
			when (VendorForLegacyVendorRel exists)
		
		ImportLinesExist
			restricted
			when (PurchaseOrderImportLinesRel exists)
		
		PurchaseOrderExists
			restricted
			when (PurchaseOrder entered)
			
		AddType
			restricted
			when (PurchaseOrderImportRecordType.AddPurchaseOrder)
		
		CancelType
			restricted
			when (PurchaseOrderImportRecordType.CancelPurchaseOrder)
			
		UpdateType
			restricted
			when (PurchaseOrderImportRecordType.UpdatePurchaseOrder)
			
		DoNotUpdateType
			restricted
			when (PurchaseOrderImportRecordType.DoNotUpdate)
			
		AddOrUpdateType
			restricted
			when ((AddType
			and   PurchaseOrder !entered)
			or    UpdateType)
    
    	ClosedPO
    		restricted
    		when (Closed = "Y")
    		
    	HistoricalPO
    		restricted
    		when (Closed = "H")
		
		BillToDetailsNotEntered
			restricted
			when (PurchaseOrderBillToName not entered
			and PurchaseOrderBillToAddress not entered
			and PurchaseOrderBillToContact not entered
			and PurchaseOrderBillToPhoneNumber not entered
			and PurchaseOrderBillToEmailAddress not entered)
    
    Field Rules

        RunGroup
        	required
        
        PurchaseOrder
        	if (InterfacedPurchaseOrderRel exists
        	and !PurchaseOrderImportRecordType.AddPurchaseOrder)
        		PurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
        
        Closed
            default to "N"
            
        PrintReceipt
        	initial value is true    
			
	Actions
		Create is a Create Action
			
			Action Rules
				if (AddType)
					if (InterfacedVendor !entered
					and CrossReferenceVendor !entered)
						constraint (Vendor entered)
							"VendorOrInterfacedVendorOrCrossReferenceVendorMustBeEntered"
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules
		
		Update is an Update Action
				
			Action Rules
				if (AddType)
					if (InterfacedVendor !entered
					and CrossReferenceVendor !entered)
						constraint (Vendor entered)
							"VendorOrInterfacedVendorOrCrossReferenceVendorMustBeEntered"
				
				if (RunGroup changed)
					LocalRunGroup = old RunGroup
					invoke UpdateRunGroup PurchaseOrderImportLinesOldRunGroupRel
						invoked.PrmRunGroup = RunGroup
					invoke UpdateRunGroup PurchaseOrderImportAOCsOldRunGroupRel
						invoked.PrmRunGroup = RunGroup
		
		FastUpdate is an Update Action
			restricted
			bypass field rules
		
		Delete is a Delete Action
			Entrance Rules
				if (PurchaseOrder entered)
					LocalPurchaseOrder = PurchaseOrder
					LocalType          = PurchaseOrderImportRecordType
					LocalCompany       = Company
				invoke Delete PurchaseOrderImportLinesRel
				invoke Delete PurchaseOrderImportAOCsRel
				invoke Delete PurchaseOrderImportCommentsRel
				if (PurchaseOrderImportRecordType.AddPurchaseOrder
				and !LocalPurchaseOrderRel.Released
				and LocalPurchaseOrderRel.InterfaceInProcess = 1)
					invoke Delete InterfacedPurchaseOrderRel
				
			Exit Rules
				if (LocalType = 1
				and !LocalPurchaseOrderRel.Released
				and LocalPurchaseOrderRel.InterfaceInProcess = 1)
					invoke Delete LocalPurchaseOrderRel
				else	
				if (LocalType > 0)
					invoke UpdateFromInterface LocalPurchaseOrderRel
						invoked.SetInterfaceInProcessFalse   = true
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
		
		MassDelete is a Set Action
			Parameters
				PrmRunGroup                 is a RunGroup
					default label is "RunGroup"
			
			Parameter Rules
				PrmRunGroup
					required
					
			Instance Selection
				where (RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				Company	
				PurchaseOrderImport
				
			Action Rules
				Empty Set Rules
				
					invoke MassDelete PurchaseOrderImportLine
						invoked.PrmRunGroup                 = PrmRunGroup
					
					invoke MassDelete PurchaseOrderImportAddOnCharge
						invoked.PrmRunGroup                 = PrmRunGroup
				
				RunGroup Set Rules
					
					Exit Rules
						
						invoke MassDelete PurchaseOrderImportLine
							invoked.PrmRunGroup                 = PrmRunGroup
					
						invoke MassDelete PurchaseOrderImportAddOnCharge
							invoked.PrmRunGroup                 = PrmRunGroup
				
				Instance Rules
				
					invoke Delete
		
		InterfacePurchaseOrders is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmHROrganization           is an HROrganization
					default label is "HROrganization"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany                  is a PurchasingCompany
				DefaultBuyer                is a Buyer
					context of PrmHROrganization
				ReleasePurchaseOrders       is Boolean
				IncludePurchaseOrdersInError is Boolean
				PrmMoveErrorsToNewRunGroup  is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
				EnableGTINProcessing        is Boolean	
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmHROrganization
					default to actor.context.HROrganization
				
				PrmRunGroup
					LocalRunGroup     = PrmRunGroup
					required
						"RunGroupIsRequired"
				
				ReleasePurchaseOrders
					initial value is true
				
				IncludePurchaseOrdersInError
					if (PrmRunGroup like "*ERRORS_*")
						default to true
						
			Local Fields
				LocalInterfaceResultView		is a PurchaseOrderInterfaceResult view	
				LocalPOLineCount                is Numeric 12
				LocalPOCount                    is Numeric 12   
				LocalPODistributionCount        is Numeric 12
				LocalPOCommentCount             is Numeric 12
				LocalPOAOCCount                 is Numeric 12
			
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and  ((PrmCompany  not entered
				and    GeneralLedgerCompanyRel.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or     Company	= PrmCompany))

			Sort Order
				RunGroup
				Company	
				
			Action Rules

				Empty Set Rules
					
					invoke Create PurchaseOrderInterfaceResult
						assign result to LocalInterfaceResultView
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.RunTime						= current timestamp
						invoked.RunGroup					= PrmRunGroup
						invoked.Company						= PrmCompany
						invoked.IncludePurchaseOrdersInError = IncludePurchaseOrdersInError
						invoked.DefaultBuyer                = DefaultBuyer
						invoked.ReleasePurchaseOrders       = ReleasePurchaseOrders
						invoked.EnableGTINProcessing        = EnableGTINProcessing
						invoked.SpecificAction              = PrmSpecificAction
						invoked.MoveErrorsToNewRunGroup     = PrmMoveErrorsToNewRunGroup	
						invoked.ErrorRunGroupPrefix         = PrmErrorRunGroupPrefix
					
					invoke LoadInterfacedPurchaseOrderLines PurchaseOrderImportLine
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup                 = PrmRunGroup
						invoked.PrmCompany                  = PrmCompany
						invoked.PrmResult                   = LocalInterfaceResultView.PurchaseOrderInterfaceResult
						invoked.PrmSpecificAction           = PrmSpecificAction
						
				RunGroup Set Rules
					Entrance Rules
			
						ErrorOccurred							= false
						LocalRunGroup                           = PrmRunGroup
						invoke Create PurchaseOrderInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.Company						= PrmCompany
							invoked.IncludePurchaseOrdersInError = IncludePurchaseOrdersInError
							invoked.DefaultBuyer                = DefaultBuyer
							invoked.ReleasePurchaseOrders       = ReleasePurchaseOrders
							invoked.SpecificAction              = PrmSpecificAction
							invoked.EnableGTINProcessing        = EnableGTINProcessing
							invoked.MoveErrorsToNewRunGroup     = PrmMoveErrorsToNewRunGroup	
							invoked.ErrorRunGroupPrefix         = PrmErrorRunGroupPrefix
							
					Exit Rules
					
						LocalInterfaceResult                    = LocalInterfaceResultView.PurchaseOrderInterfaceResult
						LocalRunGroup                           = PrmRunGroup
						LocalFinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup
						invoke Update LocalInterfaceResultsRel
							invoked.InterfaceCounts.PassedHeaderCount          	= LocalPOCount         
							invoked.InterfaceCounts.Additional1Count	   		= LocalPOCommentCount
							invoked.InterfaceCounts.DetailCount                 = LocalPOLineCount
							invoked.InterfaceCounts.AddOnChargeCount            = LocalPOAOCCount
							invoked.InterfaceCounts.DistributionCount           = LocalPODistributionCount							
																
						invoke LoadInterfacedPurchaseOrderLines PurchaseOrderImportLine
							invoked.PrmFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmCompany                  = PrmCompany
							invoked.PrmResult                   = LocalInterfaceResultView.PurchaseOrderInterfaceResult
							invoked.PrmSpecificAction           = PrmSpecificAction

				Instance Rules
					if    (IncludePurchaseOrdersInError
					or     !RecordInError)
						
						LocalRunGroup                       = PrmRunGroup
						ErrorOccurred						= false
						RecordInError                       = false
						initialize ErrorLevel                          
						initialize ErrorMessage
						if (InterfacedPurchaseOrderRel exists
        				and !PurchaseOrderImportRecordType.AddPurchaseOrder)
        					PurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder
						LocalPurchaseOrder                  = PurchaseOrder
						LocalPOCount                       += 1
						LocalPOLineCount                   += LineCountForAPO  
						LocalPODistributionCount           += DistributionCountForAPO
						LocalPOCommentCount                += CommentCountForAPO
						LocalPOAOCCount                    += AOCCountForAPO
				
						if (not Dropship)
							if ((DropshipName entered 		and  DropshipAddress.Country not entered)							
							or 	(DropshipName not entered	and  DropshipAddress.Country entered)								
							or  (DropshipName not entered	and (DropshipContact entered or DropshipPhoneNumber entered)))		
								ErrorOccurred		= true
								LocalErrorMessage	= NameAndAddressRequiredMessage

						if (!ErrorOccurred)
							if (PurchaseOrderImportRecordType.AddPurchaseOrder
							and (PurchaseOrder = 0
                    		or   InterfacedPurchaseOrderRel not exists)
							and (PrmSpecificAction = 0
							or  PrmSpecificAction = 1))
								if (ClosedPO)
									ErrorOccurred       = true
									LocalErrorMessage 	= CannotSetToClosedMessage
								else
								if (!InterfacedPurchaseOrderRel exists)
									invoke Unreleased.CreateFromInterface PurchaseOrder
										assign result to InterfacedPurchaseOrderNumber
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										fill in fields from this instance
											except invoked.Vendor
										if (Buyer !entered)
											invoked.Buyer                                           = DefaultBuyer
										if (Vendor entered)
											invoked.Vendor                                          = Vendor
										else
										if (InterfacedVendor entered
										and VendorMappingTableExists)
											invoked.Vendor                                          = InterfaceMappingVendorRel.Vendor
										else
										if (InterfacedVendor entered
										and LegacyVendorExists)
											invoked.Vendor                                          = VendorForLegacyVendorRel.Vendor
										else
										if (CrossReferenceVendor entered)
											invoked.Vendor                                          = first CrossReferenceVendorRel.Vendor
										invoked.Reference1                                          = PurchaseOrderImport
										invoked.LocalFromInterface                                  = true
										invoked.InterfaceInProcess		  							= 1
										invoked.OriginatingInterfaceRun   							= LocalInterfaceResultView.PurchaseOrderInterfaceResult
										invoked.ShipToLocation										= ShipToLocation
										if (BillToDetailsNotEntered)
											if (ShipToLocation.PurchaseOrderBillToName entered
											and ShipToLocation.PurchaseOrderBillToAddress.DeliveryAddress.AddressLine1 entered)
												invoked.PurchaseOrderBillToName 						= ShipToLocation.PurchaseOrderBillToName
												invoked.PurchaseOrderBillToAddress 						= ShipToLocation.PurchaseOrderBillToAddress
												invoked.PurchaseOrderBillToContact 						= ShipToLocation.PurchaseOrderBillToContact
												invoked.PurchaseOrderBillToPhoneNumber 					= ShipToLocation.PurchaseOrderBillToPhoneNumber
												invoked.PurchaseOrderBillToEmailAddress 				= ShipToLocation.PurchaseOrderBillToEmailAddress
											else
												invoked.PurchaseOrderBillToName 						= Company.PurchaseOrderBillToName
												invoked.PurchaseOrderBillToAddress 						= Company.PurchaseOrderBillToAddress
												invoked.PurchaseOrderBillToContact 						= Company.PurchaseOrderBillToContact
												invoked.PurchaseOrderBillToPhoneNumber 					= Company.PurchaseOrderBillToPhoneNumber
												invoked.PurchaseOrderBillToEmailAddress 				= Company.PurchaseOrderBillToEmailAddress
										if (HistoricalPO)
											invoked.Released   = true
											invoked.Issued     = true 
											invoked.CloseDate  = current corporate date
											invoked.PurchaseOrderLifeCycleState = 4 
									if (ErrorOccurred = false)
										invoke Create InterfacedPurchaseOrder
											fill in fields from this instance
											invoked.Company                                             = Company
											invoked.PurchaseOrderImport                                 = PurchaseOrderImport
											invoked.PurchaseOrder                                       = InterfacedPurchaseOrderNumber.PurchaseOrder
											invoked.POCode                                              = POCode 
										PurchaseOrder                                                   = InterfacedPurchaseOrderNumber.PurchaseOrder
										LocalPurchaseOrder                                              = InterfacedPurchaseOrderNumber.PurchaseOrder
								else
								if (InterfacedPurchaseOrderRel exists)
									ErrorOccurred 		= true
									LocalErrorMessage 	= PurchaseOrderAlreadyExistsMessage
								ResultProcessedBy = LocalInterfaceResultView.PurchaseOrderInterfaceResult
								
								if (ErrorOccurred)
									invoke SetError
										invoked.PrmErrorMessage 		= LocalErrorMessage
										invoked.PrmResult       		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
										invoked.PrmErrorLevel   		= 1
																			
								if (!ErrorOccurred)
									if (PurchaseOrderImportCommentsRel exists)
										for each PurchaseOrderImportCommentsRel
											if (!ErrorOccurred)
												invoke SystemCreate PurchaseOrderComment
													resume on error
														ErrorOccurred     = true
														LocalErrorMessage = error message
													fill in fields from each
													invoked.PurchaseOrder        = LocalPurchaseOrder
												if (!ErrorOccurred)
													invoke FastDelete each
												if (ErrorOccurred)
													invoke SetError
														invoked.PrmErrorMessage = LocalErrorMessage
														invoked.PrmResult       = LocalInterfaceResultView.PurchaseOrderInterfaceResult
														invoked.PrmErrorLevel   = 6	
							else
							if (PurchaseOrderImportRecordType.AddPurchaseOrder
							and PurchaseOrder != 0
							and (PrmSpecificAction = 0
							or  PrmSpecificAction = 1))
								ResultProcessedBy = LocalInterfaceResultView.PurchaseOrderInterfaceResult
								invoke UpdateFromInterface PurchaseOrderRel
									invoked.PrmOriginatingInterfaceRun  = LocalInterfaceResultView.PurchaseOrderInterfaceResult
							else
							if ((PurchaseOrderImportRecordType.UpdatePurchaseOrder
							or  PurchaseOrderImportRecordType.DoNotUpdate)
							and (PrmSpecificAction = 0
							or  PrmSpecificAction = 2))
								if ((PurchaseOrderImportRecordType.UpdatePurchaseOrder
								or  PurchaseOrderImportRecordType.DoNotUpdate)
								and PurchaseOrder !entered)   
									PurchaseOrder = InterfacedPurchaseOrderRel.PurchaseOrder 
								if (PurchaseOrder entered)
									if (PurchaseOrderRel.Released = false
									and PurchaseOrderImportRecordType.UpdatePurchaseOrder)
										invoke Unreleased.Update PurchaseOrderRel
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											fill in fields from this instance
											invoked.OriginatingInterfaceRun   = LocalInterfaceResultView.PurchaseOrderInterfaceResult
											invoked.InterfaceInProcess		  = 2
											invoked.FromInterfaceUpdate       = true
											if (Vendor entered)
												invoked.Vendor                                          = Vendor
											else
											if (InterfacedVendor entered
											and VendorMappingTableExists)
												invoked.Vendor                                          = InterfaceMappingVendorRel.Vendor
											else
											if (InterfacedVendor entered
											and LegacyVendorExists)
												invoked.Vendor                                          = VendorForLegacyVendorRel.Vendor
											else
											if (CrossReferenceVendor entered)
												invoked.Vendor                                          = first CrossReferenceVendorRel.Vendor
											else
											if (invoked.Vendor !entered
											and InterfacedPurchaseOrderRel exists)
												invoked.Vendor                                          = InterfacedPurchaseOrderRel.PurchaseOrder.Vendor
									else 
									if  (PurchaseOrderRel.Released = true
									and  PurchaseOrderImportRecordType.UpdatePurchaseOrder
									and (PurchaseOrderRel.IsClosed = false
									or   Company.ReopenPurchaseOrder))
										invoke Released.Update PurchaseOrderRel
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											fill in fields from this instance
											invoked.OriginatingInterfaceRun   							= LocalInterfaceResultView.PurchaseOrderInterfaceResult
											invoked.InterfaceInProcess		  							= 2
											invoked.FromInterfaceUpdate                                 = true
											if (Vendor entered)
												invoked.Vendor                                          = Vendor
											else
											if (InterfacedVendor entered
											and VendorMappingTableExists)
												invoked.Vendor                                          = InterfaceMappingVendorRel.Vendor
											else
											if (InterfacedVendor entered
											and LegacyVendorExists)
												invoked.Vendor                                          = VendorForLegacyVendorRel.Vendor
											else
											if (CrossReferenceVendor entered)
												invoked.Vendor                                          = first CrossReferenceVendorRel.Vendor
											else
											if (invoked.Vendor !entered
											and InterfacedPurchaseOrderRel exists)
												invoked.Vendor                                          = InterfacedPurchaseOrderRel.PurchaseOrder.Vendor
									else
									if (PurchaseOrderRel.IsClosed
									and !Company.ReopenPurchaseOrder)
										ErrorOccurred 		= true
										LocalErrorMessage 	= CannotUpdateClosedMessage
									else
									if (PurchaseOrderImportRecordType.DoNotUpdate)
										invoke UpdateFromInterface PurchaseOrderRel
											invoked.SetUpdateInProcessTrue  	= true
											invoked.PrmOriginatingInterfaceRun  = LocalInterfaceResultView.PurchaseOrderInterfaceResult
								else
								if (PurchaseOrder !entered)
									ErrorOccurred 		= true
									LocalErrorMessage 	= NotFoundToUpdateMessage
								ResultProcessedBy = LocalInterfaceResultView.PurchaseOrderInterfaceResult
								
								if (ErrorOccurred)
									invoke SetError
										invoked.PrmErrorMessage 		= LocalErrorMessage
										invoked.PrmResult       		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
										invoked.PrmErrorLevel   		= 1
																			
								if (!ErrorOccurred)
									if (PurchaseOrderImportCommentsRel exists)
										for each PurchaseOrderImportCommentsRel
											if (!ErrorOccurred)
												invoke SystemCreate PurchaseOrderComment
													resume on error
														ErrorOccurred     = true
														LocalErrorMessage = error message
													fill in fields from each
													invoked.PurchaseOrder        = PurchaseOrder
												if (!ErrorOccurred)
													invoke FastDelete each
												if (ErrorOccurred)
													invoke SetError
														invoked.PrmErrorMessage = LocalErrorMessage
														invoked.PrmResult       = LocalInterfaceResultView.PurchaseOrderInterfaceResult
														invoked.PrmErrorLevel   = 6
							else
							if (PurchaseOrderImportRecordType.CancelPurchaseOrder
							and (PrmSpecificAction = 0
							or  PrmSpecificAction = 3))
								
								if (PurchaseOrder entered
								and PurchaseOrderRel.PurchaseOrderEligibleToBeCancelled)
									invoke Released.InvokeCancelPurchaseOrder PurchaseOrderRel
										invoked.PrmOriginatingInterfaceRun 	= LocalInterfaceResultView.PurchaseOrderInterfaceResult	
								else
								if (PurchaseOrder !entered)
									ErrorOccurred 		= true
									LocalErrorMessage 	= NotFoundToCancelMessage
								else
								if (PurchaseOrderRel.PurchaseOrderEligibleToBeCancelled = false)
									ErrorOccurred 		= true
									LocalErrorMessage 	= NotEligibleToCancelMessage
								ResultProcessedBy = LocalInterfaceResultView.PurchaseOrderInterfaceResult
							
								if (ErrorOccurred)
									invoke SetError
										invoked.PrmErrorMessage 		= LocalErrorMessage
										invoked.PrmResult       		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
										invoked.PrmErrorLevel   		= 1
						else
							invoke SetError
								invoked.PrmErrorMessage	= LocalErrorMessage
								invoked.PrmResult 		= LocalInterfaceResultView.PurchaseOrderInterfaceResult
								invoked.PrmErrorLevel	= 1	

		FinishLoadInterface is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmRunGroup             	is a RunGroup
				PrmCompany              	is a Company
				PrmResult               	is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
						       
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					
			Local Fields
				LocalCountFailedPO       is Numeric 12
				LocalCountPO             is Numeric 12
				LocalLastTimeStamp		 is TimeStamp
				
				
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and  ((PrmCompany  not entered
				and    GeneralLedgerCompanyRel.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or     Company	= PrmCompany))
				
			Sort Order
				RunGroup
				Company
			
			Action Rules
				Empty Set Rules
					invoke FinishLoadInterfaceLines PurchaseOrderImportLine
						invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup         	  = PrmRunGroup
						invoked.PrmCompany          	  = PrmCompany
						invoked.PrmResult           	  = PrmResult
						invoked.PrmSpecificAction         = PrmSpecificAction
					
				RunGroup Set Rules
								
					Exit Rules
						invoke FinishLoadInterfaceLines PurchaseOrderImportLine
							invoked.PrmFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup         	  = PrmRunGroup
							invoked.PrmCompany          	  = PrmCompany
							invoked.PrmResult           	  = PrmResult
							invoked.PrmSpecificAction   	  = PrmSpecificAction
						
						LocalInterfaceResult 					= PrmResult
						LocalFinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup
						if (LocalCountFailedPO > 0)
							invoke Update LocalInterfaceResultsRel
								invoked.InterfaceCounts.PassedHeaderCount += LocalCountPO
								invoked.InterfaceCounts.FailedHeaderCount += LocalCountFailedPO
													
				Instance Rules
					LocalInterfaceResult 					= PrmResult
					LocalFinanceEnterpriseGroup				= PrmFinanceEnterpriseGroup
					if (!RecordInError)

						if ((PrmSpecificAction = 0)
						or ((PurchaseOrderImportRecordType.UpdatePurchaseOrder
						or   PurchaseOrderImportRecordType.DoNotUpdate)
						and  PrmSpecificAction = 2)
						or (PurchaseOrderImportRecordType.AddPurchaseOrder
						and PrmSpecificAction = 1)
						or (PurchaseOrderImportRecordType.CancelPurchaseOrder
						and PrmSpecificAction = 3))
							invoke UpdateFromInterface PurchaseOrderRel
								invoked.SetInterfaceInProcessFalse   = true
								if (LocalInterfaceResultsRel.ReleasePurchaseOrders
								and !ClosedPO
								and !HistoricalPO)
									invoked.ReleasePurchaseOrder = true
								if (PurchaseOrderImportRecordType.AddPurchaseOrder)
									invoked.PrmTransientFromMassPOCreateAndRelease = true
							if (ImportLinesExist)
								for each PurchaseOrderImportLinesRel 
									invoke FastDelete each
							invoke FastDelete

							if ((BudgetEditConfigurationRel.BatchBudgetEdit)					
							and (GeneralLedgerSystemCodeRel.EncumbranceOption.Track or GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit)) 
								if ((LocalLastTimeStamp not entered) or (current timestamp - LocalLastTimeStamp) > 60)
									invoke InitiateBudgetEdit BudgetEditConfigurationRel in background
									LocalLastTimeStamp = current timestamp
							
					else
						LocalCountFailedPO 	+= 1
						LocalCountPO        -= 1
				
		UpdateRunGroup is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
				NewRunGroup					is a RunGroup

			Instance Selection
				where (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				Company
			
			Action Rules
				Instance Rules
					RunGroup   = NewRunGroup
		
		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like PurchaseOrderInterfaceResult
 				PrmErrorLevel           is Numeric 2
 					
			Action Rules
				LocalInterfaceResult   			= PrmResult
				LocalFinanceEnterpriseGroup		= GeneralLedgerCompanyRel.FinanceEnterpriseGroup							
				RecordInError					= true
				ErrorMessage					= PrmErrorMessage
				ErrorLevel                      = PrmErrorLevel
				

