CashLedgerReconciliationStatement is a BusinessClass
    owned by cb
    prefix is CSR
    sql name is CLedgerReconciliationStatement
    classic name is CBSTMTREC

    Ontology
        symbolic key is CashLedgerReconciliationStatement
            classic set name is CSRNEWPRIMARYINDEX
            classic name for CashLedgerReconciliationStatement.StatementNumber is STMT-NBR
            classic name for CashLedgerReconciliationStatement.StatementSuffix is STMT-SUFFIX
            classic name for CashLedgerReconciliationStatement.LineNumber is LINE-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        FinancialInstitution
        FinancialInstitutionBranch
        BankAccountNumber
            classic name is BANK-ACCT-NBR
        RecordType            is AlphaUpper size 1
            States
                CashLedgerRecord       value is "C"
                BeginningBalanceRecord value is "B"
        BankTransactionCode
            classic name is BANK-INST-CODE
        TransactionNumber
            classic name is TRANS-NBR
        ReconciliationDate    is Date
            classic name is RECON-DATE
        ReconciliationAmount  is an InternationalAmount
            classic name is RECON-AMT
        Function              is AlphaUpper size 1
            States
                Reconcile   value is "R"
                Void        value is "V"
                StopPayment value is "S"
        Description
        TransactionUserField1 is AlphaUpper size 30
            classic name is USER-FIELD1
        TransactionUserField2 is a UserField2
            classic name is USER-FIELD2
        TransactionUserField3 is AlphaUpper size 30
            classic name is USER-FIELD3
        TransactionUserField4 is a UserField2
            classic name is USER-FIELD4
        ReconciliationBalance is an InternationalAmount
            classic name is RECON-BAL
        Status                is AlphaUpper size 1
            classic name is LINE-STATUS
            States
                Open   value is "O"
                Closed value is "C"
        LastMaintenanceDate   is TimeStamp
            classic name is MAINT-DATE
        Operator
        
	Transient Fields
		OverrideCashCode						is a CashCode
					
    Conditions
        IsOpen
        	restricted
            when (not Status.Closed)

		IsCreated
			restricted
			when (CashLedgerReconciliationStatement exists)
		
		IsCashLedgerRecordType
			when (RecordType.CashLedgerRecord)
	
    Relations










        CashCodesRel
			one-to-many relation to CashCode
			Field Mapping uses Set3
				related.CashManagementGroup 		= CashManagementGroup	
				related.FinancialInstitution		= FinancialInstitution
				related.FinancialInstitutionBranch	= FinancialInstitutionBranch
				related.BankAccountNumber			= BankAccountNumber

		CashLedgerTransactionsByBankTransactionCodeRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set6
				related.CashManagementGroup		= CashManagementGroup	
				related.CashCode				= CashCode
			
    Sets
        Set3
            indexed
            Sort Order
                Status
                CashLedgerReconciliationStatement

        Set1
            not indexed
            Sort Order
                CashLedgerReconciliationStatement

		ByStatementNumber
			Sort Order
				CashLedgerReconciliationStatement.StatementNumber
				CashLedgerReconciliationStatement.StatementSuffix
				CashLedgerReconciliationStatement.LineNumber
				
    Field Rules



        ReconciliationDate
            required

        TransactionNumber
            required

		CashCode
			if (CashCode not entered)
				constraint (FinancialInstitution entered)
					"CashCodeOrBankIdentificationIsRequired"
				constraint (FinancialInstitutionBranch entered)
					"CashCodeOrBankIdentificationIsRequired"                  
				constraint (BankAccountNumber entered)
					"CashCodeOrBankAccountNumberIsRequired"                   
				constraint (CashCodesRel exists)
					"CashCodeNotFoundForEnteredBankIdAndAccountNumber"        
				CashCode = first CashCodesRel.CashCode
			else


				FinancialInstitution = CashCode.FinancialInstitution
				FinancialInstitutionBranch = CashCode.FinancialInstitutionBranch
				BankAccountNumber = CashCode.BankAccountNumber

		ReconciliationAmount
			if (Function.Reconcile)
				required
					"ReconciliationAmountRequiredForReconcileStatus"          
				if (ReconciliationAmount changed)
					constraint (ReconciliationBalance not entered) 
						"CannotChangeTransactionsAlreadyApplied"
		
		Status
			default to Status.Open

		ReconciliationBalance
			initial value is 0

		RecordType
			initial value is RecordType.CashLedgerRecord
			default to RecordType.CashLedgerRecord

		Function
			initial value is Function.Reconcile
			default to Function.Reconcile
		
		CashLedgerReconciliationStatement.LineNumber
			required

	Actions		
		Create is a Create Action
													
		Update is an Update Action
			valid when (IsOpen)
			Entrance Rules
				constraint (IsOpen)
					"CannotEdit;StatementLineClosed"                          




			Action Rules
				if (OverrideCashCode entered)
					CashCode = OverrideCashCode					
			
		Delete is a Delete Action
			valid when (IsOpen)
			Entrance Rules
				constraint (IsOpen)
					"CannotEdit;StatementLineClosed"                          
		
		ReconciliationBalanceTotal is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmIssueAmount is an InternationalAmount
			Action Rules
				ReconciliationBalance += PrmIssueAmount
				if (ReconciliationBalance = ReconciliationAmount)
					Status = Status.Closed
				else
					Status = Status.Open
					
