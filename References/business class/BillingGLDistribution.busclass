BillingGLDistribution is a BusinessClass
    owned by bl
    prefix is ODI
    classic name is OEDIST
    default label is "BillingDistribution"

    Ontology
        symbolic key is BillingGLDistribution
            classic set name is ODISET0








    Patterns
        implements StaticJava
        disable AuditIndex
		implements Archivable 

    Persistent Fields
		GeneralLedgerDate         	 is an ExchangeDate
			default label is "GlobalLedgerDate"
		DistributionAccount 		 is a FinanceCodeBlock
		AuthorizationNumber
		PostingType
        JournalEntryTransactionObjID is an ObjId
            classic name is GLT-OBJ-ID


        BillingProcessLevel
            classic name is PROCESS-LEVEL

        Customer
        Project
            classic name is ACTIVITY
        BaseAmount                   is an InternationalAmount
            classic name is AMOUNT-BASE

		CurrencyAmount
            classic name is AMOUNT-CURR
        Currency					 is a FromCurrency
            classic name is CURRENCY-CODE
        CurrencyRate
            classic name is CURR-RATE
        ReceivableGeneralLedgerCode   
		AssociatedLine          	is like UniqueID 
		TaxCode
		TaxableAmount				is an InternationalAmount
		UnitsAmount					is a Unit
	Derived Fields
		ThisProgramNameText is a MessageField
			restricted
			"BillingGLDistribution"

		DerivedBLGLDistributionCurrAmount	is a DerivedField
			type is like InternationalAmount
			restricted
			return CurrencyAmount * -1
			
		DerivedBLGLDistributionBaseAmount	is a DerivedField
			type is like InternationalAmount
			restricted
			return BaseAmount * -1				
		

		
	Transient Fields
		BypassStructureRelationEdit 

    Conditions

        IsLinePosting
        	restricted
            when (BillingInvoice entered
            and   BillingGLDistribution not entered)

        IsMiscPosting
        	restricted
            when (BillingInvoice entered
            and   BillingGLDistribution entered)




	Local Fields
		LocalCalculateBillingFinanceStructure	is a CalculateBillingFinanceStructure
		LocalAnswer								is Alpha size 1
		LocalAccount							is a FinanceCodeBlock
		LocalEnterpriseGroup					is like FinanceEnterpriseGroup	
		LocalJournalizeGroup					is a JournalizeGroup			
		LocalInvoiceType 						is AlphaUpper size 1
		LocalReceivableInvoice					is AlphaUpper 22 	

		LocalOriginalCurrency						is Alpha size 5
		LocalBasecurrency 							is Alpha size 5
		LocalReportCurrency                         is Alpha size 5
		LocalBLLineAddOnChargeAmount 				is like InternationalAmount
		LocalBLLineBaseCurrencyAddOnChargeAmount 	is like InternationalAmount
		LocalLineTaxRate							is Decimal size 7.5
		LocalBLLineTaxCode							is Alpha size 15
		LocalBODAccountingEntity					is Alpha size 25
		LocalTaxRate								is Decimal size 7.5
		LocalBLTaxCode								is Alpha size 15
		LocalTaxExemptCode 							is Alpha size 15
		LocalAddOnCharge							is Alpha size 10
		LocalTotalAddOnAmount						is like InternationalAmount
		LocalInvoiceAddOnChargeBaseAmount           is like InternationalAmount
		LocalInvoiceAddOnChargeReportAmount			is like InternationalAmount
		LocalInvoiceAddOnChargeTaxCurrAmount		is like InternationalAmount
		LocalInvoiceAddOnChargeTaxBaseCurrAmount	is like InternationalAmount
		LocalInvoiceAddOnChargeTaxReportCurrAmount	is like InternationalAmount
		LocalBLLineReportCurrencyAddOnChargeAmount  is like InternationalAmount
		LocalBLLineAOCTaxCurrencyAmount				is like InternationalAmount
		LocalBLLineAOCTaxBaseCurrencyAmount			is like InternationalAmount
		LocalBLLineAOCTaxReportCurrencyAmount		is like InternationalAmount
		LocalBLAOCCurrency							is Alpha size 5
		LocalBLAddOnChargeTrackType					is Alpha size 25
		LocalBLAddOnChargeMisc						is Alpha size 25
		LocalBLAddOnChargeTaxRate					is Decimal size 7.5
		LocalBLAOCTaxType							is Alpha size 25
		LocalBLAddOnChargeSeq						is Numeric 6
		LocalBLAddOnChargeType						is Alpha size 25
		LocalBLLineAddOnChargeType					is Alpha size 25
		LocalBLLineAddOnChargeSeq					is Numeric 6
		LocalBLLineQuantity							is Decimal size 13.4
		LocalBLLineSellingUOM						is Alpha size 4
		LocalBLLineAddOnChargeMisc					is Alpha size 25
		LocalBLLineAddOnChargeDescription			is Alpha size 60
		LocalBLLineAddOnChargeTrackType				is Alpha size 25
		LocalLineInvChargeTaxRate					is Decimal size 7.5
		

				
    Relations

		RecInvoiceRel
         	one-to-one relation to ReceivableInvoice
            Field Mapping uses Set6
                related.Company 						= Company
                related.AlternateReceivableInvoiceType  = LocalInvoiceType
                related.ReceivableInvoice          		= LocalReceivableInvoice

		ReceivableCompanyRel
         	one-to-one relation to ReceivableCompany
            Field Mapping uses symbolic key
                related.Company 						= Company




















        BillingInvoiceRel
            one-to-one relation to BillingInvoice
            required
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingInvoice.InvoicePrefix = BillingGLDistribution.BillingInvoice.InvoicePrefix
                related.BillingInvoice.InvoiceNumber = BillingGLDistribution.BillingInvoice.InvoiceNumber
							
		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 
				related.OriginatingTransaction = reference to this instance   
		
		GLTransactionDetailForRunGroupRel
			one-to-many relation to GLTransactionDetail
			Field Mapping uses ByJournalizeGroup
				related.FinanceEnterpriseGroup	 = LocalEnterpriseGroup
				related.JournalizeGroup			 = LocalJournalizeGroup
				related.Status					 = 0
				
        CustdescRel
            one-to-one relation to Customer
            required
            Field Mapping uses symbolic key
                related.CustomerGroup = Company.CustomerGroupField.CustomerGroup
                related.Customer      = Customer
                
    Sets

		SetARCode
			indexed
			Sort Order
				Company
				BillingInvoice
				ReceivableGeneralLedgerCode
				BillingInvoiceLine
				BillingGLDistribution
				
        Set2
            indexed
            Sort Order
                Company


				BillingInvoice
                AuthorizationNumber

				BillingInvoiceLine



                DistributionAccount.AccountingUnit
                DistributionAccount.GeneralLedgerChartAccount

                BillingGLDistribution
            	PostingType



























                
	Actions
		Create is a Create Action
		FastUpdate is an Update Action 
			restricted
			bypass field rules
		Delete is a Delete Action
		CreateFromBatch is a Create Action
			restricted
			Parameters
				PrmJournalizeGroup      	is a JournalizeGroup
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmAccountingEntity			is a AccountingEntity
				PrmTransactionAmount		is an InternationalAmount
				PrmTransactionDate			is Date
				PrmPostingDate				is Date
				PrmCurrencyCode				is like Currency
				PrmFinanceCodeBlock		    is like FinanceCodeBlock 
				PrmReportCurrencyAmount     is an InternationalAmount  
				
			Action Rules
				invoke Create this instance
					invoked.BypassStructureRelationEdit = true 
				invoke Create GLTransactionDetail
					fill in fields from this instance		 
					invoked.OriginatingTransaction 	= reference to this instance
					invoked.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
					invoked.System					= "BL"
					if(BillingGLDistribution.BillingInvoice entered)
						invoked.Reference				= "Invoice "+BillingInvoice.DerivedBillingInvoice
					else	
						invoked.Reference				= "BillingGLDistribution"
					invoked.AccountingEntity		= PrmAccountingEntity 
					invoked.TransactionAmount		= PrmTransactionAmount 
					invoked.CurrencyCode			= PrmCurrencyCode 
					invoked.TransactionDate			= PrmTransactionDate 
					invoked.PostingDate				= PrmPostingDate 
					invoked.FinanceCodeBlock		= PrmFinanceCodeBlock 
					invoked.Description				= Customer.Name

					invoked.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount	= PrmReportCurrencyAmount 
					invoked.ReportCurrencyAmount.KeepRateOnly = true







					if (BillingGLDistribution.BillingInvoice.BillingInvoiceType.Credit)
						invoked.GeneralLedgerEvent 		=  "CM"
					else
						invoked.GeneralLedgerEvent 		=  "DM" 													
					invoked.AutoReverse 			= false
					invoked.JournalizeGroup         = PrmJournalizeGroup
					invoked.UnitsAmount				= UnitsAmount
			Exit Rules
				invoke Unreleased.Release GLTransactionDetailRel 
		FundRedistributions is an Instance Action
			restricted
			Parameters
				PrmJournalizeGroup	is a JournalizeGroup
			Action Rules
				LocalAccount = BillingGLDistribution.DistributionAccount	
				LocalEnterpriseGroup = Company.CustomerBusinessGroup.FinanceEnterpriseGroup
				LocalJournalizeGroup = PrmJournalizeGroup
				LocalReceivableInvoice = BillingInvoiceRel.DerivedBillingInvoiceForParallelReference
				if(BillingGLDistribution.CurrencyAmount < 0)
					LocalInvoiceType	   = "C"
				else
					LocalInvoiceType	   = "I"
				for each BillingInvoiceRel.BillingGLDistributionARCodeRel
					invoke FastUpdate each 
						invoked.BypassStructureRelationEdit = true
					initialize LocalCalculateBillingFinanceStructure
					LocalCalculateBillingFinanceStructure.InputBillingCompany = each.Company
					LocalCalculateBillingFinanceStructure.InputBillingGLDistribution = each.BillingGLDistribution
					LocalCalculateBillingFinanceStructure.InputBillingInvoice	= each.BillingGLDistribution.BillingInvoice
					LocalCalculateBillingFinanceStructure.InputBillingInvoiceLine	= each.BillingGLDistribution.BillingInvoiceLine
					LocalCalculateBillingFinanceStructure.InputFinanceCodeBlock = LocalAccount
					LocalAnswer = LocalCalculateBillingFinanceStructure.CalculateBillingOffsetFinanceStructure		   		
	   				invoke Create BillingGLDistribution
	   					fill in fields from this instance
   						invoked.BaseAmount = each.BaseAmount*-1
   						invoked.CurrencyAmount = each.CurrencyAmount*-1
   						invoked.DistributionAccount = LocalCalculateBillingFinanceStructure.OutputFinanceCodeBlock
						invoked.TaxCode = each.TaxCode
						invoked.TaxableAmount = each.TaxableAmount
					if(each.PostingType != "CC"
					and each.PostingType != "CI")	
		   				invoke CreateHistoricalOnly ReceivableGLDistribution
		   					invoked.FinanceEnterpriseGroup 						  		= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
		   					invoked.TransType						  			  		= RecInvoiceRel.ReceivableInvoiceType
							invoked.Invoice							  			  		= LocalReceivableInvoice	
		   					invoked.ReceivableCompanyDataGroup.TransactionCompany 		= Company
		   					invoked.ReceivableCompanyDataGroup.ReceivableProcessLevel	= BillingGLDistribution.BillingProcessLevel
							invoked.ReceivableCompanyDataGroup.Customer					= BillingGLDistribution.Customer
	   						invoked.GeneralLedgerAccount 						 	    = each.DistributionAccount
		   					invoked.OriginalCurrency  	 						 	    = each.Currency 
		   					invoked.GeneralLedgerDate 	 						        = each.GeneralLedgerDate
		   					invoked.TransactionDate 							        = each.GeneralLedgerDate
		   					invoked.Origin										  		= "RI"
		   					invoked.Status				 						  		= 9
		   					invoked.BatchNumber									  		= RecInvoiceRel.BatchNumber
		   					invoked.DistributionAmount.CurrencyAmount 			  		= each.CurrencyAmount
		   					invoked.AccumulationType									= "D"
		   					invoked.DistributionSource									= "A"
							invoked.AssociatedLine										= each.AssociatedLine
		   					invoked.TaxCode												= each.BillingGLDistribution.TaxCode
		   					invoked.TransactionTaxableAmount							= each.BillingGLDistribution.TaxableAmount

						if (ReceivableCompanyRel.InterfaceTransDistribCreate)
							invoke CreateHistoricalOnly ReceivableGLDistribution
								invoked.FinanceEnterpriseGroup 						  		= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
								invoked.TransType						  			  		= RecInvoiceRel.ReceivableInvoiceType
								invoked.Invoice							  			  		= LocalReceivableInvoice	
								invoked.ReceivableCompanyDataGroup.TransactionCompany 		= Company
								invoked.ReceivableCompanyDataGroup.ReceivableProcessLevel	= BillingGLDistribution.BillingProcessLevel
								invoked.ReceivableCompanyDataGroup.Customer					= BillingGLDistribution.Customer
								invoked.GeneralLedgerAccount 						 	    = LocalCalculateBillingFinanceStructure.OutputFinanceCodeBlock
								invoked.OriginalCurrency  	 						 	    = each.Currency 
								invoked.GeneralLedgerDate 	 						        = each.GeneralLedgerDate
								invoked.TransactionDate 							        = each.GeneralLedgerDate
								invoked.Origin										  		= "RI"
								invoked.Status				 						  		= 9
								invoked.BatchNumber									  		= RecInvoiceRel.BatchNumber
								invoked.DistributionAmount.CurrencyAmount 			  		= each.CurrencyAmount * -1
								invoked.AccumulationType									= "S"
								invoked.DistributionSource									= "A"

	   				invoke Create GLTransactionDetail
   						fill in fields from this instance		 
						invoked.OriginatingTransaction 	= reference to each.BillingGLDistribution
						invoked.FinanceEnterpriseGroup	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
						invoked.System					= "BL"
						if(BillingGLDistribution.BillingInvoice entered)
							invoked.Reference				= "Invoice "+BillingInvoice.DerivedBillingInvoice
						else	
							invoked.Reference				= "BillingGLDistribution"
						invoked.AccountingEntity		= Company.AccountingEntity 
						invoked.TransactionAmount		= each.CurrencyAmount*-1 
						invoked.CurrencyCode			= each.Currency 
						invoked.TransactionDate			= each.GeneralLedgerDate 
						invoked.PostingDate				= each.GeneralLedgerDate  
						invoked.FinanceCodeBlock		= LocalCalculateBillingFinanceStructure.OutputFinanceCodeBlock 
						invoked.Description				= Customer.Name
						invoked.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount	= each.BaseAmount*-1 
						invoked.ReportCurrencyAmount.KeepRateOnly = true
						if (BillingGLDistribution.BillingInvoice.BillingInvoiceType.Credit)
							invoked.GeneralLedgerEvent 		=  "CM"
						else
							invoked.GeneralLedgerEvent 		=  "DM" 													
						invoked.AutoReverse 			= false
						invoked.JournalizeGroup         = PrmJournalizeGroup
				invoke Delete
				invoke Released.Delete GLTransactionDetailRel
			Exit Rules
				invoke Unreleased.Release GLTransactionDetailForRunGroupRel
				
	   	FixGLTransactions is a Set Action
	   		restricted
	   		run in foreground
	   		completion message is "JournalizationCompleteForRunGroup<MyJournalizeGroup>AndCompany<PrmCompany>"
	   		Parameters
	   			PrmCompany 		   is a BillingCompany
	   				default label is "Company"
	   			PrmEnterpriseGroup is a FinanceEnterpriseGroup
	   				default label is "FinanceEnterpriseGroup"
	   				
	   		Parameter Rules
	   			PrmCompany
	   				required
	   			PrmEnterpriseGroup
	   				initial value is Company.FinanceEnterpriseGroup
	   				default to Company.FinanceEnterpriseGroup
	   				
	   		Local Fields	
    			LocalSystemCode            	is a GeneralLedgerSystemCode
					context of PrmEnterpriseGroup
				MyJournalizeGroup 			is like JournalizeGroup
						
			Instance Selection
				where (Company = PrmCompany
				and   GeneralLedgerDate <= current corporate date
				and   GLTransactionDetailRel exists
				and   GLTransactionDetailRel.JournalizeGroup not entered
				and   GLTransactionDetailRel.Status.Released)
			Action Rules
				Set Rules
					Entrance Rules
						LocalSystemCode = "BL"
    					increment LocalSystemCode.LastJournalizeGroup
           				MyJournalizeGroup = LocalSystemCode.DerivedJournalizeGroup
					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmEnterpriseGroup in background
							invoked.PrmJournalizeGroup = MyJournalizeGroup	
				Instance Rules
					invoke UpdateJournalizeGroup GLTransactionDetailRel
						invoked.PrmJournalizeGroup = MyJournalizeGroup
						
									  	
		JournalizeDistributionsForBatch is an Instance Action 
			restricted
			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmBillingCompany		is a BillingCompany
				PrmPostThruDate			is Date
				PrmJournalizeGroup      is like JournalizeGroup
			Action Rules
				invoke JournalizeDistributions BillingGLDistribution
					invoked.PrmEnterpriseGroup = PrmEnterpriseGroup
					invoked.PrmBillingCompany  = PrmBillingCompany
					invoked.PrmPostThruDate	   = PrmPostThruDate
					invoked.PrmJournalizeGroup = PrmJournalizeGroup

		JournalizeDistributions is a Set Action		

			restricted
			completion message is "BillingDistributionPostingCompleteForJournalizeGroup...:<MyJournalizeGroup>"
			run in background

			Parameters
				PrmEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmBillingCompany	    is a BillingCompany
					default label is "BillingCompany"
				PrmPostThruDate			is Date
					default label is "PostThruDate"
				PrmJournalizeGroup      is like JournalizeGroup
			Parameter Rules
				PrmEnterpriseGroup
					initial value is Company.FinanceEnterpriseGroup
					required
				PrmBillingCompany 	 
					required
				PrmPostThruDate		 
					required	

			Local Fields	
    			LocalSystemCode            	is a GeneralLedgerSystemCode
    				context of PrmEnterpriseGroup
				MyJournalizeGroup 			is like JournalizeGroup 


			Instance Selection
				where (Company = PrmBillingCompany
				and    GLTransactionDetailRel.JournalizeGroup = PrmJournalizeGroup
				and    GeneralLedgerDate <= PrmPostThruDate)

			Sort Order  
				Company
				BillingProcessLevel
				GeneralLedgerDate

			Action Rules								
				Set Rules
					Entrance Rules
						MyJournalizeGroup = PrmJournalizeGroup
    					LocalSystemCode = "BL"
					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmEnterpriseGroup in background
							invoked.PrmJournalizeGroup = MyJournalizeGroup

				Instance Rules
					display "BillingInvoice=<BillingInvoice>;BillingInvoiceLine=<BillingInvoiceLine>;PrmJournalizeGroup=<PrmJournalizeGroup>"
					
		Purge is a Purge Action
			restricted
			Entrance Rules
				invoke PurgeValidSubLedgerTrx GLTransactionDetailRel						
