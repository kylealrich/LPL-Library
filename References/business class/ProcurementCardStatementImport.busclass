ProcurementCardStatementImport is a BusinessClass
    owned by po
    prefix is PX1
    classic name is PDCCHRGHDR

    Ontology
        symbolic key is ProcurementCardStatementImport
            classic set name is PX1SET1
            classic name is STATEMENT
            classic name for ProcurementCardProgram is PCARD-PROGRAM

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
        Company           is a PurchasingCompany
        StatementFromDate is Date
            classic name is STMT-FR-DATE
        StatementToDate   is Date
            classic name is STMT-TO-DATE
        RecordInError     is Boolean
        ErrorMessage	  is Alpha 150
        ErrorLevel        is Numeric 2
        	States
        		Statement       value is 1
        		Transaction     value is 2
        		Distribution    value is 3           
        ResultProcessedBy                is a ProcurementCardStatementInterfaceResult

    Local Fields
    	
    	ErrorOccurred                    is Boolean
		LocalInterfaceResult             is like ProcurementCardStatementInterfaceResult
		LocalProcurementGroup            is like ProcurementGroup
    	LocalErrorMessage                is Alpha 150
    
    Derived Fields
    
    	NoImportTransactionsMessage is a MessageField
    		restricted
    		"CannotAddStatement;MustHaveTransactionRecordsToProcess"
    		
    	DuplicateStatementMessage is a MessageField
    		restricted
    		"StatementAlreadyExists"
    
    Conditions
		HasErrors
			restricted
			when (ErrorMessage entered)
			
		ImportTransactionsExist
			restricted
			when (ProcurementCardStatementImportTransactionRel exists)
			
		StatementExists
			restricted
			when (ProcurementCardStatementRel exists)
			
		SecurityGroupAllowsAccess
			when((Company not entered)
				or (Company entered and Company.SecurityGroupAllowsAccess))
		
		CompanyExists
			restricted
			when (Company entered)
		
		ProcurementGroupExists
			restricted
			when (ProcurementGroup entered)
		
		ProcurementCardExists
			restricted
			when (ProcurementCardProgram entered)

		ProcurementCardStatementExists
			restricted
			when (ProcurementCardStatementImport entered)

    Relations
    
    	StatementImportByProcurementGroupRel
    		one-to-many relation to ProcurementCardStatementImport
    	  		Field Mapping uses symbolic key
    	  			related.ProcurementGroup      = LocalProcurementGroup

		LocalInterfaceResultsRel
			one-to-one relation to ProcurementCardStatementInterfaceResult
			Field Mapping uses symbolic key
				related.ProcurementGroup						= LocalProcurementGroup
				related.ProcurementCardStatementInterfaceResult	= LocalInterfaceResult 

		ProcurementCardStatementImportTransactionRel
			one-to-many relation to ProcurementCardStatementImportTransaction
			delete cascades
			Field Mapping uses symbolic key
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatementImport          = ProcurementCardStatementImport
				
		ProcurementCardStatementRel
			one-to-one relation to ProcurementCardStatement
			Field Mapping uses symbolic key
				related.ProcurementGroup                        = ProcurementGroup
				related.ProcurementCardProgram                  = ProcurementCardProgram
				related.ProcurementCardStatement                = ProcurementCardStatementImport
				
		FactSheetRel
			one-to-one relation to SampleDocumentTemplate
			Field Mapping uses symbolic key
				related.SampleDocumentTemplate      = "ProcurementCardStatementInterface_ST"

	Sets
	
		ByResult
			Sort Order
				ResultProcessedBy
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatementImport
	
	Actions
	
		Create is a Create Action
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules
		
		Update is an Update Action
		
		Delete is a Delete Action
		
			Entrance Rules
				invoke Delete ProcurementCardStatementRel
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules

		InterfaceProcurementCardStatement is an Instance Action
			completion message is "LoadInterfacedProcurementCardStatementsIsSubmitted"
			Parameters
				PrmProcurementGroup					is a ProcurementGroup
					default label is "ProcurementGroup"
				PrmProgram                          is a ProcurementCardProgram 
					default label is "Program"
				PrmStatement                        is a ProcurementCardStatementImport
					default label is "Statement"
				PrmResult                           is a ProcurementCardStatementInterfaceResult
					default label is "EnterResultNumberIfRerunningStatementWithErrors"
				PrmPostingDate						is a PostingDate
					default label is "PostingDate"
			Parameter Rules
			
				PrmProcurementGroup
					initial value is ProcurementGroup
					default to ProcurementGroup
					
				PrmProgram
					initial value is ProcurementCardProgram
					default to ProcurementCardProgram
					
				PrmStatement
					initial value is ProcurementCardStatementImport
					default to ProcurementCardStatementImport
					
				PrmResult
					initial value is ResultProcessedBy
					default to ResultProcessedBy 
			
			Action Rules
			
				invoke InterfaceProcurementCardStatements
					invoked.PrmProcurementGroup    = PrmProcurementGroup
					invoked.Program                = PrmProgram
					invoked.Statement              = PrmStatement
					invoked.PrmResult              = PrmResult
					invoked.PrmPostingDate		   = PrmPostingDate
		
		InterfaceProcurementCardStatements is a Set Action
			restricted
			completion message is "LoadInterfacedProcurementCardStatementsIsSubmitted"
			Parameters
				PrmProcurementGroup					is a ProcurementGroup
					default label is "ProcurementGroup"
				Program                             is a ProcurementCardProgram 
				Statement                           is a ProcurementCardStatementImport
				PrmResult                           is a ProcurementCardStatementInterfaceResult
				PrmPostingDate						is a PostingDate
			Parameter Rules
				
				PrmProcurementGroup
					LocalProcurementGroup     = PrmProcurementGroup
					required
						"ProcurementGroupIsRequired"
					constraint (StatementImportByProcurementGroupRel exists)
						"NoRecordsToProcessForProcurementGroup"
				
			Local Fields
				LocalInterfaceResultView		is a ProcurementCardStatementInterfaceResult view				
			
			Instance Selection			
				where (ProcurementGroup				= PrmProcurementGroup
				and   (ProcurementCardProgram 		= Program
				or     Program !entered)
				and   (ProcurementCardStatementImport = Statement
				or     Statement !entered)
				and   (PrmResult                    = ResultProcessedBy
				or     ResultProcessedBy !entered))

			Sort Order
				ProcurementGroup
				ProcurementCardProgram
				ProcurementCardStatementImport
						
			Action Rules
			
				ProcurementGroup Set Rules
					Entrance Rules
			
						ErrorOccurred							= false
						
						invoke Create ProcurementCardStatementInterfaceResult
							assign result to LocalInterfaceResultView
							invoked.RunTime							= current timestamp
							invoked.ProcurementGroup				= PrmProcurementGroup
							invoked.ProcurementCardProgram	 		= Program
							invoked.ProcurementCardStatement	 	= Statement
							
					Exit Rules
					
						invoke InterfaceProcurementCardStatementTransactions ProcurementCardStatementImportTransaction 
							invoked.PrmProcurementGroup  		= PrmProcurementGroup
							invoked.PrmResult    		        = LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
							invoked.Program                     = Program
							invoked.Statement                   = Statement

				Instance Rules
				
					if (ProcurementCardStatementRel exists
					and !RecordInError)
						ErrorOccurred 		= true
						LocalErrorMessage 	= DuplicateStatementMessage
					else
						ErrorOccurred						= false
						RecordInError                       = false
						initialize ErrorLevel                          
						initialize ErrorMessage
					if (ProcurementCardStatementRel exists)
						invoke UpdateFromInterface ProcurementCardStatementRel
							invoked.PrmOriginatingInterfaceRun = LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
					ResultProcessedBy 				= LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
					if (ProcurementCardStatementRel !exists)
						if  (ImportTransactionsExist)
							invoke Create ProcurementCardStatement
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message
								fill in fields from this instance
								invoked.ProcurementCardStatement = ProcurementCardStatementImport
								invoked.InterfaceInProcess		 = 1
								invoked.OriginatingInterfaceRun  = LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
								invoked.PostingDate				 = PrmPostingDate
						else
						if (!ImportTransactionsExist)
							ErrorOccurred 		= true
							LocalErrorMessage 	= NoImportTransactionsMessage
					if (ErrorOccurred)
						invoke SetError
							invoked.PrmErrorMessage 		= LocalErrorMessage
							invoked.PrmResult       		= LocalInterfaceResultView.ProcurementCardStatementInterfaceResult
							invoked.PrmErrorLevel   		= 1	
							
		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmResult      			is like ProcurementCardStatementInterfaceResult
 				PrmErrorLevel           is Numeric size 2
 					
			Action Rules
				LocalInterfaceResult   = PrmResult
				LocalProcurementGroup  = ProcurementGroup
						
				RecordInError					= true
				ErrorMessage					= PrmErrorMessage
				ErrorLevel                      = PrmErrorLevel
				
				invoke UpdateErrorInformation LocalInterfaceResultsRel
		
		FinishLoadInterface is a Set Action
			restricted
			Parameters
				PrmProcurementGroup     is like ProcurementGroup
				PrmResult               is like ProcurementCardStatementInterfaceResult
						       
			Instance Selection
				where (ResultProcessedBy = PrmResult)
				
			Sort Order
				ProcurementGroup
				
			Action Rules
				Empty Set Rules
					LocalProcurementGroup      = PrmProcurementGroup
					LocalInterfaceResult       = PrmResult
					invoke Finish LocalInterfaceResultsRel
				
				ProcurementGroup Set Rules
								
					Exit Rules
						LocalProcurementGroup      = PrmProcurementGroup
						LocalInterfaceResult       = PrmResult
						invoke Finish LocalInterfaceResultsRel
						
				Instance Rules
					LocalInterfaceResult = PrmResult
					if (!RecordInError)
						invoke UpdateFromInterface ProcurementCardStatementRel
							invoked.SetInterfaceInProcessFalse   = true
						if (ImportTransactionsExist)
							for each ProcurementCardStatementImportTransactionRel
								invoke FastDelete each
						invoke FastDelete
						
						
		
	
