ADBAggregateBalance is a BusinessClass
    owned by ad
    prefix is ADAVG

    Ontology
    	symbolic key is ADBAggregateBalance

    Patterns
 		implements DynamicCreation
		implements LightweightAuditing
		disable AuditIndex
 		disable EffectiveDated
		disable AsOfDateProcessing 		

    Persistent Fields
       	AccountingUnit
		FinanceDimension1
		BalanceOnPostDate				is an InternationalAmount
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
		BalanceOnTransactionDate		is an InternationalAmount
 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals

	Transient Fields
		PrmBalanceOnPostDate			is an InternationalAmount
		PrmBalanceOnTransactionDate		is an InternationalAmount

	Derived Fields
		NoOfDays						is a DerivedField
			type is Numeric 4 
			if (ReportingBasis.ADBRunDate		< ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate)
				return ((ReportingBasis.ADBRunDate - ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedStartDate) + 1)
			else
				return ((ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate - ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedStartDate) + 1)
		AverageOnPostDate				is a DerivedField
			type is like InternationalAmount 
	 			precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
			return (BalanceOnPostDate / NoOfDays)
		AverageOnTransactionDate		is a DerivedField
			type is like InternationalAmount 
 				precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
			return (BalanceOnTransactionDate / NoOfDays)

		YTDNoOfDays						is a DerivedField
			type is Numeric 4
			if (ReportingBasis.ADBRunDate < ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate)
				return ((ReportingBasis.ADBRunDate - CalendarYearTypeYearRel.DerivedStartDate) + 1)
			else
				return ((ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate - CalendarYearTypeYearRel.DerivedStartDate) + 1)

		YTDAggregateOnPostDate is a DerivedField
			type is like InternationalAmount 
				precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
			if (ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType.Year)
				return BalanceOnPostDate
			else
				LocalFromStartDateNumeric  = CalendarYearTypeYearRel.StartDateNumeric
				LocalToStartDateNumeric    = ADBAggregateBalance.GeneralLedgerCalendarPeriod.StartDateNumeric
				if (ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType.Quarter)
					LocalPeriodType            = 2
				if (ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType.Month)
					LocalPeriodType            = 3
				return sum PeriodToDateAggregateRel.BalanceOnPostDate

		YTDAverageOnPostDate is a DerivedField
			type is like InternationalAmount 
				precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
			if (ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType.Year)
				return AverageOnPostDate
			else
				return (YTDAggregateOnPostDate / YTDNoOfDays)

		QTDNoOfDays						is a DerivedField
			type is Numeric 4
			if (ReportingBasis.ADBRunDate < ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate)
				return ((ReportingBasis.ADBRunDate - CalendarQuarterPeriodRel.DerivedStartDate) + 1)
			else
				return ((ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate - CalendarQuarterPeriodRel.DerivedStartDate) + 1)

		QTDAggregateOnPostDate is a DerivedField
			type is like InternationalAmount 
				precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
			if (ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType.Month)
				LocalFromStartDateNumeric  = CalendarQuarterPeriodRel.StartDateNumeric
				LocalToStartDateNumeric    = ADBAggregateBalance.GeneralLedgerCalendarPeriod.StartDateNumeric
				LocalPeriodType            = 3
				return sum PeriodToDateAggregateRel.BalanceOnPostDate
			else
				return BalanceOnPostDate

		QTDAverageOnPostDate is a DerivedField
			type is like InternationalAmount 
				precision is AccountingEntity.FunctionalCurrency.NumberOfDecimals
			if (ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType.Month)
				return (QTDAggregateOnPostDate / QTDNoOfDays)
			else
				return AverageOnPostDate

	Local Fields
		LocalFromStartDateNumeric		is Numeric 7
		LocalToStartDateNumeric			is Numeric 7
		LocalPeriodType					is Numeric 1

	Relations
		ADBDailyAmountRel
			one-to-many relation to ADBDailyAmount
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.ReportingBasis					= ReportingBasis
				related.AccountingEntity				= AccountingEntity
			Instance Selection
				where (related.ADBDailyAmount.ADBOrganizationDimension	= ADBAggregateBalance.ADBOrganizationDimension
				and    related.ADBDailyAmount.GeneralLedgerChartAccount	= ADBAggregateBalance.GeneralLedgerChartAccount
				and    related.ADBDailyAmount.RunDate					>=  ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedStartDate
				and    related.ADBDailyAmount.RunDate					<=  ADBAggregateBalance.GeneralLedgerCalendarPeriod.DerivedEndDate)

		CalendarYearTypeYearRel	
		    one-to-many relation to GeneralLedgerCalendarPeriod
		    Field Mapping uses ByTopNode
		        related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
		        related.TopNode                = ADBAggregateBalance.GeneralLedgerCalendarPeriod.TopNode
		        related.Year                   = ADBAggregateBalance.GeneralLedgerCalendarPeriod.Year
		        related.PeriodType             = 1	
		
		CalendarQuarterPeriodRel 
		    one-to-many relation to GeneralLedgerCalendarPeriod
		    Field Mapping uses ByTopNode
		        related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
		        related.TopNode                = ADBAggregateBalance.GeneralLedgerCalendarPeriod.TopNode
		        related.Year                   = ADBAggregateBalance.GeneralLedgerCalendarPeriod.Year
		        related.PeriodType             = 2 
			Instance Selection
				where (related.StartDateNumeric <= ADBAggregateBalance.GeneralLedgerCalendarPeriod.StartDateNumeric
				and    related.EndDateNumeric   >= ADBAggregateBalance.GeneralLedgerCalendarPeriod.EndDateNumeric)

		PeriodToDateAggregateRel
		    one-to-many relation to ADBAggregateBalance
		    Field Mapping uses symbolic key
		        related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
		        related.ReportingBasis         = ReportingBasis
		        related.AccountingEntity       = AccountingEntity
		    Instance Selection
		        where (related.ADBAggregateBalance.ADBOrganizationDimension = ADBAggregateBalance.ADBOrganizationDimension
		        and   related.ADBAggregateBalance.GeneralLedgerChartAccount = ADBAggregateBalance.GeneralLedgerChartAccount
		        and   related.ADBAggregateBalance.GeneralLedgerCalendarPeriod.PeriodType = LocalPeriodType
		        and   related.ADBAggregateBalance.GeneralLedgerCalendarPeriod.Year = ADBAggregateBalance.GeneralLedgerCalendarPeriod.Year
		        and   related.ADBAggregateBalance.GeneralLedgerCalendarPeriod.StartDateNumeric >= LocalFromStartDateNumeric
		        and   related.ADBAggregateBalance.GeneralLedgerCalendarPeriod.StartDateNumeric <= LocalToStartDateNumeric)

	Conditions
		DisplayADBInTransactionDate
			restricted
			when (config.UsePostDateAndTranDateInAdb = true)

	Sets				
		ByGeneralLedgerCalendarPeriod
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				ADBAggregateBalance.GeneralLedgerCalendarPeriod
				AccountingEntity
				ADBAggregateBalance.GeneralLedgerChartAccount
				AccountingUnit
				FinanceDimension1

	Field Rules

	Rule Blocks
		UpdateAggregateAmount
			BalanceOnPostDate				+= PrmBalanceOnPostDate
			BalanceOnTransactionDate		+= PrmBalanceOnTransactionDate
			initialize PrmBalanceOnPostDate
			initialize PrmBalanceOnTransactionDate

	Actions
		Create is a Create Action
			restricted
			bypass field rules
			Action Rules
				if (FinanceEnterpriseGroup.ADBSelection.AccountAndAccountingUnit)
					AccountingUnit			= ADBAggregateBalance.ADBOrganizationDimension
				if (FinanceEnterpriseGroup.ADBSelection.AccountAndDimension1)
					FinanceDimension1		= ADBAggregateBalance.ADBOrganizationDimension
				include UpdateAggregateAmount

		Update is an Update Action
			restricted
			bypass field rules
			Action Rules
				include UpdateAggregateAmount

		Delete is a Delete Action
			restricted
			Action Rules

		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis			is like ReportingBasis
					default label is "ReportingBasis"
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and	   ReportingBasis				= PrmReportingBasis)
			Set Is
				PrmFinanceEnterpriseGroup
				PrmReportingBasis
			Action Rules
				Empty Set Rules
				Instance Rules
					invoke Delete 

		Purge is a Purge Action
			restricted

		PurgeData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmReportingBasis				is a ReportingBasis
				PrmAccount						is like GeneralLedgerChartAccount
				PrmFiscalYear					is like GeneralLedgerCloseYear
				PrmAccountingEntity				is like AccountingEntity
				PrmADBOrganizationDimension		is like AccountingUnit
				PrmAccountToPurge				is a ADBAccountType

			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    (PrmReportingBasis           not entered or PrmReportingBasis   = ReportingBasis)
				and    (PrmAccount                  not entered or PrmAccount          = ADBAggregateBalance.GeneralLedgerChartAccount)
				and    (PrmAccountingEntity         not entered or PrmAccountingEntity = AccountingEntity)
				and    (PrmADBOrganizationDimension not entered or PrmADBOrganizationDimension = ADBAggregateBalance.ADBOrganizationDimension)
				and    (PrmFiscalYear               not entered
				or      ((PrmAccountToPurge.SystemUndistributedRetainedEarnings and ADBAggregateBalance.GeneralLedgerCalendarPeriod.Year  = PrmFiscalYear)
				or       (PrmAccountToPurge.NormalBalanceSheet                  and ADBAggregateBalance.GeneralLedgerCalendarPeriod.Year >= PrmFiscalYear))))

			Action Rules
				Instance Rules
					invoke Purge
