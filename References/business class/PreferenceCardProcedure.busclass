PreferenceCardProcedure is a BusinessClass
    owned by ic
    prefix is PCCP

    Ontology
        symbolic key is PreferenceCardProcedure

    Patterns

        implements StaticJava
        implements DynamicCreation

    Persistent Fields
        Procedure           is a ClinicalProcedure
            default label is "ClinicalProcedure"
        Active              is Boolean

    Transient Fields

    Derived Fields
        ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforPreferenceCardProcedure"

        ContextMessageText is a MessageField
            restricted
            "PreferenceCardProcedure<PreferenceCardProcedure>"

        ActiveMessage is a LabelField
            "Active"
        
        InactiveMessage is a LabelField
            "Inactive"

        ActiveDisplay is a DerivedField
            type is MessageField
            default label is "Active"
            if (Active)
                return ActiveMessage
            else
                return InactiveMessage

        ProcedureNotEnteredMessage is a MessageField
            "ClinicalProcedureIsRequired"

        ProcedureDoesNotExistMessage is a MessageField
            restricted
            "ClinicalProcedureDoesNotExist"
        
        InactiveProcedureMessage is a MessageField
            "ClinicalProcedureIsInactive"

        ProcedureAlertDisplayMessage is a DerivedField
            type is MessageField
            if (Procedure not entered)
                return ProcedureNotEnteredMessage
            else
                if (Procedure not exists)
                    return ProcedureDoesNotExistMessage
                else
                    if (not Procedure.Active)
                        return InactiveProcedureMessage

    Local Fields

    Context Fields

    Rule Blocks
        CreateOrUpdateEdits
            invoke Purge PreferenceCardProcedureErrorRel

            if (Procedure not entered)
                invoke Create PreferenceCardError
                    fill in fields from this instance
                    invoked.ErrorRecordType                 = 2 
                    invoked.ErrorMessageNumber              = 7
                    invoked.ErrorPreferenceCard             = PreferenceCard
                    invoked.ErrorRecordNumber               = PreferenceCardProcedure
            else 
                if (Procedure not exists)
                    invoke Create PreferenceCardError
                        fill in fields from this instance
                        invoked.ErrorRecordType             = 2 
                        invoked.ErrorMessageNumber          = 8
                        invoked.ErrorPreferenceCard         = PreferenceCard
                        invoked.ErrorRecordNumber           = PreferenceCardProcedure
                        invoked.ErrorClinicalProcedure      = Procedure
                else 
                    if (SameProcedureRel exists)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType         = 2 
                            invoked.ErrorMessageNumber      = 9
                            invoked.ErrorPreferenceCard     = PreferenceCard
                            invoked.ErrorRecordNumber       = PreferenceCardProcedure
                            invoked.ErrorClinicalProcedure  = Procedure

                    if (not Procedure.Active)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType         = 2 
                            invoked.ErrorMessageNumber      = 10
                            invoked.ErrorPreferenceCard     = PreferenceCard
                            invoked.ErrorRecordNumber       = PreferenceCardProcedure
                            invoked.ErrorClinicalProcedure  = Procedure

    Relations
        PreferenceCardProcedureErrorRel
            one-to-many relation to PreferenceCardError
            Field Mapping uses symbolic key
                related.ItemGroup                       = ItemGroup
                related.PreferenceCard                  = PreferenceCard
            Instance Selection
                where (related.ErrorRecordType.Procedure
                and    related.ErrorRecordNumber        = PreferenceCardProcedure)

        SameProcedureRel
            one-to-many relation to PreferenceCardProcedure
            Field Mapping uses ByClinicalProcedure
                related.ItemGroup                       = ItemGroup
                related.Procedure                       = Procedure
                related.PreferenceCard                  = PreferenceCard
            Instance Selection
                where (related.PreferenceCardProcedure != PreferenceCardProcedure)

    Conditions
        ValidRecordExists
            restricted
            when (PreferenceCardProcedure exists
            and   ValidProcedureExists)

        ValidProcedureExists
            restricted
            when (Procedure entered
            and   Procedure exists)

        DisplayAlert
            restricted
            when (not ValidProcedureExists
            or    not Procedure.Active)

    Field Groups
        PreferenceCardProcedureFieldGroup
            ItemGroup
            PreferenceCardProcedure
            Procedure

    Sets
        ByClinicalProcedure
            duplicates
            Sort Order
                ItemGroup
                Procedure
                PreferenceCard

    Field Rules

    Actions
        Create is a Create Action
            Exit Rules
                include CreateOrUpdateEdits

        Update is an Update Action
            Entrance Rules
                include CreateOrUpdateEdits

        Delete is a Delete Action
            Entrance Rules
                invoke Purge PreferenceCardProcedureErrorRel

        ReviewErrors is an Instance Action
            restricted
            Action Rules
                include CreateOrUpdateEdits
