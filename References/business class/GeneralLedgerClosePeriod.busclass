GeneralLedgerClosePeriod is a BusinessClass
	default label is "GlobalLedgerClosePeriod"
	owned by GeneralLedger
	representative text is "<PeriodName>"
	
	prefix is GLClP
	
	Ontology
		symbolic key is GeneralLedgerClosePeriod
	
	Patterns
        implements StaticJava
		implements BODId

	Persistent Fields
		BeginDate				is Alpha 7
		GeneralLedgerCloseYear
			default label is "GlobalLedgerCloseYear"
		PeriodNumber			is Numeric 3		
		PeriodName				is Alpha 25
			translatable
		WeeksInPeriod			is Numeric 3		

	
	Context Fields
		ContextYear				is a GeneralLedgerCloseYear
#ifdef module integration
		FSMInboundBODTracker
#endif
			
	Local Fields
		LocalDateAlpha			is Alpha 8
		LocalDayNumeric			is Numeric 3		
		LocalDate				is Date	
		LocalPriorEndDate		is Numeric 7
		LocalYearAlpha			is Alpha 4
		LocalYearNumeric		is Numeric 4
		LocalEndDateNumeric		is Numeric 7
		LocalJulianAlpha		is Alpha 7
		TempDefaultPeriodName	is Alpha 50
    		Text Variables
				MonthName		  value is GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date month name
				MonthAbbreviation value is GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date month short name
				MonthNumber		  value is GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date month
				CloseYear		  value is GeneralLedgerCloseYear
				EndDateYear		  value is GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date year
				NthPeriodOfYear   value is PeriodNumber
		LocalActionCode			 is an ActionCode
		LocalBODCurrentTimeStamp is a BODCurrentTimeStamp
		LocalYearDiff			 is Numeric 6

		LocalNativeLPLBODTrigger			is Boolean
		LocalGLCPDate						is Alpha 25
		LocalEndDate						is Alpha 25
		LocalStartDate						is Alpha 25
		LocalDerivedBeginGregorian			is Alpha 25
		LocalGLCYear						is Alpha 25
		LocalGLCPeriod						is Alpha 25
		LocalPeriodName						is Alpha 25
		LocalPeriodNumber					is Alpha 25
		LocalStatusCode						is Alpha 10
		LocalCalendarPeriodXML				is XMLDocument


		LocalAccountingEntity				is a AccountingEntity
		LocalPeriodXML						is XMLDocument
		LocalEntityLedgerPeriodStatus		is Alpha 8
		LocalAddressCode					is Alpha 9
		LocalBeginningBalancePeriodName		is Alpha 60
		LocalBeginningBalancePeriod			is Alpha 30
		LocalBeginDateUpdate				is Boolean
#ifdef module integration
		NewBODTracker  						is a FSMInboundBODTracker view
#endif
		LocalFSMInboundBODTracker			is Numeric 15
		Error            					is Boolean
	    ErrorMessage     					is Alpha 300
		LocalBODTrigger						is Boolean
		LocalConfigurationParameter			is Alpha size up to 200
		
	Transient Fields
		TransientBeginDateUpdate			is Boolean
		
	Conditions
		PeriodInContextYear
			restricted
			when (GeneralLedgerCloseYear = ContextYear)
			
	Derived Fields
		DerivedBeginDate	is a DerivedField
			type is Numeric 7
			restricted
			
			LocalPriorEndDate = last PriorPeriodsInThisYear.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
			
			if (!LocalPriorEndDate entered)
				LocalPriorEndDate = last PeriodsInPriorYear.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				
			if (!LocalPriorEndDate entered)
				LocalYearAlpha    = GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				LocalYearNumeric  = LocalYearAlpha
				LocalPriorEndDate = (LocalYearNumeric * 1000)	
			
			return (LocalPriorEndDate + 1)

		DerivedBeginGregorian is a DerivedField
			type is Date
			restricted
			LocalJulianAlpha = DerivedBeginDate
			LocalDayNumeric  = LocalJulianAlpha[5:7]
			LocalDateAlpha   = LocalJulianAlpha[1:4] + "0101" 
			LocalDate 		 = LocalDateAlpha
			LocalDate 	    += (LocalDayNumeric - 1)
			return LocalDate
						
    	DerivedDefaultPeriodName is a DerivedField
    		type is Alpha size 50
    		restricted
    		TempDefaultPeriodName 	 = GeneralLedgerCloseConfiguration.DefaultPeriodName
    		DerivedDefaultPeriodName = TempDefaultPeriodName text
    	
    	DerivedNthPeriodOfYear is a DerivedField
    		type is Numeric 3
    		restricted
    		return (instance count of PriorPeriodsInThisYear.GeneralLedgerClosePeriod + 1)
    	
    	DerivedEndDate is a DerivedField
			type is Date
			return GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date


    	DerivedPeriodMonth is a DerivedField
			type is Numeric 2
			LocalDate = DerivedEndDate - 7 days
			return LocalDate month

    	DerivedPeriodYear is a DerivedField
			type is Numeric 4
			LocalDate = DerivedEndDate - 7 days
			return LocalDate year		

    	

#ifdef module integration
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (LocalActionCode = "D" or LocalActionCode.Delete) 
				return "Deleted"
			else 
				return "Open"
	
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (LocalActionCode = "C" or LocalActionCode.Create)
				return "Add"
			else
				return "Replace"
				
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
		
		DerivedBODVariationID is a DerivedField
			type is Alpha size 25
			restricted
			return  bod id.VariationID
			
		DerivedAccountingEntity is a DerivedField
			type is Alpha size 25
			restricted
			return  ""
					
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return current timestamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
			
		DerivedlastModUserID is a DerivedField
			type is Alpha size 25
			restricted
			return update stamp.actor
			
		DerivedyearMethod is a DerivedField
			type is Alpha size 25
			restricted
			if  (GeneralLedgerCloseConfiguration.PeriodsAreEndOfMonth = true)
				return "Calendar"
			else 
				return "Fiscal"
				
		DerivedDescription is a DerivedField
			type is Alpha size 25
			restricted
			return GeneralLedgerCloseConfiguration.Description
					
		DerivedSystemOfRecord is a DerivedField
			type is Alpha size 25
			restricted
			return 0	
		
		FormattedDerivedGLCPBeginGregorian is a DerivedField
			type is Alpha size 25
			restricted
			return LocalDerivedBeginGregorian[1:4] + "-" + LocalDerivedBeginGregorian[5:6] + "-" + LocalDerivedBeginGregorian[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
			
		FormattedDerivedDate is a DerivedField
			type is Alpha size 25
			restricted
			return LocalGLCPDate[1:4] + "-" + LocalGLCPDate[5:6] + "-" + LocalGLCPDate[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
		
		FormattedDerivedstartDate is a DerivedField
			type is Alpha size 25
			restricted
			return LocalStartDate[1:4] + "-" + LocalStartDate[5:6] + "-" + LocalStartDate[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
		
		FormattedDerivedEndDate is a DerivedField
			type is Alpha size 25
			restricted
			return LocalEndDate[1:4] + "-" + LocalEndDate[5:6] + "-" + LocalEndDate[7:8] + "T" + "00" +":"+"00" +":"+"00" + "Z"
			

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
			if (DerivedDelimiter not entered)
				return "-"
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedBODID is a DerivedField
			type is Alpha 100
			restricted
			return "infor-nid:" + DerivedTenantID +":" + DerivedAccountingEntity + ":" + ":" +DerivedDocumentID + ":" +DerivedBODRevision+"?FinancialCalendar&verb=Sync&TrackerID="+ LocalFSMInboundBODTracker
					
		DerivedDocumentID is a DerivedField
			type is Alpha size 25
			restricted
			return FinanceEnterpriseGroup+DerivedDelimiter+GeneralLedgerCloseConfiguration
					



		FinancialCalendarBODXML is a DerivedField
			type is XMLDocument
			restricted
			FinancialCalendarBODXML = template.IONSyncFinancialCalendar_GeneralLedgerClosePeriod_Header_ST document for this instance



		CalendarPeriodXML is a DerivedField
			type is XMLDocument
			restricted
			if (GeneralLedgerCalendarPeriodRel exist)
				return LocalCalendarPeriodXML
			else
				return ""
				
		GeneralLedgerCloseYearXML is a DerivedField
			type is XMLDocument
			restricted
			if (GeneralLedgerCloseYearRel exist)
				initialize GeneralLedgerCloseYearXML
				for each GeneralLedgerCloseYearRel
					initialize LocalCalendarPeriodXML
					LocalStartDate	= each.DerivedStartDate
					LocalGLCYear	= each.GeneralLedgerCloseYear
					if (GeneralLedgerClosePeriodRel exist)
						for each GeneralLedgerClosePeriodRel
							LocalGLCPeriod 				= each.GeneralLedgerClosePeriod
							LocalDerivedBeginGregorian	= each.DerivedBeginGregorian
							LocalPeriodName				= each.PeriodName
							LocalPeriodNumber			= each.PeriodNumber
							if (each.LocalActionCode = "D" or each.LocalActionCode.Delete)
								LocalStatusCode			= "Deleted"
							else
								LocalStatusCode			= "Open"
							if (GeneralLedgerCalendarPeriodRel exist)
								LocalGLCPDate			= GeneralLedgerCalendarPeriodRel.Date
							LocalEntityLedgerPeriodStatus = "Open"
							if(DerivedIntegrationApplication = "Local.ly")
								if(EntityLedgerPeriodRel exists)
				 					LocalEntityLedgerPeriodStatus = EntityLedgerPeriodRel.PeriodStatus
				 				if (LocalAccountingEntity.FinanceEnterpriseGroup = FinanceEnterpriseGroup)
				 					LocalAddressCode = LocalAccountingEntity.AddressCode
				 				if(LocalAddressCode = "FR")
				 					if(LocalEntityLedgerPeriodStatus = "3")
				 						LocalEntityLedgerPeriodStatus = "Closed"
				 					else
				 						LocalEntityLedgerPeriodStatus = "Open"
				 				else
				 					LocalEntityLedgerPeriodStatus = "Open"
							LocalCalendarPeriodXML += template.IONSyncFinancialCalendar_GeneralLedgerClosePeriod_Header_CalendarPeriod_ST document for this instance
					LocalEndDate    = LocalGLCPDate
					if (DerivedIntegrationApplication = "Local.ly" and DerivedBeginningBalanceFlag = "Y")
						LocalBeginningBalancePeriod = LocalGLCYear +"000"
						LocalBeginningBalancePeriodName = "January" +" " + LocalGLCYear + " "+ "Beginning" + " "+" Period"
						LocalPeriodXML = template.IONSyncFinancialCalendar_GeneralLedgerClosePeriod_PeriodXML_ST document for this instance
					GeneralLedgerCloseYearXML += template.IONSyncFinancialCalendar_GeneralLedgerClosePeriod_Header_GLCloseYear_ST document for this instance
				return GeneralLedgerCloseYearXML
			else 
				return ""
						
		DerivedIntegrationApplication is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "IntegrationApplication"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedBeginningBalanceFlag is a DerivedField
			type is Alpha size 2
			restricted
			LocalConfigurationParameter = "FinancialCalendar_With_BeginningBalance"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
#endif

    					
	Sets
	
		ByEndDateForYear
			no duplicates
			Sort Order
				FinanceEnterpriseGroup
				GeneralLedgerCloseConfiguration
				GeneralLedgerCloseYear
				GeneralLedgerClosePeriod	
		ByEndDate
			no duplicates
			Sort Order
				FinanceEnterpriseGroup
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod	
		ByYearPeriodDescending
			no duplicates
			Sort Order
				FinanceEnterpriseGroup
				GeneralLedgerCloseConfiguration
				GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod descending
		
		ByGeneralLedgerCalendarPeriod
			Sort Order
				FinanceEnterpriseGroup
				GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
				GeneralLedgerCloseConfiguration				
							
	Relations

		DatesWithinConfigurationRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDate
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
		
		PriorPeriodsRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
			Instance Selection
				where (related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod < GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod)				

		FollowingPeriodsRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
			Instance Selection
				where (related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod > GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod)				
				
		PriorPeriodsInThisYear
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
				related.GeneralLedgerCloseYear          = GeneralLedgerCloseYear
			Instance Selection
				where (related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod < GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod)

		FollowingPeriodsInThisYear
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
				related.GeneralLedgerCloseYear          = GeneralLedgerCloseYear
			Instance Selection
				where (related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod > GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod)
								
		PeriodsInPriorYear
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
				related.GeneralLedgerCloseYear          < GeneralLedgerCloseYear

		PeriodsInFollowingYears
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
				related.GeneralLedgerCloseYear          > GeneralLedgerCloseYear
		
		PeriodsInThisYear
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDateForYear
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
				related.GeneralLedgerCloseYear          = GeneralLedgerCloseYear	
				

		GeneralLedgerCloseYearRel
			one-to-many relation to GeneralLedgerCloseYear
			Field Mapping uses symbolic key 	
				related.FinanceEnterpriseGroup		    = FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
				
		GeneralLedgerClosePeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key 	
				related.FinanceEnterpriseGroup		    = FinanceEnterpriseGroup
				related.GeneralLedgerCloseConfiguration = GeneralLedgerCloseConfiguration
			Instance Selection
				where(related.GeneralLedgerCloseYear   		= LocalGLCYear)
	
		GeneralLedgerCalendarPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses symbolic key 	
				related.FinanceEnterpriseGroup		    = FinanceEnterpriseGroup
				related.GeneralLedgerCalendarPeriod     = LocalGLCPeriod
		
#ifdef module integration					
		
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "FinancialCalendar"
            	related.FSMBODConfiguration.Direction 	= 1
            
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker
		
		EntityLedgerPeriodRel
			one-to-one relation to EntityLedgerPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		    						= FinanceEnterpriseGroup
				related.AccountingEntity 										= LocalAccountingEntity
				related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod    = LocalGLCPeriod
				related.Ledger 													= FinanceEnterpriseGroup.CoreLedger
						
#endif

		EntityLedgerPeriodDeleteRel
    		one-to-many relation to EntityLedgerPeriod
    		Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup   = FinanceEnterpriseGroup
        		related.AccountingEntity         = LocalAccountingEntity
        		related.GeneralLedgerClosePeriod = GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
        		
        AccountingEntityRel
    		one-to-many relation to AccountingEntity
    		Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
    		Instance Selection
        		where (related.CloseConfiguration = GeneralLedgerCloseConfiguration)

		AccountingEntityPeriodRel
			one-to-one relation to AccountingEntityPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.AccountingEntity					= LocalAccountingEntity
				related.GeneralLedgerClosePeriod			= GeneralLedgerClosePeriod

	Field Rules
	
		PeriodName
			default to DerivedDefaultPeriodName
			default to GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.DisplayPeriod
		
		PeriodNumber
			PeriodNumber = DerivedNthPeriodOfYear
				

	Rule Blocks
		
		ResetFollowingPeriodBeginDate
			LocalEndDateNumeric = GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod
			
			if (FollowingPeriodsInThisYear exists)		
				invoke Update first FollowingPeriodsInThisYear
					invoked.BeginDate = (LocalEndDateNumeric + 1)
					invoked.TransientBeginDateUpdate = LocalBeginDateUpdate
				invoke Update FollowingPeriodsInThisYear 
					invoked.TransientBeginDateUpdate = LocalBeginDateUpdate
			else
			if (PeriodsInFollowingYears exists)
				invoke Update first PeriodsInFollowingYears
					invoked.BeginDate = (LocalEndDateNumeric + 1)	
					invoked.TransientBeginDateUpdate = LocalBeginDateUpdate					
	
		TriggerFinancialCalendarRules
			trigger "GeneralLedgerClosePeriodService" PA service
				resume on error
				title is "EG:<FinanceEnterpriseGroup>CC:<GeneralLedgerCloseConfiguration>YR:<GeneralLedgerCloseYear>PD:<GeneralLedgerClosePeriod>"
				Criteria
					FinanceEnterpriseGroup
					GeneralLedgerCloseConfiguration
					GeneralLedgerCloseYear
				Variables
					LocalActionCode
						variable name is ActionCode
					DerivedBeginDate
						variable name is BeginDate
					GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.DisplayPeriod
						variable name is DisplayPeriod
					include persistent fields from FinanceEnterpriseGroup
					include persistent fields from FinanceEnterpriseGroup.EnterpriseChart
					include persistent fields from GeneralLedgerCloseConfiguration
					include persistent fields from GeneralLedgerClosePeriod
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
					LocalAccountingEntity
						variable name is LocalAccountingEntity
					
					
					

	Actions
		Create is an Action
			restricted
			Action Rules
			
				BeginDate = DerivedBeginDate
				
				LocalEndDateNumeric = GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod 
			
			Exit Rules	
				include ResetFollowingPeriodBeginDate	
							
		Update is an Action
			Action Rules
				if (TransientBeginDateUpdate)
					BeginDate = GeneralLedgerClosePeriod.DerivedBeginDate
					LocalEndDateNumeric = GeneralLedgerClosePeriod.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod		

		Delete is an Action	 		
			Entrance Rules
				LocalBeginDateUpdate = true
				if (FinanceEnterpriseGroup.BODTrigger)
					LocalActionCode = LocalActionCode.Delete
					increment bod id.VariationID
					include TriggerFinancialCalendarRules




	 		Action Rules

				LocalEndDateNumeric = BeginDate
				LocalEndDateNumeric -= 1	
				
				include ResetFollowingPeriodBeginDate	 		
	 			
	 		Exit Rules
	 			for each AccountingEntityRel
    				LocalAccountingEntity = each.AccountingEntity
					if (AccountingEntityPeriodRel exists)
						invoke Delete AccountingEntityPeriodRel
    				for each EntityLedgerPeriodDeleteRel
        				invoke DeleteAllLedgerPeriods each

		MoveToDifferentYear is an Instance Action
			
			Parameters
				PrmCloseYear			    is a GeneralLedgerCloseYear
					default label is "NewCloseYear"
				PrmAssignDefaultPeriodName	is Boolean
					default label is "AssignDefaultPeriodName"
			
			Local Fields
				LocalYearLow					is a Year
				LocalYearHigh					is a Year
			
			Parameter Rules
				PrmCloseYear
					required
					LocalYearLow  = PrmCloseYear - 2
					LocalYearHigh = PrmCloseYear + 2

					constraint (GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date year >= LocalYearLow
					and         GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date year <= LocalYearHigh)

						"InvalidCloseYearForDate<GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date>"
											
			Action Rules
				GeneralLedgerCloseYear = PrmCloseYear
				PeriodNumber = DerivedNthPeriodOfYear				
				if (PrmAssignDefaultPeriodName)
					PeriodName = DerivedDefaultPeriodName
				









		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation          is Alpha size 22
					default label is "Location"
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
					
		TriggerFinancialCalendarRules is an Instance Action
			restricted
			default label is untranslatable
			Entrance Rules
			Parameters
				PrmAccountingEntity is AlphaUpper 15
			Action Rules
				LocalAccountingEntity = PrmAccountingEntity
				if (FinanceEnterpriseGroup.BODTrigger)
					LocalActionCode = LocalActionCode.Update
					increment bod id.VariationID
					include TriggerFinancialCalendarRules




#ifdef module integration
		SendGeneralLedgerClosePeriodNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is FinancialCalendarBODXML
					bod type is "Sync.FinancialCalendar"
					document id is DerivedDocumentID
					variation id is DerivedBODVariationID


		TriggerFinancialCalendarBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "FinancialCalendar"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "GeneralLedgerClosePeriod"
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup 
					invoked.PrmMainUserTemplate			= "IONSyncFinancialCalendar_GeneralLedgerClosePeriod_Header_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "FinancialCalendar"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= GeneralLedgerCloseConfiguration
							invoked.Reference2				= GeneralLedgerClosePeriod
							invoked.Reference3				= "GeneralLedgerClosePeriod"
							invoked.Reference4				= GeneralLedgerCloseYear
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedBODVariationID
							invoked.BODID					= DerivedBODID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedAccountingEntity
							invoked.Reference1				= GeneralLedgerCloseConfiguration
							invoked.Reference2				= GeneralLedgerClosePeriod
							invoked.Reference3				= "GeneralLedgerClosePeriod"
							invoked.Reference4				= GeneralLedgerCloseYear
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendGeneralLedgerClosePeriodNativeLPLBOD
						resume on error
							Error            							= true
							ErrorMessage     							= error message
							
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODID
							invoked.BODXML								= FinancialCalendarBODXML
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODID
							invoked.BODXML									= FinancialCalendarBODXML
#endif

	Action Exit Rules
		invoke UpdateRebuildInfo GeneralLedgerCloseConfiguration
			invoked.PrmYearCreated = GeneralLedgerCloseYear
					
		if (FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields" and action !="TriggerFinancialCalendarRules" ) 
					LocalActionCode = LocalActionCode.Update
					if (action type.Create)
						LocalActionCode = LocalActionCode.Create
					else
						if (action type.Delete)
							LocalActionCode = LocalActionCode.Delete
		
					increment bod id.VariationID
					include TriggerFinancialCalendarRules



		
		
