PositivePayOutputFileHeader is a BusinessClass
    owned by cb
    prefix is PPOFH

    Ontology
        symbolic key is PositivePayOutputFileHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
    	FileName					is AlphaUpper 50
    	OutputFile					is a PaymentAttachment
    		default label is "OutputFile"
    	CSVFile						is a PaymentAttachment
    	InProcess					is Boolean
		CreateDate					is TimeStamp
		

        CashCode
        CcgCashCodeGrp          is a CashCodeGroup
        BankInstCode            is a BankTransactionCode
        ReportOption            is AlphaUpper size 1
            States
                Detail  value is "D"
                Summary value is "S"
        DateFormat              is Numeric size 1
            States
                Yyyymmdd value is 1
                    default label is "YYYYMMDD"
                Mmddyyyy value is 2
                    default label is "MMDDYYYY"
        IncludeDecimal          is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        CreateCsv               is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        ReportOnOpen            is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        ReportOnVoid            is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        ReportOnStop            is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        ReportOnStale           is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        ReportOnEscheat         is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
		ReportOnPendingStopPay	is AlphaUpper size 1		
			States
				Yes value is "Y"
				No  value is "N"
		ProcessedByThirdParty	is Boolean
		ReceiverBank			is Numeric 2				
			States
				BankOfAmerica			value is 1
				WellsFargo				value is 2
				JPMC					value is 3
				USBank					value is 4
		ReceiverEmailAddress	is an EmailAddress
			holds pii
                
	Transient Fields
    
    Local Fields
		    
    Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 10	
			(instance count of PositivePayOutputFileDetail set)

    Conditions
    	CashCodeEntered
			restricted
			when (CashCode entered)

		CashCodeGroupEntered
			restricted
			when (CcgCashCodeGrp entered)
    
		HasAttachment
			restricted
			when (OutputFile entered)

		HasCSVAttachment
			restricted
			when (CSVFile entered)

		SecurityGroupAllowsAccess
			restricted
			when ((CashCode !entered
			and   CcgCashCodeGrp !entered)
			or   (CashCode entered
			and   CcgCashCodeGrp !entered
			and   CashCode.SecurityGroupAllowsAccess)
			or   (CashCode !entered
			and   CcgCashCodeGrp entered
			and   CcgCashCodeGrp.SecurityGroupAllowsAccess))
		ReceiverBankEntered
			restricted
			when (ReceiverBank entered)
		
		ReceiverEmailAddressEntered
			restricted
			when (ReceiverEmailAddress entered)

    Relations

    Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			CashManagementGroup

    	ByProcessedByThirdParty
    		Sort Order
    			ProcessedByThirdParty	descending
    			CashManagementGroup
    			PositivePayOutputFileHeader

    Field Rules
        CreateDate
            default to current timestamp 

		FileName
			default to "PositivePayOutputFile"            

		CreateCsv
			initial value is "Y"
			force default to "Y"

	Actions
		Create is a Create Action
			restricted
			Action Rules
				InProcess = true
				
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
			restricted

		FinishProcessing is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				trigger "PositivePayOutputFile" PA service
					resume on error
					title is "CreatePositivePayOutputFile"
					Variables
						CashManagementGroup
						PositivePayOutputFileHeader
				InProcess = false

		CreateOutputFiles is an Instance Action
			Action Rules
				trigger "PositivePayOutputFile" PA service
					title is "CreatePositivePayOutputFile"
					Variables
						CashManagementGroup
						PositivePayOutputFileHeader

		ResetProcessedByThirdParty is an Instance Action								
			valid when (ProcessedByThirdParty)											
			Action Rules																
				ProcessedByThirdParty = false											

		PurgePositivePayOutputFileHeaderRecords is a Set Action
			run in foreground
			completion message is "PurgeOfPositivePayOutputFileHeaderRecordsHasCompleted"
			Parameters
				PrmCashManagementGroup					is a CashManagementGroup
					default label is "CashManagementGroup"
				PrmCreateDateRange							is a DateRange
					default label is "CreateDateRange"
				
			Parameter Rules
				PrmCashManagementGroup
					required
				PrmCreateDateRange
					required
			Instance Selection
				where (CreateDate date within PrmCreateDateRange
				and    CashManagementGroup = PrmCashManagementGroup)
	
			Action Rules
				Instance Rules
					invoke Purge
						
