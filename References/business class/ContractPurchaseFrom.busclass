ContractPurchaseFrom is a BusinessClass
	owned by po
	prefix is CTPF
	
	Ontology
		symbolic key is ContractPurchaseFrom
		
	Patterns
	
	Persistent Fields
		SupplierVendor 		is a Vendor
		PurchaseFrom		is a VendorLocation
		AllItemTypes 		is Numeric size 1
    		States
    			SpecialsOnly 		value is 0
    			AllItemTypes 		value is 1
	
	Local Fields
		LocalContractGroup is like ContractGroup
		LocalContract      is like Contract

	Transient Fields
		AutoCreate           is Boolean

	Conditions
		LocationCompanyOnly
			when (ContractPurchaseFrom.ParticipantLocation.Location = blank
			and	  ContractPurchaseFrom.ParticipantLocation.RequestingLocation = blank)
		HasLocationOrRequestingLocation
			when (ContractPurchaseFrom.ParticipantLocation.Location != blank
			or    ContractPurchaseFrom.ParticipantLocation.RequestingLocation != blank)
		PayablesVendorLocationActive
			when (PurchaseFrom.Status.Active)
			
	Sets
		ByLocationRequestingLocation
			duplicates
     		indexed
      		Sort Order
           		ContractGroup
            	SupplierVendor
            	ContractPurchaseFrom.ParticipantLocation.Company
            	ContractPurchaseFrom.ParticipantLocation.Location
            	ContractPurchaseFrom.ParticipantLocation.RequestingLocation
            Instance Selection
            	where (HasLocationOrRequestingLocation)
            	
    	ByPurchaseFrom
    		Sort Order
    			ContractGroup
            	Contract
            	ContractPurchaseFrom.ParticipantLocation.Company
            	ContractPurchaseFrom.ParticipantLocation.Location
            	ContractPurchaseFrom.ParticipantLocation.RequestingLocation
            	
        BySupplier
        	Sort Order
           		ContractGroup
            	SupplierVendor
            	ContractPurchaseFrom.ParticipantLocation.Company
            	ContractPurchaseFrom.ParticipantLocation.Location
            	ContractPurchaseFrom.ParticipantLocation.RequestingLocation
            	Contract
    		
	Relations
		PurchasingCompanyRel
    		one-to-one relation to PurchasingCompany
            Field Mapping uses symbolic key
          		related.Company	  = ContractPurchaseFrom.ParticipantLocation.Company
          		
  	  	CompanyOnlyRel
    		one-to-one relation to SupplierPurchaseFrom
            Field Mapping uses symbolic key
          		related.SupplierGroup				 									= ContractGroup
           		related.Supplier					  									= Contract.Supplier
           		related.SupplierPurchaseFrom.ParticipantLocation.Company  				= ContractPurchaseFrom.ParticipantLocation.Company
           		related.SupplierPurchaseFrom.ParticipantLocation.Location 				= blank
           		related.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation		= blank
           		related.SupplierPurchaseFrom.ParticipantLocation.PricingGroup           = blank
           		
     	LocationRequestingLocationRel
    		one-to-many relation to SupplierPurchaseFrom
            Field Mapping uses ByLocationRequestingLocation
          		related.SupplierGroup				  						= ContractGroup
           		related.Supplier					  						= Contract.Supplier
           		related.SupplierPurchaseFrom.ParticipantLocation.Company 	= ContractPurchaseFrom.ParticipantLocation.Company
           		
     	LocalContractRel
     		one-to-one relation to Contract
     		Field Mapping uses symbolic key
     			related.ContractGroup	= LocalContractGroup
     			related.Contract        = LocalContract
     	
     	SupplierPurchaseFromRel
     		one-to-many relation to SupplierPurchaseFrom
     		Field Mapping uses symbolic key
     			related.SupplierGroup 												= ContractGroup
     			related.Supplier													= Contract.Supplier
     			related.SupplierPurchaseFrom.ParticipantLocation.Company			= ContractPurchaseFrom.ParticipantLocation.Company
            	related.SupplierPurchaseFrom.ParticipantLocation.Location			= ContractPurchaseFrom.ParticipantLocation.Location
            	related.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation	= ContractPurchaseFrom.ParticipantLocation.RequestingLocation
           		related.SupplierPurchaseFrom.ParticipantLocation.PricingGroup       = blank
            Instance Selection
            	where (related.PayablesVendorLocationActive)
            	
	Field Rules
		SupplierVendor
			initial value is Contract.Supplier.Vendor
			SupplierVendor = Contract.Supplier.Vendor
		PurchaseFrom
			required
		AllItemTypes
			initial value is SupplierPurchaseFromRel.AllItemTypes					
			
	Actions
		Create is a Create Action
			valid when (Contract.CanCreatePurchaseFroms)
			Action Rules
				if (ContractPurchaseFrom.ParticipantLocation.Company entered)
					constraint(PurchasingCompanyRel exists)
						"POCompanyDoesNotExist"
				if (!AutoCreate)
					if (LocationCompanyOnly)
	    				if (LocationRequestingLocationRel exists)
	    					confirmation required
	    						"ContractPurchaseFromsExistByLocationOrRequestingLocation;DoYouWantToContinue?"
					if (HasLocationOrRequestingLocation)
						if (CompanyOnlyRel exists)
							confirmation required
								"ContractPurchaseFromRecordExistsForWholeCompany;DoYouWantToContinue?"
			Exit Rules
				if (SupplierPurchaseFromRel exists)
					PurchaseFrom = SupplierPurchaseFromRel.PurchaseFrom
					
		Update is an Update Action
			valid when (Contract.CanCreatePurchaseFroms)
		
		Delete is a Delete Action
			valid when (Contract.CanCreatePurchaseFroms)
			
		CreateFromMembersOrParticipants is a Set Action   
			restricted
			default label is "CreatePurchaseFromForMembersOrParticipants"
			Parameters
				PrmContractGroup is a ContractGroup
					default label is "ContractGroup"
				PrmContract		 is a Contract
					default label is "Contract"
			
			Parameter Rules
				PrmContractGroup
					initial value is ContractGroup
					
				PrmContract
					initial value is Contract
			
			Action Rules
				Instance Rules
					LocalContractGroup = PrmContractGroup
					LocalContract      = PrmContract
					invoke CreatePurchaseFromsForParticipants LocalContractRel		
			
			
					
				
				
		
	

	
