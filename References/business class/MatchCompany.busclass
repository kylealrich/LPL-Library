MatchCompany is a BusinessClass  
    owned by ma
    prefix is MAY
    classic name is MACOMPANY

    Ontology
        symbolic key is MatchCompany
            classic set name is MAYSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Name
        MatchTable









        PostingAccountNoReceiptLine          is Numeric size 1
            classic name is MTCH-EXCESS-NR
            States
                MatchedNoReceipt value is 1
                MatchWriteOff    value is 2
        PostingAccountQuantityExceedsReceipt is Numeric size 1
            sql name is PAccountQuantityExceedsReceipt
            classic name is MTCH-EXC-REC
            States
                MatchedNoReceipt value is 1
                MatchWriteOff    value is 2
        DiscountRecognition                  is AlphaUpper size 1
            classic name is DISC-TAKEN
            States
                Payment value is "P"
                Match   value is "M"
        CurrencyExchangeSetPoint             is AlphaUpper size 1
            classic name is CURR-SET-FLAG
            States
                PurchaseOrderEntry value is "E"
                ReceiptDate        value is "R"
                InvoiceDate        value is "I"


        HoldUntilInspected                   is AlphaUpper size 1		
            classic name is HOLD-INSP-FLAG
            States
                Yes value is "Y"
                No  value is "N"
        WriteOffAmount                       is an InternationalAmount
            classic name is WRITE-OFF-AMT
        VendorClaimType
            classic name is VEN-CLAIM-TYPE
        VendorClaimHoldCode                  is a PayablesHoldCode
            classic name is CLAIM-HOLD-CD
        AutomaticChargebackHoldCode          is a PayablesHoldCode
            classic name is CB-HOLD-CODE
        ReplaceGoods                         is Boolean
        ShipOrHold
        ERSLocationControl                   is Boolean
            classic name is ERS-LOC-CNTRL
        UpdateERSCost                        is Boolean
            classic name is ERS-COST-UPD
        EDIAutoRelease                       is Boolean
            classic name is EDI-AUTO-REL
        PoolOption                           is Numeric size 1
            States
                CompanyVendor                            value is 1
                CompanyVendorPurchaseOrder               value is 2
                CompanyVendorPurchaseOrderShipToLocation value is 3
        ReconciliationUnderTolerance         is AlphaUpper size 1
            classic name is UNDER-TOL-FLAG
            States
                Yes    value is "Y"
                Ignore value is "I"
                No     value is "N"
        ReconciliationOverTolerance          is AlphaUpper size 1
            classic name is OVER-TOL-FLAG
            States
                Yes    value is "Y"
                Ignore value is "I"
                No     value is "N"
        ItemDiffFlag                         is AlphaUpper size 1
        ValidatePurchaseOrder                is Boolean
            classic name is VALIDATE-PO
        PurchaseOrderRequired                is Boolean
            classic name is REQUIRE-PO
        EnablePOCostMessaging                is Boolean
            classic name is PO-COST-MSG-FL
        POCostMessageMatchRule               is a MatchRule
            classic name is PO-MATCH-RULE
        POCostMessageRuleOrder               is a PoOrder
            classic name is PO-ORDER
        POCostMessagingWorkflow              is a PoCostMsgFl
            classic name is PO-COST-WF
        ReconciliationRuleGroup              is a MatchRule
            classic name is MATCH-RULE
        ReconciliationRuleOrder              is an Order
            classic name is ORDER
        ReconMessagingWorkflow               is a PoCostMsgFl
            classic name is RECON-MSG-WF
        NoReceiptNotifyDays                  is Numeric size 3
            classic name is NO-RCPT-DAYS
        NoReceiptNotifyOption                is AlphaUpper size 1
            classic name is NO-RCPT-OPTION
            States
                None           value is blank
                Online         value is "1"
                Batch          value is "2"
                OnlineAndBatch value is "3"
        UnmatchedInvoiceNotifyDays           is Numeric size 3
            classic name is DAYS-TO-WORK
        UnmatchedInvoiceNotifyOption         is AlphaUpper size 1
            classic name is DAYS-TO-WRK-OP
            States
                None           value is blank
                Online         value is "1"
                Batch          value is "2"
                OnlineAndBatch value is "3"
        InvalInvOpt                          is AlphaUpper size 1
        WorkflowBasisDate                    is AlphaUpper size 1
            classic name is WF-CALC-DATE
            States
                DueDate      value is "1"
                DiscountDate value is "2"






        EnableDropshipMessaging              is a PoCostMsgFl
            classic name is DRPSHP-MSG-FL
        DropshipMessageMatchRule             is a MatchRule
            classic name is DRPSHP-RULE
        DropshipMessageRuleOrder             is a PoOrder
            classic name is DRPSHP-ORDER
        DropshipMessagingWorkflow            is a PoCostMsgFl
            classic name is DRPSHP-WF
        ServiceMessagingOption               is AlphaUpper size 1
            classic name is SRVC-MSG-FL
            States
                ExtendedAmountOnly         value is "Y"
                UnderQuantityAndCost       value is "U"
                OverQuantityAndCost        value is "O"
                OverOrUnderQuantityAndCost value is "B"
                NoMessage                  value is "N"
        ServiceMessageMatchRule              is a MatchRule
            classic name is SRVC-RULE
        ServiceMessageRuleOrder              is a PoOrder
            classic name is SRVC-ORDER
        ServiceMessagingWorkflow             is a PoCostMsgFl
            classic name is SRVC-WF
        AllowZeroCostInvoice				 is Boolean
        	classic name is OPEN-ZERO-COST
        PrpSubAcct                           is Numeric size 4
        UbillSubAcct
        AocdSubAcct
        IraSubAcct
        LastMatchObjectID    				 is Numeric 12
			disable Auditing
        SummarizeDistribution                is AlphaUpper size 1	
            default label is "SummarizeInventoryDistributions"
            classic name is SUMM-APDIST-FL
            States
                DoNotSummarize   value is "N"
                Summarize		 value is blank
        SummarizeAllDistributions            is Boolean 
            default label is "CreateSummarizedDistributionRecordsForMatchAndServiceContractInvoices"
        ReceiptAccrualAccount                is a FinanceCodeBlock
            classic name for ReceiptAccrualAccount.AccountingUnit is ACC-ACCT-UNIT
            classic name for ReceiptAccrualAccount.GeneralLedgerChartAccount is ACC-ACCOUNT
        CostVarianceAccount                  is a FinanceCodeBlock
            classic name for CostVarianceAccount.AccountingUnit is VAR-ACCT-UNIT
            classic name for CostVarianceAccount.GeneralLedgerChartAccount is VAR-ACCOUNT
        ToleranceOffsetAccount               is a FinanceCodeBlock
            classic name for ToleranceOffsetAccount.AccountingUnit is TOL-ACCT-UNIT
            classic name for ToleranceOffsetAccount.GeneralLedgerChartAccount is TOL-ACCOUNT
        DiscountAccrualAccount               is a FinanceCodeBlock
            classic name for DiscountAccrualAccount.AccountingUnit is DACC-ACCT-UNIT
            classic name for DiscountAccrualAccount.GeneralLedgerChartAccount is DACC-ACCOUNT
        ReceiptWriteOffAccount               is a FinanceCodeBlock
            classic name for ReceiptWriteOffAccount.AccountingUnit is REC-ACCT-UNIT
            classic name for ReceiptWriteOffAccount.GeneralLedgerChartAccount is REC-ACCOUNT
        MatchedNotReceivedAccount            is a FinanceCodeBlock
            classic name for MatchedNotReceivedAccount.AccountingUnit is MNR-ACCT-UNIT
            classic name for MatchedNotReceivedAccount.GeneralLedgerChartAccount is MNR-ACCOUNT
        MatchWriteOffAccount                 is a FinanceCodeBlock
            classic name for MatchWriteOffAccount.AccountingUnit is MWO-ACCT-UNIT
            classic name for MatchWriteOffAccount.GeneralLedgerChartAccount is MWO-ACCOUNT
        PrepayAccount                        is a FinanceCodeBlock
            classic name for PrepayAccount.AccountingUnit is PRP-ACCT-UNIT
            classic name for PrepayAccount.GeneralLedgerChartAccount is PRP-ACCOUNT
        AddOnChargeDifferenceAccount         is a FinanceCodeBlock
            classic name for AddOnChargeDifferenceAccount.AccountingUnit is AOCD-ACCT-UNIT
            classic name for AddOnChargeDifferenceAccount.GeneralLedgerChartAccount is AOCD-ACCOUNT
        UnderbillAccount                     is a FinanceCodeBlock
            classic name for UnderbillAccount.AccountingUnit is UBILL-ACCT-UNT
            classic name for UnderbillAccount.GeneralLedgerChartAccount is UBILL-ACCOUNT
        InventoryReceiptArchivalAccount      is a FinanceCodeBlock
            classic name for InventoryReceiptArchivalAccount.AccountingUnit is IRA-ACCT-UNIT
            classic name for InventoryReceiptArchivalAccount.GeneralLedgerChartAccount is IRA-ACCOUNT
        InvoiceBalanceRoundingToleranceAccount      is a FinanceCodeBlock
        InvoiceBalanceRoundingToleranceAmount	    is an InternationalAmount
        InvoiceBalanceRoundingTolerancePercent      is Percent size 7.5
        InvoiceBalanceRoundingToleranceCondition    is AlphaUpper size 3
            States
                And value is "AND"
                Or  value is "OR"
        PurchaseOrderInvoiceCancel                is Numeric 1
            States
                DoNotAllowOnPaidInvoices   value is 1
		EnableCreditForRebill					  is Boolean
		EnableServiceCostMessaging				  is Boolean
		PerformBudgetEditsForApproval			  is Boolean             
		MatchMemoDelayHardEdits					  is Boolean
		BypassBudgetEditIfNoDifferences		      is Boolean		
        CreateReconMessageForLines                is Boolean    


































	Context Fields
		FinanceEnterpriseGroup

	Transient Fields
		TriggerEdits is Boolean
		
	Local Fields
		LocalHoldCode is a PayablesHoldCode
		LocalInventoryLocation is like InventoryLocation

	Conditions
		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or    CompanySecurityGroupMemberRel exists)




		OneSourceGeneric
			restricted
			when (TaxEntityRel.ThirdParty.Sabrix
			and   TaxConfigurationRel.OneSourceVersionType.Generic)
				
		MatchOneCallPerInvoice
			when (MatchOneCallPerInvoiceRel exists
			and   MatchOneCallPerInvoiceRel.Value = "YES")

	Relations
		SecurityGroupRel
			one-to-one relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 		  = FinanceEnterpriseGroup
				related.DimensionGroupType	   		  = 2
				related.FinanceDimensionSecurityGroup = ActorSecurityGroup
				
		SecurityGroupDetailRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses Set1
				related.Company						= Company
				related.GeneralLedgerCompanyGroup  	= SecurityGroupRel.GeneralLedgerCompanyGroup
					
		CompanySecurityGroupMemberRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company                     = Company

        CBHoldCodeRel
            one-to-one relation to PayablesHoldCode
            Field Mapping uses symbolic key
                related.VendorGroup = Company.VendorGroup
                related.HoldCode = AutomaticChargebackHoldCode
				
        VendorClaimHoldCodeRel
            one-to-one relation to PayablesHoldCode
            Field Mapping uses symbolic key
                related.VendorGroup = Company.VendorGroup
                related.HoldCode = VendorClaimHoldCode

		APDiscountAccrualsRel
			one-to-many relation to PayablesInvoiceDiscount
			Field Mapping uses symbolic key
				related.Company		=	Company
			Instance Selection
				where (related.Status = 1)
				
		MAMatchTableRel
			one-to-many relation to MatchTable
			Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.MatchTable				= MatchTable
			Instance Selection
				where (related.MatchTableLevel = "C"
				or     related.MatchTableLevel = "B")
				
		PayablesProcessLevelsRel
			one-to-many relation to PayablesProcessLevel
			Field Mapping uses symbolic key
				related.Company = Company

        PurchasingCompanyRel
            one-to-one relation to PurchasingCompany
            Field Mapping uses symbolic key
                related.Company = Company
                
        PayablesCompanyRel
            one-to-one relation to PayablesCompany
            Field Mapping uses symbolic key
                related.Company = Company
                
        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company = Company
                
		MatchReconQueueRel
			one-to-many relation to MatchReconQueue
			Field Mapping uses symbolic key
				related.Company = Company

		InventoriedItemLocationRel
			one-to-many relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company				= Company
				related.InventoryLocation	= LocalInventoryLocation
			Instance Selection
				where (related.InventoryTracked)

		DefaultProcessLevelRel
			one-to-many relation to PayablesProcessLevel
			Field Mapping uses symbolic key
				related.Company 			= Company
			Instance Selection
				where (related.DefaultProcessLevel)

		EnableServiceCostMessagingRel
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= "EnableServiceQuantityCostMessage"

	   	TaxEntityRel
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.TaxEntity 				= Company.AccountingEntity

		TaxConfigurationRel						
			one-to-one relation to TaxConfiguration
			Field Mapping uses symbolic key
				related.TaxConfiguration		= Company.FinanceEnterpriseGroup

		MatchOneCallPerInvoiceRel
			one-to-one relation to ConfigurationParameter
			Field Mapping uses symbolic key
				related.ConfigurationParameter.ConfigurationID	= "config"
				related.ConfigurationParameter.Name				= "MatchOneCallPerInvoice"


	Derived Fields
		ActorSecurityGroup is a DerivedField
			type is like CompanySecurityGroup
			return actor.context.CompanySecurityGroup
				
    Sets







	Field Rules
	
		PostingAccountNoReceiptLine
			required
					
		PostingAccountQuantityExceedsReceipt
			required
			
		AutomaticChargebackHoldCode
			LocalHoldCode = AutomaticChargebackHoldCode
			constraint (CBHoldCodeRel.HoldOnInvoice > " ")
				"HoldCodeMustBeDefinedAsAnInvoiceHoldCode" 
			
		VendorClaimHoldCode
			LocalHoldCode = VendorClaimHoldCode
			constraint (VendorClaimType = "CB")
				"CannotEnterVendorClaimsHoldCodeIfNotDoingChargebacks" 
			constraint (VendorClaimHoldCodeRel.HoldOnInvoice > " ")
				"HoldCodeMustBeDefinedAsAnInvoiceHoldCode" 

		DiscountRecognition
            default to "P"
			required

			if (DiscountAccrualAccount.ToAccountingEntity not entered)		
				initialize DiscountAccrualAccount.Ledger					

			if (DiscountRecognition = "P")
				constraint (!DiscountAccrualAccount entered)
					"DiscountAccrualAccountMustBeBlankIfDiscountRecognitionIsAtPaymentClose" 
			else	//Disc-Taken = "M"
				constraint (DiscountAccrualAccount entered)
					"DiscountAccrualAccountIsRequiredIfDiscountRecognitionIsAtMatch" 
					
			if (DiscountRecognition changed)
				constraint (!APDiscountAccrualsRel exists)
					"CannotChangeDiscountRecognition;UnpostedDiscountExists" 
					
		MatchTable
			required
			constraint (MAMatchTableRel exists)
				"MatchTableTypeIsNeitherCompanyNorBothCompanyAndVendor" 
				
		EnablePOCostMessaging
			if (EnablePOCostMessaging)
				constraint (POCostMessageMatchRule entered)
					"MustEnterAValidRuleGroup" 
				constraint (ValidatePurchaseOrder)
					"ValidatePOMustBeTrue"  
				
		ServiceMessagingOption
            default to "N" 
			if (ServiceMessagingOption != "N")


				constraint (ValidatePurchaseOrder)
					"ValidatePOMustBeTrue"  

		ServiceMessageMatchRule
			if (!ServiceMessagingOption.NoMessage
            and ServiceMessagingOption entered)
				required

		EnableServiceCostMessaging
			if (EnableServiceCostMessaging)
				constraint (!ServiceMessagingOption.NoMessage)
					"ServiceMessagingOptionRequiredIfServiceCostMessagingEnabled"

		ReconciliationUnderTolerance
            default to "N"



			if  (ReconciliationUnderTolerance = "I")
				constraint (ReconciliationOverTolerance = "Y")
					"OverAndUnderToleranceCombinationIsNotValid"  
				
		ReconciliationOverTolerance
            default to "N"



			if  (ReconciliationOverTolerance = "I")
			    constraint (ReconciliationUnderTolerance = "Y")
					"OverAndUnderToleranceCombinationIsNotValid"  

		ReconciliationRuleGroup
			if (!ReconciliationUnderTolerance.No
			or  !ReconciliationOverTolerance.No)
				required				

		NoReceiptNotifyDays
			constraint (NoReceiptNotifyOption entered)
				"MustSelectNoReceiptProcessingOption"  
				
		UnmatchedInvoiceNotifyDays
			constraint (UnmatchedInvoiceNotifyOption entered)
				"MustSelectDaysToWorkProcessingOption"  
				
		NoReceiptNotifyOption
			constraint (WorkflowBasisDate entered)
				"MustSelectWorkflowCalculationDateOption"  
				
		UnmatchedInvoiceNotifyOption
			constraint (WorkflowBasisDate entered)
				"MustSelectWorkflowCalculationDateOption"  
				
        PoolOption
            required

        VendorClaimType
            required

        CurrencyExchangeSetPoint
            default to "E"

        HoldUntilInspected
            default to "N"

        POCostMessagingWorkflow
            default to "N"

        ReconMessagingWorkflow
            default to "N"

        ShipOrHold
            default to "S"

        ServiceMessagingWorkflow
            default to "N"

        ValidatePurchaseOrder
            initial value is true

        ReceiptAccrualAccount   
        	required
        		"PurchaseOrderReceiptAccrualIsRequired"						

        CostVarianceAccount
        	required
        		"ItemCostVarianceIsRequired"			            

        ToleranceOffsetAccount
        	required 
        		"InvoiceToleranceAccountIsRequired"	             
        		
        ReceiptWriteOffAccount
        	required              
        		"ReceiptWriteOffAccountIsRequired"
        
        MatchedNotReceivedAccount
        	required          
        		"MatchNotReceivedAccountIsRequired"	
        		
        MatchWriteOffAccount
        	required
       			"MatchWriteOffAccountIsRequired" 		                
       
        PrepayAccount
        	required
        		"MatchPrepayAccountIsRequired"		                       
        
        AddOnChargeDifferenceAccount
        	required
        		"AddOnChargeDifferenceAccountIsRequired"

        UnderbillAccount
        	if (InventoryReceiptArchivalAccount not entered
        	or  ToleranceOffsetAccount not entered)
	        	required
    	    		"UnderbillAccountIsRequiredIfEitherInventoryReceiptArchivalOrToleranceOffsetAreNotEntered"
        		
        InventoryReceiptArchivalAccount
        	required
        		"InventoryReceiptArchivalAccountIsRequired"


        POCostMessageRuleOrder
	        if (POCostMessageMatchRule entered)
        		required
        			"RuleOrderRequiredIfPOCostMessageRuleEntered"

        ReconciliationRuleOrder
    	    if (ReconciliationRuleGroup entered)
        		required
        			"RuleOrderRequiredIfReconciliationRuleEntered"

        DropshipMessageRuleOrder
        	if (DropshipMessageMatchRule entered)
        		required
        			"RuleOrderRequiredIfDropshipMessageRuleEntered"

        ServiceMessageRuleOrder
        	if (ServiceMessageMatchRule entered)
        		required
        			"RuleOrderRequiredIfServiceMessageRuleEntered"

        InvoiceBalanceRoundingToleranceAccount
			if (InvoiceBalanceRoundingToleranceAmount entered)
				required
					"InvoiceBalanceRoundingToleranceAccountRequiredIfAmountEntered"

		EnableCreditForRebill
			if (EnableCreditForRebill)
			    constraint (!SummarizeDistribution.Summarize)
			        "SummarizeInventoryDistributionsMustBeSetToDoNotSummarizeToEnableCreditForRebill"


    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action

 		GetNextMatchObjectID is an Instance Action
 			restricted
 			refresh and lock this instance
			Action Rules
           		LastMatchObjectID += 1

		SummarizeDistributionsForMatchAndServiceInvoices is an Instance Action 
            valid when (SummarizeAllDistributions)
            Parameters 
                ParmPayablesCompany         is a PayablesCompany
                    default label is "Company" 
				ParmInvoiceDate             is Date 
                    default label is "SummarizeForAllReleasedMatchOrServiceInvoicesOnOrAfterThisDate"

            Parameter Rules 
                ParmPayablesCompany     
                    initial value is Company 
                    default to Company 
                ParmInvoiceDate 
                    required   

            Action Rules 
                
                invoke CreateDistributionSummarySet MatchPurchaseOrderInvoice 
                    invoked.ParmPayablesCompany     = ParmPayablesCompany
                    invoked.ParmInvoiceDate         = ParmInvoiceDate

                invoke CreateDistributionSummarySet MatchServiceContractInvoice 
                    invoked.ParmPayablesCompany     = ParmPayablesCompany
                    invoked.ParmInvoiceDate         = ParmInvoiceDate                    

        TurnOffOneCall is an Instance Action
			restricted
		    Action Rules
		        invoke Delete MatchOneCallPerInvoiceRel
		
		TurnOnOneCall is an Instance Action
			restricted
		    Action Rules
		        invoke Create MatchOneCallPerInvoiceRel
		            invoked.Value = "YES"
	           	
		EnableBudgetBypassWhenNoDifferencesForAllCompanies is a Set Action	
			default label is untranslatable
            Parameters  
                PrmVendorGroup is a VendorGroup
                    default label is "VendorGroup" 
			Action Rules
				Instance Rules
                    if (PrmVendorGroup not entered
                    or  PrmVendorGroup = PayablesCompanyRel.VendorGroup)
                        BypassBudgetEditIfNoDifferences = true 

		DisableBudgetBypassWhenNoDifferencesForAllCompanies is a Set Action	
			default label is untranslatable
            Parameters  
                PrmVendorGroup is a VendorGroup 
                    default label is "VendorGroup" 
			Action Rules
				Instance Rules
                    if (PrmVendorGroup not entered
                    or  PrmVendorGroup = PayablesCompanyRel.VendorGroup)
                        BypassBudgetEditIfNoDifferences = false  
