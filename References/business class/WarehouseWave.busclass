WarehouseWave is a BusinessClass
    owned by wh
    prefix is WHPLW

    Ontology
        symbolic key is WarehouseWave

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Description					  
        Active     
        Requester
        RequestingCompanyAndLocation           
        RequestingReportGroup               
            default label is "RequestingLocationGroup"
        FromPriority                          is a WavePriority
        ToPriority                            is a WavePriority            
        InventoryClass                          
        SpecialHandlingCode                   
        MajorPPEClass                         
        MinorPPEClass                          
        PersonalProtectiveEquipment           is Boolean
        CriticalItem                          is Boolean
        Sterile                               is Boolean
        Reprocessed                           is Boolean
        Pharmaceutical                        is Boolean
        SingleUse                             is Boolean
        Dressing                              is Boolean
        Reusable                              is Boolean
        Analytics                             is Boolean
        Drugs                                 is Boolean
        Implantable                           is Boolean
        Instrument                            is Boolean
        Narcotic                              is Boolean
        ContainsHumanTissue                   is Boolean
    
    Field Rules
        Description
            required

        RequestingCompanyAndLocation
            if (RequestingCompanyAndLocation.RequestingCompany entered 
            or RequestingCompanyAndLocation.RequestingLocation entered)
                constraint (RequestingCompanyAndLocation.RequestingCompany  entered 
                and RequestingCompanyAndLocation.RequestingLocation   entered)
                    "MustEnterBoth_Requesting_CompanyAnd_Requesting_Location"
            if (Company not = RequestingCompanyAndLocation.RequestingCompany)
				constraint (InterEntityRelationRel exists)
					"InterEntityRelationNotDefinedBetween_Company<Company>And_Company<RequestingCompanyAndLocation.RequestingCompany>"
                constraint (Company.ItemGroup = RequestingCompanyAndLocation.RequestingCompany.ItemGroup)
					"BothCompaniesItemGroupsMustMatch"

        RequestingReportGroup
            if (RequestingCompanyAndLocation entered)
                cannot be entered
                    "CannotEnterBoth_Requesting_LocationAnd_Requesting_Location_Group"

        FromPriority
            if (ToPriority entered)
                required
                    "MustEnterBothEndsOfThe_Priority_Range"
                    
            if (FromPriority entered and ToPriority entered)
                constraint(FromPriority <= ToPriority)
                    "From_PriorityCannotBeGreaterThan_To_Priority"
            
        ToPriority 
            if (FromPriority entered) 
                required
                    "MustEnterBothEndsOfThe_Priority_Range"  

        SpecialHandlingCode
            if (SpecialHandlingCode entered)
                constraint(SpecialHandlingCode.Active)
                    "Special_Handling_CodeMustBe_Active"
         
    Conditions
        RecordExists
			restricted
			when (WarehouseWave exists) 
            
    Relations
        MajorClassRel
            one-to-many relation to MajorClass
            Field Mapping uses symbolic key 
                related.ItemGroup                     = Company.ItemGroup
                related.ClassType                     = ClassType.InventoryClass
            Instance Selection
                where(related.Active = true)

        MinorClassRel
            one-to-many relation to MinorClass
            Field Mapping uses symbolic key 
                related.ItemGroup                     = Company.ItemGroup
                related.ClassType                     = ClassType.InventoryClass
                related.MajorClass                    = InventoryClass.MajorInventoryClass 
            Instance Selection
                where(related.Active = true)

        MajorPPEClassRel
            one-to-many relation to MajorClass
            Field Mapping uses symbolic key 
                related.ItemGroup                      = Company.ItemGroup
                related.ClassType                      = ClassType.PersonalProtectiveEquipment
            Instance Selection
                where(related.Active = true)
        
        MinorPPEClassRel
            one-to-many relation to MinorClass
            Field Mapping uses symbolic key 
                related.ItemGroup                       = Company.ItemGroup
                related.ClassType                       = ClassType.PersonalProtectiveEquipment
                related.MajorClass                      = MajorPPEClass
            Instance Selection
                where(related.Active = true)
        
        SpecialHandlingCodeClassRel
            one-to-many relation to SpecialHandlingCode
            Field Mapping uses symbolic key 
                related.ItemGroup                       = Company.ItemGroup
            Instance Selection
                where(related.Active = true)
        GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company			                = Company
        InterEntityRelationRel
			one-to-one relation to GLInterEntityRelation
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		    = GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.AccountingEntity			    = Company.AccountingEntity
				related.ToAccountingEntity			    = RequestingCompanyAndLocation.RequestingCompany.AccountingEntity
