ProcurementCardStatementTransactionDistribution is a BusinessClass
    owned by po
    prefix is PDS
    sql name is PCSTransactionDistribution
    classic name is PDCHRGDIST

    Ontology
        symbolic key is ProcurementCardStatementTransactionDistribution
            classic set name is PDSSET1
            sql name is PCSTransactionDistribution
            classic name is SEQ-NBR
            classic name for ProcurementCardProgram is PCARD-PROGRAM
            classic name for ProcurementCardStatement is STATEMENT
            classic name for ProcurementCardStatementTransaction.TransactionNumber is TXN-NBR

    Patterns
        implements ContextualParent
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        ProcurementChargeDistributionType
            sql name is PChargeDistributionType
            classic name is DIST-TYPE
        DistributionAmount                is an InternationalAmount
            classic name is DIST-AMOUNT
        TaxCode
        	protected
        	restricted
        AssetFlag
        AssetDescription                  is a Description
            classic name is ASSET-DESC
        TagNumber
            classic name is TAG-NBR
        Item
            classic name is ITEM-NBR
        ItemDescription
            classic name is ITEM-DESC
        ItemQuantity
        AssetTemplate
        InServiceDate                     is Date
            classic name is INSRV-DATE
        PurchaseDate                      is Date
        ModelNumber
        SerialNumber
        HoldAsset                         is Boolean
            classic name is HOLD-AM
        Asset
        AssetGroup
        Combine
        BarCode
        DistributionAccount               is a TransactionCodeBlock
            classic name for DistributionAccount.ToAccountingEntity is DIST-COMPANY
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for DistributionAccount.Project is ACTIVITY
		CreatedByFES					is Boolean		
			protected
		FESManuallyUpdated				is Boolean
			protected
	Transient Fields
		TransientAccountingEntity           is an AccountingEntity  
			derive value from ProcurementCardStatementTransaction.Company.AccountingEntity

	Derived Fields
		EntityAccountingUnit		is a DerivedField
			type is Alpha up to 40
			restricted

			if (DistributionAccount.AccountingUnit entered)
				return DistributionAccount.ToAccountingEntity + "-" + DistributionAccount.AccountingUnit
			else
				return DistributionAccount.ToAccountingEntity
			
						
		DisplayDistributionAccount	is a StringField
			type is Alpha size up to 88
			restricted
			DistributionAccount.Ledger
			"-"
			EntityAccountingUnit	
			"-"
			DistributionAccount.GeneralLedgerChartAccount
			
		DerivedGoodsDistributionValue is a DerivedField
			type is like InternationalAmount
			restricted
			return (sum GoodsDistributionsForTransactionRel.DistributionAmount)
			
		DerivedTaxDistributionValue is a DerivedField
			type is like InternationalAmount
			restricted
			return (sum TaxDistributionsForTransactionRel.DistributionAmount)
		
		DerivedCardProgramNameDescription is a MessageField
			default label is "ProgramName"
			"<ProcurementCardProgram>_-_<ProcurementCardProgram.Description>"

		DerivedCardTransaction is a MessageField
			default label is "Statement"
			"<ProcurementCardStatement>_-_<ProcurementCardStatementTransaction.TransactionNumber>"
		
		DerivedCardName is a MessageField
			default label is "CardNumber"
			"<ProcurementCardStatementTransaction.ProcurementCardNumber.DisplayProcurementCardNumber>_-<ProcurementCardStatementTransaction.CardUserName>"

		DerivedMerchantName is a MessageField
			default label is "MerchantName"
			"<ProcurementCardStatementTransaction.Merchant>_-_<ProcurementCardStatementTransaction.Merchant.Name>"


    Local Fields
        LocalGeneralLedgerSystemCode 				is a GeneralLedgerSystemCode
		LocalPostingDate							is a PostingDate
		LocalTransactionDate						is an ExchangeDate
        LocalFEGtext								is like FinanceEnterpriseGroup
		LocalDerivedFESKey 							is a FrontEndSplitKey

    Conditions
    	DistributionExist
    		restricted
    		when (ProcurementCardStatementTransactionDistribution exist)
    		
    	HasNoPurchaseOrder
    		restricted
    		when (ProcurementCardStatementTransaction.PurchaseOrder !entered)
    	
    	CanProcess
    		restricted
    		when (not (ProcurementCardStatementTransaction.Status.Closed or ProcurementCardStatementTransaction.Status.Cancelled)
    		and   HasNoPurchaseOrder)
    			
	Relations
	
		GeneralLedgerSystemCodeRel 
			one-to-many relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ProcurementGroup.FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode	= "PO"
	
		GoodsDistributionsForTransactionRel
			one-to-many relation to ProcurementCardStatementTransactionDistribution
			Field Mapping uses symbolic key
				related.ProcurementGroup					= ProcurementGroup
				related.ProcurementCardProgram      		= ProcurementCardProgram
				related.ProcurementCardStatement    		= ProcurementCardStatement
				related.ProcurementCardStatementTransaction = ProcurementCardStatementTransaction
			Instance Selection
				where (related.ProcurementChargeDistributionType.Goods)
				
		TaxDistributionsForTransactionRel
			one-to-many relation to ProcurementCardStatementTransactionDistribution
			Field Mapping uses symbolic key
				related.ProcurementGroup					= ProcurementGroup
				related.ProcurementCardProgram      		= ProcurementCardProgram
				related.ProcurementCardStatement    		= ProcurementCardStatement
				related.ProcurementCardStatementTransaction = ProcurementCardStatementTransaction
			Instance Selection
				where (related.ProcurementChargeDistributionType.Tax)


		FESDistributionsByKeyLocalRel
			one-to-many relation to FrontEndSplitDistributions
			Field Mapping uses ByFundingGroup
				related.FinanceEnterpriseGroup						= LocalFEGtext
			    related.FrontEndSplitDistributions.FundingKey 		= LocalDerivedFESKey

		FESDistByDistributionRel
			one-to-many relation to FrontEndSplitDistributions
			Field Mapping uses ByFundingGroup
				related.FinanceEnterpriseGroup						= LocalFEGtext
			    related.FrontEndSplitDistributions.FundingKey 		= LocalDerivedFESKey
			    related.FrontEndSplitDistributions.Distribution		= ProcurementCardStatementTransactionDistribution	
	
	Rule Blocks
		ValidateProjectDates
			if (GeneralLedgerSystemCodeRel exists)
				LocalGeneralLedgerSystemCode = GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode
			if (DistributionAccount.Project entered
			and ProcurementGroup.FinanceEnterpriseGroup.ProjectDateEdit entered)
				if (ProcurementGroup.FinanceEnterpriseGroup.ProjectDateEdit.TransactionDate)
					LocalTransactionDate		= ProcurementCardStatementTransaction.TransactionDate
				else
					if (ProcurementCardStatementTransaction.PostDate entered)
						LocalPostingDate  		= ProcurementCardStatementTransaction.PostDate
					else
						LocalPostingDate  		= current corporate date
	
	Field Rules		
		TaxCode
			default to ProcurementCardStatementTransaction.TaxCode
		
		DistributionAmount
			required
	
		DistributionAccount 
			required

		ProcurementChargeDistributionType
			default to ProcurementChargeDistributionType.Goods
			initial value is ProcurementChargeDistributionType.Goods
			required

		CreatedByFES
			if(CreatedByFES
			and	action type.Update
			and	(DistributionAccount.Ledger changed
			or	DistributionAccount.ToAccountingEntity changed
			or	DistributionAccount.AccountingUnit changed
			or	DistributionAccount.GeneralLedgerChartAccount changed
			or	DistributionAccount.Project changed
			or	DistributionAccount.FinanceDimension1 changed
			or	DistributionAccount.FinanceDimension2 changed
			or	DistributionAccount.FinanceDimension3 changed
			or	DistributionAccount.FinanceDimension4 changed
			or	DistributionAccount.FinanceDimension5 changed
			or	DistributionAccount.FinanceDimension6 changed
			or	DistributionAccount.FinanceDimension7 changed
			or	DistributionAccount.FinanceDimension8 changed
			or	DistributionAccount.FinanceDimension9 changed
			or	DistributionAccount.FinanceDimension10 changed
        	or	DistributionAmount changed))
				confirmation required
					"OverrideFrontEndSplit?"
				FESManuallyUpdated = true
		
		AssetTemplate
			if (Asset entered)
				cannot be entered
					"Enter_AssetOr_Asset_Template,NotBoth"

		ItemQuantity
			if (AssetTemplate entered)
				required
					"MustEnterQuantityIf_Asset_TemplateIsEntered"

	Actions
		Create is a Create Action
			valid when (CanProcess)
			Action Rules
				include ValidateProjectDates
				
				constraint (ProcurementCardStatementTransaction.PurchaseOrder not entered)
					"DistributionsForAPurchaseOrderChargeCannotBeCreated"	
					
			Exit Rules
			
				if (ProcurementChargeDistributionType.Goods)
					if (DerivedGoodsDistributionValue != (ProcurementCardStatementTransaction.ItemChargeLessTax))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = false)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = true
					if (DerivedGoodsDistributionValue = (ProcurementCardStatementTransaction.ItemChargeLessTax))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = true)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = false
								
				if (ProcurementChargeDistributionType.Tax)
					if (DerivedTaxDistributionValue != (ProcurementCardStatementTransaction.TaxAmount))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = false)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = true
					if (DerivedTaxDistributionValue = (ProcurementCardStatementTransaction.TaxAmount))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = true)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = false
					
		CreateFromInterface is a Create Action
			restricted
			Action Rules
				include ValidateProjectDates
				
				constraint (ProcurementCardStatementTransaction.PurchaseOrder not entered)
					"DistributionsForAPurchaseOrderChargeCannotBeCreated"	
		
		Update is an Update Action
			valid when (CanProcess)
			Action Rules
					
				include ValidateProjectDates
					


					
				constraint (ProcurementCardStatementTransaction.PurchaseOrder not entered)
					"DistributionsForAPurchaseOrderChargeCannotBeModified"											//"@e.PD22.114"

			Exit Rules		
				if (DistributionAmount changed)

								
					if (ProcurementChargeDistributionType.Goods)
						if (DerivedGoodsDistributionValue != ProcurementCardStatementTransaction.ItemChargeLessTax)
							if (ProcurementCardStatementTransaction.DistributionOutOfBalance = false)
								invoke FastUpdate ProcurementCardStatementTransaction
									invoked.DistributionOutOfBalance = true
						if (DerivedGoodsDistributionValue = ProcurementCardStatementTransaction.ItemChargeLessTax)
							if (ProcurementCardStatementTransaction.DistributionOutOfBalance = true)
								invoke FastUpdate ProcurementCardStatementTransaction
									invoked.DistributionOutOfBalance = false
									
					if (ProcurementChargeDistributionType.Tax)
						if (DerivedTaxDistributionValue != ProcurementCardStatementTransaction.TaxAmount)
							if (ProcurementCardStatementTransaction.DistributionOutOfBalance = false)
								invoke FastUpdate ProcurementCardStatementTransaction
									invoked.DistributionOutOfBalance = true
						if (DerivedTaxDistributionValue = ProcurementCardStatementTransaction.TaxAmount)
							if (ProcurementCardStatementTransaction.DistributionOutOfBalance = true)
								invoke FastUpdate ProcurementCardStatementTransaction
									invoked.DistributionOutOfBalance = false
								
		UpdateAmountFromStatementTransaction  is an Instance Action
			restricted
			Parameters
				PrmDistributionAmount   is an InternationalAmount
			Action Rules
				DistributionAmount       =    PrmDistributionAmount
					
		Delete is a Delete Action
			valid when (CanProcess)
			
			Entrance Rules
			
				if (ProcurementChargeDistributionType.Goods)
					if ((DerivedGoodsDistributionValue - DistributionAmount) != (ProcurementCardStatementTransaction.ItemChargeLessTax))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = false)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = true
					if ((DerivedGoodsDistributionValue - DistributionAmount) = (ProcurementCardStatementTransaction.ItemChargeLessTax))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = true)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = false
								
				if (ProcurementChargeDistributionType.Tax)
					if ((DerivedTaxDistributionValue - DistributionAmount) != (ProcurementCardStatementTransaction.TaxAmount))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = false)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = true
					if ((DerivedTaxDistributionValue - DistributionAmount) = (ProcurementCardStatementTransaction.TaxAmount))
						if (ProcurementCardStatementTransaction.DistributionOutOfBalance = true)
							invoke FastUpdate ProcurementCardStatementTransaction
								invoked.DistributionOutOfBalance = false
			
		CreateInvoiceDistribution is an Instance Action
			restricted
			Parameters
				PrmCompany								is like Company
				PrmVendor								is like Vendor
				PrmPayablesInvoice						is like PayablesInvoice
				PrmDistributionType						is like PayablesDistributionType
				PrmDistributionAccount					is like TransactionCodeBlock

			Action Rules
				invoke Create PayablesInvoiceDistribution 
					fill in user fields from this instance
					invoked.Company                                       				   = PrmCompany
					invoked.Vendor                                        				   = PrmVendor
					invoked.PayablesInvoice                               				   = PrmPayablesInvoice
					invoked.DistributionType                           					   = PrmDistributionType
					invoked.DistributionDate											   = ProcurementCardStatementTransaction.PostDate
					invoked.GLTPostingDate											   	   = ProcurementCardStatementTransaction.PostDate

					invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount  	   = DistributionAmount
					invoked.DistributionAmount.CurrencyAmount              				   = DistributionAmount
					invoked.DistributionAmount.ExchangeDate								   = InServiceDate
					invoked.ProcurementCardStatement                     				   = ProcurementCardStatement
					invoked.TransactionNumber                             				   = ProcurementCardStatementTransaction.TransactionNumber	
					if (PrmDistributionAccount entered)
						invoked.DistributionAccount										   = PrmDistributionAccount
					else
						invoked.DistributionAccount										   = DistributionAccount
					invoked.AssetFlag                                                      = AssetFlag
					invoked.TransientAssetGroup										       = AssetGroup
					invoked.TransientAsset												   = Asset
					invoked.TransientAssetTemplate										   = AssetTemplate
					invoked.TransientItemQuantity									   	   = ItemQuantity
					invoked.TransientAssetDescription									   = AssetDescription
					invoked.TransientHoldAsset										       = HoldAsset
					invoked.TransientCombine											   = Combine
					invoked.TaxCode														   = TaxCode
					invoked.CreatedByFES												   = CreatedByFES
					invoked.FESManuallyUpdated											   = FESManuallyUpdated

		SplitFESDistributions is a Set Action
			default label is "FrontEndSplitDistributions"
			restricted
			Parameters
				PrmProcurementGroup				is a ProcurementGroup
					default label is "ProcurementGroup"
				PrmProcurementCardProgram		is a ProcurementCardProgram
					context of PrmProcurementGroup
					default label is "ProcurementCardProgram"
				PrmProcurementCardStatement		is a ProcurementCardStatement
					context of PrmProcurementGroup
					default label is "ProcurementCardStatement"
				PrmCompany						is a PurchasingCompany
					default label is "PurchasingCompany"
				PrmTransactionNumber			is a ProcurementCardTransaction
					default label is "ProcurementCardTransaction"
			Parameter Rules
				PrmProcurementGroup
					required
				PrmProcurementCardProgram
					required
				PrmProcurementCardStatement
					required
				PrmCompany
					required
				PrmTransactionNumber
					required
			Local Fields
				LocalForEachSumFESAmount					is an InternationalAmount
				LocalCommitCounter							is Numeric size 3
			Instance Selection
				where (ProcurementGroup = PrmProcurementGroup
				and ProcurementCardProgram = PrmProcurementCardProgram
				and ProcurementCardStatement = PrmProcurementCardStatement
				and ProcurementCardStatementTransaction.Company = PrmCompany
				and ProcurementCardStatementTransaction.TransactionNumber = PrmTransactionNumber
				and DistributionAmount != 0
				and DistributionAccount.Project entered
				and !DistributionAccount.Project.ExcludeFromFES
				and DistributionAccount.Project.first FESProjectShadowContractRel.SummaryProject.ProjectContract.FrontEndSplits
				and not CreatedByFES)
			Rule Blocks
				FillLocalFEGtextAndDerivedFESKey
					LocalFEGtext		= PrmCompany.FinanceEnterpriseGroup
       				LocalDerivedFESKey	= "PCS-"+PrmProcurementGroup+"-"+PrmProcurementCardProgram+"-"+PrmProcurementCardStatement+"-"+PrmCompany+"-"+PrmTransactionNumber					
			Action Rules
				Empty Set Rules

				Set Rules
					Entrance Rules
						include FillLocalFEGtextAndDerivedFESKey
						invoke Purge FESDistributionsByKeyLocalRel
						initialize LocalCommitCounter
					Exit Rules
						include FillLocalFEGtextAndDerivedFESKey
						invoke Purge FESDistributionsByKeyLocalRel
						if (LocalCommitCounter > 0)
                			commit transaction
				Instance Rules
					include FillLocalFEGtextAndDerivedFESKey
					invoke SplitDistribution FrontEndSplitDistributions in foreground
						invoked.PrmFinanceEnterpriseGroup 		= ProcurementGroup.FinanceEnterpriseGroup
						invoked.PrmAccountingEntity				= ProcurementCardStatementTransaction.Company.AccountingEntity
						invoked.PrmDistribution					= ProcurementCardStatementTransactionDistribution
						invoked.PrmDerivedFESKey				= LocalDerivedFESKey
						invoked.PrmDistributionAmount			= DistributionAmount
						invoked.PrmDistributionAmountWithTax	= DistributionAmount
						invoked.PrmTransactionDate				= PrmProcurementCardStatement.StatementDate
						if(PrmProcurementCardStatement.PostingDate entered)
							invoked.PrmPostingDate					= PrmProcurementCardStatement.PostingDate
						else
							invoked.PrmPostingDate					= PrmProcurementCardStatement.StatementDate
						invoked.PrmTransactionCurrencyCode		= ProcurementCardStatementTransaction.Company.AccountingEntity.FunctionalCurrency
						invoked.PrmDistributionBy				= DistributionBy.ByAmount
						invoked.PrmNumberOfDecimalsQuantity		= Item.NumberOfDecimalsQuantity
						invoked.PrmGeneralLedgerSystemCode		= first GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode
						invoked.PrmTransactionCodeBlock			= DistributionAccount
					if(FESDistByDistributionRel exists)
						initialize LocalForEachSumFESAmount
              			for each FESDistByDistributionRel
						  	LocalForEachSumFESAmount += each.FESAmount
              				invoke Create ProcurementCardStatementTransactionDistribution
								fill in fields from this instance
									except invoked.DistributionAccount
									except invoked.DistributionAmount
									except invoked.CreatedByFES
									except invoked.FESManuallyUpdated
								invoked.DistributionAccount			= each.CodeBlock
								if (last FESDistByDistributionRel.FrontEndSplitDistributions = each.FrontEndSplitDistributions)
									invoked.DistributionAmount		= each.FESAmount + (DistributionAmount - LocalForEachSumFESAmount)
								else
									invoked.DistributionAmount		= each.FESAmount
								invoked.CreatedByFES					= true
							increment LocalCommitCounter
						if (LocalCommitCounter > 50)
							commit transaction
							initialize LocalCommitCounter
						invoke Delete 

		DeleteFrontEndSplitDistributions is a Set Action
			restricted
			Parameters
				PrmProcurementGroup				is a ProcurementGroup
					default label is "ProcurementGroup"
				PrmProcurementCardProgram		is a ProcurementCardProgram
					context of PrmProcurementGroup
					default label is "ProcurementCardProgram"
				PrmProcurementCardStatement		is a ProcurementCardStatement
					context of PrmProcurementGroup
					default label is "ProcurementCardStatement"
				PrmCompany						is a PurchasingCompany
					default label is "PurchasingCompany"
				PrmTransactionNumber			is a ProcurementCardTransaction
					default label is "ProcurementCardTransaction"
			Parameter Rules
				PrmProcurementGroup
					required
				PrmProcurementCardProgram
					required
				PrmProcurementCardStatement
					required
				PrmCompany
					required
				PrmTransactionNumber
					required
			Instance Selection
				where (ProcurementGroup = PrmProcurementGroup
				and ProcurementCardProgram = PrmProcurementCardProgram
				and ProcurementCardStatement = PrmProcurementCardStatement
				and ProcurementCardStatementTransaction.Company = PrmCompany
				and ProcurementCardStatementTransaction.TransactionNumber = PrmTransactionNumber
				and CreatedByFES)
			Action Rules
				Empty Set Rules

				Instance Rules
					invoke Delete
