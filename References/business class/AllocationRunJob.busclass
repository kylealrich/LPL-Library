AllocationRunJob is a BusinessClass
    owned by Allocations
    prefix is ALRJ

    Ontology
    	symbolic key is AllocationRunJob

    Persistent Fields
        JobType                 is an AllocationRunJobType
            default label is "JobName"
        Allocation
        AllocationLine
        AllocationStep
        AllocationWeightGenerator
        Currency
        BackgroundGroup			is AlphaUpper up to 200
        AsyncActionRequest
            delete ignored
        Status                  is Numeric size 1
            States  
                NotStarted      value is 0
                InProgress      value is 5
                Finished        value is 8

        StartTime               is TimeStamp
        EndTime                 is TimeStamp

    Transient Fields
        TransientStatus         is Numeric size 2 
            default label is "Status"
            derive value from DerivedStatus
    		States

                NotStarted           value is 0
    			Queued               value is 1
    			PendingStart         value is 2
    			LostWhilePending     value is 3
    			FailedToStart        value is 4
    			InProgress           value is 5
    			LostWhileInProgress  value is 6
    			TerminatedAbnormally value is 7 
    			Finished             value is 8
    			InRetry 			 value is 9    				
    			WaitForActionGroup	value is 10
    			WaitForSynchronizedGroup value is 11

    Sets
        ByAllocationLine
            indexed
            Instance Selection
                where (IsLineType)
            Sort Order
                FinanceEnterpriseGroup
                AllocationSourceSystem
                AllocationControl
                Allocation
                AllocationLine
                AllocationRun
                JobType

        ByWeightGenerator
            indexed
            Instance Selection
                where (IsBuilderType)
            Sort Order
                FinanceEnterpriseGroup
                AllocationSourceSystem
                AllocationControl
                AllocationWeightGenerator
                AllocationRun
                JobType

        ByAllocationRunCurrency
            indexed
            Instance Selection
                where (IsInterfaceChangeAsRequestType)
            Sort Order
                FinanceEnterpriseGroup
                AllocationRun
                Currency
                JobType        

    Relations
        AsyncActionTriggerRel
            one-to-many relation to AsyncActionTrigger
            Field Mapping uses AsyncActionTriggerByRequestTriggerID
                related.AsyncActionRequest     = AsyncActionRequest
        
        AllocationRel
            one-to-one relation to Allocation
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
                related.AllocationSourceSystem      = AllocationSourceSystem
                related.AllocationControl           = AllocationControl
                related.Allocation                  = Allocation

        AllocationLineRel
            one-to-one relation to AllocationLine
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
                related.AllocationSourceSystem      = AllocationSourceSystem
                related.AllocationControl           = AllocationControl
                related.Allocation                  = Allocation
                related.AllocationLine              = AllocationLine

        AllocationWeightGeneratorRel
            one-to-one relation to AllocationWeightGenerator
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
                related.AllocationSourceSystem      = AllocationSourceSystem
                related.AllocationControl           = AllocationControl
                related.AllocationWeightGenerator   = AllocationWeightGenerator
        
        AllocationLineWithBuilderRel
            one-to-many relation to AllocationLine
            Field Mapping uses ByStepNumber
                related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
                related.AllocationSourceSystem      = AllocationSourceSystem
                related.AllocationControl           = AllocationControl
                related.Active						= true	
            Instance Selection
                where ((related.Allocation = AllocationRun.Allocation 
                or     (AllocationRun.AllocationGroup entered and related.Allocation.AllocationGroup = AllocationRun.AllocationGroup) 
                or     (AllocationRun.Allocation not entered and AllocationRun.AllocationGroup not entered))
                and    (related.ToAllocationWeightGenerator = AllocationWeightGenerator or related.FromAllocationWeightGenerator = AllocationWeightGenerator))

    Conditions
        IsLineType
            restricted
            when (JobType.RunLine or JobType.InterestAllocationOverrideGeneration)

        HasError
            restricted
            when (TransientStatus.LostWhileInProgress
            or    TransientStatus.FailedToStart
            or    TransientStatus.TerminatedAbnormally)

        HasAsyncTrigger
            restricted
            when (AsyncActionTriggerRel exists)
            
        IsBuilderType
            restricted
            when (JobType.WeightBuilder)

        IsInterfaceChangeAsRequestType
            restricted
            when (JobType.InterfaceAsChangeRequest)
    Derived Fields
        DerivedStatus is a DerivedField
            type is Numeric size 2
            if (not Status.Finished
            and AsyncActionTriggerRel exists)
                return AsyncActionTriggerRel.Status 
            else 
                return Status
                
        HoverAsyncErrorMessage is a MessageField
            "<first AsyncActionTriggerRel.ErrorMessage>"

    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted


        Start is an Instance Action
            restricted
            Action Rules
                initialize EndTime
                StartTime = current timestamp
                Status = Status.InProgress
                

        End is an Instance Action
            restricted
            Action Rules
                EndTime = current timestamp
                Status = Status.Finished

        UpdateStatusToFinished is an Instance Action
            restricted
            Action Rules
                Status = Status.Finished
                
