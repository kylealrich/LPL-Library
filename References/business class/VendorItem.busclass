VendorItem is a BusinessClass
	owned by po
	prefix is PIV
	classic name is POITEMVEN

	Ontology
		symbolic key is VendorItem
			classic set name is PIVSET1
			classic name is VEN-ITEM
			classic name for ProcurementGroup is PROCURE-GROUP

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		VendorItemDescription		is a Description
			classic name is VEN-ITEM-DESC
			default label is "Description"
		VendorBuyUOM				is a UnitOfMeasureEdit
			classic name for VendorBuyUOM.UnitOfMeasure is VBUY-UOM
		VendorPriceUOM				is a UnitOfMeasureEdit
			classic name for VendorPriceUOM.UnitOfMeasure is VPRI-UOM
		ManufacturerNumber
			classic name is MANUF-NBR
		VendorItemStripped			is like VendorItem
		ManufacturerNumberStripped	is a ManufacturerNumber
		Manufacturer
			classic name for Manufacturer.ManufacturerCode is MANUF-CODE
			classic name for Manufacturer.ManufacturerDivision is MANUF-DIVISION
		LabelerIdentificationCode
			classic name is LIC-CODE
		LastLeadtime				is a LeadtimeDays
			protected
		MinimumOrderQuantity		is a Quantity
			classic name is MIN-ORD-QTY
		WarrantyPeriod				is Numeric size 6
			classic name is WARRANTY-PER
		Active						is Boolean
			classic name is STATUS
		UseAsDefault				is Boolean
			default label is "Use_asDefault"
			classic name is DEFAULT-FLG
		FromWorksheetResult			is an ItemInformationWorksheetResult
			delete ignored

	Local Fields
		LocalManufacturer					is like Manufacturer
		LocalManufacturerNumber				is like ManufacturerNumber
		LocalUnitOfMeasure					is a UnitOfMeasure
		LocalPriceUnitOfMeasure             is a UnitOfMeasure
		LocalReplacementVendorItem			is a VendorItem
		LocalContract						is like Contract
		LocalReplacementManufacturer		is like Manufacturer
		LocalReplacementManufacturerNumber	is like ManufacturerNumber
		LocalFromReplaceAndInactivate		is Boolean
		LocalUseAsDefault					is Boolean
		LocalFromItemInactivation           is Boolean
		LocalVendor                         is like Vendor
		LocalVendorItem                     is like VendorItem
		LocalFromItemActivation             is Boolean
		LocalManufacturerInformationChanged is Boolean
		LocalItem                           is like Item
		LocalUOMChanged                     is Boolean
		LocalUsingItemDescription           is Boolean

	Derived Fields
		RepresentativeText is a StringField
			type is Text
			default label is "VendorItem"
			VendorItem " - " VendorItemDescription

		NumberOfActiveVendorItemsForAnItem is a ComputeField
			type is Numeric 6
			restricted
			(instance count of MultipleVendorItemRel)

		ItemCodeUsed is a ConditionalField
			type is Numeric size 1
			restricted
			if (HasItem)
				1
			else
				blank

		DerivedVendorFromSupplier is a DerivedField
			type is Numeric 9
			restricted
			return (SupplierRel.Vendor)

		VendorItemDisplay is a MessageField
			default label is "VendorItem"
			"<VendorItemDescription>_(<VendorItem>)"

		DerivedTotalPOInvLineFactQuantity is a DerivedField
			type is Decimal size 13.2
			return (sum PoInvLineFactsRel.ReportPOLineQuantity)

		DerivedTotalPOInvLineFactPOCost is a DerivedField
			type is Decimal size 21.2
			return (sum PoInvLineFactsRel.ReportDerivedExtendedAmountFromPo)

		DerivedTotalPOInvLineFactInvoiceQuantity is a DerivedField
			type is Decimal size 13.2
			return (sum PoInvLineFactsRel.MatchedQty)

		DerivedTotalPOInvLineFactInvoiceAmountCost is a DerivedField
			type is Decimal size 21.2
			return (sum PoInvLineFactsRel.ReportDerivedExtendedInvoiceAmt)

		DerivedStrippedManufacturerNumber is a DerivedField
			type is like ManufacturerNumber
			return com.lawson.apps.procurement.base.StringUtils.stripNonAlphaNumeric(ManufacturerNumber)

		DerivedStrippedVendorItem is a DerivedField
			type is Alpha size 32
			return com.lawson.apps.procurement.base.StringUtils.stripNonAlphaNumeric(VendorItem)

		DerivedItemBaseCost is a DerivedField
			type is Decimal size 21.8
			restricted
			return Item.ItemContractLinesRel.BaseCost

		VendorPackagingXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			if(DerivedItemBaseCost > 0)
				return VendorItemHDpropertiesXMLBOD
			else
			if(DerivedItemBaseCost = 0)
				return VendorItemLNpropertiesXMLBOD

		VendorItemLNpropertiesXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			VendorItemLNpropertiesXMLBOD = template.IONSyncItemMaster_VendorItem_VendorItemLNproperties_ST document for this instance

		VendorItemHDpropertiesXMLBOD is a DerivedField
			type is XMLDocument
			restricted
			VendorItemHDpropertiesXMLBOD = template.IONSyncItemMaster_VendorItem_VendorItemHDproperties_ST document for this instance

		DerivedItemStockWeight is a DerivedField
			type is Decimal size 9.3
			restricted
			return Item.StockWeight

		DerivedStockUOM is a DerivedField
			type is Alpha 4
			restricted
			return Item.StockUOM

		DerivedSupplierAE is a DerivedField
			type is Alpha 20
			restricted
			return ""

		DerivedUOMPerqunatity is a DerivedField
			type is Alpha 13
			restricted
			return BODItemUOMRel.UOMConversion

	Field Groups
		TextSearchable
			VendorItemDescription
			ManufacturerNumber

	Context Fields
		PurchasingCompany
		PurchaseOrder
		PurchaseOrderLine
		SourcingEventResponse
		SourcingEventLineResponse

	Conditions
		VendorItemExists
			restricted
			when (VendorItem exists)

		GTINsExist
			restricted
			when (IcitemgtinRel exists)

		IsActive
			restricted
			when (Active)

		DescriptionMismatch
			restricted
			when (VendorItemDescription != Item.Description)

		ManufacturerMismatch
			restricted
			when (Manufacturer 			!= Item.Manufacturer
			or    ManufacturerNumber 	!= Item.ManufacturerNumber)

		EligibleForDefaultVendorItem
			restricted
			when (!UseAsDefault
			and   IsActive)

		ValidForSupplier
			restricted
			when (ProcurementGroup = actor.agent(SupplierSourceId).SupplierGroup
			and   Vendor           = actor.agent(SupplierSourceId).LocationVendor)

		HasSameManufacturerForDifferentItem
			when (SameManufacturerNumberDifferentItemRel exists)
		
		HasBuyUOMAndManufacturer
			classic name is HAS-MANUF-BUOM
			restricted
			when (VendorBuyUOM.UnitOfMeasure entered
			or    Manufacturer.ManufacturerCode entered
			or    ManufacturerNumber entered)

		HasManufacturerInfo
			classic name is HAS-MANUF-INFO
			restricted
			when (Manufacturer.ManufacturerCode entered
			or    ManufacturerNumber entered)

		FromWorksheet
			restricted
			when (FromWorksheetResult entered)

		HasItem
			classic name is ITEM-ENTERED
			restricted
			when (Item entered)

		NationalDrugCodeExists
			classic name is ITEMNDC-EXISTS
			restricted
			when (first NationalDrugCodeRel exists)

		HasManufacturerCode
			classic name is MANUF-CD-ENTRD
			restricted
			when (Manufacturer.ManufacturerCode entered)

		MultipleVendorItems
			restricted
			when (AnotherVendorItemRel exists)

		MultipleActiveVendorItems
			restricted
			when (AnotherActiveVendorItemRel exists)

		PurchaseOrderLineVendorItem
			restricted
	   		when (PurchaseOrderLine	in context)

	   	SourcingEventLineResponseVendorItem
	   		restricted
	   		when (SourcingEventLineResponse in context)

	   	MismatchWithContract
			when (ContractLinesMismatchSearchRel exists)

		ActiveContractLinesExist
			restricted
			when (ContractLinesForVendorItemAndVendorRel exists)

		ActiveGPOContractLinesExist
			restricted
			when (GPOContractLinesForVendorItemAndVendorRel exists)

		ContractInterfaceLinesExist
			restricted
			when (ContractImportLinesForVendorItemRel exists)

		ContractLinesForManufOrDistributorExist
			restricted
			when (ActiveContractLinesExistForManufOrDistRel exists)

		FromGPOContract
			restricted
			when (GPOContractImportLinesForVendorItemRel exists)

		ManufacturerNumberNotStripped
			restricted
			when (ManufacturerNumber != ManufacturerNumberStripped)

		VendorItemNotStripped
			restricted
			when (VendorItem != VendorItemStripped)

		SuggestedContractLineItemsExistForManufacturer
			restricted
			when (SuggestedContractLineItemsManufacturerNumberRel exists)

		SuggestedContractLineItemsExist
			restricted
			when (SuggestedOpenContractLineItemsRel exists)

		NoItemSyncUpdate
			restricted
			when (ItemGroupRel.DoNotUpdateInactiveVendorItem
			and   !Active)

		HasItemManufacturerMismatch 
			when (ItemManufacturerMismatchRel exists)

		VendorGTINInUse
			restricted
			when (PurchasingVendorRel exists
			and PurchasingVendorRel.GTINInUse)

		CanBackorderVendorItem
			restricted
			when (IsActive
			and   VendorBackorderDetailsRel not exists)

		ConsolidatedItemMasterExists
			restricted 
			when (ConsolidatedItemMasterRel exists) 			

	Relations
		PoInvLineFactsRel
			one-to-many relation to PoInvLineFact
			Field Mapping uses ByItemVendor
				related.ProcureGroup = ProcurementGroup
				related.Item = VendorItem.Item
				related.PoInvLineFact.Vendor = VendorItem.Vendor

		ItemManufacturerMismatchRel 
			one-to-many relation to Item
			Field Mapping uses symbolic key 
				related.ItemGroup		= ProcurementGroup
				related.Item            = Item
			Instance Selection
				where ((related.Manufacturer 		!= Manufacturer
				or      related.ManufacturerNumber 	!= ManufacturerNumber)
				and     Active 
				and     related.Manufacturer entered
				and     related.ManufacturerNumber entered) 

		PrmReplacementItemRel 
			one-to-one relation to Item
			Field Mapping uses symbolic key 
				related.ItemGroup		= ProcurementGroup
				related.Item            = LocalItem

		ItemGroupRel
			one-to-one relation to ItemGroup
			Field Mapping uses symbolic key
				related.ItemGroup    = ProcurementGroup

		IcitemgtinRel
			one-to-many relation to ItemGTIN
			valid when (HasManufacturerInfo)
			Field Mapping uses Set7
				related.ItemGroup 			= ProcurementGroup
                related.Manufacturer		= Manufacturer
                related.ManufacturerNumber	= ManufacturerNumber
                related.Item				= Item

		OtherVendorItemRel
	   		one-to-one relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup  	    = ProcurementGroup
				related.Item    			    = Item
				related.Vendor 				    = LocalVendor
				related.VendorItem			    = LocalVendorItem

		ReplacementVendorItemRel
	   		one-to-one relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup  	    = ProcurementGroup
				related.Item    			    = Item
				related.Vendor 				    = Vendor
				related.VendorItem			    = LocalReplacementVendorItem

		PrmVendorItemRel
	   		one-to-one relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup  	    = ProcurementGroup
				related.Item    			    = Item
				related.Vendor 				    = Vendor
				related.VendorItem			    = LocalVendorItem

		SupplierRel
			one-to-one relation to Supplier
			Field Mapping uses symbolic key
 				related.SupplierGroup 	= actor.agent(SupplierSourceId).SupplierGroup
 				related.Supplier   		= actor.agent(SupplierSourceId).Supplier

		VendorItemsForVendorAndManufacturerInfoChangeRel    
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup				= ProcurementGroup
				related.Vendor							= Vendor
				related.Manufacturer					= Manufacturer
				related.ManufacturerNumber				= ManufacturerNumber
			Instance Selection
				where (related.Active
				and    Active
				and    related.ManufacturerNumber entered
				and    related.VendorItem != VendorItem
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = VendorBuyUOM
				or     VendorBuyUOM !entered))

		VendorItemsForVendorAndVendorItemRel
			one-to-many relation to VendorItem
			Field Mapping uses Set3
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.VendorItem			= LocalReplacementVendorItem
			Instance Selection
				where (related.Active
				and    related.Item		!= Item)

		GPOContractImportLinesForVendorItemRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByItemVendorItem
				related.ContractGroup              = ProcurementGroup
				related.ItemNumber                 = Item
				related.VendorItem                 = VendorItem
			Instance Selection
				where (related.ContractImport.Vendor = Vendor
				and    related.GPOContractNumberEntered)

		ContractLineVenItemRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen     
				related.ContractGroup	= ProcurementGroup
				related.ItemNumber		= Item
				related.VendorItem		= VendorItem
			Instance Selection
				where (related.Contract.Vendor = Vendor)

		ContractLinesForVendorItemMismatchRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and   (related.Manufacturer 		!= Manufacturer
				or     related.ManufacturerNumber   != ManufacturerNumber))

		ContractLineManufacturerInfoDifferentVendorItemRel
			one-to-many relation to ContractLine
			Field Mapping uses BySpecialItemCreate
				related.ContractGroup                		= ProcurementGroup
				related.Manufacturer.ManufacturerCode 		= LocalReplacementManufacturer.ManufacturerCode
				related.Manufacturer.ManufacturerDivision	= LocalReplacementManufacturer.ManufacturerDivision
				related.ManufacturerNumber                  = LocalReplacementManufacturerNumber
			Instance Selection
				where (related.VendorItem                  != LocalReplacementVendorItem
				and    related.ManufacturerNumber entered)

		ContractLinesForVendorItemAndVendorRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and   !related.ContractLineState.Closed)

		ContractLinesForVendorItemAndVendorActiveStatusRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and    related.ContractLineState.Active)

		ActiveContractLinesExistForManufOrDistRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and   !related.ContractLineState.Closed
				and    related.IsManufacturerOrDistributorContract)

		ContractImportLinesForVendorItemRel
			one-to-many relation to ContractLineImport
			Field Mapping uses ByItemVendorItem
				related.ContractGroup              = ProcurementGroup
				related.ItemNumber                 = Item
				related.VendorItem                 = VendorItem
			Instance Selection
				where (related.ContractImport.Vendor = Vendor)

		GPOContractLinesForVendorItemAndVendorRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and   !related.ContractLineState.Closed
				and    related.Contract.GPONegotiated)

		ContractsWhereVendorItemWillDuplicateRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemItemUom
				related.ContractGroup               = ProcurementGroup
				related.Contract                    = LocalContract
				related.VendorItem                  = LocalReplacementVendorItem
				related.ItemNumber                  = Item
				related.UOM                         = LocalUnitOfMeasure
	  		Instance Selection
				where (!related.ContractLineState.Closed)

		MultipleVendorItemRel
			one-to-many relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup	= ProcurementGroup
				related.Item				= Item
				related.Vendor				= Vendor
			Instance Selection
				where (related.Active)

		GTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses symbolic key
				related.ItemGroup     = ProcurementGroup
				related.Item          = Item

		IcitemgtinbuomRel
			one-to-many relation to ItemGTIN
			valid when (HasBuyUOMAndManufacturer)
			Field Mapping uses Set7
				related.ItemGroup = ProcurementGroup
			Instance Selection
				where (related.Manufacturer.ManufacturerCode = Manufacturer.ManufacturerCode
				and   related.Manufacturer.ManufacturerDivision = Manufacturer.ManufacturerDivision
				and   related.ManufacturerNumber = ManufacturerNumber
				and   related.Item = Item
				and   related.UnitOfMeasure = VendorBuyUOM)

		NationalDrugCodeRel
			one-to-many relation to NationalDrugCode
			Field Mapping uses symbolic key
				related.ItemGroup = ProcurementGroup
				related.Item      = Item

		AnotherVendorItemRel
			one-to-many relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup	= ProcurementGroup
				related.Item				= Item
				related.Vendor				= Vendor
			Instance Selection
				where (related.VendorItem not = VendorItem)

		AnotherActiveVendorItemRel
			one-to-many relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup	= ProcurementGroup
				related.Item				= Item
				related.Vendor				= Vendor
			Instance Selection
				where (related.Active
				and	   related.VendorItem not = VendorItem)

		AnotherDefaultVendorItemRel
			one-to-many relation to VendorItem
			Field Mapping uses symbolic key
				related.ProcurementGroup	= ProcurementGroup
				related.Item				= Item
				related.Vendor				= Vendor
			Instance Selection
				where (related.UseAsDefault
				and    related.Active
				and	   related.VendorItem not = VendorItem)

		DuplicateVendorItemRel
			one-to-many relation to VendorItem
			Field Mapping uses Set3
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.VendorItem			= VendorItem
			Instance Selection
				where (related.Item != Item
				and    related.Active)

		DuplicateManufacturerInfoRel   
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.Manufacturer		= Manufacturer
				related.ManufacturerNumber	= ManufacturerNumber
			Instance Selection
				where (HasManufacturerInfo
				and    related.VendorItem != VendorItem
				and    related.Item        = Item
				and    related.Active)

		DuplicateItemManufacturerInfoRel   
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.Manufacturer		= Item.Manufacturer
				related.ManufacturerNumber	= Item.ManufacturerNumber
			Instance Selection
				where (related.VendorItem != VendorItem
				and    related.Active
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = VendorBuyUOM
				or     VendorBuyUOM !entered))				

		NewReplacementItemManufacturerInfoRel   
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.Manufacturer		= LocalReplacementManufacturer
				related.ManufacturerNumber	= LocalReplacementManufacturerNumber
			Instance Selection
				where (related.VendorItem != VendorItem
				and    related.Active
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = LocalUnitOfMeasure
				or     LocalUnitOfMeasure !entered))	

		SameManufacturerInfoAsReplacementRel    
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.Manufacturer		= LocalReplacementManufacturer
				related.ManufacturerNumber	= LocalReplacementManufacturerNumber
			Instance Selection
				where (related.Active
				and    related.UniqueID    != UniqueID
				and    LocalReplacementManufacturerNumber entered
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = LocalUnitOfMeasure
				or     LocalUnitOfMeasure !entered))

		SameManufacturerInfoAsExistingReplacementRel 
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup	= ProcurementGroup
				related.Vendor				= Vendor
				related.Manufacturer		= LocalReplacementManufacturer
				related.ManufacturerNumber	= LocalReplacementManufacturerNumber
			Instance Selection
				where (related.Active
				and    related.UniqueID    != LocalReplacementVendorItem.UniqueID
				and    LocalReplacementManufacturerNumber entered
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = LocalUnitOfMeasure
				or     LocalUnitOfMeasure !entered))				

		VendorItemsForVendorAndManufacturerInformationRel  
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup				= ProcurementGroup
				related.Vendor							= Vendor
				related.Manufacturer					= Manufacturer
				related.ManufacturerNumber				= ManufacturerNumber
			Instance Selection
				where (related.Active
				and    related.ManufacturerNumber entered
				and    related.UniqueID                != UniqueID
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = VendorBuyUOM
				or     VendorBuyUOM !entered))	

		DuplicateManufacturerRel   
			one-to-many relation to VendorItem
			Field Mapping uses Set5
				related.ProcurementGroup				= ProcurementGroup
				related.Vendor							= Vendor
				related.Manufacturer					= Manufacturer
				related.ManufacturerNumber				= ManufacturerNumber
			Instance Selection
				where (related.Active
				and    related.ManufacturerNumber entered
				and    related.UniqueID                != UniqueID
				and   (related.VendorBuyUOM !entered
				or     related.VendorBuyUOM = VendorBuyUOM
				or     VendorBuyUOM !entered))					

		SameManufacturerNumberDifferentItemRel 
			one-to-many relation to VendorItem 
			Field Mapping uses Set5
				related.ProcurementGroup				= ProcurementGroup
				related.Vendor							= Vendor
				related.Manufacturer					= Manufacturer
				related.ManufacturerNumber				= ManufacturerNumber
			Instance Selection
				where (Active
				and    related.Active
				and    related.ManufacturerNumber entered
				and    related.Item                    != Item) 
		PurchasingVendorRel
			one-to-one relation to PurchasingVendor
			Field Mapping uses symbolic key
				related.VendorGroup	= ProcurementGroup
				related.Vendor		= Vendor

		ContractLinesMismatchSearchRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and   (related.Manufacturer 		!= LocalManufacturer
				or     related.ManufacturerNumber   != LocalManufacturerNumber))

		ContractLinesToSetToItemmast 
			one-to-many relation to ContractLine 
			Field Mapping uses ByVendorItemVendor
				related.ContractGroup		= ProcurementGroup
				related.VendorItem			= VendorItem
			Instance Selection
				where (related.Contract.Vendor 		= Vendor
				and    related.ItemType = 3
				and    !related.ContractLineState.Closed)		
		ContractLinesByUOMRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemVendor
				related.ContractGroup		= ProcurementGroup
				related.VendorItem			= VendorItem
			Instance Selection
				where (related.UOM	!= LocalUnitOfMeasure
				and    !related.OnHold
				and     related.Contract.Vendor = Vendor
				and    !related.ContractLineState.Closed)

		ContractLinesForActivationRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemVendor
				related.ContractGroup		= ProcurementGroup
				related.VendorItem			= VendorItem
			Instance Selection
				where (related.Contract.Vendor 		= Vendor
				and    !related.ContractLineState.Closed
				and    related.Manufacturer    		= Manufacturer
				and    related.ManufacturerNumber	= ManufacturerNumber
				and    ManufacturerNumber entered)

		ContractLinesForVendorItemUOMRel
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= VendorItem
			Instance Selection
				where (related.Contract.Vendor		= Vendor
				and    !related.UOM = LocalReplacementVendorItem.VendorBuyUOM)

		ContractLinesNoVendorItemRel 
			one-to-many relation to ContractLine
			Field Mapping uses ByVendorItemOpen
				related.ContractGroup				= ProcurementGroup
				related.ItemNumber					= Item
				related.VendorItem					= blank
			Instance Selection
				where (UseAsDefault
				and    related.Contract.Vendor		= Vendor		
				and    related.Manufacturer    		= Manufacturer
				and    related.ManufacturerNumber	= ManufacturerNumber)

		ConsolidatedItemMasterRel
			one-to-many relation to ConsolidatedItemMaster
			Field Mapping uses ByInternalItem     
				related.ContractGroup	    = ProcurementGroup
				related.InternalItem		= Item      
		ManufacturerInfoEditRel
			one-to-one relation to Manufacturer
			Field Mapping uses symbolic key
				related.ItemGroup          				= ProcurementGroup
				related.Manufacturer        			= Manufacturer

		RecallProductItemByManufRel 
			one-to-many relation to RecallProductItem
			Field Mapping uses ByManufInfo
				related.RecallGroup 								= ProcurementGroup 
				related.ManufacturerInformation.Manufacturer		= Manufacturer 
				related.ManufacturerInformation.ManufacturerNumber	= ManufacturerNumber
			Instance Selection 
				where (related.RecallNotice.Vendor !entered
				or     related.RecallNotice.Vendor = Vendor)

		RecallProductItemByManufNumberOnlyRel 
			one-to-many relation to RecallProductItem
			Field Mapping uses ByManufacturerNumber
				related.RecallGroup 								= ProcurementGroup 
				related.ManufacturerInformation.ManufacturerNumber	= ManufacturerNumber
			Instance Selection 
				where (related.RecallNotice.Vendor !entered
				or     related.RecallNotice.Vendor = Vendor)

		ItemRequisitionLinesRel
			one-to-many relation to RequisitionLine
			Field Mapping uses ByItem
				related.Item 					= Item
			Instance Selection
				where (related.Company.BusinessGroup  = ProcurementGroup)

		SuggestedContractLineItemsManufacturerNumberRel
			one-to-many relation to ContractLineSuggestedItem
			Field Mapping uses BySuggestedVendorItem
				related.ContractGroup        = ProcurementGroup
				related.SuggestedVendorItem  = VendorItem
			Instance Selection
				where ((related.MessageNumber = 1
				or     related.MessageNumber = 5)
				and    related.NotResolved)

		SuggestedOpenContractLineItemsRel
			one-to-many relation to ContractLineSuggestedItem
			Field Mapping uses BySuggestedVendorItem
				related.ContractGroup        = ProcurementGroup
				related.SuggestedVendorItem  = VendorItem
			Instance Selection
				where (related.NotResolved)

		LocalItemUOMBuyRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup         = ProcurementGroup
				related.Item              = LocalItem
				related.UnitOfMeasure     = LocalUnitOfMeasure

		LocalItemUOMPriceRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup         = ProcurementGroup
				related.Item              = LocalItem
				related.UnitOfMeasure     = LocalPriceUnitOfMeasure

		ItemUOMBuyRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup         = ProcurementGroup
				related.Item              = Item
				related.UnitOfMeasure     = VendorBuyUOM.UnitOfMeasure

		ItemUOMPriceRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup         = ProcurementGroup
				related.Item              = Item
				related.UnitOfMeasure     = VendorPriceUOM.UnitOfMeasure

		ItemLocationRel
			one-to-many relation to ItemLocation
			Field Mapping uses ByItem
				related.Item = Item
			Instance Selection
				where (related.Item.ItemGroup = ProcurementGroup)

		VendorBackorderDetailsRel
			one-to-many relation to VendorBackorderDetails
			Field Mapping uses ByProcurementGroup
				related.ProcurementGroup 			= ProcurementGroup
				related.Item 						= Item
				related.Vendor 						= Vendor
				related.VendorItem 					= VendorItem
			Instance Selection
				where (related.IsOpen
				and   (related.RecordType = "VI"    
				or     related.RecordType = "PO"))	

		VendorItemBackorderDetailsRel
			one-to-many relation to VendorBackorderDetails
			Field Mapping uses ByProcurementGroup
				related.ProcurementGroup 			= ProcurementGroup
				related.Item 						= Item
				related.Vendor 						= Vendor
				related.VendorItem 					= VendorItem
			Instance Selection
				where (related.IsOpen
				and    related.RecordType = "VI")    

		NewVendorItemBackorderDetailsRel
			one-to-many relation to VendorBackorderDetails
			Field Mapping uses ByProcurementGroup
				related.ProcurementGroup 			= ProcurementGroup
				related.Item 						= Item
				related.Vendor 						= Vendor
				related.VendorItem 					= LocalReplacementVendorItem
			Instance Selection
				where (related.IsOpen
				and    related.RecordType = "VI")	

		NewItemBackorderDetailsRel
			one-to-many relation to VendorBackorderDetails
			Field Mapping uses ByProcurementGroup
				related.ProcurementGroup 			= ProcurementGroup
				related.Item 						= LocalItem
				related.Vendor 						= Vendor
				related.VendorItem 					= VendorItem
			Instance Selection
				where (related.IsOpen
				and    related.RecordType = "VI")	


		BODItemUOMRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup  							= ProcurementGroup
				related.Item								= Item
				related.UnitOfMeasure                       = Item.StockUOM

	Sets
		Set2
			indexed
			Sort Order
				ProcurementGroup
				VendorItem
				Item
				Vendor

		Set3
			indexed
			Sort Order
				ProcurementGroup
				Vendor
				VendorItem
				Item

		Set4
			indexed
			Sort Order
				ProcurementGroup
				Vendor
				Item
				VendorItem

		Set5
			indexed
			Sort Order
				ProcurementGroup
				Vendor
				Manufacturer
				ManufacturerNumber
				Item
				VendorItem

		Set6
			indexed
			Instance Selection
				where (HasManufacturerInfo)
			Sort Order
				ProcurementGroup
				Manufacturer
				ManufacturerNumber
				Item
				VendorBuyUOM
				Vendor
				VendorItem

		ByStrippedManufacturerNumber
			indexed
			Instance Selection
				where (HasManufacturerInfo)
			Sort Order
				ProcurementGroup
				ManufacturerNumberStripped
				Vendor
				Manufacturer
				Item
				VendorBuyUOM
				VendorItem

		ByStrippedVendorItem
			indexed
			Sort Order
				ProcurementGroup
				Vendor
				VendorItemStripped
				Item
				VendorBuyUOM
				VendorItem

		ByStrippedVendorItemFirst
			indexed
			Sort Order
				ProcurementGroup
				VendorItemStripped
				Vendor
				Item
				VendorBuyUOM
				VendorItem

		ByManufacturerNumber
			indexed
			Instance Selection
				where (HasManufacturerInfo)
			Sort Order
				ProcurementGroup
				ManufacturerNumber
				Manufacturer
				Item
				VendorBuyUOM
				Vendor
				VendorItem

		FromWorksheet
			Instance Selection
				where (FromWorksheet)
			Sort Order
				FromWorksheetResult
				ProcurementGroup
				Item
				Vendor
				VendorItem

	Field Rules
		Item
			constraint (Item exists)
				"ItemDoesNotExist"

		VendorItem
			if (IsActive)
				constraint (DuplicateVendorItemRel not exists)
					"VendorItemAlreadyExistsForVendor"                         

		VendorItemDescription
			default to Item.Description
			required

		VendorBuyUOM.UnitOfMeasure
			LocalUnitOfMeasure = VendorBuyUOM.UnitOfMeasure

			constraint (ContractLinesByUOMRel not exists)
				"BuyUnitOfMeasureMustMatchTheUnitOfMeasureOnAssociatedContractLines"

			constraint (VendorItemsForVendorAndManufacturerInfoChangeRel !exists)
				"VendorItem<first VendorItemsForVendorAndManufacturerInfoChangeRel.VendorItem>AlreadyExistsWithTheSameManufacturerInformationForVendor<Vendor>;MustEnterDifferentManufacturerInformationOrBuyUnitsOfMeasureForEachVendorItem"

		VendorPriceUOM.UnitOfMeasure
			if (!Item.CatchWeightCode.NotCatchWeight)
				cannot be entered
					"VendorPrice_\U\O\MCannotBeEnteredForCatchWeightItems"    

			LocalUnitOfMeasure = VendorPriceUOM.UnitOfMeasure

			constraint (ContractLinesByUOMRel not exists)
				"PriceUnitOfMeasureMustMatchTheUnitOfMeasureOnAssociatedContractLines"

		Manufacturer
			constraint (ManufacturerInfoEditRel exists)
   				"ManufacturerCodeAndDivisionCombinationDoesNotExist"

			constraint (VendorItemsForVendorAndManufacturerInfoChangeRel !exists)
				"VendorItem<first VendorItemsForVendorAndManufacturerInfoChangeRel.VendorItem>AlreadyExistsWithTheSameManufacturerInformationForVendor<Vendor>;MustEnterDifferentManufacturerInformationOrBuyUnitsOfMeasureForEachVendorItem"



			default to Item.Manufacturer

		ManufacturerNumber


			default to Item.ManufacturerNumber

			if (PurchasingVendorRel exists
			and PurchasingVendorRel.GTINInUse)
				required
					"ManufacturerInformationRequiredWhenVendorUses_\G\T\I\Ns" 

			constraint (VendorItemsForVendorAndManufacturerInfoChangeRel !exists)
					"VendorItem<first VendorItemsForVendorAndManufacturerInfoChangeRel.VendorItem>AlreadyExistsWithTheSameManufacturerInformationForVendor<Vendor>;MustEnterDifferentManufacturerInformationOrBuyUnitsOfMeasureForEachVendorItem"
 
   			if (ManufacturerNumber changed
   			or  Manufacturer changed)
  				if (ContractLinesForVendorItemAndVendorRel exists)
  					confirmation required
  						"ContractLinesForThisVendorAndVendorItemExist;ContractLinesWillBeUpdatedWithNewManufacturerInformation;DoYouWantToContinue?"
					for each ContractLinesForVendorItemAndVendorRel
						invoke FastUpdate each
							invoked.Manufacturer			= Manufacturer
							invoked.ManufacturerNumber		= ManufacturerNumber

				if (ContractImportLinesForVendorItemRel exists)
					for each ContractImportLinesForVendorItemRel
						invoke FastUpdateWithoutEdits each
							invoked.UpdateManufacturerInformation		= true
							invoked.NewManufacturer                     = LocalReplacementManufacturer
							invoked.NewManufacturerNumber               = LocalReplacementManufacturerNumber

		ManufacturerNumberStripped
			default to DerivedStrippedManufacturerNumber

		VendorItemStripped
			default to DerivedStrippedVendorItem

		LabelerIdentificationCode
			default to Manufacturer.LabelerIdentificationCode

		Active
			initial value is true

		UseAsDefault
			if (action type.Create
			and not MultipleActiveVendorItems
			and Active)
				default to true

	Actions
		Create is a Create Action
			Field Rules
				Active = true

			Action Rules
				constraint (Item.Active)
					"Item<Item>IsInactive"
				
				if (UseAsDefault)
  	 				if (AnotherDefaultVendorItemRel exists)
  	 					confirmation required
  	 						"Warning,DefaultVendorItemWillBeChangedFrom<first AnotherDefaultVendorItemRel.VendorItem>To<VendorItem>;Proceed?"
  	 					invoke ResetDefaultVendorItem AnotherDefaultVendorItemRel

				if (VendorBuyUOM entered)
					constraint (ItemUOMBuyRel.OKForBuying)
						"<VendorBuyUOM.UnitOfMeasure>IsNotAValidBuyUnitOfMeasureForThisItem"

				if (VendorPriceUOM entered)
					constraint (ItemUOMPriceRel.OKForBuying)
						"<VendorPriceUOM.UnitOfMeasure>IsNotAValidBuyUnitOfMeasureForThisItemAndCannotBeUsedForPriceUOM"

			Exit Rules
				invoke BuildTextIndex Item



				if (ContractLinesToSetToItemmast exists)
					confirmation required 
						"ContractLinesExistWhichWillHaveItemNumberUpdated;DoYouWantToContinue?"
					for each ContractLinesToSetToItemmast
						invoke ChangeSpecialItemToItem each 
							invoked.PrmNewItemNumber        = Item
							invoked.PrmFromWorksheetResult	= FromWorksheetResult

				if (ContractLinesNoVendorItemRel exists)
					for each ContractLinesNoVendorItemRel
						invoke FastUpdateWithoutEdits each
							invoked.UpdateVendorItem	= true 
							invoked.NewVendorItem       = VendorItem

				if (ManufacturerNumber entered)
					if (RecallProductItemByManufNumberOnlyRel exists)
						for each RecallProductItemByManufNumberOnlyRel 
							if (each.Item !entered
							and each.CanUpdateItemOnNotice)
								invoke UpdateItemFromExternal each 
									invoked.ParmVendorItem 	= VendorItem
									invoked.ParmItem 		= Item 
					if (RecallProductItemByManufRel exists)
						for each RecallProductItemByManufRel 
							if (each.Item !entered
							and each.CanUpdateItemOnNotice)
								invoke UpdateItemFromExternal each 
									invoked.ParmVendorItem 	= VendorItem
									invoked.ParmItem 		= Item 

        BackorderVendorItem is an Instance Action
            valid when (CanBackorderVendorItem)
			Parameters
				PrmBackorderType				is Numeric size 1
					States
						VendorBackorder			value is 1
						ManufacturerBackorder	value is 2
        		PrmStartDate	                is Date
				PrmEstimatedAvailabilityDate	is Date
			Action Rules
				invoke CreateVendorItemBackorder VendorBackorderDetails
					fill in fields from this instance
					invoked.BackorderType						= PrmBackorderType
					invoked.StartDate							= PrmStartDate
					invoked.EstimatedAvailabilityDate			= PrmEstimatedAvailabilityDate


		Update is an Update Action
			Action Rules

				if (UseAsDefault changed)
					constraint (!old UseAsDefault)
						"CannotChange,ADefaultVendorItemMustExist"

				if (VendorBuyUOM entered)
					constraint (ItemUOMBuyRel.OKForBuying)
						"<VendorBuyUOM.UnitOfMeasure>IsNotAValidBuyUnitOfMeasureForThisItem"

				if (VendorPriceUOM entered)
					constraint (ItemUOMPriceRel.OKForBuying)
						"<VendorPriceUOM.UnitOfMeasure>IsNotAValidBuyUnitOfMeasureForThisItemAndCannotBeUsedForPriceUOM"

				if (UseAsDefault)   
  	 				if (AnotherDefaultVendorItemRel exists)
  	 					invoke ResetDefaultVendorItem AnotherDefaultVendorItemRel

			Exit Rules

				if (TextSearchable changed)
					invoke BuildTextIndex Item

				if (VendorBuyUOM changed
				or  VendorPriceUOM changed)
  					if (ContractLinesForVendorItemAndVendorActiveStatusRel exists)
  						confirmation required
  							"ContractLinesForThisVendorAndVendorItemExistWhichMayBePutInErrorByThisChange;Use_Active_Contract_LinesNavigationToMaintainContractLines;DoYouWantToContinue?"
						for each ContractLinesForVendorItemAndVendorActiveStatusRel
							invoke TestMismatchWithVendorUOMError each	

		FastUpdate is an Update Action
			bypass field rules
			restricted
			Action Rules

  	 	ResetDefaultVendorItem is an Update Action
  	 		restricted
  	 		Action Rules
				UseAsDefault = false


		Delete is a Delete Action
  	 		Entrance Rules
  	 			constraint (ContractLinesForVendorItemAndVendorRel !exists)
  	 				"CannotDelete;ContractLineExistsForVendorItem;MustInactivate"
  	 			if (MultipleVendorItems)
  	 				constraint (!UseAsDefault)
	  	 				"CannotDeleteDefaultVendorItem"
				if (Active)
					invoke BuildTextIndex Item
			Action Rules
				invoke Delete VendorBackorderDetailsRel

		UpdateLastLeadtimeFromPOReceiptLine is an Instance Action
			restricted
			refresh and lock this instance

			Parameters
				PrmLastLeadtime		is a LeadtimeDays

			Action Rules
				LastLeadtime = PrmLastLeadtime
			
    	ActivateFromItem is an Instance Action
    		restricted
    		
    		Action Rules
    			
    			LocalFromItemActivation = true
    			if (DuplicateManufacturerRel !exists)
    				invoke Activate
    	
    	Activate is an Instance Action
    		valid when (!IsActive)
    		Action Rules
    			constraint (Item.Active = true)
    				"CannotActivate;Item<Item>IsInactive"
    			
    			constraint (VendorItemsForVendorAndManufacturerInformationRel !exists)
    				"VendorItem<first VendorItemsForVendorAndManufacturerInformationRel.VendorItem>AlreadyExistsWithTheSameManufacturerInformationForVendor<Vendor>;MustEnterDifferentManufacturerInformationOrBuyUnitsOfMeasureForEachVendorItem"
    				
				LocalReplacementVendorItem						= VendorItem
   				constraint (VendorItemsForVendorAndVendorItemRel !exists)
   					"ThisVendorItemAlreadyExistsAsAnActiveVendorItemForVendor<Vendor>AndItem<first VendorItemsForVendorAndVendorItemRel.Item>"

				if (VendorGTINInUse)
					constraint (ManufacturerNumber entered)
						"ManufacturerInformationRequiredWhenVendorUses_\G\T\I\Ns"

				Active = true

 				if (!LocalFromItemActivation)
	 				for each ContractLineVenItemRel
	   					invoke RemoveInactiveVendorItemError each

				if (LocalFromItemActivation = true)
					for each ContractLinesForActivationRel
   						invoke RemoveInactiveVendorItemError each
   							invoked.ChangeToItem = Item

				if (NumberOfActiveVendorItemsForAnItem = 0) 
					UseAsDefault = true
			Exit Rules

				invoke BuildTextIndex Item

		InactivateFromItem is an Instance Action
			restricted

			Action Rules

				LocalFromItemInactivation = true
				invoke Inactivate

		Inactivate is an Instance Action
			valid when (IsActive)

			Action Rules

				if (UseAsDefault
				and !LocalFromReplaceAndInactivate
				and !LocalFromItemInactivation)
					constraint (NumberOfActiveVendorItemsForAnItem <= 1)
						"CannotInactivate;VendorItemIsTheDefaultItem"

				Active          = false
				UseAsDefault    = false

				if (!LocalFromReplaceAndInactivate
				and !LocalFromItemInactivation)
					for each ContractLineVenItemRel
						invoke CreateInactiveVendorItemError each

			Exit Rules
				invoke BuildTextIndex Item
				if (initiating action = "Inactivate")
					if (VendorBackorderDetailsRel exists)
						invoke CloseBackorder VendorBackorderDetailsRel

		MakeDefault is an Instance Action
			valid when (EligibleForDefaultVendorItem)
			Action Rules
				if (AnotherDefaultVendorItemRel exists)
					confirmation required
						"Warning,DefaultVendorItemWillBeChangedFrom<first AnotherDefaultVendorItemRel.VendorItem>To<VendorItem>;Proceed?"
					invoke ResetDefaultVendorItem AnotherDefaultVendorItemRel

				UseAsDefault = true

			Exit Rules
				invoke BuildTextIndex Item


		UpdateVendorItemMfgInfoAndDesc is an Instance Action						
			restricted
			Parameters
				PrmUpdateManufacturer       is Boolean
				PrmManufacturer				is a Manufacturer
				PrmManufacturerNumber		is a ManufacturerNumber
				PrmUpdateVendorDescription	is Boolean
				PrmDescription				is a Description

			Action Rules

				if (PrmUpdateVendorDescription)
					VendorItemDescription =	PrmDescription

				if (PrmUpdateManufacturer)
					Manufacturer 		= PrmManufacturer
					ManufacturerNumber	= PrmManufacturerNumber

			Exit Rules
				ManufacturerNumberStripped = DerivedStrippedManufacturerNumber
				invoke BuildTextIndex Item


		UpdateContractMfgInfoAndDesc is an Instance Action
			restricted
			Parameters
				PrmManufacturer 			is a Manufacturer
				PrmManufacturerNumber   	is a ManufacturerNumber
				PrmDescription				is a Description
				PrmDescription2				is a Description
				PrmDescription3				is a Description3
				UpdateDescription           is Boolean
				UpdateManufacturer          is Boolean
				
			Action Rules
				for each ContractLinesForVendorItemAndVendorRel
					invoke FastUpdateWithoutEdits each
 						if (UpdateManufacturer)
							invoked.UpdateManufacturerInformation = true
							invoked.NewManufacturer			= PrmManufacturer
							invoked.NewManufacturerNumber	= PrmManufacturerNumber
						if (UpdateDescription)
							invoked.UpdateDescription = true
							invoked.Description1      = PrmDescription
							invoked.Description2      = PrmDescription2
							invoked.Description3      = PrmDescription3

				for each ContractImportLinesForVendorItemRel
					invoke FastUpdateWithoutEdits each
			 			if (PrmManufacturer entered)
							invoked.UpdateManufacturerInformation = true
							invoked.NewManufacturer			= PrmManufacturer
							invoked.NewManufacturerNumber	= PrmManufacturerNumber
						if (PrmDescription entered)
							invoked.UpdateItemDescription1	= true
							invoked.NewItemDescription1		= PrmDescription
			 			if (PrmDescription2 entered)
			 				invoked.UpdateItemDescription2	= true
							invoked.NewItemDescription2		= PrmDescription2
			 			if (PrmDescription3 entered)
			 				invoked.UpdateItemDescription3	= true
			 				invoked.NewItemDescription3		= PrmDescription3		

		ChangeManufacturerInformation is an Instance Action
			Parameters
				PrmManufacturer     				is a Manufacturer
				PrmManufacturerNumber   			is a ManufacturerNumber
				PrmBuyUnitOfMeasure                 is a UnitOfMeasureEdit
				PrmPriceUnitOfMeasure               is a UnitOfMeasureEdit 
				PrmUpdateManufacturerNumberOnItem	is Boolean
				PrmUpdateManufacturerNumberOnGTIN	is Boolean
				PrmChangeFromManufacturer			is Boolean
				PrmChangeToItemInformation          is Boolean

			Parameter Rules

				PrmManufacturer
					initial value is Manufacturer
				PrmManufacturerNumber
					initial value is ManufacturerNumber
					if (VendorGTINInUse)
						required
							"ManufacturerInformationRequiredWhenVendorUses_\G\T\I\Ns"
					if (PrmChangeFromManufacturer)
						default to ManufacturerNumber
				PrmBuyUnitOfMeasure 
					initial value is VendorBuyUOM
					default to VendorBuyUOM
				PrmPriceUnitOfMeasure 
					initial value is VendorPriceUOM
					default to VendorPriceUOM
				PrmUpdateManufacturerNumberOnItem
					constraint ((PrmManufacturer entered and PrmManufacturerNumber entered)
					or			(PrmManufacturer not entered and PrmManufacturerNumber not entered))
						"CannotUpdateItemAndItemLocation.MustEnterFullManufacturerInformation"
					if (PrmChangeToItemInformation)
						constraint (PrmUpdateManufacturerNumberOnItem = false)
							"CannotUpdateManufacturerInformationBothToAndFromItem"
				PrmChangeToItemInformation 
					if (PrmUpdateManufacturerNumberOnItem)
						constraint (PrmChangeToItemInformation = false)
							"CannotUpdateManufacturerInformationBothToAndFromItem"

			Action Rules
				if (PrmChangeToItemInformation = false)
					LocalReplacementManufacturer					= PrmManufacturer
					LocalReplacementManufacturerNumber				= PrmManufacturerNumber
				else 
					LocalReplacementManufacturer					= Item.Manufacturer
					LocalReplacementManufacturerNumber				= Item.ManufacturerNumber					
				LocalUnitOfMeasure                              = PrmBuyUnitOfMeasure.UnitOfMeasure
				if (PrmBuyUnitOfMeasure.UnitOfMeasure != VendorBuyUOM.UnitOfMeasure
				or  PrmPriceUnitOfMeasure.UnitOfMeasure != VendorPriceUOM.UnitOfMeasure)
					LocalUOMChanged = true

				if (LocalReplacementManufacturerNumber entered)
					constraint (SameManufacturerInfoAsReplacementRel !exists)
						"VendorItem<first SameManufacturerInfoAsReplacementRel.VendorItem>AlreadyExistsWithTheSameManufacturerInformationForVendor<Vendor>;MustEnterDifferentManufacturerInformationOrBuyUnitsOfMeasureForEachVendorItem"

  				LocalManufacturer		= LocalReplacementManufacturer
				LocalManufacturerNumber = LocalReplacementManufacturerNumber
				if (MismatchWithContract)
  					confirmation required
  						"Warning;ContractLinesForThisVendorAndVendorItemExistWithDifferentManufacturerInformationThanHasBeenEntered;DoYouWantToContinue?"

   				if (SuggestedContractLineItemsExistForManufacturer)
   					confirmation required
   						"ContractLinesExistThatHaveSuggestedItemsBasedOnTheOldManufacturerInformation;YouMayWishToViewTheSuggestedItemsForContractLinesAndRunTheRecheckForSuggestedItemsAfterPerformingThisProcess"

				if (PrmUpdateManufacturerNumberOnGTIN)
					for each IcitemgtinRel
						invoke UpdateManufacturerInfo each
							invoked.PrmManufacturer		  	= LocalReplacementManufacturer
							invoked.PrmManufacturerNumber 	= LocalReplacementManufacturerNumber

   				
				
				Manufacturer           			= LocalReplacementManufacturer
   				ManufacturerNumber     			= LocalReplacementManufacturerNumber
				VendorBuyUOM.UnitOfMeasure		= PrmBuyUnitOfMeasure.UnitOfMeasure
				VendorPriceUOM.UnitOfMeasure	= PrmPriceUnitOfMeasure.UnitOfMeasure

				if (LocalUOMChanged)
  					if (ContractLinesForVendorItemAndVendorActiveStatusRel exists)
  						confirmation required
  							"ContractLinesForThisVendorAndVendorItemExistWhichMayBePutInErrorByUOMChanges;Use_Active_Contract_LinesNavigationToMaintainContractLines;DoYouWantToContinue?"

   				for each ContractLinesForVendorItemAndVendorRel
					invoke FastUpdateWithoutEdits each
						invoked.UpdateManufacturerInformation					= true						
						invoked.NewManufacturer                                 = LocalReplacementManufacturer
						invoked.NewManufacturerNumber                           = LocalReplacementManufacturerNumber
						invoked.UOMChanged                                      = LocalUOMChanged

				for each ContractImportLinesForVendorItemRel
					invoke FastUpdateWithoutEdits each
						invoked.UpdateManufacturerInformation		= true
						invoked.NewManufacturer                     = LocalReplacementManufacturer
						invoked.NewManufacturerNumber               = LocalReplacementManufacturerNumber

				if (PrmUpdateManufacturerNumberOnItem)
					invoke UpdateManufacturerInfo Item
						invoked.PrmManufacturer 		= PrmManufacturer
						invoked.PrmManufacturerNumber 	= PrmManufacturerNumber
						invoked.FromVendorItem          = true

					for each ItemLocationRel
						invoke UpdateManufacturerInfo each
							invoked.PrmManufacturer 		= PrmManufacturer
							invoked.PrmManufacturerNumber 	= PrmManufacturerNumber

				if (ManufacturerNumber entered)
					if (RecallProductItemByManufNumberOnlyRel exists)
						for each RecallProductItemByManufNumberOnlyRel 
							if (each.Item !entered
							and each.CanUpdateItemOnNotice)
								invoke UpdateItemFromExternal each 
									invoked.ParmVendorItem 	= VendorItem
									invoked.ParmItem 		= Item 
					if (RecallProductItemByManufRel exists)
						for each RecallProductItemByManufRel 
							if (each.Item !entered
							and each.CanUpdateItemOnNotice)
								invoke UpdateItemFromExternal each 
									invoked.ParmVendorItem 	= VendorItem
									invoked.ParmItem 		= Item 

			Exit Rules

				ManufacturerNumberStripped = DerivedStrippedManufacturerNumber
				invoke BuildTextIndex Item

		ReplaceAndInactivate is an Instance Action
			valid when (Active)
			Parameters
				PrmProcurementGroup										is a ProcurementGroup
				PrmReplacementItem                                      is like Item
				PrmVendor												is a Vendor
				PrmReplacementVendorItem								is a VendorItem
				PrmReplacementVendorItemDescription						is a Description
				PrmReplacementManufacturer          					is a Manufacturer
				PrmReplacementManufacturerNumber    					is a ManufacturerNumber
				PrmBuyUnitOfMeasure                 					is a UnitOfMeasureEdit
				PrmPriceUnitOfMeasure                                   is a UnitOfMeasureEdit 
				ReplaceWithNewOrExistingItem        					is Numeric size 1
					States
						New							value is 1
							default label is "NewVendorItem"
						Existing                    value is 2
							default label is "ExistingVendorItem"
						NewItem                     value is 3
							default label is "DifferentItemMasterItem"
				PrmUseManufacturerFromItem                              is Boolean
				PrmUpdateAssociatedContracts                            is Boolean
				PrmUpdateManufacturerNumberOnItem			 			is Boolean
				PrmUpdateManufacturerNumberOnGTIN			 			is Boolean
				PrmInactivateItem                                       is Boolean
					default label is "InactivateItem"
				PrmMakeItemNotAvailableForUse                           is Boolean 
					default label is "MakeItemNotAvailableForUse"

			Parameter Rules
				PrmProcurementGroup
					default to ProcurementGroup
					initial value is ProcurementGroup
					required

				
				PrmVendor
					default to Vendor
					initial value is Vendor

				PrmReplacementItem
					if (PrmReplacementItem entered)
						LocalItem			= PrmReplacementItem
						constraint (PrmReplacementItemRel exists)
							"Item<PrmReplacementItem>DoesNotExist"

				PrmReplacementVendorItem
					if (PrmReplacementItem !entered)
						constraint (PrmReplacementVendorItem != VendorItem)
							"TheCurrentVendorItemCannotBeReplacedWithItself"
					
					if (ContractLinesForVendorItemAndVendorRel exists)
						for each ContractLinesForVendorItemAndVendorRel
							LocalContract              = each.Contract
							LocalUnitOfMeasure         = each.UOM
							LocalReplacementVendorItem = PrmReplacementVendorItem
							constraint (ContractsWhereVendorItemWillDuplicateRel not exists)
								"ChangingVendorItemWouldCauseDuplicateVendorItemAndUOMOnContract<each.Contract>;Line<each.ContractLine>;CannotUpdate"

					if (ReplaceWithNewOrExistingItem !=3)
						required
							"ReplacementVendorItemIsRequired"

				PrmReplacementVendorItemDescription
					if (PrmReplacementVendorItem exists)
						force default to PrmReplacementVendorItem.VendorItemDescription
					else
						default to VendorItemDescription

				PrmReplacementManufacturer
					if (PrmReplacementVendorItem exists)
						force default to PrmReplacementVendorItem.Manufacturer
					else
						default to Manufacturer

				PrmReplacementManufacturerNumber
					if (PrmReplacementVendorItem exists)
						force default to PrmReplacementVendorItem.ManufacturerNumber
					else
						default to ManufacturerNumber

				PrmBuyUnitOfMeasure 
					initial value is VendorBuyUOM.UnitOfMeasure

				PrmPriceUnitOfMeasure 
					initial value is VendorPriceUOM.UnitOfMeasure		

				PrmUseManufacturerFromItem 
					initial value is true
				
				PrmMakeItemNotAvailableForUse 
					if (PrmInactivateItem = false)
						force default to false				
			Action Rules
				LocalReplacementVendorItem						= PrmReplacementVendorItem
				LocalReplacementManufacturer                    = PrmReplacementManufacturer
				LocalReplacementManufacturerNumber              = PrmReplacementManufacturerNumber
				LocalUnitOfMeasure                              = PrmBuyUnitOfMeasure.UnitOfMeasure
				LocalPriceUnitOfMeasure 						= PrmPriceUnitOfMeasure.UnitOfMeasure
				if (PrmReplacementItem entered)
					LocalItem                                   = PrmReplacementItem
				else 
				if (PrmReplacementVendorItem exists)
					LocalItem                                   = PrmReplacementVendorItem.Item                             
				if (PrmBuyUnitOfMeasure.UnitOfMeasure != VendorBuyUOM.UnitOfMeasure
				or  PrmPriceUnitOfMeasure.UnitOfMeasure != VendorPriceUOM.UnitOfMeasure)
					LocalUOMChanged = true

				if (ReplaceWithNewOrExistingItem = 2)
					constraint (ReplacementVendorItemRel exists)
						"ReplacementVendorItemDoesNotExist" 					

				if (LocalUOMChanged)
  					if (ContractLinesForVendorItemAndVendorActiveStatusRel exists)
  						confirmation required
  							"ContractLinesForThisVendorAndVendorItemExistWhichMayBePutInErrorByUOMChanges;Use_Active_Contract_LinesNavigationToMaintainContractLines;DoYouWantToContinue?"

				if (PrmReplacementItem entered
				or  PrmReplacementVendorItem exists)
					if (PrmPriceUnitOfMeasure entered)
						constraint (LocalItemUOMPriceRel.OKForBuying)
							"<PrmPriceUnitOfMeasure.UnitOfMeasure>IsNotAValidBuyUnitOfMeasureForThisItemAndCannotBeUsedForPriceUOM"

					if (PrmBuyUnitOfMeasure entered)
						constraint (LocalItemUOMBuyRel.OKForBuying)
							"<PrmBuyUnitOfMeasure.UnitOfMeasure>IsNotAValidBuyUnitOfMeasureForThisItem"

   				if  (PrmReplacementVendorItem exists
   				and !PrmReplacementVendorItem.Active)
   					confirmation required
   						"TheReplacementVendorItemIsCurrentlyInactive;ItWillBeActivatedAsAResultOfThisProcess;DoYouWantToContinue?"

				if (PrmReplacementVendorItem entered
				and	PrmReplacementVendorItem !exists)
					constraint (!VendorItemsForVendorAndVendorItemRel exists)
						"TheReplacementVendorItemAlreadyExistsAsAVendorItemForVendor<Vendor>,AndItem<first VendorItemsForVendorAndVendorItemRel.Item>"
					if (PrmReplacementManufacturer 			!= Manufacturer
					or  PrmReplacementManufacturerNumber 	!= ManufacturerNumber)
						constraint (!SameManufacturerInfoAsReplacementRel exists)
							"ReplacementManufacturerInformationIsAlreadyUsedForVendorItem<first SameManufacturerInfoAsReplacementRel.VendorItem>;MustEnterDifferentManufacturerInformationOrBuyUnitsOfMeasureForEachVendorItem"

					constraint (!ContractLineManufacturerInfoDifferentVendorItemRel exists)
						"ContractLineSpecialItemAlreadyExistsWithSameManufacturerInformation;Contract<first ContractLineManufacturerInfoDifferentVendorItemRel.Contract>;ContractLine<first ContractLineManufacturerInfoDifferentVendorItemRel.ContractLine>"
				else
				if (PrmReplacementVendorItem exists)
					if (PrmReplacementVendorItem.VendorPriceUOM entered
					and VendorPriceUOM entered)
						constraint (PrmReplacementVendorItem.VendorPriceUOM = VendorPriceUOM)
							"TheReplacementCannotBeDone,TheVendorPriceUnitsOfMeasureDoNotMatch"
					if (PrmReplacementVendorItem.VendorBuyUOM entered
					and VendorBuyUOM entered)
						constraint (PrmReplacementVendorItem.VendorBuyUOM = VendorBuyUOM)
							"TheReplacementCannotBeDone,TheVendorBuyUnitsOfMeasureDoNotMatch"
 					else
						if (PrmReplacementVendorItem.VendorBuyUOM entered)
							constraint (!ContractLinesForVendorItemUOMRel exists)
								"ReplacementVendorItemBuyUOMOf<PrmReplacementVendorItem.VendorBuyUOM>DoesNotMatchContract<first ContractLinesForVendorItemUOMRel.Contract>Line<first ContractLinesForVendorItemUOMRel.ContractLine>UOMOf<first ContractLinesForVendorItemUOMRel.UOM>"

   					if  (PrmReplacementManufacturer entered
   					and	(PrmReplacementManufacturer 		!= Manufacturer
   					or   PrmReplacementManufacturerNumber 	!= ManufacturerNumber))
   						constraint (!SameManufacturerInfoAsExistingReplacementRel exists)
   							"ReplacementManufacturerInformationIsAlreadyUsedForVendorItem<first SameManufacturerInfoAsReplacementRel.VendorItem>"
   					constraint (!ContractLineManufacturerInfoDifferentVendorItemRel exists)
   						"ContractLineSpecialItemAlreadyExistsWithSameManufacturerInformation;Contract<first ContractLineManufacturerInfoDifferentVendorItemRel.Contract>;ContractLine<first ContractLineManufacturerInfoDifferentVendorItemRel.ContractLine>"

				if (ReplaceWithNewOrExistingItem.NewItem)
					constraint (PrmReplacementItem != Item)
						"NewItemForVendorItemMustBeDifferentThanCurrentItem"
					constraint (PrmReplacementItem entered)
						"MustEnterANewItemForVendorItem"	
					constraint (PrmReplacementItemRel.Active)
						"CannotUseAnInactiveItemAsANewItemForVendorItem"
					if (PrmUseManufacturerFromItem
					and PrmReplacementItemRel.ManufacturerNumber entered)
						LocalReplacementManufacturer 		= PrmReplacementItemRel.Manufacturer
						LocalReplacementManufacturerNumber	= PrmReplacementItemRel.ManufacturerNumber
						LocalUnitOfMeasure                  = PrmBuyUnitOfMeasure
						constraint (NewReplacementItemManufacturerInfoRel !exists)
							"ManufacturerInformationFromItemIsAlreadyUsedForVendorItem<first NewReplacementItemManufacturerInfoRel.VendorItem>"

					if (VendorItemDescription = Item.Description)
						LocalUsingItemDescription = true

				invoke ReplaceAndInactivateUpdate
					invoked.PrmProcurementGroup					= ProcurementGroup
					invoked.PrmItem								= Item
					invoked.PrmVendorItem						= VendorItem
					invoked.PrmVendor							= Vendor
					if (ReplaceWithNewOrExistingItem = 3)
						invoked.PrmReplacementItem              = PrmReplacementItem
					if (ReplaceWithNewOrExistingItem !=3)
						invoked.PrmReplacementVendorItem		= PrmReplacementVendorItem
					invoked.PrmReplacementVendorItemDescription	= PrmReplacementVendorItemDescription
					invoked.PrmReplacementManufacturer          = PrmReplacementManufacturer
					invoked.PrmReplacementManufacturerNumber    = PrmReplacementManufacturerNumber
					invoked.PrmBuyUnitOfMeasure.UnitOfMeasure   = PrmBuyUnitOfMeasure.UnitOfMeasure
					invoked.PrmPriceUnitOfMeasure.UnitOfMeasure = PrmPriceUnitOfMeasure.UnitOfMeasure
					invoked.PrmUpdateAssociatedContracts        = PrmUpdateAssociatedContracts
					invoked.PrmUpdateManufacturerNumberOnItem   = PrmUpdateManufacturerNumberOnItem
					invoked.PrmUpdateManufacturerNumberOnGTIN   = PrmUpdateManufacturerNumberOnGTIN
					invoked.PrmInactivateItem                   = PrmInactivateItem
					invoked.PrmMakeItemNotAvailableForUse       = PrmMakeItemNotAvailableForUse
					invoked.PrmLocalUOMChanged                  = LocalUOMChanged
					invoked.PrmUsingItemDescription             = LocalUsingItemDescription
					invoked.PrmUseManufacturerFromItem          = PrmUseManufacturerFromItem
					invoked.ReplaceWithNewOrExistingItem        = ReplaceWithNewOrExistingItem					

		StripAllVendorItems is a Set Action
			restricted
			Parameters
				ProcurementGroup

			Instance Selection
				where (ProcurementGroup = ProcurementGroup
				and   (ManufacturerNumberStripped !entered
				or     VendorItemStripped !entered))

			Action Rules
				Instance Rules

					ManufacturerNumberStripped = DerivedStrippedManufacturerNumber
					VendorItemStripped         = DerivedStrippedVendorItem

		ReplaceAndInactivateUpdate is a Set Action
			restricted
			Parameters
				PrmProcurementGroup					is a ProcurementGroup
				PrmItem								is like Item
				PrmVendor							is a Vendor
				PrmVendorItem						is a VendorItem
				PrmReplacementItem                  is an Item
				PrmReplacementVendorItem			is a VendorItem
				PrmReplacementVendorItemDescription	is a Description
				PrmReplacementManufacturer          is a Manufacturer
				PrmReplacementManufacturerNumber    is a ManufacturerNumber
				PrmBuyUnitOfMeasure                 is a UnitOfMeasureEdit 
				PrmPriceUnitOfMeasure               is a UnitOfMeasureEdit
				PrmUpdateAssociatedContracts        is Boolean
				PrmUpdateManufacturerNumberOnItem	is Boolean
				PrmUpdateManufacturerNumberOnGTIN	is Boolean
				PrmInactivateItem                   is Boolean 
				PrmMakeItemNotAvailableForUse       is Boolean 
				PrmLocalUOMChanged                  is Boolean
				PrmUsingItemDescription             is Boolean 
				PrmUseManufacturerFromItem          is Boolean 
				ReplaceWithNewOrExistingItem        is Numeric size 1

			Parameter Rules
				PrmReplacementVendorItemDescription
					if (PrmReplacementVendorItem exists)
						force default to PrmReplacementVendorItem.VendorItemDescription

			Instance Selection
				where (PrmProcurementGroup = ProcurementGroup
				and    PrmVendorItem       = VendorItem
				and    PrmVendor           = VendorItem.Vendor
				and    PrmItem			   = Item)

			Action Rules
				Instance Rules

					LocalVendorItem					= PrmVendorItem
					LocalReplacementVendorItem		= PrmReplacementVendorItem
					LocalItem						= PrmReplacementItem
					LocalUseAsDefault				= PrmVendorItemRel.UseAsDefault
					LocalFromReplaceAndInactivate = true
					if (PrmReplacementManufacturerNumber != ManufacturerNumber
					or  PrmReplacementManufacturer       != Manufacturer)					
						LocalManufacturerInformationChanged = true
					invoke Inactivate

					if (PrmInactivateItem)
						invoke ChangeItemStatus Item 
							invoked.PrmStatus 							= "I"
							invoked.PrmMakeItemNotAvailableForUse		= PrmMakeItemNotAvailableForUse
							invoked.PrmResetContractLinesToSpecialUsing	= 0

					if (LocalManufacturerInformationChanged)
						if (PrmUpdateManufacturerNumberOnItem)
							invoke UpdateManufacturerInfo Item
								invoked.PrmManufacturer 		= PrmReplacementManufacturer
								invoked.PrmManufacturerNumber 	= PrmReplacementManufacturerNumber
								invoked.FromVendorItem          = true

							for each ItemLocationRel
								invoke UpdateManufacturerInfo each
									invoked.PrmManufacturer 		= PrmReplacementManufacturer
									invoked.PrmManufacturerNumber 	= PrmReplacementManufacturerNumber

						if (PrmUpdateManufacturerNumberOnGTIN)
							for each IcitemgtinRel
								invoke UpdateManufacturerInfo each
									invoked.PrmManufacturer		  	= PrmReplacementManufacturer
									invoked.PrmManufacturerNumber 		= PrmReplacementManufacturerNumber

					if ((ReplaceWithNewOrExistingItem = 1
					and  ReplacementVendorItemRel !exists)
					or   ReplaceWithNewOrExistingItem = 3)
						invoke Create
							invoked.ProcurementGroup					= PrmProcurementGroup
							if (PrmReplacementItem !entered)
								invoked.Item                            = PrmItem
							else
								invoked.Item							= PrmReplacementItem
							invoked.Vendor								= PrmVendor
							if (PrmReplacementVendorItem entered)
								invoked.VendorItem						= PrmReplacementVendorItem
							else
							if (PrmReplacementVendorItem !entered)
								invoked.VendorItem                      = PrmVendorItem 
							if (PrmUsingItemDescription = true)
								invoked.VendorItemDescription           = PrmReplacementItem.Description
							else
								invoked.VendorItemDescription          	= PrmReplacementVendorItemDescription
							if (PrmReplacementItem entered
							and PrmUseManufacturerFromItem)
								invoked.Manufacturer                    = PrmReplacementItem.Manufacturer 
								invoked.ManufacturerNumber              = PrmReplacementItem.ManufacturerNumber
							else 
								invoked.Manufacturer                   	= PrmReplacementManufacturer
								invoked.ManufacturerNumber     	        = PrmReplacementManufacturerNumber
							invoked.VendorBuyUOM						= PrmBuyUnitOfMeasure.UnitOfMeasure
							invoked.VendorPriceUOM						= PrmPriceUnitOfMeasure.UnitOfMeasure
							invoked.LabelerIdentificationCode			= PrmVendorItem.LabelerIdentificationCode
							invoked.MinimumOrderQuantity				= PrmVendorItem.MinimumOrderQuantity
							invoked.WarrantyPeriod						= PrmVendorItem.WarrantyPeriod
							invoked.UseAsDefault						= LocalUseAsDefault

					if (ReplacementVendorItemRel exists)
						invoke Update ReplacementVendorItemRel
							if (LocalUseAsDefault = true)
								invoked.UseAsDefault                    = true
							invoked.Active = true

					if (PrmReplacementItem entered) 
						for each ContractLinesForVendorItemAndVendorRel
							invoke FastUpdateWithoutEdits each 
								invoked.UpdateItemNumber	= true 
								invoked.NewItem             = PrmReplacementItem
								invoked.UOMChanged          = PrmLocalUOMChanged

						for each ContractImportLinesForVendorItemRel
							invoke FastUpdateWithoutEdits each
								invoked.NewItem             = PrmReplacementItem 

						if (VendorItemBackorderDetailsRel exists
						and NewItemBackorderDetailsRel  not exists)
							invoke FastUpdate VendorItemBackorderDetailsRel
								invoked.Item 				= PrmReplacementItem

					if (PrmReplacementVendorItem entered)
						for each ContractLinesForVendorItemAndVendorRel
							invoke FastUpdateWithoutEdits each
								invoked.UpdateVendorItem								= true
								invoked.NewVendorItem									= PrmReplacementVendorItem
								invoked.NewVendorItemDescription                        = PrmReplacementVendorItemDescription
								invoked.NewManufacturer									= PrmReplacementManufacturer
								invoked.NewManufacturerNumber							= PrmReplacementManufacturerNumber


							if (PrmUpdateAssociatedContracts)
								if (each.IsManufacturerContract)
									for each each.DistributorContractLineRel
										LocalVendor       = each.Contract.Vendor
										LocalVendorItem   = each.VendorItem
										invoke ChangeManufacturerInformation OtherVendorItemRel
											invoked.PrmManufacturer 		= PrmReplacementManufacturer
											invoked.PrmManufacturerNumber 	= PrmReplacementManufacturerNumber
								else
								if (each.IsDistributorContract)
									for each each.MfgContractLineRel
										LocalVendor       = each.Contract.Vendor
										LocalVendorItem   = each.VendorItem
										invoke ChangeManufacturerInformation OtherVendorItemRel
											invoked.PrmManufacturer 		= PrmReplacementManufacturer
											invoked.PrmManufacturerNumber 	= PrmReplacementManufacturerNumber

						for each ContractImportLinesForVendorItemRel
							invoke FastUpdateWithoutEdits each
								invoked.UpdateWithReplacementVendorItem		= true
								invoked.NewVendorItem						= PrmReplacementVendorItem
								invoked.NewManufacturer                     = PrmReplacementManufacturer
								invoked.NewManufacturerNumber               = PrmReplacementManufacturerNumber

						if (VendorItemBackorderDetailsRel exists
						and NewVendorItemBackorderDetailsRel  not exists)
							invoke FastUpdate VendorItemBackorderDetailsRel
								invoked.VendorItem 							= PrmReplacementVendorItem

	Action Exit Rules

		if  (ConsolidatedItemMasterExists
		and  not LocalFromItemInactivation
		and  not LocalFromItemActivation)
			invoke Update ConsolidatedItemMasterRel     								

