VendorAccountRestriction is a BusinessClass
	owned by ap
	prefix is VAR

	Ontology
		symbolic key is VendorAccountRestriction


	Persistent Fields
		AccountingEntity
			delete cascades
		AccountingUnit
			delete cascades
		GeneralLedgerChartAccount
			default label is "GlobalLedgerChartAccount"
			delete cascades
		Project
			delete cascades
		Dimension1						is a FinanceDimension1
			delete cascades
		Dimension2						is a FinanceDimension2
			delete cascades
		Dimension3						is a FinanceDimension3
			delete cascades
		Dimension4						is a FinanceDimension4
			delete cascades
		Dimension5						is a FinanceDimension5
			delete cascades
		Dimension6						is a FinanceDimension6
			delete cascades
		Dimension7						is a FinanceDimension7
			delete cascades
		Dimension8						is a FinanceDimension8
			delete cascades
		Dimension9						is a FinanceDimension9
			delete cascades
		Dimension10						is a FinanceDimension10
			delete cascades


	Field Rules
		AccountingEntity
			if (GeneralLedgerChartAccount not entered)
				required
					"AtMinimum,MustEnterEitherAnAccountOrEntity"

		GeneralLedgerChartAccount
			if (AccountingEntity not entered)
				required
					"AtMinimum,MustEnterEitherAnAccountOrEntity"


	Local Fields
		LocalAccount		is like GeneralLedgerChartAccount
		LocalEntity			is like AccountingEntity
		LocalUnit			is like AccountingUnit


	Derived Fields
		DerivedAccountLabel is a DerivedField
			type is Alpha 30
			return VendorGroup.BusinessGroup.FinanceEnterpriseGroup.AccountLabel

		DerivedUnitLabel is a DerivedField
			type is Alpha 30
			return VendorGroup.BusinessGroup.FinanceEnterpriseGroup.AccountingUnitLabel
			
		DerivedEntityLabel is a DerivedField
			type is Alpha 30
			return VendorGroup.BusinessGroup.FinanceEnterpriseGroup.AccountingEntityLabel


	Rule Blocks
		CreateUpdateRules
			if (GeneralLedgerChartAccount not entered)
				if (AccountingUnit entered)
					LocalAccount = blank
					LocalEntity = AccountingEntity
					LocalUnit = blank
					constraint (!VendorAccountRestrictionRel exists)
						"EntityExistsWithNoUnitRestrictions;CannotAddARestrictionUnlessTheOtherRecordIsDeletedOrChangedToHaveARestriction"
				else
					LocalAccount = blank
					LocalEntity = AccountingEntity
					constraint (!VendorAccountEntityWithUnitRel exists)
						"EntityExistsWithUnitRestrictions;CannotHaveOneWithNoRestriction"
			else
				if (AccountingEntity entered)
					if (AccountingUnit entered)
						LocalAccount	= GeneralLedgerChartAccount
						LocalEntity		= AccountingEntity
						LocalUnit		= blank
						constraint (!VendorAccountRestrictionRel exists)
							"AccountAndEntityExistsWithNoUnitRestrictions;CannotAddARestrictionUnlessTheOtherRecordIsDeletedOrChangedToHaveARestriction"
					else
						LocalAccount	= GeneralLedgerChartAccount
						LocalEntity		= AccountingEntity
						constraint (!VendorAccountEntityWithUnitRel exists)
							"AccountAndEntityExistsWithUnitRestrictions;CannotHaveOneWithNoRestriction"
							
						LocalAccount	= GeneralLedgerChartAccount
						LocalEntity		= blank
						LocalUnit		= blank
						constraint (!VendorAccountRestrictionRel exists)
							"AccountExistsWithNoEntityRestrictions;CannotAddARestrictionUnlessTheOtherRecordIsDeletedOrChangedToHaveARestriction"
				else
					LocalAccount		= GeneralLedgerChartAccount
					constraint (!VendorAccountWithEntityRel exists)
						"AccountExistsWithEntityRestrictions;CannotHaveOneWithNoRestriction"


	Conditions
		GeneralLedgerChartAccountEntered
			restricted
			when (GeneralLedgerChartAccount entered)
			
		AccountingEntityEntered
			restricted
			when (AccountingEntity entered)

		AccountingUnitEntered		
			restricted
			when (AccountingUnit entered)

		AccountingEntityEnteredAndNotHasAccountOnly		
			restricted
			when (AccountingEntity entered
			and  !HasAccountOnly)

		HasAccountOnly
			restricted
			when (GeneralLedgerChartAccount entered
			and   AccountingEntity not entered)
			
		HasEntityOnly
			restricted
			when (GeneralLedgerChartAccount not entered
			and   AccountingEntity entered
			and   AccountingUnit not entered)
			
		HasEntityUnit
			restricted
			when (GeneralLedgerChartAccount not entered
			and   AccountingEntity entered
			and   AccountingUnit entered)
			
		HasAccountEntityUnit
			restricted
			when (GeneralLedgerChartAccount entered
			and   AccountingEntity entered
			and   AccountingUnit entered)

    	RecordExists
    		restricted
    		when (VendorAccountRestriction exists)

	Relations
		VendorAccountRestrictionRel
			one-to-many relation to VendorAccountRestriction
			Field Mapping uses ByAccountEntityUnit
				related.VendorGroup					= VendorGroup
				related.Vendor						= Vendor
				related.GeneralLedgerChartAccount	= LocalAccount
				related.AccountingEntity			= LocalEntity
				related.AccountingUnit				= LocalUnit
			Instance Selection
				where (related.UniqueID != UniqueID)

		VendorAccountEntityWithUnitRel
			one-to-many relation to VendorAccountRestriction
			Field Mapping uses ByAccountEntity
				related.VendorGroup					= VendorGroup
				related.Vendor						= Vendor
				related.GeneralLedgerChartAccount	= LocalAccount
				related.AccountingEntity			= LocalEntity
			Instance Selection
				where (related.AccountingUnit entered
				and    related.UniqueID != UniqueID)

		VendorAccountWithEntityRel
			one-to-many relation to VendorAccountRestriction
			Field Mapping uses ByAccount
				related.VendorGroup					= VendorGroup
				related.Vendor						= Vendor
				related.GeneralLedgerChartAccount	= LocalAccount
			Instance Selection
				where (related.AccountingEntity entered
				and    related.UniqueID != UniqueID)


	Sets
		ByAccountEntityUnit
			Sort Order
				VendorGroup
				Vendor
				GeneralLedgerChartAccount
				AccountingEntity
				AccountingUnit
				
		ByAccountEntity
			duplicates
			Sort Order
				VendorGroup
				Vendor
				GeneralLedgerChartAccount
				AccountingEntity

		ByAccount
			duplicates
			Sort Order
				VendorGroup
				Vendor
				GeneralLedgerChartAccount

	Actions
		Create is a Create Action
			Action Rules
				include CreateUpdateRules
						
		Update is an Update Action
			Action Rules
				include CreateUpdateRules

		Delete is a Purge Action

