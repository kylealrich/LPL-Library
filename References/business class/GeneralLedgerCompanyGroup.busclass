GeneralLedgerCompanyGroup is a BusinessClass
	default label is "GlobalLedgerCompanyGroup"
    owned by glif
    prefix is GCG
    classic name is GLCPYGRP

    Ontology
        symbolic key is GeneralLedgerCompanyGroup
            classic set name is GCGSET1
            classic name is COMPANY-GROUP

    Patterns
        implements StaticJava
        disable AuditIndex

	Context Fields
		FinanceEnterpriseGroup
		
    Persistent Fields

        Description  is Alpha size 30
        	translatable
        LastSequence is Numeric size 4
            classic name is LST-SEQ-NBR
		CompanyList				is Text  
		
    Derived Fields

        ShortDesc is a StringField
            type is Alpha size 15
            restricted
            Description

		RepresentativeText is a StringField
			type is Text
			default label is "CompanyGroup"
			GeneralLedgerCompanyGroup " - " Description
            
    Local Fields
		LocalPayablesCompany		is like Company

	Conditions
		
		UsedInASecurityGroup
			when (SecurityGroupRel exists)
			
		SecurityGroupAllowsAccess
			when (actor.context.CompanySecurityGroup = blank
			or    actor.context.CompanySecurityGroup.FinanceDimensionStructure = GeneralLedgerCompanyGroup)

#ifdef module bl
        CompanyMemberIsUsingIDM
        	restricted
            when (BillingCompanyUsingIDMRel exists)
#endif

    Relations

        GeneralLedgerCompanyGroupMembersRel
            one-to-many relation to GeneralLedgerCompanyGroupMember
            delete cascades
            Field Mapping uses Set2
                related.GeneralLedgerCompanyGroup = GeneralLedgerCompanyGroup

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = actor.context.FinanceEnterpriseGroup
				related.DimensionGroupType			  = 2
				related.FinanceDimensionStructure	  = GeneralLedgerCompanyGroup
				
		ICSystemClosingControlCompaniesWithControlRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "IC"
			Instance Selection
				where (related.Control 	= true
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		ICSystemClosingControlCompaniesWithOpenPeriodRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "IC"
			Instance Selection
				where (related.CurrentPeriodNotClosed
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		POSystemClosingControlCompaniesWithControlRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "PO"
			Instance Selection
				where (related.Control 	= true
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		POSystemClosingControlCompaniesWithOpenPeriodRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "PO"
			Instance Selection
				where (related.CurrentPeriodNotClosed
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		BLSystemClosingControlCompaniesWithControlRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "BL"
			Instance Selection
				where (related.Control 	= true
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

#ifdef module bl
		BillingCompanyUsingIDMRel
			one-to-many relation to BillingCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.UseIDM
				and    related.Company = any GeneralLedgerCompanyGroupMembersRel.Company)
#endif
		BLSystemClosingControlCompaniesWithOpenPeriodRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "BL"
			Instance Selection
				where (related.CurrentPeriodNotClosed
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		FRSystemClosingControlCompaniesWithControlRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "FR"
			Instance Selection
				where (related.Control 	= true
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		FRSystemClosingControlCompaniesWithOpenPeriodRel
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "FR"
			Instance Selection
				where (related.CurrentPeriodNotClosed
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)
				
		CBSystemClosingControlCompaniesWithControlRel	
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "CB"
			Instance Selection
				where (related.Control 	= true
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		CBSystemClosingControlCompaniesWithOpenPeriodRel	
			one-to-many relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "CB"
			Instance Selection
				where (related.CurrentPeriodNotClosed
				and    related.Company	= any GeneralLedgerCompanyGroupMembersRel.Company)

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= LocalPayablesCompany
				

	Field Rules
		Description
			required

	Actions
		Create is an Action
		Update is an Action
		Delete is an Action
		
		BuildCompanyList is an Instance Action 
		    restricted
		    Local Fields
		        FirstCompany is Boolean
		    Action Rules
		        initialize CompanyList
		        FirstCompany = true
		        for each GeneralLedgerCompanyGroupMembersRel
		            if (not FirstCompany)
		                CompanyList += ","
		            else
		                FirstCompany = false
		            CompanyList += each.Company

		RebuildCompanyLists is a Set Action  
		    restricted
		    Parameters
		    Action Rules
		        Instance Rules
		            invoke BuildCompanyList

		CopyMembers is an Instance Action
			Parameters
				CopyToGroup is an GeneralLedgerCompanyGroup
				
			Parameter Rules
			
				CopyToGroup
					constraint (CopyToGroup != GeneralLedgerCompanyGroup)
						"CannotCopyAGroupToItself"
						
			Action Rules
				for each GeneralLedgerCompanyGroupMember set
					invoke Create GeneralLedgerCompanyGroupMember
						resume on error
						fill in fields from each
							except invoked.GeneralLedgerCompanyGroup
						invoked.GeneralLedgerCompanyGroup = CopyToGroup
					commit transaction				

#ifdef module ap
		UpdateReconciliationBalance is an Instance Action
			restricted
			Parameters
				PrmReconciliationDate		is Date
				PrmReportDocument			is UniqueID
				PrmSystemCode				is like GeneralLedgerSystemCode
				PrmAccrualCode				is AlphaUpper size 10
				PrmSecondarySortCriteria	is AlphaUpper size 25
				
			Action Rules
				for each GeneralLedgerCompanyGroupMembersRel
					LocalPayablesCompany = each.Company
					invoke UpdateReconciliationBalance GeneralLedgerCompanyRel
						invoked.PrmReconciliationDate	= PrmReconciliationDate
						invoked.PrmReportDocument		= PrmReportDocument
						invoked.PrmSystemCode			= PrmSystemCode
						invoked.PrmAccrualCode			= PrmAccrualCode
						invoked.PrmSecondarySortCriteria = PrmSecondarySortCriteria
#endif			

		SendNotificationForEmptyReceivableStatement is an Instance Action
			restricted
			Local Fields
				LocalActor							is an Actor
			Action Rules
				LocalActor = actor
			
				send notification
					to LocalActor
					description is "NoRecordsToProcessForReceivablesStatementForCompanyGroup_<GeneralLedgerCompanyGroup>"
					priority is high
					detail is "NoRecordsToProcessForReceivablesStatement.PleaseCheckTheInvoicesAndParametersToProcessAndRunTheBatchProgramAgain."
