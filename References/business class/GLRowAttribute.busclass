GLRowAttribute is a BusinessClass
	default label is "GLRowAttribute"
    owned by GeneralLedger
    prefix is GLRDA

    Ontology
    	symbolic key is GLRowAttribute

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByGLRowDimension

    Persistent Fields
		ColumnHeader		is Alpha 100
		ColumnSize			is Numeric 3
		DisplayOrder		is Numeric 2
			disable Auditing
		BirstFieldNumber	is Numeric 4

	Transient Fields
		NewDisplayOrder		is Numeric 2
		BirstModel
			derive value from BirstModelBCRel.BirstModel
		ReplicationSet
			derive value from BirstModelBCRel.ReplicationSet
		RepSetBC
			derive value from BirstModelBCRel.RepSetBC
		BirstModelBC
			derive value from BirstModelBCRel.BirstModelBC
		BirstModelBCField
			derive value from BirstModelBCField2Rel.BirstModelBCField

	Field Rules
		DisplayOrder
			autosequence using ByGLRowDimension
		ColumnHeader
			default to BirstModelBCField.BirstFieldName
		ColumnSize
			default to 10

	Conditions
		AddAttributeIsAllowed
			when (GLRowDimension.PostingLevelOnly = true
			and GLRowAttributeExistsRel not exists)
		
		IsAttribute
			when (BirstModelBCFieldRel.IsAttribute)

		RecordExists
			restricted
			when (GLRowAttribute exists)

	Local Fields
		LocalRowDimension	is LPLName

	Rule Blocks
		UpdateExitRules
			if (IsRowAttributeUpdated)
				if (GLReportRel exists)
					for each GLReportRel
						if (each.IntegrationStatus = 1)
							invoke UpdateGLReport each
								invoked.PrmIntegrationStatus = 2
				if(SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2
		
	Derived Fields
		DerivedEmptyDataMessage		is a DerivedField
			type is MessageField
			if (GLRowAttributeExistsRel exists)
				return AttributeAlreadyExistsMessage
			else
			if (!GLRowDimension.PostingLevelOnly)
				return NoAttributeAllowedMessage
			else
			if (GLRowAttributeRel not exists)
				return NoDataMassage
		
		NoDataMassage	is a MessageField
			restricted
			"Add_attribute(s)_to_the_row_dimension"
		
		AttributeAlreadyExistsMessage	is a MessageField
			restricted
			"Attributes_are_already_associated_with_another_Dimension"

		NoAttributeAllowedMessage	is a MessageField
			restricted
			"Attributes_cannot_be_added_to_a_Summary_Level_Row_Dimension"

		DerivedNoDataDescription	is a DerivedField
			type is MessageField
			if (GLRowAttributeRel not exists
			and GLRowDimension.PostingLevelOnly)
				return NoDataDescriptionMessage
			else
				return ""

		NoDataDescriptionMessage	is a MessageField
			restricted
			"Select_attributes_to_display_as_rows_on_the_report."

		DerivedDimension is a DerivedField
			type is LPLName
			restricted
			initialize LocalRowDimension
			if (GLRowDimension.Dimension = "GeneralLedgerTotal.AccountingUnit")
				LocalRowDimension = "AccountingUnit"
			if (GLRowDimension.Dimension = "GeneralLedgerTotal.GeneralLedgerChartAccount")
				LocalRowDimension = "GeneralLedgerChartAccount"
			if (GLRowDimension.Dimension = "GeneralLedgerTotal.Project")
				LocalRowDimension = "Project"
			if (GLRowDimension.Dimension = "AccountingEntity")
				LocalRowDimension = "AccountingEntity"
			if (GLRowDimension.Dimension = "FinanceDimension1")
				LocalRowDimension = "FinanceDimension1"
			if (GLRowDimension.Dimension = "FinanceDimension2")
				LocalRowDimension = "FinanceDimension2"
			if (GLRowDimension.Dimension = "FinanceDimension3")
				LocalRowDimension = "FinanceDimension3"
			if (GLRowDimension.Dimension = "FinanceDimension4")
				LocalRowDimension = "FinanceDimension4"
			if (GLRowDimension.Dimension = "FinanceDimension5")
				LocalRowDimension = "FinanceDimension5"
			if (GLRowDimension.Dimension = "FinanceDimension6")
				LocalRowDimension = "FinanceDimension6"
			if (GLRowDimension.Dimension = "FinanceDimension7")
				LocalRowDimension = "FinanceDimension7"
			if (GLRowDimension.Dimension = "FinanceDimension8")
				LocalRowDimension = "FinanceDimension8"
			if (GLRowDimension.Dimension = "FinanceDimension9")
				LocalRowDimension = "FinanceDimension9"
			if (GLRowDimension.Dimension = "FinanceDimension10")
				LocalRowDimension = "FinanceDimension10"
			if (GLRowDimension.Dimension = "Scenario")
				LocalRowDimension = "Scenario"
			return LocalRowDimension

		DerivedAttributeName is a DerivedField
			type is Alpha size 300
			restricted
			return BirstModelBCField.BirstFieldName

		DerivedAttributeDataType is a DerivedField
			type is Alpha size 300
			restricted
			return BirstModelBCField.DataType

		IsRowAttributeUpdated is a DerivedField
			type is Boolean
			restricted
			if(ColumnHeader changed
			or BirstModelBCField changed
			or ColumnSize changed
			or DisplayOrder changed
			or NewDisplayOrder changed)
				return true
			else
				return false

	Actions
		Create is a Create Action
			Field Rules
				BirstModelBCField
					required
			Entrance Rules
				if (GLRowView.Lock)	
					constraint(GLRowView.DerivedCreatedBy = actor)
						"ThisHasBeenLockedForEditing"
				constraint (BirstModelBCFieldNotAttributeCreateRel exists)
					"CannotAddAnInvalidAttribute,PleaseSelectFromTheList"
				constraint (!GLRowAttributeDuplicateRel exists)
					"AttributeAlreadyExist"
			Exit Rules
				BirstFieldNumber = BirstModelBCField
				if (GLReportRel exists)
					for each GLReportRel
						if (each.IntegrationStatus = 1)
							invoke UpdateGLReport each
								invoked.PrmIntegrationStatus = 2
				
				if(SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2
		
		Update is an Update Action
			Field Rules
				BirstFieldNumber
					if (BirstFieldNumber != BirstModelBCField)
						BirstFieldNumber = BirstModelBCField
			Entrance Rules
				if (GLRowView.Lock)
					constraint(create stamp.actor = actor)
						"ThisHasBeenLockedForEditing"
				constraint (BirstModelBCFieldNotAttributeUpdateRel exists)
					"CannotAddAnInvalidAttribute,PleaseSelectFromTheList"
				constraint (!GLRowAttributeDuplicateRel exists)
					"AttributeAlreadyExist"
			Exit Rules
				include UpdateExitRules
		
		Delete is a Delete Action
			Entrance Rules
				if (GLRowView.Lock)	
					constraint(GLRowView.DerivedCreatedBy = actor)
						"ThisHasBeenLockedForEditing"
			Exit Rules
				if (GLReportRel exists)
					for each GLReportRel
						if (each.IntegrationStatus = 1)
							invoke UpdateGLReport each
								invoked.PrmIntegrationStatus = 2
				
				if(SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2

	Relations
		LocalRepSetBCRel
			one-to-many relation to RepSetBC
			Field Mapping uses ByBusinessClass
				related.ReplicationSet					= "FSM_COLUMNAR_GL_ST"
				related.BusinessClass					= DerivedDimension

		BirstModelBCFieldRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByForeignKey
				related.BirstModel					= "CSFLA"	
				related.ReplicationSet				= "FSM_COLUMNAR_GL_ST"
				related.RepSetBC					= first LocalRepSetBCRel.RepSetBC
				related.IncludeInBirstModel			= true
			Instance Selection
				where (related.IsAttribute)

		BirstModelBCFieldNotAttributeUpdateRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByForeignKey
				related.BirstModel					= "CSFLA"	
				related.ReplicationSet				= "FSM_COLUMNAR_GL_ST"
				related.RepSetBC					= first LocalRepSetBCRel.RepSetBC
				related.IncludeInBirstModel			= true
			Instance Selection
				where (related.BirstModelBCField	= BirstFieldNumber
				and	  related.IsAttribute)

		BirstModelBCFieldNotAttributeCreateRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByForeignKey
				related.BirstModel					= "CSFLA"	
				related.ReplicationSet				= "FSM_COLUMNAR_GL_ST"
				related.RepSetBC					= first LocalRepSetBCRel.RepSetBC
				related.IncludeInBirstModel			= true
			Instance Selection
				where (related.BirstModelBCField	= BirstModelBCField
				and	  related.IsAttribute)

		BirstModelBCField2Rel
			one-to-one relation to BirstModelBCField
			Field Mapping uses symbolic key
				related.BirstModel				= "CSFLA"
				related.ReplicationSet			= "FSM_COLUMNAR_GL_ST"
				related.RepSetBC				= first LocalRepSetBCRel.RepSetBC
				related.BirstModelBCField		= BirstFieldNumber
	
		BirstModelBCRel
			one-to-one relation to BirstModelBC
			Field Mapping uses symbolic key
				related.BirstModel				= "CSFLA"	
				related.ReplicationSet			= "FSM_COLUMNAR_GL_ST"
				related.RepSetBC				= first LocalRepSetBCRel.RepSetBC

		GLRowAttributeDuplicateRel
			one-to-many relation to GLRowAttribute
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.GLRowView				= GLRowView
				related.GLRowDimension			= GLRowDimension
			Instance Selection
				where (related.BirstModelBCField	= BirstModelBCField
				and	   related.UniqueID				!= UniqueID)

		GLRowAttributeExistsRel
			one-to-many relation to GLRowAttribute
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.GLRowView			    	= GLRowView
			Instance Selection
				where (related.GLRowDimension				!= GLRowDimension)

		GLRowAttributeRel
			one-to-many relation to GLRowAttribute
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.GLRowView			    	= GLRowView
	
		GLReportRel
			one-to-many relation to GLReport
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.RowView		    = GLRowView)

		SubReportRel
			one-to-many relation to GLReportElement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.SubReport.RowView		= GLRowView)
	
	Sets
		ByGLRowDimension
			Sort Order
				FinanceEnterpriseGroup
				GLRowView
				GLRowDimension
				DisplayOrder
				GLRowAttribute

		ByDisplayOrder
			Sort Order
				FinanceEnterpriseGroup
				GLRowView
				DisplayOrder
				GLRowDimension
				GLRowAttribute
