AnalyticsCopyToAnotherTenant is a BusinessClass
	owned by filecreation
	
	prefix is ANCTT
	
	Ontology
		symbolic key is AnalyticsCopyToAnotherTenant
		
	Patterns
		disable AsOfDateProcessing
        disable EffectiveDated
	
	Persistent Fields
		TargetEnvironmentType				is Alpha size 300
			States
			 	ActiveSpacesWithDowntime 	value is 1
			 	StagingEnvironment			value is 2
		CopyProcessCompleted				is Boolean
		CurrentStep							is Alpha size 6
		Status 								is Numeric size 1
			States
				InProgress  value is 1
				Completed	value is 2
				Canceled	value is 3
		UpgradeTenantToTenantWorkunit 		is a PfiWorkunit
		PreUpgradeStepsCompleted			is Boolean
		SourceTenantID						is Alpha size 300
		SourceEnvironmentType				is Alpha size 3 
		TargetEnvironment					is Alpha size 5
		TargetTenantID						is Alpha size 300
		TargetTenantBirstUserID				is Alpha size 300
		IONApiUrlTarget						is Alpha size 300
	
	Field Rules
		
		Status
			default to Status.InProgress
			if(CopyProcessCompleted)
				Status=Status.Completed
	
		CopyProcessCompleted
			if(CopyProcessCompleted)
				constraint (last LatestUpgradeTenantToTenantRel exists and last LatestUpgradeTenantToTenantRel.Status entered )
					"CannotProceedFurtherWithoutRunningTheFlow"
				constraint (last LatestUpgradeTenantToTenantRel.Status.Completed)
					"WorkUnitStatusShouldBeCompleted"
		
		CurrentStep
			default to "0/3"
			if(AnalyticsConfigParaTargetTenantIDRel exists)
				CurrentStep="1/2"
			if(CopyProcessCompleted )
				CurrentStep="2/2"
			
			
	Derived Fields
	
		DerivedUpgradeTenantToTenantServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			return "AnalyticsUpgradeTenantToTenant"
		
		DerivedProductVersionSMDDef is a DerivedField
			type is Alpha size 200
			restricted
			return AnalyticsConfigParaTenantIDRel.Value+"-SMD-CSF-Consumer"

		DerivedProductVersionSTCDef is a DerivedField
			type is Alpha size 200
			restricted
			return AnalyticsConfigParaTenantIDRel.Value+"-STC-CSF-Consumer"

		DerivedProductVersionATCDef is a DerivedField
			type is Alpha size 200
			restricted
			return AnalyticsConfigParaTenantIDRel.Value+"-CSF-Consumer"

		CancelMessage is a MessageField
            restricted
            "ThisActionWillCancelTheProcess.DoYouWantToContinue?"

		DerivedUpdateIONSchemasServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsUpdateIONSchemasServiceDefRel.Value not entered)
				return "UpdateIONSchemas"
			else
				return AnalyticsUpdateIONSchemasServiceDefRel.Value

		DerivedDataFlowToAnalyticsFullLoadServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsDataFlowToAnalyticsFullLoadServiceDefRel.Value not entered)
				return "DataFlowToAnalyticsFullLoad"
			else
				return AnalyticsDataFlowToAnalyticsFullLoadServiceDefRel.Value
 
		DerivedDataFlowToAnalyticsIncrementalLoadServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsDataFlowToAnalyticsIncrementalLoadServiceDefRel.Value not entered)
				return "DataFlowToAnalyticsIncrementalLoad"
			else
				return AnalyticsDataFlowToAnalyticsIncrementalLoadServiceDefRel.Value

		AnalyticsLibraryURL is a DerivedField
			type is URL
			return "https://docs.infor.com/ifsm/latest/en-us/ifsmanalyticslib/default.html?helpcontent=ifsmanalyticsag/ycb1691994681148.html"
			
	Conditions
			
		IsStatusCompleted
			when(CopyProcessCompleted entered)

		IsDisableButtonText
			when (last AnalyticsCopyToAnotherTenantRel.Status.InProgress)

		IsUpgradeCompleted
			when (ProductVersionSMDRel.Value>ProductVersionATCRel.Value 
			and ProductVersionATCRel exists 
			and ProductVersionSMDRel exists 
			and ProductVersionSMDRel.Value != "missing"
			and ProductVersionATCRel.Value != "missing" 
			and UpdateNotRequired=false)

		UpdateNotRequired
			when(ProductVersionATCRel.Value contains "11.0.7")

		IsValuesNotSetUp
			when(AnalyticsConfigParaTargetSpaceTypeRel not exists 
			or AnalyticsConfigParaSourceSpaceTypeRel not exists 
			or AnalyticsConfigParaTargetTenantIDRel not exists 
			or AnalyticsConfigParaTargetTenantBirstUserIDRel not exists
			or AnalyticsConfigParaIONApiUrlTargetRel not exists)

		IsEnableButton
			when (not last AnalyticsCopyToAnotherTenantRel.Status.InProgress
			and not IsValuesNotSetUp
			and not ProductVersionATCRel.UpdateNotRequired
			and not last AnalyticsUpgradeStagingRel.Status.InProgress
			and not last AnalyticsUpgradeActiveSpaceRel.Status.InProgress
			and not last PostPfiWorkunitsRel.Status.Processing 
			and not last DataFlowToAnalyticsFLRel.Status.Processing 
			and not last DataFlowToAnalyticsILRel.Status.Processing)

		IsStagingProcessRunning
			when (last AnalyticsUpgradeStagingRel.Status.InProgress)

		IsActiveSpaceProcessRunning
			when (last AnalyticsUpgradeActiveSpaceRel.Status.InProgress)

		IsCopyConsumerCustomizationWorkunitExists
			when(UpgradeTenantToTenantWorkunit > 0)

	Relations	
		AnalyticsCopyToAnotherTenantRel		
			one-to-many relation to AnalyticsCopyToAnotherTenant
			Field Mapping uses symbolic key
			
		AnalyticsConfigParaTenantIDRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "TenantID"
				
		ProductVersionSMDRel
			one-to-many relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
			Instance Selection
				where (related.AnalyticsConsoleConfigurations = DerivedProductVersionSMDDef)

		ProductVersionSTCRel
			one-to-many relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
			Instance Selection
				where (related.AnalyticsConsoleConfigurations = DerivedProductVersionSTCDef)

		ProductVersionATCRel
			one-to-many relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
			Instance Selection
				where (related.AnalyticsConsoleConfigurations = DerivedProductVersionATCDef)
		
		UpgradeTenantToTenantPfiTriggerRel 
			one-to-many relation to PfiTrigger
			Field Mapping uses ByServiceName
				related.ServiceName=DerivedUpgradeTenantToTenantServiceDef
				
		UpgradeTenantToTenantRel
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition=DerivedUpgradeTenantToTenantServiceDef

		AnalyticsConfigParaTargetTenantIDRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "TargetTenantID"

		AnalyticsConfigParaTargetTenantBirstUserIDRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "TargetTenantBirstUserID"

		AnalyticsConfigParaIONApiUrlTargetRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "IONApiUrlTarget"	

		PfiTaskRel
			one-to-one relation to PfiTask
			Field Mapping uses symbolic key
				related.PfiTask.TaskName="BirstSystemAdmin"
				related.PfiTask.TaskType=PfiTaskType.Group
		
		PfiUserCategoryRel
            one-to-many relation to PfiUserTask
            Field Mapping uses ByTaskNameTaskTypeUserProfile
				related.PfiTask.TaskName="BirstSystemAdmin"
				related.PfiTask.TaskType =PfiTaskType.Group 

		AnalyticsConfigParaTargetSpaceTypeRel
			one-to-one relation to AnalyticsConsoleConfigurations
            Field Mapping uses symbolic key
                related.AnalyticsConsoleConfigurations = "TargetSpaceType"
 
		AnalyticsConfigParaSourceSpaceTypeRel
			one-to-one relation to AnalyticsConsoleConfigurations 
            Field Mapping uses symbolic key
                related.AnalyticsConsoleConfigurations = "SourceSpaceType"

		AnalyticsUpgradeStagingRel		
			one-to-many relation to AnalyticsUpgradeStaging
			Field Mapping uses symbolic key
			
		AnalyticsUpgradeActiveSpaceRel		
			one-to-many relation to AnalyticsUpgradeActiveSpace
			Field Mapping uses symbolic key

		AnalyticsUpdateIONSchemasServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "UpdateIONSchemasServiceDef"

		AnalyticsDataFlowToAnalyticsFullLoadServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "DataFlowToAnalyticsFullLoadServiceDef"

		AnalyticsDataFlowToAnalyticsIncrementalLoadServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "DataFlowToAnalyticsIncrementalLoadServiceDef"

		LatestUpgradeTenantToTenantRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedUpgradeTenantToTenantServiceDef
				related.PfiWorkunit = UpgradeTenantToTenantWorkunit

		PostPfiWorkunitsRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedUpdateIONSchemasServiceDef

		DataFlowToAnalyticsFLRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedDataFlowToAnalyticsFullLoadServiceDef

		DataFlowToAnalyticsILRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedDataFlowToAnalyticsIncrementalLoadServiceDef

		
		
	Actions
		
		Create is an Action
			Action Rules
				SourceTenantID=AnalyticsConfigParaTenantIDRel.Value
				SourceEnvironmentType=AnalyticsConfigParaSourceSpaceTypeRel.Value
				TargetTenantID=AnalyticsConfigParaTargetTenantIDRel.Value
				TargetTenantBirstUserID	=AnalyticsConfigParaTargetTenantBirstUserIDRel.Value			
				IONApiUrlTarget=AnalyticsConfigParaIONApiUrlTargetRel.Value
				TargetEnvironment=AnalyticsConfigParaTargetSpaceTypeRel.Value
		
		Update is an Action
		
		Delete is an Action
		
		StartUpgradeTenant is a Set Action
			no records message is "Started"
			restricted
			run in foreground
			Instance Selection
				where(false)
			Action Rules
				Empty Set Rules
					constraint(PfiTaskRel exists)
						"Birst_System_Admin_has_not_yet_created._Please_click_the_Create_Birst_System_Admin_task_group_first."
					constraint(PfiUserCategoryRel exists)
						"BirstSystemAdminHasNoUserAdded,PleaseClickThe_Add_UserIn_Birst_System_Admin_GroupAndAssignAtLeastOneUser."
					constraint(UpgradeTenantToTenantPfiTriggerRel exists)
						"<DerivedUpgradeTenantToTenantServiceDef>IsNotYetConfigured.PleaseClickThe_Create_Service_Trigger_In_Integration_Architect_Analytics_Console_Dashboard."
					invoke Start UpgradeTenantToTenantPfiTriggerRel
					invoke Update last AnalyticsCopyToAnotherTenantRel
						invoked.UpgradeTenantToTenantWorkunit=UpgradeTenantToTenantPfiTriggerRel.TriggeredWorkunit
		
		UpdateVersion is a Set Action
			no records message is "ProductVersionsHaveBeenUpdatedSuccessfully"
			run in foreground
			Instance Selection
				where(false)
			Action Rules
				Empty Set Rules
					invoke UpdateProductVersion AnalyticsConsoleConfigurations

		Cancel is an Instance Action
			confirmation required
				"<CancelMessage>"
			completion message is "TheProcessHasBeenCanceledSuccessfully"
			Action Rules
           		invoke Update 
           			invoked.Status=Status.Canceled

