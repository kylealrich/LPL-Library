MinorClass is a BusinessClass
    owned by ic
    prefix is MIN
    classic name is MINORCL

    Ontology
        symbolic key is MinorClass
            classic set name is MINSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        Active      is Boolean
            classic name is ACTIVE-STATUS

	Local Fields
		LocalContractTierGroup
		LocalContractImportTierGroup
		LocalContractRebate
	
	Attach Rules
		if (not BypassRuleForTheseFiles)
			constraint (Active)
				"MinorClass<MinorClass>MustBeActive"
			
    Conditions

        IsActive
        	restricted
            when (Active)

        IsInactive
        	restricted
            when (not Active)
		
		BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "PurchaseOrderEDIOutput")

	Derived Fields
		ClassNameDescription is a MessageField
			default label is "MinorClass"
			"<Description>_(<MinorClass>)"

    Relations
    
    	ContractTierQualifierCodeRel
    		one-to-many relation to ContractTierQualifierCode
            Field Mapping uses  symbolic key
          		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
           		related.Contract			  = LocalContractTierGroup.Contract
           		related.ContractTier		  = LocalContractTierGroup.ContractTier
           		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
           	Instance Selection
           		where (related.MinorPurchasingClass = MinorClass
           		and    related.MajorPurchasingClass = MajorClass)
    	
    	MajorOnlyTierQualifierCodeRel
    		one-to-many relation to ContractTierQualifierCode
            Field Mapping uses  symbolic key
          		related.ContractGroup		  = LocalContractTierGroup.ContractGroup
           		related.Contract			  = LocalContractTierGroup.Contract
           		related.ContractTier		  = LocalContractTierGroup.ContractTier
           		related.ContractTierQualifier = LocalContractTierGroup.ContractTierQualifier
           	Instance Selection
           		where (related.MinorPurchasingClass = blank
           		and    related.MajorPurchasingClass = MajorClass)
    	
    	ContractImportTierQualifierCodeRel
    		one-to-many relation to ContractImportTierQualCode
            Field Mapping uses  symbolic key
          		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
           		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
           		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
           		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
           	Instance Selection
           		where (related.MinorPurchasingClass = MinorClass
           		and    related.MajorPurchasingClass = MajorClass)
    	
    	MajorOnlyImportTierQualifierCodeRel
    		one-to-many relation to ContractImportTierQualCode
            Field Mapping uses  symbolic key
          		related.ContractGroup		        = LocalContractImportTierGroup.ContractGroup
           		related.ContractImport			    = LocalContractImportTierGroup.ContractImport
           		related.ContractImportTier		    = LocalContractImportTierGroup.ContractImportTier
           		related.ContractImportTierQualifier = LocalContractImportTierGroup.ContractImportTierQualifier
           	Instance Selection
           		where (related.MinorPurchasingClass = blank
           		and    related.MajorPurchasingClass = MajorClass)

    	ContractRebateQualifierCodeRel
    		one-to-many relation to ContractRebateQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		    = LocalContractRebate.ContractGroup
           		related.Contract			    = LocalContractRebate.Contract
           		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
           		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
           	Instance Selection
           		where (related.MinorPurchasingClass = MinorClass
           		and    related.MajorPurchasingClass = MajorClass)
           		
    	MajorOnlyRebateQualifierCodeRel
    		one-to-many relation to ContractRebateQualifierCode
            Field Mapping uses symbolic key
          		related.ContractGroup		    = LocalContractRebate.ContractGroup
           		related.Contract			    = LocalContractRebate.Contract
           		related.ContractRebateType		= LocalContractRebate.ContractRebateType
				related.ContractRebate			= blank
           		related.ContractRebateQualifier = LocalContractRebate.ContractRebateQualifier
           	Instance Selection
           		where (related.MinorPurchasingClass = blank
           		and    related.MajorPurchasingClass = MajorClass)
           		
    
    Field Rules

        Active
            initial value is true

        Description
            required

		MinorClass
			constraint (!MajorClass.IsFreightClass)
				"MinorClassCannotBeDefinedForFreightClass"

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
		
		AttachToTierQualifier is an Instance Action
			Parameters
				ContractGroup
				Contract
				ContractTier
				ContractTierQualifier 
			Parameter Rules
				ContractGroup 
					required
				Contract
					required
				ContractTier
					required
				ContractTierQualifier
					required
			
			Action Rules
				constraint (Contract.NotClosed)
    				"CannotAdd;ContractIsClosed"		
				LocalContractTierGroup.ContractGroup 		 = ContractGroup
				LocalContractTierGroup.Contract 			 = Contract
				LocalContractTierGroup.ContractTier 		 = ContractTier
				LocalContractTierGroup.ContractTierQualifier = ContractTierQualifier
    			constraint (!MajorOnlyTierQualifierCodeRel exists)
    				"CannotAddMinorClass;TierQualifierCodeRecordAlreadyExistsForTheEntireMajorPurchasingClass"
				if (!ContractTierQualifierCodeRel exists)
					invoke Create ContractTierQualifierCode
						invoked.Contract			  = Contract
						invoked.ContractGroup		  = ContractGroup
						invoked.ContractTier		  = ContractTier
						invoked.ContractTierQualifier = ContractTierQualifier
						invoked.MajorPurchasingClass  = MajorClass
						invoked.MinorPurchasingClass  = MinorClass
						invoked.ItemCategory		  = 3
		
    	AttachToImportTierQualifier is an Instance Action
			Parameters
				ContractGroup
				ContractImport
				ContractImportTier
				ContractImportTierQualifier 
			Parameter Rules
				ContractGroup 
					required
				ContractImport
					required
				ContractImportTier
					required
				ContractImportTierQualifier
					required
			
			Action Rules
				LocalContractImportTierGroup.ContractGroup 		 = ContractGroup
				LocalContractImportTierGroup.ContractImport 			 = ContractImport
				LocalContractImportTierGroup.ContractImportTier 		 = ContractImportTier
				LocalContractImportTierGroup.ContractImportTierQualifier = ContractImportTierQualifier
    			constraint (!MajorOnlyImportTierQualifierCodeRel exists)
    				"CannotAddMinorClass;TierQualifierCodeRecordAlreadyExistsForTheEntireMajorPurchasingClass"
				if (!ContractImportTierQualifierCodeRel exists)
					invoke Create ContractImportTierQualCode
						invoked.ContractImport			    = ContractImport
						invoked.ContractGroup		        = ContractGroup
						invoked.ContractImportTier		    = ContractImportTier
						invoked.ContractImportTierQualifier = ContractImportTierQualifier
						invoked.MajorPurchasingClass        = MajorClass
						invoked.MinorPurchasingClass        = MinorClass
						invoked.ItemCategory		        = 3
    	
    	AttachToRebateQualifier is an Instance Action
			Parameters
				ContractGroup
				Contract
   				ContractRebateType			
   				ContractRebate				
   				ContractRebateQualifier
			Parameter Rules
				ContractGroup 
					required
				Contract
					required
				ContractRebateType
					required
				ContractRebate
					required
				ContractRebateQualifier
					required
			
			Action Rules
    			constraint (Contract.CanAmend)
    				"ContractMustBeInDraftOrAddendumOrAmendmentStatusToModifyRebateQualifiers"					
    			constraint (ContractRebate.Active)
    				"ContractRebateMustBeActiveToAddRebateQualifierCodes"
    			constraint (!Contract.ServicePurchaseType)
					"RebateQualifierCodesCannotBeEnteredForServiceContracts"    				
				LocalContractRebate.ContractGroup 		    = ContractGroup
				LocalContractRebate.Contract	            = Contract
				LocalContractRebate.ContractRebateType      = ContractRebateType
				LocalContractRebate.ContractRebateQualifier = ContractRebateQualifier			
    			constraint (!MajorOnlyRebateQualifierCodeRel exists)
    				"CannotAddMinorClass;RebateQualifierCodeRecordAlreadyExistsForTheEntireMajorPurchasingClass"
				if (!ContractRebateQualifierCodeRel exists)
					invoke Create ContractRebateQualifierCode
						invoked.ContractGroup		    = ContractGroup
						invoked.Contract			    = Contract
						invoked.ContractRebateType		= ContractRebateType
						invoked.ContractRebateQualifier = ContractRebateQualifier
						invoked.MajorPurchasingClass    = MajorClass
						invoked.MinorPurchasingClass    = MinorClass
						invoked.ItemCategory		    = 3
