ContractNotificationEmailAttachment is a BusinessClass
	owned by po
	prefix is CNEA
	sql name is CMEmailAttachment
	
	Ontology
		symbolic key is ContractNotificationEmailAttachment
		
	Patterns

	Persistent Fields
		ContractRedlineDocument               is Numeric size 3      
		ContractDocument                      is Numeric size 3      
		SequenceNumber                        is Numeric size 4      
		LineNumber                            is Numeric size 6
		ContractCommentType                   is Numeric size 2
		DocumentVersion		  				  is Numeric size 3		 
		IDMUniqueId							  is an IDMPID			 
			protected
			restricted 
		ContractRedlineDocumentTitle		  is like Description

	Conditions
	
		ForContractDocument
			restricted  
			when (ContractDocumentRel exists
			and   not ContractDocumentRel.IsAddendumDocument)
			
		ForRedlineDocument
			restricted
			when (ContractRedlineDocument entered)
			
		LineNumberEntered
			restricted
			when (LineNumber entered)
			
		ForHeaderComment
			restricted
			when (LineNumber !entered
			and   SequenceNumber entered)
			
		ForAddendumDocument
			restricted
			when (ContractDocumentRel exists
			and   ContractDocumentRel.IsAddendumDocument)
	
	Relations
		ContractRedlineDocumentRel
			one-to-one relation to ContractRedlineDocument
			Field Mapping uses symbolic key
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.ContractRedlineDocument    = ContractRedlineDocument
				
		ContractCommentRel
			one-to-one relation to ContractComment
			Field Mapping uses BySequenceNumber
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.ContractCommentType        = ContractCommentType
				related.SequenceNumber             = SequenceNumber
				
		ContractLineCommentRel
			one-to-one relation to ContractLineComment
			Field Mapping uses BySequenceNumber
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.ContractLine			   = LineNumber
				related.ContractCommentType        = ContractCommentType
				related.SequenceNumber             = SequenceNumber
				
		ContractAttachmentRel
			one-to-one relation to ContractAttachment
			Field Mapping uses BySequence
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.Sequence                   = SequenceNumber
				
		ContractLineAttachmentRel
			one-to-one relation to ContractLineAttachment
			Field Mapping uses BySequence
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.ContractLine			   = LineNumber
				related.Sequence                   = SequenceNumber
		
		ContractDocumentRel
			one-to-one relation to ContractDocument
			Field Mapping uses symbolic key
				related.ContractGroup              = ContractGroup
				related.Contract                   = Contract
				related.ContractDocument    	   = ContractDocument

	Derived Fields
		DerivedIDMDocumentURL is a DerivedField
            type is Alpha size 2083
            if (IDMUniqueId entered)
                IDMItem.DocumentType = "FSM_ContractDocument"
                IDMItem.IDMUniqueId  = IDMUniqueId
                return IDMItem.GetLink

        DerivedRedlineDocumentTitle is a DerivedField
            type is like Description
            if (ContractRedlineDocumentTitle entered and ContractRedlineDocumentRel.Title not entered)
                return ContractRedlineDocumentTitle
            else
                return ContractRedlineDocumentRel.Title

	Local Fields
		IDMItem
	
	Sets
	
		ByRedlineDocument
			Sort Order
				ContractGroup
				Contract
				ContractRedlineDocument
				ContractNotificationEmail
				ContractNotificationEmailAttachment
			Instance Selection
				where (ForRedlineDocument)
				
		ByComment
			Sort Order
				ContractGroup
				Contract
				SequenceNumber
				ContractNotificationEmail
				ContractNotificationEmailAttachment
			Instance Selection
				where (ForHeaderComment)
				
		ByLineComment
			Sort Order
				ContractGroup
				Contract
				LineNumber
				SequenceNumber
				ContractNotificationEmail
				ContractNotificationEmailAttachment
			Instance Selection
				where (LineNumberEntered)
	
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		PopulateIDMRedlineTitle is a Set Action
			default label is untranslatable
			restricted

			Parameters
				PrmContractGroup 						is like ContractGroup
				PrmContract								is like Contract
				PrmContractRedlineDocument				is like ContractRedlineDocument

			Instance Selection
				where (PrmContractGroup           = ContractGroup
 				and    PrmContract 				  = Contract
				and    PrmContractRedlineDocument = ContractRedlineDocument)

			Action Rules
				Instance Rules
					invoke Update
						invoked.ContractRedlineDocumentTitle = ContractRedlineDocumentRel.Title
