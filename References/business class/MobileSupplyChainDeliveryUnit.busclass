MobileSupplyChainDeliveryUnit is a BusinessClass
    owned by mscm
    prefix is DLVTU
    sql name is "MSCMDeliveryUnit"

    Ontology
        symbolic key is MobileSupplyChainDeliveryUnit

    Patterns
        implements ContextualParent
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        CarrierTrackingNumber       is a TrackingNumber
        Status                      is AlphaUpper size 2
            States
                OnLoadingDock   value is "O"
                Inactive        value is "I" 
                Received        value is "R"
                Picked          value is "P"
                InTransit       value is "T"
                Delivered       value is "D"
        RequisitionTextSearch       is Text
            text searchable
            scannable
        
    Derived Fields
        DerivedInforTrackingNumber is a DerivedField
            type is AlphaUpper size 50
            return MobileSupplyChainDeliveryTicket.InforTrackingNumber
        
        DerivedReceivingLocationName is a DerivedField
            type is Text
            default label is "ReceivingLocationName"
            return MobileSupplyChainLocation.Name
        
        DerivedDocumentNumber is a DerivedField
            type is Text
            return MobileSupplyChainDeliveryTicket.DocumentNumber
        
        DerivedPurchaseOrder is a DerivedField
            type is Text
            return MobileSupplyChainDeliveryTicket.PurchaseOrder

    Sets
        ByCarrierTrackingNumberAndStatus
            Sort Order
                CarrierTrackingNumber
                Status
                MobileSupplyChainDeliveryTicket.create stamp descending
                Company
                MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket
                MobileSupplyChainDeliveryUnit


        ByCarrierTrackingNumber
            Sort Order
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainCompany
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainLocation
                CarrierTrackingNumber
                create stamp descending
                Company
                MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket
                MobileSupplyChainDeliveryUnit

        ByDeliverToLocation
            Sort Order
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainCompany
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainLocation
                create stamp descending
                Company
                MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket
                MobileSupplyChainDeliveryUnit

        ByDocumentNumber
            Sort Order
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainCompany
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket.DocumentNumber
                create stamp descending
                Company
                MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket
                MobileSupplyChainDeliveryUnit

        ByInforTrackingNumber
            Sort Order
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainCompany
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket.InforTrackingNumber
                create stamp descending
                Company
                MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket
                MobileSupplyChainDeliveryUnit

        ByPurchaseOrder
            Sort Order
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainCompany
                MobileSupplyChainDeliveryTicket.DeliverToCompanyLocation.MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket.PurchaseOrder
                create stamp descending
                Company
                MobileSupplyChainLocation
                MobileSupplyChainDeliveryTicket
                MobileSupplyChainDeliveryUnit

    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted

        TransitionToInTransit is an Instance Action
            restricted
            Entrance Rules
                Status = Status.InTransit

        UpdateStatus is an Instance Action
            restricted
            Local Fields
                LocalDeliveredLines is Numeric size 6
            Entrance Rules
                LocalDeliveredLines = instance count of DeliveredLinesRel
                
                if (LocalDeliveredLines > 0)
                    if (MobileSupplyChainDeliveryTicket.DeliveryType.MiscellaneousReceipt) 
                        Status = Status.Delivered
                    else
                    if (LocalDeliveredLines = MobileSupplyChainDeliveryTicket.NumberOfLines)
                        Status = Status.Delivered
               
        PrintDeliveryLabel is an Instance Action
            restricted
            Parameters
                PrmLabelPrinter     is like MobileSupplyChainPrinter
            Action Rules

                invoke Create MobileSupplyChainPrinterJob
                    invoked.FinanceEnterpriseGroup          = Company.FinanceEnterpriseGroup
                    invoked.Company                         = Company
                    invoked.MobileSupplyChainPrinter        = PrmLabelPrinter
                    invoked.TransactionType                 = MobileSupplyChainPrintingTransactionType.LabelManagement
                    invoked.PrintDataFile                   = template.MiscellaneousReceiptDeliveryLabel_ST document for this instance

        CreateNote is an Instance Action
            restricted

            Parameters
                PrmDeliveryNoteCode is a DeliveryNoteCode
                PrmDeliveryNote     is a DeliveryNote

            Action Rules
                invoke Create MobileSupplyChainDeliveryNote
                    invoked.Company                         = Company
                    invoked.MobileSupplyChainLocation       = MobileSupplyChainLocation
                    invoked.MobileSupplyChainDeliveryTicket = MobileSupplyChainDeliveryTicket
                    invoked.MobileSupplyChainDeliveryUnit   = MobileSupplyChainDeliveryUnit
                    invoked.DeliveryNoteCode                = PrmDeliveryNoteCode
                    invoked.DeliveryNote                    = PrmDeliveryNote
                    
        CreateEventLinesByUnit is an Instance Action
            restricted
            Parameters
                PrmMobileSupplyChainDeliveryEvent   is a MobileSupplyChainDeliveryEvent
                PrmDeliveryNoteCode                 is a DeliveryNoteCode
                PrmDeliveryNote                     is a DeliveryNote

            Action Rules
                for each MobileSupplyChainDeliveryLine set
                    invoke CreateEventLine each
                        invoked.PrmMobileSupplyChainDeliveryEvent  = PrmMobileSupplyChainDeliveryEvent

                if (PrmDeliveryNoteCode entered)
                    invoke CreateNote
                        invoked.PrmDeliveryNoteCode = PrmDeliveryNoteCode
                        invoked.PrmDeliveryNote     = PrmDeliveryNote

        BuildRequisitionTextSearch is an Instance Action 
            restricted
            run in background
            Action Rules
                build text search field RequisitionTextSearch
                    Fields
                        DeliveryLineRequisitionRel.Requisition

    Relations
        DeliveredLinesRel
            one-to-many relation to MobileSupplyChainDeliveryLine
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.MobileSupplyChainLocation       = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket = MobileSupplyChainDeliveryTicket
                related.MobileSupplyChainDeliveryUnit   = MobileSupplyChainDeliveryUnit
            Instance Selection
                where (related.IsDelivered)

        MobileSupplyChainDeliveryEventLineRel
            one-to-many relation to MobileSupplyChainDeliveryEventLine
            Field Mapping uses ByDeliveryLine
                related.Company                                 = Company
                related.MobileSupplyChainLocation               = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket         = MobileSupplyChainDeliveryTicket
                related.MobileSupplyChainDeliveryUnit           = MobileSupplyChainDeliveryUnit

        DeliveryEventPerTicketRel
            one-to-many relation to MobileSupplyChainDeliveryEvent
            Field Mapping uses symbolic key
                related.Company                                 = Company
                related.MobileSupplyChainLocation               = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket         = MobileSupplyChainDeliveryTicket
                
        MobileSupplyChainDeliveryNoteRel
            one-to-many relation to MobileSupplyChainDeliveryNote
            Field Mapping uses symbolic key
                related.Company                                 = Company
                related.MobileSupplyChainLocation               = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket         = MobileSupplyChainDeliveryTicket
                related.MobileSupplyChainDeliveryUnit           = MobileSupplyChainDeliveryUnit
            Instance Selection
                where (related.MobileSupplyChainDeliveryLine    = 0)

        OnLoadingDockDeliveryUnitRel
            one-to-many relation to MobileSupplyChainDeliveryUnit
            Field Mapping uses ByCarrierTrackingNumberAndStatus
                related.CarrierTrackingNumber                   = CarrierTrackingNumber
                related.Status                                  = MobileSupplyChainDeliveryUnit.Status.OnLoadingDock
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup   = Company.FinanceEnterpriseGroup)

        DeliveryLineRequisitionRel
            one-to-many relation to MobileSupplyChainDeliveryLine
            Field Mapping uses symbolic key 
                related.Company                         = Company
                related.MobileSupplyChainLocation       = MobileSupplyChainLocation
                related.MobileSupplyChainDeliveryTicket = MobileSupplyChainDeliveryTicket
                related.MobileSupplyChainDeliveryUnit   = MobileSupplyChainDeliveryUnit
            Instance Selection
                where (related.Requisition entered)
