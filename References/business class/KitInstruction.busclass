KitInstruction is a BusinessClass
    owned by ic
    prefix is KTI
    classic name is KTINSTRUCT

	Ontology
		symbolic key is KitInstruction
			classic set name is KTISET1
			classic name for KitItem.InventoryLocation is LOCATION
            classic name for KitItem.Item is KIT-ITEM
			classic name for KitInstruction.Sequence is SEQ
			classic name for KitInstruction.InstructionLine is INST-LINE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Instruction       is a Comment

    Conditions
        IsComponent
        	restricted
            when (KitInstruction.Sequence entered)

        IsKitItem
            classic name is KITITEM
            restricted
            when (KitInstruction.Sequence not entered)

    Relations
        KitComponentRel
            classic name is COMPONENT
            one-to-one relation to KitComponent
            valid when (IsComponent)
            required
            Field Mapping uses symbolic key
                related.ItemGroup                              = ItemGroup
                related.KitItem								   = KitItem
                related.KitComponent.ComponentSequence         = KitInstruction.Sequence
                related.KitComponent.OptionalComponentSequence = blank

        KitComponentsRel
            one-to-many relation to KitComponent
            Field Mapping uses symbolic key
                related.ItemGroup                              = ItemGroup
                related.KitItem								   = KitItem
           	Instance Selection	
                where (related.KitComponent.ComponentSequence  = KitInstruction.Sequence)
	               
      	KitInstructionsRel
      		one-to-many relation to KitInstruction
            Field Mapping uses symbolic key
                related.ItemGroup         		  = ItemGroup
                related.KitItem.Company           = KitItem.Company
                related.KitItem.InventoryLocation = KitItem.InventoryLocation
           	 	related.KitItem.Item           	  = KitItem.Item
      	
      	InventoryCompanyRel
     		one-to-one relation to InventoryCompany
     		Field Mapping uses symbolic key
     			related.Company           = KitItem.Company

     	InventoryLocationRel
     		one-to-one relation to InventoryLocation
     		Field Mapping uses symbolic key
     			related.Company           = KitItem.Company
     			related.InventoryLocation = KitItem.InventoryLocation    	
           		
    Field Rules
        Instruction
            required

        ItemGroup
            required

        KitItem
        	if (KitItem.InventoryLocation entered)
        		constraint (KitItem.Company entered)
        			"MustEnterBothCompanyAndLocation"
	       		constraint (InventoryLocationRel exists)
    	   			"LocationDoesNotExist"
       		if (KitItem.Company entered)
    	    	constraint (InventoryCompanyRel.ItemGroup = ItemGroup)
	        		"CompanyDoesNotBelongToThisItemGroup"
       			constraint (KitItem.InventoryLocation entered)
       				"MustEnterBothCompanyAndLocation"
    
       	
	Actions
        Create is a Create Action
			Action Rules
				if (KitInstruction.Sequence not entered)
					increment KitItem.InstructionLine by 1 
					KitInstruction.InstructionLine = KitItem.InstructionLine
				else
					invoke Update KitComponentsRel
						invoked.InstructionLine += 1
					KitInstruction.InstructionLine = KitComponentsRel.InstructionLine
					
        Update is an Update Action

        Delete is a Delete Action

