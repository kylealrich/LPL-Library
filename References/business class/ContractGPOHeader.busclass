ContractGPOHeader is a BusinessClass
	owned by po
	
	prefix is CCPX
	
	Ontology
		symbolic key is ContractGPOHeader
		
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
	
	Persistent Fields
		GPOContractNumber                                            	 
		TierLevel                   	is like ContractImportTier       
		ManufacturerSupplier        	is a GPOSupplierID                 
			default label is "VendorNumber"
		ContractDescription         	is a Description3                
		TierDescription             	is a Description3                
		StartDate                   	is Date                          
		EndDate                     	is Date		                     

		ContractTypeCode                is AlphaUpper 9
			States
				LocallyNegotiated		value is "LCM"
				GPOCustomerContract     value is "CST"
				NationalGPOContract     value is "NAT"
				GroupGPOContract        value is "GRP"

		OriginalEffectiveDate	     	is Date                          
		AgreementDate                 	is Date  
		RebateIndicator                 is Boolean                       
			default label is "RebateEligible"
		RebateCode                      is Alpha 10 
		ContractTypeID                  is Numeric 1 
			States
				Supply                  value is 1
				Equipment               value is 2
				Service                 value is 3
				Energy                  value is 4
				RXSupply                value is 5
				RXService               value is 6
		ContractClassID                 is Numeric 1  
			States
				Manufacturer            value is 1
				Supplier                value is 2
		ContractStatusID                is Numeric 2  
			States
				Preferred               	value is 1
				Primary                 	value is 2
				Sole      	    	        value is 3
				Dual        	            value is 4
				Multi                   	value is 5
				Optional                	value is 6
				SoleOrDual               	value is 7
				SoleOrDualOrMulti         	value is 8
				SoleOrDualOrMultiOrOptional value is 9
				SoleOrMulti              	value is 10
				SoleOrMultiOrOptional     	value is 11
				SoleOrOptional           	value is 12
				DualOrMulti              	value is 13
				DualOrMultiOrOptional     	value is 14
				DualOrOptional           	value is 15
				MultiOrOptional          	value is 16
				NotApplicable               value is 17
				SoleOrDualOrOptional      	value is 18
		OrderFromID                     is Numeric 1   
			States
				Direct                  value is 1
				Distributed             value is 2
				Both                    value is 3
		AgreementArea                   is Alpha size 50 
		ReplacesContractNumber          is Alpha size 305 
		FacilityLOCRequired             is Boolean                          
			default label is "FacilityELOCIndicator"
		GroupLOCAvailable               is Boolean                          
			default label is "GroupLOCIndicator"
		FacilitySIPAvailable            is Boolean                          
			default label is "FacilitySIPIndicator"
		GroupSIPAvailable               is Boolean                          
			default label is "GroupSIPIndicator"
		EnvironmentallyFriendly         is Boolean        
		DepartmentName                  is Alpha size 50
		ClassOfTradeList                is Alpha size 255                   
			default label is "ClassOfTrade"

		MainContractIndicator           is Boolean        

		ContractVendorName              is Alpha size 80       
	    	default label is "VendorName"
	    PurchasingChannel               is Alpha size 32
		ContractCycleNumber				is Numeric 10
		ContractAmendmentNumber			is Numeric 10
		ContractVendorLegalName			is Alpha size 80
		ContractVendorTaxIDNumber		is Alpha size 9
		HPGContractType					is Alpha size 32
		TieredContractIndicator			is Boolean
		ContractVendorDiversityIndicator is Boolean
		DisintermediatedIndicator		is Boolean
		S2EligibleIndicator				is Boolean
		NationalContractNumber			is Alpha size 120
		FirmPricingDescription			is Alpha size 32
		FOBTerms                        is Alpha size 255
		PaymentTerms				    is Text
		PrimaryContactName				is Alpha size 255
		PrimaryContactPhoneNumber		is Alpha size 50
		PrimaryContactEmailAddress		is an EmailAddress 
			holds pii
		RecordModifiedTimestamp         is Alpha size 20				
		HPGContractTypeCode             is Alpha size 32   
			default label is "ContractType"
		ContractAwardStatus             is Alpha size 32   
		NetPaymentTerms                 is Numeric 3          
		PaymentDiscount                 is Decimal size 7.4
		PaymentDiscountDays             is Numeric 3

	Local Fields

		LocalGPOContractNumber      is like GPOContractNumber
		LocalTierLevel              is like ContractImportTier
		LocalGPOSupplier            is like Supplier
		LocalManufacturerSupplier   is like GPOSupplierID
		LocalDistributorSupplier    is like GPOSupplierID
		LocalSupplier               is like Supplier
		LocalContractGroup          is like ContractGroup
		LocalMembership             is like ContractImportGPOMembership
		LocalContractImport         is like ContractImport
		LocalContractType           is like ContractType
		LocalContractSubtype        is like ContractSubtype
		LocalCompany                is like InventoryCompany
		LocalLocation               is like InventoryLocation
		LocalRequestingLocation     is like RequestingLocation
		LocalPricingGroup           is like PricingGroup
		LocalStartDate              is Date
		LocalTierDistributor        is like ContractImportTierDistributor
	Derived Fields
	
		GPOContractCount 				is a ComputeField
			type is Numeric 2
			restricted
			(instance count of ContractGPOHeaderForSupplierRel)

	Conditions
		
		BlanketOrStanding
			restricted
			when (((LocalContractSubtype not entered)
			and    (ContractTypeRel.PurchaseType.Blanket
			or      ContractTypeRel.PurchaseType.Standing))
			or    ((LocalContractSubtype entered)
			and    (ContractSubtypeRel.PurchaseType.Blanket
			or      ContractSubtypeRel.PurchaseType.Standing)))

		ServicePurchaseType
			restricted
			when (((LocalContractSubtype not entered)
			and    (ContractTypeRel.PurchaseType.Service))
			or    ((LocalContractSubtype entered)
			and    (ContractSubtypeRel.PurchaseType.Service)))

		ContractDoesNotExist
			restricted
			when (ToContractImportRel !exists) 
		
		ContractExists 
			restricted 
			when (ToContractImportRel exists)
		
		CanCreateNewContract
			restricted
			when (SupplierMapped
			and  (TierLevel > 0
			or    MainContractIndicator = true
			or    ContractVendorTaxIDNumber entered) 
			and   ContractDoesNotExist
			and   !IsFutureContract
			and   !NoParticipants)

		SupplierMapped
			restricted
			when (first ManufacturerBySupplierRel.ContractSupplier entered)
			
		MappedToSupplierOrNoLines   
			restricted
			when (SupplierMapped
			or    NoItems
			or    NoParticipants)
		
		SupplierNotMapped
			restricted
			when (MappingSupplierExists
			and   first ManufacturerBySupplierRel.ContractSupplier !entered) 
		
		MappingSupplierDoesNotExist
			restricted
			when (ManufacturerBySupplierRel !exists)
		
		MappingSupplierExists
			restricted
			when (ManufacturerBySupplierRel exists)
		
		NoItems
			restricted
			when (ItemRel !exists)
			
		NoParticipants
			restricted
			when (ParticipantRel !exists)
			
		TierEntered
			restricted
			when (TierLevel >0)

		HasParticipants
			restricted
			when (ParticipantRel exists)

		HasDistributorWithMarkup
			restricted
			when (DistributorWithMarkupRel exists)
			
		SupplierOnlyMarkupExists
			restricted
			when (DistributorSupplierOnlyRel exists)

		ParticipantsSameAsSupplier
			restricted
			when (ContractGPOParticipantSameSupplierRel exists)	
			
		MappedParticipantsExist
			restricted
			when (MappedContractGPOParticipantRel exists)
		
		ContractDistributorMarkupExists
			restricted
			when (DistributorRel exists)
		
		OtherManufSuppliersExist
			restricted
			when (GPOOtherSupplierNavRel exists)
	
		MixedParticipantTiers
			restricted
			when (MixedParticipantTierRel exists)
			
		IsFutureContract
			restricted
			when (PastContractRel exists)
		
		SupplierHasItems
			restricted
			when (ItemSupplierRel exists)
			
		SupplierHasParticipants
			restricted
			when (ParticipantSupplierRel exists)
			
		SupplierHasMarkups
			restricted
			when (DistributorForSupplierRel exists)

		DistributorHasItems
			restricted
			when (ItemSupplierRel exists)

		NoDeltaItems 
			restricted 
			when (ContractGPOItemDeltaRel !exists
			and   ContractGPOHeaderDeltaRel !exists
			and   ContractGPOSupplierDeltaRel !exists
			and   ContractGPODistributorDeltaRel !exists
			and   ContractGPOParticipantDeltaRel !exists)

	Relations
		
		ContractTypeRel
			one-to-one relation to ContractType
			Field Mapping uses symbolic key
				related.ContractGroup      = LocalContractGroup
				related.ContractType       = LocalContractType
		
		ContractSubtypeRel
			one-to-one relation to ContractSubtype
			Field Mapping uses symbolic key
				related.ContractGroup      = LocalContractGroup
				related.ContractType       = LocalContractType
				related.ContractSubtype    = LocalContractSubtype

		GPOOtherSupplierRel
			one-to-many relation to ContractGPOHeader
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber  = LocalGPOContractNumber
			Instance Selection
				where (related.MainContractIndicator = false)

		GPOOtherSupplierNavRel
			one-to-many relation to ContractGPOHeader
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber  = GPOContractNumber
			Instance Selection
				where (related.MainContractIndicator = false)

		ContractGPOSupplierRel
			one-to-many relation to ContractGPOSupplier
			Field Mapping uses ByGPOSupplier
				related.GPOSupplier        = ManufacturerSupplier 

		ContractGPODistributor2021Rel
			one-to-many relation to ContractGPOSupplier
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber  = GPOContractNumber
			Instance Selection
				where (related.ContractDistributorEffectiveDate = LocalStartDate) 			

		ContractImportDistributorRel
			one-to-one relation to ContractImportDistributor
			Field Mapping uses symbolic key
				related.ContractGroup      			= LocalContractGroup
				related.ContractImport     			= LocalContractImport
				related.ContractImportDistributor 	= LocalSupplier

		ItemRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses ByGPOContractItems
				related.GPOContractNumber    = GPOContractNumber
			
		ItemSupplierRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses BySupplier
				related.GPOContractNumber    = LocalGPOContractNumber
				related.ItemSupplier         = LocalDistributorSupplier		

		ParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber    = GPOContractNumber
						
		ParticipantSupplierRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByItemSupplier
				related.GPOContractNumber    = LocalGPOContractNumber
				related.ItemSupplier         = LocalDistributorSupplier					

		MixedParticipantTierRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber    = GPOContractNumber
			Instance Selection
				where (related.HasMixedTiers)	
				
		DistributorWithMarkupRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByGPOContractMemberSupplierNumber
				related.GPOContractNumber    = GPOContractNumber
			Instance Selection
				where (related.MarkupPercent > 0)			

		ManufacturerContractRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses ByGPOContract
				related.GPOContractNumber    = GPOContractNumber
		
		DistributorRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByGPOContractMemberSupplierNumber
				related.GPOContractNumber    = GPOContractNumber
		
		DistributorSupplierOnlyRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses BySupplierOnly
				related.ItemSupplier        = ManufacturerSupplier
		
		DistributorForSupplierRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses BySupplier
				related.GPOContractNumber    = GPOContractNumber
				related.ItemSupplier         = LocalDistributorSupplier 

		ContractImportTierRel
			one-to-one relation to ContractImportTier
			Field Mapping uses ByGPOContractNumber
				related.ContractGroup   	= LocalContractGroup	
				related.GPOContractNumber 	= LocalGPOContractNumber
				related.ContractImportTier  = LocalTierLevel
				related.ContractImport      = LocalContractImport
				
		ToContractImportRel
			one-to-many relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = GPOContractNumber
		
		ContractImportRel
			one-to-one relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber 	= LocalGPOContractNumber
				related.ContractGroup       = LocalContractGroup			
	
		ManufacturerSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup               = LocalContractGroup
				related.ContractImportGPOSupplier 	= LocalManufacturerSupplier

		ManufacturerBySupplierRel
			one-to-many relation to ContractImportGPOSupplier
			Field Mapping uses BySupplierFirst
				related.ContractImportGPOSupplier 	= ManufacturerSupplier

		DistributorSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup               = LocalContractGroup
				related.ContractImportGPOSupplier 	= LocalDistributorSupplier

		ContractGPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = LocalGPOContractNumber
				
		ContractGPOCategoryRel
			one-to-many relation to ContractGPOCategory
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = GPOContractNumber
		
		ContractGPOTermRel
			one-to-many relation to ContractGPOTerm
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = GPOContractNumber

		MappedContractGPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = LocalGPOContractNumber
			Instance Selection
				where (related.MappedToMember)

		ContractGPOParticipantSameSupplierRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByItemSupplier
				related.GPOContractNumber   = LocalGPOContractNumber
				related.ItemSupplier        = ManufacturerSupplier

		ContractImportTierMemberRel
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByMembership
				related.ContractGroup       		= LocalContractGroup
				related.GPOContractNumber   		= LocalGPOContractNumber
				related.ContractImportGPOMembership = LocalMembership
	
		ContractImportTierMemberByLocationRel
			one-to-one relation to ContractImportTierMember
			Field Mapping uses symbolic key
				related.ContractGroup                     		= LocalContractGroup
				related.ContractImport                    		= LocalContractImport
				related.ContractImportTierMember.Company    	= LocalCompany
				related.ContractImportTierMember.Location       = LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   = LocalPricingGroup
	
		PastContractRel  
			one-to-many relation to ContractGPOHeader
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber           = GPOContractNumber
			Instance Selection
				where (related.StartDate            < StartDate)

		ContractGPOHeaderForSupplierRel
			one-to-many relation to ContractGPOHeader
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber           = LocalGPOContractNumber
				
		SupplierRel
			one-to-one relation to Supplier
			Field Mapping uses symbolic key
				related.SupplierGroup               = LocalContractGroup
				related.Supplier                    = LocalGPOSupplier

		ContractImportMemberRel 
			one-to-one relation to ContractImport
			Field Mapping uses symbolic key
				related.ContractGroup               = LocalContractGroup 
				related.ContractImport              = LocalContractImport

		ContractGPODistributorMappedRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByGPOContractMemberSupplierNumber
				related.GPOContractNumber   				= GPOContractNumber
				related.ContractImportGPOMembership       	= LocalMembership  
			Instance Selection
				where (related.SupplierMapped)				

		ContractGPODistributorMappedMemberManufacturerRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByMember
				related.ContractImportGPOMembership       	= LocalMembership  
			Instance Selection
				where (related.SupplierMapped
				and    related.NoContractNumber
				and    related.ManufacturerSupplierEntered
				and    related.ManufacturerSupplier        = ContractImportMemberRel.ManufacturerSupplier)	

		ContractGPODistributorMappedMemberOnlyRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByMember
				related.ContractImportGPOMembership       	= LocalMembership  
			Instance Selection
				where (related.SupplierMapped
				and    related.NoContractNumber)	

		ContractImportTierDistributorDuplicateRel
			one-to-one relation to ContractImportTierDistributor
			Field Mapping uses symbolic key
				related.ContractGroup       						= LocalContractGroup
				related.ContractImport      						= LocalContractImport
				related.ContractImportTierMember.Company    		= LocalCompany
				related.ContractImportTierMember.Location       	= LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   	= LocalPricingGroup
				related.ContractImportTierDistributor				= LocalTierDistributor	

		ContractGPOHeaderDeltaRel
			one-to-many relation to ContractGPOHeaderDelta 
			Field Mapping uses ByGPOContractNumber
				related.ContractNumber = GPOContractNumber
		ContractGPOItemDeltaRel
			one-to-many relation to ContractGPOItemDelta 
			Field Mapping uses ByGPOContractNumber
				related.ContractNumber = GPOContractNumber
		ContractGPOSupplierDeltaRel
			one-to-many relation to ContractGPOSupplierDelta 
			Field Mapping uses ByGPOContractNumber
				related.ContractNumber = GPOContractNumber
		ContractGPOParticipantDeltaRel 
			one-to-many relation to ContractGPOParticipantDelta 
			Field Mapping uses ByGPOContractNumber
				related.ContractNumber = GPOContractNumber
		ContractGPODistributorDeltaRel
			one-to-many relation to ContractGPODistributorDelta 
			Field Mapping uses ByContractMemberSupplierNumber
				related.ContractNumber = GPOContractNumber


		ContractGPOSupplierForMemberRel
			one-to-many relation to ContractGPOSupplier
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber           = GPOContractNumber
			Instance Selection
				where (related.HasItems
				and    related.NoParticipants
				and    related.SupplierMappedForNewDistributor) 										
	Sets
		ByStartDateDescending
			Sort Order
				StartDate descending
				GPOContractNumber 
				ContractGPOHeader
	
		ByGPOContractNumber
			Sort Order
				GPOContractNumber
				ContractGPOHeader
				
		ByGPOSupplier
			Sort Order
				ManufacturerSupplier
				ContractGPOHeader
		
		ByTier
			Sort Order
				GPOContractNumber
				TierLevel
				ContractGPOHeader				
						
	Rule Blocks

		CreateDistributorAndDistributorTier  

			LocalCompany			= ContractImportTierMemberByLocationRel.ContractImportTierMember.Company 
			LocalLocation           = ContractImportTierMemberByLocationRel.ContractImportTierMember.Location 
			LocalRequestingLocation = ContractImportTierMemberByLocationRel.ContractImportTierMember.RequestingLocation
			LocalPricingGroup       = ContractImportTierMemberByLocationRel.ContractImportTierMember.PricingGroup
			for each ContractGPODistributorMappedRel   
				LocalDistributorSupplier = each.ItemSupplier
				LocalTierDistributor     = DistributorSupplierRel.ContractSupplier
				LocalSupplier            = DistributorSupplierRel.ContractSupplier
				if (ContractImportMemberRel.Supplier != DistributorSupplierRel.ContractSupplier
				and DistributorHasItems)
					if (ContractImportDistributorRel !exists)
						invoke Create ContractImportDistributor
							invoked.ContractGroup                                   = ContractGroup
							invoked.ContractImport                                  = LocalContractImport
							invoked.ContractImportDistributor                       = DistributorSupplierRel.ContractSupplier
							invoked.GPODistributorSupplier                          = each.ItemSupplier
							invoked.EffectiveDate                                   = each.StartDate
							invoked.ExpirationDate                                  = each.EndDate
							invoked.Priority                                        = DistributorPriority
							invoked.StandardMarkupCalculation                       = StandardMarkupCalculation
				
					if (ContractImportTierDistributorDuplicateRel !exists)	
						invoke Create ContractImportTierDistributor
							invoked.ContractGroup                                   = ContractGroup
							invoked.ContractImport                                  = LocalContractImport
							invoked.ContractImportTierMember.Company                = LocalCompany                         						
							invoked.ContractImportTierMember.Location               = LocalLocation 
							invoked.ContractImportTierMember.RequestingLocation     = LocalRequestingLocation
							invoked.ContractImportTierMember.PricingGroup		    = LocalPricingGroup
							invoked.ContractImportTierDistributor                   = DistributorSupplierRel.ContractSupplier
							invoked.ContractImportGPOMembership                     = ContractImportTierMemberByLocationRel.ContractImportGPOMembership
			if (ContractImportMemberRel.GPOIsVizient)
				for each ContractGPODistributorMappedMemberManufacturerRel
					LocalDistributorSupplier = each.ItemSupplier
					LocalTierDistributor     = DistributorSupplierRel.ContractSupplier
					LocalSupplier            = DistributorSupplierRel.ContractSupplier
					if (DistributorHasItems)
						if (ContractImportRel.Supplier != DistributorSupplierRel.ContractSupplier)
							if (ContractImportDistributorRel !exists)
								invoke Create ContractImportDistributor
									invoked.ContractGroup                                   = ContractGroup
									invoked.ContractImport                                  = LocalContractImport
									invoked.ContractImportDistributor                       = DistributorSupplierRel.ContractSupplier
									invoked.GPODistributorSupplier                          = each.ItemSupplier
									invoked.EffectiveDate                                   = each.StartDate
									invoked.ExpirationDate                                  = each.EndDate    					
									invoked.Priority			                            = DistributorPriority
									invoked.StandardMarkupCalculation                       = StandardMarkupCalculation
						if (ContractImportTierDistributorDuplicateRel !exists)	
							invoke Create ContractImportTierDistributor
								invoked.ContractGroup                                   = ContractGroup
								invoked.ContractImport                                  = LocalContractImport
								invoked.ContractImportTierMember.Company                = LocalCompany                         						
								invoked.ContractImportTierMember.Location               = LocalLocation 
								invoked.ContractImportTierMember.RequestingLocation     = LocalRequestingLocation
								invoked.ContractImportTierMember.PricingGroup		    = LocalPricingGroup
								invoked.ContractImportTierDistributor                   = DistributorSupplierRel.ContractSupplier
								invoked.ContractImportGPOMembership                     = ContractImportTierMemberByLocationRel.ContractImportGPOMembership	
			for each ContractGPODistributorMappedMemberOnlyRel
				LocalDistributorSupplier = each.ItemSupplier
				LocalTierDistributor     = DistributorSupplierRel.ContractSupplier
				LocalSupplier            = DistributorSupplierRel.ContractSupplier
				if (DistributorHasItems)
					if (ContractImportRel.Supplier != DistributorSupplierRel.ContractSupplier)
						if (ContractImportDistributorRel !exists)
							invoke Create ContractImportDistributor
								invoked.ContractGroup                                   = ContractGroup
								invoked.ContractImport                                  = LocalContractImport
								invoked.ContractImportDistributor                       = DistributorSupplierRel.ContractSupplier
								invoked.GPODistributorSupplier                          = each.ItemSupplier
								invoked.EffectiveDate                                   = each.StartDate
								invoked.ExpirationDate                                  = each.EndDate    		
								invoked.Priority                                        = DistributorPriority
								invoked.StandardMarkupCalculation                       = StandardMarkupCalculation			       			
					if (ContractImportTierDistributorDuplicateRel !exists)	
						invoke Create ContractImportTierDistributor
							invoked.ContractGroup                                   = ContractGroup
							invoked.ContractImport                                  = LocalContractImport
							invoked.ContractImportTierMember.Company                = LocalCompany                         						
							invoked.ContractImportTierMember.Location               = LocalLocation 
							invoked.ContractImportTierMember.RequestingLocation     = LocalRequestingLocation
							invoked.ContractImportTierMember.PricingGroup		    = LocalPricingGroup
							invoked.ContractImportTierDistributor                   = DistributorSupplierRel.ContractSupplier
							invoked.ContractImportGPOMembership                     = ContractImportTierMemberByLocationRel.ContractImportGPOMembership	
			if (ContractImportMemberRel.GPOIsHealthTrust2021)
				
				for each ContractGPOSupplierForMemberRel    
					LocalGPOSupplier  			= each.GPOSupplier   
					LocalDistributorSupplier 	= each.GPOSupplier
					LocalTierDistributor     	= DistributorSupplierRel.ContractSupplier
					LocalSupplier               = DistributorSupplierRel.ContractSupplier
					
					if (ContractImportDistributorRel !exists)
						invoke Create ContractImportDistributor
							invoked.ContractGroup                                   = ContractGroup
							invoked.ContractImport                                  = LocalContractImport
							invoked.ContractImportDistributor                       = DistributorSupplierRel.ContractSupplier
							invoked.GPODistributorSupplier                          = each.GPOSupplier
							invoked.EffectiveDate                                   = each.ContractDistributorEffectiveDate
							invoked.ExpirationDate                                  = each.ContractDistributorExpireDate    		
							invoked.Priority                                        = DistributorPriority
					if (ContractImportTierDistributorDuplicateRel !exists)	
						
						invoke Create ContractImportTierDistributor
							invoked.ContractGroup                                   = ContractGroup
							invoked.ContractImport                                  = LocalContractImport
							invoked.ContractImportTierMember.Company                = LocalCompany                         						
							invoked.ContractImportTierMember.Location               = LocalLocation 
							invoked.ContractImportTierMember.RequestingLocation     = LocalRequestingLocation
							invoked.ContractImportTierMember.PricingGroup		    = LocalPricingGroup
							invoked.ContractImportTierDistributor                   = DistributorSupplierRel.ContractSupplier
							invoked.ContractImportGPOMembership                     = ContractImportTierMemberByLocationRel.ContractImportGPOMembership	

			if (ContractImportMemberRel.GPOIsVizient)
				invoke DeterminePurchaseMethod ContractImportTierMemberByLocationRel

	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Delete		

		CreateNewContractForThisGPOContract is an Instance Action
			completion message is "ContractInterfaceCreated;ViewInInterfaceContractsWithNoContracts"			
			valid when (CanCreateNewContract)
			Parameters
				ContractGroup
				ParmGPOSupplier       					is a Supplier
					default label is "GPOSupplier"
				ParmGPOContractNumber 					is a GPOContractNumber
					default label is "GPOContractNumber"
				TypeOfContract                          is Numeric 1
					States
						Manufacturer                    value is 1
						Distributor                     value is 2
						Service                         value is 3
						MultiTierDistributor            value is 4
				ContractType
				ContractSubtype
				ContractClassification
				ContractSubclassification
				ManufacturerCodeDivision 				is a Manufacturer	
				UseGPOContractNumberAsWorkingContract 	is Boolean			
				Contact                  				is an Employee
					default label is "PrimaryInternalContact"
				MemberProcessing                        is Numeric 1
					States
						ProcessOnlyMappedMembers        value is 1
						ErrorIfAllMembersNotMapped      value is 2
				UseTemplate                             is a ContractImport
				DoNotCreateDistributorContract          is Boolean
				DistributorPriority                     is Numeric 1
				StandardMarkupCalculation               is Boolean
			
			Parameter Rules
				ContractGroup
					required
					
					if (StartDate > current date)
						confirmation required
							"ContractWillBeCreatedWithAStartDateInTheFuture;DoYouWantToContinue?"
					constraint (ContractGroup.GPOUsed > 0)
						"MustSelectA_GPO_UsedOnContractGroupToRunThisProcess"
				
					LocalContractGroup   = ContractGroup
					LocalContractType    = ContractType
					LocalContractSubtype = ContractSubtype
					LocalGPOSupplier     = ParmGPOSupplier
					if (ContractType entered)
						if (TypeOfContract.Manufacturer
						or  TypeOfContract.Distributor
						or  TypeOfContract.MultiTierDistributor)
							constraint (!ServicePurchaseType)
								"ContractTypesForManufacturerAndDistributorContractsCannotBeServicePurchaseTypes"
							constraint (!BlanketOrStanding)
								"ContractTypesForManufacturerAndDistributorContractsCannotBeBlanketOrStandingPurchaseTypes"
						if (TypeOfContract.Service)
							constraint (ServicePurchaseType)
								"ContractTypesForServiceContractsMustUseAServicePurchaseType"
					LocalManufacturerSupplier = ManufacturerSupplier 
					constraint (NoDeltaItems)
						"DeltaRecordsExistForThisContract;MustRunLoadUpdatedRecordsAndProcessUpdates"
					constraint (SupplierMapped)
						"ManufacturerSupplierNotMappedToASupplier"
					LocalGPOContractNumber = ParmGPOContractNumber
					if (TypeOfContract.Manufacturer
					and !DoNotCreateDistributorContract)
						if (ContractGroup.GPOUsed = 2)
							for each ContractGPOParticipantRel
								constraint (each.MappedToSupplierOrNoLines)
									"DistributorSupplier<each.ItemSupplier>IsNotMappedAndHasLines"
						if (ContractGroup.GPOUsed = 4)
							for each ContractGPOSupplierRel
								constraint (each.MappedToSupplierOrNoLines)
									"DistributorSupplier<each.GPOSupplier>IsNotMappedAndHasLinesAndMembers"  
						if (ContractGroup.GPOUsed = 1)
							for each GPOOtherSupplierRel
								constraint (each.MappedToSupplierOrNoLines)
									"DistributorSupplier<each.ManufacturerSupplier>IsNotMappedAndHasLinesAndMembers" 
				ParmGPOSupplier
					initial value is ContractGroup.DefaultGPOSupplier
					required
					constraint (SupplierRel exists)
						"GPOSupplierDoesNotExist"
					constraint (SupplierRel.Supplier.GroupPurchasingOrganization) 
						"GPOSupplierIsNotDefinedAsAGroupPurchasingOrganization" 					

				ParmGPOContractNumber
					initial value is GPOContractNumber
					default to GPOContractNumber
					LocalContractGroup 		= ContractGroup
					LocalGPOContractNumber 	= GPOContractNumber
					constraint (ContractImportRel !exists)
						"ContractInterfaceAlreadyExists"
				
				TypeOfContract
					initial value is TypeOfContract.Manufacturer
					LocalGPOContractNumber = ParmGPOContractNumber
					if (ContractGroup.GPOIsHealthTrust)
						if (ContractClassID = 1)
							if (TypeOfContract.Distributor
							or  TypeOfContract.MultiTierDistributor)
								confirmation required
									"ClassIDIndicatesThisShouldBeAManufacturerTypeContract;DoYouWantToContinue?"
						if (ContractClassID = 2)
							if (TypeOfContract.Manufacturer) 
								confirmation required
									"ClassIDIndicatesThisShouldNotBeAManufacturerTypeContract;DoYouWantToContinue?"
							if (MixedParticipantTiers
							and TypeOfContract.Distributor)  
								confirmation required
									"ThisGPOContractMayBeAMultiTierDistributorContract;HaveYouSelectedTheCorrectTypeOfContractToCreate?"
					if (TypeOfContract.Manufacturer
					or  TypeOfContract.MultiTierDistributor)
						if (NoItems)
							confirmation required
								"ThisGPOContractHasNoItems;DoYouWantToCreateAManufacturerContract?"
					if (TypeOfContract.Manufacturer
					or  TypeOfContract.Distributor)
						if (ContractDescription contains "DISTRIBUTION"
						or  GPOContractNumber = 82)
							confirmation required
								"ThisGPOContractMayBeAMultiTierDistributorContract;HaveYouSelectedTheCorrectTypeOfContractToCreate?"
					if (TypeOfContract.Service)
						constraint (NoItems)
							"CannotCreateServiceContract;GPOContractHasItems"
						constraint (HasParticipants)
							"CannotCreateServiceContract;GPOContractHasNoParticipants"
					required
							 
				ManufacturerCodeDivision
					LocalGPOSupplier     			= ParmGPOSupplier
					LocalGPOContractNumber          = ParmGPOContractNumber
					LocalContractGroup              = ContractGroup
					LocalManufacturerSupplier       = ContractGPOHeaderForSupplierRel.ManufacturerSupplier
					initial value is UseTemplate.ManufacturerCodeDivision
					default to ManufacturerSupplierRel.ManufacturerCodeDivision
					
					if (!TypeOfContract.Service
					and ManufacturerCodeDivision !entered
					and UseTemplate.ManufacturerCodeDivision !entered)
						constraint (ManufacturerSupplierRel.ManufacturerCodeDivision entered)
							"ManufacturerCodeDivisionMustBeEntered;OneIsNotDefinedForTheSupplierOrTemplate"		

				ContractType
					initial value is UseTemplate.ContractType
					default to ContractGroup.DefaultGPOContractType
					
				ContractSubtype
					initial value is UseTemplate.ContractSubtype
				
				ContractClassification
					initial value is UseTemplate.ContractClassification
					default to ContractGroup.DefaultGPOContractClassification
					
				ContractSubclassification
					initial value is UseTemplate.ContractSubclassification
					
				Contact
					initial value is UseTemplate.Contact
					default to ContractGroup.DefaultGPOContact
				
				UseGPOContractNumberAsWorkingContract
					initial value is true

				MemberProcessing
					initial value is ContractGroup.DefaultMemberProcessing
					required
					if (MemberProcessing = 2)
						for each ContractGPOParticipantRel
							constraint (each.MappedToMember)
								"MembersExistThatAreNotMapped"	
					else
						constraint (MappedParticipantsExist)
							"NoMappedMembersExist;MustHaveAtLeastOneMappedMember"				
				
				UseTemplate
					constraint (UseTemplate.Template)
						"UseTemplateMustBeATemplate"
				
			Action Rules
			
				invoke CreateNewContract
					invoked.ContractGroup     				= ContractGroup
					invoked.ParmGPOSupplier   				= ParmGPOSupplier
					invoked.ParmGPOContractNumber 			= ParmGPOContractNumber
					invoked.ParmSupplier                    = ManufacturerSupplier
					invoked.ContractType            		= ContractType
					invoked.ContractSubtype         		= ContractSubtype
					invoked.ContractClassification  		= ContractClassification
					invoked.ContractSubclassification       = ContractSubclassification
					invoked.ManufacturerCodeDivision 		= ManufacturerCodeDivision 							
					invoked.UseGPOContractNumberAsWorkingContract = UseGPOContractNumberAsWorkingContract
					invoked.Contact                         = Contact
					invoked.TypeOfContract                  = TypeOfContract
					invoked.MemberProcessing                = MemberProcessing
					invoked.UseTemplate                     = UseTemplate
					invoked.DoNotCreateDistributorContract  = DoNotCreateDistributorContract
					invoked.DistributorPriority             = DistributorPriority
					invoked.StandardMarkupCalculation       = StandardMarkupCalculation
					if (ContractGroup.GPOIsHealthTrust2021)
						invoked.ParmStartDate               = StartDate

		CreateNewContract is a Set Action
			restricted
			Parameters
				ContractGroup
				ParmGPOSupplier       					is like Supplier
				ParmGPOContractNumber 					is a GPOContractNumber
				ParmSupplier                            is a GPOSupplierID
				TypeOfContract                          is Numeric 1
					States
						Manufacturer                    value is 1
						Distributor                     value is 2
						Service                         value is 3
						MultiTierDistributor            value is 4
				ContractType
				ContractSubtype
				ContractClassification
				ContractSubclassification
				ManufacturerCodeDivision 				is a Manufacturer
				UseGPOContractNumberAsWorkingContract 	is Boolean				
				Contact                  				is like Employee
				MemberProcessing                        is Numeric 1
					States
						ProcessOnlyMappedMembers        value is 1
						ErrorIfAllMembersNotMapped      value is 2
				UseTemplate                             is a ContractImport
				DoNotCreateDistributorContract          is Boolean
				DistributorPriority                     is Numeric 1
				StandardMarkupCalculation               is Boolean
				ParmStartDate                           is Date
			
			Local Fields
				LocalContractImportView		is a ContractImport view	
				
			Instance Selection
				where  (GPOContractNumber = ParmGPOContractNumber
				and   ((ParmStartDate     = StartDate
				and     ParmStartDate entered)
				or      ParmStartDate !entered)
				and    (ParmSupplier !entered
				or      ParmSupplier = ManufacturerSupplier))
			
			Sort Order
				GPOContractNumber
				ContractGPOHeader
			
			Action Rules
			
				GPOContractNumber Set Rules
					Entrance Rules

						LocalGPOSupplier     			= ParmGPOSupplier
						LocalContractGroup              = ContractGroup
						LocalManufacturerSupplier       = ManufacturerSupplier
						LocalDistributorSupplier        = first DistributorWithMarkupRel.ItemSupplier
						LocalStartDate                  = StartDate
						invoke CreateNoRules ContractImport
							assign result to LocalContractImportView
							if (UseTemplate entered)
								invoked.Name            					= UseTemplate.Name
								invoked.Description             			= UseTemplate.Description
								invoked.CurrencyCode            			= UseTemplate.CurrencyCode
								invoked.PerOrderMinimumAmount   			= UseTemplate.PerOrderMinimumAmount
								invoked.PerOrderMaximumAmount   			= UseTemplate.PerOrderMaximumAmount
								invoked.UOMSMustMatch         				= UseTemplate.UOMSMustMatch
								invoked.UOMSMustMatchForSpecials            = UseTemplate.UOMSMustMatchForSpecials
								invoked.UseContractUOM						= UseTemplate.UseContractUOM
								invoked.LifetimeEdit                        = UseTemplate.LifetimeEdit
								invoked.AllowReferenceOfItemsNotOnContract  = UseTemplate.AllowReferenceOfItemsNotOnContract
								invoked.LineLevelBreaks						= UseTemplate.LineLevelBreaks
								invoked.TermsCode							= UseTemplate.TermsCode
								invoked.PoUserField1						= UseTemplate.PoUserField1
								invoked.PoUserField3						= UseTemplate.PoUserField3
								invoked.PoUserField5						= UseTemplate.PoUserField5
								invoked.VendorAgreementReference			= UseTemplate.VendorAgreementReference
								invoked.CommodityCode						= UseTemplate.CommodityCode
								invoked.DefaultAccount						= UseTemplate.DefaultAccount
								invoked.PayablesDistributionCode			= UseTemplate.PayablesDistributionCode
								invoked.DefaultTaxable						= UseTemplate.DefaultTaxable
								invoked.TaxCode								= UseTemplate.TaxCode
								invoked.NotificationPercent                 = UseTemplate.NotificationPercent
								invoked.PreferredContract                   = UseTemplate.PreferredContract
								invoked.EnableClinicalSystemUseLocations    = UseTemplate.EnableClinicalSystemUseLocations
							invoked.ContractGroup       		= ContractGroup
							invoked.GPOContractNumber   		= GPOContractNumber
							if (TypeOfContract.MultiTierDistributor
							and HasDistributorWithMarkup
							and ContractGroup.GPOUsed =2)
								invoked.Supplier                = DistributorSupplierRel.ContractSupplier        
								invoked.ManufacturerSupplier    = first DistributorWithMarkupRel.ItemSupplier
							else
								invoked.Supplier           		= ManufacturerSupplierRel.ContractSupplier
								invoked.ManufacturerSupplier    = ManufacturerSupplier  
							if (UseTemplate.Name !entered)
								invoked.Name               		= ContractDescription
							invoked.ContractType       		    = ContractType
							invoked.ContractSubtype             = ContractSubtype
							invoked.ContractClassification 		= ContractClassification
							invoked.ContractSubclassification   = ContractSubclassification
							if (HPGContractTypeCode = "S2")
								invoked.S2Contract              = true
							invoked.NationalContractNumber      = NationalContractNumber
							if (ManufacturerCodeDivision entered)
								invoked.ManufacturerCodeDivision = ManufacturerCodeDivision
							else
							if  (UseTemplate entered
							and	 UseTemplate.ManufacturerCodeDivision entered)
								invoked.ManufacturerCodeDivision = UseTemplate.ManufacturerCodeDivision
							else
								invoked.ManufacturerCodeDivision = ManufacturerSupplierRel.ManufacturerCodeDivision		
							invoked.GPOSupplier         		= ParmGPOSupplier
							invoked.GPONegotiated       		= true
							invoked.EffectiveDate       		= StartDate
							invoked.ExpirationDate      		= EndDate
							if (TypeOfContract.Manufacturer
							or  TypeOfContract.MultiTierDistributor)
								invoked.ManufacturerOrDistributor.ManufacturerContract = true
							else 
							if (TypeOfContract.Distributor)
								invoked.ManufacturerOrDistributor.DistributorContract = true
							invoked.ContractImportSource        = 2
							if (UseGPOContractNumberAsWorkingContract)
								invoked.WorkingContractID       = GPOContractNumber
								invoked.WorkingContractType     = 4 
							invoked.Contact                     = Contact
							if (TypeOfContract.MultiTierDistributor)
								invoked.MultiTierDistributor    = true
							invoked.SupplierSourceId            = 1
							if (ManufacturerSupplierRel.ContractSupplier.Vendor.InvoiceCurrency entered)
								invoked.CurrencyCode 			= ManufacturerSupplierRel.ContractSupplier.Vendor.InvoiceCurrency
							else
								invoked.CurrencyCode 			= ContractGroup.DefaultCurrency
							if (UseTemplate.Priority !entered)
								invoked.Priority                = 5
							else
								invoked.Priority                = UseTemplate.Priority
							invoked.LineLevelBreaks             = 1
							invoked.CreationInProgress          = true
							invoked.DoNotCreateDistributorContract = DoNotCreateDistributorContract
						LocalContractImport             = LocalContractImportView.ContractImport						

					Exit Rules
						
						LocalContractGroup   		= ContractGroup
						LocalGPOContractNumber      = ParmGPOContractNumber
						LocalContractImport         = LocalContractImportView.ContractImport
						if (ContractGroup.GPOIsHealthTrust
						and TypeOfContract.Manufacturer)
							for each GPOOtherSupplierRel
								LocalManufacturerSupplier = each.ManufacturerSupplier
								LocalSupplier             = ManufacturerSupplierRel.ContractSupplier
								LocalDistributorSupplier  = each.ManufacturerSupplier
								if (ManufacturerSupplierRel.ContractSupplier entered
								and ContractImportDistributorRel !exists
								and SupplierHasItems
								and SupplierHasParticipants
								and DoNotCreateDistributorContract = false)
									invoke Create ContractImportDistributor
										invoked.ContractGroup                                   = ContractGroup
										invoked.ContractImport                                  = LocalContractImport
				            			invoked.GPODistributorSupplier                          = each.ManufacturerSupplier
										invoked.ContractImportDistributor                       = ManufacturerSupplierRel.ContractSupplier
										invoked.Priority                                        = DistributorPriority
						if (ContractGroup.GPOIsHealthTrust2021
						and TypeOfContract.Manufacturer)
							for each ContractGPODistributor2021Rel
								LocalManufacturerSupplier = each.GPOSupplier
								LocalSupplier             = ManufacturerSupplierRel.ContractSupplier
								LocalDistributorSupplier  = each.GPOSupplier
								if (ManufacturerSupplierRel.ContractSupplier entered
								and ContractImportDistributorRel !exists
								and SupplierHasItems
								and DoNotCreateDistributorContract = false)
									invoke Create ContractImportDistributor
										invoked.ContractGroup                                   = ContractGroup
										invoked.ContractImport                                  = LocalContractImport
				            			invoked.GPODistributorSupplier                          = each.GPOSupplier
										invoked.ContractImportDistributor                       = ManufacturerSupplierRel.ContractSupplier
										invoked.Priority                                        = DistributorPriority
										invoked.EffectiveDate                                   = each.ContractDistributorEffectiveDate 
										invoked.ExpirationDate                                  = each.ContractDistributorExpireDate 						
						
						for each MappedContractGPOParticipantRel
							LocalMembership 		= each.GPOMembership
							LocalContractGroup 		= ContractGroup
							LocalGPOContractNumber 	= GPOContractNumber
							LocalCompany            = each.ContractImportGPOMembershipRel.ParticipantLocation.Company                   
							LocalLocation           = each.ContractImportGPOMembershipRel.ParticipantLocation.Location             
							LocalRequestingLocation = each.ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation             
							LocalPricingGroup       = each.ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup             
							if (ContractImportTierMemberRel !exists
							and ContractImportTierMemberByLocationRel !exists)
								invoke IndirectCreate ContractImportTierMember
									invoked.ContractGroup            					= ContractGroup
									invoked.ContractImport           					= LocalContractImport
									invoked.ContractImportTierMember.Company 			= each.ContractImportGPOMembershipRel.ParticipantLocation.Company
									invoked.ContractImportTierMember.Location 			= each.ContractImportGPOMembershipRel.ParticipantLocation.Location
									invoked.ContractImportTierMember.RequestingLocation = each.ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation
									invoked.ContractImportTierMember.PricingGroup 		= each.ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup
									if (TypeOfContract.Manufacturer
									or  TypeOfContract.MultiTierDistributor)
										invoked.Tier                   					= each.TierLevel
									invoked.LocalTierName                               = each.TierName
									invoked.LocalTierDescription                        = each.TierDefinition
									invoked.ContractImportGPOMembership 				= each.GPOMembership
									invoked.GPOContractNumber        					= GPOContractNumber
	                                invoked.EffectiveDate            					= each.StartDate
	                                invoked.ExpirationDate           					= each.EndDate
	                                if (TypeOfContract.Distributor)
	                                	invoked.FromDistributor                         = true
									if (TypeOfContract.Service) 
										invoked.FromService                             = true
									invoked.FromContractCreation                        = true
									invoked.DistributorPriority                         = DistributorPriority
									invoked.DoNotCreateDistributorContract              = DoNotCreateDistributorContract
									invoked.StandardMarkupCalculation                   = StandardMarkupCalculation
							else
							if (ContractImportTierMemberRel exists
							and TypeOfContract.Manufacturer
							and first ContractImportTierMemberRel.EffectiveDate > each.StartDate)
								if (ContractGroup.GPOIsHealthTrust2021)
									LocalTierLevel = each.TierLevel
									if (ContractImportTierRel !exists)
										invoke Create ContractImportTier
											invoked.ContractGroup       = ContractGroup
											invoked.ContractImport      = LocalContractImport
											invoked.ContractImportTier  = each.TierLevel
											invoked.Description         = each.TierDefinition
											invoked.TierTitle           = each.TierName
											invoked.TierMarkupDiscount  = "F" 											
											invoked.GPOContractNumber   = GPOContractNumber
								invoke Update first ContractImportTierMemberRel
									invoked.EffectiveDate        = each.StartDate
									invoked.ExpirationDate       = each.EndDate
									invoked.Tier                 = each.TierLevel
							else
							if (ContractImportTierMemberRel exists
							and TypeOfContract.Manufacturer
							and first ContractImportTierMemberRel.EffectiveDate = each.StartDate
							and ContractGroup.GPOIsVizient
							and first ContractImportTierMemberRel.Tier         != each.TierLevel)
								invoke Update first ContractImportTierMemberRel
									invoked.Tier                 = blank 


							if (ContractImportTierMemberRel !exists
							and ContractImportTierMemberByLocationRel exists)
								if (TypeOfContract.Manufacturer
								and DoNotCreateDistributorContract = false
								and each.ItemSupplier != ContractImportRel.ManufacturerSupplier
								and (ContractImportRel.DerivedToContract !entered
								or  (ContractImportRel.DerivedToContract entered
								and !ContractImportRel.ContractsFromImportNavRel.Contract.ContractStatus.Closed)))
									include CreateDistributorAndDistributorTier

						if (!TypeOfContract.Service)
							invoke AddAndUpdateLines ContractGPOItem
								invoked.ParmContractGroup                               = ContractGroup
								invoked.ParmContractImport                              = LocalContractImport
								invoked.ParmGPOContractNumber                           = GPOContractNumber
					   			invoked.FromContractCreation                            = true
								if (TypeOfContract.Distributor)
									invoked.ParmDistributor                             = true                                    
								invoked.DistributorPriority                             = DistributorPriority
								invoked.DoNotCreateDistributorContract                  = DoNotCreateDistributorContract
								invoked.StandardMarkupCalculation                       = StandardMarkupCalculation

				Instance Rules
					LocalContractGroup     		= ContractGroup  
					LocalTierLevel         		= TierLevel
					LocalGPOContractNumber 		= GPOContractNumber
					LocalManufacturerSupplier 	= ManufacturerSupplier
					LocalContractImport         = LocalContractImportView.ContractImport	
						
					if ((TypeOfContract.Manufacturer
					or  TypeOfContract.MultiTierDistributor)
					and ContractGroup.GPOIsVizient)
						invoke Create ContractImportTier
							invoked.ContractGroup       = ContractGroup
							invoked.ContractImport      = LocalContractImport
							invoked.ContractImportTier  = TierLevel
							invoked.Description         = TierDescription
							invoked.TierTitle           = TierLevel
							if (TypeOfContract.MultiTierDistributor
							and HasDistributorWithMarkup)
								invoked.TierMarkupDiscount  			= "M"
								invoked.TierPricingAmtPct.TierPercent 	= first DistributorWithMarkupRel.MarkupPercent/100
							else 
								invoked.TierMarkupDiscount  = "F" 											
							invoked.GPOContractNumber   = GPOContractNumber

		LoadAndUpdateSuppliers is a Set Action   
			restricted
			Parameters
				SupplierGroup
			
			Action Rules
			
				Instance Rules
					LocalContractGroup     			= SupplierGroup
					LocalManufacturerSupplier       = ManufacturerSupplier
					if (ManufacturerSupplierRel !exists)
						invoke Create ContractImportGPOSupplier					
							invoked.SupplierGroup      			= SupplierGroup
							invoked.ContractImportGPOSupplier   = ManufacturerSupplier
							invoked.Name               			= ContractVendorName
							invoked.TaxID                       = ContractVendorTaxIDNumber

		FindNewDistributorHT is an Instance Action    
			restricted
			Parameters
				ParmContractGroup      is a ContractGroup
				ParmContractImport     is like ContractImport
				AutoUpdateContracts    is Boolean
				
			Action Rules
				LocalContractImport         = ParmContractImport
				LocalContractGroup   		= ParmContractGroup
				LocalGPOContractNumber      = GPOContractNumber
				LocalManufacturerSupplier 	= ManufacturerSupplier
				LocalSupplier             	= ManufacturerSupplierRel.ContractSupplier
				LocalDistributorSupplier  	= ManufacturerSupplier
				if (ManufacturerSupplierRel.ContractSupplier entered
				and ContractImportDistributorRel !exists
				and SupplierHasItems
				and SupplierHasParticipants)
					invoke AddNewDistributor ContractImportDistributor
						invoked.ContractGroup                                   = ParmContractGroup
						invoked.ContractImport                                  = ParmContractImport
            			invoked.GPODistributorSupplier                          = ManufacturerSupplier
						invoked.ContractImportDistributor                       = ManufacturerSupplierRel.ContractSupplier
						invoked.LocalAutoUpdateContracts                        = AutoUpdateContracts
										
		FixGPOContractNumber is a Set Action
			default label is untranslatable
			restricted
			
			Instance Selection
				where (GPOContractNumber = "NULL")

			Action Rules
				
				Instance Rules
					initialize GPOContractNumber
	
