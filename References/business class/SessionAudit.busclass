SessionAudit is a BusinessClass
    prefix is LGAU
    stored in environment



    Ontology
        symbolic key is SessionAudit

    Patterns
        implements CRUD

    Persistent Fields
        SessionId 	is Alpha size 80
        UserName	is Alpha 20	
        ServiceName	is AlphaUpper size 100
        SSODomainName	is AlphaUpper size 20
    	Action 		is Alpha size 20
		HostAddress is Alpha size 100
		TenantId	is Alpha size up to 22
       	Time		is TimeStamp
		Message		is Alpha size 100

	Local Fields
		LocDateForStart is Date

	Derived Fields
		StartOfDay is a NativeField
			type is TimeStamp
			restricted

	Sets
		ByIdentity
            indexed
            duplicates
            Sort Order
                UserName
                ServiceName
                Time
 
 		ByTime
            indexed
            duplicates
            Sort Order
            	Time
                UserName
                ServiceName
                		
    Actions

        Create is an Action

        Delete is an Action
 		Purge is a Purge Action
		
		PurgeAll is a Set Action
			Action Rules
				Instance Rules
					invoke Purge

		PurgeSessionAudit is a Set Action
			Parameters
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3

			Local Fields
				LocalThruTime 	is TimeStamp

			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocDateForStart = (ThruDate + 1 day)
						LocalThruTime = StartOfDay
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThroughEndDateOrOffset"
					else
						LocDateForStart  = current date - (PurgeOffsetDays - 1)
						LocalThruTime = StartOfDay
				
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThroughEndDateAndOffset"

			Instance Selection
				where (Time < LocalThruTime) 
									
			Action Rules
				Instance Rules
					invoke Purge
