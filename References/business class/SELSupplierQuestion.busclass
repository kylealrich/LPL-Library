SELSupplierQuestion is a BusinessClass 
    owned by ss
    prefix is ELSQ

    Ontology
    	symbolic key is SELSupplierQuestion
    	   			    			
    Patterns

    Persistent Fields
		QuestionText									is Text
			sql name is SEQUE
			text searchable
   		ResponseType					is Numeric size 1
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
   				List 				value is 4 
   				YesNo 				value is 5
   				YesNoText			value is 6 
   		Attachment
   		DisplayOrder
   		Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion								is like Question
   		CMQuestion										is Boolean
		SourcingEventLineQuestion
   		QuestionWeighting				
   		LastQuestionValueDisplayOrder					is Numeric 6
   			disable Auditing
   		LastQuestionAnswerDisplayOrder					is Numeric 6
   			disable Auditing

	Transient Fields
		NewDisplayOrder				is a DisplayOrder
		CreateFromRepository		is Boolean

	Local Fields
		LocalConditionalQuestion	is like Question
		LocalListValue				is like SourcingEventLineQuestionValue
		LocalYesOrNo				is Numeric 1 
           	States
           		Yes	value is 1
           		No	value is 2 
		FromRepository				is Boolean 
		FromConditional				is Boolean		
		LocalFromConditional		is Boolean
		LocalQuestion				is like Question

   	Derived Fields
   		IsQuestionRequired is a ConditionalField
   			type is Alpha 2
   			if (!QuestionRel.ResponseRequired)
   				blank
			else
				"*"

   		AnswerMessage is a MessageField
   			restricted
   			"Answer"

   		RequiredQuestion is a MessageField
   			"Required"
   		
		DerivedQuestionText is a DerivedField
			type is Alpha 250
			return QuestionText

   		AnswerTextWithRequired is a StringField
 			type is Text
   			restricted
   			IsQuestionRequired 
   			" "
   			AnswerMessage

 		IsAttachmentRequired is a ConditionalField
 			type is Alpha 2
   			restricted
   			if (!QuestionRel.AlwaysRequireResponseAttachment)
   				blank
			else
				"*"
	
		AttachDocumentMessage is a MessageField
   			restricted
			"AttachDocument"

   		AttachmentTextWithRequired is a StringField
 			type is Text
   			restricted
   			IsAttachmentRequired
   			" "
   			AttachDocumentMessage

	Field Groups
   			
   	Conditions
		
		IsYesNo
   			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)
		
		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)
		
		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)
		
		HasListValues
   			restricted
			when (SourcingEventLineQuestionValueRel exists)
		
		EligibleForCM
   			restricted
			when (SourcingEvent.ContractOutputExists
			or    SourcingEvent.ContractOutput)
			
		HasAttachment
   			restricted
			when (Attachment entered)
		
		QuestionEntered
   			restricted
			when (Question entered)
			
		RequiredQuestionNoAnswer
			restricted
			when ((QuestionRel.ResponseRequired
			or     SourcingEventLineQuestionRel.ResponseRequired)
			and   !QuestionIsAnswered)

		ResponseRequired
            when (Question.ResponseRules.ResponseRequiredAndAttachmentRequired
            or    Question.ResponseRules.ResponseRequired
            or    Question.YesNoResponseRules.ResponseRequired
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
		AdHocResponseRequired
            when (SourcingEventLineQuestionRequiredRel exists)
        
		QuestionIsAnswered
			restricted
			when (SELSupplierQuestionResponseRel exists)

		DisplayAnswerRequiredText 
			restricted
			when (RequiredQuestionNoAnswer
			or    MayRequireAttachmentNoAttachment)

		MayRequireAttachmentNoAttachment 
			restricted 
			when (QuestionRel.MayRequireResponseAttachment
			and  !AnswerAttachmentExists)

		HasAnySourcingEventLineConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		RepositoryQuestion 
			restricted 
			when (Question entered)

		RepositoryHasConditionalQuestions	
			restricted 
			when (RepositoryConditionalQuestionRel exists)

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)
		
		HasDirectConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		AnswerAttachmentExists
			restricted
			when (SourcingEventQuestionAnswerAttachmentRel exists)

		AnyQuestionAnswers
   			restricted
			when (AnyQuestionAnswersRel exists)
			
        AlwaysRequireResponseAttachment
   			restricted
            when (Question.ResponseRules.ResponseRequiredAndAttachmentRequired
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)

		AllowResponseAttachment
			restricted
			when (QuestionRel.AllowResponseAttachment)
            
		AllowResponseEventLineQuestionAttachment
			restricted
			when (SourcingEventLineQuestionRel.AllowResponseAttachment
			and   Question = 0)
            
	Relations
		AnyQuestionAnswersRel is a SELSupplierQuestionResponse set

		QuestionResponseAttachmentsRel 
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
			Instance Selection
				where (related.HasQuestionAnswerAttachment)
				
		SourcingEventLineQuestionValueRel 
			one-to-many relation to SourcingEventLineQuestionValue
			Field Mapping uses symbolic key
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventLine				= SourcingEventLine
				related.SourcingEventLineQuestion		= Question
				
		SELSupplierQuestionResponseRel
			one-to-many relation to SELSupplierQuestionResponse
			Field Mapping uses ByQuestion
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent
				related.SourcingEventLine					= SourcingEventLine
				related.SELSupplierQuestion					= SELSupplierQuestion

		SourcingEventLineQuestionRel 
			one-to-one relation to SourcingEventLineQuestion 
			Field Mapping uses symbolic key
				related.Company						= Company 
				related.SourcingEvent				= SourcingEvent
				related.SourcingEventLine			= SourcingEventLine 
				related.SourcingEventLineQuestion	= SELSupplierQuestion.SourcingEventLineQuestion

		SourcingEventLineQuestionRequiredRel 
			one-to-many relation to SourcingEventLineQuestion 
			Field Mapping uses symbolic key
				related.Company						= Company 
				related.SourcingEvent				= SourcingEvent
				related.SourcingEventLine			= SourcingEventLine 
				related.SourcingEventLineQuestion	= SELSupplierQuestion.SourcingEventLineQuestion
			Instance Selection
				where (related.ResponseRequired)

		SameRepositoryQuestionRel 
			one-to-many relation to SourcingEventLineQuestion 
			Field Mapping uses ByQuestion
				related.Company					= Company 
				related.SourcingEvent			= SourcingEvent
				related.SourcingEventLine		= SourcingEventLine 
				related.Question				= LocalConditionalQuestion

		SourcingEventQuestionAnswerAttachmentRel is a ContractQuestionAnswer set
			Instance Selection
				where (related.Attachment entered)

		CreatedFromQuestionRel 
			one-to-one relation to SourcingEventLineQuestion 
			Field Mapping uses symbolic key 
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent
				related.SourcingEventLine					= SourcingEventLine 
				related.SourcingEventLineQuestion			= CreatedFromQuestion

		ConditionalQuestionsDirectRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		RepositoryConditionalQuestionRel    
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		QuestionRel 
			one-to-one relation to Question 
			Field Mapping uses symbolic key
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question

		RepositoryListValuesRel
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 
		
		SourcingEventLineQuestionValuesRel
			one-to-many relation to SourcingEventLineQuestionValue 
			Field Mapping uses symbolic key 
				related.Company						= Company
				related.SourcingEvent				= SourcingEvent
				related.SourcingEventLine			= SourcingEventLine
				related.SourcingEventLineQuestion	= SELSupplierQuestion.SourcingEventLineQuestion

		QuestionListValueRel 
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= LocalQuestion

		SubmittedQuestionResponsesRel
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventQuestion   = SourcingEventLineQuestion
			Instance Selection
				where (related.SourcingEventResponse.Status.Submitted)
				
		SubmittedResponsesRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses ByEvent
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
			Instance Selection
				where (related.Status.Submitted)

		RequiredLineAttachmentsRel
			one-to-many relation to SourcingEventLineQuestion
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventLine		= SourcingEventLine
			Instance Selection
				where (related.AlwaysRequireResponseAttachment)

	Sets
		ByDisplayOrder
			duplicates
 			Sort Order
 				Company
 				SourcingEvent
				SourcingEventLine
 				DisplayOrder

		ByQuestion
			Sort Order
				NotifiedSupplier.SupplierGroup
	 			NotifiedSupplier.Supplier
	 			NotifiedSupplier.SupplierSourceId
				Company
    			SourcingEvent
				SourcingEventLine
    			Question
				SELSupplierQuestion
    			
    	ByQuestionFirst
    		Sort Order
				NotifiedSupplier
    			Question
    			Company
    			SourcingEvent
				SourcingEventLine
    			SELSupplierQuestion
 		
   	Field Rules
   		QuestionText
   			required

   	Rule Blocks
 					   					
 	Actions
		Create is a Create Action
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEvent.LastQuestionDisplayOrder 

			Action Rules

				LocalFromConditional = FromConditional 

			Exit Rules

				if (ResponseType.List)
					if (Question entered)
						for each RepositoryListValuesRel
							invoke CreateDisplay SELSupplierQuestionValue
								invoked.NotifiedSupplier.SupplierGroup		= NotifiedSupplier.SupplierGroup
								invoked.NotifiedSupplier.Supplier			= NotifiedSupplier.Supplier
								invoked.NotifiedSupplier.SupplierSourceId	= NotifiedSupplier.SupplierSourceId
								invoked.Company								= Company
								invoked.SourcingEvent						= SourcingEvent
								invoked.SourcingEventLine					= SourcingEventLine
								invoked.SELSupplierQuestion					= SELSupplierQuestion
								invoked.SELSupplierQuestionValue			= each.QuestionListValue
								invoked.DisplayOrder						= each.DisplayOrder
								fill in fields from each
					else
						for each SourcingEventLineQuestionValuesRel
							invoke CreateDisplay SELSupplierQuestionValue
								invoked.NotifiedSupplier.SupplierGroup		= NotifiedSupplier.SupplierGroup
								invoked.NotifiedSupplier.Supplier			= NotifiedSupplier.Supplier
								invoked.NotifiedSupplier.SupplierSourceId	= NotifiedSupplier.SupplierSourceId
								invoked.Company								= Company
								invoked.SourcingEvent						= SourcingEvent
								invoked.SourcingEventLine					= SourcingEventLine
								invoked.SELSupplierQuestion					= SELSupplierQuestion
								invoked.SELSupplierQuestionValue			= each.SourcingEventLineQuestionValue
								invoked.DisplayOrder						= each.DisplayOrder
								fill in fields from each

		CreateFromWizard is a Create Action   
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEvent.LastQuestionDisplayOrder 

			Action Rules
				if (!SourcingEvent.TwoStepBiddingUsed
				and !SourcingEvent.BestAndFinalOffer)
					constraint (SourcingEvent.InActionableState)
						"CannotAddQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				else
					if (SourcingEvent.TwoStepBiddingUsed)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterStepTwo_(Pricing)HasStarted"
					else 
					if (SourcingEvent.BestAndFinalOffer)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterBestAndFinalProcessHasStarted"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddQuestionWhileEventIsPendingEventApproval"

				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddQuestion)
					invoke Amend Open.Notified SourcingEvent
			
			Exit Rules
		
		Update is an Update Action
			valid when (SourcingEvent.InEditableState)
		   	Action Rules
				if (Question entered
				and (QuestionText changed
				or	 Attachment changed))
					initialize Question
				
		Delete is a Delete Action
			valid when (SourcingEvent.InEditableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteQuestionWhileEventIsPendingEventApproval"

		DeleteConditionalQuestions is a Set Action  
			restricted 
			Parameters 
			 	ParmCompany					is a SourcingCompany 
			 	ParmSourcingEvent			is a SourcingEvent
				ParmSourcingEventLine		is a SourcingEventLine 
			 	ParmOriginalQuestion		is like SELSupplierQuestion
				ParmLastOldQuestion			is like SELSupplierQuestion
				ParmNotifiedSupplier		is a NotifiedSupplier   

			Instance Selection 
			 	where (ParmNotifiedSupplier					= NotifiedSupplier
			 	and    ParmCompany							= Company
			 	and    ParmSourcingEvent					= SourcingEvent
				and    ParmSourcingEventLine				= SourcingEventLine 
			 	and    ParmOriginalQuestion					= CreatedFromQuestion
				and    SELSupplierQuestion					<= ParmLastOldQuestion)

			Action Rules
			 	Instance Rules
					invoke Delete		
