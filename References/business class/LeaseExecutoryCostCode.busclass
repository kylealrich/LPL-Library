LeaseExecutoryCostCode is a BusinessClass
    owned by lm
    prefix is EXA
    classic name is LMEXECASGN
    default label is "NonLeaseComponent"

    Ontology
        symbolic key is LeaseExecutoryCostCode
            classic set name is EXASET2
            classic name for ExecutoryCostCode is EXEC-CODE

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        ExecutoryAccount    is a FinanceCodeBlockFull
            classic name for ExecutoryAccount.ToAccountingEntity is PT-COMPANY
            classic name for ExecutoryAccount.AccountingUnit is ACCT-UNIT
            classic name for ExecutoryAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for ExecutoryAccount.Project is ACTIVITY

        LeaseAllocationCode
            classic name is ALLOC-CD
        Taxable				is Boolean								
        TaxCode
        TaxUsageCode
            classic name is TAX-USAGE-CD
        VariableCost		        is Boolean
        CombineWithLeasePayment		is Boolean
        DoNotCreateAPInvoice		is Boolean
			default label is "DoNotCreatePayablesInvoice"

    Relations

        LeasePaymentDetailRel
            one-to-many relation to LeasePaymentDetail
            Field Mapping uses Set5
                related.Company 								= Company
                related.Lease   								= Lease
                related.LeasePaymentDetail.ExecutoryCostCode 	= ExecutoryCostCode

        ExecutoryCostCodeDetailRel 
         	one-to-many relation to ExecutoryCostCodeDetail      
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= Company.FinanceEnterpriseGroup
                related.ExecutoryCostCode 						= ExecutoryCostCode 
                related.Company 								= Company

        LeasePaymentsRel
            one-to-one relation to LeasePayment
            Field Mapping uses symbolic key
            	related.Company		= Company
                related.Lease 		= Lease
                related.Vendor   	= Lease.Lessor

	Conditions
	
		HasReleasedLeaseAllocationCode
			restricted
			when (LeaseAllocationCode entered
			and   LeaseAllocationCode.IsReleased)

        LeaseHasNoLeaseFunds
            restricted
            when (!Lease.LeaseFundExists)
			
    Sets

        Set1
            indexed
            Sort Order
                Company
                Lease
                ExecutoryCostCode

	Field Rules





				
		ExecutoryAccount		
			if (!LeaseAllocationCode entered)
				default to first ExecutoryCostCodeDetailRel.DistributionAccount
		
			constraint (!LeaseAllocationCode entered)
				"CanEnterEitherANonLeaseComponentAccountOrAllocationCode"
			
			if (!ExecutoryAccount.ToAccountingEntity entered)
				initialize ExecutoryAccount.Ledger
					
		LeaseAllocationCode			
			constraint (!ExecutoryAccount entered)
				"CanEnterEitherANonLeaseComponentAccountOrAllocationCode"

        DoNotCreateAPInvoice
            if (DoNotCreateAPInvoice)
                constraint (LeasePaymentsRel.LeaseInvoiceAccrualAccount entered)
                    "ToNotCreatePayablesInvoicesTheLeaseInvoiceAccrualAccountMustBeEntered"

	Actions
		Create is a Create Action
			Exit Rules
				if (LeaseAllocationCode not entered)
					constraint (ExecutoryAccount entered)
						"NonLeaseComponentAccountRequired"
                if (!LeaseHasNoLeaseFunds)
                    constraint (LeaseAllocationCode not entered)
                        "AllocationCodeNotAllowedWhenLeaseFundsExist"							

		Update is an Update Action
			Exit Rules
				if (LeaseAllocationCode not entered)
					constraint (ExecutoryAccount entered)
						"NonLeaseComponentAccountRequired"	
                if (!LeaseHasNoLeaseFunds)
                    constraint (LeaseAllocationCode not entered)
                        "AllocationCodeNotAllowedWhenLeaseFundsExist"
                        											
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted			
