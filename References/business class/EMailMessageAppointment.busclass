EMailMessageAppointment is a BusinessClass
    owned by la
    prefix is EMMP

    Ontology
		part of EMailMessageQueue
    		relative key is Sequence is Numeric size 6

	Patterns
    	implements CRUD
		implements CreateStamp
		disable Auditing
		disable EffectiveDated
		
    Persistent Fields
    	Id				is Alpha size up to 250
		Type			is Numeric size 1
			States
				Request	value is 1
				Cancel  value is 2
		Location		is Alpha size up to 250
		StartTime		is TimeStamp
		EndTime			is TimeStamp
		Duration		is Numeric size 6
		DurationType
		Reminder		is Numeric size 6
		ReminderType	is a DurationType
		Name			is Alpha size up to 250
		SendType		is Numeric size 1
			States
				Meeting 				value is 1
				Attachment				value is 2
				Message					value is 3
				AppointmentAttachement	value is 4
					default label is "AppointmentAttachment"
        		AppointmentMessage		value is 5 

	Field Rules
		Id
			required
		Type
			required
		EndTime
			if (Type.Request 
			and !Duration entered)
				constraint (EndTime entered)
					"MustSupplyEndTimeOrDuration"
		DurationType
			if (Duration entered)
				required
			else
				cannot be entered
		
		ReminderType
			if (Reminder entered)
				required
			else
				cannot be entered				

	Conditions
    	CanUpdate
    		default label is untranslatable
    		when (EMailMessageQueue.Status.Hold
    		or    EMailMessageQueue.Status.Failed) 

		CanDelete
			default label is untranslatable
    		when (EMailMessageQueue.Status.Hold
    		or 	  EMailMessageQueue.Status.Complete	
    		or    EMailMessageQueue.Status.Failed)  
    		
	Derived Fields
		DurationString is a DerivedField
			type is Alpha size 14
			return Duration + " " + DurationType
		
		ReminderString is a DerivedField
			type is Alpha size 14
			return Reminder + " " + ReminderType
    													
	Actions
		Create is a Create Action			
			allow anonymous access			
			restricted
			bypass field rules
			Action Rules
				autosequence Sequence
					minimize contention
					
		Update is an Update Action
			valid when (CanUpdate)
		
		Delete is a Delete Action
			valid when (CanDelete)
			
		PurgeRecord is a Purge Action
			valid when (CanDelete)
			confirmation required
				"ThisWillPhysicallyDeleteTheRecord.AreYouSureYouWantToPurge?"
