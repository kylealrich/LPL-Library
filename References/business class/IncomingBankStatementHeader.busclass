IncomingBankStatementHeader is a BusinessClass
	owned by cashmgmt
	prefix is BSDH

	Ontology
		symbolic key is IncomingBankStatementHeader

	Patterns
		implements DynamicCreation		

    Persistent Fields
		CashManagementGroup

    Derived Fields
		DerivedExpectedStatementCount is a DerivedField
			type is Numeric 4
			default label is "ExpectedStatements"
			if (!IsBankDay)
				return 0
			else
				return instance count of ExpectedIncomingBankStatementDetailRel

		DerivedMissingStatementCount is a DerivedField
			type is Numeric 4
			default label is "MissingStatements"
			if (!IsBankDay)
				return 0
			else
			if (MissingIncomingBankStatementDetailRel exists)
				return instance count of MissingIncomingBankStatementDetailRel
			else
				return 0

		DerivedCompletedStatementCount is a DerivedField
			type is Numeric 4
			default label is "CompletedStatements"
			if (CompletedIncomingBankStatementDetailRel exists)
				return instance count of CompletedIncomingBankStatementDetailRel
			else
				return 0

		PercentCompleted is a DerivedField
			type is Percent 3
			if (DerivedExpectedStatementCount > 0)
				return (DerivedCompletedStatementCount / DerivedExpectedStatementCount)
			else
				return 0

		DerivedCalendarStatus is a DerivedField
			type is Alpha up to 250
			if (!IsBankDay)
				return "Non Bank Day"
			else
			if (NoStatementReceived)
				return "No Statements Received"
			else
			if (MissingStatement)
				return "Missing Statements"
			else
			if (AllStatementsReceived)
				return "All Statements Received"
			else
				return ""

		DerivedRedAlertMouseOverText is a DerivedField
			type is Alpha up to 250
			if (NoStatementReceived)
				return "No Statements Received"
			else
			if (MissingStatement)
				return PercentCompleted + " Statements Completed"
			else
				return ""

		ReportStartDate is a DerivedField
			type is Date
			return last IncomingBankStatementHeaderRel.IncomingBankStatementHeader - 7
		
    Conditions
    	MissingStatement
    		restricted
    		when (IsBankDay
    		and	  MissingIncomingBankStatementDetailRel exists)

		NoStatementReceived
			restricted
			when (IsBankDay
			and   ReceivedIncomingBankStatementDetailRel not exists)
		
		AllStatementsReceived 
			restricted
			when (IsBankDay
			and   IncomingBankStatementDetail set exists
			and	  MissingIncomingBankStatementDetailRel not exists)
		
		HasStatement 
			restricted
			when (IncomingBankStatementDetail set exists)
		
		NonBankDayHasStatement 
			restricted
			when (!IsBankDay
			and	  IncomingBankStatementDetail set exists)
						
		IsBankDay
			when (SystemCalendarDateRel.IsBankDay)

		MissingOrNoStatementReceived 
			restricted
			when (MissingStatement
			or	  NoStatementReceived)

	Relations
		ReceivedIncomingBankStatementDetailRel
			one-to-many relation to IncomingBankStatementDetail
			Field Mapping uses ByStatus
				related.EnterpriseGroup				= EnterpriseGroup
				related.SystemCalendar				= SystemCalendar
				related.IncomingBankStatementHeader = IncomingBankStatementHeader
			Instance Selection
				where (related.Status.Completed
				or	   related.Status.Excluded
				or	  (related.Status.Missing
				and	   related.MissingStatus.ReceivedNotImported)
				and    related.SecurityGroupAllowsAccess)
				
		ExcludedIncomingBankStatementDetailRel
			one-to-many relation to IncomingBankStatementDetail
			Field Mapping uses ByStatus
				related.EnterpriseGroup				= EnterpriseGroup
				related.SystemCalendar				= SystemCalendar
				related.IncomingBankStatementHeader = IncomingBankStatementHeader
				related.Status						= 3 
			Instance Selection
				where (related.SecurityGroupAllowsAccess)
				
		CompletedIncomingBankStatementDetailRel
			one-to-many relation to IncomingBankStatementDetail
			Field Mapping uses ByStatus
				related.EnterpriseGroup				= EnterpriseGroup
				related.SystemCalendar				= SystemCalendar
				related.IncomingBankStatementHeader = IncomingBankStatementHeader
				related.Status						= 2 
			Instance Selection	
				where (related.SecurityGroupAllowsAccess)

		MissingIncomingBankStatementDetailRel
			one-to-many relation to IncomingBankStatementDetail
			Field Mapping uses ByStatus
				related.EnterpriseGroup				= EnterpriseGroup
				related.SystemCalendar				= SystemCalendar
				related.IncomingBankStatementHeader = IncomingBankStatementHeader
				related.Status						= 1 
			Instance Selection
				where (related.SecurityGroupAllowsAccess)
				
		ExpectedIncomingBankStatementDetailRel
			one-to-many relation to IncomingBankStatementDetail
			Field Mapping uses ByStatus
				related.EnterpriseGroup				= EnterpriseGroup
				related.SystemCalendar				= SystemCalendar
				related.IncomingBankStatementHeader = IncomingBankStatementHeader
			Instance Selection
				where (!related.Status.Excluded
				and related.SecurityGroupAllowsAccess) 

		SystemCalendarRel
			one-to-one relation to SystemCalendar
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= EnterpriseGroup
				related.SystemCalendar 		= SystemCalendar

		SystemCalendarDateRel
			one-to-one relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= EnterpriseGroup
				related.SystemCalendar 		= SystemCalendar
				related.SystemCalendarDate	= IncomingBankStatementHeader

		IncomingBankStatementHeaderRel
			one-to-many relation to IncomingBankStatementDetail
			Field Mapping uses symbolic key
				related.EnterpriseGroup		= EnterpriseGroup
				related.SystemCalendar		= SystemCalendar

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			refresh and lock this instance

		Delete is a Purge Action
			restricted
