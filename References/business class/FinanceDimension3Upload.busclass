FinanceDimension3Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD3UP   
    
    Ontology
    	symbolic key is FinanceDimension3Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension3			is like FinanceDimension3
    	FinanceDimension3				is like FinanceDimension3
		SequenceDimension3				is like FinanceDimension3
		ParentSequenceDimension3		is like FinanceDimension3
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode
#ifdef module tx
		ProductTaxCategory		
#endif
#ifndef module tx
		ProductTaxCategory	is AlphaUpper size 15
#endif
		RevenuePartialAccount	is a FinanceCodeBlockNoProjectFD2
			default label is "PartialRevenueFinanceStructure"

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension3Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension3 is a DerivedField
			type is like FinanceDimension3
			restricted
			if (ParentFinanceDimension3 entered)
				return DerivedParentFinanceDimension3String
			else
				return TopNodeString

		DerivedSequenceParentDimension3 is a DerivedField
			type is like FinanceDimension3
			restricted
			if (ParentSequenceDimension3 entered)
				return ParentSequenceDimension3
			else
				return DerivedParentFinanceDimension3
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension3Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension3String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension3Structure.StructureSequence
			"_"
			ParentFinanceDimension3
		
		DerivedSummaryFinanceDimension3 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension3Structure.StructureSequence
			"_"
			FinanceDimension3

		DerivedSummarySequenceDimension3 is a DerivedField
			type is like FinanceDimension3
			restricted
			if (SequenceDimension3 entered)
				return SequenceDimension3
			else 
				return DerivedSummaryFinanceDimension3
			
	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
		PostingInEnterpriseStructureBOLDimension3
			restricted
			when (RecordType.Posting
			and   FinanceDimension3Structure = FinanceEnterpriseGroup.EnterpriseDim3Structure
			and   FinanceEnterpriseGroup.BillingOverrideLevel.Dimension3)
		InEnterpriseStructureBOLDimension3
			restricted
			when (FinanceDimension3Structure = FinanceEnterpriseGroup.EnterpriseDim3Structure
			and   FinanceEnterpriseGroup.BillingOverrideLevel.Dimension3)

 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension3Structure
				FinanceDimension3
				FinanceDimension3Upload.RunGroup
				FinanceDimension3Upload.Sequence
 	
	Relations
		FinanceDimension3HierarchyRel
			one-to-one relation to FinanceDimension3Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension3Structure = FinanceDimension3Structure
				related.FinanceDimension3		   = FinanceDimension3
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension3Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension3Structure = FinanceDimension3Structure
			Instance Selection
				where (related.FinanceDimension3Upload.RunGroup = FinanceDimension3Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 8
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 8
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension3
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"





    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension3Upload.Sequence entered)
					FinanceDimension3Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension3Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted

		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension3Structure		is a FinanceDimension3Structure
					default label is "FinanceDimension3Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension3Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension3	is a FinanceDimension3
				DynamicRulesExist		is Boolean		
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension3Structure	    = PrmFinanceDimension3Structure
				and    FinanceDimension3Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension3Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension3Structure
				FinanceDimension3Upload.RunGroup
				FinanceDimension3Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
										
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension3 = FinanceDimension3
					else
						LocalFinanceDimension3 = DerivedSummarySequenceDimension3
					if (LocalFinanceDimension3 exists)
						if  (RecordType.Posting
						and !FinanceDimension3HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension3
								resume on error
									LocalErrorMessage		= error message		
								invoked.PrmStructure		= FinanceDimension3Structure		
								invoked.PrmParentDimension  = DerivedSequenceParentDimension3
						else					
							if (DerivedSequenceParentDimension3 != LocalFinanceDimension3.DerivedParentDimension)
								invoke Move LocalFinanceDimension3
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension3
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension3
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension3 
										except invoked.ParentFinanceDimension3
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension3
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension3
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension3
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension3 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension3
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension3Structure		is a FinanceDimension3Structure
					default label is "FinanceDimension3Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension3Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension3Structure	    = PrmFinanceDimension3Structure
				and    FinanceDimension3Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension3Structure   is a FinanceDimension3Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension3Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension3Structure	    = PrmFinanceDimension3Structure
				and    FinanceDimension3Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
