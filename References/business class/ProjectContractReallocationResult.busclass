ProjectContractReallocationResult is a BusinessClass
	sql name is "ProjConRealRslt"
	owned by Projects
	prefix is PJCRR
	
	Ontology
		part of ProjectContract
			relative key is SequenceNumber
	
	Patterns
		disable Auditing
	
	Persistent Fields
		PostingDate				is Date
		JournalizeGroup
		TransactionCount		is like SequenceNumber
			default label is "TransactionsProcessed"
		RunTime					is TimeStamp
		
    Conditions
		JournalizeGroupEntered
			restricted
			when (JournalizeGroup entered)

		JournalControlExists
			restricted
			when (JournalizeGroup entered
			and   GeneralLedgerJournalControlRel exists)
			
		InterfaceTransactionsExist
			restricted
			when (JournalizeGroup entered
			and   GLTransactionInterfaceRel exists)

		InterfaceResultsExist
			restricted
			when (JournalizeGroup entered
			and   GLTransactionInterfaceResultRel exists)
			
    Relations
    	GLTransactionJournalizeGroupRel
    		one-to-many relation to GLTransactionDetail
    		Field Mapping uses ByJournalizeGroup
    			related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
    			related.JournalizeGroup			= JournalizeGroup
    			
		GeneralLedgerJournalControlRel
			one-to-one relation to GeneralLedgerJournalControl
			Field Mapping uses ByJournalUniqueID
				related.UniqueID = first GLTransactionJournalizeGroupRel.RelatedJournalTransaction(GeneralLedgerTransaction).GeneralLedgerJournalControl.UniqueID

		GLTransactionInterfaceRel
			one-to-many relation to GLTransactionInterface
			Field Mapping uses ByRunGroupEntity
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.GLTransactionInterface.RunGroup = JournalizeGroup
				
		GLTransactionInterfaceResultRel
			one-to-many relation to GLTransactionInterfaceResult
			Field Mapping uses ByRunGroup
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.RunGroup				= JournalizeGroup
			
	Sets
		ByRunTime
			Sort Order
				FinanceEnterpriseGroup
				RunTime descending
				ProjectContract
				SequenceNumber
										
	Field Rules
		SequenceNumber
			autosequence
			
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			valid when (JournalizeGroupEntered)

		Purge is a Purge Action
			restricted
			
