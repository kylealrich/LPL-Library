MenuSecurity is a BusinessClass
    owned by security
    prefix is SECMS
    framework type is Security

	Ontology
		symbolic key is MenuSecurity

	Patterns
		implements CRUD
		implements SecurityCache
		disable AuditIndex
		implements AuditLogEntryActions

	Persistent Fields
        Status 			                is a UserObjectStatus
        Description		                is a ConfigDescription
		Configuration                   is Text
        DeliveredConfiguration          is Text
		Delivered 		                is Boolean
        ItemsToMerge                    is Text
        CopiedFrom                      is a MenuSecurity
        LastReviewed                    is TimeStamp
            protected
        LastReviewedBy                  is like Actor
            protected
        AutoMerge                       is Boolean

    Conditions
        CanDelete
            default label is untranslatable
            when (not Delivered or DeveloperMode)

        CanEditDelivered
            default label is untranslatable
            when (DeveloperMode or InternalExecution)

        Copied
            when (CopiedFrom.Role != blank)

        Configured

            when ((Delivered and ConfigurationSorted != DeliveredConfigurationSorted)
            or (!Delivered and Copied and RelCopiedFrom.Delivered and ConfigurationSorted != RelCopiedFrom.DeliveredConfigurationSorted))

        HasSuggestion
            when (BasedOnDelivered and ConfigurationSorted != Suggested)

        CopiedFromDifferent
            when (Copied and CopiedFrom.ConfigurationSorted != ConfigurationSorted)

        BasedOnDelivered
            when (Delivered or RelCopiedFrom.Delivered)

        IsNewDelivered
            when (BasedOnDelivered and DeliveredSince = DeliveredConfigurationLastChanged)

        IsChangedDelivered
            when (BasedOnDelivered and DeliveredSince != DeliveredConfigurationLastChanged)

        HasUnreviewedChanges
            when (BasedOnDelivered and DeliveredConfigurationLastChanged > LastReviewed corporate date)

        RoleUsedByActors
            when (RelActiveActorsInRole exists)

        NeedsReview
            when (InUse and HasUnreviewedChanges)

        InUse
            when (Status.Active and RoleUsedByActors)

        AutoUpdateEnabled
            when (AutoMerge)

    Local Fields
        LocSuggestedCalc is Boolean
        LocSuggested is Text
        LocRole is a Role

    Transient Fields
        BasedOnDeliveredPolicy is a MenuSecurity
            derive value from CalcBasedOnDeliverPolicy

    Derived Fields
        CalcBasedOnDeliverPolicy is a DerivedField
            type is Text
            restricted
            if (Delivered)
                return MenuSecurity
            if (RelCopiedFrom.Delivered)
                return RelCopiedFrom.MenuSecurity
            return blank

        LastDeliveredInfoMsg is a DerivedField
            type is Text




            if (Delivered)
                if (IsNewDelivered)
                    return LastDeliveredNewInfoMsg
                return LastDeliveredChangedInfoMsg
            if (Copied and CopiedFrom.Delivered)
                if (CopiedFrom.IsNewDelivered)
                    return LastDeliveredNewInfoMsg
                return LastDeliveredChangedInfoMsg
            return blank

        LastDeliveredNewInfoMsg is a MessageField
            "NotReviewedSinceDeliveredPolicyAdded"

        LastDeliveredChangedInfoMsg is a MessageField
            "NotReviewedSinceDeliveredPolicyChanged"

        DeliveredSince is a DerivedField
            type is TimeStamp



            if (Delivered)
                return Delivered date last changed to true
            if (Copied and CopiedFrom.Delivered)
                return CopiedFrom.Delivered date last changed to true
            return blank

        DeliveredConfigurationLastChanged is a DerivedField
            type is Date



            if (Delivered)
                return DeliveredConfiguration date last changed
            if (Copied and CopiedFrom.Delivered)
                return CopiedFrom.DeliveredConfiguration date last changed
            return blank

        Suggested is a NativeField
            type is Text

        SuggestionMsg is a MessageField
            "SuggestionIsBasedOnUnreviewedChangesMadeToTheDeliveredPolicy_'<BasedOnDeliveredPolicy.MenuSecurity.Role>'"

		DerivedLastChange is a MessageField
            "LastChanged<update stamp.timestamp>By<update stamp.actor>"

        NoRestrictedItems is a MessageField
            "DoesNotRestrictAccessTo_Navigation_Items"

        ConfiguredMF is a MessageField
            "Configured"

        DeliveredMF is a MessageField
            "Delivered"

        PolicyTag is a DerivedField
            type is Alpha 200
            if (Configured)
                return ConfiguredMF
            if (Delivered)
                return DeliveredMF
            else
                return ""

        DeveloperMode is a NativeField
            type is Boolean
            restricted

        ConfigurationSorted is a NativeField
            type is Text

        DeliveredConfigurationSorted is a NativeField
            type is Text

        ConfigurationCompare is a DerivedField
            type is LPL
            if (ConfigurationSorted entered)
                return ConfigurationSorted
            return NoRestrictedItems

        DeliveredConfigurationCompare is a DerivedField
            type is LPL
            if (!Delivered and Copied)
                if (RelCopiedFrom.DeliveredConfigurationSorted entered)
                    return RelCopiedFrom.DeliveredConfigurationSorted
                return NoRestrictedItems
            if (DeliveredConfigurationSorted entered)
                return DeliveredConfigurationSorted
            return NoRestrictedItems

        CopiedFromConfigurationCompare is a DerivedField
            type is LPL
            if (Copied and RelCopiedFrom.Configuration entered)
                return RelCopiedFrom.ConfigurationCompare
            return NoRestrictedItems

        InternalExecution is a NativeField
		    type is Boolean
		    restricted

 	Relations
        RelActiveActorsInRole
            one-to-many relation to ActorRole
            Field Mapping uses ByRoleAndActor
                related.ActorRole.Role = MenuSecurity.Role



 	    RelBasedOnDeliveredPolicy
            one-to-one relation to MenuSecurity
            valid when (BasedOnDelivered)
            Field Mapping uses symbolic key
                related.MenuSecurity.WebApp = BasedOnDeliveredPolicy.WebApp
                related.MenuSecurity.Role = BasedOnDeliveredPolicy.Role

        RelCopiedFrom
            one-to-one relation to MenuSecurity
            valid when (Copied)
            Field Mapping uses symbolic key
                related.MenuSecurity.WebApp = CopiedFrom.WebApp
                related.MenuSecurity.Role = CopiedFrom.Role

        RelAllRelatedWebAppRoles
            one-to-many relation to MenuSecurity
            Field Mapping uses ByWebApp
                related.MenuSecurity.WebApp = MenuSecurity.WebApp

        RelCopiedFromRoles
            one-to-many relation to Role
            Field Mapping uses symbolic key
                related.Role = RelAllRelatedWebAppRoles.MenuSecurity.Role
            Instance Selection
                where (related.Role != MenuSecurity.Role)

        RelLocRole
            one-to-many relation to MenuSecurity
            Field Mapping uses ByRole
                related.MenuSecurity.Role = LocRole

	Actions
		Create is a Create Action

        CreateInternal is a Create Action
            valid when (CanEditDelivered)
            restricted
            bypass field rules

            Entrance Rules
                constraint (CanEditDelivered)
                    "CreateAllowedBy_InforOnly"

		Update is an Update Action
            Action Rules
                if (DeliveredConfiguration changed)
                    constraint (DeveloperMode or InternalExecution)
                        "UpdateAllowedBy_InforOnly"

		UpdateInternal is an Update Action
		    valid when (CanEditDelivered)
		    restricted
		    bypass field rules

            Entrance Rules
                constraint (CanEditDelivered)
                    "UpdateAllowedBy_InforOnly"

        Delete is a Delete Action
            valid when (CanDelete)

	    Configure is an Instance Action
            disable multiple instance selection
            completion message is "Configured"

            Parameters
                ParamConfiguration is Text
                    default label is "Configuration"

            Parameter Rules
                ParamConfiguration
                    initial value is Configuration

            Action Rules
                Configuration = ParamConfiguration

    	Activate is an Instance Action
    		valid when (not Status.Active)
    		completion message is "Activated"
    		Action Rules
    			Status = Status.Active

    	Deactivate is an Instance Action
    		valid when (not Status.Inactive)
    		completion message is "Deactivated"
    		Action Rules
    			Status = Status.Inactive

        Copy is an Instance Action
            completion message is "Copied"
            disable multiple instance selection

            Parameters
                ParamRole is a Role
                    default label is "Role"
                ParamStatus is a UserObjectStatus
                    default label is "Status"

            Parameter Rules
                ParamRole
                    required
                ParamStatus
                    initial value is UserObjectStatus.Inactive

            Action Rules
                invoke Create
                    invoked.MenuSecurity.WebApp = MenuSecurity.WebApp
                    invoked.MenuSecurity.Role = ParamRole
                    invoked.Status = ParamStatus
                    invoked.Configuration = Configuration
                    invoked.CopiedFrom = MenuSecurity

	    UpdateDescription is an Instance Action
			disable multiple instance selection
			restricted
            completion message is "DescriptionUpdated"

			Parameters
				ParamDescription is a ConfigDescription
					default label is "Description"

			Parameter Rules
				ParamDescription
					initial value is Description

            Action Rules
                Description = ParamDescription

        CreatePolicy is an Import Action
            completion message is "PolicyCreated"
            Parameters
                ParamWebApp is a WebApp
                    default label is "WebApplication"
                ParamRole is a Role
                    default label is "Role"
                ParamStatus is a UserObjectStatus
                    default label is "Status"

            Parameter Rules
                ParamWebApp
                    required
                ParamRole
                    required
                ParamStatus
                    initial value is UserObjectStatus.Inactive

            Action Rules
                invoke Create
                    MenuSecurity.WebApp = ParamWebApp
                    MenuSecurity.Role = ParamRole
                    Status = ParamStatus

        Revert is an Instance Action
            completion message is "PolicyReverted"
            restricted

            Parameters
                ConfigurationParam is Text
                    default label is "Configuration"

            Action Rules
                Configuration = ConfigurationParam

        RevertToDelivered is an Instance Action
            valid when (BasedOnDelivered)
            completion message is "PolicyReverted"
            restricted
            Action Rules
                confirmation required
                    "RevertTo_DeliveredPolicy?"


                if (Delivered)
                    Configuration = DeliveredConfiguration
                if (!Delivered and Copied and CopiedFrom.Delivered)
                    Configuration = CopiedFrom.DeliveredConfiguration

        RevertToCopiedFrom is an Instance Action
            valid when (Copied)
            completion message is "PolicyReverted"
            restricted
            Action Rules
                confirmation required
                    "RevertTo_Copied_FromPolicy?"

                Configuration = RelCopiedFrom.Configuration

        AcceptSuggested is an Instance Action
            valid when (BasedOnDelivered)
            completion message is "PolicyUpdated"
            restricted
            confirmation required
                "UpdateTo_SuggestedPolicy"

            Action Rules
                Configuration = Suggested

        MarkReviewed is an Instance Action
            valid when (BasedOnDelivered)
            completion message is "DeliveredPolicyChangesMarkedReviewed"
            restricted
            confirmation required
                "MarkAllDeliveredPolicyChangesReviewed?TheChangesWillNoLongerBeSuggested."

            Action Rules
                LastReviewedBy = actor
                LastReviewed = current timestamp

        SetAutoUpdateOn is an Instance Action
            restricted
            completion message is "PolicyWillBeUpdatedAutomatically"
            Action Rules
                AutoMerge = true

        SetAutoUpdateOff is an Instance Action
            restricted
            completion message is "PolicyWillNotBeUpdatedAutomatically"
            Action Rules
                AutoMerge = false

        RunAutoUpdate is a Set Action
            restricted
    		run in foreground
    		Instance Selection
				where (AutoMerge)

			Action Rules
				Instance Rules
					Configuration = Suggested

        UpdateCopyFrom is an Instance Action
            Parameters
                RoleParam is a Role
                    default label is "Role"
                UpdatePolicyParam is Boolean
                    default label is "UpdatePolicy"

		    Parameter Rules
                RoleParam
                    initial value is CopiedFrom.Role

		    Entrance Rules
		        LocRole = RoleParam

                constraint(RoleParam != MenuSecurity.Role)
                    "CannotCopyFromTheSame_Policy!"

		        constraint(RoleParam not entered or RelLocRole exist)
		            "PolicyDoesNotExistFor_Role"

            Action Rules
                if (RoleParam != blank)
                    CopiedFrom.WebApp = MenuSecurity.WebApp
                    CopiedFrom.Role = RoleParam

                    if (UpdatePolicyParam)
                        Configuration = CopiedFrom.Configuration

                if (RoleParam = blank)
                    CopiedFrom = blank

        UpdateAllInaccessibleChildren is a Set Action
            restricted
            Instance Selection
                where (Configuration != blank)

            Action Rules
                Instance Rules
                    invoke UpdateInaccessibleChildren

        UpdateInaccessibleChildren is an Instance Action
            restricted
            Action Rules


	    ImportPolicies is an Import Action
	        Parameters
                ParamWebApp is a WebApp
                    default label is "WebApplication"
                ParamMarkActive is Boolean
                    default label is "MarkActive"
                ParamRoleTemplate is Numeric size 1
                    default label is "Role"
                    States
                        Standard         value is 0
                        Custom           value is 1
                        All              value is 2

            Parameter Rules
                ParamWebApp
                    required

            Action Rules
                invoke RunCommand MsImport in background
                    invoked.WebApp = ParamWebApp
                    invoked.RoleTemplate = ParamRoleTemplate
                    if (!ParamMarkActive)
                        invoked.Status = 1

	    SyncPolicy is an Instance Action
	        Parameters
                ParamMarkActive is Boolean
                    default label is "MarkActive"
                ParamRoleTemplate is Numeric size 1
                    default label is "Role"
                    States
                        Standard         value is 0
                        Custom           value is 1
                        All              value is 2

            Parameter Rules

            Action Rules
                invoke RunCommand MsImport in background
                    invoked.WebApp = MenuSecurity.WebApp
                    invoked.RoleTemplate = ParamRoleTemplate
                    if (!ParamMarkActive)
                        invoked.Status = 1

        EditWithComparison is an Instance Action
            valid when (BasedOnDelivered)
            completion message is "PolicyUpdated"
            disable multiple instance selection
            restricted

            Parameters
                ParamPolicy is Text
                    default label is "NewPolicy"

            Parameter Rules
                ParamPolicy
                    initial value is ConfigurationSorted

            Action Rules
                Configuration = ParamPolicy

        EditCopiedWithComparison is an Instance Action
            valid when (Copied)
            completion message is "PolicyUpdated"
            disable multiple instance selection
            restricted

            Parameters
                ParamPolicy is Text
                    default label is "NewPolicy"

            Parameter Rules
                ParamPolicy
                    initial value is ConfigurationSorted

            Action Rules
                Configuration = ParamPolicy


        EditWithSuggestedComparison is an Instance Action
            valid when (BasedOnDelivered)
            completion message is "PolicyUpdated"
            disable multiple instance selection
            restricted

            Parameters
                ParamPolicy is Text
                    default label is "NewPolicy"

            Parameter Rules
                ParamPolicy
                    initial value is Suggested

            Action Rules
                Configuration = ParamPolicy

	Sets
        ByRole
            Sort Order
                MenuSecurity.Role
                MenuSecurity.WebApp

        ByWebApp
            Sort Order
                MenuSecurity.WebApp
                MenuSecurity.Role

		ByStatus
			Sort Order
			    Status
			    MenuSecurity.WebApp
			    MenuSecurity.Role
			    
		ByStatusAndRole
			Sort Order
			    Status
			    MenuSecurity.Role
			    MenuSecurity.WebApp

    Create Rules
        if (!CanEditDelivered)
            constraint (DeliveredConfiguration not entered)
                "MustBeInDeveloperMode"

            constraint (ItemsToMerge not entered)
                "MustBeInDeveloperMode"

            constraint (Delivered not entered)
                "MustBeInDeveloperMode"

	Delete Rules
		constraint (CanDelete)
			"DeleteNotAllowedOnADeliveredPolicy"

    Field Rules
        DeliveredConfiguration
            if (!DeveloperMode and !InternalExecution)
                if (action type.Create)
                    cannot be entered
                        "DeliveredPolicyIsReadOnly"
                else
                    cannot be changed
                        "DeliveredPolicyIsReadOnly"
        ItemsToMerge
            if (!DeveloperMode and !InternalExecution)
                if (action type.Create)
                    cannot be entered
                        "ItemsToMergeIsReadOnly"
                else
                    cannot be changed
                        "ItemsToMergeIsReadOnly"

        Delivered
            if (!DeveloperMode and !InternalExecution)
                if (action type.Create)
                    cannot be entered
                        "DeliveredIsReadOnly"
                else
                    cannot be changed
                        "DeliveredIsReadOnly"
