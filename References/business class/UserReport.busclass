UserReport is a BusinessClass
    owned by la
    prefix is USRT
	disable data area copy

    Ontology
    	symbolic key is UserReport

    Patterns
    	implements CreateStamp
        disable Auditing
        disable EffectiveDated

    Persistent Fields
    	Actor 	
    	ReportDocument
    	Name 		is a snapshot of ReportDocument.Name 	
    	MimeType	is a snapshot of ReportDocument.MimeType
    		default label is "MIME_Type"
    	UserFolder  is a snapshot of ReportDocument.UserFolder
		SourceKey1  is a snapshot of ReportDocument.SourceKey1
		SourceKey2  is a snapshot of ReportDocument.SourceKey2
		SourceKey3  is a snapshot of ReportDocument.SourceKey3
		
    Field Rules
		ReportDocument
			required
			
	Conditions
		Public
			when (Actor = blank)















		ReportDocumentReportDataExists
			default label is "RelatedReportDocumentReportDataExists"
			when (ReportDocument.ReportData exists)
 
		CreatedToday
			when (CreatedDaysOld = 0)

		MimeTypeIsExcel
			when (MimeType = "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet") 
	    		
	    MimeTypeIsCSV
			when (MimeType = "text/csv") 

	    MimeTypeIsPDF
			when (MimeType = "application/pdf") 
	    		
	    MimeTypeIsXML
			when (MimeType = "text/xml")

        MimeTypeHasIcon
            when (MimeTypeIsCSV or MimeTypeIsExcel or MimeTypeIsPDF or MimeTypeIsXML)


		    
    Derived Fields    		
        PDFNotificationDescription is a MessageField
            "PDF_report<Name>isAvailableToView"    
    
        CSVNotificationDescription is a MessageField
            "CSV_report<Name>isAvailableToView"    

        XMLNotificationDescription is a MessageField
            "XML_report<Name>isAvailableToView"    

        ExcelNotificationDescription is a MessageField
            "Excel_report<Name>isAvailableToView"   
            
        ReportNotificationDescription is a MessageField
            "Report<Name>isAvailableToView"   
         
    	NotificationDescription is a DerivedField
    	    type is Alpha size 100
    	    if (MimeTypeIsPDF)
    	        return PDFNotificationDescription
    	    if (MimeTypeIsCSV)
    	        return CSVNotificationDescription
    	    if (MimeTypeIsXML)
    	        return XMLNotificationDescription
            if (MimeTypeIsExcel)
                return ExcelNotificationDescription
    	    return ReportNotificationDescription
    	    
    	PDF is a MessageField 
    		"PDF" 
    	CSV is a MessageField 
    		"CSV" 
    	Excel is a MessageField 
    		"Excel" 
    	XML is a MessageField 
    		"XML" 
    	
    	FileType is a DerivedField 
	    	type is Alpha size 100 
	    	if (MimeTypeIsExcel) 
	    		return Excel 
	    	if (MimeTypeIsCSV) 
	    		return CSV 
	    	if (MimeTypeIsPDF) 
	    		return PDF 
	    	if (MimeTypeIsXML) 
	    		return XML
	    	return MimeType
	    	
	    OpenMyPrintFilesMessage is a MessageField
	    	"OpenMyPrintFiles"
	    	
	    MyPrintFilesLink is a NativeField
	    	type is Text
	    	
		DefaultSubjectMessage is a MessageField
            restricted
            "Document_'<Name>'_IsAvailableToView"   	    	

		TodayMsg is a MessageField
		    restricted
		    "Today"

 		YesterdayMsg is a MessageField
 		    restricted
 		    "Yesterday"

		CreatedMsg is a MessageField
		    restricted
 		    "<CreatedDaysOld>DaysAgo"

        CreatedDevMsg is a DerivedField
            type is Text
            default label is "Created"
            if (CreatedDaysOld < 1)
                return TodayMsg
            if (CreatedDaysOld = 1)
                return YesterdayMsg
            return CreatedMsg

		CurrentDateGMT is a DerivedField
			type is Date 
			default label is untranslatable
			return current date

		CreateDateGMT is a DerivedField
			type is Date 
			default label is untranslatable
			return create stamp

		CreatedDaysOld is a DerivedField
			type is Numeric 3
			return (CurrentDateGMT - CreateDateGMT)

























	
	Relations
		RemainingUserReports
    		one-to-many relation to UserReport
			Field Mapping uses ByReportDocument
				related.ReportDocument = ReportDocument
			Instance Selection
				where (related.UserReport != UserReport)

	Sets
		ByName
			no duplicates
			indexed
			Sort Order
				Name
				UserReport
				
		ByMimeType
			no duplicates
			indexed
			Sort Order
				MimeType
				UserReport	
				
		
		ByActorMimeType
			no duplicates
			indexed
			Sort Order
				Actor
				MimeType
				create stamp
				UserReport				
				
		ByStamp
			indexed
			Sort Order
				create stamp
				UserReport
	
	
		ByActorName
			no duplicates
			indexed
			Sort Order
				Actor
				Name
				UserReport

		ByActorTime
			no duplicates
			indexed
			Sort Order
				Actor
				create stamp descending 
				UserReport	
				
		ByActorSource
			no duplicates
			indexed
			Sort Order
				Actor
				UserFolder
				SourceKey1
				SourceKey2
				SourceKey3
				create stamp descending 
				UserReport		
																
		ByReportDocument
			no duplicates
			indexed
			Sort Order
				ReportDocument
				UserReport
				
		ByReportDocumentActor
			no duplicates
			indexed
			Sort Order
				ReportDocument
				Actor
								
	Actions	
		Create is a Create Action 
			restricted	
			
			Exit Rules
				invoke Notify
				
		CreateInternal is a Create Action // Need a create action to invoke that doesn't do "Notify"
			default label is "Create"
			restricted
						
		CreateUserReport is a Create Action
			default label is "Create"
			restricted	
			
			Parameters
				ParamActor 						is an Actor
					default label is "Actor"
				ParamReportDocument				is a ReportDocument
					default label is "ReportDocument"
				ParamReportEmailOption			is a ReportEmailOption
					default label is "ReportEmailOption"
				ParamReportDistributionProfile	is a ReportDistributionProfile
					default label is "ReportDistributionProfile"
				ParamBypassUserNotification		is Boolean
					default label is "BypassUserNotification"
				
			Local Fields
				LocalEmailAddress 	  	is an EmailAddress
				LocalFromEmailAddress	is an EmailAddress
				LocalSubject 			is Text
				LocalBodySupplied 		is Boolean
				
			Action Rules
				if (ParamReportEmailOption entered)
					if (ParamReportDistributionProfile entered and ParamReportDistributionProfile.ReportEmailOption != ParamReportEmailOption)
						confirmation required
							"ConflictingReportEmailOptionOptionsSupplied.<ParamReportDistributionProfile label>ValueOf<ParamReportDistributionProfile.ReportEmailOption>WillBeUsed.Continue?"
						ParamReportEmailOption = ParamReportDistributionProfile.ReportEmailOption 
						
					if (ParamActor not entered)
						confirmation required
							"ReportEmailOption<ParamReportEmailOption>IsNotValidForPublicReportAndWillBeIgnored,Continue?"
						initialize ParamReportEmailOption
					else
						if (not ParamActor.ContactInfo.EmailAddress entered and not ParamActor.ContactInfo.AlternateEmail entered)
							confirmation required
								"Actor<ParamActor>DoesNotHaveAnEmailAddress.ReportEmailOption<ParamReportEmailOption>WillBeIgnored,Continue?"
							initialize ParamReportEmailOption
				else
				if (ParamReportDistributionProfile entered)
					ParamReportEmailOption = ParamReportDistributionProfile.ReportEmailOption
					
				invoke CreateInternal this instance
					invoked.Actor 			= ParamActor
					invoked.ReportDocument 	= ParamReportDocument
								
				if (ParamReportEmailOption entered)

					if (ParamActor.ContactInfo.EmailAddress entered)
						LocalEmailAddress = ParamActor.ContactInfo.EmailAddress
					else
						LocalEmailAddress = ParamActor.ContactInfo.AlternateEmail
				
					if (ParamReportDistributionProfile entered)
						invoke SetLocalFields ParamReportDistributionProfile
							invoked.ParamUserReport = UserReport
						
						LocalFromEmailAddress = ParamReportDistributionProfile.RenderedReportEmailTemplateFrom
						LocalSubject = ParamReportDistributionProfile.RenderedReportEmailTemplateSubject
					
					if (LocalFromEmailAddress not entered)
						LocalFromEmailAddress = LocalEmailAddress
				
					if (LocalSubject not entered)
						LocalSubject = DefaultSubjectMessage
						
					if (ParamReportDistributionProfile.RenderedReportEmailTemplateBody plain text entered)
						LocalBodySupplied = true
					else
						LocalBodySupplied = false
					
					send email
						to LocalEmailAddress 
						from LocalFromEmailAddress
						subject "<LocalSubject>"
						Contents
							if (LocalBodySupplied)
								"<ParamReportDistributionProfile.RenderedReportEmailTemplateBody>"
							if (ParamReportEmailOption = ParamReportEmailOption.IncludeDocumentLink or ParamReportEmailOption = ParamReportEmailOption.Both)		

								"<MyPrintFilesLink>"
							if (not LocalBodySupplied and (ParamReportEmailOption = ParamReportEmailOption.IncludeDocument or ParamReportEmailOption = ParamReportEmailOption.Both))
								"Document_'<Name>'_(<ParamReportDocument.FileName>)HasBeenIncludedAsAnAttachment"
						Attachments
							if (ParamReportEmailOption = ParamReportEmailOption.IncludeDocument or ParamReportEmailOption = ParamReportEmailOption.Both)
								attachment ParamReportDocument.Document
									name is ParamReportDocument.FileName
									mime type is ParamReportDocument.MimeType 
			Exit Rules
				if (not ParamBypassUserNotification
				and (ParamReportDistributionProfile not entered or not ParamReportDistributionProfile.SuppressUserNotification))
					invoke Notify
				
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			Exit Rules
				if (not RemainingUserReports exists)
					invoke Delete ReportDocument
					
		DeleteInternal is a Delete Action 
			restricted					
			
		Notify is an Instance Action 
			restricted
									
			Exit Rules						
				if (Actor exists)
					invoke Create UserNotification
					    invoked.Actor 			= Actor
						invoked.Description		= this instance(locale of Actor.IsoLocale).NotificationDescription							
						invoked.SourceType 		= 1 
						invoked.SourceObject 	= reference to ReportDocument
						invoked.MimeType		= ReportDocument.MimeType
						invoked.AsyncID			= ReportDocument.AsyncID
						
		PurgeMyReports is a Set Action
			Parameters
				IncludeFolders		is Boolean
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3
				
			Local Fields
				LocalThruDate 	is Date
					value is ThruDate
			


			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"		
								
			Instance Selection
				where (Actor = actor
				and    create stamp < LocalThruDate
				and   (IncludeFolders
				or     UserFolder = blank))
				
			Sort Order
				Actor
				UserFolder
				create stamp
				UserReport		
				
			Action Rules
				Instance Rules
					invoke Delete													
	
		PurgeActorReports is a Set Action
			Parameters
				ParamActor			is an Actor
					default label is "Actor"
				ParamDeletedActor is like Actor
					default label is "DeletedActor"
				IncludeFolders		is Boolean
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3
				
			Local Fields
				LocalThruDate 	is Date
					value is ThruDate
			


			Parameter Rules
				ParamActor
					if (ParamActor entered)
						ParamDeletedActor = ParamActor
							
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"		
								
			Instance Selection
				where (Actor = ParamDeletedActor
				and    create stamp < LocalThruDate
				and   (IncludeFolders
				or     UserFolder = blank))
				
			Sort Order
				Actor
				UserFolder
				create stamp
				UserReport		
				
			Action Rules
				Instance Rules
					invoke Delete			
						
		PurgeReports is a Set Action
			Parameters
				IncludeFolders		is Boolean
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3
		
			Local Fields
				LocalThruDate 	is Date
					value is ThruDate
			


			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"
												
			Instance Selection
				where (create stamp < LocalThruDate
				and   (IncludeFolders
				or     UserFolder = blank))

			Sort Order
				create stamp
				UserReport	
				
			Action Rules
				Instance Rules
					invoke Delete	
						
		PurgeFolderReports is a Set Action
			Parameters
				FolderId			is a UserFolder
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3
				FolderUID			is UniqueID 
					default label is "DeletedFolderId"
		
			Local Fields
				LocalThruDate 	is Date
					value is ThruDate
			


			Parameter Rules
				FolderId
					if (FolderId entered)
						FolderUID = FolderId
					
				ThruDate   
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"
												
			Instance Selection
				where (UserFolder = FolderUID
				and    create stamp < LocalThruDate)

			Sort Order
				UserFolder
				create stamp
				UserReport	
				
			Action Rules
				Instance Rules
					invoke Delete

		DistributeToGroup is an Instance Action

			Parameters
				ReportDistributionGroup
				DynamicDistributionActors
				ImplicitlyIncludeCurrentActor is Boolean
				IgnoreDuplicates is Boolean
				
			Parameter Rules
				ReportDistributionGroup
					required
				
				DynamicDistributionActors
					if (ReportDistributionGroup.DynamicActorsOnlyProfiles)
                		required
                			"AtLeastOneActorValueIsRequired"
                			
               	ImplicitlyIncludeCurrentActor
               		initial value is (not Actor = actor)
               			
               	IgnoreDuplicates
               		initial value is true

			Action Rules
				constraint (not ReportDistributionGroup.BurstOnlyReportDistribution)
					"ReportDistributionGroupContainsOnlyBurstProfilesWhichCannotBeDistributedInThisManner"
					
				if (ReportDistributionGroup.HasBurstReportDistribution)
					confirmation required
						"ReportDistributionGroupContainsBurstProfilesWhichWillBeSkipped.Continue?"
					
				if (not ReportDistributionGroup.HasDynamicActorReportDistribution)
                	initialize DynamicDistributionActors
						
				invoke DistributeReportDocument ReportDistributionGroup
					invoked.ReportDocument = ReportDocument
					invoked.DynamicDistributionActors = DynamicDistributionActors
					invoked.IgnoreDuplicates = IgnoreDuplicates
					
					if (ImplicitlyIncludeCurrentActor)
						invoked.DoNotAddCurrentActor = false
					else
						invoked.DoNotAddCurrentActor = true
							
		ScheduleConvert is an Instance Action
		    default label is "ScheduleReportGeneration"	
			valid when (ReportDocumentReportDataExists)

			Parameters
                ReportDistributionGroup		
							
            Parameter Rules
					
            Action Rules
				invoke ScheduleConvert ReportDocument.ReportData
                    invoked.ReportDistributionGroup = ReportDistributionGroup
                    

