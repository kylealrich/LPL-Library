GlobalLocationNumber is a BusinessClass
    owned by ic
    prefix is GLO
    classic name is GLNLOC

    Ontology
        symbolic key is GlobalLocationNumber
            classic set name is GLOSET1
            classic name is GLN-NBR

    Patterns
        implements StaticJava
        disable AuditIndex


    Persistent Fields

        Description
        ParentGlobalLocationNumber 				is a GlobalLocationNumber
            classic name is PARENT
        Active                   	 			is Boolean
            classic name is STATUS
	
	Transient Fields
		CompanyLocationBin
		Company                        			is a GeneralLedgerCompany                       
			disable surrogates
		RequestingCompanyAndLocation             
		VendorAndVendorLocation
		PurchasingVendorAndPurchaseFromLocation
		Buyer
		Manufacturer
		GlobalLocationNumberType
	
	
    Conditions

        IsActive
        	restricted
            when (Active)

        HasParent
        	restricted
            when (ParentGlobalLocationNumber entered)

        IsInactive
        	restricted
            when (not Active)
            
        IsRecordExist
        	restricted
        	when(GlobalLocationNumberDetailsRel exist)    

		ItemGroupEntered
			restricted
			when (ItemGroup entered)

		GlobalLocationNumberEntered
			restricted
			when (GlobalLocationNumber entered)

		BypassRuleForTheseFiles
			restricted
			when  (parentcontext.name = "PurchaseOrderEDIOutput")
    Relations

        GlobalLocationNumberDetailsRel is a GlobalLocationNumberDetail set
            classic name is GLNLOCDTL

    Sets

        Set2
            indexed
            Instance Selection
                where (HasParent)
            Sort Order
                ItemGroup
                ParentGlobalLocationNumber
                GlobalLocationNumber
                
	Field Rules
		
		Description
			required		 		
	    
	    ParentGlobalLocationNumber
	    	constraint (GlobalLocationNumber != ParentGlobalLocationNumber ) 
	    		"ParentGlobalLocationNumber<ParentGlobalLocationNumber>CannotBeSameAsGlobalLocationNumber<GlobalLocationNumber>" 
                
					
		Active
            initial value is true
			if (GlobalLocationNumberDetailsRel exists)
				cannot be changed
					"GlobalLocationNumber<GlobalLocationNumber>AssignedRemoveAssociationBeforeSettingTheStatusToInactive" 

	
	Attach Rules
		if (not BypassRuleForTheseFiles)
			constraint (Active)
				"GlobalLocationNumber<GlobalLocationNumber>IsInactive"

	Rule Blocks
		GlobalLocationNumberDetail	
			if (GlobalLocationNumberType entered)
  	    		invoke Create GlobalLocationNumberDetail
					invoked.ItemGroup 											= ItemGroup
		  	    	invoked.GlobalLocationNumber								= GlobalLocationNumber
		  	    	invoked.GlobalLocationNumberDetail.GlobalLocationNumberType	= GlobalLocationNumberType
					
					if (GlobalLocationNumberType.GeneralLedgerCompany)	
		  	    		invoked.Company											= Company
		  	    	else
		  	    	if (GlobalLocationNumberType.InventoryCompany
	           		or  GlobalLocationNumberType.InventoryLocation 
					or  GlobalLocationNumberType.Bin)
		  	    		invoked.CompanyLocationBin								= CompanyLocationBin
		  	    	else
					if (GlobalLocationNumberType.Vendor
					or  GlobalLocationNumberType.VendorLocation)			
						invoked.VendorAndVendorLocation							= VendorAndVendorLocation
					else 						
					if (GlobalLocationNumberType.PurchasingVendor
					or  GlobalLocationNumberType.PurchaseFromLocation)
						invoked.PurchasingVendorAndPurchaseFromLocation			= PurchasingVendorAndPurchaseFromLocation
					else
					if (GlobalLocationNumberType.RequestingLocation)
						invoked.RequestingCompanyAndLocation					= RequestingCompanyAndLocation
	           		else
	           		if (GlobalLocationNumberType.ManufacturerCode	
					or  GlobalLocationNumberType.ManufacturerDivision)
						invoked.Manufacturer									= Manufacturer
					else
					if (GlobalLocationNumberType.Buyer)
						invoked.Buyer											= Buyer

	Actions		
	    Create is a Create Action
	    	Exit Rules
				include GlobalLocationNumberDetail

	    Update is an Update Action
	    	Exit Rules
				include GlobalLocationNumberDetail
		
		Delete is a Delete Action
			Action Rules
				constraint(GlobalLocationNumberDetailsRel not exists)
					"CannotDelete<GlobalLocationNumber>;AssignedTo<first GlobalLocationNumberDetailsRel.GlobalLocationNumberDetail.GlobalLocationNumberType>"   
			
			
