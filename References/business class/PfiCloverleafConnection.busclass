PfiCloverleafConnection is a BusinessClass
    owned by pfi
    prefix is PfiCL
	contains environment data
	disable data area copy
		preserve target data
    default label is "CloverleafConnection"

    Ontology
        symbolic key is PfiCloverleafConnection

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated

    Persistent Fields
    	Protocol		is Alpha 20
    		States
    			Tcpip	value is "TCPIP"
    			    default label is "TCP/\IP"



        MessageEncoding			is Alpha 20	
        	States
        		USASCII		value is "US-ASCII"
        		UTF8		value is "UTF-8"
        		UTF16BE		value is "UTF-16BE"
        		UTF16LE		value is "UTF-16LE"

		UseByteOrderMark		is Boolean         		
        TcpipHost				is Alpha size up to 1000
            default label is "TCP/\IPHost"
        TcpipPort				is Numeric size 5
            default label is "TCP/\IPPort"
        TcpipSocketTimeout		is Numeric size 6
            default label is "TCP/\IPSocketTimeout"
        TcpipIndicatorLength	is Numeric size 2
            default label is "TCP/\IPIndicatorLength"
        TcpipIndicatorFill		is Alpha size 1
            default label is "TCP/\IPIndicatorFill"
        TcpipIndicatorJustify	is Numeric size 1
            default label is "TCP/\IPIndicatorJustify"
	        States
	        	Left	value is 1
	        	Right	value is 2
        TcpipLengthCalc			is Numeric size 1
            default label is "TCP/\IPLengthCalc"
        	States
        		Include	value is 1
        		Exclude	value is 2
        		



	Derived Fields
        CloverleafConnectionIncludesSpace is a DerivedField
            type is Boolean
            Index = 100
            while (Index > 0
            and    PfiCloverleafConnection[Index] = blank)
                Index -= 1
            
            while (Index > 0
            and    PfiCloverleafConnection[Index] != blank)
                Index -= 1
                
            if (Index > 0)
                return false
            else
                return true
	
        
	Local Fields
        Index	is Numeric 3
        
        I1	is Numeric 3
        I2	is Numeric 3
        RestrictedChars is Alpha 6

        ProhibitedCharFound is Boolean

	Field Rules
       
        PfiCloverleafConnection


            RestrictedChars = ":" + "&" + "<" + ">" + "\"" + "'"		
			ProhibitedCharFound = false
			I1 = 1
			while ((ProhibitedCharFound = false) and (I1 < 7))
				I2 = 1
				while ((ProhibitedCharFound = false) and (I2 < 101))
					if (PfiCloverleafConnection[I2] = RestrictedChars[I1])
						ProhibitedCharFound = true
					else
						I2 += 1
				I1 += 1
												
    		constraint (ProhibitedCharFound = false)
    			"CloverleafConnectionNameContainsProhibitedCharacter"
			
            constraint (CloverleafConnectionIncludesSpace)
                "CloverleafConnectionNameCannotIncludeSpaces"        
		
		Protocol 
			initial value is Protocol.Tcpip
			default to Protocol.Tcpip		
			required
			
		MessageEncoding
			initial value is MessageEncoding.UTF8
			default to MessageEncoding.UTF8		
			required
			
        UseByteOrderMark
        	default to false

	SubType Protocol.Tcpip Field Rules		

		TcpipHost
			required
				"HostIsRequired"

		TcpipPort
			required
				"PortIsRequired"

    	TcpipSocketTimeout
    		constraint (TcpipSocketTimeout >= 0)
    			"SocketTimeoutCannotBeNegative"

		TcpipIndicatorLength
			initial value is 10
			default to 10		
			required
				"LengthOfIndicatorIsRequired"
    		constraint (TcpipIndicatorLength >= 1)
    			"LengthOfIndicatorHasToBeAtLeastOne"
			
		TcpipIndicatorFill
			initial value is 0
			default to 0		
			required
				"IndicatorFillCharacterIsRequired"

		TcpipIndicatorJustify
			initial value is TcpipIndicatorJustify.Right
			default to TcpipIndicatorJustify.Right		
			required
				"JustificationIsRequired"

		TcpipLengthCalc
			initial value is TcpipLengthCalc.Include
			default to TcpipLengthCalc.Include		
			required
				"LengthCalculationIsRequired"
				





				
			
