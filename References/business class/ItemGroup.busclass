ItemGroup is a BusinessClass
    owned by ic
    prefix is ITG
    classic name is ITEMGROUP

    Ontology
        symbolic key is ItemGroup
            classic set name is ITGSET1

    Patterns
        implements StaticJava
        disable AuditIndex
        implements BODId

    Persistent Fields

        Description
		NumericItemNumber			is Boolean
		AutomaticItemNumbering		is Boolean
		DefaultNumberOfDecimalsCost  is a NumberOfDecimalsCost
            classic name is DEF-DEC-COST
        DefaultNumberOfDecimalsPrice is a NumberOfDecimalsCost
            classic name is DEF-DEC-PRICE
        DefaultItemCreationMethod    is AlphaUpper size 1    
            classic name is DEF-ITEM-CR-FL
            States
                Sequential  value is "1"
                Assign      value is "2"
                ManualInput value is "3"
		CommodityCodeStructure
            classic name is COMM-STRUCTURE
		AllowUOMChangesWithOpenTransactions		is Boolean
			classic name is UOM-EDIT-FLAG
        ItemCommentTypeArray
            classic name for ItemCommentTypeArray.ItemCommentTypeGroup.ItemCommentType is COM-TYPE
            classic name for ItemCommentTypeArray.ItemCommentTypeGroup.Description is COM-TYPE-DESC
            classic name for ItemCommentTypeArray.ItemCommentTypeGroup.ReportOptionItemList is COM-OPT-IC210
            classic name for ItemCommentTypeArray.ItemCommentTypeGroup.ReportOptionLocationList is COM-OPT-IC211
            classic name for ItemCommentTypeArray.ItemCommentTypeGroup.ReportOptionIntransitList is COM-OPT-IC110
		LastItem									is Numeric 18
			disable Auditing
		ItemImageMaximumSize						is Numeric 8
		ItemImageUOM								is Numeric 1
			States
				Bytes			value is 0
				Kilobytes		value is 1
				Megabytes		value is 2
				Gigabytes       value is 3
		ItemSyncOption				  is Numeric 1	
			States									
				DoNotAllowItemSync	value is 0		
				OnlyMatchingValues	value is 1		
				BlankValues			value is 2		
				UpdateIfDifferent	value is 3		
				AllValues			value is 4
		DoNotUpdateInactiveVendorItem          is Boolean
		RequireManufacturerOnNewInterfaceItems is Boolean
		AutoUpdateGLN 						   is Boolean
			classic name is AUTO-UPD-GLN 
		StructureRow
		RequesterValidationOption					is Numeric 1
			States
				NotInUse			value is 0
				AllowValues			value is 1
				DisallowValues		value is 2
		AutomaticItemSubstitute				 is Boolean
		IgnoreSubstituteItemsInReplenishment is Boolean
		AllowSubstitutesOnWarehouseLocationRule	is Boolean
			default label is "AllowSubstitutesWhenUsingWarehouseLocationRule"

		RecipeManagementInUse				is Boolean
			default label is "KitchenManagement"
		UseIDMTemplate 						is Boolean
		RecipeTemplate				is an IDMTemplate
			default label is "Recipe"
		AllowMultipleGTINs					is Boolean
			default label is "AllowMultipleG\T\I\Ns"
		HideHealthcareInformation           is Boolean 
		PreferenceCardsInUse				is Boolean
		WeightUOM							is Numeric 1
			default label is "WeightMeasurement"
			States
				NotSelected			value is 0
				Ounce				value is 1
				Pound				value is 2
				Ton					value is 3
		VolumeUOM							is Numeric 1
			default label is "VolumeMeasurement"
			States
				NotSelected			value is 0
				FluidOunce			value is 1
				CubicFeet 			value is 2
				Gallon 				value is 3
				Quart				value is 4
		SetForClinicalUseToTrue             is Boolean
			default label is "WhenItemIsCreatedSetForClinicalUseToTrue"
		NoDirectUpdateToManufacturerInformation is Boolean
			default label is "DoNotAllowManufacturerInformationToBeUpdatedDirectlyOnItem"
		DefaultManufacturerNumberOnGTIN     is Boolean 
			default label is "WhenGTINIsCreated,DefaultManufacturerNumberFromItem"
		AllocatePriorToSubstitution			is Boolean
		ImageSource							is Numeric 1
			default label is "ItemImageSource"
			States
				StoredItemImage		value is 0
				LinkedItemImage		value is 1
				
	Local Fields
		ActionCode
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp
		TempCounter1					is Numeric 2
		TempCounter2					is Numeric 2
		LocalInventoryCompany			is an InventoryCompany
			disable surrogates
		LocalRequester					is a Requester
		LocalFormerStructureRow			is Alpha 25	
		LocalNativeLPLBODTrigger		is Boolean				
		NewBODTracker  					is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker		is Numeric 15
		Error            				is Boolean
	    ErrorMessage     				is Alpha 300
	    LocalBODTrigger					is Boolean
	    LocalConfigurationParameter		is Alpha size up to 200
	    
	Context Fields
		FSMInboundBODTracker
		
	Derived Fields
		RepresentativeText is a StringField
			type is Text
			default label is "ItemGroup"
			ItemGroup " - " Description
		
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforERPEnterpriseCommonItemGroup"
			
		ContextMessageText is a MessageField
			restricted
			"ItemGroup<ItemGroup><Description>"

		ItemGroupDescription is a MessageField
			"<ItemGroup>_-_<Description>"

		ItemImageMaximumSizeBytes is a ConditionalField
			type is Numeric 16
			if (ItemImageUOM.Kilobytes)
				(ItemImageMaximumSize * 1024)
			else
			if (ItemImageUOM.Megabytes)
				(ItemImageMaximumSize * 1024 * 1024)
			else
			if (ItemImageUOM.Gigabytes)				
				(ItemImageMaximumSize * 1024 * 1024 * 1024)				
			else
				ItemImageMaximumSize
				
		DerivedRestrictedByLabel			is a DerivedField
			type is MessageField
			if (StructureRow entered)
				return StructureRow.DisplayLabel
				
		ActorBusinessGroup is a DerivedField
			type is like BusinessGroup
			return actor.context.BusinessGroup

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "tenantID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "releaseID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "logicalID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "VersionID"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter = "appProdline"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or ActionCode.Create)
				return "Add"
			else
			if(action type.Update or action type.Delete)
				return "Replace"
			else
				return ""	
				
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or ActionCode.Create)
				return "Open"
			else
			if(action type.Delete or ActionCode.Delete)
				return "Deleted"
			else 
			if(action type.Update or ActionCode.Update)
				return "Open"
			else
				return ""	
				
		DerivedFinanceEnterpriseGroup is a DerivedField
			type is Alpha 4
			restricted
			return	ItemGroup.BusinessGroup.FinanceEnterpriseGroup
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
			
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return  "infor-nid:" + DerivedTenantID + ":" + ItemGroup+ ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
		
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
			
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
		
		ItemGroupXMLBOD is a DerivedField
			type is XMLDocument
	        restricted
	       	ItemGroupXMLBOD = template.IONSyncCodeDefination_ItemGroup_ST document for this instance

		RSSSetAllocationPriorityLevel is a MessageField
			restricted
			"RSS_Set_Default_Allocation_Priority_Level"
	Conditions

		BusinessGroupAllowsAccess
			when ((actor.context.BusinessGroup != ""
			and	   actor.context.BusinessGroup = ItemGroup)
			or	  (actor.context.BusinessGroup = "")
			or    (ItemGroup !entered))
		ValidForSupplier
			restricted
			when (ItemGroup = actor.agent(SupplierSourceId).SupplierGroup)
		HasItemCommentType1
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[1].ItemCommentType entered)
		HasItemCommentType2
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[2].ItemCommentType entered)
		HasItemCommentType3
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[3].ItemCommentType entered)
		HasItemCommentType4
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[4].ItemCommentType entered)
		HasItemCommentType5
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[5].ItemCommentType entered)
		HasItemCommentType6
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[6].ItemCommentType entered)
		HasItemCommentType7
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[7].ItemCommentType entered)
		HasItemCommentType8
			restricted	
			when (ItemCommentTypeArray.ItemCommentTypeGroup[8].ItemCommentType entered)
		HasItemCommentType9
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[9].ItemCommentType entered)
		HasItemCommentType10
			restricted
			when (ItemCommentTypeArray.ItemCommentTypeGroup[10].ItemCommentType entered)
		CommodityCodesExist
			restricted
			when (CommodityCodesRel exists)
		ItemExist
			restricted
			when (ItemsRel exists)
		ItemSyncUpdateCompanyOrLocation
			restricted
			when (InventoryCompaniesWithItemSyncUpdate exists
			or    InventoryLocationsWithItemSyncUpdate exists)
		RSSLPLAllocationPriorityLevelEnabled
			restricted
            when (RSSLPLAllocationPriorityConfigurationParameterRel exists)
		AllocationPriorityLevelExistsAndValidForDefault
			restricted
			when (RSSLPLAllocationPriorityLevelEnabled
			and   RSSLPLAllocationPriorityLevel50Exists)
		RSSLPLAllocationPriorityLevel50Exists
			restricted
			when (RSSLPLAllocationPriorityLevel50Rel not exists)
			
    Relations
		KitchenManagementEnabledCompanyRel
			one-to-many relation to InventoryCompany
			Field Mapping uses Set2
				related.ItemGroup	= ItemGroup
			Instance Selection
				where (related.KitchenOrderInUse)

		PreferenceCardEnabledCompanyRel
			one-to-many relation to InventoryCompany
			Field Mapping uses Set2
				related.ItemGroup = ItemGroup
			Instance Selection
				where (not related.PreferenceCardOption.DefaultToItemGroup)

		RecipeItemsRel is an Item set
			Instance Selection
				where (related.Recipe)
			
		IngredientItemsRel is an Item set
			Instance Selection
				where (related.Ingredient)

        ContractGroupRel 
			one-to-one relation to ContractGroup 
			Field Mapping uses symbolic key 
				related.ContractGroup = ItemGroup 
		
		InventoryCompaniesRel
            classic name is COMPANY
            one-to-many relation to InventoryCompany
            delete restricted
            Field Mapping uses Set2
                related.ItemGroup = ItemGroup

        InventoryCompaniesWithItemSyncUpdate
        	one-to-many relation to InventoryCompany
            delete restricted
            Field Mapping uses Set2
                related.ItemGroup = ItemGroup
            Instance Selection
            	where (ItemSyncOption.OnlyMatchingValues		
				or     ItemSyncOption.BlankValues					
				or     ItemSyncOption.UpdateIfDifferent			
				or     ItemSyncOption.AllValues)		
        
        InventoryLocationsWithItemSyncUpdate
        	one-to-many relation to InventoryLocation
            delete restricted
            Field Mapping uses symbolic key
            Instance Selection
            	where (ItemSyncOption.OnlyMatchingValues		
				or     ItemSyncOption.BlankValues					
				or     ItemSyncOption.UpdateIfDifferent			
				or     ItemSyncOption.AllValues
				and    related.Company.ItemGroup = ItemGroup)	
        
        GlobalLocationNumbersRel
            classic name is GLNLOC
            one-to-many relation to GlobalLocationNumber
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup

        HazardCodesRel
            classic name is HAZARDCODE
            one-to-many relation to HazardCode
            delete restricted
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup

		UnitsOfMeasureRel is a UnitOfMeasure set 
		
        InventoryClassesRel
            classic name is INV-CLASS
            one-to-many relation to MajorClass
            delete restricted
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup
                related.ClassType = "I"

        ItemsRel
            classic name is ITEMMASTER
            one-to-many relation to Item
            delete restricted
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup
                
        ActiveItemsRel
            classic name is ITEMMASTER
            one-to-many relation to Item
            delete restricted
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup
			Instance Selection
				where (related.Active)

        PurchasingClassesRel
            classic name is PURCH-CLASS
            one-to-many relation to MajorClass
            delete restricted
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup
                related.ClassType = "P"

        SalesClassesRel
            classic name is SALES-CLASS
            one-to-many relation to MajorClass
            delete restricted
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup
                related.ClassType = "S"

		ProcurementGroupRel
			one-to-one relation to ProcurementGroup
			Field Mapping uses symbolic key
				related.ProcurementGroup = ItemGroup				
				
		SpecialOrServiceItemRel
			one-to-many relation to ContractLine
			Field Mapping uses ByCanUseForCostDefaultingErrorsExist
				related.ContractGroup 			= ItemGroup
			Instance Selection
				where ((related.ItemType.Special
				or     related.ItemType.Service)
				and    related.UseForCostDefaultingSomewhere)
								
		CommodityCodesRel is a CommodityCode set
				
		SpecialKeywordShadowRel
			one-to-one relation to KeywordShadow
			Field Mapping uses ByContractLine
  				related.ItemGroup	=	ItemGroup
				related.Contract	=   SpecialOrServiceItemRel.Contract
				related.ContractLine =	SpecialOrServiceItemRel.ContractLine   									

		RequestersForInventoryCompanyRel
			classic name is REQUESTER
			one-to-many relation to Requester
			Field Mapping uses Set2
				related.HROrganization	= ItemGroup.FinanceEnterpriseGroup.HROrganization
				related.Company			= LocalInventoryCompany

		ContractLineSpecialItemWorkRel  
			one-to-many relation to ContractLineSpecialItemWork
			Field Mapping uses ByContractLine
				related.ContractGroup   = ItemGroup
		
		RequesterFinanceStructureRel
			one-to-many relation to RequesterFinanceStructure
			Field Mapping uses symbolic key
				related.HROrganization	= ItemGroup.FinanceEnterpriseGroup.HROrganization
				related.Requester		= LocalRequester	

		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1
           
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker

		DuplicateGTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup				= ItemGroup
			Instance Selection
				where (related.HasDuplicateGTINPerUOM)
		
		RSSLPLAllocationPriorityConfigurationParameterRel
            one-to-many relation to ConfigurationParameter
            Field Mapping uses symbolic key
                related.ConfigurationParameter.ConfigurationID  = "config"
                related.ConfigurationParameter.Name             = "RSS_LPLAllocationPriorityLevel"
			Instance Selection
				where (related.ConfigurationParameter.Value = "true")
		
		RSSLPLAllocationPriorityLevelRel
			one-to-many relation to AllocationPriorityLevel
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ItemGroup.FinanceEnterpriseGroup
			Instance Selection
				where (related.PriorityCategoryType = "S")

		InventoryCompanyPreferenceCardInUseRel
			one-to-many relation to InventoryCompany
			Field Mapping uses Set2
				related.ItemGroup				= ItemGroup
			Instance Selection
				where (related.PreferenceCardOption.PreferenceCardsInUse)

		PreferenceCardRel
			one-to-many relation to PreferenceCard
			Field Mapping uses symbolic key
				related.ItemGroup		= ItemGroup

		RSSLPLAllocationPriorityLevel50Rel
			one-to-many relation to AllocationPriorityLevel
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ItemGroup.FinanceEnterpriseGroup
				related.AllocationPriorityLevel	= "50"

	Rule Blocks
		TriggerItemGroupServiceRules
			trigger "ItemGroupService" PA service
				resume on error
				title is "EG:<ItemGroup.BusinessGroup.FinanceEnterpriseGroup>IG:<ItemGroup>"
				Criteria
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
				Variables
					ActionCode
					include persistent fields from ItemGroup
					ItemGroup.BusinessGroup.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
			
    Field Rules

        Description
            required

		NumericItemNumber
			initial value is true

		AutomaticItemNumbering
			initial value is true
			if (AutomaticItemNumbering)
				constraint (NumericItemNumber)
					"CannotAutoAssignIfNumericItemNumberIsUnchecked"
			
		CommodityCodeStructure                        
			if (old CommodityCodeStructure entered
			and CommodityCodesExist)
				cannot be changed
					"CannotChangeOrRemoveCommodityCodeStructure;CommodityCodesExist"                      

		ItemCommentTypeArray
			initialize TempCounter1
			while (TempCounter1 < 10)
				TempCounter1 += 1

				if (ItemCommentTypeArray.ItemCommentTypeGroup[TempCounter1].ItemCommentType entered)
					TempCounter2 = 1
					while (TempCounter2 < TempCounter1)
						constraint (ItemCommentTypeArray.ItemCommentTypeGroup[TempCounter1].ItemCommentType != ItemCommentTypeArray.ItemCommentTypeGroup[TempCounter2].ItemCommentType)
							"CommentType;AlreadyExists"                             

						TempCounter2 += 1
		
		ItemImageMaximumSize
        	required
        	initial value is 20
        	if (ItemImageMaximumSize = blank)
        		ItemImageMaximumSize = 20
        		ItemImageUOM = ItemImageUOM.Kilobytes
        	
		ItemImageUOM
        	initial value is ItemImageUOM.Kilobytes
        	
        StructureRow
        	if (StructureRow entered)
        		constraint (!RequesterValidationOption.NotInUse)
        			"ValidationOptionMustBeAllowOrDisallow"
        
        AutomaticItemSubstitute
        	if (not AutomaticItemSubstitute)
        		initialize IgnoreSubstituteItemsInReplenishment
        		initialize AllowSubstitutesOnWarehouseLocationRule
				initialize AllocatePriorToSubstitution
        	
		RecipeTemplate
			if (RecipeManagementInUse
			and UseIDMTemplate)
				required
				
				constraint (RecipeTemplate.IDMDocumentType.Recipe)
					"InvalidTemplate"
			else
				initialize RecipeTemplate

		RecipeManagementInUse
			if (RecipeManagementInUse changed
			and not RecipeManagementInUse)
				constraint (KitchenManagementEnabledCompanyRel not exist)
					"An_\Inventory_\CompanyFor_\Kitchen_\ManagementIsEnabledFor_\Item_\Group<ItemGroup>"
					
				constraint (RecipeItemsRel not exist)
					"RecipeItemsExistUnderItemGroup<ItemGroup>"
					
				constraint (IngredientItemsRel not exist)
					"IngredientItemsExistUnderItemGroup<ItemGroup>"

		AllowMultipleGTINs
			if (AllowMultipleGTINs changed and not AllowMultipleGTINs)
				constraint (DuplicateGTINRel not exists)
					"Multiple_\G\T\I\NsWithSameUOMExist"

		PreferenceCardsInUse
			if (PreferenceCardsInUse changed)
				if (not PreferenceCardsInUse)
					constraint (InventoryCompanyPreferenceCardInUseRel not exists)
						"InventoryCompanyWithPreferenceCardInUseIsExisting"
				if (PreferenceCardRel exists)
					for each PreferenceCardRel
						invoke ReviewErrors each

	Actions
		Create is a Create Action
		
		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmDocumentID		is Alpha size 100
					default label is "DocumentID"
				PrmRevision			is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord	is Alpha size 1		
					default label is "SystemOfRecord"


					
			Action Rules	
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord



		Update is an Update Action

        	Field Rules
		    	StructureRow
		    		if (StructureRow changed)
		    			LocalFormerStructureRow				= old StructureRow.DisplayLabel
		    			for each InventoryCompaniesRel
		    				LocalInventoryCompany			= each.Company
		    				if (RequestersForInventoryCompanyRel exists)
		    					for each RequestersForInventoryCompanyRel
		    						LocalRequester			= each.Requester
									constraint (!RequesterFinanceStructureRel exists)
										"ThereAreRequestersThatHaveRestrictionsFor<LocalFormerStructureRow>"		    									

				ItemSyncOption
					if (ItemSyncOption changed
					and ItemSyncOption.DoNotAllowItemSync)
						constraint (!ItemSyncUpdateCompanyOrLocation)
							"CannotSetItemSyncOptionToDoNotAllowItemSync;CompaniesOrLocationsExistThatAllowItemSync"		
	
			Exit Rules
				if (PreferenceCardEnabledCompanyRel exists)
					for each PreferenceCardEnabledCompanyRel
						invoke FastUpdate each
							invoked.PreferenceCardOption 		= 0 
							invoked.AllowDemandReservation		= false

		Delete is a Delete Action
			Entrance Rules
				if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger)
					ActionCode	= ActionCode.Delete
					increment bod id.VariationID
					include TriggerItemGroupServiceRules


		SendItemGroupBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ItemGroupXMLBOD
					bod type is "Sync.CodeDefinition"
					document id is ItemGroup
					variation id is	BODVariationID
					
		TriggerItemGroupNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "ItemGroup"
					invoked.PrmFinanceEnterpriseGroup	= ItemGroup.BusinessGroup.FinanceEnterpriseGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_ItemGroup_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= ItemGroup
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.Reference2				= "ItemGroup"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.BODDocumentID			= ItemGroup
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.Reference2				= "ItemGroup"
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendItemGroupBODNativeLPL
						resume on error
							Error            							= true
							ErrorMessage     							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODFormattedId
							invoked.BODXML								= ItemGroupXMLBOD
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status									= 3
							invoked.CloseDate								= system current timestamp
							invoked.BODID									= DerivedBODFormattedId
							invoked.BODXML									= ItemGroupXMLBOD

					
		RecreateKeywords is a Set Action
			restricted
			Parameters
				ParmItemGroup	is an ItemGroup
					default label is "ItemGroup"
			Parameter Rules
				ParmItemGroup
					required
						"ItemGroupIsRequired"
			Instance Selection
				where 	(ItemGroup	= ParmItemGroup)
				
			Action Rules
				Instance Rules
					invoke RecreateKeywordsForItemGroup KeywordShadow
						invoked.ParmItemGroup	= ParmItemGroup
						
		RecreateKeywordsAllItemGroups is a Set Action
			restricted
			Instance Selection
				where 	(ItemGroup	= ItemGroup)
				
			Action Rules
				Instance Rules
					invoke RecreateKeywordsForItemGroup KeywordShadow
						invoked.ParmItemGroup = ItemGroup

		RecreateItemLocationKeywords is a Set Action
			restricted
			Parameters
				ParmItemGroup	is an ItemGroup
					default label is "ItemGroup"
			Parameter Rules
				ParmItemGroup
					required
						"ItemGroupIsRequired"
			Instance Selection
				where 	(ItemGroup	= ParmItemGroup)
				
			Action Rules
				Instance Rules
					invoke RecreateKeywordsFromItemLocationAllItemGroups KeywordShadow
						invoked.ParmItemGroup	= ParmItemGroup
						
		RecreateKeywordsFromItemLocationAllItemGroups is a Set Action
			restricted
			Instance Selection
				where 	(ItemGroup	= ItemGroup)
				
			Action Rules
				Instance Rules
					invoke RecreateKeywordsFromItemLocationAllItemGroups KeywordShadow
						invoked.ParmItemGroup = ItemGroup

		CreateItemInformationWorksheetRecords is a Set Action  
			restricted
			Parameters
				ParmItemGroup is an ItemGroup
					default label is "ConvertSpecialItemWorkToItemInformationWorksheet"
				
			Parameter Rules
				ParmItemGroup
					required
						"ItemGroupIsRequired"
			
			Instance Selection
				where 	(ItemGroup	= ParmItemGroup)
				
			Action Rules
					
				Instance Rules
					for each ContractLineSpecialItemWorkRel
						invoke CreateItemInformationWorksheet each
			
		SetDefaultAllocationPriorityLevel is an Instance Action
			restricted
			Action Rules
				confirmation required
                    "Warning:_ThisWillCreateAn_Allocation_Priority_Level50AndWillSetItAsDefault._Proceed?"
				if (AllocationPriorityLevelExistsAndValidForDefault)
					invoke Create AllocationPriorityLevel
						invoked.FinanceEnterpriseGroup	= ItemGroup.FinanceEnterpriseGroup	
						invoked.AllocationPriorityLevel	= 50
						invoked.Description				= "Normal"
						invoked.UseAsDefault			= true
						invoked.PriorityCategoryType	= "S"	

		SetDefaultAllocationPriorityLevelForFEG is an Instance Action
			restricted
			Action Rules
				if (RSSLPLAllocationPriorityLevel50Exists)
					invoke Create AllocationPriorityLevel
						invoked.FinanceEnterpriseGroup	= ItemGroup.FinanceEnterpriseGroup	
						invoked.AllocationPriorityLevel	= 50
						invoked.Description				= "Normal"
						invoked.UseAsDefault			= true
						invoked.PriorityCategoryType	= "S"	
			
		RSSLPLSetDefaultAllocationPriorityLevel is a Set Action
			restricted
			Instance Selection
				where (RSSLPLAllocationPriorityLevel50Exists)
			
			Action Rules
				Instance Rules
					invoke SetDefaultAllocationPriorityLevelForFEG
						
	Action Exit Rules
		if (ItemGroup.BusinessGroup.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields")
					ActionCode	= ActionCode.Update
					if (action type.Create)
						ActionCode	= ActionCode.Create
					increment bod id.VariationID
					include TriggerItemGroupServiceRules

					
