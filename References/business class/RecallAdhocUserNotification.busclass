RecallAdhocUserNotification is a BusinessClass
	owned by recall
	prefix is RADV

	Ontology
		symbolic key is RecallAdhocUserNotification

	Persistent Fields
		RecallDomain

	Transient Fields
		TransientNoticeDocument			is BinaryDocument 

	Derived Fields

		DerivedEmail is a DerivedField
			type is Alpha 101
			default label is "Email"
			return RecallAdhocUser.Email 
		
		DerivedName is a DerivedField
			type is Alpha 101
			default label is "Name"
			return RecallAdhocUser.Name.PreferredFirstAndLastName

		DerivedPhoneDisplay is a StringField
			type is Alpha up to 60
			default label is "Phone"
			RecallAdhocUser.Phone.InternationalPrefix
			" "
			RecallAdhocUser.Phone.SubscriberNumber
			" "
			RecallAdhocUser.Phone.Extension

		NRACURL is a DerivedField 
			type is Alpha 200 
			default label is "URL"
			if (NRACReferenceRel exists)
				return "SupportingDocument: " + first NRACReferenceRel.Link
			else 
				return blank

	Conditions

	Sets

		ByUserForNotification
			Sort Order
				RecallGroup
				RecallAdhocUser
				RecallNotice

		ByDomain 
			Sort Order 
				RecallGroup 
				RecallNotice 
				RecallDomain 
				RecallAdhocUser

	Relations

		NRACReferenceRel 
  			one-to-many relation to RecallNoticeReference
  			Field Mapping uses symbolic key
  				related.RecallGroup		= RecallGroup
  				related.RecallNotice 	= RecallNotice
  			Instance Selection
  				where (related.Link entered
				and    related.ReferenceType = 3)

	Field Rules
		
	Actions

		Create is a Create Action
			restricted 
			Action Rules

			Exit Rules

				if (RecallNotice.ActiveRecallNotice
				and RecallGroup.SendRecallNoticeEmail
				and RecallAdhocUser.Active)
					generate document RecallDocumentReport as pdf in portrait font offset is -2
						set using action SendNotificationCreate

		Update is an Update Action
			restricted

		Delete is a Delete Action
			valid when (!RecallNotice.HistoricalRecallNotice)
				
			Action Rules 

				if (RecallNotice.ActiveRecallNotice
				and RecallGroup.SendRecallNoticeEmail
				and RecallAdhocUser.Active)
					send email 
						to RecallAdhocUser.Email 
						from RecallNotice.RecallCoordinator.RecallUser.EmployeeWorkEmailAddress
						subject "<RecallGroup.FinalRecallNoticeUnsubscribeEmailSubject>"
						Contents
							"<RecallGroup.FinalRecallNoticeUnsubscribeEmailContent>"
							"<NRACURL>"

		SendNotificationCreate is an Instance Action
			restricted
			run in background
			Parameters
				Report is BinaryDocument
		
			Action Rules
				TransientNoticeDocument = Report
				send email 
					to RecallAdhocUser.Email 
					from RecallNotice.RecallCoordinator.RecallUser.EmployeeWorkEmailAddress
					subject "<RecallGroup.FinalRecallNoticeEmailSubject>"
					Attachments
						attachment TransientNoticeDocument
							name is "NoticeDocument"
							mime type is "application/pdf"
					Contents
						"<RecallGroup.FinalRecallNoticeEmailContent>"
						"<NRACURL>"

		Purge is a Purge Action
			restricted

