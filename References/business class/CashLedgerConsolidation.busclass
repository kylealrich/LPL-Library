CashLedgerConsolidation is a BusinessClass
    owned by cb
    prefix is CBO
    classic name is CBCONSOL

    Ontology
        symbolic key is CashLedgerConsolidation
            classic set name is CBOSET1
            classic name for CashLedgerConsolidation.BankTransactionCode is BANK-INST-CODE
            classic name for CashLedgerConsolidation.LastMaintenanceDate is MAINT-DATE

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields

        BankBeginningBalance    is an InternationalAmount
            classic name is BANK-BEG-BAL
        BankEndingBalance       is an InternationalAmount
            classic name is BANK-END-BAL
        OpenBeginningBalance    is an InternationalAmount
            classic name is OPEN-BEG-BAL
        AccountBeginningBalance is an InternationalAmount
            classic name is ACCT-BEG-BAL
        IssuedAmount            is an InternationalAmount
            classic name is ISSUED
        IssuedCount             is a NbrOfTrans
            classic name is ISSUED-UNITS
        ReconciledAmount        is an InternationalAmount
            classic name is RECONCILED
        ReconciledCount         is a NbrOfTrans
            classic name is RECON-UNITS
        VoidedAmount            is an InternationalAmount
            classic name is VOIDED
        VoidedCount             is a NbrOfTrans
            classic name is VOIDED-UNITS
        StopPaidAmount          is an InternationalAmount
            classic name is STOP-PAID
        StopPaidCount           is a NbrOfTrans
            classic name is STOP-UNITS
        AdjustmentsAmount       is an InternationalAmount
            classic name is ADJUSTMENTS
        StaleDatedAmount        is an InternationalAmount
            classic name is STALE-DATED
        StaleDatedCount         is a NbrOfTrans
            classic name is STALE-UNITS
        EscheatedAmount         is an InternationalAmount
            classic name is ESCHEATED
        EscheatedCount          is a NbrOfTrans
            classic name is ESCH-UNITS
        OpenEndingBalance       is an InternationalAmount
            classic name is OPEN-END-BAL
        AccountEndingBalance    is an InternationalAmount
            classic name is ACCT-END-BAL
        LastMaintenanceTime     is TimeStamp
            classic name is MAINT-TIME


	Derived Fields
						
		CashCodeBankBeginningBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfCompaniesRel.BankBeginningBalance)
			
		CashCodeBankEndingBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfCompaniesRel.BankEndingBalance)

		CashCodeAccountBeginningBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfCompaniesRel.AccountBeginningBalance)
			
		CashCodeAccountEndingBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfCompaniesRel.AccountEndingBalance)
			
		CashCodeOpenBeginningBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfCompaniesRel.OpenBeginningBalance)
			
		CashCodeOpenEndingBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfCompaniesRel.OpenEndingBalance)

		CompanyOpenBeginningBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfBankTransactionCodesRel.OpenBeginningBalance)
			
		CompanyOpenEndingBalanceSum is a DerivedField  
			type is like InternationalAmount
			restricted
			return (sum CashLedgerConsolidationSumOfBankTransactionCodesRel.OpenEndingBalance)

	Transient Fields
		TransientFromCurrency						is a FromCurrency
			derive value from CashCode.Currency

	Local Fields
		LocalLastMaintenanceDate	is Date
		LocalCompany				is like Company
		LocalBankTransactionCode	is like BankTransactionCode
		
	Field Rules
		LastMaintenanceTime
			force default to current timestamp
						
	Conditions

		CashCodeLevelExists
			restricted
			when (CashLedgerConsolidationCashCodeRel exists)
				
        IsCashCodeLevel
        	restricted
            when (CashLedgerConsolidation.BankTransactionCode 	not entered
			and   CashLedgerConsolidation.Company				not entered)
            	
        IsCompanyLevel
        	restricted
            when (CashLedgerConsolidation.BankTransactionCode 	not entered
			and   CashLedgerConsolidation.Company				entered)
            	
        IsBankTransactionCodeLevel
        	restricted
            when (CashLedgerConsolidation.BankTransactionCode 	entered
			and   CashLedgerConsolidation.Company				entered)
            	            	            	
    Sets

        Set2
            indexed
            Sort Order
            	CashManagementGroup	
                CashLedgerConsolidation.LastMaintenanceDate
                CashCode
                CashLedgerConsolidation.Company
                CashLedgerConsolidation.BankTransactionCode

        Set3
            indexed
            Sort Order
            	CashManagementGroup	
                CashCode
                CashLedgerConsolidation.Company
                CashLedgerConsolidation.BankTransactionCode
                CashLedgerConsolidation.LastMaintenanceDate descending

        Set4
            indexed
            Sort Order
            	CashManagementGroup	
                CashCode
                CashLedgerConsolidation.BankTransactionCode
                CashLedgerConsolidation.Company
                CashLedgerConsolidation.LastMaintenanceDate descending

        Set5
            indexed
            Sort Order
            	CashManagementGroup	
                CashCode
                CashLedgerConsolidation.LastMaintenanceDate descending
                CashLedgerConsolidation.Company
                CashLedgerConsolidation.BankTransactionCode

	Relations

		CashLedgerConsolidationCashCodeLevelRel 
			one-to-many relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
			Instance Selection
				where (related.CashLedgerConsolidation.BankTransactionCode  = blank	
				and    related.CashLedgerConsolidation.Company			    = blank  	
				and    related.CashLedgerConsolidation.LastMaintenanceDate	= CashLedgerConsolidation.LastMaintenanceDate)

		CashLedgerConsolidationCompanyLevelRel 
			one-to-many relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
			Instance Selection
				where (related.CashLedgerConsolidation.BankTransactionCode 	= blank 
				and    related.CashLedgerConsolidation.Company			   != blank   			      
				and    related.CashLedgerConsolidation.LastMaintenanceDate	= CashLedgerConsolidation.LastMaintenanceDate)
				
		CashLedgerConsolidationBankTransactionCodeLevelRel 
			one-to-many relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
			Instance Selection
				where (related.CashLedgerConsolidation.BankTransactionCode != blank 
				and    related.CashLedgerConsolidation.Company			   != blank   
				and    related.CashLedgerConsolidation.LastMaintenanceDate	= CashLedgerConsolidation.LastMaintenanceDate)

		CashLedgerConsolidationCashCodeRel 
			one-to-one relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
				related.CashLedgerConsolidation.LastMaintenanceDate			= CashLedgerConsolidation.LastMaintenanceDate
				related.CashLedgerConsolidation.Company						= blank  	
				related.CashLedgerConsolidation.BankTransactionCode 		= blank

		CashLedgerConsolidationCompanyRel 
			one-to-one relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
				related.CashLedgerConsolidation.LastMaintenanceDate 		= CashLedgerConsolidation.LastMaintenanceDate
				related.CashLedgerConsolidation.Company						= CashLedgerConsolidation.Company  			      
				related.CashLedgerConsolidation.BankTransactionCode 		= blank

		CashLedgerConsolidationSumOfCompaniesRel
			one-to-many relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
			Instance Selection
				where (related.CashLedgerConsolidation.BankTransactionCode 	= blank
				and    related.CashLedgerConsolidation.Company			   != blank  			      
				and    related.CashLedgerConsolidation.LastMaintenanceDate	= CashLedgerConsolidation.LastMaintenanceDate)
								
		CashLedgerConsolidationSumOfBankTransactionCodesRel 
			one-to-many relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashCode.CashManagementGroup	
				related.CashCode 											= CashCode
			Instance Selection
				where (related.CashLedgerConsolidation.BankTransactionCode != blank
				and    related.CashLedgerConsolidation.Company			    = CashLedgerConsolidation.Company
				and    related.CashLedgerConsolidation.LastMaintenanceDate	= CashLedgerConsolidation.LastMaintenanceDate)

		CashLedgerConsolidationByCashCodeRel
			one-to-many relation to CashLedgerConsolidation
			Field Mapping uses Set5  
				related.CashManagementGroup 								= CashManagementGroup	
				related.CashCode 											= CashCode
				related.CashLedgerConsolidation.LastMaintenanceDate			= CashLedgerConsolidation.LastMaintenanceDate			
			Instance Selection
				where (related.CashLedgerConsolidation.BankTransactionCode !entered
				or    (related.CashLedgerConsolidation.BankTransactionCode	entered
				and    related.OpenEndingBalance							entered))	

		CashLedgerConsolidationCurrentDateRel
			one-to-one relation to CashLedgerConsolidation
			Field Mapping uses symbolic key  
				related.CashManagementGroup 								= CashManagementGroup	
				related.CashCode 											= CashCode
				related.CashLedgerConsolidation.BankTransactionCode 		= LocalBankTransactionCode
				related.CashLedgerConsolidation.Company						= LocalCompany  	
				related.CashLedgerConsolidation.LastMaintenanceDate			= current corporate date

										
	Rule Blocks

		AmountEdits
			constraint( IssuedAmount not entered
			and			ReconciledAmount not entered
			and			VoidedAmount not entered
			and			StopPaidAmount not entered
			and			AdjustmentsAmount not entered
			and			StaleDatedAmount not entered
			and			EscheatedAmount not entered)
				"CannotDelete;PostedAmountsExists"
		
	Actions
		ForwardPreviousDayBalances is an Instance Action 
			Action Rules
				constraint (CashLedgerConsolidation.LastMaintenanceDate < current corporate date)
					"BalanceRecordDateOf<CashLedgerConsolidation.LastMaintenanceDate>MustBePriorToCurrentDate"
				for each CashLedgerConsolidationByCashCodeRel
					LocalBankTransactionCode	= each.CashLedgerConsolidation.BankTransactionCode
					LocalCompany  				= each.CashLedgerConsolidation.Company
					if (CashLedgerConsolidationCurrentDateRel !exist)
						invoke Create
							invoked.CashManagementGroup							= CashManagementGroup
							invoked.CashCode									= CashCode
							invoked.CashLedgerConsolidation.BankTransactionCode = each.CashLedgerConsolidation.BankTransactionCode
							invoked.CashLedgerConsolidation.Company				= each.CashLedgerConsolidation.Company				
							invoked.CashLedgerConsolidation.LastMaintenanceDate = current corporate date
							invoked.LastMaintenanceTime 						= current timestamp
							invoked.BankBeginningBalance		 				= each.BankEndingBalance
							invoked.BankEndingBalance 							= each.BankEndingBalance
							invoked.OpenBeginningBalance		 				= each.OpenEndingBalance
							invoked.OpenEndingBalance							= each.OpenEndingBalance
							invoked.AccountBeginningBalance		 				= each.AccountEndingBalance																						
							invoked.AccountEndingBalance		 				= each.AccountEndingBalance

		UpdateCurrentDayBalances is an Instance Action 
			Parameters
				PrmIssuedCount		is like NbrOfTrans
					default label is "IssuedCount"			
				PrmIssuedAmount		is an InternationalAmount
					default label is "IssuedAmount"			
			Local Fields
				LocalIssuedAmount	is an InternationalAmount
			Action Rules
				if (CashLedgerConsolidation.BankTransactionCode.TransactionType.DebitTransaction)
					LocalIssuedAmount				= (PrmIssuedAmount *-1)
				else
					LocalIssuedAmount				= PrmIssuedAmount							
				IssuedCount							+= PrmIssuedCount
				IssuedAmount						+= LocalIssuedAmount
				if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
					ReconciledAmount				+= LocalIssuedAmount
					ReconciledCount					+= PrmIssuedCount																													
				else																													
					OpenEndingBalance				+= LocalIssuedAmount	
				invoke Update CashLedgerConsolidationCompanyRel
					invoked.IssuedCount				+= PrmIssuedCount
					invoked.IssuedAmount			+= LocalIssuedAmount
					invoked.AccountEndingBalance	+= LocalIssuedAmount
					if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
						invoked.ReconciledAmount	+= LocalIssuedAmount
						invoked.BankEndingBalance 	+= LocalIssuedAmount
						invoked.ReconciledCount		+= PrmIssuedCount
					else																													
						invoked.OpenEndingBalance	+= PrmIssuedAmount	
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.IssuedCount				+= PrmIssuedCount
					invoked.IssuedAmount			+= LocalIssuedAmount
					invoked.AccountEndingBalance	+= LocalIssuedAmount
					if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
						invoked.ReconciledAmount	+= LocalIssuedAmount
						invoked.BankEndingBalance 	+= LocalIssuedAmount
						invoked.ReconciledCount		+= PrmIssuedCount
					else																													
						invoked.OpenEndingBalance	+= PrmIssuedAmount	
				
		CreateCurrentDayBalance is a Create Action 
			Field Rules
				IssuedAmount
					if (CashLedgerConsolidation.BankTransactionCode.TransactionType.DebitTransaction)
						IssuedAmount		= (IssuedAmount	*-1)
				ReconciledAmount
					if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)
						ReconciledAmount	= IssuedAmount
						ReconciledCount		= IssuedCount
					else
						OpenEndingBalance	= IssuedAmount
						
			Exit Rules				
				if (CashLedgerConsolidationCompanyRel exists)
					invoke Update CashLedgerConsolidationCompanyRel
						invoked.IssuedCount				+= IssuedCount
						invoked.IssuedAmount			+= IssuedAmount
						invoked.AccountEndingBalance	+= IssuedAmount
						if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
							invoked.ReconciledAmount	+= ReconciledAmount
							invoked.BankEndingBalance 	+= ReconciledAmount
							invoked.ReconciledCount		+= ReconciledCount
						else
							invoked.OpenEndingBalance	+= IssuedAmount	
				else
					invoke Create CashLedgerConsolidationCompanyRel
						invoked.IssuedCount				= IssuedCount
						invoked.IssuedAmount			= IssuedAmount
						invoked.AccountEndingBalance	= IssuedAmount
						if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
							invoked.ReconciledAmount	= ReconciledAmount
							invoked.BankEndingBalance 	= ReconciledAmount
							invoked.ReconciledCount		= ReconciledCount
						else
							invoked.OpenEndingBalance	= IssuedAmount	
				if (CashLedgerConsolidationCashCodeRel exists)
					invoke Update CashLedgerConsolidationCashCodeRel
						invoked.IssuedCount				+= IssuedCount
						invoked.IssuedAmount			+= IssuedAmount
						invoked.AccountEndingBalance	+= IssuedAmount
						if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
							invoked.ReconciledAmount	+= ReconciledAmount
							invoked.BankEndingBalance 	+= ReconciledAmount
							invoked.ReconciledCount		+= ReconciledCount
						else
							invoked.OpenEndingBalance	+= IssuedAmount	
				else
					invoke Create CashLedgerConsolidationCashCodeRel
						invoked.IssuedCount				= IssuedCount
						invoked.IssuedAmount			= IssuedAmount
						invoked.AccountEndingBalance	= IssuedAmount
						if (CashLedgerConsolidation.BankTransactionCode.AutomaticReconciliation)																																			
							invoked.ReconciledAmount	= ReconciledAmount
							invoked.BankEndingBalance 	= ReconciledAmount
							invoked.ReconciledCount		= ReconciledCount
						else
							invoked.OpenEndingBalance	= IssuedAmount	
				

		Create is a Create Action
			restricted 
		
		Update is an Update Action
			restricted

		CreateBankBalanceRecord is a Create Action 
			Entrance Rules
				constraint (CashLedgerConsolidation.Company entered)
					"CompanyRequired"
			Action Rules					
			Exit Rules
				if (CashLedgerConsolidationCashCodeRel exists)
					invoke Update CashLedgerConsolidationCashCodeRel
						invoked.BankBeginningBalance						= CashCodeBankBeginningBalanceSum  
						invoked.BankEndingBalance							= CashCodeBankEndingBalanceSum  
				else				    
					invoke Create 
						fill in fields from this instance
						invoked.CashLedgerConsolidation.BankTransactionCode	= blank
						invoked.CashLedgerConsolidation.Company				= blank

		UpdateBankBalanceRecord is an Update Action
			Action Rules								
			Exit Rules																																																														
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.BankBeginningBalance	= CashCodeBankBeginningBalanceSum
					invoked.BankEndingBalance		= CashCodeBankEndingBalanceSum					

				    							
		DeleteBankBalanceRecord is a Delete Action
			Entrance Rules
				include AmountEdits
				constraint (AccountBeginningBalance not entered
				and			AccountEndingBalance not entered)
					"CannotDelete;PostedAccountBalancesExists"
				constraint (OpenBeginningBalance not entered
				and			OpenEndingBalance not entered)
					"CannotDelete;PostedOpenBalancesExists"					
			Action Rules								
			Exit Rules																																																														
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.BankBeginningBalance	= CashCodeBankBeginningBalanceSum
					invoked.BankEndingBalance		= CashCodeBankEndingBalanceSum		
										
		CreateAccountBalanceRecord is a Create Action 
			Entrance Rules
				constraint (CashLedgerConsolidation.Company entered)
					"CompanyRequired"
			Action Rules
			Exit Rules
				if (CashLedgerConsolidationCashCodeRel exists)
					invoke Update CashLedgerConsolidationCashCodeRel
						invoked.AccountBeginningBalance						= CashCodeAccountBeginningBalanceSum  
						invoked.AccountEndingBalance						= CashCodeAccountEndingBalanceSum  
				else
					invoke Create  
						fill in fields from this instance
						invoked.CashLedgerConsolidation.BankTransactionCode	= blank
						invoked.CashLedgerConsolidation.Company				= blank

		UpdateAccountBalanceRecord is an Update Action
			Action Rules
			Exit Rules																																																														
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.AccountBeginningBalance	= CashCodeAccountBeginningBalanceSum
					invoked.AccountEndingBalance	= CashCodeAccountEndingBalanceSum
														
		DeleteAccountBalanceRecord is a Delete Action
			Entrance Rules
				include AmountEdits
				constraint (BankBeginningBalance not entered
				and			BankEndingBalance not entered)
					"CannotDelete;PostedBankBalancesExists"				
				constraint (OpenBeginningBalance not entered
				and			OpenEndingBalance not entered)				
					"CannotDelete;PostedOpenBalancesExists"					
			Action Rules
			Exit Rules
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.AccountBeginningBalance	= CashCodeAccountBeginningBalanceSum
					invoked.AccountEndingBalance	= CashCodeAccountEndingBalanceSum
																		
				    					    								
		CreateOpenBalanceRecord is a Create Action 
			Entrance Rules
				constraint (CashLedgerConsolidation.Company entered)
					"CompanyRequired"
				constraint (CashLedgerConsolidation.BankTransactionCode entered)
					"BankTransactionCodeRequired"					
			Action Rules
			Exit Rules
				if (CashLedgerConsolidationCompanyRel exists)
					invoke Update CashLedgerConsolidationCompanyRel
						invoked.OpenBeginningBalance					   = CompanyOpenBeginningBalanceSum  
						invoked.OpenEndingBalance						   = CompanyOpenEndingBalanceSum  
				else				    
					invoke Create  
						fill in fields from this instance
						invoked.CashLedgerConsolidation.BankTransactionCode	= blank
				if (CashLedgerConsolidationCashCodeRel exists)
					invoke Update CashLedgerConsolidationCashCodeRel
						invoked.OpenBeginningBalance					   = CashCodeOpenBeginningBalanceSum  
						invoked.OpenEndingBalance		                   = CashCodeOpenEndingBalanceSum  
				else				    
					invoke Create  
						fill in fields from this instance
						invoked.CashLedgerConsolidation.BankTransactionCode	= blank
						invoked.CashLedgerConsolidation.Company				= blank
			

		UpdateOpenBalanceRecord is an Update Action
			Action Rules
			Exit Rules																																																														
				invoke Update CashLedgerConsolidationCompanyRel
					invoked.OpenBeginningBalance	= CompanyOpenBeginningBalanceSum
					invoked.OpenEndingBalance		= CompanyOpenEndingBalanceSum			
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.OpenBeginningBalance	= CashCodeOpenBeginningBalanceSum
					invoked.OpenEndingBalance		= CashCodeOpenEndingBalanceSum	
											
		DeleteOpenBalanceRecord is a Delete Action
			Entrance Rules
				include AmountEdits
				constraint (AccountBeginningBalance not entered
				and			AccountEndingBalance not entered)
					"CannotDelete;PostedAccountBalancesExists"	
				constraint (BankBeginningBalance not entered
				and			BankEndingBalance not entered)
					"CannotDelete;PostedBankBalancesExists"												
			Action Rules
			Exit Rules																																																														
				invoke Update CashLedgerConsolidationCompanyRel
					invoked.OpenBeginningBalance	= CompanyOpenBeginningBalanceSum
					invoked.OpenEndingBalance		= CompanyOpenEndingBalanceSum			
				invoke Update CashLedgerConsolidationCashCodeRel
					invoked.OpenBeginningBalance	= CashCodeOpenBeginningBalanceSum
					invoked.OpenEndingBalance		= CashCodeOpenEndingBalanceSum	
											
		
	
