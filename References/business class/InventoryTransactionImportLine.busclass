InventoryTransactionImportLine is a BusinessClass
    owned by ic
    prefix is ITI
    classic name is ICCTRNLIN

    Ontology
        symbolic key is InventoryTransactionImportLine
            classic set name is ITISET1
            classic name is LINE-NBR
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
        TransactionSystemCode
        Item
        TransactionLineExists     is Boolean
        Quantity
        UnitCost                  is an InternationalCost
        AddOnCostUnitCost         is an InternationalCost
            classic name is AOC-UCOST
        TransactionUOM            is a UnitOfMeasure
            classic name is TRAN-UOM
        AddOnChargeUnitCost       is an InternationalCost
        Reference
        Bin
        InventoryReasonCode
            classic name is REASON-CODE
        LineComment               is AlphaUpper size 30
            classic name is LINE-COMM
        OffsetAccount             is a TransactionCodeBlock
            classic name is OFFSET-ACCT
        AddOnCostAccount          is a TransactionCodeBlock
        FromToCompanyLocationBin
        FromDocument              is an InventoryTransaction
        NationalDrugCode
        GrantNumber
        EAMWorkOrderNumber        is a WorkOrderNumber
        SecondaryQuantity
            classic name is SEC-QTY
        OriginalQuantity          is Decimal size 13.4
            classic name is ORIG-QTY
		UseLastIssueCost          is Boolean

    Local Fields
        LocalPostingDate               is a PostingDate
        LocalGeneralLedgerSystemCode   is a GeneralLedgerSystemCode
		
    Conditions

        RecordExists
			restricted
			when (InventoryTransactionImportLine exists) 
    	
    	DetailsExist
    		restricted
    		when (InventoryTransactionImportLineDetailsRel exists)

        BinDetailsExist
            restricted
            when (InventoryTransactionImportLineBinDetailssRel exists)
    		
    Relations

        InventoryTransactionImportLineDetailsRel
            classic name is ICCTRNDTL
            one-to-many relation to InventoryTransactionImportLineDetail
            delete cascades
            Field Mapping uses symbolic key
                related.Company           				= Company
                related.InventoryLocation 				= InventoryLocation
                related.InventoryDocumentType       	= InventoryDocumentType
                related.InventoryTransactionImport 		= InventoryTransactionImport
                related.InventoryTransactionImportLine 	= InventoryTransactionImportLine

        InventoryTransactionImportLineBinDetailssRel
            classic name is ICCTRNDTL
            one-to-many relation to InventoryTransactionImportLineDetail
            delete cascades
            Field Mapping uses symbolic key
                related.Company           				= Company
                related.InventoryLocation 				= InventoryLocation
                related.InventoryDocumentType       	= InventoryDocumentType
                related.InventoryTransactionImport 		= InventoryTransactionImport
                related.InventoryTransactionImportLine 	= InventoryTransactionImportLine
            Instance Selection
                where (related.Bin entered)
                
        InventoryTransactionLineRel
        	one-to-one relation to InventoryTransactionLine
        	Field Mapping uses Set1
        		related.Company           							= Company
                related.InventoryLocation 							= InventoryLocation
                related.TransactionSystemCode   	    			= TransactionSystemCode
                related.InventoryTransaction	 					= InventoryTransactionImport
                related.InventoryTransactionLine.WarehouseShipment	= 0
                related.InventoryTransactionLine.LineNumber 		= InventoryTransactionImportLine
                related.InventoryTransactionLine.ComponentSequence 	= 0

        GeneralLedgerSystemCodeRel
            one-to-one relation to GeneralLedgerSystemCode
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup                  = Company.FinanceEnterpriseGroup
                related.GeneralLedgerSystemCode                 = "IC"

    Rule Blocks
        ValidateProjectDates
            if (GeneralLedgerSystemCodeRel exists)
                LocalGeneralLedgerSystemCode = GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode    
            if (Company.FinanceEnterpriseGroup.ProjectDateEdit.PostingDate)
                if (InventoryTransactionImport.GeneralLedgerDate entered)                                
                    LocalPostingDate = InventoryTransactionImport.GeneralLedgerDate
                else                                 
                    LocalPostingDate = current corporate date
        		
                
    Field Rules
    
    	TransactionSystemCode
    		default to "IC"
    	
    	Item 
    		required
    		
    	Quantity
    		required            
    
    Actions
    
    	Create is a Create Action
            Action Rules
                include ValidateProjectDates
    	
    	CreateNoRules is a Create Action
			restricted
			bypass field rules
    	
    	Update is an Update Action
  			valid when (!TransactionLineExists)
            Action Rules
                include ValidateProjectDates
    	
    	Delete is a Delete Action
    	
    		Entrance Rules
    		
    			if (TransactionLineExists)
    				invoke Delete InventoryTransactionLineRel
    	
    	FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
    	

