RecallOrganizationUser is a BusinessClass
    owned by recall
    prefix is RMROU

    Ontology
    	symbolic key is RecallOrganizationUser
    
    Patterns
    	implements ContextualParent
    		
    Persistent Fields
		RecallDomain		is a snapshot of RecallOrganization.RecallDomain
		InventoryCompany	is a snapshot of RecallOrganization.InventoryCompany
		RequestingLocation	is a snapshot of RecallOrganization.RequestingLocation
		InventoryLocation	is a snapshot of RecallOrganization.InventoryLocation
		TypeOfUser			is Numeric size 2
			States
				Responder		value is 1
				Watcher			value is 2
				Reviewer        value is 3
	
	Field Rules
		TypeOfUser
			if (TypeOfUser.Responder)
				if (OtherRespondersRel exists)
					confirmation required
						"Warning;TheCurrentResponder<first OtherRespondersRel.RecallUser.RecallUserName>WillBeChangedTo<RecallUser.RecallUserName>.WouldYouLikeToContinue?"
					invoke FastUpdate OtherRespondersRel
						invoked.NewTypeOfUser = 2 
						
			required
						
	Conditions
		ActiveUser
			restricted
			when (RecallUser.Active)
		
		HasLocationsDefined
			restricted
			when (RequestingLocation entered
			or	  InventoryLocation entered)
		
		CanBeAttachedToNotice
			restricted
			when (RecallOrganization.Active
			and	  RecallUser.Active)
		
		NoOtherRespondersExist
			restricted			
			when (!OtherRespondersRel exists)
			
		ActiveResponder
			restricted
			when (TypeOfUser.Responder)

		ActiveWatcher
			restricted
			when (TypeOfUser.Watcher)
			
	Relations
		OtherRespondersRel
			one-to-many relation to RecallOrganizationUser
			Field Mapping uses symbolic key
				related.RecallGroup		   = RecallGroup
				related.RecallOrganization = RecallOrganization
			Instance Selection
				where (related.TypeOfUser.Responder
				and	   related.RecallUser != RecallUser)
					
	Sets
		ByRecallDomain
			indexed
			Sort Order
				RecallGroup
				RecallDomain
				TypeOfUser
				RecallUser
				RecallOrganization
		
		ByDomainOrganization
			indexed
			Sort Order
				RecallGroup
				RecallDomain
				RecallOrganization
				TypeOfUser
				RecallUser
				
		ByRequestingLocation
			indexed
			Sort Order
				RecallGroup
				InventoryCompany
				RequestingLocation
				TypeOfUser
				RecallUser
				RecallOrganization
		
		ByInventoryLocation
			indexed
			Sort Order
				RecallGroup
				InventoryCompany
				InventoryLocation
				TypeOfUser
				RecallUser
				RecallOrganization
				
		ByUser
			Sort Order
				RecallGroup
				RecallUser
				RecallOrganization
				
		ByOrganization
			Sort Order
				RecallGroup
				RecallOrganization
				RecallUser
		
		ForResponder
			Sort Order
				RecallGroup
				RecallOrganization
				RecallUser
			Instance Selection
   				where (ActiveResponder)
   				
   		ForWatcher
			Sort Order
				RecallGroup
				RecallOrganization
				RecallUser
			Instance Selection
   				where (ActiveWatcher)
								
	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
		
			Field Rules
				TypeOfUser
				
					if (old TypeOfUser = 1
					or  old TypeOfUser = 2)
						constraint (TypeOfUser = 1
						or          TypeOfUser = 2)
							"CannotChangeFromResponderOrWatcherToReviewer"
						
		FastUpdate is an Instance Action
			restricted
			Parameters
				NewTypeOfUser is Numeric size 2
					
			Action Rules
				TypeOfUser = NewTypeOfUser
			
		Delete is a Delete Action		
		
		Purge is a Purge Action
			restricted
