SubleaseBillingInvoice is a BusinessClass
	owned by lm
	prefix is SLBI

	Ontology
		symbolic key is SubleaseBillingInvoice 
	
	Patterns
		implements Archivable
	
	Persistent Fields
		SubleaseGroup
		Customer
		CompanyCustomer
		Status							is Numeric 1
			States
				Unreleased			value is 0

				Released			value is 4

		InvoiceNumber					is like ReceivableInvoice
		Description
		InvoiceDate						is an ExchangeDate
		DueDate							is Date
		PostDate						is Date
		Currency						is a FromCurrency
		TaxAmount						is a FinanceCurrencyAmountGroup
		InvoiceAmount					is a FinanceCurrencyAmountGroup						
		ReceivableInvoiceType
		ReceivableInvoice
		SubleaseBillingInvoiceTemplate
		GeneratedInvoice				is an AlternateAttachment
		OriginatingDocument 			is BusinessObjectReference 
		EmailTimestamp					is TimeStamp
		IDMJob							is like IDMJob
		IDMDocumentPID					is an DocumentPID
		
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with GeneratedInvoice	
									
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with GeneratedInvoice

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with GeneratedInvoice

	Local Fields
		LocalNonLeaseComponent			is an ExecutoryCostCode
		LocalGeneralLedgerAccount		is a FinanceCodeBlockFull
		LocalPaymentAmount				is an InternationalAmount
		LocalDistributionAmount			is an InternationalAmount
		LocalMemoDistributionAmount		is an InternationalAmount
		LocalAccumulatedAmount			is an InternationalAmount
		LocalLeaseAllocationCodeDetail	is Numeric 6
		IDMGenerateDocument
		IDMAttributes
		IDMItem
		LocalRecipientEmailAddress		is like MultipleEmailAddress 
			holds pii
		LocalIDMEmailCc					is like MultipleEmailAddress 
			holds pii
		LocalSubleaseBillingInvoiceEmailSubject is Alpha 255
			Text Variables
				CompanyName				value is Company.Name
				CustomerName			value is Customer.Name
				SubleaseBillingInvoice  value is SubleaseBillingInvoice
				InvoiceDate				value is InvoiceDate
		IDMXMLDefinition
		AttributeCtr					is Numeric 2
		LocalExecute					is Boolean
		LocalAttributeCtr   			is Numeric 2
						
	Derived Fields
		DerivedIDMInvoiceLink is a DerivedField
			type is Alpha 2083
			restricted
    		if (IDMDocumentPID entered) 
    			IDMItem.DocumentType	= "FSM_SubleaseBillingInvoice"
    			IDMItem.IDMUniqueId		= IDMDocumentPID 
	   			return IDMItem.GetLink
			return blank
			
		DerivedToEmailAddress is a DerivedField 
			type is like MultipleEmailAddress
			holds pii
			restricted
			initialize LocalRecipientEmailAddress
			if (CompanyCustomerRel.SubleaseBillingInvoiceEmailContactsOnly)
				for each CompanyCustomerContactRel
					if (each.EmailSubleaseBillingInvoice
					and each.EmailAddress entered)
						if (LocalRecipientEmailAddress entered)
							LocalRecipientEmailAddress += ", "
						LocalRecipientEmailAddress += each.EmailAddress
			else
				LocalRecipientEmailAddress  = CompanyCustomerRel.EmailAddress
			return LocalRecipientEmailAddress	
		
		DerivedEmailCc			is a DerivedField 
			type is like MultipleEmailAddress
			holds pii
			restricted
			initialize LocalIDMEmailCc
			if (not CompanyCustomerRel.SubleaseBillingInvoiceEmailContactsOnly)
				for each CompanyCustomerContactRel
					if (LocalIDMEmailCc entered
					and each.EmailSubleaseBillingInvoice)
						LocalIDMEmailCc += ", "
					if (each.EmailSubleaseBillingInvoice)
						LocalIDMEmailCc += each.EmailAddress
			return LocalIDMEmailCc	
			
		ReceivableCompanyName is a StringField
			type is like Name
			Company.Name
		
		ReceivableCompanyAddressLine1 is a StringField
			type is like AddressLine
			Company.PostalAddress.DeliveryAddress.AddressLine1
		
		ReceivableCompanyAddressLine2 is a StringField
			type is like AddressLine
			Company.PostalAddress.DeliveryAddress.AddressLine2
			
		ReceivableCompanyAddressLine3 is a StringField
			type is like AddressLine
			Company.PostalAddress.DeliveryAddress.AddressLine3
			
		ReceivableCompanyAddressLine4 is a StringField
			type is like AddressLine
			Company.PostalAddress.DeliveryAddress.AddressLine4
				
		ReceivableCompanyCity is a StringField
			type is like Municipality
			Company.PostalAddress.Municipality
		
		ReceivableCompanyState is a StringField
			type is like StateProvince	
			Company.PostalAddress.StateProvince
			
		ReceivableCompanyZipCode is a StringField
			type is like PostalCode 
			Company.PostalAddress.PostalCode
		
		ReceivableCompanyPhoneNumber is a StringField
			type is Alpha size 15
			Company.PhoneNumber.SubscriberNumber
		
		CustomerName is a StringField
			type is like VendorName
			Customer.Name
		
		CustomerDeliveryAddressLine1 is a StringField
			type is like AddressLine
			Customer.PostalAddress.DeliveryAddress.AddressLine1
		
		CustomerDeliveryAddressLine2 is a StringField
			type is like AddressLine
			Customer.PostalAddress.DeliveryAddress.AddressLine2
			
		CustomerDeliveryAddressLine3 is a StringField
			type is like AddressLine
			Customer.PostalAddress.DeliveryAddress.AddressLine3
			
		CustomerDeliveryAddressLine4 is a StringField
			type is like AddressLine
			Customer.PostalAddress.DeliveryAddress.AddressLine4
		
		CustomerCity is a StringField
			type is like Municipality
			Customer.PostalAddress.Municipality
		
		CustomerState is a StringField
			type is like StateProvince	
			Customer.PostalAddress.StateProvince
			
		CustomerZipCode is a StringField
			type is like PostalCode 
			Customer.PostalAddress.PostalCode
		
		CustomerBillToName is a DerivedField
			type is Alpha size 120
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.Name
			else
				return Customer.Name
		
		CustomerBillToDeliveryAddressLine1 is a DerivedField
			type is like AddressLine
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.DeliveryAddress.AddressLine1
			else
				return Customer.PostalAddress.DeliveryAddress.AddressLine1
						
		CustomerBillToDeliveryAddressLine2 is a DerivedField
			type is like AddressLine
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.DeliveryAddress.AddressLine2
			else
				return Customer.PostalAddress.DeliveryAddress.AddressLine2
				
		CustomerBillToDeliveryAddressLine3 is a DerivedField
			type is like AddressLine
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.DeliveryAddress.AddressLine3
			else
				return Customer.PostalAddress.DeliveryAddress.AddressLine3
				
		CustomerBillToDeliveryAddressLine4 is a DerivedField
			type is like AddressLine
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.DeliveryAddress.AddressLine4
			else
				return Customer.PostalAddress.DeliveryAddress.AddressLine4
				
		CustomerBillToCity is a DerivedField
			type is like Municipality
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.Municipality
			else
				return Customer.PostalAddress.Municipality
				
		CustomerBillToState is a DerivedField
			type is like StateProvince
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.StateProvince
			else
				return Customer.PostalAddress.StateProvince
				
		CustomerBillToZipCode is a DerivedField
			type is like PostalCode
			if (DefaultBillToRel.CustomerBillTo.Name entered)
				return DefaultBillToRel.CustomerBillTo.PostalAddress.PostalCode
			else
				return Customer.PostalAddress.PostalCode
		
		PurchaseOrder is a StringField
			type is like CustomerPurchaseOrder
			ReceivableInvoice.CustomerPurchaseOrder
			
		TotalTaxAmount is a DerivedField
			type is like InternationalAmount
			return TaxAmount.CurrencyAmount
			
		TotalInvoiceAmount is a DerivedField
			type is like InternationalAmount
			return InvoiceAmount.CurrencyAmount
		
		TotalTransactionAmount is a DerivedField
			type is like InternationalAmount
			return TaxAmount.CurrencyAmount + InvoiceAmount.CurrencyAmount
		
		DerivedInvoiceSubject is a StringField
			type is Alpha 30
			"Invoice"
			" "
			InvoiceNumber
		
		DerivedInvoiceContent is a StringField
			type is Alpha up to 120
			DerivedInvoiceSubject
			" "
			"for"
			" "
			Customer.Name
			
		DerivedFileName	is a DerivedField
			type is like IDMFileName
			restricted
			if (Status.Unreleased)
				return "DRAFT_"+"Sublease Billing Invoice_"+Company+SubleaseBillingInvoice+OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate.DerivedOutputFormat
			else
				return "Sublease Billing Invoice_"+Company+SubleaseBillingInvoice+OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate.DerivedOutputFormat
				
		IDMDescriptionMsg is a MessageField
			restricted
			"Create_Sublease_Billing_Invoice"
			
		IDMDescriptionEmailMsg is a MessageField
			restricted
			"Sublease_Billing_Invoice_<SubleaseBillingInvoice>_was_sent_to_customer_<CustomerName>"
			
		PrintNotificationMsg is a MessageField
			restricted
			"Sublease_Billing_Invoice_<SubleaseBillingInvoice>_was_printed_to_<SubleaseGroup.Sublease.Printer>"
		
		DerivedIDMEmailSubject is a DerivedField
			type is Alpha size 255
			restricted
			if (Company.SubleaseBillingInvoiceEmailSubject entered)
				LocalSubleaseBillingInvoiceEmailSubject = Company.SubleaseBillingInvoiceEmailSubject
			return LocalSubleaseBillingInvoiceEmailSubject text
			
		
	Field Rules
		Customer
			required
			
		PostDate
			if (Company.CompanySystemClosingControlRel.Control)
				constraint (PostDate within Company.CompanySystemClosingControlRel.ValidEntryDate)
					"PostDateIsNotWithinValidEntryDatesForCompany<Company>;ValidDateRangeIs<Company.CompanySystemClosingControlRel.ValidEntryDate.Begin>-<Company.CompanySystemClosingControlRel.ValidEntryDate.End>"
		
	Conditions
		HasGeneratedInvoice
			restricted
			when (GeneratedInvoice entered
			and not Company.UseIDM)
			
		HasIDMGeneratedInvoice
			restricted
			when (IDMDocumentPID entered
			and Company.UseIDM)
		
		IsValidForActorContext
			restricted
			when (Company.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)			
			
		ValidForIDMGeneration
			restricted
			when (Company.UseIDM
			and OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate entered
			and IDMDocumentPID not entered)
			
		ValidForIDMRegeneration
			restricted
			when (Company.UseIDM
			and OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate entered
			and IDMDocumentPID entered)
			
	Relations
		SubleaseBillingInvoiceTransactionRel		
			one-to-many relation to SubleaseTransaction
			Field Mapping uses symbolic key
				related.Company													= SubleaseGroup.PayablesCompany	
				related.Lease													= SubleaseGroup.Lease
				related.Sublease												= SubleaseGroup.Sublease
			Instance Selection
				where (related.TransactionDate									= InvoiceDate)
				
		SubleaseExecutoryCostCodeRel
			one-to-one relation to SubleaseExecutoryCostCode
			Field Mapping uses symbolic key
				related.Company						= SubleaseGroup.PayablesCompany						
				related.Lease						= SubleaseGroup.Lease
				related.Sublease					= SubleaseGroup.Sublease
				related.ExecutoryCostCode			= LocalNonLeaseComponent
  
  		SubleaseBillingScheduleDetailByInvoiceDateRel
			one-to-many relation to SubleaseBillingScheduleDetail
			Field Mapping uses ByInvoiceDate
				related.Company						= SubleaseGroup.PayablesCompany						
				related.Lease						= SubleaseGroup.Lease
				related.Sublease					= SubleaseGroup.Sublease
				related.CompanyAndCustomer.Company	= Company
				related.CompanyAndCustomer.Customer	= Customer
				related.InvoiceDate 				= InvoiceDate
			Instance Selection
				where (related.Released)
				
		DefaultBillToRel
			one-to-many relation to CustomerBillTo
			Field Mapping uses Set2
				related.CustomerGroup	 			= Customer.CustomerGroup
				related.Customer		 			= Customer
				
		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company						= Company
				related.Customer					= Customer
				
		CompanyCustomerContactRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses symbolic key
				related.Company						= Company
				related.Customer					= Customer
				
		IDMJobRel
			one-to-one relation to IDMJob
			Field Mapping uses symbolic key
				related.IDMJob						= IDMJob

		IDMAdditionalAttributesLinesRel
			one-to-many relation to IDMAdditionalAttributesLines
			Field Mapping uses symbolic key
				related.IDMAdditionalAttributesHeader = "FSM_SubleaseBillingInvoice"
			Instance Selection
				where(related.IDMAdditionalAttributesHeader.ActivateAdditionalAttributes
				and	  related.ActivateAdditionalAttributes.Active)
	
	Sets
		BySublease
			Sort Order
				SubleaseGroup.PayablesCompany						
				SubleaseGroup.Lease
				SubleaseGroup.Sublease
				Company
				SubleaseBillingInvoice
						
	Rule Blocks
		CreateReceivableInvoiceAndDistributions
			ReceivableInvoiceType = "I" 
			invoke Create ReceivableInvoice
				assign result to ReceivableInvoice
				invoked.Company												= Company
				invoked.Customer											= Customer
				invoked.ReceivableInvoiceType								= ReceivableInvoiceType
				invoked.ReceivableInvoice									= InvoiceNumber
				invoked.BatchNumber											= current corporate date
				invoked.ReceivableProcessLevel								= OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.ProcessLevel
				invoked.ReceivableGeneralLedgerCode							= OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.ReceivableGeneralLedgerCode
				invoked.TransactionDate										= InvoiceDate
				invoked.GeneralLedgerDate									= PostDate
				invoked.InvoiceAmount				 						= InvoiceAmount
				invoked.TransientSignedTransactionAmount 					= InvoiceAmount.CurrencyAmount
				invoked.OriginalCurrency									= Currency
				invoked.TermsCode											= OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.TermsCode
				invoked.Description											= Description
				invoked.Origin												= "S" 
				invoked.System												= "LM"


			LocalGeneralLedgerAccount = (OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.ReceivableGeneralLedgerCode.DistributionAccount)
			LocalMemoDistributionAmount = TotalInvoiceAmount
			include CreateSubleaseTransaction 
				
			for each SubleaseBillingScheduleDetailByInvoiceDateRel
				invoke Create ReceivableInvoiceDetail
					fill in fields from ReceivableInvoice.ReceivableInvoice
					invoked.ReceivableInvoiceDetail.ReceivableInvoiceDetailType		= ReceivableInvoiceType
					invoked.ReceivableInvoiceDetail.Invoice							= InvoiceNumber
					invoked.ReceivableInvoiceDetail.PaymentSeq						= 1
					invoked.Status													= 1
					invoked.InvoiceAmount.CurrencyAmount							= each.PaymentAmount
					invoked.OriginalCurrency										= each.Currency
					if (DueDate not entered)
						invoked.TransientCalculateTerms								= true
					else
						invoked.DueDate												= DueDate


				if (each.SubleaseBillingScheduleDetail.ExecutoryCostCode entered)
					LocalNonLeaseComponent = each.SubleaseBillingScheduleDetail.ExecutoryCostCode
					if (SubleaseExecutoryCostCodeRel.ExecutoryAccount entered)
						LocalGeneralLedgerAccount = SubleaseExecutoryCostCodeRel.ExecutoryAccount
						LocalDistributionAmount = each.PaymentAmount
						include CreateReceivableGLDistribution
						LocalMemoDistributionAmount = LocalDistributionAmount * -1
						include CreateSubleaseTransaction 
					else
						initialize LocalAccumulatedAmount
						LocalPaymentAmount = each.PaymentAmount
						LocalLeaseAllocationCodeDetail = SubleaseExecutoryCostCodeRel.LeaseAllocationCode.last LeaseAllocationCodeDetail set.LeaseAllocationCodeDetail 
						for each SubleaseExecutoryCostCodeRel.LeaseAllocationCode.LeaseAllocationCodeDetail set
							if (each.LeaseAllocationCodeDetail != LocalLeaseAllocationCodeDetail)
								LocalDistributionAmount = (LocalPaymentAmount * each.AllocationPercent)
								LocalAccumulatedAmount += LocalDistributionAmount 
							else
								LocalDistributionAmount = (LocalPaymentAmount - LocalAccumulatedAmount)
							LocalGeneralLedgerAccount = each.DistributionAccount
							include CreateReceivableGLDistribution 
							LocalMemoDistributionAmount = LocalDistributionAmount * -1
							include CreateSubleaseTransaction 
							

				if (!each.SubleaseBillingScheduleDetail.ExecutoryCostCode entered
				and OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.StraightLineAmount != each.PaymentAmount)
					LocalGeneralLedgerAccount = SubleaseGroup.Sublease.AccruedRentAccount
					LocalDistributionAmount = ((OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.StraightLineAmount - each.PaymentAmount) * -1)
					include CreateReceivableGLDistribution
					LocalMemoDistributionAmount = LocalDistributionAmount * -1
					include CreateSubleaseTransaction 
					

				if (!each.SubleaseBillingScheduleDetail.ExecutoryCostCode entered)
					if (SubleaseGroup.Sublease.LeaseRevenueAccount entered)
						LocalGeneralLedgerAccount = SubleaseGroup.Sublease.LeaseRevenueAccount
						LocalDistributionAmount = OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.StraightLineAmount
						include CreateReceivableGLDistribution
						LocalMemoDistributionAmount = LocalDistributionAmount * -1
						include CreateSubleaseTransaction 
					else
						initialize LocalAccumulatedAmount
						LocalPaymentAmount = OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.StraightLineAmount
						LocalLeaseAllocationCodeDetail = SubleaseGroup.Sublease.LeaseAllocationCode.last LeaseAllocationCodeDetail set.LeaseAllocationCodeDetail 
						for each SubleaseGroup.Sublease.LeaseAllocationCode.LeaseAllocationCodeDetail set
							if (each.LeaseAllocationCodeDetail != LocalLeaseAllocationCodeDetail)
								LocalDistributionAmount = (LocalPaymentAmount * each.AllocationPercent)
								LocalAccumulatedAmount += LocalDistributionAmount 
							else
								LocalDistributionAmount = (LocalPaymentAmount - LocalAccumulatedAmount)
							LocalGeneralLedgerAccount = each.DistributionAccount
							include CreateReceivableGLDistribution
							LocalMemoDistributionAmount = LocalDistributionAmount * -1
							include CreateSubleaseTransaction 
				
			if (GeneratedInvoice entered)
				invoke Create ReceivableInvoiceAttachment
					invoked.Company												= Company
					invoked.ReceivableInvoiceType								= ReceivableInvoiceType
					invoked.ReceivableInvoice									= ReceivableInvoice.ReceivableInvoice 
					invoked.Name												= "Invoice" 
					invoked.Attachment											= GeneratedInvoice
							
		CreateReceivableGLDistribution
			invoke Create ReceivableGLDistribution
				invoked.FinanceEnterpriseGroup									= Company.FinanceEnterpriseGroup
				invoked.ReceivableCompanyGroup.GlCompany						= Company
				invoked.ReceivableCompanyDataGroup.TransactionCompany			= Company
				invoked.ReceivableCompanyGroup.OriginCompany					= Company
				invoked.GeneralLedgerDate										= InvoiceDate
				invoked.ReceivableCompanyDataGroup.Customer						= Customer
				invoked.Origin													= "RI"
				invoked.AccumulationType										= "D"
				invoked.DistributionSource										= "A"
				invoked.DocumentNumber											= InvoiceNumber
				invoked.GeneralLedgerAccount									= LocalGeneralLedgerAccount
				invoked.DistributionAmount.CurrencyAmount						= LocalDistributionAmount
				invoked.BatchNumber												= ReceivableInvoice.BatchNumber
				invoked.Invoice													= ReceivableInvoice.ReceivableInvoice
				invoked.TransType												= ReceivableInvoice.ReceivableInvoiceType
				invoked.TransactionDate											= InvoiceDate
				invoked.ReceivableCompanyDataGroup.ReceivableProcessLevel		= ReceivableInvoice.ReceivableProcessLevel
				invoked.OriginalCurrency										= ReceivableInvoice.OriginalCurrency
				invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyRate	= ReceivableInvoice.InvoiceAmount.FunctionalAmount.EnteredCurrencyRate
				invoked.CurrencyDecimalsNumber									= ReceivableInvoice.CurrencyDecimalsNumber
		
		CreateSubleaseTransaction
			invoke Create SubleaseTransaction
				invoked.Company													= SubleaseGroup.PayablesCompany	
				invoked.Lease													= SubleaseGroup.Lease
				invoked.Sublease												= SubleaseGroup.Sublease
				invoked.CompanyAndCustomer.Company								= Company
				invoked.CompanyAndCustomer.Customer								= Customer
				invoked.Status													= 9 
				invoked.GLFinanceCodeBlock										= LocalGeneralLedgerAccount
				invoked.TransactionAccount										= LocalGeneralLedgerAccount
				invoked.GLTransactionAmount										= LocalMemoDistributionAmount
				invoked.TransactionAmount.TransactionAmount						= LocalMemoDistributionAmount
				invoked.TransactionAmount.BaseAmount.ToCurrency					= SubleaseGroup.Sublease.BaseCurrency
				invoked.TransactionAmount.BaseAmount.EnteredCurrencyRate		= SubleaseGroup.Sublease.BaseCurrencyRate
				invoked.SubleaseTransactionType									= 1 
				invoked.SubleaseTransactionProcess								= 2 
				invoked.Description												= Description
				invoked.TransactionDate											= InvoiceDate
				invoked.PostingDate												= InvoiceDate
				invoked.Reference												= SubleaseGroup.Sublease
				invoked.OriginatingTransaction									= reference to this instance
				
		IDMGenerateSubleaseBillingInvoice
			initialize IDMGenerateDocument								


			IDMGenerateDocument.IDMXMLDefinition.Busclass	  	= reference to this instance
			IDMGenerateDocument.IDMXMLDefinition.ListName	  	= "SubleaseBillingInvoiceForIDMList"
			IDMGenerateDocument.IDMXMLDefinition.DocumentName	= "SubleaseBillingInvoice"
	   
			initialize IDMAttributes
			
			IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "Company"
			IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= Company
			IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "CompanyName"
			IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= Company.Name
			IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "Customer"
			IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue	= Customer
			IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "CustomerName"
			IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= Customer.Name
			IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "HeadLease"
			IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= SubleaseGroup.Lease
			IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "Sublease"
			IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= SubleaseGroup.Sublease
			IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeName	= "InvoiceNumber"
			IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeValue	= SubleaseBillingInvoice
			IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeName	= "InvoiceDate"
			IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeDate	= InvoiceDate
			IDMAttributes.SingleValue.IDMAttribute[9].IDMAttributeName	= "InvoiceAmount"
			IDMAttributes.SingleValue.IDMAttribute[9].IDMAttributeValue	= InvoiceAmount.CurrencyAmount
			
			if (IDMAdditionalAttributesLinesRel exists)
				AttributeCtr = 10
				include IDM.IDMAdditionalAttributes
			
			IDMGenerateDocument.IDMAttributes	 = IDMAttributes
			IDMGenerateDocument.TemplateUniqueId = OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate.IDMUniqueId
			IDMGenerateDocument.DocumentType	 = "FSM_SubleaseBillingInvoice"
			IDMGenerateDocument.FileName		 = DerivedFileName
			
			IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
			
			if (CompanyCustomerRel.EmailSubleaseBillingInvoice)

				if (Company.SubleaseBillingInvoiceEmailSubject entered)
					IDMGenerateDocument.IDMEmail.Subject = DerivedIDMEmailSubject
				else
					IDMGenerateDocument.IDMEmail.Subject = DerivedInvoiceSubject

				if (Company.SubleaseBillingInvoiceEmailTemplate entered)
					IDMGenerateDocument.EmailTemplateUniqueID	 = Company.SubleaseBillingInvoiceEmailTemplate.IDMUniqueId
				else
					IDMGenerateDocument.IDMEmail.Body	= blank
					
				IDMGenerateDocument.IDMEmail.From		 = CompanyCustomerRel.SubleaseBillingInvoiceFromAndReplyToEmail
				IDMGenerateDocument.IDMEmail.To			 = DerivedToEmailAddress
		
			if (CompanyCustomerContactRel exists
			and CompanyCustomerContactRel.EmailSubleaseBillingInvoice)
				IDMGenerateDocument.IDMEmail.Cc			 = DerivedEmailCc
					
			if (CompanyCustomerRel.PrintSubleaseBillingInvoice)		
				if (SubleaseGroup.Sublease.Printer entered)
					IDMGenerateDocument.IDMPrinter					= SubleaseGroup.Sublease.Printer
					
			invoke CreateFromGenerateDocument IDMJob
				assign result to IDMJobView
				invoked.Actor				= actor
				invoked.Description			= IDMDescriptionMsg
				invoked.IDMGenerateDocument	= IDMGenerateDocument
			
			IDMJob = IDMJobView.IDMJob
			
			invoke UpdateIDMDocumentPID in background
				run after IDMJobView.AsyncId
												
	Actions
		Create is a Create Action
			restricted
			Action Rules
				if (not Company.UseIDM)
					if (SubleaseBillingInvoiceTemplate entered)
						initialize GeneratedInvoice.File
						initialize GeneratedInvoice.Title
						initialize GeneratedInvoice.MimeType
						GeneratedInvoice.File		= SubleaseBillingInvoiceTemplate.File document
						GeneratedInvoice.Title		= InvoiceNumber
						GeneratedInvoice.MimeType	= SubleaseBillingInvoiceTemplate.MimeType
			Exit Rules
				if (Company.UseIDM)
					if (OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate entered
					and OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate.CheckIDMDocumentExistence)
						invoke GenerateIDMSubleaseBillingInvoice
				
					
						
		Update is an Update Action
			restricted
			
		Delete is a Delete Action
			valid when (Status.Unreleased)
			Entrance Rules
				constraint (!ReceivableInvoice entered)
					"CannotDelete;ReceivableInvoiceHasBeenCreated"
					
				for each SubleaseBillingScheduleDetailByInvoiceDateRel
					invoke Unrelease each
				
		GenerateIDMSubleaseBillingInvoice is an Instance Action
			valid when (ValidForIDMGeneration)
			default label is "GenerateDocument"
			
			Local Fields
				IDMJobView	is an IDMJob view
			
			Action Rules
				include IDMGenerateSubleaseBillingInvoice
				
		RegenerateIDMSubleaseBillingInvoice is an Instance Action
			valid when (ValidForIDMRegeneration)
			default label is "RegenerateDocument"
			
			Local Fields
				IDMJobView	is an IDMJob view
			
			Action Rules
				include IDMGenerateSubleaseBillingInvoice
		
				
		UpdateIDMDocumentPID is an Instance Action
			restricted
			Action Rules
				if(IDMJobRel.GenerationStatus.Finished)
					IDMDocumentPID = IDMJobRel.MDSID

		IDMPrint is an Instance Action
		    valid when (HasIDMGeneratedInvoice)
		    default label is "PrintInvoice"
		    Parameters
		        IDMPrinter
		        
		    Parameter Rules
		        IDMPrinter
		            initial value is SubleaseGroup.Sublease.Printer
		            required
		
		    Action Rules
		    
		        initialize IDMItem
		        IDMItem.DocumentType = "FSM_SubleaseBillingInvoice"
		        IDMItem.IDMUniqueId  = IDMDocumentPID
		        IDMItem.IDMPrinter	 = IDMPrinter
		        
		        constraint (IDMItem.GetItemDetails)
		            "DocumentDoesNotExistInIDM"
		
		        IDMItem.IDMPID = IDMItem.IDMItemDetails.PID
		        IDMItem.IDMUniqueId = IDMItem.IDMItemDetails.MdsId
		
		        invoke SendToPrinter IDMJob
		            invoked.Description = PrintNotificationMsg
		            invoked.FileName	= IDMItem.IDMItemDetails.FileName
		            invoked.IDMItem		= IDMItem
					
		IDMEmailInvoice is an Instance Action
		    default label is "EmailInvoice"
		    valid when (HasIDMGeneratedInvoice)
		    Local Fields
		        IDMJobView	is an IDMJob view
		        
		    Parameters
		        EmailFrom	is an EmailAddress
		            default label is "From"
					holds pii
		        EmailTo		is an MultipleEmailAddress 
		            default label is "To"
					holds pii
		        EmailCc		is an MultipleEmailAddress 
		            default label is "Cc"
					holds pii
		        EmailSubject is Alpha size 255
		        	default label is "Subject"
		        	Text Variables
						CompanyName				 value is Company.Name
						CustomerName			 value is "John Doe"
						SubleaseBillingInvoice 	 value is "1022"
						InvoiceDate 			 value is current corporate date
		        EmailTemplate is an IDMTemplate
		    Parameter Rules
		        EmailFrom
		            initial value is CompanyCustomerRel.SubleaseBillingInvoiceFromAndReplyToEmail
		            required
		        EmailTo
		            initial value is DerivedToEmailAddress
		            required
		        EmailCc
		            initial value is DerivedEmailCc
		        EmailSubject
		        	initial value is Company.SubleaseBillingInvoiceEmailSubject
		        EmailTemplate
		        	initial value is Company.SubleaseBillingInvoiceEmailTemplate
		        
		    Action Rules
		        
		        initialize IDMItem
		        
		        IDMItem.IDMXMLDefinition.Busclass	  	= reference to this instance
		        IDMItem.IDMXMLDefinition.ListName	  	= "SubleaseBillingInvoiceForIDMList"
		        IDMItem.IDMXMLDefinition.DocumentName	= "SubleaseBillingInvoice"
		
		        IDMItem.DocumentType		  = "FSM_SubleaseBillingInvoice"
		        IDMItem.IDMUniqueId			  = IDMDocumentPID
		        IDMItem.IDMEmail.From		  = EmailFrom
		        IDMItem.IDMEmail.To			  = EmailTo
		        IDMItem.IDMEmail.Cc	 		  = EmailCc
		        IDMItem.IDMEmail.Subject      = EmailSubject
		        IDMItem.EmailTemplateUniqueID = EmailTemplate.IDMUniqueId
		        

		        if (EmailSubject entered)
		        	LocalSubleaseBillingInvoiceEmailSubject = EmailSubject
		            IDMItem.IDMEmail.Subject = LocalSubleaseBillingInvoiceEmailSubject text
		        else
		            IDMItem.IDMEmail.Subject = DerivedInvoiceSubject
		

		        if (EmailTemplate entered)
		            IDMItem.EmailTemplateUniqueID	 = EmailTemplate.IDMUniqueId
		        else
		            IDMItem.IDMEmail.Body	= blank
		        
		        constraint (IDMItem.GetItemDetails)
		            "DocumentDoesNotExistInIDM"
		
		        IDMItem.IDMPID = IDMItem.IDMItemDetails.PID
		        IDMItem.IDMUniqueId = IDMItem.IDMItemDetails.MdsId
		
		        invoke SendToEmail IDMJob
		            invoked.Description	 = IDMDescriptionEmailMsg
		            invoked.FileName	 = IDMItem.IDMItemDetails.FileName
		            invoked.IDMItem		 = IDMItem
					
		UploadToIDM is an Instance Action  
			valid when (GeneratedInvoice.ValidForIDMUpload)

			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField with GeneratedInvoice
						
													
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (GeneratedInvoice.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField with GeneratedInvoice

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop

		EmailInvoice is an Instance Action
			valid when (HasGeneratedInvoice)
			run in background
			completion message is "EmailSent"
			disable multiple instance selection
			Parameters
				ToEmail			is EmailAddressField with multiple addresses
				FromEmail		is an EmailAddress
				CCEmail			is EmailAddressField with multiple addresses
				BccEmail		is EmailAddressField with multiple addresses
				Subject			is Text
				EmailContent	is RichText

			Parameter Rules
				ToEmail
					initial value is CompanyCustomer.EmailAddress
					required
						"ToEmailIsRequired"
						
				FromEmail
					initial value is actor.agent(Employee).Employee.EmployeeWorkEmailAddress
					required
						"FromEmailIsRequired"
						
				Subject
					initial value is DerivedInvoiceSubject
					required
						"EmailSubjectIsRequired"
					
				EmailContent
					initial value is DerivedInvoiceContent
					required
						"EmailSubjectIsRequired"

			Local Fields
				LocalFileName	is Alpha size 105 
				
			Action Rules
				if (EmailTimestamp entered)
					confirmation required
						"InvoiceWasEmailedOn<EmailTimestamp>;WouldYouLikeToResend?"
				
				LocalFileName = GeneratedInvoice.Title + ".rtf"	
				send email
					to ToEmail
					from FromEmail
					cc CCEmail
					bcc BccEmail
					subject "<Subject>"
					Attachments
						attachment GeneratedInvoice.File
							name is LocalFileName
							mime type is GeneratedInvoice.MimeType
					Contents
						"<EmailContent>"
				
				EmailTimestamp = current timestamp
		
		MassRelease is a Set Action
			completion message is "MassReleaseComplete"
			no records message is "MassReleaseComplete"
			Parameters
				PrmCompany				is a PayablesCompany
					default label is "Company"
				PrmLease				is a Lease
					default label is "HeadLease"
				PrmSublease				is a Sublease
					default label is "Sublease"
				PrmCompanyGroup			is a GeneralLedgerCompanyGroup
					default label is "CompanyGroup"
				PrmProcessThruDate		is Date
					default label is "ProcessThruDate"
					
			Parameter Rules
				PrmCompany
					initial value is PrmSublease.Company
					constraint (!PrmCompanyGroup entered)
						"CanEnter_\\Company,_\\Company/\\Head_\\Lease/\\Sublease,Or_\\Company_\\Group"
					if (PrmSublease entered)
						required
							"CompanyIsRequired"
										
				PrmLease
					initial value is PrmSublease.Lease
					if (PrmSublease entered)
						required
							"HeadLeaseIsRequired"
						
				PrmCompanyGroup
					constraint (!PrmCompany entered)
						"CanEnter_\\Company,_\\Company/\\Head_\\Lease/\\Sublease,Or_\\Company_\\Group"
					constraint (!PrmLease entered)
						"CanEnter_\\Company,_\\Company/\\Head_\\Lease/\\Sublease,Or_\\Company_\\Group"
					constraint (!PrmSublease entered)
						"CanEnter_\\Company,_\\Company/\\Head_\\Lease/\\Sublease,Or_\\Company_\\Group"
						
				PrmProcessThruDate
					required
						"ProcessThruDateIsRequired"
					constraint (PrmCompany entered
					or			PrmCompanyGroup entered)
						"Company,_\\Company/\\Head_\\Lease/\\Sublease,Or_\\Company_\\GroupIsRequired"
					
			Instance Selection
				where (false)
				
			Action Rules
				Empty Set Rules
					if (PrmCompany entered
					or  PrmSublease entered)
						invoke MassReleaseSet SubleaseBillingInvoice
							invoked.PrmCompany 				= PrmCompany
							if (PrmSublease entered)
								invoked.PrmLease 			= PrmLease
								invoked.PrmSublease 		= PrmSublease
							invoked.PrmProcessThruDate 		= PrmProcessThruDate	
					
					if (PrmCompanyGroup entered)
						for each PrmCompanyGroup.GeneralLedgerCompanyGroupMembersRel
							invoke MassReleaseSet SubleaseBillingInvoice
								invoked.PrmCompany 			= each.Company
								invoked.PrmProcessThruDate	= PrmProcessThruDate
								
		MassReleaseSet is a Set Action
			restricted
			run in background
			Parameters
				PrmCompany				is a PayablesCompany
				PrmLease				is a Lease
				PrmSublease				is a Sublease
				PrmProcessThruDate		is Date
					
			Parameter Rules
				PrmCompany
					required
					
				PrmProcessThruDate
					required
							
			Instance Selection
				where (Company = PrmCompany
				and   (!PrmSublease entered
				or	  (SubleaseGroup.Lease = PrmLease
				and	   SubleaseGroup.Sublease = PrmSublease))
				and    Status.Unreleased
				and    InvoiceDate <= PrmProcessThruDate)
				
			Action Rules
				Instance Rules
					invoke Release
								
		Release is an Instance Action
			valid when (Status.Unreleased)
			Action Rules
				if (Company.CompanySystemClosingControlRel.Control)
					constraint (PostDate within Company.CompanySystemClosingControlRel.ValidEntryDate)
						"PostDateIsNotWithinValidEntryDatesForCompany<Company>;ValidDateRangeIs<Company.CompanySystemClosingControlRel.ValidEntryDate.Begin>-<Company.CompanySystemClosingControlRel.ValidEntryDate.End>"
		



				
				include CreateReceivableInvoiceAndDistributions
				
				Status = Status.Released
				
			Exit Rules
				invoke Unreleased.Release ReceivableInvoice
				if (Company.UseIDM)
					if (OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate entered
					and OriginatingDocument(SubleaseBillingScheduleDetail).SubleaseBillingSchedule.SubleaseBillingInvoiceIDMTemplate.CheckIDMDocumentExistence)
						invoke GenerateIDMSubleaseBillingInvoice
				
		Purge is a Purge Action
			restricted
								
