ContractGPOParticipantDelta is a BusinessClass    
	owned by po
	
	prefix is CXPD
	
	Ontology
		symbolic key is ContractGPOParticipantDelta
	
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       		
	Persistent Fields
		HealthTrustGPOID	                  is Alpha 20                      
		FacilityName                          is Alpha 100                     
		ContractNumber                        is a GPOContractNumber           
		TierNumber                            is like ContractImportTier       
		TierParticipationEffectiveDate        is Date                          
		TierParticipationExpireDate           is Date	                       
		HealthTrustCOID                       is Alpha 7                      
		TierDefinition                        is Text                          
		MembershipEligibleDate                is Date                         
		RecordModifiedTimestamp               is Alpha 20		              
		TierName                              is Alpha size 192                
		OperationFlag                         is Alpha 10         		        

	Local Fields
		LocalContractGroup                    is a ContractGroup
		LocalGPOContractNumber                is a GPOContractNumber
		LocalHealthTrustGPOID                 is Alpha 20
		LocalTierParticipationEffectiveDate   is Date 
		LocalTierNumber                       is like ContractImportTier 
	
	Conditions
	
	Relations

		ContractImportRel
			one-to-many relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber   = LocalGPOContractNumber
				related.ContractGroup       = LocalContractGroup

		ContractGPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber	= LocalGPOContractNumber 
				related.GPOMembership       = LocalHealthTrustGPOID
			Instance Selection 
				where (related.TierLevel	= LocalTierNumber
				and    related.StartDate    = LocalTierParticipationEffectiveDate) 

	Sets
		ByGPOContractNumber
			Sort Order
				ContractNumber
				HealthTrustGPOID
				ContractGPOParticipantDelta
	
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassParticipantDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Purge 

		Purge is a Purge Action 

		LoadParticipantRecords is a Set Action 
			restricted 
			Parameters 
				ParmContractGroup		is a ContractGroup
			
			Sort Order
				ContractNumber
				ContractGPOParticipantDelta
			
			Action Rules 

				Instance Rules 

					LocalContractGroup              		= ParmContractGroup
					LocalGPOContractNumber 					= ContractNumber
					LocalHealthTrustGPOID           		= HealthTrustGPOID
					LocalTierParticipationEffectiveDate     = TierParticipationEffectiveDate 
					LocalTierNumber                       	= TierNumber

					if (OperationFlag = "Add")
						if (ContractGPOParticipantRel !exists)
							invoke Create ContractGPOParticipant 
								invoked.GPOContractNumber         	      	= ContractNumber                
								invoked.GPOMembership	                	= HealthTrustGPOID 
								invoked.MembershipName                      = FacilityName 
								invoked.TierLevel                          	= TierNumber      
								invoked.StartDate      						= TierParticipationEffectiveDate  
								invoked.EndDate         					= TierParticipationExpireDate  
								invoked.COID                     			= HealthTrustCOID 
								invoked.TierDefinition                   	= TierDefinition                               
								invoked.MembershipEligibleDate              = MembershipEligibleDate                           
								invoked.TierName                            = TierName       								      
								invoked.RecordModifiedTimestamp             = RecordModifiedTimestamp   

							if (ContractImportRel exists)
								invoke FastUpdate ContractImportRel 
									invoked.HeaderUpdated = true  


					else 
					if (OperationFlag = "Update")
						if (ContractGPOParticipantRel exists)
							invoke Update first ContractGPOParticipantRel 
								invoked.EndDate         					= TierParticipationExpireDate
								invoked.MembershipEligibleDate              = MembershipEligibleDate
							if (ContractImportRel exists)
								invoke FastUpdate ContractImportRel 
									invoked.HeaderUpdated = true 

					else 
					if (OperationFlag = "DEL")
						if (ContractGPOParticipantRel exists)
							invoke Delete first ContractGPOParticipantRel 
	
