ContractSubcontractorDiversity is a BusinessClass
	owned by po
	
	prefix is CXD
	
	Ontology
		symbolic key is ContractSubcontractorDiversity
	
	Patterns
	
	Persistent Fields
		Supplier
		DiversityPercent               is a Percent
		EffectiveDate                  is a snapshot of Contract.EffectiveDate
	
	Derived Fields
		
		DisplayCommittedAmount is a DerivedField
			type is like InternationalAmount
			return (ContractSubcontractor.DisplayCommittedAmount * DiversityPercent)
				
		DerivedDiversityPctOfContract is a ComputeField
			type is Percent 5.2
			((DisplayCommittedAmount / Contract.DerivedDiversityAmtFromPct) * DiversityPercent)
			
		SubName is a StringField
			type is Alpha 30
			ContractSubcontractor.Subcontractor.SubcontractorName
		
		DerivedSupplierContactName is a StringField
		    type is Alpha size 101
		    restricted
		    actor.agent(SupplierSourceId).SupplierSourceId.MainContact.FirstAndLastName
		    
		DerivedSupplierName is a StringField
			type is Alpha size 30
			restricted
			actor.agent(SupplierSourceId).Supplier.SupplierName
	
	Context Fields
		ContractDateRange							is a DateRange
	
	Conditions
		MultipleDiversityCodes
			when (OtherContractDiversityRel exists)
			
		EffectiveDateWithinRange
    		restricted
    		when (EffectiveDate within ContractDateRange)
    		
    	ClosedContract
			restricted
			when(Contract.ContractStatus.Closed)
		ValidForDelete
			restricted
			when (!ClosedContract
			and   !ContractSubcontractor.ContractSubcontractorPaymentRels exists)
	
	Relations
		
		SupplierDiversityResponseRel
			one-to-many relation to SubcontractorDiversity
			Field Mapping uses BySubcontractor
				related.SupplierGroup  = ContractGroup
				related.Subcontractor  = ContractSubcontractor.Subcontractor
				
		OtherContractDiversityRel
			one-to-many relation to ContractSubcontractorDiversity
			Field Mapping uses symbolic key
				related.ContractGroup    			= ContractGroup
				related.Contract         			= Contract
				related.ContractSubcontractor 		= ContractSubcontractor
			Instance Selection
				where (related.PayablesDiversityCode != PayablesDiversityCode)
				
		SupplierGroupExtensionRel
			one-to-one relation to SupplierGroupExtension
			Field Mapping uses symbolic key
				related.SupplierGroup	= ContractGroup
	
	Sets
		ByDiversityCode
			Sort Order
				PayablesDiversityCode
				ContractGroup
				Contract
				ContractSubcontractor
				
		BySupplier
			allow duplicates
			Sort Order
				PayablesDiversityCode
				Supplier
	
	Field Rules
	
		DiversityPercent
		
			default to 1
	
	Actions
		Create is a Create Action
			restricted
			
			Action Rules
				Supplier = Contract.Supplier

		Update is an Update Action
			valid when (!ClosedContract)
			
			Exit Rules
				if (actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress entered)
					if (ContractGroup.SupplierGroupRel.EmailOnContractSubcontractorUpdate)
						send email
							to Contract.PrimaryContactRel.DerivedEmail
							from Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
							cc actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress
							cc Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
							subject "<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailSubject>"
							Contents 
								"<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailContent>"
								"MaintainedBy<DerivedSupplierContactName>Of<DerivedSupplierName>"
								if (DiversityPercent changed)
									"DiversityPercentHasBeenChanged"
			
		Delete is a Delete Action
			valid when (ValidForDelete)
			
			Exit Rules
				if (actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress entered)
					if (ContractGroup.SupplierGroupRel.EmailOnContractSubcontractorUpdate)
						send email
							to Contract.PrimaryContactRel.DerivedEmail
							from Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
							cc actor.agent(SupplierSourceId).SupplierSourceId.EmailAddress
							cc Contract.Supplier.SupplierGroup.NotificationEmailAddressOnSub
							subject "<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailSubject>"
							Contents 
								"<SupplierGroupExtensionRel.FinalMaintainContractSubcontractorEmailContent>"
								"SubcontractorDiversityHasBeenDeletedBy<DerivedSupplierContactName>Of<DerivedSupplierName>"
	

	
