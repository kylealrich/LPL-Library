TrueCostResourceUtilizationDriverEvent is a BusinessClass
	owned by truecost
	sql name is TrueCostRUDE
	prefix is TCDE
	
	Ontology
		symbolic key is TrueCostResourceUtilizationDriverEvent
		
	Patterns
	
	Persistent Fields
		Value 					is a TrueCostAmount
		EventDate				is Date
			default label is "<DateLabel>"
		AccountingEntity
			default label is "<TrueCostConfiguration.AccountingEntityLabel>"
		AccountingUnit
			default label is "<TrueCostConfiguration.AccountingUnitLabel>"
		GeneralLedgerChartAccount
			restricted
			default label is "<TrueCostConfiguration.AccountLabel>"
		Project
			default label is "<TrueCostConfiguration.ProjectLabel>"
		FinanceDimension1
			default label is "<TrueCostConfiguration.FinanceDimension1Label>"
		FinanceDimension2
			default label is "<TrueCostConfiguration.FinanceDimension2Label>"
		FinanceDimension3
			default label is "<TrueCostConfiguration.FinanceDimension3Label>"
		FinanceDimension4
			default label is "<TrueCostConfiguration.FinanceDimension4Label>"
		FinanceDimension5
			default label is "<TrueCostConfiguration.FinanceDimension5Label>"
		FinanceDimension6
			default label is "<TrueCostConfiguration.FinanceDimension6Label>"
		FinanceDimension7
			default label is "<TrueCostConfiguration.FinanceDimension7Label>"
		FinanceDimension8
			default label is "<TrueCostConfiguration.FinanceDimension8Label>"
		FinanceDimension9
			default label is "<TrueCostConfiguration.FinanceDimension9Label>"
		FinanceDimension10
			default label is "<TrueCostConfiguration.FinanceDimension10Label>"
		GeneralLedgerCalendarPeriod
			default label is "Period"
			
	Local Fields
		LocalJulianDate							is Alpha 7
		LocalEventPeriod 						is a GeneralLedgerCalendarPeriod
		LocalTrueCostDriverEventTotal			is a TrueCostResourceUtilizationDriverEventTotal
		LocalStartDate							is Alpha 8
		LocalEndDate							is Alpha 8
		LocalStartDay							is Alpha 3
		LocalStartYear							is Alpha 4
		LocalEndDay								is Alpha 3
		LocalEndYear							is Alpha 4
		LocalDate1								is Date
		LocalDate2								is Date
		LocalAlphaDay							is Alpha 3
		LocalAlphaYear							is Alpha 4
		LocalSumAmount							is Decimal 31.3
		LocalDimension							is Alpha 105
		LocalAmount								is a TrueCostAmount
		LocalPreviousAmount						is a TrueCostAmount
					
	Field Rules		
		EventDate
			if (IsByDriverEvent)
				required

			cannot be changed
			
		GeneralLedgerCalendarPeriod
			if (IsByEffectiveDate)
				required

			cannot be changed
				
		Value
			required
			constraint (Value > 0)
				"ValueCannotBeNegative"	

		AccountingEntity
			required
				"<TrueCostConfiguration.AccountingEntityLabel>IsRequired"

			cannot be changed

		GeneralLedgerChartAccount
			if (TrueCostResourceUtilizationDriver.HasAccount)
				required
					"<TrueCostConfiguration.AccountLabel>IsRequired"

				cannot be changed

			constraint (GeneralLedgerChartAccount.ChartType.Posting)
				"<TrueCostConfiguration.AccountLabel>MustBePosting"


		AccountingUnit
			if (TrueCostResourceUtilizationDriver.HasAccountingUnit)
				required
					"<TrueCostConfiguration.FinanceEnterpriseGroup.AccountingUnitLabel>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceEnterpriseGroup.AccountingUnitLabel>IsNotEnabledInDriver"
							
			constraint (AccountingUnit.PostingAccountingUnit)
				"<TrueCostConfiguration.AccountingUnitLabel>MustBePosting"

		Project
			if (TrueCostResourceUtilizationDriver.HasProject)
				required
					"<TrueCostConfiguration.FinanceEnterpriseGroup.ProjectLabel>IsRequired"

				cannot be changed
			else
				cannot be entered
					"<TrueCostConfiguration.FinanceEnterpriseGroup.ProjectLabel>IsNotEnabledInDriver"
			
			constraint (Project.ProjectType.Posting)
				"<TrueCostConfiguration.ProjectLabel>MustBePosting"


		FinanceDimension1
			if (TrueCostResourceUtilizationDriver.HasDimension1)
				required
					"<TrueCostConfiguration.FinanceDimension1Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension1Label>IsNotEnabledInDriver"
					
			constraint (FinanceDimension1.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension1Label>MustBePosting"

		FinanceDimension2
			if (TrueCostResourceUtilizationDriver.HasDimension2)
				required
					"<TrueCostConfiguration.FinanceDimension2Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension2Label>IsNotEnabledInDriver"

			constraint (FinanceDimension2.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension2Label>MustBePosting"

		FinanceDimension3
			if (TrueCostResourceUtilizationDriver.HasDimension3)
				required
					"<TrueCostConfiguration.FinanceDimension3Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension3Label>IsNotEnabledInDriver"

			constraint (FinanceDimension3.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension3Label>MustBePosting"

		FinanceDimension4
			if (TrueCostResourceUtilizationDriver.HasDimension4)
				required
					"<TrueCostConfiguration.FinanceDimension4Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension4Label>IsNotEnabledInDriver"

			constraint (FinanceDimension4.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension4Label>MustBePosting"

		FinanceDimension5
			if (TrueCostResourceUtilizationDriver.HasDimension5)
				required
					"<TrueCostConfiguration.FinanceDimension5Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension5Label>IsNotEnabledInDriver"

			constraint (FinanceDimension5.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension5Label>MustBePosting"

		FinanceDimension6
			if (TrueCostResourceUtilizationDriver.HasDimension6)
				required
					"<TrueCostConfiguration.FinanceDimension6Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension6Label>IsNotEnabledInDriver"

			constraint (FinanceDimension6.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension6Label>MustBePosting"

		FinanceDimension7
			if (TrueCostResourceUtilizationDriver.HasDimension7)
				required
					"<TrueCostConfiguration.FinanceDimension7Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension7Label>IsNotEnabledInDriver"

			constraint (FinanceDimension7.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension7Label>MustBePosting"

		FinanceDimension8
			if (TrueCostResourceUtilizationDriver.HasDimension8)
				required
					"<TrueCostConfiguration.FinanceDimension8Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension8Label>IsNotEnabledInDriver"

			constraint (FinanceDimension8.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension8Label>MustBePosting"

		FinanceDimension9
			if (TrueCostResourceUtilizationDriver.HasDimension9)
				required
					"<TrueCostConfiguration.FinanceDimension9Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension9Label>IsNotEnabledInDriver"

			constraint (FinanceDimension9.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension9Label>MustBePosting"

		FinanceDimension10
			if (TrueCostResourceUtilizationDriver.HasDimension10)
				required
					"<TrueCostConfiguration.FinanceDimension10Label>IsRequired"

				cannot be changed

			else
				cannot be entered
					"<TrueCostConfiguration.FinanceDimension10Label>IsNotEnabledInDriver"

			constraint (FinanceDimension10.DimensionType.Posting)
				"<TrueCostConfiguration.FinanceDimension10Label>MustBePosting"
	Sets
		ByEventDateDescending	
			not indexed		
			Sort Order
				TrueCostConfiguration
				TrueCostResourceUtilizationDriver
				TrueCostResourceUtilizationDriverEvent.EventDateJulian descending
				TrueCostResourceUtilizationDriverEvent.DimensionCode descending

	Conditions
		HasCalendar
			restricted
			when(TrueCostConfiguration.Calendar entered)
		
		IsByDriverEvent
			restricted
			when (TrueCostResourceUtilizationDriver.DriverType.ByEventDate)
		IsByEffectiveDate
			restricted
			when (TrueCostResourceUtilizationDriver.DriverType.ByEffectiveDate)
			
	Derived Fields
		JulianDate is a DerivedField
			type is Alpha 7
			restricted
			LocalAlphaYear = EventDate year
			LocalAlphaDay  = EventDate year day
			JulianDate = LocalAlphaYear
			if (LocalAlphaDay size = 1)
				JulianDate += "00"
			else
			if (LocalAlphaDay size = 2)
				JulianDate += "0"
			JulianDate += LocalAlphaDay

		JulianStartDate is a DerivedField
			type is Alpha 7
			restricted
			LocalStartDate	= EventDate period
			LocalStartDate += "01"
			LocalDate1		= LocalStartDate 
			LocalStartYear	= LocalDate1 year
			LocalStartDay	= LocalDate1 year day
			JulianStartDate = LocalStartYear
			if (LocalStartDay size = 1)
				JulianStartDate += "00"
			else
			if (LocalStartDay size = 2)
				JulianStartDate += "0"
			JulianStartDate += LocalStartDay

		JulianEndDate is a DerivedField
			type is Alpha 7
			restricted
			LocalEndDate	= EventDate period
			LocalEndDate	+= EventDate days in month
			LocalDate2		= LocalEndDate 
			LocalEndYear	= LocalDate2 year
			LocalEndDay		= LocalDate2 year day
			JulianEndDate	= LocalEndYear
			if (LocalEndDay size = 1)
				JulianEndDate += "00"
			else
			if (LocalEndDay size = 2)
				JulianEndDate += "0"
			JulianEndDate += LocalEndDay

        DimensionCode        is a StringField
            type is Alpha 105
            restricted
            AccountingEntity.DerivedSequenceNumber
            AccountingUnit.DerivedSequenceNumber
            GeneralLedgerChartAccount.DerivedSequenceNumber
            Project.DerivedSequenceNumber
			FinanceDimension1.DerivedSequenceNumber
			FinanceDimension2.DerivedSequenceNumber
			FinanceDimension3.DerivedSequenceNumber
			FinanceDimension4.DerivedSequenceNumber
			FinanceDimension5.DerivedSequenceNumber
			FinanceDimension6.DerivedSequenceNumber
			FinanceDimension7.DerivedSequenceNumber
			FinanceDimension8.DerivedSequenceNumber
			FinanceDimension9.DerivedSequenceNumber
			FinanceDimension10.DerivedSequenceNumber
			
		DateLabel is a DerivedField 
			type is Alpha 15
			restricted
			if (IsByDriverEvent)
				return "Event Date"
			else
				return "Effective Date"
		DefaultLabel is a DerivedField 
			type is Alpha 14
			restricted
			if (IsByDriverEvent)
				return "Driver Event"
			else
				return "Driver Data"
	Relations
		GeneralLedgerCalendarPeriodsRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses ByTopNodeTypeAndJulianDates
				related.FinanceEnterpriseGroup 						= TrueCostConfiguration
				related.TopNode										= TrueCostConfiguration.Calendar.TopNode
				related.PeriodType									= 3 
				related.EndDateJulian								>= LocalJulianDate
			Instance Selection
				where (related.StartDateJulian						<= LocalJulianDate)

    	TrueCostResourceUtilizationDriverEventRel
    		one-to-many relation to TrueCostResourceUtilizationDriverEvent
    		Field Mapping uses symbolic key
    			related.TrueCostConfiguration									= TrueCostConfiguration
    			related.TrueCostResourceUtilizationDriver						= TrueCostResourceUtilizationDriver
    			related.TrueCostResourceUtilizationDriverEvent.DimensionCode	= LocalDimension
    			related.TrueCostResourceUtilizationDriverEvent.EventDateJulian	>= JulianStartDate
    		Instance Selection
    			where (related.TrueCostResourceUtilizationDriverEvent.EventDateJulian <= JulianEndDate)
    	


	Rule Blocks
		CheckDriverEventSum
			LocalDimension 	= TrueCostResourceUtilizationDriverEvent.DimensionCode
			LocalSumAmount	= Value + sum TrueCostResourceUtilizationDriverEventRel.Value
			constraint (LocalSumAmount >= 0)
				"DriverEventValueCannotBeNegative"
	
		UpdateDriverEventTotal
			if (HasCalendar)
				LocalJulianDate									= JulianDate
				LocalEventPeriod								= GeneralLedgerCalendarPeriodsRel.GeneralLedgerCalendarPeriod
				if (LocalEventPeriod entered)
					LocalTrueCostDriverEventTotal.DimensionCode	= TrueCostResourceUtilizationDriverEvent.DimensionCode
					LocalTrueCostDriverEventTotal.EventPeriod	= LocalEventPeriod
					invoke Update LocalTrueCostDriverEventTotal
						invoked.TrueCostConfiguration				 = TrueCostConfiguration
						invoked.TrueCostResourceUtilizationDriver	 = TrueCostResourceUtilizationDriver
						invoked.Value 								+= LocalAmount
						if (action type.Create)
							invoked.AccountingEntity				 = AccountingEntity
							invoked.AccountingUnit					 = AccountingUnit
							invoked.GeneralLedgerChartAccount		 = GeneralLedgerChartAccount
							invoked.Project							 = Project
							invoked.FinanceDimension1				 = FinanceDimension1
							invoked.FinanceDimension2				 = FinanceDimension2
							invoked.FinanceDimension3				 = FinanceDimension3
							invoked.FinanceDimension4				 = FinanceDimension4
							invoked.FinanceDimension5				 = FinanceDimension5
							invoked.FinanceDimension6				 = FinanceDimension6
							invoked.FinanceDimension7				 = FinanceDimension7
							invoked.FinanceDimension8				 = FinanceDimension8
							invoked.FinanceDimension9				 = FinanceDimension9
							invoked.FinanceDimension10				 = FinanceDimension10
							
			
	Actions
		Create is a Create Action
			completion message is "<DefaultLabel>Created"
			Action Rules	
				if (IsByEffectiveDate)
					constraint (EventDate not entered)
						"<DateLabel>CannotBeEntered"
					
					EventDate	= GeneralLedgerCalendarPeriod.Date
				else
					constraint (GeneralLedgerCalendarPeriod not entered)
						"PeriodCannotBeEntered"
						
				TrueCostResourceUtilizationDriverEvent.DimensionCode	= DimensionCode
				TrueCostResourceUtilizationDriverEvent.EventDateJulian  = JulianDate	
				constraint (TrueCostResourceUtilizationDriverEvent not exists)
					"<DefaultLabel>AlreadyExist"
					
				if (IsByDriverEvent)  
					include CheckDriverEventSum
					
			Exit Rules
				if (IsByDriverEvent)
					LocalAmount		= Value
					include UpdateDriverEventTotal
					
		Update is an Update Action
			completion message is "<DefaultLabel>Updated"
			Action Rules
				if (IsByDriverEvent)
					LocalPreviousAmount = old Value
					include CheckDriverEventSum
			Exit Rules
				if (IsByDriverEvent)
					LocalAmount		= Value - LocalPreviousAmount
					include UpdateDriverEventTotal
				
		Delete is a Delete Action
			completion message is "<DefaultLabel>Deleted"
			Action Rules
				if (IsByDriverEvent)
					LocalDimension 	= TrueCostResourceUtilizationDriverEvent.DimensionCode
					LocalSumAmount	= sum TrueCostResourceUtilizationDriverEventRel.Value
					constraint (LocalSumAmount >= 0)
						"DriverEventValueCannotBeNegative"	
			Exit Rules
				if (IsByDriverEvent)
					LocalAmount		= Value * -1
					include UpdateDriverEventTotal

		CopyRecord is an Instance Action

			Parameters
				PrmAccountingEntity					is an AccountingEntity
					default label is "<TrueCostConfiguration.AccountingEntityLabel>"
				PrmGeneralLedgerChartAccount		is a GeneralLedgerChartAccount
					default label is "<TrueCostConfiguration.AccountLabel>"
				PrmAccountingUnit					is an AccountingUnit
					default label is "<TrueCostConfiguration.AccountingUnitLabel>"
				PrmProject							is a Project
					default label is "<TrueCostConfiguration.ProjectLabel>"
				PrmFinanceDimension1				is a FinanceDimension1
					default label is "<TrueCostConfiguration.FinanceDimension1Label>"
				PrmFinanceDimension2				is a FinanceDimension2
					default label is "<TrueCostConfiguration.FinanceDimension2Label>"
				PrmFinanceDimension3				is a FinanceDimension3
					default label is "<TrueCostConfiguration.FinanceDimension3Label>"
				PrmFinanceDimension4				is a FinanceDimension4
					default label is "<TrueCostConfiguration.FinanceDimension4Label>"
				PrmFinanceDimension5				is a FinanceDimension5
					default label is "<TrueCostConfiguration.FinanceDimension5Label>"
				PrmFinanceDimension6				is a FinanceDimension6
					default label is "<TrueCostConfiguration.FinanceDimension6Label>"
				PrmFinanceDimension7				is a FinanceDimension7
					default label is "<TrueCostConfiguration.FinanceDimension7Label>"
				PrmFinanceDimension8				is a FinanceDimension8
					default label is "<TrueCostConfiguration.FinanceDimension8Label>"
				PrmFinanceDimension9				is a FinanceDimension9
					default label is "<TrueCostConfiguration.FinanceDimension9Label>"
				PrmFinanceDimension10				is a FinanceDimension10
					default label is "<TrueCostConfiguration.FinanceDimension10Label>"
				PrmEventDate						is Date
					default label is "<DateLabel>"
				PrmGeneralLedgerCalendarPeriod		is a GeneralLedgerCalendarPeriod
					default label is "Period"
				PrmValue							is a TrueCostAmount
					default label is "Value"
			Parameter Rules
				PrmAccountingEntity
					initial value is AccountingEntity
				PrmGeneralLedgerChartAccount
					initial value is GeneralLedgerChartAccount
				PrmAccountingUnit
					initial value is AccountingUnit
				PrmProject
					initial value is Project
				PrmFinanceDimension1
					initial value is FinanceDimension1
				PrmFinanceDimension2
					initial value is FinanceDimension2
				PrmFinanceDimension3
					initial value is FinanceDimension3
				PrmFinanceDimension4
					initial value is FinanceDimension4
				PrmFinanceDimension5
					initial value is FinanceDimension5
				PrmFinanceDimension6
					initial value is FinanceDimension6
				PrmFinanceDimension7
					initial value is FinanceDimension7
				PrmFinanceDimension8
					initial value is FinanceDimension8
				PrmFinanceDimension9
					initial value is FinanceDimension9
				PrmFinanceDimension10
					initial value is FinanceDimension10
				PrmValue
					initial value is Value
				
			Action Rules
                invoke Create TrueCostResourceUtilizationDriverEvent
                	invoked.TrueCostConfiguration			  = TrueCostConfiguration
                	invoked.TrueCostResourceUtilizationDriver = TrueCostResourceUtilizationDriver
                    invoked.Value					 		  = PrmValue
                    if (IsByDriverEvent)
                    	invoked.EventDate				  	  = PrmEventDate
                    else
                    	invoked.GeneralLedgerCalendarPeriod	  = PrmGeneralLedgerCalendarPeriod
                    invoked.AccountingEntity		  		  = PrmAccountingEntity
                    invoked.AccountingUnit			  		  = PrmAccountingUnit
                    invoked.GeneralLedgerChartAccount 		  = PrmGeneralLedgerChartAccount
                    invoked.Project			     	  		  = PrmProject
                    invoked.FinanceDimension1    	  		  = PrmFinanceDimension1
                    invoked.FinanceDimension2    	  		  = PrmFinanceDimension2
                    invoked.FinanceDimension3    	  		  = PrmFinanceDimension3
                    invoked.FinanceDimension4    	  		  = PrmFinanceDimension4
                    invoked.FinanceDimension5    	  		  = PrmFinanceDimension5
                    invoked.FinanceDimension6    	  		  = PrmFinanceDimension6
                    invoked.FinanceDimension7    	  		  = PrmFinanceDimension7
                    invoked.FinanceDimension8    	  		  = PrmFinanceDimension8
                    invoked.FinanceDimension9    	  		  = PrmFinanceDimension9
                    invoked.FinanceDimension10     	  		  = PrmFinanceDimension10

		RebuildSummarizedDriverEvents is a Set Action
			restricted
			default label is untranslatable

			Parameters				
				PrmTrueCostConfiguration				is a TrueCostConfiguration
					default label is "Configuration"

			Parameter Rules		
				PrmTrueCostConfiguration
					required

			Local Fields
				LocalActor								is an Actor
				
			Instance Selection
				where(TrueCostResourceUtilizationDriverEvent.TrueCostConfiguration = PrmTrueCostConfiguration)
    		Action Rules

				Set Rules
					Entrance Rules
						LocalActor 				   = actor
					Exit Rules
						send notification
							to LocalActor
							description is "RebuildSummarizedDriverEventsHasBeenCompleted"
							priority is high
							detail is "RebuildSummarizedDriverEventsHasBeenCompleted"

    			Instance Rules
					LocalJulianDate										= JulianDate
					LocalEventPeriod 									= GeneralLedgerCalendarPeriodsRel.GeneralLedgerCalendarPeriod
					if (LocalEventPeriod entered)
						LocalTrueCostDriverEventTotal.DimensionCode		= TrueCostResourceUtilizationDriverEvent.DimensionCode
						LocalTrueCostDriverEventTotal.EventPeriod		= TrueCostResourceUtilizationDriverEvent.LocalEventPeriod
						invoke Update LocalTrueCostDriverEventTotal
							invoked.TrueCostConfiguration				 	= TrueCostConfiguration
							invoked.TrueCostResourceUtilizationDriver	 	= TrueCostResourceUtilizationDriver
							invoked.Value 									+= Value
				
                    
