PurchaseOrderImportLineDistribution is a BusinessClass
    owned by po
    prefix is CMS
    sql name is POrderImportLineDistribution
    classic name is POCMMDIST

    Ontology
        symbolic key is PurchaseOrderImportLineDistribution

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
    	RunGroup
    	Company							is a PurchasingCompany
    	PurchaseOrderImport     		is like PurchaseOrderImport
			default label is "InterfacedPurchaseOrder"
		POCode
		LineNumber
		AddOnCharge
		DistributionSequence
        AssetTemplate
        Asset
        HasError                 is Boolean
        DistributionPercent      is Percent size 6.3
            classic name is DIST-PERCENT
        DistributionQuantity     is a Quantity
            classic name is DIST-QTY
        DistributionAmount       is an InternationalAmount
            classic name is DIST-AMOUNT
        DistributionAccount      is a TransactionCodeBlock
            classic name for DistributionAccount.ToAccountingEntity is DIST-COMPANY
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for DistributionAccount.Project is ACTIVITY

	Local Fields
		LocalInterfaceResult            is like PurchaseOrderInterfaceResult
		LocalErrorMessage               is Alpha 150	
		LocalFinanceEnterpriseGroup     is like FinanceEnterpriseGroup
		LocalPurchaseOrder              is like PurchaseOrder
		LocalGeneralLedgerSystemCode 	is a GeneralLedgerSystemCode
		LocalPostingDate				is a PostingDate
		LocalTransactionDate			is an ExchangeDate

	Transient Fields
		TransientExchangeDate               is an ExchangeDate
		TransientPostingDate                is a PostingDate
		TransientFromCurrency				is a FromCurrency
			derive value from DerivedFromCurrency

	Context Fields
		ContextPurchaseOrderImport is a PurchaseOrderImport
	
	Derived Fields
		DerivedFromCurrency	is a DerivedField
			type is like FromCurrency
			restricted
			if (PurchaseOrderImportRel.Currency entered)
				return PurchaseOrderImportRel.Currency
			else
			if (DistributionAccount.GeneralLedgerChartAccount.Currency entered)
				return DistributionAccount.GeneralLedgerChartAccount.Currency

	Conditions
	
		OtherDistributionsExist
			restricted
			when (OtherDistributionsForLineRel exists)
			
		PurchaseOrderImportExists
			restricted
			when (PurchaseOrderImportRel exists)
	
	Relations
		PurchaseOrderImportLineRel
			one-to-many relation to PurchaseOrderImportLine
			Field Mapping uses Set1
				related.Company						= Company
				related.PurchaseOrderImport     	= PurchaseOrderImport
				related.POCode                      = POCode
				related.LineNumber					= LineNumber
				
		PurchaseOrderImportRel
			one-to-many relation to PurchaseOrderImport
			Field Mapping uses ByRunGroup2
				related.RunGroup            = RunGroup
				related.Company				= Company
				related.PurchaseOrderImport	= PurchaseOrderImport
				related.POCode              = POCode
				
		LocalInterfaceResultsRel
			one-to-one relation to PurchaseOrderInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= LocalFinanceEnterpriseGroup
				related.PurchaseOrderInterfaceResult		= LocalInterfaceResult 
				
		OtherDistributionsForLineRel
			one-to-many relation to PurchaseOrderImportLineDistribution
			Field Mapping uses Set1
				related.Company                             = Company
				related.PurchaseOrderImport                 = PurchaseOrderImport
				related.POCode                              = POCode
				related.LineNumber                          = LineNumber
			Instance Selection
				where (related.PurchaseOrderImportLineDistribution  != PurchaseOrderImportLineDistribution)
				
		POLineDistributionRel
			one-to-one relation to PurchaseOrderLineDistribution
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrder						= LocalPurchaseOrder
				related.PurchaseOrderLine					= LineNumber
				related.AddOnCharge							= AddOnCharge
				related.PurchaseOrderLineDistribution		= DistributionSequence

		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode	= "PO"

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

	Rule Blocks
		ValidateProjectDates
			if (GeneralLedgerSystemCodeRel exists)
				LocalGeneralLedgerSystemCode = GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode
			if (DistributionAccount.Project entered
			and GeneralLedgerCompanyRel.FinanceEnterpriseGroup.ProjectDateEdit entered)
				if (GeneralLedgerCompanyRel.FinanceEnterpriseGroup.ProjectDateEdit.TransactionDate)
					if (PurchaseOrderImportRel.PurchaseOrderDate entered)
						LocalTransactionDate		= PurchaseOrderImportRel.PurchaseOrderDate
					else
						LocalTransactionDate		= current corporate date
				else
					LocalPostingDate				= PurchaseOrderImportLineRel.EarlyDeliveryDate
					if (PurchaseOrderImportLineRel.EarlyDeliveryDate not entered
					and PurchaseOrderImportRel.DefaultDeliveryDate entered)
						LocalPostingDate			= PurchaseOrderImportRel.DefaultDeliveryDate
					else
						LocalPostingDate			= current corporate date

    Sets

		Set1
			not indexed
			Sort Order
				Company
				PurchaseOrderImport
				POCode
				LineNumber
				AddOnCharge
				DistributionSequence
				PurchaseOrderImportLineDistribution
				
		ByRunGroup
			Sort Order
				RunGroup
				Company
				PurchaseOrderImport
				POCode
				LineNumber
				AddOnCharge
				DistributionSequence
				PurchaseOrderImportLineDistribution

	Field Rules
		RunGroup
			default to PurchaseOrderImportRel.RunGroup
			default to PurchaseOrderImportLineRel.RunGroup
			required
		Company
			required
			cannot be changed
		PurchaseOrderImport
			required
			cannot be changed
		POCode
			initial value is ContextPurchaseOrderImport.POCode
		LineNumber
			required
			cannot be changed
		AddOnCharge
			cannot be changed
		DistributionPercent
			if (DistributionPercent !entered
			and DistributionQuantity !entered
			and DistributionAmount !entered)
				default to 100%
				
		DistributionAccount
			required
			if (DistributionAccount.Project entered)
				default TransientExchangeDate to PurchaseOrderImportRel.PurchaseOrderDate
				default TransientExchangeDate to PurchaseOrderImportLineRel.PurchaseOrder.PurchaseOrderDate
				default TransientPostingDate  to PurchaseOrderImportRel.PurchaseOrderDate
				default TransientPostingDate  to PurchaseOrderImportLineRel.PurchaseOrder.PurchaseOrderDate
			
	Actions
		Create is a Create Action
			Field Rules
				DistributionSequence
					autosequence using Set1
			Action Rules
				include ValidateProjectDates

		Update is an Update Action
			Action Rules
				include ValidateProjectDates

		Delete is a Delete Action
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
		
		UpdateRunGroup is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmRunGroup					is a RunGroup
				NewRunGroup					is a RunGroup

			Instance Selection
				where (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    RunGroup = PrmRunGroup)
				
			Sort Order
				RunGroup
				Company
			
			Action Rules
				Instance Rules
					RunGroup   = NewRunGroup
				
		LoadInterfacedPurchaseOrderLineDistributions is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup 
				PrmRunGroup					is a RunGroup
				PrmCompany                  is a PurchasingCompany
				PrmResult                   is like PurchaseOrderInterfaceResult
				PrmSpecificAction           is Numeric 1
					States
						Create              value is 1
						Update              value is 2
						Cancel              value is 3
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					
			Local Fields

				LocalPurchaseOrderRecordInError is Boolean
				PurchaseOrderWithErrors         is like PurchaseOrderImport
			
			Instance Selection			
				where (RunGroup	= PrmRunGroup
				and   ((PrmCompany  not entered
				and     GeneralLedgerCompanyRel.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
				or      Company	= PrmCompany))

			Sort Order
				RunGroup
				Company	
				PurchaseOrderImport
				POCode
				
			Action Rules
				Empty Set Rules
					invoke LoadInterfacedSpreadOrStandaloneAddOnCharges PurchaseOrderImportAddOnCharge
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup                 = PrmRunGroup
						invoked.PrmCompany                  = PrmCompany
						invoked.PrmResult                   = PrmResult	
						invoked.PrmSpecificAction           = PrmSpecificAction			
				
				RunGroup Set Rules
					Exit Rules
						
						invoke LoadInterfacedSpreadOrStandaloneAddOnCharges PurchaseOrderImportAddOnCharge
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup                 = PrmRunGroup
							invoked.PrmCompany                  = PrmCompany
							invoked.PrmResult                   = PrmResult	
							invoked.PrmSpecificAction           = PrmSpecificAction	
						
				POCode Set Rules
					
					Entrance Rules
						LocalPurchaseOrder = PurchaseOrderImportRel.PurchaseOrder
						LocalPurchaseOrderRecordInError = PurchaseOrderImportRel.RecordInError
						
				Instance Rules
					LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
					LocalInterfaceResult = PrmResult
					LocalPurchaseOrder = PurchaseOrderImportRel.PurchaseOrder
					if (!PurchaseOrderImportExists)  
						LocalPurchaseOrder 				= PurchaseOrderImportLineRel.PurchaseOrder
						LocalPurchaseOrderRecordInError = PurchaseOrderImportLineRel.HasError 
					if  (LocalPurchaseOrderRecordInError = false)
						HasError = false
						if  (PurchaseOrderImport !=PurchaseOrderWithErrors
						and (PrmSpecificAction = 1
						or   PrmSpecificAction = 0
						or   PrmSpecificAction = 2))
							if (POLineDistributionRel not exists)
								invoke Create PurchaseOrderLineDistribution
									resume on error
										HasError    		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.PurchaseOrder 					= LocalPurchaseOrder
									invoked.PurchaseOrderLine               = LineNumber
									invoked.AssetInformation.Asset 			= Asset
									invoked.AssetInformation.AssetTemplate 	= AssetTemplate
									if (LocalInterfaceResultsRel.ReleasePurchaseOrders = true)
										invoked.TransientFromMassPOCreateAndRelease = true
							else
								invoke Update POLineDistributionRel
									resume on error
										HasError    		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
									invoked.PurchaseOrder 					= LocalPurchaseOrder
									invoked.PurchaseOrderLine               = LineNumber
									invoked.AssetInformation.Asset 			= Asset
									invoked.AssetInformation.AssetTemplate 	= AssetTemplate
									if (LocalInterfaceResultsRel.ReleasePurchaseOrders = true)
										invoked.TransientFromMassPOCreateAndRelease = true
																	
							if (HasError)
								invoke SetError PurchaseOrderImportRel
									invoked.PrmErrorMessage = LocalErrorMessage
									invoked.PrmResult       = PrmResult
									invoked.PrmErrorLevel   = 8
								PurchaseOrderWithErrors = PurchaseOrderImport 
							else
								if (!OtherDistributionsExist)
									if (first PurchaseOrderImportLineRel.PurchaseOrder entered)
										invoke UpdateFromInterface first PurchaseOrderImportLineRel.PurchaseOrderRel
											invoked.SetInterfaceInProcessFalse   = true
											if (LocalInterfaceResultsRel.ReleasePurchaseOrders)
												invoked.ReleasePurchaseOrder = true	
									invoke FastDelete first PurchaseOrderImportLineRel
								invoke FastDelete
							
