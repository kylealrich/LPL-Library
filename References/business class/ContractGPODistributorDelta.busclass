ContractGPODistributorDelta is a BusinessClass  
	owned by po
	
	prefix is CGXN
	
	Ontology
		symbolic key is ContractGPODistributorDelta
	
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated	
	
	Persistent Fields
		HealthTrustGPOID                    is like ContractImportGPOMembership	
		HealthTrustCOID                     is like ContractImportGPOMembership
		ContractNumber						is a GPOContractNumber   			
		DistributorNumber                   is a GPOSupplierID       			
		DistributorName                     is Alpha size 32
		DistributorLegalName                is Alpha size 80
		DistributorTaxIDNumber              is Alpha size 9
		MarkupPercent                       is Decimal size 7.4  		
		PrimaryDistributorIndicator         is Boolean              			
		SecondaryDistributorIndicator       is Boolean                         
		RecordModifiedTimestamp         	is Alpha size 20
		OperationFlag                   	is Alpha size 10		

	Local Fields 
		LocalGPOContractNumber	is a GPOContractNumber
		LocalContractGroup      is a ContractGroup

	Conditions
		
	Relations
	
		ContractGPODistributorRel
			one-to-one relation to ContractGPODistributor
			Field Mapping uses ByGPOContractMemberSupplierNumber
				related.GPOContractNumber			= ContractNumber 
				related.ContractImportGPOMembership	= HealthTrustGPOID
				related.ItemSupplier                = DistributorNumber
		
		ContractImportRel
			one-to-one relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber 	= LocalGPOContractNumber
				related.ContractGroup       = LocalContractGroup	

	Sets

		ByContractMemberSupplierNumber
			Sort Order
				ContractNumber
				HealthTrustGPOID
				DistributorNumber

	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassDistributorDelete is a Set Action
			disable checkpoint
			
			Action Rules
				
				Instance Rules
					invoke Purge
				
		Purge is a Purge Action
			restricted	

		LoadDistributorRecords is a Set Action 
			restricted 
			Parameters 
				ParmContractGroup		is a ContractGroup
			
			Sort Order
				ContractNumber
				ContractGPODistributorDelta
			
			Action Rules 

				Instance Rules 

					LocalContractGroup              = ParmContractGroup
					LocalGPOContractNumber 			= ContractNumber

					if (OperationFlag = "Add")
						if (ContractGPODistributorRel !exists)
							invoke Create ContractGPODistributor 
								invoked.GPOContractNumber 				= ContractNumber
								invoked.ContractImportGPOMembership     = HealthTrustGPOID
								invoked.HealthTrustCOID                 = HealthTrustCOID
								invoked.ItemSupplier                    = DistributorNumber
								invoked.MarkupPercent                   = MarkupPercent      		
								invoked.PreferredVendor                 = PrimaryDistributorIndicator                       			
								invoked.SecondaryDistributorIndicator   = SecondaryDistributorIndicator                             
								invoked.RecordModifiedTimestamp         = RecordModifiedTimestamp	
							
							if (ContractImportRel exists)
								invoke FastUpdate ContractImportRel 
									invoked.HeaderUpdated = true  


					else 
					if (OperationFlag = "Update")
						if (ContractGPODistributorRel exists)
							invoke Update ContractGPODistributorRel 
								invoked.HealthTrustCOID                 = HealthTrustCOID
								invoked.MarkupPercent                   = MarkupPercent      		
								invoked.PreferredVendor                 = PrimaryDistributorIndicator                       			
								invoked.SecondaryDistributorIndicator   = SecondaryDistributorIndicator                             
								invoked.RecordModifiedTimestamp         = RecordModifiedTimestamp	
							
							if (ContractImportRel exists)
								invoke FastUpdate ContractImportRel 
									invoked.HeaderUpdated = true 

					else 
					if (OperationFlag = "DEL")
						if (ContractGPODistributorRel exists)
							invoke Delete ContractGPODistributorRel 			
			

	
