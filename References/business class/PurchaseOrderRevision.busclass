PurchaseOrderRevision is a BusinessClass
    owned by po
    prefix is PVS
    classic name is POREVISION

    Ontology
        symbolic key is PurchaseOrderRevision
            classic set name is PVSSET1
            classic name is PO-REVISION

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields
		Buyer                        is a snapshot of PurchaseOrder.Buyer
        IssueMethod                  is a PurchaseOrderIssueMethod
            classic name is ISSUE-METH
        IssueDate                    is TimeStamp
        TransmissionStatus           is Numeric size 1
            classic name is XMIT-STATUS
            States
                Unknown    value is 0
                	default label is "_"
                Successful value is 1
                Failed     value is 2
        TransmissionDate             is TimeStamp
            classic name is XMIT-DATE
        NumberOfTransmissionAttempts is Numeric size 3
            classic name is XMIT-TRIES
        AcknowledgementResult        is like PurchaseOrderAcknowledgementResult
        AcknowledgementType          is AlphaUpper size 2
            classic name is ACK-TYPE
            States
                AckWDetailChange         value is "AC"
                    default label is "Ack - With Detail & Change"
                AckWDetailNoChange       value is "AD"
                    default label is "Ack - With Detail, No Change"
                AckWExcDetailOnly        value is "AE"
                    default label is "Ack - With Exception Detail Only"
                AckHoldStatus            value is "AH"           
                    default label is "Ack - Hold Status"
                AckNoDetailOrChange      value is "AK"
                    default label is "Ack - No Detail or Change"
                AckProductReplenishment  value is "AP"
                    default label is "Ack - Product Replenishment"
                Accepted                 value is "AT"
                ManualAcknowledgement	 value is "MA"
                	default label is "Manual"
                NoAcknowledgmentNeeded   value is "NA"
                RejectWithDetail         value is "RD"
                    default label is "Reject with Detail"
                RejectWithExcDetailOnly  value is "RF"
                    default label is "Reject with Exc Detail Only"
                RejectedNoDetail         value is "RJ"
                    default label is "Rejected - No Detail"
                RejectedNotAsAgreed      value is "RN"
                    default label is "Rejected - Not as agreed"
                RejectedWithCounterOffer value is "RO"
                VRv                      value is "RV"
                    default label is "Rejected - Violates Industry Practices"
                MutuallyDefined          value is "ZZ"
                SupplierPortal			 value is "SP"
        AcknowledgementDate          is Date
            classic name is ACK-DATE
		TransmissionMessage			is Alpha size 100
		TransmissionMessageDetail	is Alpha size 200
		TransmissionPayloadID		is Alpha size 100
            
    Local Fields
    	LocalBuyer  is like Buyer
    
    Derived Fields
    
    	AcknowledgementLinkback is a MessageField
			restricted
			"<linkback(webapp is Buyer navigation is AcknowledgementNav text is \"here\")>"
    	
    	DerivedTransmissionMessageLabel is a DerivedField
    		type is MessageField
    		restricted
    		if (PurchaseOrder.IssueMethod.EDI)
    			return "Transmission Message"
    		else
    			return "Acknowledgement Message"
    
    Conditions
    
    	AcknowledgementMessagesExist
    		restricted
    		when (PurchaseOrderAcknowledgementMessageRel exists)
    
   		OtherRevisions
   			when (OtherRevisionsRel exists)
   			
   		AcknowledgementInProgress
   			restricted
   			when (PurchaseOrder.PurchaseOrderEDIManualAcknowledgementRel exists
   			and   not IssueMethod.EDI
   			and   AcknowledgementType not entered
   			and   TransmissionMessage entered)
   		
   		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
   	
   	Relations
   	
   		OtherRevisionsRel
   			one-to-many relation to PurchaseOrderRevision
   			Field Mapping uses symbolic key
   				related.Company    		= Company
   				related.PurchaseOrder 	= PurchaseOrder
   			Instance Selection
   				where (related.UniqueID !=UniqueID)
    
		PurchaseOrderAcknowledgementMessageRel 
			one-to-many relation to PurchaseOrderAcknowledgementMessage
			Field Mapping uses ByRevision
				related.Company  				= Company
				related.PurchaseOrder 			= PurchaseOrder
				related.PurchaseOrderRevision 	= PurchaseOrderRevision
					
		RevisionsForABuyerRel
			one-to-many relation to PurchaseOrderRevision
			Field Mapping uses ByBuyer
				related.Buyer                   = LocalBuyer
				related.Company                 = Company
			    related.PurchaseOrder           = PurchaseOrder
			    related.PurchaseOrderRevision   = PurchaseOrderRevision
		
		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company
			
    Sets
    	
    	ByAcknowledgementResult
    		Sort Order
    			AcknowledgementResult
    			Company
    			Buyer
    			PurchaseOrder.Vendor
    			PurchaseOrder.PurchaseFromLocation
    			PurchaseOrder
    			PurchaseOrderRevision
    			
    	ByDescending
    		Sort Order
    			Company
    			PurchaseOrder
    			PurchaseOrderRevision descending
    			
    	ByBuyer
    		Sort Order
    			Buyer
    			Company
    			PurchaseOrder descending
    			PurchaseOrderRevision
    			
    	ByStatusUpdate
    		Sort Order
    			TransmissionPayloadID
    			Buyer
    			Company
    			PurchaseOrder
    			PurchaseOrderRevision
    			IssueDate descending
    			TransmissionDate
    
    Field Rules
    
    	Buyer

    		force default to PurchaseOrder.Buyer
    
    Actions
        Create is a Create Action
        	restricted
        
        CreateFirst is a Create Action
        	restricted
        	bypass field rules
        
        	Action Rules
        	
        		Buyer = PurchaseOrder.Buyer
        
        Update is an Update Action
        	restricted
        	bypass field rules
        	
        Delete is a Delete Action
        	restricted
        	
 		Purge is a Purge Action
			restricted
			bypass relational integrity rules
				
		PopulateBuyer is a Set Action
			restricted
			Parameters
				ParmCompany is a PurchasingCompany
					default label is "Company"
					
			Parameter Rules
				ParmCompany
					required
					
			Instance Selection
				where (ParmCompany = Company
				and    Buyer !entered)
				
			Action Rules
			
				Instance Rules
				
					Buyer = PurchaseOrder.Buyer
				
