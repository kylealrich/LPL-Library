RecallTaskInstructionQuestion is a BusinessClass
    owned by recall
    prefix is RMTQ

    Ontology
    	symbolic key is RecallTaskInstructionQuestion
    	
    Persistent Fields
    	Description				is Text
    		default label is "Question"
   		ResponseType			is Numeric size 2
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
  				List 				value is 4
   				YesNo 				value is 5
   				YesNoText			value is 6 
   		Attachment 
		ResponseRules            is Numeric size 1
            States
                NotRequired                             value is 1
                ResponseRequired                        value is 2
                ResponseRequiredAndAttachmentRequired   value is 3
        YesNoResponseRules       is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                 value is 1
                ResponseRequired                            value is 2
                ResponseRequiredAndAttachmentRequired       value is 3
                ResponseRequiredAndAttachmentRequiredIfYes  value is 4
                ResponseRequiredAndAttachmentRequiredIfNo   value is 5
        YesNoTextResponseRules   is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                         value is 1
                ResponseRequiredAndTextResponseRequired             value is 2
                ResponseRequiredAndTextOrAttachmentRequired         value is 3
                ResponseRequiredAndTextOrAttachmentRequiredIfYes    value is 4
                ResponseRequiredAndTextOrAttachmentRequiredIfNo     value is 5
                ResponseRequiredAndTextAndAttachmentRequired        value is 6
                ResponseRequiredAndTextAndAttachmentRequiredIfYes   value is 7
                ResponseRequiredAndTextAndAttachmentRequiredIfNo    value is 8
   		AllowResponseAttachment	is Boolean
   			default label is "AllowAttachment"
   	
   	Derived Fields
   		DescriptionMessage is a MessageField
   			restricted
   			"Description"
   		
   		RequiredDescription is a ConditionalField
   			type is Text
   			restricted
   			if (ResponseRequired)
   				"*<DescriptionMessage>"
   			else
   				DescriptionMessage
   				
   	Field Rules
   		Description
			required
				"DescriptionIsRequired"
		
		ResponseType
			required
				"ResponseTypeIsRequired"
	
		ResponseRules
            if (NonYesNoResponseType)
                required
                    "MustSelectAResponseRule"
                YesNoResponseRules = 0
                YesNoTextResponseRules = 0
                    
        YesNoResponseRules
            if (ResponseType.YesNo)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoTextResponseRules= 0
        
        YesNoTextResponseRules
            if (ResponseType.YesNoText)
                required
                    "MustSelectAResponseRule"
                ResponseRules= 0
                YesNoResponseRules= 0
            
        AllowResponseAttachment
            if (MayRequireResponseAttachment)
                AllowResponseAttachment = true
			
	Conditions
		IsListResponseType
			restricted
			when (ResponseType.List)
		
		ListQuestionWithNoValues
			restricted
    		when (IsListResponseType
    		and	  !RecallTaskInstructionQuestionValue set exists)
    			
    	HasAttachment
    		restricted
    		when (Attachment entered)
    		
    	ResponseRequired
    		restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    ResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        MayRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)  
    	
    	NonYesNoResponseType
    		restricted
			when (ResponseType.Date
			or    ResponseType.List
			or    ResponseType.Text
			or    ResponseType.Number)
    		
	Actions
		Create is a Create Action
		
		Update is an Update Action
			Action Rules
				if (!ResponseType.List
				and RecallTaskInstructionQuestionValue set exists)
					invoke Delete RecallTaskInstructionQuestionValue set
					
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
