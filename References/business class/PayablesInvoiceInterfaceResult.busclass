PayablesInvoiceInterfaceResult is a BusinessClass
	owned by ap
	
	prefix is APPIR
	
	Ontology
		symbolic key is PayablesInvoiceInterfaceResult
		
	Patterns
        disable AuditIndex

       	disable EffectiveDated
	
	Persistent Fields
		RunGroup							is AlphaUpper 30
		RunTime								is TimeStamp
		RunType								is Numeric 1
			States
				PayablesInvoiceInterface		value is 1
				MatchInvoiceInterface			value is 2
				DocumentInvoiceInterface		value is 3
		Status								is Numeric 1
			disable Auditing 
			States
				InProcess      				 	value is 0
				Complete						value is 1
				Incomplete 						value is 2
		ErrorRunGroup						is AlphaUpper 30
		OriginalRunGroup					is AlphaUpper 30
		RecordsProcessed					is Numeric 10
			disable Auditing 



        InterfacedInvoicesAmountTotal		is an InternationalAmount
			disable Auditing 
		InterfacedInvoicesCount				is Numeric 10
			disable Auditing 
			default label is "InvoiceHeadersInterfaced"
		InterfaceInvoiceErrorCount			is Numeric 10
			disable Auditing 
			default label is "InvoiceHeaderErrors"

        InterfacedPaymentsAmountTotal		is an InternationalAmount
			disable Auditing 
		InterfacedPaymentsCount				is Numeric 10
			disable Auditing 
			default label is "PaymentRecordsInterfaced"
		InterfacePaymentErrorCount			is Numeric 10
			disable Auditing 

        InterfacedDistributionsAmountTotal	is an InternationalAmount
			disable Auditing 
		InterfacedDistributionsCount		is Numeric 10
			disable Auditing 
			default label is "DistributionRecordsInterfaced"
		InterfaceDistributionErrorCount		is Numeric 10
			disable Auditing 
			default label is "DistributionRecordsInError"

		InterfacedCommentsCount				is Numeric 10
			disable Auditing 
			default label is "CommentRecordsInterfaced"
		InterfaceCommentErrorCount			is Numeric 10
			disable Auditing 

        InterfacedDetailsAmountTotal		is an InternationalAmount
			disable Auditing 
		InterfacedDetailsCount				is Numeric 10
			disable Auditing 
		InterfaceDetailErrorCount			is Numeric 10
			disable Auditing 

        InterfacedAddOnCostsAmountTotal		is an InternationalAmount
			disable Auditing 
		InterfacedAddOnCostsCount			is Numeric 10
			disable Auditing 
		InterfaceAddOnCostsErrorCount		is Numeric 10
			disable Auditing 


		InterfacedPurchaseOrdersCount		is Numeric 10
			disable Auditing 
		InterfacePurchaseOrdersErrorCount	is Numeric 10
			disable Auditing 

		MatchOrReleaseError 				is Boolean
			disable Auditing 
		PerfectMatchCount					is Numeric 9
			disable Auditing 
		PerfectMatchAmount					is an InternationalAmount
			disable Auditing 
		MatchedInToleranceCount				is Numeric 9
			disable Auditing 
		MatchedInToleranceAmount			is an InternationalAmount
			disable Auditing 
		MatcedOutOfToleranceCount			is Numeric 9
			default label is "MatchedOutOfToleranceCount"
			disable Auditing 
		MatchedOutOfToleranceAmount			is an InternationalAmount
			disable Auditing 
		MatchedWithChargebackCount			is Numeric 9
			disable Auditing 
		MatchedWithChargebackAmount			is an InternationalAmount
			disable Auditing 
		TotalChargebackAmount				is an InternationalAmount
			disable Auditing 
		MatchedWithServiceContractCount		is Numeric 9
			disable Auditing 
		MatchedWithServiceContractAmount	is an InternationalAmount
			disable Auditing 
		UnmatchedCount						is Numeric 9
			disable Auditing 
		UnmatchedAmount						is an InternationalAmount
			disable Auditing 
		ReleasedExpenseCount				is Numeric 9
			disable Auditing 
		ReleasedExpenseAmount				is an InternationalAmount
			disable Auditing 
		ReleasedCreditCount					is Numeric 9
			disable Auditing 
		ReleasedCreditAmount				is an InternationalAmount
			disable Auditing 
		UnreleasedExpenseCount				is Numeric 9
			disable Auditing 
		UnreleasedExpenseAmount				is an InternationalAmount
			disable Auditing 
		UnreleasedCreditCount				is Numeric 9
			disable Auditing 
		UnreleasedCreditAmount				is an InternationalAmount
			disable Auditing 

		Company								is a PayablesCompany
		AuthorityCode						is a PayablesAuthorityCode
        PrmStatus                  			 is Alpha size 1
            States
                Unreleased value is 0
                Open       value is 1
                History    value is 9
		UpdateTaxSystem						is Boolean
		UpdateVendorBalance					is Boolean
		LastYearEndingDate					is Date
		IncludeInvoicesInError				is Boolean
		RunMessage							is Alpha size up to 150	
		MoveErrorsToNewRunGroup  			is Boolean
		ErrorRunGroupPrefix					is AlphaUpper 15
		PerformBalanceEdits					is Boolean 
		JournalizeDistributions				is Boolean		
			default label is "JournalizeReleasedDistributions"

		Vendor								is like Vendor
		ProcessLevel						is like PayablesProcessLevel
        InvoiceProcess          			is AlphaUpper size 1
            States
                Add         value is "A"
                AddAndMatch value is "M"
				MatchSubmitOrRelease value is "R"
		PrmProcessType				is AlphaUpper size 1
			default label is "ProcessType"
            States
                MatchInvoices    	value is "M"
                ExpenseInvoices		value is "E"
                Both				value is "B"
		PrmVendorClass						is like VendorClass
			default label is "VendorClass"
		PrmVendor							is like Vendor
			default label is "Vendor"
		MatchPoint							is Numeric 1
			States
				RuleGroupOne		value is 1
				RuleGroupTwo		value is 2
				RuleGroupThree		value is 3
		ValidateVendorItem					is Boolean
			default label is "AllowMatchOnEitherItemOrVendorItem"
		AutoCreateDetails					is Numeric 1
			States
				FromPurchaseOrder				value is 2
				FromReceipt						value is 3
				FromReceiptThenFromPO			value is 4
		DefaultProcessLevel					is like PayablesProcessLevel 
		DefaultAuthorityCode				is like PayablesAuthorityCode
		EnableGLNProcessing					is Boolean
		EnableGTINProcessing				is Boolean
		JournalizeGroup
		AsyncId								is a AsyncActionRequest
			delete ignored
        	disable Auditing
		VendorGroup									
		OriginalMTPIIResult					is a PayablesInvoiceInterfaceResult	
		KeepOriginalRunGroup				is Boolean							

	Transient Fields
		TransientRunGroup					is like RunGroup
			derive value from DerivedResubmitRunGroup
		BypassExistingJobEdit				is Boolean

	Local Fields
        Diff is Numeric size 16
				
	Derived Fields
	
		NoRecordsToProcessMsg is a MessageField
			restricted
			"NoRecordsToProcess"
			 
		DerivedResubmitRunGroup is a DerivedField
			type is like RunGroup
			if  (ErrorRunGroup entered)
				return ErrorRunGroup
			else
				return RunGroup

		DerivedResubmitIncludeInvoicesInError is a DerivedField
			type is Boolean
			if  (RunGroup contains "ERROR")
				return true
			else
				return IncludeInvoicesInError

		InProcessMessage is a MessageField
        	restricted
        	"InProcess"
        	
		CompleteMessage is a MessageField
        	restricted
        	"Complete"
        	
        IncompleteMessage is a MessageField
        	restricted
        	"Incomplete"
        	
        NoRecordsMessage is a MessageField
        	restricted
        	"AllErrorsProcessed"

		StillProcessingMsg is a MessageField
			restricted
			"***InterfaceInProgress.ClickRefreshToUpdate***"
        
        DerivedStatus is a DerivedField
        	type is Alpha 50
        	if (Status.InProcess)
        		return InProcessMessage
        	else
        	if (Status.Complete)
        		return CompleteMessage
        	else
        	if (Status.Incomplete
        	and RunType.MatchInvoiceInterface
        	and !HasPendingMatchInvoiceRecords)
        		return NoRecordsMessage
        	else
        	if (Status.Incomplete
        	and RunType.PayablesInvoiceInterface
        	and !HasPendingPayablesInvoiceRecords)
        		return NoRecordsMessage
        	else
        	if (Status.Incomplete
        	and RunType.DocumentInvoiceInterface
        	and !HasPendingDocumentInvoiceRecords)
        		return NoRecordsMessage
        	else
        		return IncompleteMessage 

		DerivedRunMessage is a DerivedField
			type is like Comment
			if (NoRecordsToProcess)
				return NoRecordsToProcessMsg
			else
				return RunMessage
			
		TotalCountMatchInvoices is a ComputeField
			type is Numeric 10
			(PerfectMatchCount + MatchedInToleranceCount + MatcedOutOfToleranceCount + MatchedWithChargebackCount + MatchedWithServiceContractCount)

		TotalCountExpenseInvoices is a ComputeField
			type is Numeric 10
			(ReleasedExpenseCount + ReleasedCreditCount + UnreleasedExpenseCount + UnreleasedCreditCount)

		TotalAmountMatchInvoices is a DerivedField
			type is like InternationalAmount
			return (PerfectMatchAmount + MatchedInToleranceAmount + MatchedOutOfToleranceAmount + MatchedWithChargebackAmount + MatchedWithServiceContractAmount)

		TotalAmountExpenseInvoices is a DerivedField
			type is like InternationalAmount
			return (ReleasedExpenseAmount + ReleasedCreditAmount + UnreleasedExpenseAmount + UnreleasedCreditAmount)

		InterfacedInvoicesTotalCount is a DerivedField
			type is Numeric 18
			default label is "InvoiceHeadersTotalCount"
			return (InterfacedInvoicesCount + InterfaceInvoiceErrorCount)

		OriginalMultiThreadRunGroup is a DerivedField	
			type is AlphaUpper 30
			return (PIIResultsMTSubGroupsRel.RunGroup)


	Field Rules
		OriginalRunGroup
			if (RunGroup like "*ERRORS_*")
				if (ErrorRunGroupRel exists)
					OriginalRunGroup = first ErrorRunGroupRel.OriginalRunGroup
				else
					OriginalRunGroup = RunGroup
			else
				OriginalRunGroup = RunGroup
	
	Conditions
		HasInvoicesCreated
			restricted
			when (PayablesInvoicesRel exists)

		HasUnreleasedInvoicesCreated			
			when (PayablesInvoicesUnreleasedRel exists)
		
		HasReleasedInvoicesCreated				
			when (PayablesInvoicesNotUnreleasedRel exists)

		HasMultiThreadSubGroupInvoices		
			when (PayablesInvoicesFromMTSubGroupRel exists)

		HasPendingPayablesInvoiceRecords
			restricted
			when (PayablesInvoiceImportRel exists)
			
		HasPendingMatchInvoiceRecords
			restricted
			when (MatchInvoiceImportRel exists)

		HasPendingDocumentInvoiceRecords
			restricted
			when (DocumentInterfaceInvoiceRel exists)
		
		HasNoCompanyInvoiceRecords		
			restricted
			when (PayablesInvoiceImportNoCompanyRel exists)
						
		IncompleteHasNoCompanyRecords		
			when  (Status.Incomplete
        	and    RunType.PayablesInvoiceInterface
        	and    HasNoCompanyInvoiceRecords)
		
		IncompleteHasPendingRecords
			when ((Status.Incomplete
        	and    RunType.MatchInvoiceInterface
        	and    HasPendingMatchInvoiceRecords)
        	or    (Status.Incomplete
        	and    RunType.PayablesInvoiceInterface
        	and    HasPendingPayablesInvoiceRecords)
        	or    (Status.Incomplete
        	and    RunType.DocumentInvoiceInterface
        	and    HasPendingDocumentInvoiceRecords))

		IncompleteAllRecordsProcessed
        	when (Status.Incomplete
        	and  ((RunType.MatchInvoiceInterface
        	and   !HasPendingMatchInvoiceRecords)
        	or    (RunType.PayablesInvoiceInterface
        	and   !HasPendingPayablesInvoiceRecords)
        	or    (RunType.DocumentInvoiceInterface
        	and   !HasPendingDocumentInvoiceRecords)))

		NoRecordsToProcess
			when (Status entered
			and   RecordsProcessed not entered)

		InvoicesStillProcessing
			when (Status.InProcess
			or    InvoicesStillProcessingRel exists)

		ShowMetrics
			restricted
			when (InterfacedInvoicesAmountTotal entered
			or    InterfacedInvoicesCount entered
			or    InterfaceInvoiceErrorCount entered
			or    InterfacedPaymentsAmountTotal entered
			or    InterfacedPaymentsCount entered
			or    InterfacePaymentErrorCount entered
			or    InterfacedDistributionsAmountTotal entered
			or    InterfacedDistributionsCount entered
			or    InterfaceDistributionErrorCount entered
			or    InterfacedCommentsCount entered
			or    InterfaceCommentErrorCount entered)

		ShowMetricsMatch
			restricted
			when (InterfacedInvoicesCount entered
			or    InterfaceInvoiceErrorCount entered
			or    InterfacedDetailsCount entered
			or    InterfacedAddOnCostsCount entered
			or    InterfacedDistributionsCount entered
			or    InterfacedCommentsCount entered)

		ShowAutoMatch
		    restricted
		    when ((InvoiceProcess.AddAndMatch
		    or     InvoiceProcess.MatchSubmitOrRelease)
		    and    AutoMatchResultsRel exists)
		    
		SecurityGroupAllowsAccess
			restricted
			when (Company !entered
			or   (Company entered
			and   Company.SecurityGroupAllowsAccess))
		
		IsValidForActorContext
			restricted
			when (FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
			    
	Relations
		PayablesInvoiceImportRel
			one-to-many relation to PayablesInvoiceImport
			Field Mapping uses ByInterfaceRun
				related.InterfaceRun 		= PayablesInvoiceInterfaceResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup	= FinanceEnterpriseGroup 
				and   (related.RunGroup											= RunGroup 
				or     related.RunGroup											= ErrorRunGroup))  
				
		PayablesInvoiceImportNoCompanyRel		
			one-to-many relation to PayablesInvoiceImport
			Field Mapping uses ByInterfaceRun
				related.InterfaceRun 		= PayablesInvoiceInterfaceResult
			Instance Selection
				where (related.Company		= blank
				and   (related.RunGroup		= RunGroup
				or     related.RunGroup		= ErrorRunGroup)) 
		
		MatchInvoiceImportRel
			one-to-many relation to MatchInvoiceImport
			Field Mapping uses ByInterfaceRun
				related.InterfaceRun 		= PayablesInvoiceInterfaceResult
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= FinanceEnterpriseGroup 
				and   (related.RunGroup											= RunGroup 
				or     related.RunGroup											= ErrorRunGroup))  
				







		PayablesInvoicesRel 
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByCurrencyCompanyVendor
				related.OriginatingInterfaceRun	= PayablesInvoiceInterfaceResult
			Instance Selection
				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				and    !related.InterfaceInProcess)

		AllPayablesInvoicesRel 
			one-to-many relation to PayablesInvoice
			Field Mapping uses symbolic key
			Instance Selection
				where (related.OriginatingInterfaceRun	= PayablesInvoiceInterfaceResult)

		PayablesInvoicesUnreleasedRel 
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByCurrencyCompanyVendor
				related.OriginatingInterfaceRun	= PayablesInvoiceInterfaceResult
			Instance Selection

				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				and    related.Status.Unreleased
				and    !related.InterfaceInProcess)
				
		PayablesInvoicesNotUnreleasedRel 
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByCurrencyCompanyVendor
				related.OriginatingInterfaceRun	= PayablesInvoiceInterfaceResult
			Instance Selection

				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				and    !related.Status.Unreleased
				and    !related.InterfaceInProcess)

		PIIResultsMTSubGroupsRel	
			one-to-many relation to PayablesInvoiceInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection	
				where (related.PayablesInvoiceInterfaceResult = OriginalMTPIIResult)

		PIISubGroupsFromMTOrigGroupRel	
			one-to-many relation to PayablesInvoiceInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection	
				where (related.OriginalMTPIIResult = PayablesInvoiceInterfaceResult)

		PayablesInvoicesFromMTSubGroupRel	
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByCurrencyCompanyVendor
				related.OriginatingInterfaceRun = PIISubGroupsFromMTOrigGroupRel.PayablesInvoiceInterfaceResult

		InvoicesStillProcessingRel 
			one-to-many relation to PayablesInvoice
			Field Mapping uses ByCurrencyCompanyVendor
				related.OriginatingInterfaceRun	= PayablesInvoiceInterfaceResult
			Instance Selection

				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				and    related.BackgroundProcessing entered
				and   !related.AsyncFailure)

		DocumentInterfaceInvoiceRel
			one-to-many relation to DocumentInterfaceInvoice
			Field Mapping uses ByInterfaceRun
				related.InterfaceRunID 			= PayablesInvoiceInterfaceResult
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup 
				
		ErrorRunGroupRel
			one-to-many relation to PayablesInvoiceInterfaceResult
			Field Mapping uses ByErrorRunGroup
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.ErrorRunGroup		   = RunGroup
		
		PayablesInvoiceImportErrorRel
			one-to-many relation to PayablesInvoiceImport
			Field Mapping uses ByInterfaceRun
				related.InterfaceRun 		= PayablesInvoiceInterfaceResult
			Instance Selection

				where (related.GeneralLedgerCompanyRel.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				and   related.RunGroup											= RunGroup 
				and   related.SenderOriginalBOD									= "CSV")  

		MatchInterfaceInProcessResultRel
			one-to-many relation to PayablesInvoiceInterfaceResult
			Field Mapping uses ByRunGroup
				related.RunGroup						= RunGroup
			Instance Selection
				where (related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				and    related.RunType					= RunType
				and    related.Status.InProcess)
				
		DocumentInterfaceInProcessResultRel
			one-to-many relation to PayablesInvoiceInterfaceResult
			Field Mapping uses ByRunGroup
				related.RunGroup						= RunGroup
			Instance Selection
				where (related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				and    related.RunType					= RunType
				and    related.Company					= Company
				and    related.Status.InProcess)
				
		AutoMatchResultsRel
			one-to-many relation to AutoMatchResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
			Instance Selection
				where (related.PrmCompany				= Company
				and    related.PrmInterfaceRun			= PayablesInvoiceInterfaceResult)
		
	Sets
		ByRunTime
			Sort Order
				RunTime descending
				PayablesInvoiceInterfaceResult descending
				FinanceEnterpriseGroup
				UniqueID
		ByRunGroup
			Sort Order
				RunGroup
				PayablesInvoiceInterfaceResult
				FinanceEnterpriseGroup
				UniqueID




		ByOriginalRunGroup
			Sort Order
				FinanceEnterpriseGroup
				OriginalRunGroup
				RunTime descending
				PayablesInvoiceInterfaceResult
		ByErrorRunGroup
			Sort Order
				FinanceEnterpriseGroup
				ErrorRunGroup
				PayablesInvoiceInterfaceResult

	Actions
		ResetAllErrors is an Instance Action
			valid when (IncompleteHasPendingRecords)
			Action Rules
				if  (RunType.PayablesInvoiceInterface)
					invoke ResetErrorMessage PayablesInvoiceImportRel
					invoke ResetErrorMessage PayablesInvoiceImportNoCompanyRel	
				else
				if  (RunType.MatchInvoiceInterface)
					invoke ResetErrorMessage MatchInvoiceImportRel
				else
				if  (RunType.DocumentInvoiceInterface)
					invoke ResetErrorMessage MatchInvoiceImportRel
			
		Create is an Action
			restricted
			Action Rules

				if (RunType.MatchInvoiceInterface)

					for each MatchInterfaceInProcessResultRel					
        				Diff = (RunTime - each.RunTime)

						constraint (Diff > 86400)
							"ExistingJobForThisRunGroupIsAlreadyInProcess"
				else
				if (RunType.DocumentInvoiceInterface
				and !BypassExistingJobEdit)

					for each DocumentInterfaceInProcessResultRel					
        				Diff = (RunTime - each.RunTime)

						constraint (Diff > 86400)
							"ExistingJobForThisCompanyIsAlreadyInProcess"
			
		Update is an Action
			restricted
			refresh and lock this instance
		Delete is an Action
			valid when (!Status.InProcess)
			Entrance Rules 
				if (RunType.PayablesInvoiceInterface) 
					for each PayablesInvoiceImportRel 
						invoke FastUpdate each 
							initialize invoked.InterfaceRun 
				else 
				if (RunType.MatchInvoiceInterface) 
					for each MatchInvoiceImportRel  
						invoke FastUpdate each 
							initialize invoked.InterfaceRun 
				else 
				if (RunType.DocumentInvoiceInterface) 
					for each DocumentInterfaceInvoiceRel  
						invoke Update each 
							initialize invoked.InterfaceRunID 
			
		Purge is a Purge Action											
			valid when (!Status.InProcess)
			Entrance Rules 
				if (RunType.PayablesInvoiceInterface) 
					for each PayablesInvoiceImportRel 
						invoke FastUpdate each 
							initialize invoked.InterfaceRun 
				else 
				if (RunType.MatchInvoiceInterface) 
					for each MatchInvoiceImportRel  
						invoke FastUpdate each 
							initialize invoked.InterfaceRun 
				else 
				if (RunType.DocumentInvoiceInterface) 
					for each DocumentInterfaceInvoiceRel  
						invoke Update each 
							initialize invoked.InterfaceRunID 
			
		ClearInProcessMessage is an Instance Action
		    valid when (Status.InProcess)
		    refresh and lock this instance
		    Action Rules
		        if (InvoicesStillProcessingRel exists)
		            confirmation required
		                "InvoicesAreStillProcessingInAutoMatch,Continue?"
		
		        if (HasPendingPayablesInvoiceRecords
		        or  HasPendingMatchInvoiceRecords
		        or  HasPendingDocumentInvoiceRecords)
		            Status = 2
		        else 
		            Status = 1
		
		SetInProcessToComplete is an Instance Action
		    valid when (Status.InProcess)
		    refresh and lock this instance
		    Entrance Rules
		    	confirmation required
		    		"ThisActionShouldNotBeTakenIfThereAreStillJobsRunningInAsync.Continue?"
		    Action Rules
		    	Status = 2

		SetInvoiceErrorFromDistribution is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance
			Action Rules
				InterfacedInvoicesCount		-= 1
				InterfaceInvoiceErrorCount	+= 1

		ResubmitInvoiceInterface is an Instance Action
			valid when (HasPendingPayablesInvoiceRecords)
			Parameters
				ResubmitRunGroup					is a RunGroup
					default label is "RunGroup"
				ResubmitCompany						is a PayablesCompany
				ResubmitAuthorityCode				is a PayablesAuthorityCode
		        ResubmitStatus                   	is Alpha size 1
		            States
		                Unreleased value is 0
		                Open       value is 1
		                History    value is 9
				ResubmitUpdateTaxSystem				is Boolean
				ResubmitUpdateVendorBalance			is Boolean
				ResubmitLastYearEndingDate			is Date
				ResubmitIncludeInvoicesInError		is Boolean
				ResubmitMoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				ResubmitErrorRunGroupPrefix			is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				ResubmitPerformBalanceEdits			is Boolean
				ResubmitJournalizeDistributions		is Boolean	
				
			Parameter Rules
				ResubmitRunGroup					
					initial value is TransientRunGroup
				ResubmitCompany					
					initial value is Company
				ResubmitAuthorityCode			
					initial value is AuthorityCode
		        ResubmitStatus                   
					initial value is PrmStatus
				ResubmitUpdateTaxSystem				
					initial value is UpdateTaxSystem
				ResubmitUpdateVendorBalance			
					initial value is UpdateVendorBalance
				ResubmitLastYearEndingDate			
					initial value is LastYearEndingDate
				ResubmitIncludeInvoicesInError		
					initial value is true
				ResubmitMoveErrorsToNewRunGroup  
					initial value is MoveErrorsToNewRunGroup
				ResubmitErrorRunGroupPrefix		
					initial value is ErrorRunGroupPrefix
				ResubmitPerformBalanceEdits			
					initial value is PerformBalanceEdits
				ResubmitJournalizeDistributions					
					initial value is JournalizeDistributions	
					
			Action Rules
				invoke InterfaceInvoices PayablesInvoiceImport
					invoked.PrmRunGroup					= ResubmitRunGroup
					invoked.PrmCompany					= ResubmitCompany
					invoked.PrmAuthorityCode			= ResubmitAuthorityCode
			        invoked.PrmStatus					= ResubmitStatus
					invoked.UpdateTaxSystem				= ResubmitUpdateTaxSystem
					invoked.UpdateVendorBalance			= ResubmitUpdateVendorBalance	
					invoked.LastYearEndingDate			= ResubmitLastYearEndingDate
					invoked.IncludeInvoicesInError		= ResubmitIncludeInvoicesInError
					invoked.PrmMoveErrorsToNewRunGroup	= ResubmitMoveErrorsToNewRunGroup
					invoked.PrmErrorRunGroupPrefix		= ResubmitErrorRunGroupPrefix
					invoked.PerformBalanceEdits			= ResubmitPerformBalanceEdits
					invoked.PrmJournalizeDistributions 	= ResubmitJournalizeDistributions	
			
		ResubmitMatchInvoiceInterface is an Instance Action
			valid when (HasPendingMatchInvoiceRecords)
			Parameters
				ResubmitRunGroup					is a RunGroup
					default label is "RunGroup"
				ResubmitCompany						is a PayablesCompany
				ResubmitVendorGroup					is a VendorGroup	
				ResubmitVendor						is a Vendor
					context of ResubmitVendorGroup		
				ResubmitProcessLevel				is a PayablesProcessLevel
				ResubmitAuthorityCode				is a PayablesAuthorityCode
					context of ResubmitVendorGroup		
		        ResubmitInvoiceProcess          	is AlphaUpper size 1
		            States
		                Add         value is "A"
		                AddAndMatch value is "M"
				ResubmitMatchPoint					is Numeric 1
					States
						RuleGroupOne		value is 1
						RuleGroupTwo		value is 2
						RuleGroupThree		value is 3
				ResubmitDefaultProcessLevel			is a PayablesProcessLevel
				ResubmitDefaultAuthorityCode		is a PayablesAuthorityCode
					context of ResubmitVendorGroup		
				ResubmitAllowMatchItemOrVendorItem			is Boolean
				ResubmitAutoCreateDetails 					is Numeric 1
		 			States
		 				FromPurchaseOrder				value is 2
		 				FromReceipt						value is 3
						FromReceiptThenFromPO			value is 4
				ResubmitIncludeInvoicesInError		is Boolean
				ResubmitMoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				ResubmitErrorRunGroupPrefix			is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				ResubmitRunGroup
					initial value is TransientRunGroup
				ResubmitCompany
					initial value is Company
					if (!ResubmitVendorGroup entered)		
						ResubmitVendorGroup = ResubmitCompany.PayablesCompany.VendorGroup
				ResubmitVendor
					initial value is Vendor
				ResubmitProcessLevel
					initial value is ProcessLevel
				ResubmitAuthorityCode
					initial value is AuthorityCode
		        ResubmitInvoiceProcess
		            initial value is InvoiceProcess
				ResubmitMatchPoint
					initial value is MatchPoint
				ResubmitDefaultProcessLevel
					initial value is DefaultProcessLevel
				ResubmitDefaultAuthorityCode
					initial value is DefaultAuthorityCode
				ResubmitAllowMatchItemOrVendorItem
					initial value is ValidateVendorItem
				ResubmitAutoCreateDetails 
					initial value is AutoCreateDetails 
				ResubmitIncludeInvoicesInError
					initial value is true
				ResubmitMoveErrorsToNewRunGroup
				  	initial value is MoveErrorsToNewRunGroup
				ResubmitErrorRunGroupPrefix
					initial value is ErrorRunGroupPrefix
				ResubmitVendorGroup			
					initial value is VendorGroup
					if (ResubmitVendor entered)
						required
							"VendorGroupRequiredWhenVendorIsEnteredWithoutCompany"
					if (ResubmitAuthorityCode entered)
						required
							"VendorGroupRequiredWhenAuthorityCodeIsEnteredWithoutCompany"
					constraint (!ResubmitCompany entered or ResubmitCompany.PayablesCompany.VendorGroup = ResubmitVendorGroup)
						"VendorGroupDifferentFromEnteredCompanyVendorGroup<ResubmitCompany.PayablesCompany.VendorGroup>"
				
			Action Rules
				invoke InterfaceInvoices MatchInvoiceImport
					invoked.PrmRunGroup					= ResubmitRunGroup
					invoked.PrmCompany					= ResubmitCompany
					invoked.PrmVendor					= ResubmitVendor
					invoked.PrmProcessLevel				= ResubmitProcessLevel
					invoked.PrmAuthorityCode			= ResubmitAuthorityCode
			        invoked.InvoiceProcess          	= ResubmitInvoiceProcess
					invoked.MatchPoint					= ResubmitMatchPoint
					invoked.DefaultProcessLevel			= ResubmitDefaultProcessLevel
					invoked.DefaultAuthorityCode		= ResubmitDefaultAuthorityCode
					invoked.AllowMatchItemOrVendorItem	= ResubmitAllowMatchItemOrVendorItem
					invoked.AutoCreateDetails 			= ResubmitAutoCreateDetails
					invoked.IncludeInvoicesInError		= ResubmitIncludeInvoicesInError
					invoked.MoveErrorsToNewRunGroup  	= ResubmitMoveErrorsToNewRunGroup
					invoked.PrmErrorRunGroupPrefix		= ResubmitErrorRunGroupPrefix
					invoked.PrmVendorGroup				= ResubmitVendorGroup	

		ResubmitDocumentInvoiceInterface is an Instance Action
			valid when (HasPendingDocumentInvoiceRecords)
			Parameters
				ResubmitCompany						is a PayablesCompany
				ResubmitVendorGroup					is a VendorGroup	
		        ResubmitInvoiceProcess          	is AlphaUpper size 1
		            States
		                Add            value is "A"
		                MatchSubmitOrRelease value is "R"
				ResubmitProcessType				is AlphaUpper size 1
		            States
		                Expense            	value is "E"
		                Match 				value is "M"
		                Both				value is "B"
				ResubmitAllowMatchItemOrVendorItem is Boolean
				ResubmitAutoCreateDetails 				is Numeric 1
		 			States
		 				FromPurchaseOrder				value is 2
		 				FromReceipt						value is 3
						FromReceiptThenFromPO			value is 4


			Parameter Rules
				ResubmitCompany
					initial value is Company
		        ResubmitInvoiceProcess
		            initial value is InvoiceProcess
		        ResubmitProcessType
		            initial value is PrmProcessType
				ResubmitAllowMatchItemOrVendorItem
					initial value is ValidateVendorItem
				ResubmitVendorGroup			
					initial value is VendorGroup
				ResubmitAutoCreateDetails 
					initial value is AutoCreateDetails 
				
			Action Rules
				invoke InterfaceInvoices DocumentInterfaceInvoice
					invoked.PrmCompany					= ResubmitCompany
			        invoked.InvoiceProcess          	= ResubmitInvoiceProcess
			        invoked.PrmProcessType          	= ResubmitProcessType
					invoked.AllowMatchItemOrVendorItem	= ResubmitAllowMatchItemOrVendorItem
					invoked.AutoCreateDetails 			= ResubmitAutoCreateDetails
					invoked.PrmVendorGroup				= ResubmitVendorGroup	

		UpdateStatus is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance
			Action Rules
				if  (!PayablesInvoiceImportRel exists
				and  Status.InProcess)  
					Status        			= 1
				











		UpdateDistribCount is an Instance Action
			default label is untranslatable
		    restricted
		    refresh and lock this instance
		    synchronized on "PayablesInvoiceInterfaceResult:<PayablesInvoiceInterfaceResult>"
		    Parameters
		        PrmInterfacedDistributionsAmountTotal	is an InternationalAmount
				PrmInterfacedDistributionsCount			is Numeric 10
				PrmInterfaceDistributionErrorCount		is Numeric 10
		    Action Rules
				InterfacedDistributionsAmountTotal	+= PrmInterfacedDistributionsAmountTotal
				InterfacedDistributionsCount		+= PrmInterfacedDistributionsCount
				InterfaceDistributionErrorCount		+= PrmInterfaceDistributionErrorCount

		AddToResults is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance
			Parameters
				Company			is a PayablesCompany
				PayablesInvoice
				ReduceUnmatched is Boolean
			Action Rules
				if (ReduceUnmatched)
					UnmatchedCount							-= 1
					UnmatchedAmount							-= PayablesInvoice.InvoiceAmount.CurrencyAmount

				if  (PayablesInvoice.IsNotMatched)
					UnmatchedCount							+= 1
					UnmatchedAmount							+= PayablesInvoice.InvoiceAmount.CurrencyAmount
				else
				if  (PayablesInvoice.MatchStatus.Matched)
					if  (PayablesInvoice.MatchProcessType.ServiceContract)
						MatchedWithServiceContractCount		+= 1
						MatchedWithServiceContractAmount	+= PayablesInvoice.InvoiceAmount.CurrencyAmount
					else
						PerfectMatchCount					+= 1
						PerfectMatchAmount					+= PayablesInvoice.InvoiceAmount.CurrencyAmount
				else
				if  (PayablesInvoice.MatchStatus.MatchedOutOfTolerance)
					MatcedOutOfToleranceCount				+= 1
					MatchedOutOfToleranceAmount				+= PayablesInvoice.InvoiceAmount.CurrencyAmount
				else
				if  (PayablesInvoice.MatchStatus.MatchedInTolerance)
					MatchedInToleranceCount					+= 1
					MatchedInToleranceAmount				+= PayablesInvoice.InvoiceAmount.CurrencyAmount
				else
				if  (PayablesInvoice.MatchStatus.MatchedWithAutoChargeback)
					MatchedWithChargebackCount				+= 1
					MatchedWithChargebackAmount				+= PayablesInvoice.InvoiceAmount.CurrencyAmount
					TotalChargebackAmount 					+= PayablesInvoice.NonCancelledChargebackRel.InvoiceAmount.CurrencyAmount

		TriggerBudgetEdit is an Instance Action		
			restricted
			default label is untranslatable
			run in background
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmBusinessClassName			is Alpha size 100
					default label is "BusinessClass"
			Action Rules
				invoke ProcessInterfaceBatchEdits BudgetEditBatch
					invoked.PrmFinanceEnterpriseGroup 	= PrmFinanceEnterpriseGroup 
					invoked.PrmBusinessClassName 		= PrmBusinessClassName


		ScheduleAutoMatch is an Instance Action
		    run in background
			default label is untranslatable
			refresh and lock this instance
			restricted
			Action Rules

				invoke AutoMatch PayablesInvoice
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
			        invoked.PrmCompany						= Company
			        invoked.PrmMatchPoint					= "1"
			        invoked.PrmInterfaceRun					= PayablesInvoiceInterfaceResult















