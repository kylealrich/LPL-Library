CashManagementGroup is a BusinessClass
	owned by cashmgmt
	prefix is CTMGP

	Ontology
		symbolic key is CashManagementGroup

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		Description											is Alpha 100
		CorporateCalendar									is a SystemCalendar
		Currency
		CashForecastApprovalRequired						is Boolean
		DebtInstrumentApprovalRequired						is Boolean
		BankInterfaceAdministrator							is a FinanceResource
		AutomaticBankStatementImport 						is Boolean
		IntradayStatementProcessing							is Numeric 2
			States
				KeepAllStatements 							value is 1
				KeepLatestStatementAndAnyWithCashPosition	value is 2
				KeepOnlyLatestStatement						value is 3
		NumberOfCategorizationGroups						is a CategorizationGroup
		StatementCurrencyGainLossCategory					is a CashTransactionCategory
		DisplayStatementCurrencyGainLossLine				is Boolean
		LastDirectForecastLoad								is TimeStamp
		ApprovalCodesNotUsedForCashForecasts				is Boolean
		RejectCashForecastReasonCodeRequired				is Boolean
		BankStatementDistribApprovalRequired				is Boolean
		ApprovalCodesNotUsedForBankStatementDist			is Boolean
		RejectStatementDistribReasonCodeRequired			is Boolean
		CashLedgerTransactionApprovalRequired				is Boolean
		ApprovalCodesNotUsedForCashTransactions				is Boolean
		RejectLedgerTransactionReasonCodeRequired			is Boolean
		VoidStopPaymentApprovalRequired						is Boolean
		ApprovalCodesNotUsedForVoidStopPayments				is Boolean
		RejectVoidStopPaymentReasonCodeRequired				is Boolean
		CashLedgerPaymentApprovalRequired					is Boolean
		ApprovalCodesNotUsedForPayments						is Boolean
		BankStatementReconciliationApprovalRequired			is Boolean
		ApprovalCodesNotUsedForBankStatementReconciliation	is Boolean
		RejectBankStatementReconReasonCodeRequired			is Boolean
		RejectCashLedgerPaymentReasonCodeRequired			is Boolean
		CashLedgerReceiptApprovalRequired					is Boolean
		ApprovalCodesNotUsedForReceipts						is Boolean
		RejectCashLedgerReceiptReasonCodeRequired			is Boolean
		CashLedgerEFTApprovalRequired						is Boolean
		ApprovalCodesNotUsedForEFTTransactions				is Boolean
		RejectCashLedgerEFTReasonCodeRequired				is Boolean
		WireTransferApprovalRequired						is Boolean
		ApprovalCodesNotUsedForWireTransfers				is Boolean
		RejectWireTransferReasonCodeRequired				is Boolean
		EnforceProcessorCannotBeApprover					is Boolean
		CashLedgerReturnedPaymentApprovalRequired			is Boolean
		ApprovalCodesNotUsedForReturnedPayments				is Boolean
		RejectCashLedgerReturnedPaymentReasonCodeRequired	is Boolean
		LastCashLedgerTransactionIdentifier					is Numeric 10
			disable Auditing
		DirectForecastProcesses								is Numeric 3
			disable Auditing
		DirectForecastProcessesCompleted					is Numeric 3
			disable Auditing
		ShowDiscountedAmount								is Boolean
		LogoImageMaximumSize								is Numeric 8
		LogoImageUOM										is Numeric 1
			States
				Bytes			value is 0
				Kilobytes		value is 1
				Megabytes		value is 2
		LastErrorRunGroupNumber						is Numeric 8
			disable Auditing
		ReportingBasis
		AutoTranNumberByCompany						is Boolean
		AutoTranNumberByCashCode					is Boolean
		AutoTranNumberByBankTransactionCode			is Boolean
		AutoTranNumberByCashCodeBankTranCode		is Boolean
		AutoTranNumberForTransactionForm			is Boolean
		AutoTranNumberForReceiptForm				is Boolean
		AutoTranNumberForReturnedPaymentForm		is Boolean
		AutoReferenceNumberByCompany				is Boolean
		AutoReferenceNumberByCashCode				is Boolean
		AutoReferenceNumberByBankTransactionCode	is Boolean
		AutoReferenceNumberByCashCodeBankTranCode	is Boolean
		AutoReferenceNumberForPaymentForm			is Boolean
		AutoReferenceNumberForEFTForm				is Boolean
		AutoTranNumberForTransferForm				is Boolean
		FTPConfiguration
		InforTreasuryIntegrationType				is Numeric 2	
			default label is "IntegrationType"
			States
				Kyriba 			value is 1
				GTreasury		value is 2
		InforTreasurySubscriptionKey				is Alpha size 250
			default label is "Client/SubscriptionKey"
		ValidateImportedBankStatementBalance		is Boolean
		CurrencyDifferenceGainAccount 				is a GeneralLedgerChartAccount	
		CurrencyDifferenceLossAccount 				is a GeneralLedgerChartAccount	

	Derived Fields
		DerivedEmptySet is a DerivedField
			type is Boolean
			restricted
			return false

		LoadDirectForecastMessage is a MessageField
			"Note:_\Direct_\ForecastDataWillBeLoadedForAll_\CompaniesAssociatedWithThe_\Cash_\Management_\GroupWhenNotEntered"

		CurrentDate is a DerivedField
			type is Date
			restricted
			return current corporate date

		BlankField is a DerivedField
			type is Alpha 1
			return blank

		NumberOfAPCompanies is a ComputeField
			type is Numeric 3
			restricted
			(instance count of PayablesCompanyRel)

		NumberOfARCompanies is a ComputeField
			type is Numeric 3
			restricted
			(instance count of ReceivableCompanyRel)

		NumberOfPOCompanies is a ComputeField
			type is Numeric 3
			restricted
			(instance count of PurchasingCompanyRel)

		NumberOfImportCompanies is a ComputeField		
			type is Numeric 3
			restricted
			(instance count of ImportCompanyRel)

		NumberOfForecastProcessesLeft is a ComputeField
			type is Numeric 3
			restricted
			(DirectForecastProcesses - DirectForecastProcessesCompleted)

		LogoImageMaximumSizeBytes is a ConditionalField
			type is Numeric 16
			if (LogoImageUOM.Kilobytes)
				(LogoImageMaximumSize * 1024)
			else
			if (LogoImageUOM.Megabytes)
				(LogoImageMaximumSize * 1024 * 1024)
			else
				LogoImageMaximumSize

		FTPConfigurationName is a DerivedField
			type is Alpha 100
			if (FTPConfiguration.System)
				return "system"
			else
			if (FTPConfiguration.System1)
				return "system1"
			else
			if (FTPConfiguration.System2)
				return "system2"
			else
			if (FTPConfiguration.System3)
				return "system3"
			else
			if (FTPConfiguration.System4)
				return "system4"
				
   	Field Rules
   		CashManagementGroup
   			initial value is actor.context.FinanceEnterpriseGroup
   			
   		Description
   			required
			default to CashManagementGroup.FinanceEnterpriseGroup.Description			

		Currency
			required

		BankInterfaceAdministrator
			if (AutomaticBankStatementImport)
				required
					"BankInterfaceAdministratorIsRequiredWhenAutomaticBankStatementImportIsChecked"
			constraint (BankInterfaceAdministrator.EmailAddress entered)
				"BankInterfaceAdministratorMustHaveAnEmailAddressDefined"

		IntradayStatementProcessing
			default to 2 

		NumberOfCategorizationGroups
			default to 10

		StatementCurrencyGainLossCategory
			constraint (StatementCurrencyGainLossCategory.StatementLinePostingOption.NonPosting)
				"StatementCurrencyGainLossCategoryMustBeNonPosting"
				
			constraint (!StatementCurrencyGainLossCategory.HasChild)
  				"StatementCurrencyGainLossCategoryCannotBeASummaryLevelCategory"
  					
		RejectCashForecastReasonCodeRequired
			constraint (CashForecastApprovalRequired)
				"ApprovalRequiredForCashForecastMustBeSelectedToRequireReasonCodeForRejectingCashForecast"
				
		RejectStatementDistribReasonCodeRequired
			constraint (BankStatementDistribApprovalRequired)
				"ApprovalRequiredForBankStatementDistributionsMustBeSelectedToRequireReasonCodeForRejectingBankStatementDistributions"
			
		RejectLedgerTransactionReasonCodeRequired
			constraint (RequiresApprovalsForCashLedgerTransactions)
				"ApprovalRequiredForCashLedgerTransactionsOrBankStatementDistributionsAndTransactionsMustBeSelectedToRequireReasonCodeForRejectingCashLedgerTransactions"
			
		RejectVoidStopPaymentReasonCodeRequired
			constraint (VoidStopPaymentApprovalRequired)
				"ApprovalRequiredForVoidOrStopPaymentsMustBeSelectedToRequireReasonCodeForRejectingVoidOrStopPayments"
			
		RejectCashLedgerPaymentReasonCodeRequired
			constraint (CashLedgerPaymentApprovalRequired)
				"ApprovalRequiredForCashLedgerPaymentsMustBeSelectedToRequireReasonCodeForRejectingCashLedgerPayments"
			
		RejectBankStatementReconReasonCodeRequired
			constraint (BankStatementReconciliationApprovalRequired)
				"ApprovalRequiredForBankStatementReconciliationMustBeSelectedToRequireReasonCodeForRejectingBankStatementReconciliation"

		RejectCashLedgerReceiptReasonCodeRequired
			constraint (CashLedgerReceiptApprovalRequired)
				"ApprovalRequiredForCashLedgerReceiptsMustBeSelectedToRequireReasonCodeForRejectingCashLedgerReceipts"

		RejectCashLedgerEFTReasonCodeRequired
			constraint (CashLedgerEFTApprovalRequired)
				"ApprovalRequiredForCashLedgerEFT/CCExtractsMustBeSelectedToRequireReasonCodeForRejectingCashLedgerEFT/CCExtracts"

		RejectWireTransferReasonCodeRequired
			constraint (WireTransferApprovalRequired)
				"ApprovalRequiredForWireTransfersMustBeSelectedToRequireReasonCodeForRejectingWireTransfers"
				
		ApprovalCodesNotUsedForCashForecasts
			constraint (CashForecastApprovalRequired)
				"ApprovalRequiredForCashForecastsMustBeSelectedToNotUseApprovalCodeForCashForecast"
		
		ApprovalCodesNotUsedForBankStatementDist
			constraint (BankStatementDistribApprovalRequired)
				"ApprovalRequiredForBankStatementDistributionsMustBeSelectedToNotUseApprovalCodeForBankStatementDistributions"

		ApprovalCodesNotUsedForCashTransactions
			constraint (RequiresApprovalsForCashLedgerTransactions)
				"ApprovalRequiredForCashLedgerTransactionsOrBankStatementDistributionsAndTransactionsMustBeSelectedToNotUseApprovalCodeForCashLedgerTransactions"

		ApprovalCodesNotUsedForVoidStopPayments
			constraint (VoidStopPaymentApprovalRequired)
				"ApprovalRequiredForVoidOrStopPaymentsMustBeSelectedToNotUseApprovalCodeForVoidOrStopPayments"

		ApprovalCodesNotUsedForPayments
			constraint (CashLedgerPaymentApprovalRequired)
				"ApprovalRequiredForCashLedgerPaymentsMustBeSelectedToNotRequireApprovalCodeForCashLedgerPayments"
		
		ApprovalCodesNotUsedForBankStatementReconciliation
			constraint (BankStatementReconciliationApprovalRequired)
				"ApprovalRequiredForBankStatementReconciliationMustBeSelectedToNotUseApprovalCodeForBankStatementReconciliation"

		ApprovalCodesNotUsedForReceipts
			constraint (CashLedgerReceiptApprovalRequired)
				"ApprovalRequiredForCashLedgerReceiptsMustBeSelectedToNotRequireApprovalCodeForCashLedgerReceipts"
		
		ApprovalCodesNotUsedForEFTTransactions
			constraint (CashLedgerEFTApprovalRequired)
				"ApprovalRequiredForCashLedgerEFT/CCExtractsMustBeSelectedToNotRequireApprovalCodeForCashLedgerEFT/CCExtracts"

		ApprovalCodesNotUsedForWireTransfers
			constraint (WireTransferApprovalRequired)
				"ApprovalRequiredForWireTransfersMustBeSelectedToNotRequireApprovalCodeForWireTransfers"
				
		LogoImageMaximumSize
			required
			initial value is 20
			if (LogoImageMaximumSize = blank)
				LogoImageMaximumSize = 20
				LogoImageUOM = LogoImageUOM.Kilobytes

		LogoImageUOM
			initial value is LogoImageUOM.Kilobytes

		FTPConfiguration
			initial value is 1 	
			default to 1		
	
		InforTreasuryIntegrationType	
			if (InforTreasurySubscriptionKey entered)
				required
					"IntegrationTypeIsRequired"

		InforTreasurySubscriptionKey	
			if (InforTreasuryIntegrationType entered)
				required
					"Client/SubscriptionKeyIsRequired"

		CurrencyDifferenceGainAccount	
			if (CurrencyDifferenceLossAccount entered)
				required
					"CurrencyDifferenceGainAccountIsRequired"

		CurrencyDifferenceLossAccount	
			if (CurrencyDifferenceGainAccount entered)
				required
					"CurrencyDifferenceLossAccountIsRequired"
					
	Context Fields
		EnterpriseGroup

	Conditions
 		CashManagementGroupExists
 			restricted
 			when (CashManagementGroup exists)
 			




		HasStatementLineDistributions
			restricted
			when (ReleasedStatementLineDistributionsRel exists)

		HasNonCashLedgerDistributions
			restricted
			when (ReleasedNonCashLedgerDistributionsRel exists)
			
		RequiresApprovalCodesForTransactions
			restricted
			when ((RequiresApprovalsForCashLedgerTransactions
			and	!ApprovalCodesNotUsedForCashTransactions)
			or	(VoidStopPaymentApprovalRequired
			and	!ApprovalCodesNotUsedForVoidStopPayments))

		RequiresApprovalCodesForPayments
			restricted
			when (CashLedgerPaymentApprovalRequired
			and   !ApprovalCodesNotUsedForPayments)

		RequiresApprovalCodesForReceipts
			restricted
			when (CashLedgerReceiptApprovalRequired
			and   !ApprovalCodesNotUsedForReceipts)

		RequiresApprovalCodesForEFTExtracts
			restricted
			when (CashLedgerEFTApprovalRequired
			and   !ApprovalCodesNotUsedForEFTTransactions)

		RequiresApprovalCodesForWireTransfers
			restricted
			when (WireTransferApprovalRequired
			and   !ApprovalCodesNotUsedForWireTransfers)
			
		RequiresApprovals
			restricted
			when (CashForecastApprovalRequired
			or	BankStatementDistribApprovalRequired
			or	CashLedgerTransactionApprovalRequired
			or	VoidStopPaymentApprovalRequired
			or	CashLedgerPaymentApprovalRequired
			or  BankStatementReconciliationApprovalRequired
			or	WireTransferApprovalRequired)

		RequiresApprovalsForCashLedgerTransactions
			restricted
			when (CashLedgerTransactionApprovalRequired
			or	BankStatementDistribApprovalRequired)
			





							
	Relations




		




		FinanceTeamRel is a FinanceTeam set

		FinanceResourceRel
			one-to-many relation to FinanceResource
			Field Mapping uses symbolic key
				related.HROrganization	= CashManagementGroup.FinanceEnterpriseGroup.HROrganization

		CompanyRel 
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key

		FinanceTeamMemberRel 
			one-to-many relation to FinanceTeamMember
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = CashManagementGroup
			Instance Selection
				where (related.ActiveResource)	

		BankStatementTotalCubeRel
			one-to-one relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
				related.BusinessClass = "BankStatementTotal"
								
		ForecastingAnalyticCubeRel
			one-to-one relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
				related.BusinessClass = "CashForecastPeriodAmount"
				
		BankServicesAnalyticCubeRel
			one-to-one relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
				related.BusinessClass = "BankServiceDetail"
		
		DebtManagementAnalyticCubeRel
			one-to-one relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
				related.BusinessClass = "DebtInstrumentIdentifierAmortizationSchedule"
						
		ReleasedStatementLineDistributionsRel
			one-to-many relation to BankStatementLineDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup	= CashManagementGroup
				related.Status				= 1 

		ReleasedNonCashLedgerDistributionsRel
			one-to-many relation to NonCashLedgerDistribution
			Field Mapping uses ByDistributionStatusByCashManagementGroup
				related.CashManagementGroup	= CashManagementGroup
				related.Status				= 1 



















		DirectForecastDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= CashManagementGroup)

		DirectForecastSummaryRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= CashManagementGroup)

		PayablesCompanyRel
			one-to-many relation to PayablesCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= CashManagementGroup)

		PurchasingCompanyRel
			one-to-many relation to PurchasingCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= CashManagementGroup)

		ReceivableCompanyRel
			one-to-many relation to ReceivableCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= CashManagementGroup)

		ImportCompanyRel		
			one-to-many relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
			Instance Selection
				where (related.Company.FinanceEnterpriseGroup	= CashManagementGroup)

		PfiConfigurationRel
			one-to-one relation to PfiConfiguration
			Field Mapping uses symbolic key
				related.PfiConfiguration = FTPConfigurationName

		GeneralLedgerSystemCodeRel											
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= CashManagementGroup
				related.GeneralLedgerSystemCode		= "CB"
				
	Rule Blocks

		AutoTranNumberRules
			if (AutoTranNumberByCompany)
				constraint 	(!AutoTranNumberByCashCode 					  
				and			 !AutoTranNumberByBankTransactionCode 
				and			 !AutoTranNumberByCashCodeBankTranCode)
					"CanOnlySelectOneMethodOfAutoNumbering"		
			if (AutoTranNumberByCashCode)					 
				constraint 	(!AutoTranNumberByCompany 					  
				and			 !AutoTranNumberByBankTransactionCode 
				and			 !AutoTranNumberByCashCodeBankTranCode)
					"CanOnlySelectOneMethodOfAutoNumbering"			
			if (AutoTranNumberByBankTransactionCode)
				constraint 	(!AutoTranNumberByCompany 
				and			 !AutoTranNumberByCashCode 					  
				and			 !AutoTranNumberByCashCodeBankTranCode) 
					"CanOnlySelectOneMethodOfAutoNumbering"	
			if (AutoTranNumberByCashCodeBankTranCode)
				constraint 	(!AutoTranNumberByCompany 
				and			 !AutoTranNumberByCashCode 					  
				and			 !AutoTranNumberByBankTransactionCode) 
					"CanOnlySelectOneMethodOfAutoNumbering"			
			if (AutoTranNumberByCompany			 
			or 	AutoTranNumberByCashCode 					  
			or	AutoTranNumberByBankTransactionCode 
			or	AutoTranNumberByCashCodeBankTranCode)
				constraint (AutoTranNumberForTransactionForm			 
				or  		AutoTranNumberForReceiptForm				 
				or  		AutoTranNumberForReturnedPaymentForm		 
				or  		AutoTranNumberForTransferForm)				 
					"AtLeastOneFormRequiredForAutoNumbering"
			if (AutoTranNumberForTransactionForm			 
			or  AutoTranNumberForReceiptForm				 
			or  AutoTranNumberForReturnedPaymentForm		 
			or  AutoTranNumberForTransferForm)				 
				constraint (AutoTranNumberByCompany			 
				or	  	AutoTranNumberByCashCode 					  
				or	 		AutoTranNumberByBankTransactionCode 
				or	 		AutoTranNumberByCashCodeBankTranCode)
					"AtLeastOneAutoNumberingLevelRequiredWithForm"	

		AutoReferenceNumberRules
			if (AutoReferenceNumberByCompany)
				constraint 	(!AutoReferenceNumberByCashCode 					  
				and			 !AutoReferenceNumberByBankTransactionCode 
				and			 !AutoReferenceNumberByCashCodeBankTranCode)
					"CanOnlySelectOneMethodOfAutoNumbering"		
			if (AutoReferenceNumberByCashCode)					 
				constraint 	(!AutoReferenceNumberByCompany 					  
				and			 !AutoReferenceNumberByBankTransactionCode 
				and			 !AutoReferenceNumberByCashCodeBankTranCode)
					"CanOnlySelectOneMethodOfAutoNumbering"			
			if (AutoReferenceNumberByBankTransactionCode)
				constraint	(!AutoReferenceNumberByCompany
				and			 !AutoReferenceNumberByCashCode 					  
				and			 !AutoReferenceNumberByCashCodeBankTranCode) 
					"CanOnlySelectOneMethodOfAutoNumbering"	
			if (AutoReferenceNumberByCashCodeBankTranCode)
				constraint 	(!AutoReferenceNumberByCompany
				and			 !AutoReferenceNumberByCashCode 					  
				and			 !AutoReferenceNumberByBankTransactionCode) 
					"CanOnlySelectOneMethodOfAutoNumbering"			
			if (AutoReferenceNumberByCompany			 
			or 	AutoReferenceNumberByCashCode 					  
			or	AutoReferenceNumberByBankTransactionCode 
			or	AutoReferenceNumberByCashCodeBankTranCode)
				constraint (AutoReferenceNumberForPaymentForm				 
				or  		AutoReferenceNumberForEFTForm)
					"AtLeastOneFormRequiredForAutoNumbering"
			if (AutoReferenceNumberForPaymentForm				 
			or  AutoReferenceNumberForEFTForm)
				constraint (AutoReferenceNumberByCompany			 
				or	  	AutoReferenceNumberByCashCode 					  
				or	 		AutoReferenceNumberByBankTransactionCode 
				or	 		AutoReferenceNumberByCashCodeBankTranCode)
					"AtLeastOneAutoNumberingLevelRequiredWithForm"													   		
		
	Actions	
		Create is a Create Action
			Entrance Rules




			Action Rules
				include AutoTranNumberRules
				include AutoReferenceNumberRules
				
		Update is an Update Action
			Action Rules
				include AutoTranNumberRules
				include AutoReferenceNumberRules

		FastUpdate is an Update Action		
			default label is untranslatable
			restricted
			bypass field rules

		Delete is a Purge Action











		LoadDirectForecast is a Set Action
			restricted
			Parameters
				CashManagementGroup
				GeneralLedgerCompany
					default label is "GlobalLedgerCompany"
				IncludeImportRecords		is Boolean		
					default label is "IncludeDirectForecastImportRecords"
				IncludeUnreleasedRecords is Boolean

			Parameter Rules
				CashManagementGroup
					required
					
				GeneralLedgerCompany
					constraint (GeneralLedgerCompany.FinanceEnterpriseGroup = CashManagementGroup)
						"Company<GeneralLedgerCompany>IsNotAssociatedWithCashManagementGroup<CashManagementGroup>"
			
			Instance Selection
				where (DerivedEmptySet)
				
			Action Rules
				Empty Set Rules
					if (GeneralLedgerCompany entered)
						invoke Purge GeneralLedgerCompany.DirectForecastSummary set
						invoke Purge GeneralLedgerCompany.DirectForecastDetail set

						if (!IncludeImportRecords)		
							invoke Update CashManagementGroup
								invoked.DirectForecastProcesses		= 3
								initialize invoked.DirectForecastProcessesCompleted
						else
							invoke Update CashManagementGroup
								invoked.DirectForecastProcesses		= 4
								initialize invoked.DirectForecastProcessesCompleted

							invoke LoadImportDetails DirectForecastDetailImport
								invoked.PrmCashManagementGroup	= CashManagementGroup
								invoked.PrmCompany				= GeneralLedgerCompany
								invoked.PrmLoadDate				= current timestamp

						invoke LoadOpenPaymentDetails PayablesInvoicePayment
							invoked.PrmCashManagementGroup		= CashManagementGroup
							invoked.PrmCompany					= GeneralLedgerCompany
							invoked.PrmLoadDate					= current timestamp
							invoked.PrmIncludeUnreleasedRecords = IncludeUnreleasedRecords

						invoke LoadOpenReceiptDetails ReceivableInvoiceDetail
							invoked.PrmCashManagementGroup	 	= CashManagementGroup
							invoked.PrmCompany				 	= GeneralLedgerCompany
							invoked.PrmLoadDate					= current timestamp


						invoke LoadOpenPurchorderDetails PurchaseOrder
							invoked.PrmCashManagementGroup	 	= CashManagementGroup
							invoked.PrmCompany				 	= GeneralLedgerCompany
							invoked.PrmLoadDate					= current timestamp
							invoked.PrmIncludeUnreleasedRecords = IncludeUnreleasedRecords


					else
						invoke Purge CashManagementGroup.DirectForecastSummaryRel
						invoke Purge CashManagementGroup.DirectForecastDetailRel

						if (!IncludeImportRecords)		
							invoke Update CashManagementGroup
								invoked.DirectForecastProcesses		= (CashManagementGroup.NumberOfAPCompanies + CashManagementGroup.NumberOfARCompanies + CashManagementGroup.NumberOfPOCompanies)
								initialize invoked.DirectForecastProcessesCompleted
						else
							invoke Update CashManagementGroup
								invoked.DirectForecastProcesses		= (CashManagementGroup.NumberOfAPCompanies + CashManagementGroup.NumberOfARCompanies + CashManagementGroup.NumberOfPOCompanies + CashManagementGroup.NumberOfImportCompanies)
								initialize invoked.DirectForecastProcessesCompleted

							for each CashManagementGroup.ImportCompanyRel
								invoke LoadImportDetails DirectForecastDetailImport
									invoked.PrmCashManagementGroup	= CashManagementGroup
									invoked.PrmCompany				= each.Company
									invoked.PrmLoadDate				= current timestamp
									invoked.PrmRunForGroup			= true

						for each CashManagementGroup.PayablesCompanyRel
							invoke LoadOpenPaymentDetails PayablesInvoicePayment
								invoked.PrmCashManagementGroup		= CashManagementGroup
								invoked.PrmCompany					= each.Company
								invoked.PrmLoadDate					= current timestamp
								invoked.PrmRunForGroup				= true
								invoked.PrmIncludeUnreleasedRecords = IncludeUnreleasedRecords


						for each CashManagementGroup.ReceivableCompanyRel
							invoke LoadOpenReceiptDetails ReceivableInvoiceDetail
								invoked.PrmCashManagementGroup		= CashManagementGroup
								invoked.PrmCompany					= each.Company
								invoked.PrmLoadDate					= current timestamp
								invoked.PrmRunForGroup				= true


						for each CashManagementGroup.PurchasingCompanyRel
							invoke LoadOpenPurchorderDetails PurchaseOrder
								invoked.PrmCashManagementGroup		= CashManagementGroup
								invoked.PrmCompany					= each.Company
								invoked.PrmLoadDate					= current timestamp
								invoked.PrmRunForGroup				= true
								invoked.PrmIncludeUnreleasedRecords = IncludeUnreleasedRecords


					invoke Update CashManagementGroup
						invoked.LastDirectForecastLoad = current timestamp

		UpdateCashManagementCubes is a Set Action
			restricted
			Parameters
				CashPosition		 		is Boolean
				CashPositionOption   		is a CubeUpdateOption
				Forecasting		 	 		is Boolean
				ForecastingOption			is a CubeUpdateOption
				BankServices		 		is Boolean
				BankServicesOption   		is a CubeUpdateOption
				
			Parameter Rules
				CashPosition
					initial value is true
				
				CashPositionOption
					initial value is 1 
					
				Forecasting
					initial value is true
					
				ForecastingOption
					initial value is 1 
					
				BankServices
					initial value is true
					
				BankServicesOption
					initial value is 1 

			Action Rules
				Set Rules
					Exit Rules
						if (CashPosition)





							if (CashPositionOption.Refresh)
								invoke Refresh BankStatementTotalCubeRel in background
							else
								invoke Reload BankStatementTotalCubeRel in background

						if (Forecasting)
							if (ForecastingOption.Refresh)
								invoke Refresh ForecastingAnalyticCubeRel in background
							else
								invoke Reload ForecastingAnalyticCubeRel in background

						if (BankServices)
							if (BankServicesOption.Refresh)
								invoke Refresh BankServicesAnalyticCubeRel in background
							else
								invoke Reload BankServicesAnalyticCubeRel in background

		UpdateDebtManagementCube is a Set Action
			restricted
			completion message is "UpdateDebtManagementCubeSubmitted"
			Parameters
				PrmRefreshReload	is a CubeUpdateOption

			Parameter Rules
				PrmRefreshReload
					initial value is 1 
					required

			Action Rules
				Set Rules
					Exit Rules
						if (PrmRefreshReload.Refresh)
							invoke Refresh DebtManagementAnalyticCubeRel in background
						else
							invoke Reload DebtManagementAnalyticCubeRel in background










		UpdateCashLedgerSourceRecord is a Set Action
			restricted
			completion message is "UpdateCashLedgerSourceRecordSubmitted"
			Parameters
				PrmCashManagementGroup	is a CashManagementGroup
					default label is "CashManagementGroup"

			Instance Selection
				where ((PrmCashManagementGroup	entered
				and	 CashManagementGroup		= PrmCashManagementGroup)
				or	  PrmCashManagementGroup	!entered)


			Action Rules
				Instance Rules
					invoke 	UpdateCashLedgerSourceRecord CashLedgerGLDistribution
						invoked.PrmCashManagementGroup	= CashManagementGroup

					invoke 	UpdateCashLedgerSourceRecord CashLedgerPayablesPayment
						invoked.PrmCashManagementGroup	= CashManagementGroup

					invoke 	UpdateCashLedgerSourceRecord CashLedgerTransaction
						invoked.PrmCashManagementGroup	= CashManagementGroup	
						
		BankTransactionHistoryPurge is a Set Action
			Parameters
				PrmCashManagementGroup	is a CashManagementGroup
					default label is "CashManagementGroup"
				PrmCashCode                is a CashCode
		        PrmCashCodeGroup           is a CashCodeGroup
		        PrmTransactionCutOffDate   is Date
		        PrmCbtBankInstType         is AlphaUpper size 1
		            States
		                CashPayments value is "C"
		                BoePayments  value is "D"
		                    default label is "BOE Payments"
		                BankServices value is "B"
		        PrmBankTransactionCode             is a BankTransactionCode
		        PrmFundTransferHeaderCutOffDate    is Date
		        PrmCashReceiptHeaderCutOffDate     is Date
		        PrmBankAccountBalanceCutOffDate    is Date
		        PrmPaymentCutOffDate               is Date
		        PrmEFTransferTransactionCutOffDate is Date
		        PrmReturnedPaymentCutOffDate       is Date
		        PrmCashManagementAccount	       is a CashManagementAccount
		    	PrmBankStatementCutOffDate         is Date
		    	PrmBankStatementReconciliationCutOffDate is Date
		    	PrmNonCashLedgerTransactionCutOffDate is Date
		    Parameter Rules
		    	PrmCashManagementGroup
   					initial value is actor.context.FinanceEnterpriseGroup
   					default to actor.context.FinanceEnterpriseGroup
   					required
   					
   				PrmCashCode
   					if(PrmBankStatementCutOffDate not entered
   					and PrmBankStatementReconciliationCutOffDate not entered
   					and PrmNonCashLedgerTransactionCutOffDate not entered
   					and PrmCashManagementAccount not entered)
	   					if (PrmCashCodeGroup not entered)
		   					required
		   						"CashCodeOrCashCodeGroupRequired"
	   					constraint(PrmCashCodeGroup not entered)
	   						"CannotEnterBothCashCodeAndCashCodeGroup"
   				
   				PrmCashCodeGroup
   					if(PrmBankStatementCutOffDate not entered
   					and PrmBankStatementReconciliationCutOffDate not entered
   					and PrmNonCashLedgerTransactionCutOffDate not entered
   					and PrmCashManagementAccount not entered)
	   					constraint(PrmCashCode not entered)
	   						"Cash_\CodeOr_\Cash_\Code_\GroupRequired"
   						
   				PrmTransactionCutOffDate
					constraint (PrmTransactionCutOffDate <= current corporate date)
					    "TransactionCutOffDateCannotBeGreaterThanSystemDate"
					    
					constraint(PrmCbtBankInstType entered)
   						"TransTypeRequiredToDeleteBankTransactions"
				
				PrmFundTransferHeaderCutOffDate
					constraint (PrmFundTransferHeaderCutOffDate <= current corporate date)
					    "FundTransferHeaderCutOffDateCannotBeGreaterThanSystemDate"
					    
				PrmCashReceiptHeaderCutOffDate
					constraint (PrmCashReceiptHeaderCutOffDate <= current corporate date)
					    "CashReceiptHeaderCutOffDateCannotBeGreaterThanSystemDate"
					    
				PrmBankAccountBalanceCutOffDate
					constraint (PrmBankAccountBalanceCutOffDate <= current corporate date)
					    "BankAccountBalanceCutOffDateCannotBeGreaterThanSystemDate"
					    
				PrmPaymentCutOffDate
					constraint (PrmPaymentCutOffDate <= current corporate date)
					    "PaymentCutOffDateCannotBeGreaterThanSystemDate"
					    
				PrmEFTransferTransactionCutOffDate
					constraint (PrmEFTransferTransactionCutOffDate <= current corporate date)
					    "EFTransferTransactionCutOffDateCannotBeGreaterThanSystemDate"
					    
				PrmReturnedPaymentCutOffDate
					constraint (PrmReturnedPaymentCutOffDate <= current corporate date)
					    "ReturnedPaymentCutOffDateCannotBeGreaterThanSystemDate"	
					    
				PrmBankStatementCutOffDate
					constraint (PrmBankStatementCutOffDate <= current corporate date)
					    "BankStatementCutOffDateCannotBeGreaterThanSystemDate"
					
					constraint(PrmCashManagementAccount entered)
   						"BankAccountRequiredToDeleteBankStatements"
   					
   					constraint(PrmCashCode not entered and PrmCashCodeGroup not entered)
   						"Cash_\CodeOr_\Cash_\Code_\GroupShouldNotBeEntered"
					    
				PrmBankStatementReconciliationCutOffDate
					constraint (PrmBankStatementReconciliationCutOffDate <= current corporate date)
					    "BankStatementReconciliationCutOffDateCannotBeGreaterThanSystemDate"
					    
					constraint(PrmCashManagementAccount entered)
   						"BankAccountRequiredToDeleteBankStatementReconciliationRecords"
   						
   					constraint(PrmCashCode not entered and PrmCashCodeGroup not entered)
   						"Cash_\CodeOr_\Cash_\Code_\GroupShouldNotBeEntered"
				
				PrmNonCashLedgerTransactionCutOffDate
					constraint (PrmNonCashLedgerTransactionCutOffDate <= current corporate date)
					    "NonCashLedgerTransactionCutOffDateCannotBeGreaterThanSystemDate"
					    
					constraint(PrmCashManagementAccount entered)
   						"BankAccountRequiredToDeleteNonCashLedgerTransaction"
   					
   					constraint(PrmCashCode not entered and PrmCashCodeGroup not entered)
   						"Cash_\CodeOr_\Cash_\Code_\GroupShouldNotBeEntered"
					
   			
   			Instance Selection			
				where (CashManagementGroup	= PrmCashManagementGroup)
				
			Local Fields
				LocalActor									is an Actor
				
			
			Action Rules	
				Set Rules
					Entrance Rules
						LocalActor			= actor
						
					Exit Rules
						send notification
							to LocalActor
							description is "PurgeProcessCompleted"
			
				Instance Rules
					display "=========BankTransactionHistoryPurge============"
					if(PrmTransactionCutOffDate entered)
						invoke CashLedgerTransactionPurge CashLedgerTransaction 
							invoked.PrmCashManagementGroup		= PrmCashManagementGroup
							invoked.PrmCashCode					= PrmCashCode
							invoked.PrmCashCodeGroup			= PrmCashCodeGroup
							invoked.PrmBankTransactionCode		= PrmBankTransactionCode
							invoked.PrmCbtBankInstType		    = PrmCbtBankInstType
							invoked.PrmTransactionCutOffDate	= PrmTransactionCutOffDate
	
					display "=========CashLedgerFundTransferHeaderPurge============"
					if(PrmFundTransferHeaderCutOffDate entered)
						invoke CashLedgerFundTransferHeaderPurge CashLedgerFundTransferHeader 
							invoked.PrmCashManagementGroup		    = PrmCashManagementGroup
							invoked.PrmCashCode					    = PrmCashCode
							invoked.PrmCashCodeGroup			    = PrmCashCodeGroup
							invoked.PrmFundTransferHeaderCutOffDate = PrmFundTransferHeaderCutOffDate
					if(PrmCashReceiptHeaderCutOffDate entered)	
						invoke CashLedgerCashReceiptHeaderPurge CashLedgerCashReceiptHeader 
							invoked.PrmCashManagementGroup		   = PrmCashManagementGroup
							invoked.PrmCashCode				       = PrmCashCode
							invoked.PrmCashCodeGroup			   = PrmCashCodeGroup
							invoked.PrmCashReceiptHeaderCutOffDate = PrmCashReceiptHeaderCutOffDate
					if(PrmPaymentCutOffDate entered)
						invoke CashLedgerPaymentPurge CashLedgerPayment 
							invoked.PrmCashManagementGroup		= PrmCashManagementGroup
							invoked.PrmCashCode					= PrmCashCode
							invoked.PrmCashCodeGroup			= PrmCashCodeGroup
							invoked.PrmPaymentCutOffDate		= PrmPaymentCutOffDate	
							invoked.PrmTransactionCutOffDate	= PrmTransactionCutOffDate
					if(PrmEFTransferTransactionCutOffDate entered)
						invoke CashLedgerElectronicFundsTransferTransactionPurge CashLedgerElectronicFundsTransferTransaction 
							invoked.PrmCashManagementGroup		       = PrmCashManagementGroup
							invoked.PrmCashCode					       = PrmCashCode
							invoked.PrmCashCodeGroup			       = PrmCashCodeGroup
							invoked.PrmEFTransferTransactionCutOffDate = PrmEFTransferTransactionCutOffDate		
					if(PrmReturnedPaymentCutOffDate entered)
						invoke CashLedgerReturnedPaymentPurge CashLedgerReturnedPayment 
							invoked.PrmCashManagementGroup		 = PrmCashManagementGroup
							invoked.PrmCashCode					 = PrmCashCode
							invoked.PrmCashCodeGroup			 = PrmCashCodeGroup
							invoked.PrmReturnedPaymentCutOffDate = PrmReturnedPaymentCutOffDate
					if(PrmBankAccountBalanceCutOffDate entered)
						invoke BankAccountBalancePurge BankAccountBalance 
							invoked.PrmCashManagementGroup		     = PrmCashManagementGroup
							invoked.PrmCashCode					     = PrmCashCode
							invoked.PrmCashCodeGroup			     = PrmCashCodeGroup
							invoked.PrmBankAccountBalanceCutOffDate	 = PrmBankAccountBalanceCutOffDate	
							
					if(PrmBankStatementReconciliationCutOffDate entered)
						invoke BankStatementReconciliationPurge BankStatementReconciliation
							invoked.PrmCashManagementGroup		             = PrmCashManagementGroup
							invoked.PrmCashManagementAccount	             = PrmCashManagementAccount
							invoked.PrmBankStatementReconciliationCutOffDate = PrmBankStatementReconciliationCutOffDate
							
					if(PrmNonCashLedgerTransactionCutOffDate entered)
						invoke NonCashLedgerTransactionPurge NonCashLedgerTransaction 
							invoked.PrmCashManagementGroup		          = PrmCashManagementGroup
							invoked.PrmCashManagementAccount	          = PrmCashManagementAccount
							invoked.PrmNonCashLedgerTransactionCutOffDate = PrmNonCashLedgerTransactionCutOffDate
					
					if(PrmBankStatementCutOffDate entered)
						invoke BankStatementPurge BankStatement 
							invoked.PrmCashManagementGroup		     = PrmCashManagementGroup
							invoked.PrmCashManagementAccount	     = PrmCashManagementAccount
							invoked.PrmBankStatementCutOffDate	     = PrmBankStatementCutOffDate

		PurgeReconciliationDetails is an Instance Action
			Parameters
				PrmCashManagementGroup				is a CashManagementGroup
					default label is "CashManagementGroup"
				PrmCashManagementAccount			is a CashManagementAccount
					default label is "CashManagementAccount"
				PrmCutOffDate						is Date
					default label is "CutOffDate"

			Parameter Rules
				PrmCashManagementAccount
					required
				PrmCutOffDate
					required

			Action Rules
				invoke BankStatementLineDetailPurge BankStatementLineDetail
					invoked.PrmCashManagementGroup		     = PrmCashManagementGroup
					invoked.PrmCashManagementAccount	     = PrmCashManagementAccount
					invoked.PrmDetailPurgeCutoffDate	     = PrmCutOffDate 

				invoke CashLedgerTransactionDetailPurge CashLedgerTransactionDetail
					invoked.PrmCashManagementGroup			 = PrmCashManagementGroup
					invoked.PrmCashManagementAccount	     = PrmCashManagementAccount
					invoked.PrmDetailPurgeCutoffDate		 = PrmCutOffDate
					
			
									
