ItemHistoryByVendorReport is a BusinessClass
	owned by procurement
	
	prefix is IHVR
	
	Ontology
		symbolic key is ItemHistoryByVendorReport
		
	
	Patterns
		disable Auditing
		disable EffectiveDated
		
	
	Persistent Fields
	
	Field Rules

	Derived Fields
		DerivedItemType is a MessageField
			"ItemType:_\<ItemHistoryByVendorReport.ItemType>"
			
		DerivedItemDescription is a DerivedField
			type is like Description
			return first PayablesInvoiceDetailTotalPerItemRel.Item.Description
			
		DerivedTotalRNIAmountPerVendor is a DerivedField
			type is like InternationalAmount
			return (sum PayablesInvoiceDetailTotalsRel.DerivedRNIAmount)
		
		DisplayVendorName is a LabelField
			"<VendorRel.Vendor>:_<VendorRel.Vendor.VendorName>"
			
		DerivedTotalReceivedQuantityPerVendor is a DerivedField
			type is like Quantity
			return (sum PayablesInvoiceDetailTotalsRel.PurchaseOrderReceiptLine.EnteredReceivedQuantity)
			
		DerivedTotalReceivedQuantityPerItem is a DerivedField
			type is like Quantity
			return (sum PayablesInvoiceDetailTotalPerItemRel.PurchaseOrderReceiptLine.EnteredReceivedQuantity)			

		DerivedTotalMatchedQuantityPerVendor is a DerivedField
			type is like Quantity
			return (sum PayablesInvoiceDetailTotalsRel.MatchedQuantity)
			
		DerivedTotalMatchedQuantityPerItem is a DerivedField
			type is like Quantity
			return (sum PayablesInvoiceDetailTotalPerItemRel.MatchedQuantity)
			
		DerivedTotalRNIQuantityPerVendor is a DerivedField
			type is like Quantity
			return (sum PayablesInvoiceDetailTotalsRel.DerivedRNIQuantity)
			
		DerivedTotalRNIQuantityPerItem is a DerivedField
			type is like Quantity
			return (sum PayablesInvoiceDetailTotalPerItemRel.DerivedRNIQuantity)

		DerivedTotalRNIAmountPerItem is a DerivedField
			type is like InternationalAmount
			return (sum PayablesInvoiceDetailTotalPerItemRel.DerivedRNIAmount)
			
		DerivedTotalForVendor is a DerivedField
			type is like InternationalAmount
			return (sum PayablesInvoiceDetailTotalsRel.TotalBaseAmount)															

		DerivedTotalForItem is a DerivedField
			type is like InternationalAmount
			restricted
			return (sum PayablesInvoiceDetailTotalPerItemRel.TotalBaseAmount)
						
		DerivedTotalForVendorPercent is a DerivedField
			type is Percent size 6.2
			return (DerivedTotalForVendor/DerivedTotalForItem)							
	
	Relations
		ItemHistoryReportByItemTypeRel
			one-to-many relation to ItemHistoryByVendorReport
			valid when (IsForItemType)
			Field Mapping uses symbolic key
				related.ItemGroup							= ItemGroup
				related.SupplyManagementReport				= SupplyManagementReport
			Instance Selection
				where (related.ItemHistoryByVendorReport.ItemType	= ItemHistoryByVendorReport.ItemType
				and	   related.ItemHistoryByVendorReport.Item entered
				and    related.ItemHistoryByVendorReport.Vendor = blank)	

		ItemHistoryReportVendorRel
			one-to-many relation to ItemHistoryByVendorReport
			valid when (IsForItem)
			Field Mapping uses symbolic key
				related.ItemGroup							= ItemGroup
				related.SupplyManagementReport				= SupplyManagementReport
			Instance Selection
				where (related.ItemHistoryByVendorReport.ItemType	= ItemHistoryByVendorReport.ItemType
				and	   related.ItemHistoryByVendorReport.Item = ItemHistoryByVendorReport.Item
				and    related.ItemHistoryByVendorReport.Vendor entered)   

		PayablesInvoiceDetailTotalPerItemRel
    		one-to-many relation to PayablesInvoiceDetail
    		valid when (IsForItem)
    		Field Mapping uses ByItemTypeItemVendor
    			related.Company 	= SupplyManagementReport.PayablesCompany
    			related.Item    	= ItemHistoryByVendorReport.Item
    			related.ItemType 	= ItemHistoryByVendorReport.ItemType
    		Instance Selection
    			where (related.TotalBaseAmount entered
    			and   (SupplyManagementReport.FromDate not entered or related.PayablesInvoice.InvoiceDate >= SupplyManagementReport.FromDate)
    			and   (SupplyManagementReport.ThruDate not entered or related.PayablesInvoice.InvoiceDate <= SupplyManagementReport.ThruDate))
    			
		PayablesInvoiceDetailPerVendorItem
			one-to-many relation to PayablesInvoiceDetail
			valid when (IsForVendor)
			Field Mapping uses ByItemTypeItemVendor
    			related.Company 	= SupplyManagementReport.PayablesCompany
    			related.Item    	= ItemHistoryByVendorReport.Item
    			related.ItemType 	= ItemHistoryByVendorReport.ItemType	
    			related.Vendor		= ItemHistoryByVendorReport.Vendor
    		Instance Selection
    			where ((SupplyManagementReport.FromDate not entered or related.PayablesInvoice.InvoiceDate >= SupplyManagementReport.FromDate)
    			and   (SupplyManagementReport.ThruDate not entered or related.PayablesInvoice.InvoiceDate  <= SupplyManagementReport.ThruDate))

		PayablesInvoiceDetailTotalsRel
			one-to-many relation to PayablesInvoiceDetail
			valid when (IsForVendor)
			Field Mapping uses ByItemTypeItemVendor
    			related.Company 	= SupplyManagementReport.PayablesCompany
    			related.Item    	= ItemHistoryByVendorReport.Item
    			related.ItemType 	= ItemHistoryByVendorReport.ItemType	
    			related.Vendor		= ItemHistoryByVendorReport.Vendor
    		Instance Selection
    			where (related.TotalBaseAmount entered
    			and   (SupplyManagementReport.FromDate not entered or related.PayablesInvoice.InvoiceDate >= SupplyManagementReport.FromDate)
    			and   (SupplyManagementReport.ThruDate not entered or related.PayablesInvoice.InvoiceDate <= SupplyManagementReport.ThruDate))		
    			
 		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup		= SupplyManagementReport.SupplyManagementReportVendorGroupField.VendorGroup
				related.Vendor			= ItemHistoryByVendorReport.Vendor   			    			

	Conditions
		AllReceiptsInvoicedPerVendor
			restricted
			when (DerivedTotalRNIAmountPerVendor = 0)		

		AllReceiptsInvoicedPerItem
			restricted
			when (DerivedTotalRNIAmountPerItem = 0)
			
		IsForItemType
			restricted
			when (ItemHistoryByVendorReport.ItemType entered
			and    ItemHistoryByVendorReport.Item   not entered
			and    ItemHistoryByVendorReport.Vendor not entered)
			
		IsForItem
			restricted
			when (ItemHistoryByVendorReport.ItemType entered
			and    ItemHistoryByVendorReport.Item entered
			and    ItemHistoryByVendorReport.Vendor not entered)
		
		IsForVendor
			restricted
			when (ItemHistoryByVendorReport.ItemType entered
			and    ItemHistoryByVendorReport.Item entered
			and    ItemHistoryByVendorReport.Vendor entered)				

	Actions
	
		Create is a Create Action
		
		Delete is a Delete Action

