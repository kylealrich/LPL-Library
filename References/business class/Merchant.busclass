Merchant is a BusinessClass
    owned by po
    prefix is PDM
    classic name is PDMERCHANT

    Ontology
        symbolic key is Merchant
            classic set name is PDMSET1
            classic name for ProcurementCardProgram is PCARD-PROGRAM

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		Active
        PhoneNumber                    is a TelephoneNumber 
        	holds pii
            classic name for PhoneNumber.InternationalPrefix is PREFIX
            classic name for PhoneNumber.SubscriberNumber is PHONE
            classic name for PhoneNumber.Extension is EXTENSION
        TaxID
        IncomeCode                     is a PayablesIncomeCode
        LegalName
        SearchName                     is a VendorName 
        	holds pii
            classic name is SNAME
        Name                           is a VendorName 
        	holds pii
            classic name is VNAME
        ContactName                    is a VendorName 
        	holds pii
            classic name is VENDOR-CONTCT
        VendorClass
            classic name is VEN-CLASS
        FaxNumber
            classic name for FaxNumber.InternationalPrefix is FAX-PREFIX
            classic name for FaxNumber.SubscriberNumber is FAX-NUM
            classic name for FaxNumber.Extension is FAX-EXT
        PostalAddress				is a PostalAddressV2	
        	holds pii
			classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
			classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
			classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
			classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
			classic name for PostalAddress.Municipality is CITY-ADDR5
			classic name for PostalAddress.StateProvince is STATE-PROV
        ExemptFromSecondaryWithholding is Boolean
            classic name is SEC-WTH-EXEMPT
        SecondaryWithholdingCode1      is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE1
        SecondaryWithholdingCode2      is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE2
        SecondaryWithholdingCode3      is a PayablesSecondaryWithholdingCode
            classic name is SEC-WTH-CODE3
        ForeignEconomicRegulatoryCode
            classic name is FOR-ECON-CODE

	Attach Rules
		constraint (Active)
			"Merchant<Merchant>MustBeActive"

	Relations
	
		MerchantGroupAssociationsRel
			one-to-many relation to MerchantGroupMerchants
			Field Mapping uses symbolic key
				related.ProcurementGroup						= ProcurementGroup
				related.ProcurementCardProgram					= ProcurementCardProgram
			Instance Selection
                where (related.MerchantGroupMerchants.Merchant	= Merchant)

        
        MerchantCrossReferencesRel
        	one-to-many relation to MerchantCrossReference
        	Field Mapping uses Set2
        		related.ProcurementGroup				= ProcurementGroup
        		related.ProcurementCardProgram			= ProcurementCardProgram
            	related.Merchant 						= Merchant

	Conditions

		RecordExists
			when (Merchant exists) 
			      	           	
	Field Rules  

		Active
		
			default to true
		
		TaxID
			if (IncomeCode entered)
				required
					"TaxIDIsRequiredWhenIncomeCodeIsEntered"
		
		IncomeCode
			if (TaxID entered)
				required
					"IncomeCodeIsRequiredWhenTaxIDIsEntered"
		
		SearchName
			if(SearchName not entered)
				default to Merchant
		LegalName
			if(LegalName not entered)
				default to Merchant
        


	Actions
	        Create is a Create Action
	        
	        	Field Rules
	        		PostalAddress
	        			required
								
	        CreateFromInterface is a Create Action
	        	restricted
	        
	        Update is an Update Action
	        
	        	Field Rules
	        		PostalAddress
	        			required
	        
	        	
	
	        Delete is a Delete Action
				Action Rules
	        		constraint(MerchantCrossReferencesRel not exists)
	        			"CannotDelete;MerchantIsAssociatedWithAVendor"
