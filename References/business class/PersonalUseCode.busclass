PersonalUseCode is a BusinessClass
	default label is "VehicleLimits"
    owned by am
    prefix is APU
    classic name is AMPUCODE

    Ontology
        symbolic key is PersonalUseCode
            classic set name is APUSET1
            classic name is PERSONAL-USE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        QualifyingBusinessUsePercentage                is a BusinessPercent
            sql name is QBusinessUsePercentage
            classic name is QUAL-BUS-PCT
        RecaptureDepreciationBelowQualifyingPercentage is Boolean
            sql name is RDBelowQualifyingPercentage
            classic name is DEPR-RCP-OPT
        ChangeMethodBelowQualifyingPercentage          is Boolean
            sql name is CMBelowQualifyingPercentage
            classic name is DEPR-METH-OPT
        AllowAnnualDepreciationLimit                   is Boolean
            classic name is DEPR-LMT-OPT
        OptionalDepreciationMethod                     is a Method
            classic name is OPT-DEPR-METH
        OptionalDepreciationLife                       is a Life
            classic name is OPT-DEPR-LIFE
        RecaptureTaxCreditBelowQualifyingPercentage    is Boolean
            sql name is RTCBelowQualifyingPercentage
            classic name is TC-RECAP-OPT1
        RecaptureTaxCreditBelowPriorYearUse            is Boolean
            sql name is RTaxCreditBelowPriorYearUse
            classic name is TC-RECAP-OPT2
        TransactionNumberOfDecimals                    is a CurrencyNumberOfDecimals
            classic name is TRAN-ND
        TaxCreditLimitBelowQualifyingPercentage        is an InternationalAmount
            sql name is TCLBelowQualifyingPercentage
            classic name is TC-LIMIT-OPT1
        TaxCreditLimitBelowPriorYearUse                is an InternationalAmount
            sql name is TCreditLimitBelowPriorYearUse
            classic name is TC-LIMIT-OPT2
        AllowPostRecoveryDeduction                     is Boolean
            classic name is PST-RCVRY-DED
        DepreciationLimitArray
            classic name for DepreciationLimitArray.DepreciationLimitGroup.DepreciationLimit is DEPR-LIMIT

    Field Rules

        Description
            required

        QualifyingBusinessUsePercentage
            required
			constraint (QualifyingBusinessUsePercentage <= 100)
				"QualifyingPercentageCannotBeGreaterThan100%"

		OptionalDepreciationMethod
			constraint (OptionalDepreciationMethod.MethodType.Depreciation)
				"OptionalDepreciationMethodMustHaveMethodTypeOfDepreciation"
			if (ChangeMethodBelowQualifyingPercentage)
				required
					"OptionalDepreciationMethodIsRequiredWhenChangeMethodBelowQualifyingPercentageIsSpecified"

		AllowAnnualDepreciationLimit
			if (AllowAnnualDepreciationLimit)
				constraint (DepreciationLimitArray entered)
					"AtLeastOneDepreciationLimitMustBeEnteredWhenAllowAnnualDepreciationLimitIsSelected"

		AllowPostRecoveryDeduction
			constraint (AllowAnnualDepreciationLimit)
				"AllowAnnualDepreciationLimitMustBeSelectedIfAllowPostRecoveryDeductionIsSelected"

		DepreciationLimitArray
			constraint (AllowAnnualDepreciationLimit)
				"AllowAnnualDepreciationLimitIsNotSelected;AnnualDepreciationLimitsCannotBeEntered"
				
	Sets
		Set1
			indexed
            Sort Order
            	FinanceEnterpriseGroup
            	Book
            	PersonalUseCode
            	EffectiveDate descending

	Actions
		Create is a Create Action
		
		CreatePUCFromPUC is a Create Action
			default label is "CopyFromLimitCode"
			Parameters
				Book
				FinanceEnterpriseGroup
				PrmPersonalUseCode is a PersonalUseCode
				PrmEffectiveDate is a EffectiveDate
			Parameter Rules
				Book
					initial value is PrmPersonalUseCode.Book
					default to PrmPersonalUseCode.Book
				FinanceEnterpriseGroup
					initial value is Book.FinanceEnterpriseGroup
					default to Book.FinanceEnterpriseGroup
				PrmEffectiveDate
					required
						"EffectiveDateIsRequired"
			Action Rules
				invoke Create this instance
					fill in fields from PrmPersonalUseCode
					invoked.EffectiveDate					= PrmEffectiveDate

		Update is an Update Action

		Delete is a Delete Action            

