TrueCostUnutilizedCapacityInterface is a BusinessClass
	owned by truecost
	sql name is UnutilizedCapacityInterface
	default label is "UnutilizedCapacityInterface"	
	prefix is TCUIN
	
	Ontology
		symbolic key is TrueCostUnutilizedCapacityInterface
		
	
	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		TrueCostCapacity					is like TrueCostCapacity
			default label is "PracticalCapacity"
		TrueCostCapacityUnutilized			is like FinanceDimension1
			default label is "SummaryVolumeDimension"
		UnutilizedCapacityDimension 		is like FinanceDimension1
			default label is "UnutilizedCapacity"
		ErrorDescription					is Alpha 150

	Local Fields
    	LocalTrueCostConfiguration			is like TrueCostConfiguration
    	LocalRunGroup						is like RunGroup
 	
	Field Rules
		TrueCostCapacity
			required
	
		TrueCostCapacityUnutilized
			required

		UnutilizedCapacityDimension
			required

	Conditions
		HasError
			restricted
			when (ErrorDescription entered)	
	Relations
		RunGroupParamRel
			one-to-many relation to TrueCostUnutilizedCapacityInterface
			Field Mapping uses symbolic key
				related.TrueCostConfiguration				  		 				= LocalTrueCostConfiguration
			Instance Selection
				where(related.TrueCostUnutilizedCapacityInterface.RunGroup = LocalRunGroup)		

		TrueCostCapacityRel
			one-to-one relation to 	TrueCostCapacity
			Field Mapping uses symbolic key
				related.TrueCostConfiguration						= TrueCostConfiguration
				related.TrueCostCapacity							= TrueCostCapacity

	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		InterfaceUnutilizedCapacity		 is a Set Action
			Parameters
				PrmTrueCostConfiguration 		is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroup						is a RunGroup
					default label is "RunGroup"
						
			Parameter Rules		
				PrmTrueCostConfiguration
					required					
				PrmRunGroup
					required
					LocalTrueCostConfiguration	=	PrmTrueCostConfiguration
					LocalRunGroup				=	PrmRunGroup
					constraint(RunGroupParamRel exists)
						"RunGroupDoesNotExist"
			Instance Selection
				where (TrueCostConfiguration										=	PrmTrueCostConfiguration
				and    TrueCostUnutilizedCapacityInterface.RunGroup		=	PrmRunGroup)		
			Local Fields
				LocalTotalCount						is Numeric 12
				LocalSuccessCount					is Numeric 12
				LocalFailedCount					is Numeric 12
				LocalActor							is an Actor
			
			Action Rules
				Set Rules
					Exit Rules
						LocalFailedCount	= LocalTotalCount - LocalSuccessCount
						LocalActor 			= actor		
						send notification
							to LocalActor
							description is "ImportTo_True\Cost_Unutilized_CapacityHasCompleted"
							priority is high
							detail is "NumberOfSuccessfulRecords<LocalSuccessCount>.___NumberOfFailedRecords<LocalFailedCount>.___TotalRecordsProcessed<LocalTotalCount>"

				Instance Rules					
					LocalTotalCount += 1
					initialize ErrorDescription
											
					invoke Create TrueCostCapacityUnutilized
						resume on error
							ErrorDescription	=	error message
						invoked.TrueCostConfiguration   				= TrueCostConfiguration
						invoked.TrueCostCapacity        				= TrueCostCapacity
						if (TrueCostCapacityRel.VolumeDimension1)
							invoked.TransientSummaryVolumeDimension1	= TrueCostCapacityUnutilized
							invoked.TransientPostingVolumeDimension1    = UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension2)
							invoked.TransientSummaryVolumeDimension2	= TrueCostCapacityUnutilized	
							invoked.TransientPostingVolumeDimension2	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension3)
							invoked.TransientSummaryVolumeDimension3	= TrueCostCapacityUnutilized 
							invoked.TransientPostingVolumeDimension3	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension4)
							invoked.TransientSummaryVolumeDimension4	= TrueCostCapacityUnutilized
							invoked.TransientPostingVolumeDimension4	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension5)
							invoked.TransientSummaryVolumeDimension5	= TrueCostCapacityUnutilized	
							invoked.TransientPostingVolumeDimension5	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension6)
							invoked.TransientSummaryVolumeDimension6	= TrueCostCapacityUnutilized	
							invoked.TransientPostingVolumeDimension6	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension7)
							invoked.TransientSummaryVolumeDimension7	= TrueCostCapacityUnutilized
							invoked.TransientPostingVolumeDimension7	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension8)
							invoked.TransientSummaryVolumeDimension8	= TrueCostCapacityUnutilized
							invoked.TransientPostingVolumeDimension8	= UnutilizedCapacityDimension
						else
						if (TrueCostCapacityRel.VolumeDimension9)
							invoked.TransientSummaryVolumeDimension9	= TrueCostCapacityUnutilized	
							invoked.TransientPostingVolumeDimension9	= UnutilizedCapacityDimension	
						else
						if (TrueCostCapacityRel.VolumeDimension10)
							invoked.TransientSummaryVolumeDimension10	= TrueCostCapacityUnutilized	
							invoked.TransientPostingVolumeDimension10	= UnutilizedCapacityDimension									
						
					if (ErrorDescription not entered)
						invoke Delete
						LocalSuccessCount +=1

		DeleteAllTransactionsForRunGroup is a Set Action
			default label is "DeleteAllTransactionsForRunGroup"
			Parameters
				PrmTrueCostConfiguration 		is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroup						is a RunGroup
					default label is "RunGroup"
			
			Parameter Rules
				PrmTrueCostConfiguration
					required
				PrmRunGroup
					required
					LocalTrueCostConfiguration	= PrmTrueCostConfiguration
					LocalRunGroup				= PrmRunGroup				
					constraint(RunGroupParamRel exists)
						"RunGroupDoesNotExist"
						
			Instance Selection				
				where(TrueCostConfiguration									= PrmTrueCostConfiguration
				and TrueCostUnutilizedCapacityInterface.RunGroup	= PrmRunGroup)
			
			Action Rules
				Instance Rules
					invoke Delete

		CopyRunGroup					is a Set Action
			
			Parameters
				PrmTrueCostConfiguration	is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroupFrom				is a RunGroup
					default label is "Copy_from_RunGroup"
				PrmRunGroupTo				is a RunGroup
					default label is "Copy_to_RunGroup"
					
			Parameter Rules
				PrmTrueCostConfiguration
					required
				PrmRunGroupFrom
					required
					LocalTrueCostConfiguration	= PrmTrueCostConfiguration
					LocalRunGroup 				= PrmRunGroupFrom
					constraint (RunGroupParamRel exists)
						"SourceRunGroupDoesNotExist"
					constraint (PrmRunGroupFrom != PrmRunGroupTo)
						"CopyFromRunGroupCannotBeTheSameAsCopyToRunGroup"
				PrmRunGroupTo
					required
					
			Instance Selection
				where (TrueCostConfiguration 								= PrmTrueCostConfiguration
				and TrueCostUnutilizedCapacityInterface.RunGroup	= PrmRunGroupFrom)
				
			Action Rules
				
				Instance Rules
					invoke Create TrueCostUnutilizedCapacityInterface
						fill in fields from this instance
							except invoked.RunGroup
						invoked.RunGroup				= PrmRunGroupTo

	
