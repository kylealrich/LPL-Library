TermAndConditionUpdate is a BusinessClass
    owned by procurement
    prefix is TCUPD

    Ontology
    	symbolic key is TermAndConditionUpdate

    Persistent Fields
		OriginalTitle				is a Description
		RevisedTitle				is a Description
		OriginalHeaderText			is Alpha 100
		RevisedHeaderText			is Alpha 100
		OriginalDescription			is Text
		RevisedDescription			is Text
		AttachmentToDelete			is Numeric 4
		DeletedAttachment			is a Attachment
		DeletedAttachmentRef		is a Description
		NewAttachment				is a Attachment
		NewAttachmentReference 		is a Description
		NewAttachmentDisplayOrder	is a DisplayOrder
		ActionTaken					is Numeric 2
			States
				Rejected	value is 1
				Approved	value is 2
				Withdrawn	value is 3
		Status						is Numeric 2
			States
				Draft		value is 1
				Pending		value is 2
				Complete	value is 3
		RequestType					is Numeric 2
			default label is "Action"
			States
				Create		value is 1
				Update		value is 2
		SpecificRequestType			is Numeric 2
			default label is "RequestType"
			States
				Create					value is 1
				UpdateTermAndCondition	value is 2
				AddAttachment			value is 3
				DeleteAttachment		value is 4
		RequestingActor				is an Actor
			default label is "RequestedBy"
		RejectionComment			is Text
		ApprovalComment				is Text
		RequestDateTime				is TimeStamp
			default label is "RequestDate"

	Transient Fields

	Derived Fields
		DerivedComparedTitle is a NativeField
			type is RichText

		DerivedComparedHeaderText is a NativeField
			type is RichText

		DerivedComparedDescription is a NativeField
			type is RichText
		
		DerivedRequesterEmailAddress is a DerivedField
			type is Alpha 100
			restricted
			return RequestingActor.ContactInfo.EmailAddress
		
		DerivedRequesterPhoneNumber is a DerivedField
			type is Alpha 100
			return RequestingActor.ContactInfo.TelephoneNumber.TelephoneDisplay

		DerivedRequesterName is a DerivedField
			type is Alpha 100
			return RequestingActor.PersonName.PreferredFirstAndLastName

	Conditions
		DisplayRejectionComment
			restricted
			when (RejectionComment entered)

		RequesterEmailAddressExists
			restricted
			when (DerivedRequesterEmailAddress entered)

		AttachmentHasBeenDeleted
			restricted
			when (SpecificRequestType.DeleteAttachment
			and	  Status.Complete)

		AttachmentToBeDeleted
			restricted
			when (SpecificRequestType.DeleteAttachment
			and	  !Status.Complete)

	Relations
		TermAndConditionAttachmentRel
			one-to-one relation to TermAndConditionAttachment
			Field Mapping uses symbolic key
				related.ProcurementGroup 			= ProcurementGroup
				related.Article						= Article
				related.TermAndCondition			= TermAndCondition
				related.TermAndConditionAttachment	= AttachmentToDelete

    Field Rules
		RevisedTitle
			if (SpecificRequestType.UpdateTermAndCondition)
				required
		RevisedDescription
			if (SpecificRequestType.UpdateTermAndCondition)
				required
		NewAttachment
			if (SpecificRequestType.AddAttachment)
				required
		AttachmentToDelete
			if (SpecificRequestType.DeleteAttachment)
				required

 	Sets
		
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
