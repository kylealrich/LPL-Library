PurchaseOrderReceiptLineImport is a BusinessClass
    owned by ma
    prefix is MRL
    classic name is MACRECLINE

    Ontology
        symbolic key is PurchaseOrderReceiptLineImport
            classic set name is MRLSET1
            classic name for PurchaseOrderReceiptLineImport.LineNumber is LINE-NBR
            classic name for PurchaseOrderReceiptImport is REFERENCE-NO

    Patterns
        implements ContextualParent
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields
		RunGroup
        PurchaseOrderReceipt
            classic name is REC-NUMBER
        Item
        Description
        ItemType
        VendorItem
            classic name is VEN-ITEM
        Location                 is an InventoryLocation
        EnteredReceivedQuantity  is like Quantity
            classic name is ENT-REC-QTY
        ReceivedUOM              is a UnitOfMeasure
            classic name is ENT-REC-UOM
        ReceivedUOMMultiplier    is an UOMMultiplier
            classic name is ENT-RUOM-MULT
        CatchWeightQuantity      is a Quantity
            classic name is CATCH-WT-QTY
        OriginalUnitCost         is an InternationalCost
            classic name is ORIG-UNIT-CST
        Bin
        CancelBackorder          is Boolean
            classic name is CANCEL-BO-FL
        RecComments              is an ErsComments
        Status                   is Numeric size 1
        	States
        		Unreleased	value is 0
        		Released	value is 1
        UpdateDate               is TimeStamp
		MatchDetailKey
        VendorPriceUOMQuantity   is a Quantity
            classic name is VPRI-QUANTITY
        VendorPriceUOM           is a UnitOfMeasure
            classic name is VPRI-UOM
        VendorPriceUOMMultiplier is an UOMMultiplier
            classic name is VPRI-UOM-MULT
        BuyUOMQuantity           is a Quantity
            classic name is BUOM-QTY
        VendorBuyUOM             is a UnitOfMeasure
            classic name is VBUY-UOM
        VendorBuyUOMMultiplier   is an UOMMultiplier
            classic name is VBUY-UOM-MULT
        TaxCode

            classic name is SUB-ACCT
        TaxUsageCode
            classic name is TAX-USAGE-CD
        MSDSRequired             is Boolean
            classic name is MSDS-FLAG
        MSDSVersion
        MSDSDocument
        CatchWeightCost          is an InternationalCost
            classic name is CATCH-WGT-COST
        TaxableUnitCost          is an InternationalCost
            classic name is TAX-UNIT-CST
        InspectionRequired       is Boolean
            classic name is INSP-REQ-FL
        CommodityCode			 is a CommCodes
        ProductTaxCategory
            classic name is PROD-TAX-CAT
        ItemGTIN
            classic name is GTIN
        LocationGLN              is like GlobalLocationNumber
        BinGLN                   is like GlobalLocationNumber
        GlobalLineType
            classic name is GLBL-LINE-TYPE
        DistributionAccount      is a FinanceCodeBlock
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
        RecordInError			 is Boolean
        Interfaced			     is Boolean
        PurchaseOrder            is a snapshot of PurchaseOrderReceiptImport.PurchaseOrder
        HSNSACCode         
			
        

	Local Fields
		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150
		LocalPurchaseOrderLine			is like PurchaseOrderLine
		LocalMatchDetailKey				is like MatchDetailKey

		LocalFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
		LocalInterfaceResult 			is like PurchaseOrderReceiptInterfaceResult

		POLineLookup
		LocalTrigger					is Numeric 1
		LocalPurchaseOrder				is a PurchaseOrder
        LocalBin                     	is like Bin 
        LocalSerial                  	is like ItemSerialNumber 
        LocalLot                     	is like ItemLot 

	Context Fields
		ContextReceiptImport is a PurchaseOrderReceiptImport
		
	Derived Fields

		CannotDeterminePurchaseOrderLineMsg is a MessageField
			restricted
			"CannotDeterminePurchaseOrderLine;MultipleLinesExistForValuesEntered"

		CannotFindMatchingPurchaseOrderLineMsg is a MessageField
			restricted
			"CannotFindMatchingPurchaseOrderLine"

		HasLineAddOnCostsWithInvalidRunGroupMsg is a MessageField
			restricted
			"AddOnCostsExistForThisLineWithInvalidRunGroup"
			
		CancelRemainingMustBeYesIfQuantityIsZeroMsg is a MessageField
			restricted
			"CancelRemainingMustBeYesIfQuantityIsZero"
		
		CannotEnterBinMessage is a MessageField
			restricted
			"CannotEnterBinIfNotBinTracked"
			
		CannotEnterLotMessage is a MessageField
			restricted
			"CannotEnterLotIfNotLotTracked"
		
		CannotEnterSerialMessage is a MessageField
			restricted
			"CannotEnterSerialIfNotSerialTracked"

	Conditions
		HasLineAddOnCostsWithInvalidRunGroup
			when (LineAddOnCostsWithInvalidRunGroupRel exists)

		IsHSNSACCodeEnabled
			restricted
			when (Company.GeneralLedgerCompany.RequireHSNSACCode)

        IsCatchWeightItem
        	restricted
        	when (Item.CatchWeightCode.AllTransactionsRequireBoth or Item.CatchWeightCode.ReceiptRequiresBoth) 
        	
        HasUnreleasedReceiptLine
        	when (UnreleasedReceiptLineRel exists)
			
    Relations

        PurchaseOrderReceiptImportRel
            one-to-one relation to PurchaseOrderReceiptImport
            Field Mapping uses symbolic key
                related.Company                    								= Company
                related.PurchaseOrderReceiptImport 								= PurchaseOrderReceiptImport

        LineAddOnCostsRel
            one-to-many relation to PurchaseOrderReceiptLineAOCImport
            Field Mapping uses ByRunGroup
            	related.RunGroup												= RunGroup
                related.Company                    								= Company
                related.PurchaseOrderReceiptImport 								= PurchaseOrderReceiptImport
            Instance Selection
            	where (related.PurchaseOrderReceiptLineAOCImport.LineNumber 	= PurchaseOrderReceiptLineImport.LineNumber
            	and    related.PurchaseOrderReceiptLineAOCImport.SeqNbr		 	= PurchaseOrderReceiptLineImport.SeqNbr)

        LineAddOnCostsWithInvalidRunGroupRel
            one-to-many relation to PurchaseOrderReceiptLineAOCImport
            Field Mapping uses symbolic key
                related.Company                    								= Company
                related.PurchaseOrderReceiptImport 								= PurchaseOrderReceiptImport
            Instance Selection
            	where (related.PurchaseOrderReceiptLineAOCImport.LineNumber 	= PurchaseOrderReceiptLineImport.LineNumber
            	and    related.PurchaseOrderReceiptLineAOCImport.SeqNbr			= PurchaseOrderReceiptLineImport.SeqNbr
            	and    related.RunGroup										   != RunGroup)

        POReceiptInventoryDetailImportRel
            one-to-many relation to POReceiptInventoryDetailImport
            Field Mapping uses ByRunGroup
            	related.RunGroup												= RunGroup
                related.Company                    								= Company
                related.PurchaseOrderReceiptImport 								= PurchaseOrderReceiptImport
            Instance Selection
            	where (related.POReceiptInventoryDetailImport.LineNumber 		= PurchaseOrderReceiptLineImport.LineNumber)

        ItemLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = Location
                related.Item              = Item

        PurchaseOrderLineLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses Set11
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
				related.MatchDetailKey				= LocalMatchDetailKey
			Instance Selection
				where (related.Closed.No
				and   (Location not entered
				or     related.ShipToLocation		= Location))

        ItemAndVendorItemLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
			Instance Selection
				where (related.Item					= Item
    			and    related.VendorItem			= VendorItem)
            
        ItemOnlyLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
			Instance Selection
				where (related.Item					= Item)

        VendorItemOnlyLookupRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
			Instance Selection
				where (related.VendorItem			= VendorItem)
        
        LineNumberLookupRel
			one-to-one relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrder						= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
				related.PurchaseOrderLine					= PurchaseOrderReceiptLineImport.LineNumber

		LocalInterfaceResultRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= LocalFinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult		= LocalInterfaceResult
		
		PurchaseOrderReceiptLineRel
			one-to-one relation to PurchaseOrderReceiptLine
			Field Mapping uses symbolic key
				related.Company								= Company
				related.PurchaseOrderReceipt				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
				related.PurchaseOrderReceiptLine			= LocalPurchaseOrderLine

		UnreleasedReceiptLineRel
			one-to-many relation to PurchaseOrderReceiptLine
			Field Mapping uses ByPurchaseOrderLine
				related.Company								= Company
				related.PurchaseOrder						= PurchaseOrderReceiptImportRel.PurchaseOrder
				related.PurchaseOrderLine					= PurchaseOrderReceiptLineImport.LineNumber
			Instance Selection
				where (related.Status.Entered
				and    not related.PurchaseOrderReceipt.IsDropShip) 

	Sets
        ByRunGroup
            Sort Order
				RunGroup
                Company
                PurchaseOrderReceiptImport
                PurchaseOrderReceiptLineImport.LineNumber
                PurchaseOrderReceiptLineImport.SeqNbr
                
        ByPurchaseOrderLine
			Sort Order
				Company
				PurchaseOrder
				PurchaseOrderReceiptLineImport.LineNumber
				RunGroup
				PurchaseOrderReceiptImport
                PurchaseOrderReceiptLineImport.SeqNbr
	
	
        
	
	Field Rules
		RunGroup
			required
			default to ContextReceiptImport.RunGroup
			initial value is ContextReceiptImport.RunGroup

	Rule Blocks
	
		GetPurchaseOrderLine
		
			if  (Item entered
			and  VendorItem entered
			and  instance count of ItemAndVendorItemLookupRel = 1)
				LocalPurchaseOrderLine = first ItemAndVendorItemLookupRel.PurchaseOrderLine
			else
			if  (Item not entered
			and  VendorItem entered
			and  instance count of VendorItemOnlyLookupRel = 1)
				LocalPurchaseOrderLine = first VendorItemOnlyLookupRel.PurchaseOrderLine
			else
			if  (Item entered
			and  VendorItem not entered
			and  instance count of ItemOnlyLookupRel = 1)
				LocalPurchaseOrderLine = first ItemOnlyLookupRel.PurchaseOrderLine
			else
				initialize LocalMatchDetailKey
				initialize LocalPurchaseOrderLine
								
				if  (MatchDetailKey entered)
					LocalMatchDetailKey = MatchDetailKey
				else
					if  (ItemType.Inventoried 
					or   ItemType.NonStock)
						LocalMatchDetailKey 	= Item
					else
						LocalMatchDetailKey 	= VendorItem
						if  (LocalMatchDetailKey not entered)
							LocalMatchDetailKey = Description
						
						if  (LocalMatchDetailKey not entered)
							LocalMatchDetailKey = Item
	
				if  (LocalMatchDetailKey entered)
					for each PurchaseOrderLineLookupRel
						if  (!ItemType.Special
						or   ReceivedUOM not entered
						or   each.EnteredBuyUOM		= ReceivedUOM)				
	
							if (LocalPurchaseOrderLine entered)
								ErrorOccurred		= true
								LocalErrorMessage	= CannotDeterminePurchaseOrderLineMsg
	
							LocalPurchaseOrderLine = each.PurchaseOrderLine
								
				if (LocalPurchaseOrderLine not entered)
					ErrorOccurred		= true
					LocalErrorMessage	= CannotFindMatchingPurchaseOrderLineMsg

	
	Actions
		Create is a Create Action
			Action Rules
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules
		
		Update is an Update Action
			Action Rules

		FastUpdate is an Update Action
			restricted
			bypass field rules

		ChangeLineNumber is an Instance Action
			Parameters 
				NewLineNumber is like LineNumber 
			Parameter Rules 
				NewLineNumber 
					required 
			Action Rules
				invoke Create PurchaseOrderReceiptLineImport 
					fill in fields from this instance 
						except invoked.PurchaseOrderReceiptLineImport.LineNumber
					invoked.PurchaseOrderReceiptLineImport.LineNumber = NewLineNumber 
				invoke Delete 

		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules

				RunGroup = NewRunGroup

		RemoveInvalidAddOnCosts is an Instance Action
			Action Rules
				invoke Delete LineAddOnCostsWithInvalidRunGroupRel
					
		Delete is a Delete Action
			Entrance Rules
				invoke Delete LineAddOnCostsRel
				invoke Delete LineAddOnCostsWithInvalidRunGroupRel
				invoke Delete POReceiptInventoryDetailImportRel
			Action Rules
					
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		ReceiptLineLoad is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				ReleaseReceipts				is Boolean
				ReleaseOption
				AllowDropshipReceivers		is Boolean
				AllowMatchItemOrVendorItem  is Boolean
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PurchaseOrderReceiptInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup	 
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany))

			Sort Order
				RunGroup
				Company
				PurchaseOrderReceiptImport

			Local Fields
				ReceiptError 			is Boolean
				ReceiptErrorMessage		is Alpha 150
				
			Action Rules

				Empty Set Rules
						
					invoke AddOnChargeLoad PurchaseOrderReceiptLineAOCImport
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmCompany					= PrmCompany
				        invoked.ReleaseReceipts				= ReleaseReceipts
						invoked.ReleaseOption				= ReleaseOption
						invoked.AllowDropshipReceivers		= AllowDropshipReceivers
						invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
						invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
						invoked.PrmInterfaceRun				= PrmInterfaceRun

				RunGroup Set Rules
						
					Exit Rules
						invoke AddOnChargeLoad PurchaseOrderReceiptLineAOCImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.ReleaseOption				= ReleaseOption
							invoked.AllowDropshipReceivers		= AllowDropshipReceivers
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
							invoked.PrmInterfaceRun				= PrmInterfaceRun

				PurchaseOrderReceiptImport Set Rules
					Entrance Rules
						ReceiptError = false
						initialize ReceiptErrorMessage
					Exit Rules




						if  (ReceiptError)
							invoke SetError PurchaseOrderReceiptImport
								invoked.PrmErrorMessage			= ReceiptErrorMessage
								invoked.PrmInterfaceResult		= PrmInterfaceRun
								invoked.PrmErrorLevel   		= 2

						if (!PurchaseOrderReceiptImportRel.RecordInError.ErrorsExist
						and !PurchaseOrderReceiptImportRel.PurchaseOrderReceiptLineAOCImportRel exists)
							if (AllowDropshipReceivers 
							and PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder.IsDropship)
								invoke CreateInProgress.TransitionToDropship PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
							else
								invoke CreateInProgress.TransitionToUnreleased PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
							
								invoke Unreleased.FastUpdate PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
									invoked.InterfaceInProcess				= false
								if (ReleaseReceipts)
									if	(ReleaseOption.ReleaseOnly)
										invoke Unreleased.Release PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											resume on error
									else
									if (ReleaseOption.ReleaseAndDeliver)
										invoke Unreleased.ReleaseAndPrintLPL PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											resume on error
									else
									if (ReleaseOption.ReleaseAndDeliverNoBackorders)
										invoke Unreleased.ReleaseAndPrintNoBackordersLPL PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											resume on error

							invoke FastDelete PurchaseOrderReceiptImportRel

				Instance Rules
					if  (!PurchaseOrderReceiptImportRel.RecordInError.ErrorsExist
					and  !ReceiptError)
					
						RecordInError 						= false
						ErrorOccurred						= false
						display "POLL:LineLoad<Item>"
						display "POLL:Get:Ref<PurchaseOrderReceiptImport>Loc<Location>AllowMatchItemOrVendorItem<AllowMatchItemOrVendorItem>"
			
						initialize POLineLookup
						POLineLookup.Company				= Company
						POLineLookup.PurchaseOrder			= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
			
						if  (PurchaseOrderReceiptLineImport.LineNumber not entered
						or   LineNumberLookupRel.QuantityFullyCancelled
						or  (Item entered
						and  LineNumberLookupRel.Item != Item)
						or  (VendorItem entered
						and  LineNumberLookupRel.VendorItem != VendorItem)
						or  (PurchaseOrderReceiptImportRel.DerivedVendorPoolOption.CompanyVendorPoShipTo	
						and  LineNumberLookupRel.ShipToLocation != Location))
							POLineLookup.PurchaseOrderLine		= PurchaseOrderReceiptLineImport.LineNumber
							POLineLookup.Item					= Item
							POLineLookup.VendorItem				= VendorItem
							POLineLookup.Description			= Description
							POLineLookup.Location				= Location
							POLineLookup.AllowMatchItemOrVendorItem = AllowMatchItemOrVendorItem
							POLineLookup.InterfaceType 			= "R"
							LocalTrigger						= POLineLookup.ExecuteLookup

							if  (POLineLookup.OutputErrorNumber entered)
								ErrorOccurred					= true
								LocalErrorMessage				= POLineLookup.OutputErrorMessage
							else
								LocalPurchaseOrderLine				= POLineLookup.PurchaseOrderLine
								Item							= POLineLookup.Item
								VendorItem						= POLineLookup.VendorItem
						else
							LocalPurchaseOrderLine = PurchaseOrderReceiptLineImport.LineNumber 

							if  (Item not entered
							and  LineNumberLookupRel.VendorItem = VendorItem)
								Item = LineNumberLookupRel.Item

							if  (VendorItem not entered
							and  LineNumberLookupRel.Item = Item)
								VendorItem = LineNumberLookupRel.VendorItem


						if  (!Interfaced)
							if (!ErrorOccurred)
								if (EnteredReceivedQuantity entered)
									if (PurchaseOrderReceiptLineRel exists)
										invoke Update PurchaseOrderReceiptLineRel
											invoked.EnteredReceivedQuantity 	+= EnteredReceivedQuantity
											invoked.CatchWeightQuantity			+= CatchWeightQuantity
									else
										invoke CreateFromInterface PurchaseOrderReceiptLine
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											fill in fields from this instance
											invoked.Company						= PurchaseOrderReceiptImportRel.Company
											invoked.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
											invoked.PurchaseOrderReceipt		= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											invoked.PurchaseOrderReceiptLine	= LocalPurchaseOrderLine
											invoked.PurchaseOrderLine 			= LocalPurchaseOrderLine
											invoked.ShipToLocation				= Location
											if (POReceiptInventoryDetailImportRel exists)
											    invoked.InterfaceHasDetails		= true
								else
									if (CancelBackorder)

									 	LocalPurchaseOrder = PurchaseOrderReceiptImportRel.PurchaseOrder	
								 			
										invoke Create POReceiptLineTemplate
											resume on error
												ErrorOccurred		= true
												LocalErrorMessage	= error message
											fill in fields from this instance

											invoked.Company						= PurchaseOrderReceiptImportRel.Company
											invoked.PurchaseOrder				= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt.PurchaseOrder
											invoked.PurchaseOrderReceipt		= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
											invoked.PurchaseOrderLine 			= LocalPurchaseOrderLine
											invoked.ShipToLocation				= Location
									else
										ErrorOccurred		= true
										LocalErrorMessage	= CancelRemainingMustBeYesIfQuantityIsZeroMsg
							
						

						if (!ErrorOccurred)
							Interfaced								= true
							
							if (POReceiptInventoryDetailImportRel exists)
								if (PurchaseOrderReceiptLineRel.IsDetailTracked)
									for each POReceiptInventoryDetailImportRel
										if (PurchaseOrderReceiptLineRel.IsBinTracked)
											if (each.Bin entered)
												LocalBin 	= each.Bin
											else
												LocalBin	= PurchaseOrderReceiptLineRel.Bin
											
										if (each.Lot entered and (PurchaseOrderReceiptLineRel.IsLotRequiredAtReceipt or PurchaseOrderReceiptLineRel.IsNonstockLotRequiredAtReceipt))
											LocalLot	= each.Lot
											
										if (each.Serial entered and (PurchaseOrderReceiptLineRel.IsSerialRequiredAtReceipt or PurchaseOrderReceiptLineRel.IsNonstockSerialRequiredAtReceipt))
											LocalSerial	= each.Serial						
											
										if (LocalBin entered or LocalLot entered or LocalSerial entered) 
											invoke Create PurchaseOrderTransactionDetail
												resume on error
													ErrorOccurred		= true
													LocalErrorMessage	= error message
												fill in fields from each
			
												invoked.PurchaseOrderReceipt											= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
												invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= "PO"
												invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
												invoked.SourceDocumentAlpha												= PurchaseOrderReceiptImportRel.NewPurchaseOrderReceipt
												invoked.PurchaseOrderTransactionDetail.LineNumber						= LocalPurchaseOrderLine
												invoked.PurchaseOrderTransactionDetail.TransactionSequence				= each.POReceiptInventoryDetailImport.Sequence
												invoked.Bin																= LocalBin
												invoked.Lot																= LocalLot
												invoked.Serial															= LocalSerial
												if (each.Quantity not entered
												and each.Serial entered)
													invoked.Quantity													= 1
												
											if (ErrorOccurred)
												end for each
											else
												invoke FastDelete each
										else 
											invoke FastDelete each
								else
									for each POReceiptInventoryDetailImportRel
										invoke FastDelete each
									
						if (ErrorOccurred)

							ReceiptError = true
							RecordInError = true
							ReceiptErrorMessage = LocalErrorMessage
							LocalFinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							LocalInterfaceResult			= PrmInterfaceRun
							if (LocalInterfaceResultRel.Status != 2)
								invoke Update LocalInterfaceResultRel
									invoked.Status        				= 2
						else
						if  (!LineAddOnCostsRel exists
						and  !POReceiptInventoryDetailImportRel exists)
							invoke FastDelete
						else
							Interfaced = true
	
