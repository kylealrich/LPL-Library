StatementPrintDetail is a BusinessClass
	owned by ar
	
	prefix is SPD
	
	Ontology
		symbolic key is StatementPrintDetail
	
	Patterns
        implements StaticJava
        disable AuditIndex
        	
	Persistent Fields
		InvoiceAccountGroup
		ChildNationalAccount 	  is Boolean
		ChildCompany			  is a ReceivableCompany
        ChildCustomer			  is a Customer
		InvoiceCompany		  	  is a ReceivableCompany
			disable surrogates
		InvoiceCustomer	     	  is a Customer
			disable surrogates
		TransactionNumber   
		ReceivableInvoiceType    	
			default label is "Type"
		PaymentType               is AlphaUpper size 1
            States
                NonArPayment          value is "N"
                    default label is "Non AR Payment"
                CustomerPayment       value is "P"
                    default label is "Customer Payment"
                BillOfExchangePayment value is "B"
                    default label is "Bill Of Exchange Payment"
        TransactionDate			  is an ExchangeDate		
		DueDate
        CustomerPurchaseOrder
        OriginalCurrency    	  is a  FromCurrency       
        TransactionAmount		  is an InternationalAmount
        OpenAmount				  is an InternationalAmount
 		Agings                    is an AgingX5InArcustomer
        	disable Auditing
        IncludeApplications				is AlphaUpper size 1
			States
                DoNotListApplications value is "N"
                ListApplications      value is "Y"
        PaymentSeq                is a Sequence     
        CurrentAmount			  is an InternationalAmount
        	
    Context Fields
    	StatementPrintHeader
      
	Field Rules


	Derived Fields
		
		DerivedInvoiceCustomer 	  is a DerivedField
			type is like Customer
			
			if (ChildCustomer entered)
				return ChildCustomer
			else
				return InvoiceAccountGroup.Customer
				
		DerivedInvoiceCompany	is a DerivedField
			type is like Company
			if (ChildCompany entered)
				return ChildCompany
			else
				return InvoiceAccountGroup.Company
		
	Relations
	
        StatementPrintHeaderRel
            one-to-one relation to StatementPrintHeader
            required
            Field Mapping uses symbolic key
                related.Company		= InvoiceAccountGroup.Company
                related.Customer 	= InvoiceAccountGroup.Customer
                
        ReceivableApplicationRel
        	one-to-many relation to ReceivableApplication
        	Field Mapping uses symbolic key
        		related.Company		= DerivedInvoiceCompany
        	Instance Selection
        		where (related.ReceivableApplication.TransType 	 = ReceivableInvoiceType
        		and	   related.ReceivableApplication.Invoice	 = TransactionNumber
        		and	   IncludeApplications.ListApplications)

	Sets
        ByCompanyCustomer
            duplicates
            Sort Order
            	InvoiceAccountGroup.Company
				InvoiceAccountGroup.Customer
				OriginalCurrency
				TransactionDate
				TransactionNumber
				
		ByNationalAccountCustomer
			duplicates
			Sort Order
				InvoiceAccountGroup.Company
				InvoiceAccountGroup.Customer
				ChildCompany
				ChildCustomer
				OriginalCurrency
				TransactionDate
				
    Actions
        Create is a Create Action
        	restricted
				
        Update is an Update Action
        	restricted

        Delete is a Delete Action
        		
        		
