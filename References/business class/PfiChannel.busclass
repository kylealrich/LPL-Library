PfiChannel is a BusinessClass
    owned by pfi
    prefix is PfiCH
    contains environment data
	disable data area copy
		preserve target data
    default label is "Channel"

    Ontology
        symbolic key is PfiChannel

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements CompoundDocument
            Document Components
                PfiReceiver set
        implements DynamicCreation






    Persistent Fields

        ChannelType     is a PfiChannelType
        Description     is a PfiDescription
        FileChannel     is a PfiFileChannel
        JMSChannel      is a PfiJMSChannel
        EventHubChannel is a PfiEventHubChannel
        IONChannel      is a PfiIONChannel
        UserChannel     is a PfiUserChannel

        IsEnabled     is Boolean
            default label is "Enabled"
        AutoReconnectMaxTries    is Numeric size 3              
        AutoReconnectTryInterval is Numeric size 6  
        AutoReconnectRunning is Boolean                         
        NotifyConnectLoss is Numeric size 1
            States
                None                     value is 0
                OnLoss                   value is 1
                OnError                  value is 2


        ConnectLossEmailTo is Text
        ConnectLossEmailCC is Text

    Derived Fields

		UpdateMessage is a DerivedField
            type is Alpha size 200
            return "Changes to an active channel will not take effect until the channel is deactivated and reactivated. Do you want to continue?"
                
        ReceiverCount is a DerivedField
            type is Decimal 4
            default label is "Receivers"
            return instance count of PfiReceiver set

        ActiveReceiverCount is a DerivedField
            type is Decimal 4
            default label is "ActiveReceivers"
            return instance count of ActiveReceiverRel

        IsActive is a DerivedField
            type is Boolean
            default label is "Active"
            if (ActiveReceiverCount > 0)
                return true
            else
                return false

        MonitoringIssuesExist is a DerivedField
            type is Boolean
            if (ActiveWithIssueReceiverRel exists)
                return true
            else
                return false

        MonitoringChannelIssuesExist is a DerivedField
            type is Boolean
            if (ActiveWithChannelIssueReceiverRel exists)
                return true
            else
                return false

        PrivatekeyText is a NativeField
            type is Text
            default label is "PrivateKeyText"

        HostKeyText is a NativeField
            type is Text

        SourceDirectoryWildCardCheck is a NativeField
            type is Boolean

        ErrorDirectoryWildCardCheck is a NativeField
            type is Boolean

        ProcessDirectoryWildCardCheck is a NativeField
            type is Boolean

        ApplyEncryption is a NativeField
            type is Boolean

        IsMultiTenant is a NativeField
            type is Boolean
            default label is "MultiTenant"
            
    Relations

        ActiveReceiverRel is a PfiReceiver set
                Instance Selection
                    where ((related.Status.Active or related.Status.ActiveWithIssue) and related.ReceiverType = ChannelType)

        ActiveWithIssueReceiverRel is a PfiReceiver set
                Instance Selection
                    where (related.Status.ActiveWithIssue and related.ReceiverType = ChannelType)

        ActiveWithChannelIssueReceiverRel is a PfiReceiver set
                Instance Selection
                    where (related.Status.ActiveWithIssue and related.LastErrorType.Channel and related.ReceiverType = ChannelType)

    Sets

        ByChannelTypeChannel
            sql name is PfiCH01
            not indexed
            Sort Order
                ChannelType
                PfiChannel

    Rule Blocks

        ValidateFields

            if (PfiChannel not entered)
                constraint (false)
                    "Field_Channel_Name_is_required"

            if (ChannelType.FileChannel)
                include ValidateFileChannelFields
            else
            if (ChannelType.JMSChannel)
                include ValidateJMSChannelFields
            else
            if (ChannelType.IONChannel)
                include ValidateIONChannelFields
            else
            if (ChannelType.UserChannel)
                include ValidateUserChannelFields

        ValidateFileChannelFields

            if (FileChannel.Directory = blank)
                constraint (false)
                    "SourceFileDirectoryIsRequired"

            if (FileChannel.ErrorFileDirectory = blank)
                constraint (false)
                    "ErrorFileDirectoryIsRequired"

            constraint (FileChannel.ErrorFileDirectory != FileChannel.Directory)
                "ErrorFileDirectoryMustDifferFromSourceDirectory"

            constraint (FileChannel.ProcessedFileDirectory != FileChannel.Directory)
                "In-\ProgressFileDirectoryMustDifferFromSourceDirectory"

            if (FileChannel.FileChannelType = blank)
                constraint (false)
                    "FileChannelTypeIsRequired"

            if (FileChannel.FileAccessCheckTime = blank)
                constraint (false)
                    "FileScanIntervalTimeIsRequired"

            if (FileChannel.FileMatchCaseSensitivity = blank)
                constraint (false)
                    "FileMatchCaseSensitivityIsRequired"

        ValidateJMSChannelFields

            if (JMSChannel.JNDIConnectionFactoryName = blank)
                constraint (false)
                    "JNDIConnectionFactoryNameIsRequired"

        ValidateIONChannelFields

            if (IONChannel.JDBCDriver = blank)
                constraint (false)
                    "JDBCDriverIsRequired"

            if (IONChannel.DatabaseURL = blank)
                constraint (false)
                    "DatabaseURLIsRequired"

            if (IONChannel.User = blank)
                constraint (false)
                    "UserIsRequired"

        ValidateUserChannelFields

            if (UserChannel.ActionType = blank)
                constraint (false)
                    "ActionTypeIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.IncomingMailServer = blank)
                    constraint (false)
                        "IncomingMailServerIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.IncomingUserName = blank)
                    constraint (false)
                        "IncomingUserNameIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.IncomingTLS.Enabled and UserChannel.IncomingPassword not entered)
                    constraint (false)
                        "IncomingPasswordIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.IncomingTLS = blank)
                    constraint (false)
                        "IncomingTLSIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.OutgoingMailServer = blank)
                    constraint (false)
                        "OutgoingMailServerIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.OutgoingUserName = blank)
                    constraint (false)
                        "OutgoingUserNameIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.OutgoingTLS.Enabled and UserChannel.OutgoingPassword not entered)
                    constraint (false)
                        "OutgoingPasswordIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.OutgoingTLS = blank)
                    constraint (false)
                        "OutgoingTLSIsRequired"

            if (UserChannel.ActionType.Email)
                if (UserChannel.EmailScanIntervalTime = blank)
                    constraint (false)
                        "ScanIntervalTimeIsRequired"
                constraint (UserChannel.EmailScanIntervalTime >= 1)
                    "ScanIntervalTimeMustBeGreaterThanOrEqualToOne"

            if (UserChannel.ActionType.Email)
                if (UserChannel.SendHelpReplies = blank)
                    constraint (false)
                        "SendHelpRepliesIsRequired"

    Field Rules

        ChannelType
            required
            cannot be changed

        IsEnabled
            default to true
            if (IsActive)
                cannot be changed
                    "CannotDisableChannelWhileActive"

        AutoReconnectMaxTries       
            if (AutoReconnectTryInterval > 0)
                required
                    "AutoReconnectMaxTriesRequiredWithAutoReconnectTryInterval"

        AutoReconnectTryInterval    
            if (AutoReconnectMaxTries > 0)
                required
                    "AutoReconnectTryIntervalRequiredWithAutoReconnectMaxTries"

    SubType ChannelType.FileChannel Field Rules
        FileChannel
            required

    SubType ChannelType.JMSChannel Field Rules
        JMSChannel
            required

    SubType ChannelType.EventHubChannel Field Rules
        EventHubChannel
            required

    SubType ChannelType.IONChannel Field Rules
        IONChannel
            required

    SubType ChannelType.UserChannel Field Rules
        UserChannel
            required

    Actions

        Create is a Create Action
            restricted

            Entrance Rules
                include ValidateFields

        Deactivate is an Instance Action
            Action Rules
                constraint (IsActive)
                    "ChannelIsNotActive"
            manual update

        ActivateAllReceivers is an Instance Action
            default label is "Activate"
            Action Rules
                constraint (IsEnabled)
                    "ChannelIsDisabled"
            manual update

        ResetAllReceiverStatuses is an Instance Action
            default label is "Reset"
            manual update

        ScanNow is an Instance Action
            Action Rules
                invoke Deactivate
                invoke ActivateAllReceivers

        TestConnectionWithTraceLog is an Instance Action
        
        TestConnection is an Instance Action
        
        Delete is a Delete Action
            Action Rules
                constraint (!IsActive)
                    "CannotDeleteActiveChannel"





    StateCycles

        ChannelLifeCycle is a StateCycle
            state field is ChannelType

            FileChannel is a State

                Create is a Create Action
                    default label is "CreateFileChannel"

                    Entrance Rules
                        include ValidateFields

                    Action Rules
                        constraint (!SourceDirectoryWildCardCheck)
                            "SourceDirectoryCannotContainWildCard"

                        constraint (!ProcessDirectoryWildCardCheck)
                            "In-\ProgressDirectoryCannotContainWildCard"

                        constraint (!ErrorDirectoryWildCardCheck)
                            "ErrorDirectoryCannotContainWildCard"

                        constraint (ApplyEncryption)
                            "EncryptionNotApplied"
                            
                RestrictedUpdate is an Update Action
                    restricted

                Update is an Update Action
					
                    Entrance Rules
                        include ValidateFields

                    Action Rules
                    	if (IsActive)
                    		confirmation required
								"<UpdateMessage>"
                        constraint (!SourceDirectoryWildCardCheck)
                            "SourceDirectoryCannotContainWildCard"

                        constraint (!ProcessDirectoryWildCardCheck)
                            "In-\ProgressDirectoryCannotContainWildCard"

                        constraint (!ErrorDirectoryWildCardCheck)
                            "ErrorDirectoryCannotContainWildCard"

                        constraint (ApplyEncryption)
                            "EncryptionNotApplied"
                            
            JMSChannel is a State

                Create is a Create Action
                    default label is "CreateJMSChannel"

                    Entrance Rules
                        include ValidateFields

                    Exit Rules
                        if (!NotifyConnectLoss.None)
                            constraint (JMSChannel.Configuration.MailServer != blank)
                                "MailServerIsRequired"

                RestrictedUpdate is an Update Action
                    restricted

                Update is an Update Action

                    Entrance Rules
                        include ValidateFields

                    Action Rules
                        if (IsActive)
                    		confirmation required
								"<UpdateMessage>"
                        if (!NotifyConnectLoss.None)
                            constraint (JMSChannel.Configuration.MailServer != blank)
                                "MailServerIsRequired"

            EventHubChannel is a State

                Create is a Create Action
                    default label is "CreateEventHubChannel"

                    Entrance Rules
                        include ValidateFields

                RestrictedUpdate is an Update Action
                    restricted

                Update is an Update Action

                    Entrance Rules
                        include ValidateFields

                    Action Rules
                        if (IsActive)
                    		confirmation required
								"<UpdateMessage>"

            IONChannel is a State

                Create is a Create Action
                    default label is "CreateIONChannel"

                    Entrance Rules
                        include ValidateFields

                RestrictedUpdate is an Update Action
                    restricted

                Update is an Update Action

                    Entrance Rules
                        include ValidateFields

                    Action Rules
                        if (IsActive)
                    		confirmation required
								"<UpdateMessage>"

                RemoveDuplicateReceivers is an Instance Action
                    restricted
                FindDuplicateReceivers is an Instance Action

            UserChannel is a State

                Create is a Create Action
                    default label is "CreateUserChannel"

                    Entrance Rules
                        include ValidateFields

                RestrictedUpdate is an Update Action
                    restricted

                Update is an Update Action

                    Entrance Rules
                        include ValidateFields

                    Action Rules
                        if (IsActive)
                    		confirmation required
								"<UpdateMessage>"
