PayablesDistributionCode is a BusinessClass
    owned by ap
    prefix is HDR
    classic name is APDISTHDR

    Ontology
        symbolic key is PayablesDistributionCode
            classic set name is HDRSET1
            classic name is DIST-CODE

    Patterns
        implements StaticJava
        disable AuditIndex
		implements ContextualParent
    
    Attach Rules
		if (parentcontext.name != "PayablesDistributionCodeDetail")
			constraint (PayablesDistributionCodeDetail set exists)
				"DistributionCodeDetailDoesNotExist" 
			if (TotalPercent entered)
				constraint (TotalPercent = OneHundredPercent)
					"DistributionCodeDetailDoesNotEqual100Percent;_\Percent_\Total=<TotalPercent>"
		
		constraint (Active)
			"DistributionCodeIsNotActive"		
					
	Derived Fields
		SumDetailDistributionPercent is a DerivedField
			type is Percent size 6.3
			restricted
			return sum PayablesDistributionCodeDetail set.DistributionPercent
    
    	OneHundredPercent	is a DerivedField
    		type is Percent size 6.3
    		restricted
    		return 100.000%

		CreateMessage is a LabelField
			restricted
			"CreateDistributionCode"
		
		FormLabel is a LabelField
			restricted
			"DistributionCode:_"

		RepresentativeText is a StringField
			type is Text
			default label is "DistributionCode"
			PayablesDistributionCode " - " Description
		
		DerivedDistributionCodeTitle	is a DerivedField
			type is Alpha size up to 132
			default label is "DistributionCode"
			return RepresentativeText

		DerivedFormTitle is a DerivedField
			type is MessageField
			restricted
			if (RecordExists)
				return FormLabel + DerivedDistributionCodeTitle
			else
				return CreateMessage	
    
    Persistent Fields

        Description
        AccountEdit             is AlphaUpper size 1
            classic name is ACCT-EDIT-FLAG
            States
                CodeAndInvoiceEntry value is "C"
                InvoiceEntry        value is "I"
                Contracts           value is "S"
        RequireAccountingUnit   is Boolean
            classic name is ACCT-UNIT-FLAG
        RequireAccount          is Boolean
            classic name is ACCOUNT-FLAG
		TotalPercent			is Percent size 6.3
			protected		
		TotalDistributionAmount is an InternationalAmount
			protected		
			classic name is TOTAL-TRAN-AMT
		TotalUnit               is Decimal size 15.2
			protected		
        Active
    
    Field Rules
		TotalPercent
			constraint (TotalPercent <= OneHundredPercent)
				"PercentTotalCannotBeGreaterThan_100;\Percent_\Total=<TotalPercent>"  
		AccountEdit
			required
        
    
    Conditions
		RecordExists
			restricted
			when (PayablesDistributionCode exists)

        IsPercentDistributionCode
        	restricted
            classic name is PERCENT-DIST
            when (TotalPercent entered
            and   TotalDistributionAmount not entered)

        IsServiceType
        	restricted
            when (AccountEdit.Contracts)

		DisplayPercentWarning
			restricted
			when (IsPercentDistributionCode
			and   TotalPercent != OneHundredPercent)

    Relations

        PayablesDistributionCodeDetailRel
            classic name is APDISTCODE
            one-to-many relation to PayablesDistributionCodeDetail
            delete cascades
            Field Mapping uses symbolic key
				related.VendorGroup					= VendorGroup
                related.PayablesDistributionCode 	= PayablesDistributionCode
		
		ContractLinesRel
			one-to-many relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup					= VendorGroup
			Instance Selection
				where (related.PayablesDistributionCode	= PayablesDistributionCode)

	Rule Blocks		
										
	Actions
		Create is a Create Action
			Action Rules
				RequireAccountingUnit = true
				RequireAccount = true
				
		Update is an Update Action
			Action Rules

		Delete is a Delete Action
			Entrance Rules
				constraint (ContractLinesRel not exists)
					"CannotDelete;DistributionCodeIsUsedOnAContract"		
							
		UpdateTotals is an Instance Action
			restricted
			Parameters
				OldDistributionAmount		is an InternationalAmount
				NewDistributionAmount		is an InternationalAmount
				OldDistributionPercent		is Percent size 6.3
				NewDistributionPercent		is Percent size 6.3
				OldDistributionUnitAmount	is an InternationalAmount
				NewDistributionUnitAmount	is an InternationalAmount
			Action Rules
				TotalDistributionAmount = TotalDistributionAmount - OldDistributionAmount + NewDistributionAmount

				TotalPercent = TotalPercent - OldDistributionPercent + NewDistributionPercent
				if (AccountEdit != "S")
					constraint (TotalPercent <= OneHundredPercent)
						"PercentTotalCannotBeGreaterThan_100;_\PercentTotal=<TotalPercent>"        









				TotalUnit = TotalUnit - OldDistributionUnitAmount + NewDistributionUnitAmount
			
		SetFlags is an Instance Action
			restricted
			Action Rules
				if (PayablesDistributionCodeDetail set exists)
					if (any PayablesDistributionCodeDetail set.DistributionAccount.AccountingUnit not entered)
						RequireAccountingUnit = true
					else
						RequireAccountingUnit = false
					if (any PayablesDistributionCodeDetail set.DistributionAccount.GeneralLedgerChartAccount not entered)
						RequireAccount = true
					else
						RequireAccount = false
				else
					RequireAccountingUnit 	= true
					RequireAccount 	= true
					
    Sets

        Set2
            indexed
            Sort Order
                PayablesDistributionCode
				VendorGroup

