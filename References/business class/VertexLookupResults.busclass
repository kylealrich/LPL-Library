VertexLookupResults is a BusinessClass
	owned by tx	 
	prefix is VtxRs

	Ontology
		symbolic key is VertexLookupResults

	Patterns
        implements StaticJava
        disable AuditIndex
    	disable Auditing		
        disable EffectiveDated
        disable UniqueID
		disable AsOfDateProcessing
				
	Persistent Fields
        RelatedObjectReference			is BusinessObjectReference 
		LookupTaxAreaId 				is like TaxCode
		ReturnedHighesTaxAreaId 		is like TaxCode
		LookupPostalAddress				is like PostalAddressV2
		LookupProcessGroup				is Alpha 50					 
		LookupBusinessGroup				is a BusinessGroup
			disable surrogates		
		LookupTaxEntity					is a TaxEntity					 
			context of VertexConfig
		LogSearchName					is a SearchName			 
			default label is "SearchName"

		LookupVendorGroup				is a VendorGroup
			default label is "VendorGroup"
		LookupVendor					is a Vendor
			default label is "Vendor"
		LookupVendorLocation			is a VendorLocation
			default label is "VendorLocation"
		LookupPayablesCompany			is a PayablesCompany		
			default label is "PayablesCompany"
		LookupPayablesProcessLevel		is a PayablesProcessLevel	
			default label is "PayablesProcessLevel"

		LookupCustomerGroup				is a CustomerGroup
			default label is "CustomerGroup"
		LookupARCompany					is a ReceivableCompany
			default label is "ReceivablesCompany"
		LookupCustomer					is a Customer
			default label is "Customer"
		LookupCompanyCustomer			is a CompanyCustomer
			default label is "CompanyCustomer"
		LookupCustomerShipToLoc			is a CustomerShipTo
			default label is "ShipTo"

		LookupBillingCompany			is a BillingCompany
			default label is "BillingCompany"
		LookupBillingProcessLevel		is a BillingProcessLevel
			default label is "BillingProcessLevel"

		LookupInventoryCompany			is a InventoryCompany
			default label is "InventoryCompany"
		LookupInventoryLocation			is a InventoryLocation
			default label is "InventoryLocation"
		LookupRequestingLocation		is a RequestingLocation	
			default label is "RequestingLocation"

		NumberOfTaxAreaIdsReturned		is Numeric 5
		AddressesReturned				is Numeric 5	 
		JursdictionsReturned			is Numeric 5	 
		StatusesReturned				is Numeric 5	 
		AddressCleansReturned 			is Numeric 5	 
		LookupDescription				is Alpha 100
		RunDate							is Date
		ModifiedDate					is TimeStamp	 
        Status                  		is Numeric size 1
            States
                NotProcessed 		value is 0
                Processed 			value is 1
                NoChangeRequired	value is 2
		WebServiceError 				is Boolean
		WebServiceErrorMessage 			is Text
		ErrorNumber						is Numeric 3
		ErrorMessage					is Alpha 100
		DisplayOption	 				is Alpha size 1
            States
            	All					    value is blank
              	Vendors           		value is "V"
              	Customers      			value is "C"
              	InventoryLocations  	value is "I"
              	BillingProcessLevels	value is "B"
              	PayablesProcessLevels	value is "P"	
              	RequestingLocations		value is "R"	

		
	Context Fields
		RunDateRange				is a DateRange

	Transient Fields
	Local Fields	
	Derived Fields
		DerivedLookupKey is a DerivedField
			type is Alpha 100
			restricted
			return "VertexLookupResults:" + CurrentTimestamp

		CurrentTimestamp is a DerivedField
			type is Alpha 20
			restricted
			return current timestamp
						
		DerivedMessage is a DerivedField
			type is MessageField
			if (WebServiceError)
				return VertexWebserviceError
			else
			if (ErrorNumber entered)
				return ErrorMessage
			else					
			if (NumberOfTaxAreaIdsReturned > 1)
				return MultipleTaxAreaIdsMsg
			else
			if (LookupDescription entered)
				return LookupDescription			

			return blank
				
		DerivedSystemValues is a DerivedField
			type is MessageField
			restricted
			if (LookupVendor entered
			and LookupVendorLocation entered)
				return DisplayVendorLocationText
			else				
			if (LookupVendor entered)
				return DisplayVendorText
			else	
			if (LookupCustomer entered)
				return DisplayCustomerText
			else
			if (LookupInventoryLocation entered)
				return DisplayInventoryLocationText		 
			else
			if (LookupRequestingLocation entered)	
				return DisplayRequestingLocationText
			else			
			if (LookupBillingCompany entered)
				return DisplayBillingText
			else
			if (LookupPayablesCompany entered)	
				return DisplayPayablesCompanyText
			else
				return InvalidLookupMsg

		DisplayVendorLocationText is a MessageField

			restricted
			"<LookupVendor.VendorName>,Location:<LookupVendorLocation>"
			
		DisplayVendorText is a MessageField
			restricted
			"<LookupVendor.VendorName>"

		DisplayCustomerText is a MessageField
			restricted
			"Customer:<LookupCustomer>,ShipTo:<LookupCustomerShipToLoc>"
			
		DisplayInventoryLocationText is a MessageField
			restricted
			"InventoryCompany:<LookupInventoryCompany>,Location:<LookupInventoryLocation>"

		DisplayRequestingLocationText is a MessageField	
			restricted
			"InventoryCompany:<LookupInventoryCompany>,Location:<LookupRequestingLocation>"

		DisplayBillingText is a MessageField
			restricted
			"BillingCompany:<LookupBillingCompany>,ProcessLevel:<LookupBillingProcessLevel>"

		DisplayPayablesCompanyText is a MessageField	
			restricted
			"PayablesCompany:<LookupPayablesCompany>,ProcessLevel:<LookupPayablesProcessLevel>"

		InvalidLookupMsg is a MessageField
			restricted
			"Error,InvalidDataForLookup..."

		MultipleTaxAreaIdsMsg is a MessageField
			restricted
			"MultipleTaxAreaIdsExist.."

		VertexWebserviceError is a MessageField
			restricted
			"VertexWebserviceErrorEncountered;InvalidDataForLookup"

	Conditions
		WithinRunDateRange
			restricted
			when (RunDateRange not entered
			or	 (RunDateRange entered
			and	  RunDate within RunDateRange))	

		VendorEntered
			restricted
			when (LookupVendor entered
			and LookupVendorLocation not entered)

		VendorAndLocationEntered
			restricted
			when (LookupVendor entered
			and   LookupVendorLocation entered)

		CustomerEntered
			restricted
			when (LookupCustomer entered
			and   LookupCustomerShipToLoc not entered)

		CustomerAndShipToEntered
			restricted
			when (LookupCustomer entered
			and   LookupCustomerShipToLoc entered)

		CustomerOrShipToEntered
			restricted
			when (LookupCustomer entered
			or    LookupCustomerShipToLoc entered)
			
		InventoryLocationEntered
			restricted
			when (LookupInventoryLocation entered)

		RequestingLocationEntered	
			restricted
			when (LookupRequestingLocation entered)

		BillingProcessLevelEntered
			restricted
			when (LookupBillingProcessLevel entered)

		PayablesProcessLevelEntered	
			restricted
			when (LookupPayablesProcessLevel entered)

	Field Rules
		RunDate
			default to current corporate date	
		DisplayOption
			if (DisplayOption not entered)
				if (LookupVendor entered)
					DisplayOption = "V"
				else
				if (LookupCustomer entered)
					DisplayOption = "C"
				else
				if (LookupInventoryLocation entered)
					DisplayOption = "I"
				else
				if (LookupBillingProcessLevel entered)
					DisplayOption = "B"
				else
				if (LookupPayablesProcessLevel entered)	
					DisplayOption = "P"
				else
				if (LookupRequestingLocation entered)	
					DisplayOption = "R"

	Relations
		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup 					= LookupVendorGroup
				related.Vendor  						= LookupVendor	

        VendorLocationRel
        	one-to-one relation to VendorLocation
        	Field Mapping uses symbolic key
        		related.VendorGroup						= LookupVendorGroup
        		related.Vendor							= LookupVendor
        		related.VendorLocation					= LookupVendorLocation

		CustomerRel
			one-to-one relation to Customer			
			Field Mapping uses symbolic key
				related.CustomerGroup 					= VertexConfig
				related.Customer						= LookupCustomer	 

        CustomerShipToRel
            one-to-one relation to CustomerShipTo	 
            Field Mapping uses symbolic key
                related.Company        					= LookupARCompany			 
				related.Customer     					= LookupCustomer			 
				related.CustomerShipTo   				= LookupCustomerShipToLoc	

		InventoryLocationRel
			one-to-one relation to InventoryLocation			
			Field Mapping uses symbolic key
				related.Company 						= LookupInventoryCompany	 
				related.InventoryLocation   			= LookupInventoryLocation	 

		RequestingLocationRel
			one-to-one relation to RequestingLocation			
			Field Mapping uses symbolic key
				related.Company 						= LookupInventoryCompany	 
				related.RequestingLocation   			= LookupRequestingLocation

        BillingProcessLevelRel
            one-to-one relation to BillingProcessLevel	 
            Field Mapping uses symbolic key
                related.Company             			= LookupBillingCompany	 
                related.BillingProcessLevel 			= LookupBillingProcessLevel

        PayablesProcessLevelRel	
            one-to-one relation to PayablesProcessLevel	 
            Field Mapping uses symbolic key
                related.Company             			= LookupPayablesCompany	 
                related.PayablesProcessLevel 			= LookupPayablesProcessLevel

	Sets	
		ByRelatedObjectReference
			Sort Order
				VertexConfig
				RelatedObjectReference
				VertexLookupResults.LookupKey
				VertexLookupResults.Sequence	
	
	Rule Blocks
	Actions
		Create is a Create Action
			Action Rules
				if (VertexLookupResults.LookupKey not entered)			
					VertexLookupResults.LookupKey = DerivedLookupKey
					VertexLookupResults.Sequence  = 1
							
		Update is an Update Action
			Action Rules
				ModifiedDate = current timestamp
		Delete is a Delete Action
		Purge is a Purge Action
			restricted
			
		FixDisplayOptions is an Instance Action	 
			restricted			 
			Parameters
			Parameter Rules
			Action Rules
				if (DisplayOption not entered)
					if (LookupVendor entered)
						DisplayOption = "V"
					else
					if (LookupCustomer entered)
						DisplayOption = "C"
					else
					if (LookupInventoryLocation entered)
						DisplayOption = "I"
					else
					if (LookupBillingProcessLevel entered)
						DisplayOption = "B"
					else
					if (LookupPayablesProcessLevel entered)	
						DisplayOption = "P"
					else
					if (LookupRequestingLocation entered)	
						DisplayOption = "R"

		PurgeVertexLookupResults is a Set Action		
			completion message is "<CompletionMessage>"
			run in foreground	 

			Parameters 
				PrmFinEntGroup		is a VertexConfig
					default label is "VertexConfig"
				PrmLookupProcessGroup		is Alpha 50	 	
					default label is "ProcessGroup" 					



				PrmVendorGroup	is a VendorGroup	 
					default label is "VendorGroup"
				PrmLookupCustomerGroup			is a CustomerGroup
					default label is "CustomerGroup"
				PrmARCompany		is a GeneralLedgerCompany	
					default label is "ReceivablesCompany"	
				PrmLookupBillingCompany			is a BillingCompany		
					default label is "BillingCompany"		 
				PrmLookupPayablesCompany		is a PayablesCompany		
					default label is "PayablesCompany"
				PrmLookupInventoryCompany		is a InventoryCompany	
					default label is "InventoryCompany"					
				PrmRunDate		is Date
					default label is "RunDateIsLessThan"
				PrmCreatedDateYear	is a Year
					default label is "RunYear"
		        PrmStatus                  	is Alpha size 1
		            States
		                AnyStatus           value is "9"
		                NotProcessed 		value is "0"
		                Processed 			value is "1"
		                NoChangeRequired	value is "2"
					default label is "Status"										
			Parameter Rules
				PrmFinEntGroup
					initial value is actor.context.FinanceEnterpriseGroup	
					required	




				PrmStatus
					initial value is 9
			Local Fields
				CompletionMessage	is Alpha 150
				RecordCount			is Numeric 10

			Instance Selection			
				where (VertexConfig = PrmFinEntGroup			 					 


			    and   (PrmLookupProcessGroup = LookupProcessGroup							 
			    or     PrmLookupProcessGroup not entered)												 
				and   (LookupVendorGroup = PrmVendorGroup						 	
				or     PrmVendorGroup not entered)						 	
			    and   (PrmARCompany = LookupARCompany					 	
			    or     PrmARCompany not entered)							 	
			    and   (PrmLookupBillingCompany = LookupBillingCompany							 
			    or     PrmLookupBillingCompany not entered)	
			    and   (PrmLookupPayablesCompany = LookupPayablesCompany
			    or     PrmLookupPayablesCompany not entered)	
			    and   (PrmLookupCustomerGroup = LookupCustomerGroup
			    or     PrmLookupCustomerGroup not entered)	
			    and   (PrmLookupInventoryCompany = LookupInventoryCompany							 
			    or     PrmLookupInventoryCompany not entered)
			    and   (PrmStatus = Status	
			    or     PrmStatus.AnyStatus)
				and   (RunDate <= PrmRunDate						 
				or     PrmRunDate not entered)				    			 			    
			    and   (PrmCreatedDateYear = RunDate year     		 	
			    or     PrmCreatedDateYear not entered))					 				 
			Sort Order

			Accumulators



			Action Rules								
				Empty Set Rules
					CompletionMessage = "PurgeProcessComplete;NoRecordsFoundToPurge"
				
				Set Rules
					Entrance Rules

					Exit Rules
						CompletionMessage = "PurgeProcessComplete:<RecordCount>...RecordsProcessed"
						
				Instance Rules

					RecordCount +=1
					invoke Purge



																						
