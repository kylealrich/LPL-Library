SourcingEventSupplierQuestion is a BusinessClass 
    owned by ss
    prefix is EVSQ

    Ontology
    	symbolic key is SourcingEventSupplierQuestion
    	   			    			
    Patterns

    Persistent Fields
		QuestionText									is Text
			sql name is SEQUE
			text searchable
   		ResponseType					is Numeric size 1
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
   				List 				value is 4 
   				YesNo 				value is 5
   				YesNoText			value is 6 
   		Attachment
   		DisplayOrder
   		Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion								is like Question
   		CMQuestion										is Boolean
		SourcingEventQuestion
   		QuestionWeighting				
   		LastQuestionValueDisplayOrder					is Numeric 6
   			disable Auditing
   		LastQuestionAnswerDisplayOrder					is Numeric 6
   			disable Auditing
		
	Transient Fields
		NewDisplayOrder				is a DisplayOrder
		CreateFromRepository		is Boolean

	Local Fields
		LocalConditionalQuestion	is like Question
		LocalListValue				is like SourcingEventQuestionValue
		LocalYesOrNo				is Numeric 1 
           	States
           		Yes	value is 1
           		No	value is 2 
		FromRepository				is Boolean 
		FromConditional				is Boolean		
		LocalFromConditional		is Boolean
		LocalQuestion				is like Question

   	Derived Fields
   		IsQuestionRequired is a ConditionalField
   			type is Alpha 2
   			if (!QuestionRel.ResponseRequired)
   				blank
			else
				"*"

   		AnswerMessage is a MessageField
   			restricted
   			"Answer"

   		RequiredQuestion is a MessageField
   			"Required"
   		
		DerivedQuestionText is a DerivedField
			type is Alpha 250
			return QuestionText

   		AnswerTextWithRequired is a StringField
 			type is Text
   			restricted
   			IsQuestionRequired 
   			" "
   			AnswerMessage

 		IsAttachmentRequired is a ConditionalField
 			type is Alpha 2
   			restricted
   			if (!QuestionRel.AlwaysRequireResponseAttachment)
   				blank
			else
				"*"
	
		AttachDocumentMessage is a MessageField
   			restricted
			"AttachDocument"

   		AttachmentTextWithRequired is a StringField
 			type is Text
   			restricted
   			IsAttachmentRequired
   			" "
   			AttachDocumentMessage

   	Field Groups
   			
   	Conditions
		
		IsYesNo
   			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)
		
		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)
		
		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)
		
		HasListValues
   			restricted
			when (SourcingEventQuestionValueRel exists)
		
		EligibleForCM
   			restricted
			when (SourcingEvent.ContractOutputExists
			or    SourcingEvent.ContractOutput)
			
		HasAttachment
   			restricted
			when (Attachment entered)
		
		QuestionEntered
   			restricted
			when (Question entered)
			
		RequiredQuestionNoAnswer
			restricted
			when ((QuestionRel.ResponseRequired
			or     SourcingEventQuestionRel.ResponseRequired)
			and   !QuestionIsAnswered)

		RequiredQuestionNoAnswerForSupplier
			restricted
			when ((QuestionRel.ResponseRequired
			or     SourcingEventQuestionRel.ResponseRequired)
			and   !QuestionIsAnsweredForSupplier)

		RequiredQuestionAdHoc
			restricted
			when (SourcingEventQuestionRel.ResponseRequired)

		ResponseRequired
            when (Question.ResponseRules.ResponseRequiredAndAttachmentRequired
            or    Question.ResponseRules.ResponseRequired
            or    Question.YesNoResponseRules.ResponseRequired
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
		QuestionIsAnswered
			restricted
			when (SESupplierQuestionResponseRel exists)

		QuestionIsAnsweredForSupplier
			restricted
			when (SESupplierQuestionResponseForSupplierRel exists)

		DisplayAnswerRequiredText 
			restricted
			when (RequiredQuestionNoAnswerForSupplier
			or    MayRequireAttachmentNoAttachment)

		MayRequireAttachmentNoAttachment 
			restricted 
			when (QuestionRel.MayRequireResponseAttachment
			and  !AnswerAttachmentExists)

		MayRequireAttachmentForEventQuestion 
			restricted 
			when (SourcingEventQuestionRel.MayRequireResponseAttachment
			and   Question = 0)

		HasAnySourcingEventConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		RepositoryQuestion 
			restricted 
			when (Question entered)

		RepositoryHasConditionalQuestions	
			restricted 
			when (RepositoryConditionalQuestionRel exists)

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)
		
		HasDirectConditionalQuestions 
			when (ConditionalQuestionsDirectRel exists)
		
		AnswerAttachmentExists
			restricted
			when (SourcingEventQuestionAnswerAttachmentRel exists)

		AnyQuestionAnswers
   			restricted
			when (AnyQuestionAnswersRel exists)
			
		AllowResponseAttachment
			restricted
			when (QuestionRel.AllowResponseAttachment)

		AllowResponseForEventQuestionAttachment 
			restricted 
			when (SourcingEventQuestionRel.AllowResponseAttachment
			and   Question = 0)

        AlwaysRequireResponseAttachment
   			restricted
            when (Question.ResponseRules.ResponseRequiredAndAttachmentRequired
            or    Question.YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    Question.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
            
	Relations
		AnyQuestionAnswersRel is a SESupplierQuestionResponse set

		QuestionResponseAttachmentsRel 
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventQuestion			= SourcingEventSupplierQuestion
			Instance Selection
				where (related.HasQuestionAnswerAttachment)
				
		SourcingEventQuestionValueRel 
			one-to-many relation to SourcingEventQuestionValue
			Field Mapping uses symbolic key
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventQuestion			= Question
				
		SESupplierQuestionResponseRel
			one-to-many relation to SESupplierQuestionResponse
			Field Mapping uses ByQuestion
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion	= SourcingEventSupplierQuestion

		SESupplierQuestionResponseForSupplierRel
			one-to-many relation to SESupplierQuestionResponse
			Field Mapping uses ByQuestion
				related.Company							= Company
				related.SourcingEvent					= SourcingEvent
				related.SourcingEventSupplierQuestion	= SourcingEventSupplierQuestion
			Instance Selection
				where (related.NotifiedSupplier			= NotifiedSupplier)

		SourcingEventQuestionRel 
			one-to-one relation to SourcingEventQuestion 
			Field Mapping uses symbolic key
				related.Company					= Company 
				related.SourcingEvent			= SourcingEvent 
				related.SourcingEventQuestion	= SourcingEventSupplierQuestion.SourcingEventQuestion

		SameRepositoryQuestionRel 
			one-to-many relation to SourcingEventQuestion 
			Field Mapping uses ByQuestion
				related.Company					= Company 
				related.SourcingEvent			= SourcingEvent 
				related.Question				= LocalConditionalQuestion

		SourcingEventQuestionAnswerAttachmentRel is a ContractQuestionAnswer set
			Instance Selection
				where (related.Attachment entered)

		CreatedFromQuestionRel 
			one-to-one relation to SourcingEventQuestion 
			Field Mapping uses symbolic key 
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent 
				related.SourcingEventQuestion				= CreatedFromQuestion

		ConditionalQuestionsDirectRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		RepositoryConditionalQuestionRel    
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 

		QuestionRel 
			one-to-one relation to Question 
			Field Mapping uses symbolic key
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question

		RepositoryListValuesRel
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key 
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= Question 
		
		SourcingEventQuestionValuesRel
			one-to-many relation to SourcingEventQuestionValue 
			Field Mapping uses symbolic key 
				related.Company					= Company
				related.SourcingEvent			= SourcingEvent
				related.SourcingEventQuestion	= SourcingEventSupplierQuestion.SourcingEventQuestion

		QuestionListValueRel 
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key
				related.ProcurementGroup		= Company.SourcingGroup 
				related.Question				= LocalQuestion

		SubmittedQuestionResponsesRel
			one-to-many relation to SourcingEventQuestionResponse
			Field Mapping uses ByQuestion
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
				related.SourcingEventQuestion   = SourcingEventQuestion
			Instance Selection
				where (related.SourcingEventResponse.Status.Submitted)
				
		SubmittedResponsesRel
			one-to-many relation to SourcingEventResponse
			Field Mapping uses ByEvent
				related.Company         		= Company
				related.SourcingEvent     		= SourcingEvent
			Instance Selection
				where (related.Status.Submitted)

		RequiredLineAttachmentsRel
			one-to-many relation to SourcingEventLineQuestion
			Field Mapping uses symbolic key
				related.Company                 = Company
				related.SourcingEvent     		= SourcingEvent
			Instance Selection
				where (related.AlwaysRequireResponseAttachment)

	Sets
		ByDisplayOrder
			duplicates
 			Sort Order
 				Company
 				SourcingEvent
 				DisplayOrder

		ByQuestion
			Sort Order
				NotifiedSupplier.SupplierGroup
	 			NotifiedSupplier.Supplier
	 			NotifiedSupplier.SupplierSourceId
				Company
    			SourcingEvent
    			Question
				SourcingEventSupplierQuestion
    			
    	ByQuestionFirst
    		Sort Order
				NotifiedSupplier
    			Question
    			Company
    			SourcingEvent
    			SourcingEventSupplierQuestion
 		
   	Field Rules
   		QuestionText
   			required

   	Rule Blocks
 					   					
 	Actions
		Create is a Create Action
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEvent.LastQuestionDisplayOrder 

			Action Rules

				LocalFromConditional = FromConditional 

			Exit Rules

				if (ResponseType.List)
					if (Question entered)
						for each RepositoryListValuesRel
							invoke CreateDisplay SESupplierQuestionValue
								invoked.NotifiedSupplier.SupplierGroup		= NotifiedSupplier.SupplierGroup
								invoked.NotifiedSupplier.Supplier			= NotifiedSupplier.Supplier
								invoked.NotifiedSupplier.SupplierSourceId	= NotifiedSupplier.SupplierSourceId
								invoked.Company								= Company
								invoked.SourcingEvent						= SourcingEvent
								invoked.SourcingEventSupplierQuestion		= SourcingEventSupplierQuestion
								invoked.SESupplierQuestionValue				= each.QuestionListValue
								invoked.DisplayOrder						= each.DisplayOrder
								fill in fields from each
					else
						for each SourcingEventQuestionValuesRel
							invoke CreateDisplay SESupplierQuestionValue
								invoked.NotifiedSupplier.SupplierGroup		= NotifiedSupplier.SupplierGroup
								invoked.NotifiedSupplier.Supplier			= NotifiedSupplier.Supplier
								invoked.NotifiedSupplier.SupplierSourceId	= NotifiedSupplier.SupplierSourceId
								invoked.Company								= Company
								invoked.SourcingEvent						= SourcingEvent
								invoked.SourcingEventSupplierQuestion		= SourcingEventSupplierQuestion
								invoked.SESupplierQuestionValue				= each.SourcingEventQuestionValue
								invoked.DisplayOrder						= each.DisplayOrder
								fill in fields from each

		CreateFromWizard is a Create Action   
			valid when (SourcingEvent.CanAddQuestion)
			Field Rules
	 			DisplayOrder
					if (!SourcingEvent.CreateByCopy)
		 				autosequence using SourcingEvent.LastQuestionDisplayOrder 

			Action Rules
				if (!SourcingEvent.TwoStepBiddingUsed
				and !SourcingEvent.BestAndFinalOffer)
					constraint (SourcingEvent.InActionableState)
						"CannotAddQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				else
					if (SourcingEvent.TwoStepBiddingUsed)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterStepTwo_(Pricing)HasStarted"
					else 
					if (SourcingEvent.BestAndFinalOffer)
						constraint (SourcingEvent.CanAddQuestion)
							"CannotAddQuestionAfterBestAndFinalProcessHasStarted"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddQuestionWhileEventIsPendingEventApproval"

				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddQuestion)
					invoke Amend Open.Notified SourcingEvent
			
			Exit Rules
		
		Update is an Update Action
			valid when (SourcingEvent.InEditableState)
		   	Action Rules
				if (Question entered
				and (QuestionText changed
				or	 Attachment changed))
					initialize Question
				
		Delete is a Delete Action
			valid when (SourcingEvent.InEditableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteQuestionWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteQuestionWhileEventIsPendingEventApproval"

		DeleteConditionalQuestions is a Set Action  
			restricted 
			Parameters 
			 	ParmCompany					is a SourcingCompany 
			 	ParmSourcingEvent			is a SourcingEvent 
			 	ParmOriginalQuestion		is like SourcingEventSupplierQuestion
				ParmLastOldQuestion			is like SourcingEventSupplierQuestion
				ParmNotifiedSupplier		is a NotifiedSupplier   

			Instance Selection 
			 	where (ParmNotifiedSupplier				= NotifiedSupplier
			 	and    ParmCompany						= Company
			 	and    ParmSourcingEvent				= SourcingEvent 
			 	and    ParmOriginalQuestion				= CreatedFromQuestion
				and    SourcingEventSupplierQuestion	<= ParmLastOldQuestion)

			Action Rules
			 	Instance Rules
					invoke Delete		
