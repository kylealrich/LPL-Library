ReconciliationDetailComment is a BusinessClass
	owned by reconciliation
	prefix is RCDC
	
	Ontology
		symbolic key is ReconciliationDetailComment
	
	Persistent Fields
		Subject	 			is Alpha size 50
    	Comment				is Text
		ReconCommentType
		From				is Alpha size 230
			default label is "MadeBy"
		CommentDate			is TimeStamp
    		default label is "Date"
    	Attachment			is an AlternateAttachment
	
	Local Fields
		LocalAttributeCtr   is Numeric 2
		
	Derived Fields

		ReconciliationCodeBlock is a DerivedField
			type is Alpha 100
			return ReconciliationAccount.ReconciliationCodeBlock
			
		AccountingEntity is a DerivedField
			type is like AccountingEntity
			return ReconciliationAccount.ReconciliationCodeBlock.AccountingEntity 
		
		AccountingUnit is a DerivedField
			type is like AccountingUnit
			return ReconciliationAccount.ReconciliationCodeBlock.AccountingUnit
		
		GeneralLedgerChartAccount is a DerivedField
			type is like GeneralLedgerChartAccount
			default label is "GlobalLedgerChartAccount"
			return ReconciliationAccount.ReconciliationCodeBlock.GeneralLedgerChartAccount
		
		Project is a DerivedField
			type is like Project
			return ReconciliationAccount.ReconciliationCodeBlock.Project
			
		FinanceDimension1 is a DerivedField
			type is like FinanceDimension1
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension1
		
		FinanceDimension2 is a DerivedField
			type is like FinanceDimension2
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension2
			
		FinanceDimension3 is a DerivedField
			type is like FinanceDimension3
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension3
			
		FinanceDimension4 is a DerivedField
			type is like FinanceDimension4
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension4
			
		FinanceDimension5 is a DerivedField
			type is like FinanceDimension5
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension5
			
		FinanceDimension6 is a DerivedField
			type is like FinanceDimension6
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension6
		
		FinanceDimension7 is a DerivedField
			type is like FinanceDimension7
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension7
			
		FinanceDimension8 is a DerivedField
			type is like FinanceDimension8
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension8
			
		FinanceDimension9 is a DerivedField
			type is like FinanceDimension9
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension9
			
		FinanceDimension10 is a DerivedField
			type is like FinanceDimension10
			return ReconciliationAccount.ReconciliationCodeBlock.FinanceDimension10


		ContextMessageEntityType is a StringField
			type is Alpha 40
			restricted
			"InforReconciliationDetailComment"

		ContextMessageText is a MessageField
			restricted
			"ReconciliationDetailComment<ReconciliationDetailComment>"
			
	Field Rules
    	
    	From
			initial value is actor.agent(Employee).FirstLastName
			required
    	
    	CommentDate
    		default to current timestamp
	
	Conditions
    	HasAttachment
    		when (Attachment entered)
    	
    	HasComment
    		when (CommentDate entered)

		PeriodEndComment
			when (ReconciliationPeriodDetail entered)
		
		HasAuditorRole
			restricted
			when (HideForAuditorRel exists)
	
	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment	
									
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
			
	Relations
		HideForAuditorRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor					= actor
 				related.ActorRole.Role			= ReconciliationManagementGroup.AuditorRole
				
	Actions
		Create is a Create Action
			Field Rules
				Subject
					required
						"SubjectIsRequired"
							
				Comment 
					required
						"CommentIsRequired"

		UploadAttachments is a Create Action
			default label is "UploadAttachments"
    		completion message is "AttachmentsUploaded"
			Action Rules
				Subject = Attachment.Title
				From	= actor.agent(Employee).FirstLastName
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
													
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
