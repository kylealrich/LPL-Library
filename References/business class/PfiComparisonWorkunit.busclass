PfiComparisonWorkunit is a BusinessClass
    owned by pfi
    prefix is PfiCW
    contains environment data
	disable data area copy
		preserve target data
    default label is "ComparisonWorkUnit"

    Ontology
        symbolic key is PfiComparisonWorkunit

    Patterns
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated

    Local Fields
        LocActivityType    is a PfiActivityType
        LocActivity        is a PfiActivity

    Derived Fields
        ActivityCount is a DerivedField
            type is Alpha size 20
            if (PfiWorkunit.PfiActivity set exists)
                return "(" + instance count of PfiWorkunit.PfiActivity set + ")"
            else
                return ""

        ComparisonActivityCount is a DerivedField
            type is Alpha size 20
            if (PfiComparisonActivity set exists)
                return "(" + instance count of PfiComparisonActivity set + ")"
            else
                return ""

    Conditions
        IsMyComparisonWorkunit
            default label is "MyComparisonActivity"
            when (Actor = actor)

    Relations

        PfiActivityByTypeRel
            one-to-many relation to PfiActivity
            Field Mapping uses ByWorkunitActivityTypeActivity
                related.PfiWorkunit  = PfiWorkunit
                related.ActivityType = LocActivityType

        PfiComparisonActivityByTypeRel is a PfiComparisonActivity set
            Instance Selection
                where (related.PfiActivity.ActivityType = LocActivityType)

        PfiComparisonActivityByActivityRel is a PfiComparisonActivity set
            Instance Selection
                where (related.PfiActivity = LocActivity)

    Actions

        Create is a Create Action

        Update is an Update Action
            restricted

        Delete is a Delete Action

        CreateAllComparisonActivity is an Instance Action

            Action Rules
                for each PfiWorkunit.PfiActivity set
                    LocActivity = each.PfiActivity
                    if (!PfiComparisonActivityByActivityRel exists)
                        invoke Create PfiComparisonActivity
                            fill in fields from each
                            invoked.Actor   = actor 

        CreateAllComparisonActivityForType is an Instance Action
            Parameters
                PrmActivityType is a PfiActivityType
                    default label is "ActivityType"

            Action Rules
                LocActivityType = PrmActivityType
                for each PfiActivityByTypeRel
                    LocActivity = each.PfiActivity
                    if (!PfiComparisonActivityByActivityRel exists)
                        invoke Create PfiComparisonActivity
                            fill in fields from each
                            invoked.Actor   = actor    

        DeleteAllComparisonActivity is an Instance Action
            Action Rules
                invoke Delete PfiComparisonActivity set

        DeleteAllComparisonActivityForType is an Instance Action
            Parameters
                PrmActivityType is a PfiActivityType
                    default label is "ActivityType"

            Action Rules
                LocActivityType = PrmActivityType
                invoke Delete PfiComparisonActivityByTypeRel
