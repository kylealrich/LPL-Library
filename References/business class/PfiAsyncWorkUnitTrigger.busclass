PfiAsyncWorkUnitTrigger is a BusinessClass
    owned by pfi
    prefix is PAWT
    stored in environment
    disable data area copy

    Ontology
		symbolic key is PfiAsyncWorkUnitTrigger
		
    Patterns
        disable Auditing
        disable EffectiveDated
        implements CreateStamp
        

    Persistent Fields
    	AsyncQueueDefinition
    	WorkUnit is Numeric size 12
    	Actor
    	DataArea
    	FlowDefinition is Alpha size 100
    	Priority 				  is a AsyncPriority
    	Type               is Numeric size 1
            States
                Trigger             value is 0
                Cancel              value is 1
                Execute             value is 2
                ExecuteUA			value is 3
                ExecuteWait			value is 4

    	Status is Numeric size 1
    		States
    			New value is 0
    			SentToMetric value is 2
    			
    	ObjectKey          is BusinessObjectReference
        SqlName            is Alpha size 15 
		WorkUnitHolder     is BusinessObjectReference
        QueueTask          is BusinessObjectReference
        ExecuteAction      is a PfiActionTaken
        ExecuteActionURL   is Text
        ActionReason       is AlphaUpper size 20 
        ActionComment      is Text
        AuthenticatedActor is an Actor                 
            as of current date
        IsProxy            is Boolean                    
            default label is "Proxy"
        Activity is Numeric size 6
        Force is Boolean
            
    Derived Fields
	    DefaultQueue is a DerivedField
			type is Alpha size 50
			restricted
			return "IpaDefaultWorkUnitDispatchQueue"
			
		IsQueueSuspended is a DerivedField
			type is Boolean
			default label is "QueueSuspended"
			if(QueueSuspendedRel.Suspended)
				return true
			else
				return false
			
    Field Rules
		Priority
			default to Priority.Normal
			initial value is Priority.Normal
			required
		AsyncQueueDefinition
			default to DefaultQueue
			required
		Actor
			required
		DataArea
			required
		FlowDefinition
			required

	Conditions
		CanBeDispatched
		    default label is "WorkUnitCanBeDispatched"
			when(not Priority.None)
		CanBeSentToMetric
		    default label is "WorkUnitCanBeSentToMetric"
			when(not Priority.None and not Status.SentToMetric)
		CanDisplayAlert
		    default label is "DisplayAlert"
			when(Priority.None or IsQueueSuspended)
		
	Sets

		Queued
			indexed
			Sort Order
				AsyncQueueDefinition
				Priority
				DataArea
				WorkUnit
			Instance Selection
				where (CanBeDispatched)	
		
		QueuedByDataArea
			indexed
			Sort Order
				DataArea
				AsyncQueueDefinition
				Priority
				WorkUnit
			Instance Selection
				where (CanBeDispatched)
					
		QueuedByNotSentToMoncleStatus
			indexed
			Sort Order
				Status
				DataArea
				AsyncQueueDefinition
				Priority
				WorkUnit
			Instance Selection
				where (CanBeSentToMetric)
				
		ByDataAreaWorkUnit
			indexed
			Sort Order
				DataArea
				WorkUnit
				
    	WorkUnitTriggerByPriority
    		indexed
    		Sort Order
    			Priority
    			AsyncQueueDefinition
    			Status
    			create stamp
    			DataArea
    			WorkUnit
    			PfiAsyncWorkUnitTrigger
						
	Relations
    	MappingsMatchedByActorDataAreaFlowMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = Actor
				related.DataArea = DataArea
				related.PfiFlowDefinition = FlowDefinition
			
		MappingsMatchedByActorFlowMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = Actor
				related.DataArea = blank
				related.PfiFlowDefinition = FlowDefinition	
			
		MappingsMatchedByDataAreaFlowMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = blank
				related.DataArea = DataArea
				related.PfiFlowDefinition = FlowDefinition
		
		MappingsMatchedByActorDataAreaMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = Actor
				related.DataArea = DataArea
				related.PfiFlowDefinition = blank
				
		MappingsMatchedByActorMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = Actor
				related.DataArea = blank
				related.PfiFlowDefinition = blank
			
		MappingsMatchedByDataAreaMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = blank
				related.DataArea = DataArea
				related.PfiFlowDefinition = blank
			
		MappingsMatchedByFlowMapAll  
			one-to-one relation to PfiAsyncQueueMapping
			Field Mapping uses Mappings
				related.Actor = blank
				related.DataArea = blank
				related.PfiFlowDefinition = FlowDefinition
				
		QueueSuspendedRel
			one-to-one relation to AsyncQueueDefinition
			Field Mapping uses symbolic key
				related.AsyncQueueDefinition = AsyncQueueDefinition

	Rule Blocks
		QueueMappingResolve
			AsyncQueueDefinition = DefaultQueue
			Priority = Priority.Normal
			MappingFound = false
			
			if (Actor entered)
				if (MappingsMatchedByActorDataAreaFlowMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByActorDataAreaFlowMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByActorDataAreaFlowMapAll.Priority
					MappingFound = true
				else
				if (MappingsMatchedByActorFlowMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByActorFlowMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByActorFlowMapAll.Priority
					MappingFound = true
				else
				if (MappingsMatchedByActorDataAreaMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByActorDataAreaMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByActorDataAreaMapAll.Priority
					MappingFound = true
				else
				if (MappingsMatchedByActorMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByActorMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByActorMapAll.Priority
					MappingFound = true
					
			if (not MappingFound and DataArea entered)
				if (MappingsMatchedByDataAreaFlowMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByDataAreaFlowMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByDataAreaFlowMapAll.Priority
					MappingFound = true
				else
				if (MappingsMatchedByDataAreaMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByDataAreaMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByDataAreaMapAll.Priority
					MappingFound = true
		
			if (not MappingFound and FlowDefinition entered)
				if (MappingsMatchedByFlowMapAll exists)
					AsyncQueueDefinition = MappingsMatchedByFlowMapAll.AsyncQueueDefinition
					Priority = MappingsMatchedByFlowMapAll.Priority
					MappingFound = true
					
			
	Actions
		Create is a Create Action
			restricted
    	 	Local Fields
				MappingFound is Boolean
				
			Exit Rules
				include QueueMappingResolve 
		
		UpdateQueue is an Instance Action
			restricted
			Local Fields
				MappingFound is Boolean
				
			Exit Rules
				include QueueMappingResolve 
						
		Update is an Update Action
		
		Delete is a Delete Action
			restricted
		
		ReQueue is a Set Action
            run in background
				
            Instance Selection
                where (true)
                
			Action Rules
                Instance Rules
                    invoke UpdateQueue
		
