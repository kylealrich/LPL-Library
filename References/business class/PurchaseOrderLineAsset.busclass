PurchaseOrderLineAsset is a BusinessClass
    owned by am
    prefix is AOH
    classic name is AMPOASTHDR

    Ontology
        symbolic key is PurchaseOrderLineAsset
            classic set name is AOHSET1
            classic name is LINE-NBR

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        Status                    is Numeric size 1
            States
                Unreleased value is 0
                Released   value is 1
        Vendor
        Dropship
            classic name is DROPSHIP-FL
        PurchaseOrderLineQuantity is an ItemQuantity
            classic name is PO-LINE-QTY
        ReceivedQuantity          is an ItemQuantity
            classic name is RCVD-QTY
        RejecytedQuantity         is an ItemQuantity
            classic name is RJCT-QTY
        ReturnedQuantity          is an ItemQuantity
            classic name is RTRN-QTY
        InvoicedQuantity          is an ItemQuantity
            classic name is INVC-QTY
        CheckbookQuantity         is an ItemQuantity
            classic name is CHBK-QTY
        AssetCreateQuanity        is an ItemQuantity
            classic name is AM-CREATE-QTY
        AssetDistributionQuantity is an ItemQuantity
            classic name is AM-DIST-QTY
        CancelledQuantity         is an ItemQuantity
            classic name is CNCL-QTY

	Transient Fields
		TransientCreateDetail				 is Boolean
		TransientCurrency					 is a Currency
		TransientTransactionNumberOfDecimals is a CurrencyNumberOfDecimals
		TransientInvoice					 is a PayablesInvoice
        TransientSuffix						 is a Suffix
        TransientBackOrderFlag				 is Boolean
		
	Local Fields
    	LocalCurrencyAmount				is a CurrencyAmount
		LocalProcessFunction	  			is Alpha size 10
			States

    			Create    value is "CREATE"
    			Change    value is "CHANGE"
    			Cancel    value is "CANCEL"
    			Delete    value is "DELETE"
    			Release   value is "RELEASE"

		LocalTotalReceivedQuantity			is like ItemQuantity
		LocalTotalInvoicedQuantity			is like ItemQuantity
		LocalTotalQuantity					is like ItemQuantity
		LocalQuantityCount					is like ItemQuantity
		LocalDetailReceivedQuantity			is like ItemQuantity
		LocalDetailRejectedQuantity			is like ItemQuantity
		LocalDetailReturnedQuantity			is like ItemQuantity
		LocalDetailInvoicedQuantity			is like ItemQuantity
		LocalDetailCancelledQuantity		is like ItemQuantity
		LocalDetailReconQuantity			is like ItemQuantity
		LocalDetailCreatedOnUpdate			is Boolean

		LocalSuffix							is like Suffix
		LocalOriginalTransactionAmount		is an InternationalAmount	
		LocalTaxAmount						is an InternationalAmount
		LocalAOCTransactionAmount			is an InternationalAmount
		LocalAOCTaxAmount					is an InternationalAmount
		LocalDistQuantity					is like ItemQuantity
		LocalDistQuantityDec				is like ItemQuantity
		LocalMatchedQuantity				is like ItemQuantity
		LocalMatchedQuantitySav				is like ItemQuantity
		LocalMatchedQuantityDec				is like ItemQuantity
		LocalChargebackQuantity				is like ItemQuantity
		LocalDetailUnitCost					is an InternationalAmount
		LocalDetailAOCUnitCost				is an InternationalAmount
		RoundedValue
		LocalAMIItemNumber					is like AssetItemNumber
		LocalAMIDescription					is like Description
		LocalAMIItemQuantity				is like ItemQuantity
		LocalAMITransactionItemCost     	is an InternationalAmount
		LocalAMITransactionItemTax			is an InternationalAmount
		LocalAverageTaxTran					is an InternationalAmount
		LocalUnitCost						is an InternationalAmount
		LocalAOCUnitCost					is an InternationalAmount
		LocalAOCTotalCost					is an InternationalAmount
		LocalAOCTaxTran						is an InternationalAmount
		LocalAverageAOCTaxTran				is an InternationalAmount
		LocalFullMatch						is Boolean

		LocalCurrencyTable					is a CurrencyTable
		LocalCurrencyExchange				is a CurrencyExchange
		LocalFromCurrency					is a FromCurrency
		LocalExchangeDate					is an ExchangeDate
		LocalAMITransactionItemCostBase     is an InternationalAmount
		LocalAMITransactionItemTaxBase		is an InternationalAmount


		LocalAccountingEntity				is like AccountingEntity
		LocalQuantity						is like Quantity
		LocalAssetAccountingUnit			is a FinanceCodeBlockNoAccountFull
		LocalCurrencyRate          			is a CurrencyExchangeRate
	Derived Fields
		DerivedTotalReceivedAssetDetails		is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsReceivedRel)

		DerivedPurchaseOrderLineCancelledQuantity is a DerivedField
			type is like Quantity
			restricted
			return (PurchaseOrderLine.CancelQuantity)

		DerivedTotalAssetDetails		is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsReceivedRel)

		DerivedStockReceivedQuantity is a DerivedField
			type is like Quantity
			restricted
    		return (ReceivedQuantity * PurchaseOrderLine.EnteredBuyUOMMultiplier)
    		
    	DerivedPurchaseOrderLineReceivedQuantity  is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsReceivedRel)
    		
    	DerivedPurchaseOrderLineRejectedQuantity  is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsRejectedRel)
			
		DerivedPurchaseOrderLineInvoicedQuantity  is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsInvoicedRel)
			
		DerivedPurchaseOrderLineReturnedQuantity  is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsReturnedRel)
			
		DerivedPurchaseOrderLineAssetCreatedQuantity  is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsAssetCreatedRel)
			
		DerivedPurchaseOrderLineDetailCancelledQuantity  is a DerivedField
			type is like ItemQuantity
			return (instance count of PurchaseOrderLineAssetDetailsCancelledRel)

		DerivedAssetDetailNotInvoicedQuantity is a DerivedField 
			type is like ItemQuantity 
			restricted 
			return (instance count of PurchaseOrderLineAssetDetailsNotInvoicedRel)
    Conditions

        IsAmpoastdtl
        	restricted
            when (first PurchaseOrderLineAssetDetailsRels exists)

        IsAmpodflts
        	restricted
            when (PurchaseOrderLineAssetDefaultsRel exists)

        IsReleased
        	restricted
            when (Status.Released)

        IsUnreleased
        	restricted
            when (Status.Unreleased)
            
		HasUnreceivedAssetDetails
			when (PurchaseOrderLineAssetDetailsNotReceivedRels exists)
			
		HasAssetDetails
			restricted
			when (PurchaseOrderLineAssetDetailsRels exists)

    Relations
    	PurchaseOrderLineDistributionsRel
            one-to-many relation to PurchaseOrderLineDistribution
            Field Mapping uses symbolic key
                related.Company                   = Company
                related.PurchaseOrder             = PurchaseOrder
                related.PurchaseOrderLine         = PurchaseOrderLine

		PurchaseOrderReceiptLinesRel
            one-to-many relation to PurchaseOrderReceiptLine
            Field Mapping uses ByPurchaseOrderLine
                related.Company                 = Company
                related.PurchaseOrder		    = PurchaseOrder
                related.PurchaseOrderLine       = PurchaseOrderLine
            Instance Selection
            	where (related.Status > blank)

        PurchaseOrderLineAssetDetailsRels
            classic name is AMPOASTDTL
            one-to-many relation to PurchaseOrderLineAssetDetail
            delete cascades
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrder = PurchaseOrder
                and   related.PurchaseOrderLineAsset = PurchaseOrderLine)
                
		PurchaseOrderLineAssetDetailsReceivedRel
			one-to-many relation to PurchaseOrderLineAssetDetail
			Field Mapping uses symbolic key
				related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Received.Yes)
                
        PurchaseOrderLineAssetDetailsNoAssetCreatedRels
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (!related.AssetCreated.Yes
                and    related.Currency 	!= TransientCurrency)
                
        PurchaseOrderLineAssetDetailsNotReceivedRels
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Received.No)
                
        PurchaseOrderLineAssetDetailsNotReceivedNotCancelledRels
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (!related.Received.Yes
                and    !related.Cancelled.Yes)

        PurchaseOrderLineAssetDetailsInvoicedRel
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Invoiced.Yes)
                
        PurchaseOrderLineAssetDetailsNotInvoicedRel
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Invoiced.No
                and    related.Cancelled.No)  
                
        PurchaseOrderLineAssetDefaultsRel
            classic name is AMPODFLTS
            one-to-one relation to PurchaseOrderLineAssetDefaults
            delete cascades
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.PurchaseOrder			= PurchaseOrder
                related.PurchaseOrderLine       = PurchaseOrderLine

        MultPayablesInvoiceDistributionsRel
        	one-to-many relation to PayablesInvoiceDistribution
        	Field Mapping uses ByPayablesInvoiceSuffixPOLine
        		related.Company				= Company
        		related.Vendor				= Vendor
			Instance Selection
				where (related.PurchaseOrder 			= PurchaseOrder
                and    related.PurchaseOrderLine 		= PurchaseOrderLine
                and    related.DistributionType			= "D"
				and    related.TaxCode not entered
				and    related.AddOnCharge not entered
        		and    related.PayablesInvoice.MatchObjectID	= TransientInvoice.MatchObjectID)
                
        PayablesInvoiceDistributionsRel
        	one-to-many relation to PayablesInvoiceDistribution
        	Field Mapping uses ByPayablesInvoiceSuffixPOLine
        		related.Company				= Company
        		related.Vendor				= Vendor
        		related.PayablesInvoice		= TransientInvoice 
        		related.Suffix				= LocalSuffix
        		related.PurchaseOrder 		= PurchaseOrder
                related.PurchaseOrderLine 	= PurchaseOrderLine
			Instance Selection
				where (related.DistributionType	= "D"
				and    related.TaxCode not entered
				and    related.AddOnCharge not entered)
                
        PayablesInvoiceDetailsRel
        	one-to-many relation to PayablesInvoiceDetail
        	Field Mapping uses ByCompanyVendorPayablesInvoice
        		related.Company				= Company
        		related.Vendor				= Vendor
        		related.PayablesInvoice		= TransientInvoice 
			Instance Selection
        		where (related.Suffix				= LocalSuffix
        		and    related.PurchaseOrder 		= PurchaseOrder
                and    related.PurchaseOrderLine 	= PurchaseOrderLine)
        
        PayablesInvoiceAddOnChargeRel
            one-to-many relation to PayablesInvoiceAddOnCharge
            Field Mapping uses symbolic key
                related.Company 			= Company
                related.PayablesInvoice		= TransientInvoice 
            	related.PurchaseOrder 		= PurchaseOrder
                related.PurchaseOrderLine 	= PurchaseOrderLine

        AssetCompanyRel
        	one-to-one relation to AssetCompany
        	Field Mapping uses symbolic key
        		related.Company = LocalAccountingEntity
        		
		AssetCompanySystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode 
				related.GeneralLedgerSystemCode			= "AM"
				related.Company							= LocalAccountingEntity
				
        PurgePurchaseOrderLineAssetDetailsRel is a PurchaseOrderLineAssetDetail set
			Instance Selection
				include deleted records
				
		PurchaseOrderLineAssetDetailsRejectedRel
			one-to-many relation to PurchaseOrderLineAssetDetail
			Field Mapping uses symbolic key
				related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Rejected.Yes)
                
		PurchaseOrderLineAssetDetailsReturnedRel
			one-to-many relation to PurchaseOrderLineAssetDetail
			Field Mapping uses symbolic key
				related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Returned.Yes)
                
		PurchaseOrderLineAssetDetailsAssetCreatedRel
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.AssetCreated.Yes)
                
		PurchaseOrderLineAssetDetailsCancelledRel
            one-to-many relation to PurchaseOrderLineAssetDetail
            Field Mapping uses symbolic key
                related.Company 				= Company
                related.PurchaseOrder 			= PurchaseOrder
                related.PurchaseOrderLineAsset 	= PurchaseOrderLine
            Instance Selection
                where (related.Cancelled.Yes)

	Rule Blocks

		EditVerify
			if (PurchaseOrderLineAssetDetailsRels exists)
				for each PurchaseOrderLineAssetDetailsRels
					if (each.Received.Yes)
						LocalDetailReceivedQuantity += 1
					if (each.Rejected.Yes)
						LocalDetailRejectedQuantity += 1
					if (each.Returned.Yes)
						LocalDetailReturnedQuantity += 1
					if (each.Invoiced.Yes)
						LocalDetailInvoicedQuantity += 1
					if (each.Cancelled.Yes)
						LocalDetailCancelledQuantity += 1
				
				constraint(LocalDetailReceivedQuantity = ReceivedQuantity)
					"AssetDetailsReceivedMustMatchTotalQty-Line<PurchaseOrderLine>"
				
				constraint(LocalDetailRejectedQuantity = RejecytedQuantity)
					"AssetDetailsRejectedMustMatchTotalQty-Line<PurchaseOrderLine>"
				
				constraint(LocalDetailReturnedQuantity = ReturnedQuantity)
					"AssetDetailsReturnedMustMatchTotalQty-Line<PurchaseOrderLine>"
				
				constraint(LocalDetailInvoicedQuantity = InvoicedQuantity)
					"AssetDetailsInvoicedMustMatchTotalQty-Line<PurchaseOrderLine>"
				
				if (!LocalProcessFunction.Change and !TransientBackOrderFlag)					
					constraint(LocalDetailCancelledQuantity = CancelledQuantity)
						"AssetDetailsCancelledMustMatchTotalQty-Line<PurchaseOrderLine>"
			else
				if (ReceivedQuantity > PurchaseOrderLineQuantity
				and ReceivedQuantity >= InvoicedQuantity)
					LocalTotalQuantity = ReceivedQuantity
				else
				if (InvoicedQuantity > PurchaseOrderLineQuantity
				and InvoicedQuantity >= ReceivedQuantity)
					LocalTotalQuantity = InvoicedQuantity
				else
					LocalTotalQuantity = PurchaseOrderLineQuantity
					
				LocalDetailReconQuantity = LocalTotalQuantity - ReturnedQuantity - RejecytedQuantity - CancelledQuantity - CheckbookQuantity
				if (LocalDetailReconQuantity = AssetCreateQuanity
				and AssetCreateQuanity != 0)
					Status = Status.Released
	
		ProcessEdits
			if (LocalProcessFunction.Change
			and TransientCreateDetail)
    			for each PurchaseOrderLineDistributionsRel
    				constraint(each.AssetInformation entered)
    					"CannotCreateDetail;AssetTemplateNotAssignedToPOLine"
			
			LocalTotalReceivedQuantity = ReceivedQuantity + CancelledQuantity
			LocalTotalInvoicedQuantity = InvoicedQuantity + CancelledQuantity
			
			if (LocalTotalReceivedQuantity > PurchaseOrderLineQuantity
			and LocalTotalReceivedQuantity >= InvoicedQuantity)
				LocalTotalQuantity = LocalTotalReceivedQuantity
			else
			if (LocalTotalInvoicedQuantity > PurchaseOrderLineQuantity
			and LocalTotalInvoicedQuantity >= LocalTotalReceivedQuantity)
				LocalTotalQuantity = LocalTotalInvoicedQuantity
			else
				LocalTotalQuantity = PurchaseOrderLineQuantity
				
			if (LocalProcessFunction.Cancel
			and CancelledQuantity = LocalTotalQuantity)
				constraint(PurchaseOrderLineAssetDetailsRels not exists)
					"CannotCancel;DetailsExist"
			
			if (LocalProcessFunction.Change)
				LocalTotalQuantity -= instance count of PurchaseOrderLineAssetDetailsRels
			
			initialize LocalQuantityCount
			
			if ((LocalProcessFunction.Create
			and TransientCreateDetail
			and LocalTotalQuantity > 0)
			or (LocalProcessFunction.Change
			and (LocalTotalQuantity > 0
			or   TransientCreateDetail)))
				if (LocalProcessFunction.Change)
					LocalDetailCreatedOnUpdate = true
				while(LocalQuantityCount < LocalTotalQuantity)
					invoke Create PurchaseOrderLineAssetDetailsRels
						invoked.Company				   			= Company
						invoked.PurchaseOrder		   			= PurchaseOrder
						invoked.PurchaseOrderLineAsset 			= PurchaseOrderLine
						invoked.Currency						= TransientCurrency
						invoked.TransactionNumberOfDecimals		= TransientTransactionNumberOfDecimals
						invoked.BaseNumberOfDecimals 			= TransientCurrency.NumberOfDecimals
						invoked.Invoice							= TransientInvoice
						invoked.Suffix							= TransientSuffix


					LocalQuantityCount += 1
			

			if (LocalProcessFunction.Change
			and !LocalDetailCreatedOnUpdate)
				if (ReceivedQuantity > 0
				and ReceivedQuantity = instance count of PurchaseOrderLineAssetDetailsRels)
					for each PurchaseOrderLineAssetDetailsNotReceivedRels
						invoke Update each
							invoked.Received = 1

			include EditVerify

		CreateAssetInterface



			if (each.AssetDetailRel exists)
				if (AssetCompanySystemClosingControlRel exists)
					LocalCurrencyTable 				 = AssetCompanySystemClosingControlRel.CurrencyTable
				else 
					LocalCurrencyTable 				 = Company.CurrencyTable

				initialize LocalCurrencyExchange

				LocalFromCurrency 				 				= each.PayablesInvoice.InvoiceCurrency
				LocalCurrencyExchange.ToCurrency 				= Company.Currency
				LocalCurrencyRate								= LocalCurrencyExchange.OutputCurrencyRate

				LocalCurrencyAmount								= LocalAMITransactionItemCost
				LocalExchangeDate 				 				= each.PayablesInvoice.InvoiceDate
				LocalAMITransactionItemCostBase 				= LocalCurrencyExchange.OutputCurrencyAmount
				
				if (LocalAMITransactionItemTax != 0)
					initialize LocalCurrencyExchange
					LocalFromCurrency 				 			= each.PayablesInvoice.InvoiceCurrency
					LocalCurrencyExchange.ToCurrency 			= Company.Currency
					LocalCurrencyExchange.EnteredCurrencyAmount = LocalAMITransactionItemTax
					LocalCurrencyAmount							= LocalCurrencyExchange.EnteredCurrencyAmount
					LocalExchangeDate 				 			= each.PayablesInvoice.InvoiceDate
					LocalAMITransactionItemTaxBase 				= LocalCurrencyExchange.OutputCurrencyAmount




				invoke Create AssetInterface
					invoked.DistributionCodeBlock									= each.DistributionAccount
					LocalAccountingEntity											= each.DistributionAccount.ToAccountingEntity
					invoked.InterfaceSystem											= "AP"
					invoked.AssetTemplate											= each.AssetDetailRel.AssetTemplate
					invoked.Company													= each.AssetDetailRel.AssetTemplate.Company
					invoked.InvoiceAndPurchaseOrderInformation.InvoiceCompany		= Company
					invoked.InvoiceAndPurchaseOrderInformation.Vendor				= Vendor
					invoked.InvoiceAndPurchaseOrderInformation.PayablesInvoice 		= TransientInvoice 
					invoked.InvoiceAndPurchaseOrderInformation.PurchaseOrder 		= PurchaseOrder
					invoked.InvoiceAndPurchaseOrderInformation.PurchaseOrderLine	= PurchaseOrderLine
					invoked.InvoiceAndPurchaseOrderInformation.DistributionSequence	= each.PayablesInvoiceDistribution			
					invoked.AssetAccountingUnitGroup 								= each.AssetDetailRel.AssetTemplate.AssetAccountingUnitGroup

					invoked.BaseItemCost.FunctionalAmount.EnteredCurrencyAmount		= LocalAMITransactionItemCostBase
					invoked.BaseItemTax.FunctionalAmount.EnteredCurrencyAmount		= LocalAMITransactionItemTaxBase
					invoked.TransactionItemCost 									= LocalAMITransactionItemCost
					invoked.TransactionItemTax 										= LocalAMITransactionItemTax
						
					if (each.AssetDetailRel.AssetTemplate.TransactionDimensionDefault.FromTemplate)		
						if (each.AssetDetailRel.AssetTemplate.AccountingUnit entered)	
							invoked.AssetAccountingUnit		= each.AssetDetailRel.AssetTemplate.AccountingUnit
						else 	
							LocalAssetAccountingUnit		= each.DistributionAccount
							invoked.AssetAccountingUnit		= LocalAssetAccountingUnit
					else 
						if (each.AssetDetailRel.AssetTemplate.TransactionDimensionDefault.FromDistribution)					
							LocalAssetAccountingUnit		= each.DistributionAccount
							invoked.AssetAccountingUnit		= LocalAssetAccountingUnit
						else 
							if (each.AssetDetailRel.AssetTemplate.TransactionDimensionDefault.LeaveBlank)
								initialize invoked.AssetAccountingUnit

					if (PurchaseOrderLineAssetDefaultsRel exists
					and PurchaseOrderLineAssetDefaultsRel.Project entered)
						invoked.DepreciationExpenseProject.Project					= PurchaseOrderLineAssetDefaultsRel.Project
					else
					if (AssetCompanyRel.DefaultDepreciationExpenseProject)
						invoked.DepreciationExpenseProject.Project					= each.DistributionAccount.Project
					if (PurchaseOrderLineAssetDefaultsRel exists
					and PurchaseOrderLineAssetDefaultsRel.PurchaseDate entered)
						invoked.PurchaseDate										= PurchaseOrderLineAssetDefaultsRel.PurchaseDate
						invoked.PostingDate											= PurchaseOrderLineAssetDefaultsRel.PurchaseDate
					else
						invoked.PurchaseDate										= each.PayablesInvoice.InvoiceDate
						invoked.PostingDate											= each.PayablesInvoice.InvoiceDate
					if (PurchaseOrderLineAssetDefaultsRel exists
					and PurchaseOrderLineAssetDefaultsRel.InServiceDate entered)
						invoked.InServiceDate										= PurchaseOrderLineAssetDefaultsRel.InServiceDate
					else
						invoked.InServiceDate										= each.PayablesInvoice.InvoiceDate
					invoked.TransactionNumberOfDecimals								= each.PayablesInvoice.NumberOfDecimals
					invoked.Currency												= each.PayablesInvoice.InvoiceCurrency
					if (PurchaseOrderLineAssetDefaultsRel exists)
						invoked.TagNumber											= PurchaseOrderLineAssetDefaultsRel.TagNumber
						invoked.AssetGroup											= PurchaseOrderLineAssetDefaultsRel.AssetGroup
						invoked.AssetAccountingUnitGroup							= PurchaseOrderLineAssetDefaultsRel.AssetAccountingUnitGroup
						invoked.Combine												= PurchaseOrderLineAssetDefaultsRel.Combine
						invoked.ModelNumber											= PurchaseOrderLineAssetDefaultsRel.ModelNumber
						invoked.AssetLocation										= PurchaseOrderLineAssetDefaultsRel.AssetLocation
						invoked.AssetDivision										= PurchaseOrderLineAssetDefaultsRel.AssetDivision
						invoked.LocationDetail										= PurchaseOrderLineAssetDefaultsRel.LocationDetail
						invoked.AssetDescription									= PurchaseOrderLineAssetDefaultsRel.AssetDescription
						if (PurchaseOrderLineAssetDefaultsRel.ItemNumber entered)
							invoked.ItemNumber										= PurchaseOrderLineAssetDefaultsRel.ItemNumber
						else 
							invoked.ItemNumber			 							= PurchaseOrderLine.Item	
						if (PurchaseOrderLineAssetDefaultsRel.ItemDescription entered)
							invoked.Description										= PurchaseOrderLineAssetDefaultsRel.ItemDescription
						else
							invoked.Description		 								= PurchaseOrderLine.Description	
					else
						invoked.ItemNumber			 								= PurchaseOrderLine.Item
						invoked.Description		 									= PurchaseOrderLine.Description				
						invoked.AssetDescription									= PurchaseOrderLine.Description  
						
					if  (each.PayablesInvoiceDetail entered)
						if (each.PayablesInvoice.MatchPrepaymentMethod.PrepayFromInvoice)
							invoked.ItemQuantity									= each.PayablesInvoiceDetail.PurchaseOrderLine.Quantity
						else
						if (each.PurchaseOrderLine.DistributionBy.ByQuantity)
							invoked.ItemQuantity									= each.PurchaseOrderLineDistributionRel.DistributionQuantity 
						else
						if (each.PayablesInvoiceDetail.EnteredQuantity >= .5
						or  each.PayablesInvoiceDetail.EnteredQuantity <= -.5)
							if (each.DistributionAmount.CurrencyAmount < .01)
								invoked.ItemQuantity									= each.PayablesInvoiceDetail.EnteredQuantity
							else 
								LocalQuantity = (each.PayablesInvoiceDetail.EnteredQuantity * each.DistributionAmount.CurrencyAmount / each.PayablesInvoiceDetail.TotalDistributionAmount) 
								round LocalQuantity to nearest 1
								invoked.ItemQuantity									= LocalQuantity 
						else
							invoked.ItemQuantity									= 1 
					else
						invoked.ItemQuantity										= AssetCreateQuanity

					if (PurchaseOrderLineAssetDefaultsRel exists
					and PurchaseOrderLineAssetDefaultsRel.HoldAsset entered)
						invoked.HoldAsset											= PurchaseOrderLineAssetDefaultsRel.HoldAsset
					else
						invoked.HoldAsset											= AssetCompanyRel.HoldAssetInterface

							

	Actions
	     
		Create is a Create Action
			Action Rules
				LocalProcessFunction = LocalProcessFunction.Create
				for each PurchaseOrderReceiptLinesRel
					ReceivedQuantity += each.EnteredReceivedQuantity

				include ProcessEdits

	    Update is an Update Action
	    	Action Rules
	    		LocalProcessFunction = LocalProcessFunction.Change
	    		Status = Status.Unreleased
	    		
	    		include ProcessEdits
	    		
	    		if (TransientCurrency entered)	    			
	    			for each PurchaseOrderLineAssetDetailsNoAssetCreatedRels
	    				invoke Update each
	    					invoked.Currency 			 = TransientCurrency
	    					invoked.BaseNumberOfDecimals = TransientCurrency.NumberOfDecimals

	    FastUpdate is an Update Action 
			restricted 
			bypass field rules 
			
		Delete is a Delete Action
	    
	    Cancel is an Instance Action
	    	restricted
	    	Parameters
	    		PrmCancelQuantity is an ItemQuantity
	    	Local Fields
	    		LocalCADetailReceivedQuantity is an ItemQuantity
	    	Action Rules
	    		if (PrmCancelQuantity = PurchaseOrderLineQuantity)
	    			Status = Status.Released
	    		CancelledQuantity = PrmCancelQuantity
	    		
	    		if (PurchaseOrderLineAssetDetailsRels exists)
	    			for each PurchaseOrderLineAssetDetailsRels
						if (each.Received.Yes)
							LocalCADetailReceivedQuantity += 1

					if (CancelledQuantity = (instance count of PurchaseOrderLineAssetDetailsRels - LocalCADetailReceivedQuantity))
						for each PurchaseOrderLineAssetDetailsNotReceivedNotCancelledRels
							invoke Update each
								invoked.Cancelled = 1
								
				LocalProcessFunction = LocalProcessFunction.Cancel
				include ProcessEdits

		Match is an Instance Action
			restricted
			Parameters
				PrmInvoice 					is a PayablesInvoice
				PrmInvoiceDetail 			is a PayablesInvoiceDetail 
        		PrmMatchedQuantity          is a Quantity
				
			Action Rules


				TransientInvoice 			= PrmInvoice
				LocalSuffix  				= PrmInvoice.Suffix

                if (LocalSuffix = 111)
                	LocalChargebackQuantity 	+= PrmMatchedQuantity
                else
					InvoicedQuantity 			+= PrmMatchedQuantity
                	AssetCreateQuanity 			+= PrmMatchedQuantity
                	AssetDistributionQuantity 	+= PrmMatchedQuantity

				initialize LocalAMITransactionItemTax
				initialize LocalAMITransactionItemCost
					    		
				if (PurchaseOrderLineAssetDetailsNotInvoicedRel exists)

					if (DerivedAssetDetailNotInvoicedQuantity > 200)
						invoke MatchAssetDetailsSet PurchaseOrderLineAssetDetail in background
							invoked.PrmCompany  = Company 
							invoked.PrmPurchaseOrder = PurchaseOrder 
							invoked.PrmPurchaseOrderLine = PurchaseOrderLine 
							invoked.PrmInvoice = PrmInvoice 
							invoked.PrmMatchedQuantity = PrmMatchedQuantity 
					else 
						initialize LocalQuantity	

						for each PurchaseOrderLineAssetDetailsNotInvoicedRel

							invoke Match each
								invoked.PrmInvoice 		= PrmInvoice
								
							LocalQuantity += 1
							if (LocalQuantity >= PrmMatchedQuantity)
								end for each
				else
				if (PrmInvoice.InvoiceType.MatchPrepayment
				and PrmInvoice.MatchPrepaymentMethod.PrepayFromInvoice)

			    	if (AssetCompanyRel.PayablesDistributionWithTax.Summary)

			    		for each MultPayablesInvoiceDistributionsRel 
			
							LocalAMITransactionItemTax  	+= each.MatchDistributionTotalTax
	                		LocalAMITransactionItemCost 	+= each.DistributionAmount.CurrencyAmount
	
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalTax
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOC
		
			    		for each MultPayablesInvoiceDistributionsRel 
					    	include CreateAssetInterface
					    	end for each
					else				
			    		for each MultPayablesInvoiceDistributionsRel 
							LocalAMITransactionItemTax  	+= each.MatchDistributionTotalTax
	                		LocalAMITransactionItemCost 	+= each.DistributionAmount.CurrencyAmount
	
						for each MultPayablesInvoiceDistributionsRel
				    		include CreateAssetInterface
							end for each
							
						initialize LocalAMITransactionItemTax
						initialize LocalAMITransactionItemCost

			    		for each MultPayablesInvoiceDistributionsRel 
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalTax
	
						if (LocalAMITransactionItemCost entered)
							for each MultPayablesInvoiceDistributionsRel 
					    		include CreateAssetInterface
					    		end for each

						initialize LocalAMITransactionItemTax
						initialize LocalAMITransactionItemCost

			    		for each MultPayablesInvoiceDistributionsRel 
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOC
	
						if (LocalAMITransactionItemCost entered)
							for each MultPayablesInvoiceDistributionsRel
				    			include CreateAssetInterface
				    			end for each
				else


					for each PrmInvoiceDetail.InvoiceDistributionsForPurchaseOrderLineRel
		
						LocalAccountingEntity				= each.DistributionAccount.ToAccountingEntity

						LocalAMITransactionItemTax  	= each.MatchDistributionTotalTax
                		LocalAMITransactionItemCost 	= each.DistributionAmount.CurrencyAmount

				    	if (AssetCompanyRel.PayablesDistributionWithTax.Summary)
							if (each.TaxCode not entered)
								LocalAMITransactionItemCost 	+= each.MatchDistributionTotalTax
								LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOC
								LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOCTax

								LocalAMITransactionItemTax 		+= each.MatchDistributionTotalAOCTax
		
								include CreateAssetInterface
						else
					    	include CreateAssetInterface
					    	
					    	if (each.MatchDistributionTotalTax entered)
					    	
				                
								initialize LocalAMITransactionItemTax
	    	            		LocalAMITransactionItemCost 	= each.MatchDistributionTotalTax
				                include CreateAssetInterface
				                
				            if (each.MatchDistributionTotalAOC entered)
				                

								LocalAMITransactionItemTax  	= each.MatchDistributionTotalAOCTax
	    	            		LocalAMITransactionItemCost 	= each.MatchDistributionTotalAOC
				                include CreateAssetInterface			    	

					    	if (each.MatchDistributionTotalAOCTax entered)
					    	
								initialize LocalAMITransactionItemTax
	    	            		LocalAMITransactionItemCost 	= each.MatchDistributionTotalAOCTax
				                include CreateAssetInterface
				                

				Status = Status.Released
							

		Unmatch is an Instance Action
			restricted
			Parameters
				PrmInvoice 					is a PayablesInvoice
				PrmInvoiceDetail 			is a PayablesInvoiceDetail 
		        PrmMatchedQuantity          is a Quantity
				
			Action Rules

				TransientInvoice 	= PrmInvoice
				LocalSuffix  		= PrmInvoice.Suffix

                if (LocalSuffix = 111)
                	LocalChargebackQuantity 	-= PrmMatchedQuantity
                else
					InvoicedQuantity 			-= PrmMatchedQuantity
                	AssetCreateQuanity 			-= PrmMatchedQuantity
                	AssetDistributionQuantity 	-= PrmMatchedQuantity
	    		
				initialize LocalAMITransactionItemTax
				initialize LocalAMITransactionItemCost

				if (PurchaseOrderLineAssetDetailsInvoicedRel exists)
			                

					if (DerivedPurchaseOrderLineInvoicedQuantity > 200)
						invoke UnmatchAssetDetailsSet PurchaseOrderLineAssetDetail
							invoked.PrmCompany  = Company 
							invoked.PrmPurchaseOrder = PurchaseOrder 
							invoked.PrmPurchaseOrderLine = PurchaseOrderLine 
							invoked.PrmInvoice = PrmInvoice 
							invoked.PrmMatchedQuantity = PrmMatchedQuantity 
					else 
						initialize LocalQuantity			                
						for each PurchaseOrderLineAssetDetailsInvoicedRel

							invoke Unmatch each
								invoked.PrmInvoice 		= PrmInvoice

							LocalQuantity += 1
							if (LocalQuantity >= PrmMatchedQuantity)
								end for each

				else
				if (PrmInvoice.InvoiceType.MatchPrepayment
				and PrmInvoice.MatchPrepaymentMethod.PrepayFromInvoice)

			    	if (AssetCompanyRel.PayablesDistributionWithTax.Summary)

			    		for each MultPayablesInvoiceDistributionsRel 
			
							LocalAMITransactionItemTax  	+= each.MatchDistributionTotalTax * -1
	                		LocalAMITransactionItemCost 	+= each.DistributionAmount.CurrencyAmount * -1
	
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalTax * -1
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOC * -1
		
				    	for each MultPayablesInvoiceDistributionsRel
				    		include CreateAssetInterface
				    		end for each
					else				
			    		for each MultPayablesInvoiceDistributionsRel 
							LocalAMITransactionItemTax  	+= each.MatchDistributionTotalTax * -1
	                		LocalAMITransactionItemCost 	+= each.DistributionAmount.CurrencyAmount * -1
	
				    	for each MultPayablesInvoiceDistributionsRel
				    		include CreateAssetInterface
				    		end for each

						initialize LocalAMITransactionItemTax
						initialize LocalAMITransactionItemCost

			    		for each MultPayablesInvoiceDistributionsRel 
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalTax * -1
	
						if (LocalAMITransactionItemCost entered)
					    	for each MultPayablesInvoiceDistributionsRel
					    		include CreateAssetInterface
					    		end for each

						initialize LocalAMITransactionItemTax
						initialize LocalAMITransactionItemCost

			    		for each MultPayablesInvoiceDistributionsRel 
    	            		LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOC * -1
	
						if (LocalAMITransactionItemCost entered)
					    	for each MultPayablesInvoiceDistributionsRel
					    		include CreateAssetInterface
					    		end for each
				else


					for each PrmInvoiceDetail.InvoiceDistributionsForPurchaseOrderLineRel
		    		
						LocalAccountingEntity			= each.DistributionAccount.ToAccountingEntity
						
						LocalAMITransactionItemTax  	= each.MatchDistributionTotalTax * -1
                		LocalAMITransactionItemCost 	= each.DistributionAmount.CurrencyAmount * -1

				    	if (AssetCompanyRel.PayablesDistributionWithTax.Summary)
							if (each.TaxCode not entered)
								LocalAMITransactionItemCost 	+= each.MatchDistributionTotalTax * -1
								LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOC * -1
    	            			LocalAMITransactionItemCost 	+= each.MatchDistributionTotalAOCTax * -1

								LocalAMITransactionItemTax 		+= each.MatchDistributionTotalAOCTax * -1

								include CreateAssetInterface
						else
					    	include CreateAssetInterface
					    	
				            if (each.MatchDistributionTotalAOC entered)
				                


								initialize LocalAMITransactionItemTax
	    	            		LocalAMITransactionItemCost 	= each.MatchDistributionTotalAOC * -1
				                include CreateAssetInterface			    	

					    	if (each.MatchDistributionTotalTax entered)
					    	

				                
								LocalAMITransactionItemTax  	= each.MatchDistributionTotalAOCTax * -1
	    	            		LocalAMITransactionItemCost 	= each.MatchDistributionTotalTax * -1
	    	            		initialize LocalAMITransactionItemTax
				                include CreateAssetInterface

					    	if (each.MatchDistributionTotalAOCTax entered)
					    	
								initialize LocalAMITransactionItemTax
	    	            		LocalAMITransactionItemCost 	= each.MatchDistributionTotalAOCTax * -1
				                include CreateAssetInterface

							
				Status = Status.Unreleased











































































































































































































































		FromVendorReturnLine is an Instance Action
			restricted
			Parameters
				PrmReturnQuantity		is an ItemQuantity
			Action Rules
				ReturnedQuantity	+= PrmReturnQuantity 	    
	    	
		Purge is a Purge Action
			restricted
			Entrance Rules
				invoke Purge PurgePurchaseOrderLineAssetDetailsRel
				
