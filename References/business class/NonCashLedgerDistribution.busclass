NonCashLedgerDistribution is a BusinessClass
	owned by cashmgmt
	prefix is NCTD

	Ontology
		symbolic key is NonCashLedgerDistribution

	Persistent Fields								
		PostingDate
		DistributionAccount			is a FinanceCodeBlock	  
			default label is "AccountInformation" 	
		DistributionAmount			is an InternationalAmount 
		Status						is Numeric 1
    		States
    			Entered		value is 0
    			Released	value is 1
    			Posted		value is 2    
    			PendingApproval	value is 3    
		BankStatement
			delete ignored
 		StatementDate				is Date
    	Comment						is Text
    	CashAccountDistribution		is Boolean
    	FinanceJournalInterface
    		delete ignored
    	FinanceJournalInterfaceLine
    		delete ignored

	Context Fields
		StatementDateRange	is a DateRange

	Transient Fields
		TransientFromCurrency		is a FromCurrency
			derive value from CashManagementAccount.Currency
    	GLFromEntity			 	is an AccountingEntity
    		derive value from CashManagementAccount.LegalEntity.AccountingEntity
		GLFinanceCodeBlock		 	is a TransactionCodeBlock				 		
			derive value from GLTransactionDetailRel.FinanceCodeBlock 
			default label is "DistributionAccount"
		GLTransactionAmount		 	is a CurrencyAmount			
			derive value from GLTransactionDetailRel.TransactionAmount 
			default label is "DistributionAmount"
        GLBaseAmount				is a FinanceCurrencyAmount
        	derive value from GLTransactionDetailRel.ReportCurrencyAmount
		TransientRunGroup 			is AlphaUpper size 30
			derive value from GLTransactionDetailRel.JournalizeGroup
		TransientReferenceToThisInstance	is BusinessObjectReference
			derive value from reference to this instance
		BypassBudgetEditing							is Boolean
		BypassBudgetAndCommitmentProcessing			is Boolean
		BypassNegativeRateEdit
		BypassUnpostBankStatement					is Boolean	
						
	Local Fields
		LocalDocumentCurrencyTotals		is a DocumentCurrencyTotals
		LocalKeepRateOnly				is Boolean
		OldGLTransactionAmount			is an InternationalAmount
		LocalExchangeDate			 	is an ExchangeDate
					 	
	Derived Fields
		OutOfBalance is a ConditionalField
			type is Boolean
			if (NonCashLedgerTransaction.OutOfBalanceAmount	entered)
				true
			else
				false

		SignedDistributionAmount is a DerivedField
			type is like InternationalAmount
			restricted
			if (NonCashLedgerTransaction.DebitCreditIndicator.Debit)
				return DistributionAmount
			else
				return (DistributionAmount	* -1)

		DerivedCommentLinkName is a DerivedField
			type is Alpha 12
			restricted
			if (Comment entered)
				return ViewCommentMessage
			else
				return AddCommentMessage

		AddCommentMessage is a MessageField
			restricted
			"AddComment"		

		ViewCommentMessage is a MessageField
			restricted
			"ViewComment"		

		NonCashLedgerDistributionPostingMessage is a MessageField 
			restricted
			"NonCashLedgerDistributionPosting"

		DerivedPeriodEndDate	is a DerivedField 
			type is Date
			restricted
			return CurrentGeneralLedgerCalendarPeriodRel.Date
		
	Field Rules
		PostingDate	
			force default to NonCashLedgerTransaction.BankStatement.CashLedgerPostingDate
			
		BankStatement
			force default to NonCashLedgerTransaction.BankStatement

		StatementDate
			force default to NonCashLedgerTransaction.BankStatement.StatementDate




		DistributionAccount
			force default to GLTransactionDetailRel.FinanceCodeBlock
		
		DistributionAmount
			force default to GLTransactionAmount

		GLFinanceCodeBlock		
			LocalExchangeDate		= StatementDate	

        GLBaseAmount
        	required
        	TransientFromCurrency	= CashManagementAccount.Currency
			LocalExchangeDate		= StatementDate
        	BypassNegativeRateEdit	= true
            
	Delete Rules
		if (GLTransactionDetailRel exist) 
			invoke Delete GLTransactionDetailRel	
		invoke DeleteCBCommitment
																			
	Rule Blocks
		SaveDocumentCurrencyTotals
			LocalDocumentCurrencyTotals 				  = NonCashLedgerTransaction.BaseTotalDistributionAmount
			LocalDocumentCurrencyTotals.OldCurrencyAmount = GLTransactionDetailRel.ReportCurrencyAmount

		CreateGLTransactionDetail
			invoke Create GLTransactionDetail		 
				invoked.OriginatingTransaction 	= reference to this instance
				invoked.FinanceEnterpriseGroup	= CashManagementGroup  
				invoked.CurrencyCode			= CashManagementAccount.Currency
				invoked.System					= "CB"
				invoked.TransactionAmount		= GLTransactionAmount
				invoked.FinanceCodeBlock		= GLFinanceCodeBlock
				invoked.ReportCurrencyAmount 	= GLBaseAmount

				invoked.Description			  	= CashManagementAccount.JournalDescription		
            	invoked.GeneralLedgerEvent 	    = "BT"
				invoked.AccountingEntity		= CashManagementAccount.LegalEntity.AccountingEntity
				invoked.TransactionDate			= StatementDate
				invoked.PostingDate				= PostingDate 
				invoked.ControlDocumentNumber 	= BankStatement
				invoked.DocumentNumber		  	= BankStatement
				invoked.AutoReverse 			= false
				invoked.ReportCurrencyAmount.KeepRateOnly = true

		UpdateGLTransactionDetail
			invoke Update GLTransactionDetailRel


				invoked.TransactionAmount					= GLTransactionAmount
				invoked.FinanceCodeBlock					= GLFinanceCodeBlock
				invoked.ReportCurrencyAmount 				= GLBaseAmount
				invoked.TransactionDate						= StatementDate
				invoked.PostingDate							= PostingDate 
				invoked.ReportCurrencyAmount.KeepRateOnly	= LocalKeepRateOnly

		ProcessBudgetAndCommitments
			if (GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit
	        or  GeneralLedgerSystemCodeRel.EncumbranceOption.Track)
				if (GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit)

		       		invoke PerformBudgetEdit
				invoke CreateCommitment

	Relations
		GLTransactionDetailRel	
			one-to-one relation to GLTransactionDetail
			valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction		 

				related.OriginatingTransaction	= reference to this instance			

		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup          = CashManagementGroup
				related.GeneralLedgerSystemCode         = "CB"

		GLCommitRel
			one-to-one relation to GLCommit
      		valid when (!action type.Create)
			Field Mapping uses ByOriginatingTransaction
				related.OriginatingTransaction  = reference to this instance

		BudgetEditErrorRel
			one-to-many relation to BudgetEditError
			Field Mapping uses ByBudgetGroup
				related.FinanceEnterpriseGroup              = CashManagementGroup
				related.BudgetEditError.BudgetEditGroup     = UniqueID  

 		CurrentGeneralLedgerCalendarPeriodRel	
			one-to-one relation to GeneralLedgerCalendarPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= CashManagementGroup
				related.GeneralLedgerCalendarPeriod			= CashManagementAccount.LegalEntity.AccountingEntity.CurrentPeriod.GeneralLedgerCalendarPeriod	
									
	Sets
		ByDistributionStatus
			Sort Order
				CashManagementGroup
				CashManagementAccount
				BankStatement
				Status
				PostingDate
				StatementDate
				NonCashLedgerTransaction
				NonCashLedgerDistribution
				
		ByDistributionStatusByAccount
			Sort Order
				CashManagementGroup
				CashManagementAccount
				Status
				PostingDate
				StatementDate
				BankStatement
				NonCashLedgerTransaction
				NonCashLedgerDistribution

		ByDistributionStatusByCashManagementGroup
			Sort Order
				CashManagementGroup
				Status
				CashManagementAccount
				BankStatement
				NonCashLedgerTransaction
				NonCashLedgerDistribution
	
	Conditions
		DistributionSearchCondition
			restricted
            when (!StatementDateRange entered
            or    StatementDateRange entered
            and   StatementDate within StatementDateRange)
			
		CanViewCommentLink
			restricted
			when (DistributionExists
			and  (Status.Entered
			or    Comment entered))
			
		DistributionExists
			restricted
			when (NonCashLedgerDistribution exists)

		BudgetErrorExists
			restricted
			when (BudgetEditErrorRel exists)
			
		CommitmentExists
			restricted
			when (GLCommitRel exists)
			
		ReleasedCommitmentExists
			restricted
			when (CommitmentExists
			and   GLCommitRel.Status = 3)





		IsUnpostedForPeriodClose	
			restricted
			when (Status.Released
			and   PostingDate <= CurrentGeneralLedgerCalendarPeriodRel.Date) 

		IsPendingApprovalForPeriodClose	
			restricted
			when (Status.PendingApproval
			and   PostingDate <= CurrentGeneralLedgerCalendarPeriodRel.Date) 
						
		OutstandingNonCashLedgerDistributionsExist	
			restricted
			when (IsUnpostedForPeriodClose
			or    IsPendingApprovalForPeriodClose)

  	Actions
		RejectAllStatementDistributions is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
				PrmCashManagementAccount	is a CashManagementAccount
				PrmBankStatement			is a BankStatement
					
			Parameter Rules
				PrmCashManagementAccount	required
				PrmBankStatement			required
				
			Instance Selection
				where (CashManagementGroup			= PrmCashManagementGroup
				and    CashManagementAccount		= PrmCashManagementAccount
				and    BankStatement				= PrmBankStatement
				and    Status.PendingApproval)
			
			Action Rules
				Instance Rules
					invoke RejectDistributionRelease PendingApproval NonCashLedgerDistribution

















































































































































































 


		ApplyRoundingAdjustment is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				FunctionalAmountAdjustment			is an InternationalAmount
			Action Rules
				GLBaseAmount 											 = GLTransactionDetailRel.ReportCurrencyAmount 
				GLBaseAmount.FunctionalAmount.EnteredCurrencyAmount		+= FunctionalAmountAdjustment
				include SaveDocumentCurrencyTotals
				LocalKeepRateOnly	= false
				include UpdateGLTransactionDetail
				LocalDocumentCurrencyTotals.NewCurrencyAmount	= GLTransactionDetailRel.ReportCurrencyAmount												        			
				LocalDocumentCurrencyTotals.PerformUpdate		= true
				if (LocalDocumentCurrencyTotals.UpdateTotals)
					invoke UpdateDocumentTotals NonCashLedgerTransaction	 											 
						invoked.PrmDocumentCurrencyTotals = LocalDocumentCurrencyTotals


		JournalizeDistributions is a Set Action		
			completion message is "NonCashLedgerDistributionPostingCompleteForJournalizeGroup...:<LocalJournalizeGroup>"
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
					default label is "CashManagementGroup"
				PrmCashManagementAccount	is a CashManagementAccount
					default label is "CashAccount"					
				PrmPostThruDate				is Date
					default label is "PostThruDate"
				PrmDescription			is a Description
					default label is "Description"

			Parameter Rules


				PrmPostThruDate		 
					required
				PrmDescription
					initial value is NonCashLedgerDistributionPostingMessage

			Local Fields
				CompletionMessage			is Alpha 150
				LocalJournalizeGroup		is a JournalizeGroup
				LocalSystemCode            	is a GeneralLedgerSystemCode
				LocalCashManagementGroup	is like CashManagementGroup
				
			Instance Selection
				where (Status.Released
				and   (CashManagementAccount	= PrmCashManagementAccount
				or     PrmCashManagementAccount not entered)
				and    StatementDate		   <= PrmPostThruDate)
				
			Sort Order  
				CashManagementGroup
				Status
				StatementDate
				CashManagementAccount

			Accumulators

			Action Rules								
				Empty Set Rules
					CompletionMessage = "NonCashLedgerDistributionPostingCompleteForJournalizeGroup...:NoRecordsFoundToJournalize<LocalJournalizeGroup>"

				Set Rules
					Entrance Rules
						LocalSystemCode 		 = "CB" 
    					increment LocalSystemCode.LastJournalizeGroup
            			LocalJournalizeGroup	 = LocalSystemCode.DerivedJournalizeGroup
						
						LocalCashManagementGroup = PrmCashManagementGroup					





					Exit Rules
						invoke InitiateJournalizeForRunGroup PrmCashManagementGroup.FinanceEnterpriseGroup in background
							invoked.PrmJournalizeGroup	= LocalJournalizeGroup
							invoked.PrmJournalizeGroupDescription	= PrmDescription
						
						LocalCashManagementGroup = PrmCashManagementGroup
						CompletionMessage = "NonCashLedgerDistributionPostingCompleteForJournalizeGroup...:<LocalJournalizeGroup>"



				CashManagementAccount Set Rules
					Exit Rules
						invoke UpdateStatementPostedStatus CashManagementAccount

				Instance Rules
					if (GLTransactionDetailRel exist)
						invoke UpdateJournalizeGroup GLTransactionDetailRel	 
							invoked.PrmJournalizeGroup = LocalJournalizeGroup  

  						make transition to Posted


 		CreateCommitment is an Instance Action
			default label is untranslatable
			restricted
			Exit Rules
				invoke CreateCommitment GLCommit
					invoked.HeaderUniqueID 					= NonCashLedgerTransaction.UniqueID
					invoked.AccountingEntity				= CashManagementAccount.LegalEntity.AccountingEntity
					invoked.FinanceEnterpriseGroup			= CashManagementGroup
					invoked.System							= "CB"
					invoked.FinanceCodeBlock				= GLFinanceCodeBlock
					invoked.CurrencyCode					= CashManagementAccount.Currency
					invoked.TransactionAmount				= GLTransactionAmount
					invoked.ReportCurrencyAmount			= GLBaseAmount     
					invoked.TransactionDate					= LocalExchangeDate	
					invoked.OriginatingTransaction			= reference to this instance
					invoked.DimensionCode					= GLFinanceCodeBlock.DimensionCode
					invoked.TransBusinessObjectRef			= reference to NonCashLedgerTransaction
					invoked.PrmAllowRebuild                 = true		

		RecalculateCommitmentAmounts is an Instance Action				
			default label is untranslatable
			restricted
			Action Rules			
				invoke Purge GLCommitRel
					invoked.PrmPurgeRecalculate = true	
				LocalExchangeDate		= StatementDate				
				invoke CreateCommitment
					
		PerformBudgetEdit is an Instance Action
			default label is untranslatable
			restricted			
			Action Rules
				invoke CheckTransaction BudgetTemplate		
					invoked.PrmFinanceEnterpriseGroup   		= CashManagementGroup
					invoked.PrmBudgetEditGroup					= UniqueID
					invoked.PrmHeaderUniqueID                   = NonCashLedgerTransaction.UniqueID
					invoked.PrmTransactionCodeBlock				= GLFinanceCodeBlock
					invoked.PrmDate								= StatementDate
					invoked.PrmReportAmounts.FunctionalAmount 	= GLBaseAmount.FunctionalAmount.EnteredCurrencyAmount
					invoked.PrmReportAmounts.ReportAmount1      = GLBaseAmount.ReportAmount1.EnteredCurrencyAmount
					invoked.PrmReportAmounts.ReportAmount2      = GLBaseAmount.ReportAmount2.EnteredCurrencyAmount
					invoked.PrmReportAmounts.ReportAmount3      = GLBaseAmount.ReportAmount3.EnteredCurrencyAmount
					invoked.PrmReportAmounts.ReportAmount4      = GLBaseAmount.ReportAmount4.EnteredCurrencyAmount
					invoked.PrmReportAmounts.ReportAmount5      = GLBaseAmount.ReportAmount5.EnteredCurrencyAmount
					invoked.PrmReportAmounts.AlternateAmount    = GLBaseAmount.AlternateAmount.EnteredCurrencyAmount
					invoked.PrmReportAmounts.AlternateAmount2   = GLBaseAmount.AlternateAmount2.EnteredCurrencyAmount
					invoked.PrmReportAmounts.AlternateAmount3   = GLBaseAmount.AlternateAmount3.EnteredCurrencyAmount
    			if (BudgetErrorExists)
					confirmation required   
                    	"Warning:BudgetsHaveBeenExceeded;ViewDetailsInBudgetErrorPanel;IfNotAddressed,ReleaseWillNotBeAllowed;Continue?"


		MaintainUnreleasedCommitment is an Instance Action
			default label is untranslatable
			restricted
			Exit Rules
				invoke MaintainUnreleasedCommitment GLCommitRel
					invoked.TransactionAmount        = GLTransactionAmount
					invoked.ReportCurrencyAmount     = GLBaseAmount
					invoked.TransactionDate          = StatementDate
					invoked.AccountingEntity         = CashManagementAccount.LegalEntity.AccountingEntity
					invoked.FinanceCodeBlock         = GLFinanceCodeBlock
					invoked.CurrencyCode             = CashManagementAccount.Currency

		DeleteCBCommitment is an Instance Action 
			default label is untranslatable
			restricted
			Action Rules
				if (CommitmentExists)
					invoke Purge GLCommitRel
				if (BudgetErrorExists)
					invoke Delete BudgetEditErrorRel
					
		Purge is a Purge Action
			restricted
			Entrance Rules
				invoke PurgeValidSubLedgerTrx GLTransactionDetailRel
																			
  	StateCycles
		StatementDistributionLifeCycle is a StateCycle
			state field is Status
		
			Entered is a State
		  		Create is a Create Action
		  			valid when (!BankStatement.PostingStatus.PendingApproval)
					Entrance Rules
						include SaveDocumentCurrencyTotals
					Action Rules
					Exit Rules	
						include CreateGLTransactionDetail
						if (!CashAccountDistribution)
							LocalDocumentCurrencyTotals.NewCurrencyAmount	= GLTransactionDetailRel.ReportCurrencyAmount	 
							LocalDocumentCurrencyTotals.PerformUpdate		= true
							increment NonCashLedgerTransaction.TransactionDistributionAmount by GLTransactionAmount
							if (LocalDocumentCurrencyTotals.UpdateTotals)
								invoke UpdateDocumentTotals NonCashLedgerTransaction	 											 
									invoked.PrmDocumentCurrencyTotals = LocalDocumentCurrencyTotals
						if (!BypassBudgetAndCommitmentProcessing)
							include ProcessBudgetAndCommitments	





						if (!BypassUnpostBankStatement)				
							invoke UnpostBankStatement BankStatement	
						
		  		Update is an Update Action
					Entrance Rules
						constraint (!NonCashLedgerTransaction.BudgetEditInProgress)		
							"CannotUpdate;BudgetEditInProgress"
						include SaveDocumentCurrencyTotals
						OldGLTransactionAmount	= GLTransactionDetailRel.TransactionAmount
					Action Rules
						LocalKeepRateOnly = true
						include UpdateGLTransactionDetail
						LocalDocumentCurrencyTotals.NewCurrencyAmount	= GLTransactionDetailRel.ReportCurrencyAmount	 
						LocalDocumentCurrencyTotals.PerformUpdate		= true
						increment NonCashLedgerTransaction.TransactionDistributionAmount by (GLTransactionAmount - OldGLTransactionAmount)
						if (LocalDocumentCurrencyTotals.UpdateTotals)
							invoke UpdateDocumentTotals NonCashLedgerTransaction	 											 
								invoked.PrmDocumentCurrencyTotals = LocalDocumentCurrencyTotals
					Exit Rules			
						if (CommitmentExists)
							if (GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit)
								if (GLFinanceCodeBlock	!= GLCommitRel.FinanceCodeBlock
								or  GLTransactionAmount != GLCommitRel.TransactionAmount
								or  GLBaseAmount 		!= GLCommitRel.ReportCurrencyAmount.FunctionalAmount.EnteredCurrencyAmount)
									invoke Delete BudgetEditErrorRel
									invoke PerformBudgetEdit
							invoke MaintainUnreleasedCommitment										
		
		  		Delete is a Delete Action
					Entrance Rules
						constraint (!NonCashLedgerTransaction.BudgetEditInProgress)		
							"CannotDelete;BudgetEditInProgress"
						include SaveDocumentCurrencyTotals
						if (!CashAccountDistribution)
							decrement NonCashLedgerTransaction.TransactionDistributionAmount by GLTransactionAmount
						LocalDocumentCurrencyTotals.PerformUpdate		= true
						if (LocalDocumentCurrencyTotals.UpdateTotals)
							invoke UpdateDocumentTotals NonCashLedgerTransaction	 											 
								invoked.PrmDocumentCurrencyTotals = LocalDocumentCurrencyTotals




		  			Exit Rules
		  				invoke UpdatePostedStatus BankStatement
		  		
		  		ReleaseDistribution is an Instance Action
		  			default label is untranslatable
		  			restricted
		  			Action Rules
						if (GLTransactionDetailRel exist)
							invoke Release GLTransactionDetailRel
		  				make transition to Released
		  		
		  		SetToPendingApproval is an Instance Action
		  			default label is untranslatable
		  			restricted
		  			Action Rules
		  				make transition to PendingApproval

		  	PendingApproval is a State
		  		
		  		ReleaseDistribution is an Instance Action
		  			default label is untranslatable
		  			restricted
		  			Action Rules
						if (GLTransactionDetailRel exist)
							invoke Release GLTransactionDetailRel
		  				make transition to Released

		  		RejectDistributionRelease is an Instance Action
		  			default label is untranslatable
		  			restricted
		  			Action Rules
		  				make transition to Entered

		  	Released is a State
		  	









		  				
		  	Posted is a State











