PfiTrigger is a BusinessClass
    owned by pfi
    prefix is PfiTR
    default label is "Trigger"
	framework type is ProcessFlow
    disable data area copy
		preserve target data
	
    Ontology
        symbolic key is PfiTrigger

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements CompoundDocument
            Document Components
                PfiTriggerFolder set
                PfiTriggerVariable set
		implements AuditLogEntryActions                

    Persistent Fields

        FlowName          is a PfiFlowDefinition
            default label is "ProcessName"
        ServiceName       is a PfiServiceDefinition
        WorkTitle         is a PfiWorkTitle
        Criteria1         is a PfiCriterion
        Criteria2         is a PfiCriterion
        Criteria3         is a PfiCriterion
        PfiFilterKey
        FilterValue       is a PfiFilterValue
        AppsKey           is a PfiAppsKey
        AppsValue         is a PfiAppsValue
        LastTriggerFolder is Numeric size 6
        TriggeredWorkunit is like PfiWorkunit
        	default label is "TriggeredWorkUnit"

    Derived Fields

        MapField1 is a DerivedField
            type is LPLName
            if (ServiceName entered)
                return ServiceName
            else
                return FlowName

    Conditions

        IsFlowTrigger
            default label is "TriggerTypeIsProcess"
            when (FlowName != blank)

        IsServiceTrigger
            default label is "TriggerTypeIsService"
            when (ServiceName != blank)

    Relations

        PfiWorkunitRel
            one-to-one relation to PfiWorkunit
            Field Mapping uses symbolic key
                related.PfiWorkunit = TriggeredWorkunit

    Sets

        ByFlowName
            sql name is PfiTR01
            not indexed
            Sort Order
                FlowName
                PfiTrigger

        ByServiceName
            sql name is PfiTR02
            not indexed
            Sort Order
                ServiceName
                PfiTrigger

        ByWorkTitle
            sql name is PfiTR03
            not indexed
            Sort Order
                WorkTitle
                PfiTrigger

    Actions

        Create is an Action

        Update is an Action

        Delete is an Action

        RemoveDuplicates is an Instance Action










        Schedule is an Instance Action
            run in background

            Queue Mapping Fields
                MapField1

            Action Rules
                invoke Start

        Start is an Instance Action

            Local Fields
                NewWorkunit is a PfiWorkunit view

            Action Rules
                if (ServiceName entered)
                    invoke CreateWorkunitByService PfiWorkunit
                        assign result to NewWorkunit
                        invoked.AppsKey              = AppsKey
                        invoked.AppsValue            = AppsValue
                        invoked.PrmServiceDefinition = ServiceName
                        invoked.WorkTitle            = WorkTitle
                        invoked.FilterKey            = PfiFilterKey
                        invoked.FilterValue          = FilterValue
                        invoked.Criterion1           = Criteria1
                        invoked.Criterion2           = Criteria2
                        invoked.Criterion3           = Criteria3
                        invoked.SourceType           = "Landmark"   
                        invoked.Source               = "PfiTrigger" 
                else
                    invoke CreateWorkunitByFlow PfiWorkunit
                        assign result to NewWorkunit
                        invoked.AppsKey        = AppsKey
                        invoked.AppsValue      = AppsValue
                        invoked.FlowDefinition = FlowName
                        invoked.WorkTitle      = WorkTitle
                        invoked.FilterKey      = PfiFilterKey
                        invoked.FilterValue    = FilterValue
                        invoked.SourceType     = "Landmark"   
                        invoked.Source         = "PfiTrigger" 

                for each PfiTriggerFolder set
                    invoke Create PfiWorkunitFolder
                        invoked.PfiWorkunit = NewWorkunit.PfiWorkunit
                        invoked.DocType     = each.DocType
                        invoked.Value       = each.Value
                        invoked.Name        = each.Name
                        invoked.AttachDate  = each.AttachDate
                        invoked.AttachBy    = each.AttachBy

                for each PfiTriggerVariable set
                    invoke Create PfiWorkunitVariable
                        invoked.PfiWorkunit         = NewWorkunit.PfiWorkunit
                        invoked.PfiWorkunitVariable = each.PfiTriggerVariable
                        invoked.VariableType        = each.VariableType
                        invoked.VariableValue       = each.VariableValue

                invoke Release NewWorkunit.PfiWorkunit
                TriggeredWorkunit = NewWorkunit.PfiWorkunit

        StartWithInputData is an Instance Action
            restricted

            Local Fields
                NewWorkunit is a PfiWorkunit view

            Parameters
                FlowName          is a PfiFlowDefinition
                WorkTitle         is a PfiWorkTitle
                InputDescription  is a PfiDescription
                InputTextData     is Text

            Action Rules
                invoke CreateWorkunitByFlow PfiWorkunit
                    assign result to NewWorkunit
                    invoked.FlowDefinition = FlowName
                    invoked.WorkTitle      = WorkTitle
                    invoked.SourceType     = "Landmark"   
                    invoked.Source         = "PfiTrigger" 

                if (InputDescription != blank )
                    invoke Create PfiWorkunitInputData
                        invoked.PfiWorkunit = NewWorkunit.PfiWorkunit
                        invoked.Description = InputDescription
                        invoked.TextData    = InputTextData

                invoke Release NewWorkunit.PfiWorkunit
                TriggeredWorkunit = NewWorkunit.PfiWorkunit
