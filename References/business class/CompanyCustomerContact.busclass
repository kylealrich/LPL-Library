CompanyCustomerContact is a BusinessClass
	owned by ar
	prefix is CON
	classic name is ARCONTACTS

	Ontology
		symbolic key is CompanyCustomerContact
			classic set name is CONSET1
			classic name is RESP-CODE

	Patterns
		implements StaticJava
		disable AuditIndex
		implements BODId

	Persistent Fields
		Name
		PhoneNumber		is a TelephoneNumber 
			holds pii
			classic name for PhoneNumber.InternationalPrefix is INT-PREFIX
			classic name for PhoneNumber.SubscriberNumber is PHONE-NMBR
			classic name for PhoneNumber.Extension is PHONE-EXT
		FaxNumber
			classic name for FaxNumber.SubscriberNumber is FAX-NMBR
			classic name for FaxNumber.Extension is FAX-EXT
		Description
			classic name is DESC
		EmailAddress	 							is an EmailAddressMulti 
			holds pii
			classic name is E-MAIL-ADDRESS

		EmailReceivablesStatement				is Boolean
			default label is "ReceivablesStatement"
		EmailBasicDunningLetter					is Boolean
			default label is "BasicDunningLetter"
		EmailAdvancedDunningLetter				is Boolean
			default label is "AdvancedDunningLetter"
		EmailPaymentRequestNotice				is Boolean
			default label is "PaymentRequestNotice"
		EmailBillingInvoice						is Boolean
			default label is "BillingInvoice"
		EmailEFTNotificationPrint				is Boolean
			default label is "EFTNotificationPrint"
		EmailReceivableInvoice					is Boolean
			default label is "ReceivableInvoice"
		EmailSubleaseBillingInvoice				is Boolean
			default label is "SubleaseBillingInvoice"
		EmailPaymentReceipt						is Boolean
			default label is "PaymentReceipt"
		EmailPrepaymentReceipt					is Boolean
			default label is "PrepaymentReceipt"

	Context Fields
		FSMInboundBODTracker


	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforContactMaster"

		ContextMessageText is a MessageField
			restricted
			"CustomerContactForCustomer<Customer>OfCompany<Company>"
		
		DerivedCompany	is a DerivedField 
			type is AlphaUpper size 4
			restricted
			return Company
		

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		BODCurrentTimeStamp is a DerivedField
			type is TimeStamp
			restricted
			BODCurrentTimeStamp = system current timestamp
			return BODCurrentTimeStamp
		
		AlphaCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			return BODCurrentTimeStamp	
			
		BODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			return AlphaCurrentTimeStamp[1:4] + "-" + AlphaCurrentTimeStamp[5:6] + "-" + AlphaCurrentTimeStamp[7:8] + "T" + AlphaCurrentTimeStamp[9:10] + ":" + AlphaCurrentTimeStamp[11:12] + ":" + AlphaCurrentTimeStamp[13:14] + "Z"	
			
		DerivedBODrevision is a DerivedField
			type is Alpha size 25	
			restricted
			return ""
			
		DerivedBODlocation is a DerivedField
			type is Alpha size 25	
			restricted
			return ""
			
		DerivedbodID is a DerivedField
			type is Alpha size 100	
			restricted
			return "infor-nid:"+DerivedTenantID+":"+DerivedBODaccountingEntity+":"+DerivedBODlocation+":"+DerivedDocumentID+":"+DerivedBODrevision+"?ContactMaster&verb=Sync&TrackerID="+ LocalFSMInboundBODTracker
		
		DerivedBODaccountingEntity is a DerivedField
			type is Alpha size 20
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedAECrossAccountingEntity
			else
				return DerivedFinanceEnterpriseGroup + DerivedDelimiter + DerivedAE
		
		DerivedBODActionCode is a DerivedField	
			type is Alpha size 10
		 	restricted	
		 	if (action type.Create or ActionCode.Create)
		 		return "Add"
		 	else
		 	if (action type.Update or action type.Delete)
				return "Replace"
			else 
				return ""
		
		DerivedVariationID is a DerivedField
			type is Alpha size 25	
			restricted
			return bod id.VariationID
							
						
		DerivedFinanceEnterpriseGroup is a DerivedField 
			type is AlphaUpper size 4
			restricted
			return GeneralLedgerCompanyRel.FinanceEnterpriseGroup
			
		DerivedAE is a DerivedField 
			type is AlphaUpper size 4
			restricted
			return GeneralLedgerCompanyRel.AccountingEntity
			
		DerivedConfigFEG is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "IsMultipleFEG"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedConfigDocID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "RequiredCleanDocumentID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedCrossGLC is a DerivedField
			type is Alpha size 50
			restricted
			if (DerivedConfigFEG = "Y")
				return DerivedFinanceEnterpriseGroup + DerivedDelimiter + DerivedBODCompany
			else
				return DerivedBODCompany
				
		DerivedAECrossAccountingEntity is a DerivedField
			type is Alpha size 50
			restricted
			if(LocalAEAlreadyExecuted != true)
				LocalAEAlreadyExecuted = true
				if (FSMBODAECrossReferenceDetailRel.DestinationValue entered)
					LocalCrossAE = FSMBODAECrossReferenceDetailRel.DestinationValue
				else
					LocalCrossAE = DerivedCrossGLC
				return LocalCrossAE
			else
				return LocalCrossAE
		
		DerivedBODCompany is a DerivedField
		 	type is Alpha size 4
		 	restricted
		 	return Company using "%d"
		
		DerivedBODCustomer is a DerivedField
		 	type is Alpha size 9
		 	restricted
		 	return Customer using "%d"
		
		DerivedDocumentID is a DerivedField 
			type is AlphaUpper size 30
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedBODCustomer + DerivedDelimiter + CompanyCustomerContact
			else
				return DerivedBODCompany + DerivedDelimiter + DerivedBODCustomer  + DerivedDelimiter + CompanyCustomerContact		 		
		
		DerivedDisplayID is a DerivedField 
			type is Alpha size 30
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedBODCustomer + DerivedDelimiter + CompanyCustomerContact
			else
				return DerivedBODCustomer + "-" + CompanyCustomerContact
			
		DerivedstatusCode is a DerivedField 
			type is Alpha size 30
			restricted
			if (action type.Delete or ActionCode.Delete)	
				return "Deleted"
			else
			if (action type.Create or action type.Update)
				return "Open"
			else 
				return ""
		
		DerivedPInternationalPrefix is a DerivedField
			type is Alpha size 6
			restricted
			return PhoneNumber.InternationalPrefix	
			
		DerivedPSubscriberNumber is a DerivedField
			type is Alpha size 15
			restricted
			return PhoneNumber.SubscriberNumber
			
		DerivedPExtension is a DerivedField
			type is Alpha size 10
			restricted
			return PhoneNumber.Extension
		
		DerivedFSubscriberNumber is a DerivedField
			type is Alpha size 15
			restricted
			return FaxNumber.SubscriberNumber
				
		DerivedFExtension is a DerivedField
			type is Alpha size 10
			restricted
			return FaxNumber.Extension
		
		DerivedCustomerID is a DerivedField
			type is Alpha size 30
			restricted
			if (DerivedConfigDocID = "Y")
				return DerivedBODCustomer
			else
				return DerivedBODCompany + DerivedDelimiter + DerivedBODCustomer
			
		ContactMasterARXMLBOD is a DerivedField 			
			type is XMLDocument
			restricted
			if (DerivedConfigDocID = "Y")
				ContactMasterARXMLBOD = template.IONContactMasterARXML_CleanDoc_ST document for this instance	
			else
				ContactMasterARXMLBOD = template.IONContactMasterARXML_ST document for this instance
	Local Fields
		LocalTitle 									is Alpha size 255
		ActionCode									is Alpha size 1
			States
				Create  value is "C"
				Update	value is "U"
				Delete	value is "D"
		LocalCounter								is Numeric size 6
		NativeLPLBODTrigger							is Boolean			
		LocalMainUserTemplate			is Alpha 250
		NewBODTracker  				is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker	is Numeric 15
		Error						is Boolean
		ErrorMessage	 			is Alpha 300
		LocalBODTrigger				is Boolean
		LocalConfigurationParameter	is Alpha size up to 200
		LocalAEAlreadyExecuted		is Boolean
		LocalCrossAE				is Alpha size 30
		LocalDocumentTypes			is Alpha 300
		LocalHasConfirmationMsg		is Boolean
		LocalBillingInvoiceEmailOnly			is Boolean
		LocalBasicDunningLetterEmailOnly		is Boolean
		LocalAdvancedDunningLetterEmailOnly		is Boolean
		LocalReceivablesStatementEmailOnly		is Boolean
		LocalPaymentRequestNoticeEmailOnly		is Boolean
		LocalEFTNotificationPrintEmailOnly		is Boolean
		LocalReceivableInvoiceEmailOnly			is Boolean
		LocalPaymentReceiptEmailOnly			is Boolean
		LocalPrepaymentReceiptEmailOnly			is Boolean
		LocalSubleaseBillingInvoiceEmailOnly	is Boolean
		
	Field Rules
		Name
			required
			

		EmailAddress
			if (EmailReceivablesStatement
			or  EmailBasicDunningLetter
			or  EmailAdvancedDunningLetter
			or  EmailPaymentRequestNotice
			or  EmailBillingInvoice
			or  EmailEFTNotificationPrint
			or  EmailReceivableInvoice
			or  EmailSubleaseBillingInvoice
			or  EmailPaymentReceipt
			or  EmailPrepaymentReceipt)
				required
			
		EmailReceivablesStatement
			if (EmailReceivablesStatement)
				constraint (CompanyCustomerRel.EmailReceivablesStatement)
					"EmailFunctionalityForReceivablesStatementMustBeEnabledOnCompanyCustomer"
					
		EmailBasicDunningLetter
			if (EmailBasicDunningLetter)
				constraint (CompanyCustomerRel.EmailBasicDunningLetter)
					"EmailFunctionalityForBasicDunningLetterMustBeEnabledOnCompanyCustomer"
					
		EmailAdvancedDunningLetter
			if (EmailAdvancedDunningLetter)
				constraint (CompanyCustomerRel.EmailAdvancedDunningLetter)
					"EmailFunctionalityForAdvancedDunningLetterMustBeEnabledOnCompanyCustomer"
					
		EmailPaymentRequestNotice
			if (EmailPaymentRequestNotice)
				constraint (CompanyCustomerRel.EmailPaymentRequestNotice)
					"EmailFunctionalityForPaymentRequestNoticeMustBeEnabledOnCompanyCustomer"
					
		EmailBillingInvoice
			if (EmailBillingInvoice)
				constraint (CompanyCustomerRel.EmailBillingInvoice)
					"EmailFunctionalityForBillingInvoiceMustBeEnabledOnCompanyCustomer"
		
		EmailEFTNotificationPrint
			if (EmailEFTNotificationPrint)
				constraint (CompanyCustomerRel.EmailEFTNotificationPrint)
					"EmailFunctionalityForEFTNotificationPrintMustBeEnabledOnCompanyCustomer"				
					
		EmailReceivableInvoice
			if (EmailReceivableInvoice)
				constraint (CompanyCustomerRel.EmailReceivableInvoice)
					"EmailFunctionalityForReceivableInvoiceMustBeEnabledOnCompanyCustomer"
					
		EmailSubleaseBillingInvoice
			if (EmailSubleaseBillingInvoice)
				constraint (CompanyCustomerRel.EmailSubleaseBillingInvoice)
					"EmailFunctionalityForSubleaseBillingInvoiceMustBeEnabledOnCompanyCustomer"

		EmailPaymentReceipt
			if (EmailPaymentReceipt)
				constraint (CompanyCustomerRel.EmailPaymentReceipt)
					"EmailFunctionalityForPaymentReceiptMustBeEnabledOnCompanyCustomer"

		EmailPrepaymentReceipt
			if (EmailPrepaymentReceipt)
				constraint (CompanyCustomerRel.EmailPrepaymentReceipt)
					"EmailFunctionalityForPrepaymentReceiptMustBeEnabledOnCompanyCustomer"

	Relations

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company

		CompanyCustomerContactRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
				related.CompanyCustomerContact	= LocalCounter


		OtherBillingInvoiceEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailBillingInvoice
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherBasicDunningLetterEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailBasicDunningLetter
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherAdvancedDunningLetterEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailAdvancedDunningLetter
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherReceivablesStatementEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailReceivablesStatement
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherPaymentRequestNoticeEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailPaymentRequestNotice
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherEFTNotificationPrintEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailEFTNotificationPrint
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherReceivableInvoiceEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailReceivableInvoice
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherPaymentReceiptEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailPaymentReceipt
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherPrepaymentReceiptEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailPrepaymentReceipt
				and related.CompanyCustomerContact != CompanyCustomerContact)

		OtherSubleaseBillingInvoiceEmailOnlyContactsRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses ByCustomer
				related.Customer				= Customer
				related.Company					= Company
			Instance Selection
				where (related.EmailSubleaseBillingInvoice
				and related.CompanyCustomerContact != CompanyCustomerContact)

		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company  = Company
				related.Customer = Customer

		CustomerRel
			one-to-one relation to Customer
			Field Mapping uses symbolic key
				related.CustomerGroup = Company.CustomerGroupField.CustomerGroup
				related.Customer	  = Customer

		CustomerGroupRel
			one-to-one relation to CustomerGroup
			Field Mapping uses symbolic key
				related.CustomerGroup	= Company.CustomerGroupField.CustomerGroup	
				
		BillingCompanyRel
			one-to-one relation to BillingCompany
			Field Mapping uses symbolic key
				related.Company					= Company


				
		FSMBODConfigurationParameterRel
			one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
				related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
			one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
				related.FSMBODConfiguration.Verb 		= 1
				related.FSMBODConfiguration.Noun 		= "ContactMaster"
				related.FSMBODConfiguration.Direction 	= 1
				
		FSMBODAECrossReferenceDetailRel
			one-to-many relation to FSMBODCrossReferenceDetail
			Field Mapping uses symbolic key
				related.FSMBODCrossReference.SourceField 		= "CSFGLC"
				related.FSMBODCrossReference.DestinationField 	= "DESTAE"
			Instance Selection
				where (related.SourceValue	= DerivedCrossGLC )
		
		FSMInboundBODTrackerRel
			one-to-one relation to FSMInboundBODTracker
			Field Mapping uses symbolic key
				related.FSMInboundBODTracker	= LocalFSMInboundBODTracker



	Conditions
		BillingInvoiceEmailValidForUpdate
			restricted
			when (EmailBillingInvoice
			and BillingCompanyRel.UseIDM)

		ShowIDMEmailDistribution
			restricted
			when (CompanyCustomerRel.EmailBillingInvoice
			or	Company.UseIDM)

		NoCompanyCustomerContactWithBillingInvoiceEmail
			restricted
			when (not OtherBillingInvoiceEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailBillingInvoice changed and !EmailBillingInvoice)))

		NoCompanyCustomerContactWithBasicDunningLetterEmail
			restricted
			when (not OtherBasicDunningLetterEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailBasicDunningLetter changed and !EmailBasicDunningLetter)))

		NoCompanyCustomerContactWithAdvancedDunningLetterEmail
			restricted
			when (not OtherAdvancedDunningLetterEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailAdvancedDunningLetter changed and !EmailAdvancedDunningLetter)))

		NoCompanyCustomerContactWithReceivablesStatementEmail
			restricted
			when (not OtherReceivablesStatementEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailReceivablesStatement changed and !EmailReceivablesStatement)))

		NoCompanyCustomerContactWithPaymentRequestNoticeEmail
			restricted
			when (not OtherPaymentRequestNoticeEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailPaymentRequestNotice changed and !EmailPaymentRequestNotice)))

		NoCompanyCustomerContactWithEFTNotificationPrintEmail
			restricted
			when (not OtherEFTNotificationPrintEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailEFTNotificationPrint changed and !EmailEFTNotificationPrint)))

		NoCompanyCustomerContactWithReceivableInvoiceEmail
			restricted
			when (not OtherReceivableInvoiceEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailReceivableInvoice changed and !EmailReceivableInvoice)))

		NoCompanyCustomerContactWithPaymentReceiptEmail
			restricted
			when (not OtherPaymentReceiptEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailPaymentReceipt changed and !EmailPaymentReceipt)))

		NoCompanyCustomerContactWithPrepaymentReceiptEmail
			restricted
			when (not OtherPrepaymentReceiptEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailPrepaymentReceipt changed and !EmailPrepaymentReceipt)))

		NoCompanyCustomerContactWithSubleaseBillingInvoiceEmail
			restricted
			when (not OtherSubleaseBillingInvoiceEmailOnlyContactsRel exists 
			and (action type.Delete 
			or (EmailSubleaseBillingInvoice changed and !EmailSubleaseBillingInvoice)))

	Rule Blocks
		TriggerContactMasterARServiceRules
			LocalTitle = "EG:"+Company.FinanceEnterpriseGroup+" CO:"+Company+" CU:"+Customer+" CCC:"+CompanyCustomerContact
			increment bod id.VariationID
			trigger "ContactMasterARService" PA service
				resume on error
				title is "<LocalTitle>"
				Criteria
					Company.FinanceEnterpriseGroup
					Company
				Variables
					ActionCode
					include persistent fields from CompanyCustomerContact
					bod id.VariationID
						variable name is VariationId	
					Company.FinanceEnterpriseGroup
						variable name is FinanceEnterpriseGroup	
					Company.GeneralLedgerCompany.AccountingEntity
						variable name is AccountingEntity

		ContactNumberAutosequence
			LocalCounter = 1
			while (LocalCounter  <= 99999)
				if (CompanyCustomerContactRel exists)
					LocalCounter = LocalCounter + 1
				else
					end while
			constraint (!LocalCounter = 100000)
				"CannotCreate:MaximumCustomerContactNumberReached"
		
		ValidateAndUpdateCompanyCustomerDistributionEmailOnly
			initialize LocalDocumentTypes
			initialize LocalHasConfirmationMsg

			if (CompanyCustomerRel.BillingInvoiceEmailOption.ContactsOnly)
				LocalBillingInvoiceEmailOnly = true
			if (NoCompanyCustomerContactWithBillingInvoiceEmail and LocalBillingInvoiceEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Billing Invoice"
				LocalBillingInvoiceEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalBasicDunningLetterEmailOnly = CompanyCustomerRel.BasicDunningLetterEmailContactsOnly
			if (NoCompanyCustomerContactWithBasicDunningLetterEmail and LocalBasicDunningLetterEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Basic Dunning Letter"
				LocalBasicDunningLetterEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalAdvancedDunningLetterEmailOnly = CompanyCustomerRel.AdvancedDunningLetterEmailContactsOnly
			if (NoCompanyCustomerContactWithAdvancedDunningLetterEmail and LocalAdvancedDunningLetterEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Advanced Dunning Letter"
				LocalAdvancedDunningLetterEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalReceivablesStatementEmailOnly = CompanyCustomerRel.ReceivablesStatementEmailContactsOnly
			if (NoCompanyCustomerContactWithReceivablesStatementEmail and LocalReceivablesStatementEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Receivables Statement"
				LocalReceivablesStatementEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalPaymentRequestNoticeEmailOnly = CompanyCustomerRel.PaymentRequestNoticeEmailContactsOnly
			if (NoCompanyCustomerContactWithPaymentRequestNoticeEmail and LocalPaymentRequestNoticeEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Payment Request Notice"
				LocalPaymentRequestNoticeEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalEFTNotificationPrintEmailOnly = CompanyCustomerRel.EFTNotificationPrintEmailContactsOnly
			if (NoCompanyCustomerContactWithEFTNotificationPrintEmail and LocalEFTNotificationPrintEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "EFT Notification Print"
				LocalEFTNotificationPrintEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalReceivableInvoiceEmailOnly = CompanyCustomerRel.ReceivableInvoiceEmailContactsOnly
			if (NoCompanyCustomerContactWithReceivableInvoiceEmail and LocalReceivableInvoiceEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Receivable Invoice"
				LocalReceivableInvoiceEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalPaymentReceiptEmailOnly = CompanyCustomerRel.PaymentReceiptEmailContactsOnly
			if (NoCompanyCustomerContactWithPaymentReceiptEmail and LocalPaymentReceiptEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Payment Receipt Letter"
				LocalPaymentReceiptEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalPrepaymentReceiptEmailOnly = CompanyCustomerRel.PrepaymentReceiptEmailContactsOnly
			if (NoCompanyCustomerContactWithPrepaymentReceiptEmail and LocalPrepaymentReceiptEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Prepayment Receipt Letter"
				LocalPrepaymentReceiptEmailOnly = false
				LocalHasConfirmationMsg = true

			LocalSubleaseBillingInvoiceEmailOnly = CompanyCustomerRel.SubleaseBillingInvoiceEmailContactsOnly
			if (NoCompanyCustomerContactWithSubleaseBillingInvoiceEmail and LocalSubleaseBillingInvoiceEmailOnly)
				if (LocalDocumentTypes entered) 
					LocalDocumentTypes += ", "
				LocalDocumentTypes += "Sublease Billing Invoice"
				LocalSubleaseBillingInvoiceEmailOnly = false
				LocalHasConfirmationMsg = true
			
			if (LocalHasConfirmationMsg)
				confirmation required
					"To_Email_OptionWillBeSetTo_Company_CustomerAnd_ContactsFor_<LocalDocumentTypes>.DoYouWantToContinue?"
				
				invoke Update CompanyCustomerRel
					if (not LocalBillingInvoiceEmailOnly and this instance.CompanyCustomerRel.BillingInvoiceEmailOption.ContactsOnly)
						initialize invoked.BillingInvoiceEmailOption
					invoked.BasicDunningLetterEmailContactsOnly 		= LocalBasicDunningLetterEmailOnly
					invoked.AdvancedDunningLetterEmailContactsOnly 		= LocalAdvancedDunningLetterEmailOnly
					invoked.ReceivablesStatementEmailContactsOnly		= LocalReceivablesStatementEmailOnly
					invoked.PaymentRequestNoticeEmailContactsOnly		= LocalPaymentRequestNoticeEmailOnly
					invoked.EFTNotificationPrintEmailContactsOnly		= LocalEFTNotificationPrintEmailOnly
					invoked.ReceivableInvoiceEmailContactsOnly 			= LocalReceivableInvoiceEmailOnly
					invoked.PaymentReceiptEmailContactsOnly 			= LocalPaymentReceiptEmailOnly
					invoked.PrepaymentReceiptEmailContactsOnly 			= LocalPrepaymentReceiptEmailOnly
					invoked.SubleaseBillingInvoiceEmailContactsOnly 	= LocalSubleaseBillingInvoiceEmailOnly

	Sets
		ByCustomer
			indexed
			Sort Order
				Customer
				Company
				CompanyCustomerContact


	Actions
		Create is a Create Action
			Action Rules
				constraint (CustomerGroupRel exists)
					"CustomerGroup<Company.CustomerGroupField.CustomerGroup>DoesNotExist"	
				constraint (CompanyCustomerRel exists)
					"Customer<Customer>IsNotSetUpForCompany<DerivedCompany>"	
				constraint (CustomerRel exists)
					"Customer<Customer>IsNotSetUpInCustomerGroup<Company.CustomerGroupField.CustomerGroup>"	

		CreateContactNumber is a Create Action
			restricted
			Action Rules
				if (CompanyCustomerContact not entered)
					include ContactNumberAutosequence
					CompanyCustomerContact = LocalCounter
				constraint (CustomerGroupRel exists)
					"CustomerGroup<Company.CustomerGroupField.CustomerGroup>DoesNotExist"	
				constraint (CompanyCustomerRel exists)
					"Customer<Customer>IsNotSetUpForCompany<DerivedCompany>"	
				constraint (CustomerRel exists)
					"Customer<Customer>IsNotSetUpInCustomerGroup<Company.CustomerGroupField.CustomerGroup>"	

		Update is an Update Action
			Entrance Rules
				include ValidateAndUpdateCompanyCustomerDistributionEmailOnly

		Delete is a Delete Action
			Entrance Rules
				if (Company.FinanceEnterpriseGroup.BODTrigger)
					ActionCode = ActionCode.Delete
					include TriggerContactMasterARServiceRules

				include ValidateAndUpdateCompanyCustomerDistributionEmailOnly
					





		
		SendContactMasterARNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is ContactMasterARXMLBOD
					bod type is "Sync.ContactMaster"
					accounting entity is DerivedBODaccountingEntity
					document id is DerivedDocumentID
					variation id is DerivedVariationID

	
		TriggerContactMasterARNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				if (DerivedConfigDocID = "Y")
					LocalMainUserTemplate = "IONContactMasterARXML_CleanDoc_ST"
				else
					LocalMainUserTemplate = "IONContactMasterARXML_ST"
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "ContactMaster"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "CompanyCustomerContact"			
					invoked.PrmFinanceEnterpriseGroup	= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.PrmCustomerBusinessGroup 	= Company.CustomerGroupField.CustomerGroup
					invoked.PrmCompany 					= Company
					invoked.PrmMainUserTemplate 		= LocalMainUserTemplate
				NativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(Company.FinanceEnterpriseGroup.BODTrigger and NativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "ContactMaster"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedBODaccountingEntity
							invoked.Customer				= Customer
							invoked.Reference1				= Company
							invoked.Reference2				= CompanyCustomerContact
							invoked.Reference3				= "CompanyCustomerContact"
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else	
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.Verb 					= 1
							invoked.Noun 					= "ContactMaster"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= DerivedVariationID
							invoked.BODID					= DerivedbodID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= DerivedFinanceEnterpriseGroup
							invoked.Direction				= 1
							invoked.BODAccountingEntity		= DerivedBODaccountingEntity
							invoked.Customer				= Customer
							invoked.Reference1				= Company
							invoked.Reference2				= CompanyCustomerContact
							invoked.Reference3				= "CompanyCustomerContact"
							initialize invoked.Error			
							initialize invoked.ErrorMessage	
					invoke SendContactMasterARNativeLPLBOD
						resume on error
							Error										= true
							ErrorMessage	 							= error message
					if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedbodID
							invoked.BODXML								= ContactMasterARXMLBOD
							
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status								= 3
							invoked.BODID								= DerivedbodID
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= ContactMasterARXMLBOD


		UpdateBODIdFields is an Instance Action
			restricted
			Parameters
				PrmAccountingEntity  is Alpha size 22
					default label is "AccountingEntity"
				PrmLocation		  is Alpha size 22
					default label is "Location" 
				PrmDocumentID		is Alpha size 100
					default label is "DocumentID"
				PrmSystemOfRecord	is Alpha size 1
					default label is "SystemOfRecord"
				PrmVariationID	   is Alpha size 22
					default label is "VariationID"
			Action Rules
				if (bod id.AccountingEntity != PrmAccountingEntity)
					bod id.AccountingEntity 	= PrmAccountingEntity
				if (bod id.Location != PrmLocation)
					bod id.Location				= PrmLocation
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
				if (bod id.VariationID != PrmVariationID)
					bod id.VariationID			= PrmVariationID				

		BODDataInitialLoadContactMasterARCriteria is a Set Action
			restricted
			Parameters
				PrmCustomerGroup						is a CustomerGroup
				PrmFromCompany							is a ReceivableCompany
				PrmToCompany							is a ReceivableCompany
				PrmFromCustomer 						is a Customer
					context of PrmCustomerGroup
				PrmToCustomer 							is a Customer
					context of PrmCustomerGroup
				PrmFromCompanyCustomerContact			is a CompanyCustomerContact
					context of PrmFromCompany
					context of PrmFromCustomer
				PrmToCompanyCustomerContact				is a CompanyCustomerContact
					context of PrmFromCompany
					context of PrmFromCustomer
			Instance Selection			
				where  (((PrmCustomerGroup entered
				and		  PrmCustomerGroup = Company.CustomerGroupField.CustomerGroup)
				or		  PrmCustomerGroup not entered)
				and	((PrmFromCompany entered
				and		 PrmFromCompany <= Company)
				or		 PrmFromCompany not entered)
				and	((PrmToCompany entered
				and		 PrmToCompany >= Company)
				or		 PrmToCompany not entered)
				and	((PrmFromCustomer entered
				and		 PrmFromCustomer <= Customer)
				or		 PrmFromCustomer not entered)				
				and	((PrmToCustomer entered
				and		 PrmToCustomer >= Customer)
				or		 PrmToCustomer not entered)				
				and	((PrmFromCompanyCustomerContact entered
				and		 PrmFromCompanyCustomerContact <= CompanyCustomerContact)
				or		 PrmFromCompanyCustomerContact not entered)	
				and	((PrmToCompanyCustomerContact entered
				and		 PrmToCompanyCustomerContact >= CompanyCustomerContact)
				or		 PrmToCompanyCustomerContact not entered))	
					
			Sort Order
			Action Rules	
				Instance Rules	
					if (Company.FinanceEnterpriseGroup.BODTrigger)
						ActionCode = ActionCode.Create
						include TriggerContactMasterARServiceRules
						
						




		Anonymize is an Instance Action	
			restricted
			Action Rules
				Name	= Customer
			
				initialize PhoneNumber
				initialize FaxNumber
				initialize EmailAddress

			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.Status						= 1
					invoked.AffectedBusinessClass		= "AR3"
					invoked.ReceivableCompany			= Company
					invoked.Customer					= Customer
					invoked.CompanyCustomerContact		= CompanyCustomerContact
			
		PurgeAuditLog is an Instance Action	
			restricted
			Action Rules
				invoke purge audit log entries
				 
		  	Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.Status						= 2
					invoked.AffectedBusinessClass		= "AR3"
					invoked.ReceivableCompany			= Company
					invoked.Customer					= Customer
					invoked.CompanyCustomerContact		= CompanyCustomerContact
					
		Purge is a Purge Action
			restricted
							
	Action Exit Rules
		if (Company.FinanceEnterpriseGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					ActionCode = ActionCode.Update
					if (action type.Create)
						ActionCode = ActionCode.Create
					if (action type.Delete)
						ActionCode = ActionCode.Delete
					include TriggerContactMasterARServiceRules
					



					
