AsyncBackgroundGroup is a BusinessClass
	owned by async
	prefix is ASBG
	stored in environment
	
	Ontology
		symbolic key is AsyncBackgroundGroup

	Patterns
		disable Auditing
        disable EffectiveDated
        disable AsOfDateProcessing
        implements UniqueID
        implements CreateStamp
     	
	Persistent Fields
		Name 				is LPLName
		LastReferenceTime 	is TimeStamp

	Conditions
    	Active
   			when (ActiveRequests exists)
   			
   		HasAuditRecords
			default label is "AuditRecordsExist"
			when (AuditingRel exists)
   			
   	Derived Fields
   		AsyncAuditingEnabled is a NativeField
			type is Boolean
			default label is untranslatable
			restricted


	Relations
		Requests
    		one-to-many relation to AsyncActionRequest
			Field Mapping uses ByBackgroundGroup
				related.BackgroundGroup = AsyncBackgroundGroup
					
		ActiveRequests
    		one-to-many relation to AsyncActionRequest
			Field Mapping uses ByBackgroundGroup
				related.BackgroundGroup = AsyncBackgroundGroup
			Instance Selection
				where (related.NotSuccessfullyCompleted)
				
		DependentRequests
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByWaitFor
				related.WaitFor = AsyncBackgroundGroup	
		
		AuditingRel
			one-to-many relation to AsyncAuditEntry
			Field Mapping uses ByBusClassID
				related.Reference.BusinessClassName = "AsyncBackgroundGroup"
				related.Reference.BusinessObjectKey = AsyncBackgroundGroup
				
	Actions
		Create is a Create Action
			restricted

			
		Delete is a Delete Action
			Entrance Rules
				constraint (not Active)
					"CannotDeleteAnActiveBackgroundGroup"
				
				if (AsyncAuditingEnabled)
					invoke Create AsyncAuditEntry
					
		DeleteInternal is a Delete Action
			restricted						

		DeleteIfInactive is an Instance Action
			restricted

			
			Parameters
				RequestCreationTime is TimeStamp
			
			Action Rules
				
				if ((LastReferenceTime not entered
				or  RequestCreationTime not entered
				or  LastReferenceTime <= RequestCreationTime) 
			 	and not Active)
					invoke DeleteIfInactiveLock	
						invoked.RequestCreationTime = RequestCreationTime
		
		DeleteIfInactiveLock is an Instance Action
			restricted
			default label is "DeleteIfInactive"

			
			Parameters
				RequestCreationTime is TimeStamp
				
			Local Fields
				RecordLocked is Boolean 
			
			Action Rules
				
				if (RecordLocked 
				and (LastReferenceTime not entered
				or  RequestCreationTime not entered
				or  LastReferenceTime <= RequestCreationTime) 
			 	and not Active)
					invoke DeleteInternal	
	
		PurgeInactive is a Set Action
			disable checkpoint
			Instance Selection
				where (not Active)
				
			Sort Order				
				Name
				
			Action Rules
				Instance Rules
					invoke DeleteIfInactive 
								
	Sets
		ByName
			indexed
			Sort Order
				Name
