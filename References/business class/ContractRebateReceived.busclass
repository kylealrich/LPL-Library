ContractRebateReceived is a BusinessClass
    owned by po
    prefix is RRC
    classic name is POREBRECD
	rpg name is DBPORRC
	
    Ontology
    	symbolic key is ContractRebateReceived
			classic set name is RRCSET1
			sql set name is RRCSET1

    Patterns
        implements StaticJava
        disable AuditIndex    	

	Persistent Fields
		DocumentDate						is Date
			classic name is DOC-DATE
		Company								is a PurchasingCompany		
		Status								is Numeric size 1
			States
				Entered 						value is 0
				CancelledVoid					value is 1
		TransactionAmount					is an InternationalAmount
			classic name is TRANS-AMOUNT
		TransactionCurrency					is a Currency
			classic name is TRANS-CURR
		ContractCurrencyAmount				is an InternationalAmount
			classic name is CONT-CURR-AMT
		CurrencyRate						is Decimal size 14.7
			classic name is CURR-RATE
		PaymentNumber						is AlphaRight size 10
			classic name is PAYMENT-NBR
    	
    Derived Fields
    	TotalRebateDistributions is a DerivedField
    		type is like InternationalAmount
    		if (Status.Entered)
    			return (sum ContractRebateDistRels.ContractCurrencyAmount)
    		else
    			return (0)
    	
    	RemainingDistribAmount is a DerivedField
    		type is like InternationalAmount
    		if  (TotalRebateDistributions > ContractCurrencyAmount)
				return 0
			else
    			return (ContractCurrencyAmount - TotalRebateDistributions)

    	NotVoidedTransactionAmount is a DerivedField
    		type is like InternationalAmount
    		restricted
			if (Status.Entered)
   				return (ContractCurrencyAmount)
			else
   				return (0)
   				
	Conditions
		Entered
			restricted
			when (Status.Entered)
			
	Sets

 		ByRebDue
			duplicates
			not indexed
 			Sort Order
		        ContractGroup
		        Contract
		        ContractRebateType
		        ContractRebateDue
 				DocumentDate
 				
    Relations
		ContractRebateDistRels	
			one-to-many relation to ContractRebateDistribution
			Field Mapping uses ByRebateReceived
				related.ContractGroup 	       = ContractGroup
				related.Contract 	           = Contract
		        related.ContractRebateType     = ContractRebateType
		        related.ContractRebateDue      = ContractRebateDue
		        related.ContractRebateReceived = ContractRebateReceived

	Field Rules
		Status
			default to "0"
		
		ContractCurrencyAmount
			if (ContractCurrencyAmount not entered)
				required
					"RebateReceivedAmountIsRequired"
			
		TransactionCurrency
			force default to Contract.CurrencyCode	
		
		CurrencyRate
			default to 1
			
		DocumentDate
			if (DocumentDate not entered)
				required
					"DateIsRequired"
							
		PaymentNumber			
			if (PaymentNumber not entered)
				required
					"PaymentOrInvoiceNumberIsRequired"
				
		TransactionAmount
			force default to ContractCurrencyAmount
			
	Actions
    	Create is a Create Action
			valid when (Contract.CanModifyRebate)
    		Action Rules
    			constraint (Contract.CanModifyRebate)
    				"ContractCannotBeInactiveToAddRebateReceipts"   	

    	Update is an Update Action
    		Action Rules
    			constraint (Contract.CanModifyRebate)
    				"ContractCannotBeInactiveToModifyRebatesReceipts"
    	
    	Delete is a Delete Action
    		Action Rules
    			constraint (Contract.CanModifyRebate)
    				"ContractCannotBeInactiveToDeleteRebatesReceipts"   	
    				
