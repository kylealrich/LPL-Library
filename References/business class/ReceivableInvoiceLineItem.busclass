ReceivableInvoiceLineItem is a BusinessClass
	owned by ar
	prefix is AIR
	classic name is ARITEM

	Ontology
		symbolic key is ReceivableInvoiceLineItem
			classic set name is AIRSET1
			classic name is SEQUENCE
			classic name for ReceivableInvoiceType is TRANS-TYPE
			classic name for ReceivableInvoice is INVOICE

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		Description
		ItemReference		is AlphaUpper size 32
			classic name is ITEM
		FinanceDimension2
			default label is "<Company.CustomerBusinessGroup.FinanceEnterpriseGroup.FinanceDimension2Label>"					
		Project
			classic name is ACTIVITY
		ProjectContract
		ItemUser1
			classic name is ITEM-USER1
		TransactionAmount	is an InternationalAmount
			classic name is AMOUNT
				precision is ReceivableInvoice.TransientManagedContextCurrency.OutputNumberOfDecimals
		Sort1
		Sort2
		Sort3
		Sort4
		AssociatedLine is like UniqueID 
		InvoiceFinanceCodeBlock is a FinanceCodeBlockFull
		TransactionUserField1 is a TransUser1
			classic name is TRANS-USER1
		GlobalLineType
			classic name is GLBL-LINE-TYPE
		
	Local Fields




	Derived Fields
		InvoiceTypeInvoice is a LabelField
			"<ReceivableInvoice.AlternateReceivableInvoiceType>_<ReceivableInvoice>"

		DerivedInvoiceTypeInvoice is a DerivedField
			type is MessageField
			return InvoiceTypeInvoice


	Transient Fields
		TransientFromCurrency is a FromCurrency
			derive value from ReceivableInvoice.OriginalCurrency
	
	Field Rules
		ItemReference
















			if (Description not entered)
				constraint (ItemReference entered)
					"DescriptionOrItemReferenceRequired"

		Description
			if (ItemReference not entered)
				constraint (Description entered)
					"DescriptionOrItemReferenceRequired"

		TransactionAmount
			if (ReceivableInvoice.Origin.IntercompanyBilling)
				required
					"DistributionAmountCannotBeZeroIfProjectValueIsInReceivableDistributionSet"
			if (ReceivableInvoice.ReceivableInvoiceType.Invoice or ReceivableInvoice.ReceivableInvoiceType.DebitMemo)	//FSM-8080 removing "or ReceivableInvoice.ReceivableInvoiceType.CreditMemo)" that was added with FSM-7374)
				constraint (TransactionAmount >= 0)
					"TransactionTypeNotMixedSignedType,CannotEnterNegativeAmount"
			if (!ReceivableInvoice.Origin.Projects)
				required

		Project

		InvoiceFinanceCodeBlock
			if (Company.DetailedInvoiceAccrualDistributions
			and IsManualEntry
			and action type.Create)
				default to ReceivableInvoice.ReceivableGeneralLedgerCodeRel.DistributionAccount
					default individual fields


	Conditions
		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	   CompanySecurityGroupMemberRel exists)

		IsManualEntry
			restricted
			when (ReceivableInvoice.Origin.ManualInvoice
			or	ReceivableInvoice.Origin.ManualMemo)



	Relations
		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company



	Sets

		ByReceivableInvoiceLineItemProject
			not indexed
			Sort Order
				Company
				ReceivableInvoiceType
				ReceivableInvoice
				Project
				ReceivableInvoiceLineItem

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

		Purge is a Purge Action
			restricted
