FinanceDimension2Structure is a BusinessClass
	owned by GeneralLedger
	
	prefix is FD2St
	
	Ontology
		symbolic key is FinanceDimension2Structure
		
	Patterns
	
    Persistent Fields
    	Description
		DimensionNode				is a FinanceDimension2
			delete ignored
		StructureSequence			is Numeric 4
		IsEnterpriseStructure		is Boolean
		UnusedDimensionStructure	is Boolean
			restricted
		Active				

   	Context Fields
		FinanceDimension2
		
    Derived Fields
		TopNodeString	    is a StringField
			type is AlphaUpper 15
			restricted
			StructureSequence
			"_TOP_NODE"

		DerivedOldPrefix	is a StringField
			type is AlphaUpper 15
			restricted
			StructureSequence
			"_"
			
		DerivedNewPrefix    is a StringField
			type is AlphaUpper 15
			restricted
			LocalNewStructureSequence
			"_"
			
		DerivedNewDimension   is a DerivedField
			type is like FinanceDimension2
			restricted
			I1 = DerivedOldPrefix size
			I1 += 1
			DerivedNewDimension = DerivedNewPrefix + LocalCopyDimension[I1:15]

		DerivedSystemDimension is a DerivedField
			type is like FinanceDimension2
			if (SystemFinanceDimension2Rel exists)
				return SystemFinanceDimension2Rel.FinanceDimension2
			else
				return UndefinedMsg
									
		UndefinedMsg is a LabelField
			restricted
			"NotDefined"
							
		InactiveMF is a MessageField
			restricted
			"Inactive"

		ActiveMF is a MessageField
			restricted
			"Active"
						
		StatusMessage is a ConditionalField
			type is Alpha size 20
			if (Active)
				ActiveMF
			else
				InactiveMF

		IsEnterpriseStructureMF is a MessageField
			restricted
			"Enterprise_Structure"
						
		IsEnterpriseStructureMessage is a ConditionalField
			type is Alpha size 20
			if (IsEnterpriseStructure)
				IsEnterpriseStructureMF
			else
				blank

	Local Fields
		LocalNewStructure	   		is a FinanceDimension2Structure
		LocalCopyDimension        	is a FinanceDimension2
		LocalNewDimension         	is a FinanceDimension2
		LocalNewStructureSequence   is Numeric 4
		LocalParentDimension	    is a FinanceDimension2
		LocalSubordinateDimension 	is like FinanceDimension2
		I1					    	is Numeric 2		
 		LocalDimensionNode			is like FinanceDimension2
		InvokedByUnusedStructure
		LocalRunGroup				is AlphaUpper 30
		LocalCounter				is Numeric 3	
		 
	Relations
		StructureDimensionRel
			one-to-many relation to FinanceDimension2
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.FinanceDimension2	!= StructureDimensionHierarchyRel.FinanceDimension2)
		StructureDimensionHierarchyRel 
			one-to-many relation to FinanceDimension2Hierarchy
			Field Mapping uses ByParentDimension
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.ParentDimension				= DimensionNode
		DimensionsInStructureRel
			one-to-many relation to FinanceDimension2
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.DimensionStructure			= FinanceDimension2Structure
		EnterpriseDimensionStructureRel
			one-to-many relation to FinanceDimension2Structure
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.IsEnterpriseStructure
			    and    related.UniqueID != UniqueID)
		NotInDimensionStructureRel
			one-to-many relation to FinanceDimension2
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.NotUsedInHierarchy)
		SystemFinanceDimension2Rel
			one-to-one relation to FinanceDimension2Hierarchy
			Field Mapping uses BySystemDimensionInStructure
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension2Structure			= FinanceDimension2Structure
		UploadErrorsRel is a FinanceDimension2Upload set
			Instance Selection
				where (related.ErrorMessage entered)
		MySummaryDimension2Rel
			one-to-many relation to FinanceDimension2
			Field Mapping uses ByStructureAndPersonResponsible
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= FinanceDimension2Structure
				related.PersonResponsible  		= actor.agent(Employee).Employee
			Instance Selection
				where (related.DimensionType.Summary)
		ProjectTeamMemberRel
			one-to-many relation to ProjectTeamMember
			Field Mapping uses ByTeamMember
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TeamMember				= actor.agent(Employee).Employee
		MySummaryDimension2TeamRel
			one-to-many relation to FinanceDimension2
			Field Mapping uses ByStructureAndProjectTeam
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= FinanceDimension2Structure
				related.ProjectTeam				= ProjectTeamMemberRel.ProjectTeam
			Instance Selection
				where (related.DimensionType.Summary)
		SummaryDimension2Rel
			one-to-many relation to FinanceDimension2
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= FinanceDimension2Structure
			Instance Selection
				where (related.DimensionType.Summary)
		RunGroupRel
			one-to-many relation to FinanceDimension2Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension2Structure			= FinanceDimension2Structure
			Instance Selection
				where (related.FinanceDimension2Upload.RunGroup = LocalRunGroup)
		ContextDimensionHierarchyRel
			one-to-many relation to FinanceDimension2Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension2Structure			= FinanceDimension2Structure
				related.FinanceDimension2					= FinanceDimension2
		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 4
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
		SecurityGroupMemberRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 4
				related.FinanceDimensionStructure     = FinanceDimension2Structure	
		SecurityGroupsRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 4
				related.FinanceDimensionStructure     = FinanceDimension2Structure
			Instance Selection
				where (related.RequiresRebuild)
		SecurityGroupsNotRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 4
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
			Instance Selection
				where (!related.RequiresRebuild)
		PostingInDimensionStructureRel
			one-to-many relation to FinanceDimension2
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup

		PersonResponsiblePostingInDimensionStructureRel
			one-to-many relation to FinanceDimension2
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.AncestorPersonResponsible = actor.agent(Employee).Employee)
					
		FinanceDimension2DepartmentRel
			one-to-many relation to FinanceDimension2Department
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup

		FinanceDimension2DivisionRel
			one-to-many relation to FinanceDimension2Division
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
														
    Conditions
		UploadRecordsExist
			restricted
			when (FinanceDimension2Upload set exists)
		UploadErrorsExist
			restricted
			when (UploadErrorsRel exists)
		EligibleForExport
			restricted
			when ((!UploadRecordsExist)
			and   (!UnusedDimensionStructure))
		AvailableStructure
			restricted
			when ((!UnusedDimensionStructure)
			and   (!ContextDimensionHierarchyRel exists))
		UsedInASecurityGroup
			when (SecurityGroupMemberRel exists)
		SecurityGroupRequiresRebuild
			when (SecurityGroupsRequiringRebuildRel exists)
												
	Sets
		ByStructureSequence
			Sort Order
				FinanceEnterpriseGroup
				StructureSequence
		





	
		ByEnterpriseStructure
			Sort Order
				FinanceEnterpriseGroup
				IsEnterpriseStructure descending
				FinanceDimension2Structure
		
	Attach Rules
		if (!parentcontext.name = "FinanceDimension2"
		and !parentcontext.name = "FinanceDimension2Upload"
		and !parentcontext.name = "FinanceDimension2Shadow"
		and !parentcontext.name = "FinanceDimension2Hierarchy"
		and !parentcontext.name = "FinanceEnterpriseGroup"
		and !parentcontext.name = "AccountAnalysisSettings"
		and !parentcontext.name = "ReportingBasis")	
			constraint (Active)
				"StructureIsNotActive"
									
    Field Rules
		Active
			if (UnusedDimensionStructure)
				Active = false  
		Description
			required
		StructureSequence
			autosequence using ByStructureSequence
			if (!StructureSequence = old StructureSequence)
				cannot be changed
					"SequenceStructureCannotBeChanged"
		IsEnterpriseStructure
			constraint (Active)
				"EnterpriseDimensionMustBeActive"
			constraint (!UnusedDimensionStructure)
				"TheUnusedDimensionStructureCannotBeTheEnterpriseStructure"				
			constraint (!EnterpriseDimensionStructureRel exists)
				"<first EnterpriseDimensionStructureRel.FinanceDimension2Structure>AlreadyDesignatedAsEnterpriseStructure"

	Rule Blocks
		UpdateFinanceEnterpriseGroupDim2Structure
			if (action type.Create)
				if (IsEnterpriseStructure)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						invoked.PrmEnterpriseDim2Structure = FinanceDimension2Structure
			else								
				if (IsEnterpriseStructure changed)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						if (!IsEnterpriseStructure)
							invoked.PrmClearDim2Structure = true
						else
							invoked.PrmEnterpriseDim2Structure = FinanceDimension2Structure

	Actions
		Create is a Create Action
			Entrance Rules



			Exit Rules
				InvokedByUnusedStructure = true
				
				invoke Create FinanceDimension2
					invoked.FinanceEnterpriseGroup  	= FinanceEnterpriseGroup
					invoked.FinanceDimension2			= TopNodeString
					invoked.DisplayDimension			= "TOP_NODE"
					invoked.DimensionStructure			= FinanceDimension2Structure
					invoked.Description					= Description + " Top Level"

					invoked.DimensionType				= DimensionType.Node
					invoked.SkipHierarchy				= true
					invoked.IsTopNode					= true				
			
				DimensionNode = TopNodeString
				include UpdateFinanceEnterpriseGroupDim2Structure
			
		T2VCreate is a Create Action
			restricted
			default label is untranslatable

		Update is an Update Action
		
			Action Rules
				include UpdateFinanceEnterpriseGroupDim2Structure
				invoke Update DimensionNode
					invoked.Description					= Description + " Top Level"

		Delete is a Delete Action
			confirmation required
				"AreYouSureYouWantToPerformTheFollowingAction:_Delete_Structure?"
			Entrance Rules
				constraint (!IsEnterpriseStructure)
					"CannotDeleteEnterpriseStructure"
				constraint (!UnusedDimensionStructure)
					"CannotDeleteUnusedDimensionStructure"
				constraint (!Active)
					"CannotDeleteActiveStructure"	
		

				for each FinanceDimension2Hierarchy set		
					increment LocalCounter
					invoke Delete each
					if (LocalCounter = 50)
						commit transaction
						initialize LocalCounter
	
			Exit Rules
				invoke DeleteAll DimensionsInStructureRel						

		RestrictedDelete is a Delete Action
			restricted
			
			Entrance Rules

			Exit Rules
				invoke DeleteAll DimensionsInStructureRel
										
		UploadHierarchy is an Instance Action
			valid when (UploadRecordsExist)
			completion message is "UploadInitiated"	
			run in background
			
			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"	
			
			Action Rules
				invoke UploadHierarchy FinanceDimension2Upload in background
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
					invoked.PrmFinanceDimension2Structure	= FinanceDimension2Structure
					invoked.PrmRunGroup						= PrmRunGroup

		ResetUploadErrors is an Instance Action

			completion message is "ResetInitiated"

			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"
									
			Action Rules
				invoke ResetAllErrors FinanceDimension2Upload in background
					invoked.PrmFinanceEnterpriseGroup  		= FinanceEnterpriseGroup
					invoked.PrmFinanceDimension2Structure	= FinanceDimension2Structure
					invoked.PrmRunGroup						= PrmRunGroup
											
		CopyStructure is an Instance Action
			valid when (!UnusedDimensionStructure)
			run in background
			completion message is "CopyInitiated"		
			
			Parameters
				NewStructure		is like FinanceDimension2Structure
				NewDescription		is like Description
				MakeNewStructureActive is Boolean
							
			Parameter Rules
				NewStructure
					required
					LocalNewStructure = NewStructure
					constraint (!LocalNewStructure exists)
						"DimensionStructureAlreadyExists"
				NewDescription
					required

			Local Fields
				AsyncId			is an AsyncActionRequest
															
			Action Rules
				invoke Create FinanceDimension2Structure
					invoked.FinanceEnterpriseGroup	   = FinanceEnterpriseGroup
					invoked.FinanceDimension2Structure = NewStructure
					invoked.Description			   	   = NewDescription
					invoked.IsEnterpriseStructure      = false
					invoked.Active					   = MakeNewStructureActive

			Exit Rules
				
				invoke CopyDimensions FinanceDimension2Hierarchy in background
					assign async action request id to AsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmStructure	 		  = FinanceDimension2Structure
					invoked.PrmNewStructure	 		  = NewStructure
					
				invoke BuildShadowFile LocalNewStructure in background
					run after AsyncId
									
		BuildShadowFile is an Instance Action
			restricted
					
			Local Fields
				LocalAsyncId				is an AsyncActionRequest
						
			Action Rules
				invoke DeleteShadowForStructure FinanceDimension2Shadow in background
					assign async action request id to LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension2Structure				
					
				invoke BuildShadowFile FinanceDimension2Hierarchy in background
					run after LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension2Structure
					
		BuildShadowFileForDimension is an Instance Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmDimension					is a FinanceDimension2
					default label is "Dimension"
		
			Action Rules
				invoke BuildShadowFile FinanceDimension2Hierarchy in foreground
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension2Structure
					invoked.PrmDimension			  = PrmDimension
		
		ExportToUpload is an Instance Action


			completion message is "ExportInitiated"
						
			Parameters
				PrmRunGroup					is AlphaUpper 30
					default label is "RunGroup"
				PrmDimensionGroup   		is a FinanceDimension2 group
					default label is "CustomGroup"
				PrmSummaryDimension		    is a FinanceDimension2
					default label is "Summary<FinanceEnterpriseGroup.FinanceDimension2Label>"	
				PrmPostingDimension			is a FinanceDimension2
					default label is "Posting<FinanceEnterpriseGroup.FinanceDimension2Label>"	
				PrmBypassEdits	  is Boolean
					default label is "BypassEdits"					
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (!RunGroupRel exists)
						"UploadRecordsAlreadyExistForThisRunGroup"	
									
				PrmDimensionGroup
					constraint (!PrmSummaryDimension entered
					and			!PrmPostingDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
						
				PrmSummaryDimension
					constraint (!PrmDimensionGroup entered
					and         !PrmPostingDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
										
				PrmPostingDimension
					constraint (!PrmDimensionGroup entered
					and         !PrmSummaryDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"					
			
			Action Rules
			
				if ((!PrmDimensionGroup entered)
				and (!PrmSummaryDimension entered)
				and (!PrmPostingDimension entered))
					invoke ExportToUpload DimensionNode in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension2Structure	  = FinanceDimension2Structure
						invoked.PrmRunGroup						  = PrmRunGroup	
						invoked.PrmBypassEdits                    = PrmBypassEdits                    		
				else
				if (PrmSummaryDimension entered)
					invoke ExportToUpload PrmSummaryDimension in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension2Structure	  = FinanceDimension2Structure
						invoked.PrmRunGroup						  = PrmRunGroup
						invoked.PrmBypassEdits                    = PrmBypassEdits						
				else
				if (PrmPostingDimension entered)														
					invoke ExportToUpload PrmPostingDimension in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension2Structure	  = FinanceDimension2Structure
						invoked.PrmRunGroup						  = PrmRunGroup
						invoked.PrmBypassEdits                    = PrmBypassEdits						
				else																		
					invoke ExportToUpload FinanceDimension2Hierarchy in background
						invoked.PrmFinanceEnterpriseGroup  		  = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension2Structure     = FinanceDimension2Structure
						invoked.PrmRunGroup				   	      = PrmRunGroup	
						invoked.PrmFinanceDimension2Group	   	  = PrmDimensionGroup
						invoked.PrmBypassEdits                    = PrmBypassEdits						

		DeleteUploadRecordsForRunGroup is an Instance Action
			valid when (UploadRecordsExist)
			confirmation required
			completion message is "DeleteSubmitted"
			
			Parameters
				PrmRunGroup			is AlphaUpper 30
					default label is "RunGroup"
					
			Parameter Rules
				PrmRunGroup
					required
					
			Action Rules
				invoke DeleteAll FinanceDimension2Upload in background
					invoked.PrmFinanceEnterpriseGroup  	  = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension2Structure = FinanceDimension2Structure
					invoked.PrmRunGroup				   	  = PrmRunGroup
						
		CleanupUnusedDimensions is an Instance Action
			restricted
			
			Action Rules
				InvokedByUnusedStructure = true
				invoke CleanupUnusedDimensions FinanceDimension2 in background
					invoked.PrmFinanceEnterpriseGroup	  = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension2Structure = FinanceDimension2Structure
					
		AddDimensionToUnusedStructure is an Instance Action
			restricted
			
			Parameters
				FinanceDimension2
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<FinanceDimension2Structure>"
					
				InvokedByUnusedStructure = true
				invoke AddPostingDimensionToStructure FinanceDimension2
					invoked.PrmStructure	   = FinanceDimension2Structure
					invoked.PrmParentDimension = DimensionNode
					
		RemoveDimensionFromUnusedStructure is an Instance Action
			restricted
			
			Parameters
				FinanceDimension2
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<FinanceDimension2Structure>"
					
				InvokedByUnusedStructure = true
				invoke RemoveFromStructure FinanceDimension2				
