RepSetBCCheckSum is a BusinessClass
	prefix is RSCS
	owned by la
	default label is "ReplicationSetBusinessClassCheckSum"
	framework type is ReplicationSet
	
	Patterns
		implements LightweightAuditing
		disable AuditIndex
		implements AuditLogEntryActions
	
	Ontology
		symbolic key is RepSetBCCheckSum
		
	Persistent Fields
		RepSetBCFieldChoice
		RowStoreField is Alpha size 300 
			is related value for RepSetBC.BusinessClass
		Condition 	is LPLText	 
			is condition for RepSetBC.BusinessClass
			
	Local Fields
    	LocalErrorField 					is Text
    	RSLocalsDerived 					is Boolean
		RSLocalResidence 					is Boolean			
		RSLocalIsUserField					is Boolean
		RSLocalRepresentationType 			is Numeric size 3
		RSLocalType							is Numeric size 1
		RSLocalFieldIsValid					is Boolean
		LocalDeleteMode						is Boolean
	
	Field Rules
		RepSetBCCheckSum
			initial value is RepSetBCFieldChoice.ReplicateFieldName
		
		RepSetBCFieldChoice
			required
			
			constraint (RepSetBCFieldChoice.IsNumeric) 
				"CheckSumMustBeNumeric"
			
			constraint (RepSetBCFieldChoice.InstanceCount or RepSetBCFieldChoice.DerivedDecimals <= RepSetBCFieldChoice.MaxDecimals)
				"NumberOfDecimalsOfFieldChoiceCannotExceed<RepSetBCFieldChoice.MaxDecimals>"
			
		RowStoreField
			if (RepSetBCFieldChoice.RepSetBCField entered or RepSetBCFieldChoice.RepSetBCClientField entered)
				if (not RepSetBCFieldChoice.IsPersistenField or RepSetBCFieldChoice.IsUserField)
					required
					
					constraint (RSFieldIsValid)
						"RowStoreFieldIsInvalidFor<DerRepSetBCBusClass>"
					
					constraint (RSDerivedResidence)
						"RowStoreFieldMustBeResidentOn<DerRepSetBCBusClass>"
					
					constraint (not RSDerivedIsUserField)
						"RowStoreFieldCannotBeAUserField"
					
					constraint (RSDerivedType = BusinessFieldType.Persistent)
		                "RowStoreFieldMustBePersistentFieldOn<DerRepSetBCBusClass>"
		                
		            if (RepSetBCFieldChoice.DerivedRepresentationType != RSDerivedRepresentationType)
		            	confirmation required
		            		"RowStoreFieldTypeDoesNotMatchTypeFor<RepSetBCFieldChoice.RelatedValue>.Continue?"
	        else
	        	initialize RowStoreField
			
		Condition
			if (IsColumnStoreCondition)
				constraint (ValidCondition)
					"ConditionCanOnlyReferenceResidentDeliveredPersistentFieldsIncludedInTheReplicationFieldsAndCanOnlyBeASingleComparison.FirstError:<LocalErrorField>"
			else
				constraint (ValidCondition)
					"ConditionCanOnlyReferenceResidentDeliveredPersistentFieldsAndConstantsAndCanOnlyBeASingleComparison.FirstError:<LocalErrorField>"
		
	Derived Fields
		NumberCheckSumRecords is a ComputeField
			type is Numeric 10
			default label is untranslatable:"NumberCheckSumRecords"
    		(instance count of RepSetBC.RepSetBCCheckSum set)
    		
   		ValidCondition is a NativeField
			type is Boolean

			
		ValidationRelatedValue is a DerivedField 
			type is Alpha size 300
			restricted
			default label is untranslatable
			return RepSetBCFieldChoice.RelatedValue
	
		DerRepSetBCBusClass is a DerivedField 
			type is LPLName
			restricted
			default label is untranslatable:"RepSetBCBusClass"
			return RepSetBC.BusinessClass
			
		RSDerivedResidence is a DerivedField
			type is Boolean
			restricted
			if (not RSLocalsDerived)
				RSLocalsDerived = RSDeriveLocals
			return RSLocalResidence
			
		RSDerivedIsUserField is a DerivedField
			type is Boolean
			restricted			
			if (not RSLocalsDerived)
				RSLocalsDerived = RSDeriveLocals
			return RSLocalIsUserField
			
		RSDerivedRepresentationType is a DerivedField
			type is Numeric size 3
			restricted	
			if (not RSLocalsDerived)
				RSLocalsDerived = RSDeriveLocals
			return RSLocalRepresentationType		
			
		RSDerivedType is a DerivedField
			type is Numeric size 1
			restricted
			if (not RSLocalsDerived)
				RSLocalsDerived = RSDeriveLocals
			return RSLocalType
			
		RSFieldIsValid is a DerivedField
			type is Boolean
			restricted	
			if (not RSLocalsDerived)
				RSLocalsDerived = RSDeriveLocals
			return RSLocalFieldIsValid	
		
		RSDeriveLocals is a NativeField
			type is Boolean
			restricted		
			
		ColumnarConditionMessage is a MessageField
			restricted			
			"ConditionAppliesTo'<RepSetBCFieldChoice.RelatedValue>'InColumnarDataAnd'<RowStoreField>'InLocalRowStoreData."	
		
		NonColumnarConditionMessage is a MessageField
			restricted			
			"ConditionAppliesTo'<RepSetBCFieldChoice.ReplicateFieldName>'InDataLakeAnd'<RowStoreField>'InLocalRowStoreData."		
			
		RowConditionMessage is a MessageField
			restricted			
			"ConditionAppliesTo'<RowStoreField>'InLocalRowStoreData."						
			
		ConditionMessage is a DerivedField	
			type is MessageField
			default label is untranslatable
			
			if (not RepSetBCFieldChoice.InstanceCount and RowStoreField entered)
				return RowConditionMessage
			else
			if (ReplicationSet.ExportFormat.COLUMNAR)
				return ColumnarConditionMessage
			else
				return NonColumnarConditionMessage
				
		DerCheckSumInitValue is a DerivedField
			type is Alpha size 127	
			default label is untranslatable
			if (RepSetBCCheckSum entered)
				return RepSetBCCheckSum
				
			return RepSetBCFieldChoice.ReplicateFieldName
			
		AtMaxMessage is a MessageField
			restricted			
			"AlreadyAtTheMaxiumAllowedCheckSumRecords."	
			
		CannotEditMessage is a MessageField
			restricted			
			"ReplicationSetBusinessClassIsNotInAnEditableState."
		
		CanCreateConstraintMessage is a DerivedField	
			type is MessageField
			default label is untranslatable		
			
			if (not CanEditFields)
				return CannotEditMessage
			
			if (NumberCheckSumRecords >= 10)
				return AtMaxMessage
			
	Conditions
		CanEditFields
			default label is untranslatable
			when (not ReplicationSet.InProcess
			and   RepSetBC.ShowValidationData)
		
		CanCreate
			default label is untranslatable
			when (CanEditFields and NumberCheckSumRecords < 10)
			
		DisplayClientFields
			default label is untranslatable
    		when (RepSetBC.RepSetBCClientField set exists)
    		
    	ShowRowStoreField
    		default label is untranslatable  
    		when (not RepSetBCFieldChoice.InstanceCount and not RepSetBCFieldChoice.IsPersistenField or RepSetBCFieldChoice.IsUserField)
    	
    	IsInstanceCount 
    		default label is untranslatable  
    		restricted
    		when (RepSetBCFieldChoice.InstanceCount)
    		
    	IsColumnStoreCondition
    		default label is untranslatable  
    		restricted
    		when ((not RowStoreField entered) and Condition entered)
    		
    	HasBaseInstanceCountCheckSum
    		default label is untranslatable  
    		restricted
    		when (BaseInstanceCountCheckSum exists)
				
	Relations

		RepSetBCIncludeDeleteKeyRel
 			one-to-many relation to RepSetBC
 			Field Mapping uses symbolic key
 				related.ReplicationSet = ReplicationSet
 			 	related.RepSetBC = RepSetBC		
 			Instance Selection
				include deleted records
				where (related.ReplicationSet = ReplicationSet)
				
		BaseInstanceCountCheckSum
			one-to-many relation to RepSetBCCheckSum
			Field Mapping uses symbolic key
				related.ReplicationSet = ReplicationSet
				related.RepSetBC = RepSetBC
			Instance Selection
				where (related.RepSetBCFieldChoice.InstanceCount and related.Condition = blank)
				
	Create Rules
		constraint (CanCreate)
			untranslatable:"<CanCreateConstraintMessage>"
			
		if (NumberCheckSumRecords = 9)
			constraint (BaseInstanceCountCheckSum exists or RepSetBCFieldChoice.InstanceCount and Condition = blank)
				"AlreadyAtTheMaxiumAllowedCheckSumRecordsIncludingTheImplicitBaseInstanceCount.CanOnlyCreateBaseInstanceCountOrRemoveOtherRecordsFirst."
				
	Actions
		Create is a Create Action
			valid when (CanCreate)
			
		CreateForCopy is a Create Action 
            restricted

		Update is an Update Action
			valid when (CanEditFields)
		
		Delete is a Delete Action
			valid when (CanEditFields)
			Entrance Rules
				LocalDeleteMode = true
			Exit Rules
				LocalDeleteMode = false

		Purge is a Purge Action
			valid when (CanEditFields)
			confirmation required
				"ThisCannotBeUndone,AreYouSureYouWantToPurge?"
			Entrance Rules
				LocalDeleteMode = true
			Exit Rules
				LocalDeleteMode = false
					
       
					
