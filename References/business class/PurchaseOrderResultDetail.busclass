PurchaseOrderResultDetail is a BusinessClass
	owned by po
	prefix is PORD
	
	Ontology
		symbolic key is PurchaseOrderResultDetail
		
	Patterns
		disable AuditIndex
		disable Auditing
		disable EffectiveDated
	
	Persistent Fields
		PurchasingCompany						is like PurchasingCompany
		PurchaseOrder							is like PurchaseOrder
		PurchaseOrderLine						is like PurchaseOrderLine
		PurchaseOrderReceipt					is like PurchaseOrderReceipt
		PurchaseOrderReceiptLine				is like PurchaseOrderReceiptLine
		SourceDocumentLineNumber				is Numeric size 6
		
		OrderedLessCancelledQuantity			is like Quantity
		BackorderedQuantity						is like Quantity
		ReceivedQuantity						is like Quantity

		ErrorMessage							is Alpha size 150
	Field Rules
	
	Conditions
		DetailExists
			when (PurchaseOrderTransactionDetailsRel exists)

		HasSpecialHandling
			when (PurchaseOrderReceiptLineRel.PurchaseOrderLine.HasSpecialHandlingCode)

		HasPOCode
			restricted
			when (PurchaseOrderReceiptRel.POCode entered)

	Relations
		PurchaseOrderRel
			one-to-one relation to PurchaseOrder
			Field Mapping uses symbolic key
				related.Company					= PurchasingCompany
				related.PurchaseOrder			= PurchaseOrder
		
		PurchaseOrderReceiptRel
			one-to-one relation to PurchaseOrderReceipt
			Field Mapping uses symbolic key
				related.Company					= PurchasingCompany
				related.PurchaseOrderReceipt	= PurchaseOrderReceipt
				
		PurchaseOrderReceiptLineRel
			one-to-one relation to PurchaseOrderReceiptLine
			Field Mapping uses symbolic key
				related.Company						= PurchasingCompany
				related.PurchaseOrderReceipt		= PurchaseOrderReceipt
				related.PurchaseOrderReceiptLine 	= PurchaseOrderReceiptLine

		PurchaseOrderTransactionDetailsRel  
			one-to-many relation to PurchaseOrderTransactionDetail
			Field Mapping uses symbolic key
				related.Company = PurchasingCompany
			Instance Selection
				where    (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType.Receiving
				 	and   related.PurchaseOrderTransactionDetail.DocumentNumberNumeric = PurchaseOrderReceipt
					and   related.PurchaseOrderTransactionDetail.LineNumber = PurchaseOrderReceiptLine
					and	 (related.Lot entered
					or	  related.Sublot entered
					or	  related.Bin entered
					or	  related.Serial entered))
	
	Derived Fields
		DerivedItemAndDescription	is a StringField
			type is Alpha 100
			PurchaseOrderReceiptLineRel.Item
			"-"
			PurchaseOrderReceiptLineRel.Description
			
		DerivedLineNumber	is a DerivedField
			type is like LineNumber
			return PurchaseOrderReceiptLineRel.PurchaseOrderLine.LineNumber	

		DisplayPurchaseOrderAndPOCode is a MessageField
			"<PurchaseOrderReceiptRel.PurchaseOrder>__-__<PurchaseOrderReceiptRel.POCode>"
	Actions	
	
		Create is a Create Action
			restricted
			Action Rules
				if (ErrorMessage entered)
					increment PurchaseOrderResult.RecordsWithError
				else
					increment PurchaseOrderResult.RecordsReleased
				
						
