FinanceResourceSchedule is a BusinessClass
	owned by sharedfinance
	
	prefix is FRSch
	
	Ontology
		symbolic key is FinanceResourceSchedule
	
	Patterns
	
	Persistent Fields
		NavigateToThis				is BusinessObjectReference
		Description					is Alpha 100
		BeginDate					is Date
		BeginTime					is Time
		DueDate						is Date
		DueTime						is Time
		Notes						is Text
		Attachment					is an AlternateAttachment
		WebsiteLink					is an URL
		CreatedBy					is Numeric 01
			States
				Reminder			value is 0
				Task				value is 1
				WorkRequest			value is 2
				OneTimeTask			value is 3
		WorkStatus					is Numeric 1
			States
				Scheduled			value is 0
				InProcess			value is 1
				Complete			value is 2
		FinancialForm				is Alpha 6
		WorkRequestedBy				is a FinanceResource 		

	Local Fields
		AddAttachment				is Boolean
		LocalGroupingForTeam		is Numeric 9
		LocalTeam					is a FinanceTeam
		LocalAttributeCtr   		is Numeric 2

	Field Rules

















					
	Derived Fields
		RedAlertMessage is a DerivedField
			type is Alpha 100
			restricted
			if (ClosePeriodTaskAssignment
			and NavigateToThis(ClosePeriodTask).ShowOpenPrereqAlertMessage)
				return NavigateToThis(ClosePeriodTask).PeriodRedAlertMessage
			else	
				if (NavigateToThis(ClosePeriodTask).TaskInJeopardy)
					return TaskOverdueMsg
				else
					return "Overdue"
			if (ReminderRedAlert)
				return "OverDue"
			if (ResourceRedAlert)
				return "ResourceIsNotActive"			
		
		YellowAlertMessage is a DerivedField
			type is Alpha 100
			restricted
			if (ClosePeriodTaskAssignment
			and NavigateToThis(ClosePeriodTask).ShowOpenPrereqAlertMessage)
				return NavigateToThis(ClosePeriodTask).PeriodYellowAlertMessage
			else
				if (NavigateToThis(ClosePeriodTask).TaskInJeopardy)
					return TaskInJeopardyMsg
				else
					if (NavigateToThis(ClosePeriodTask).EstimateDueDateBeforeSchedule)
						return TaskEarlyCompletionMsg
		
		GreenAlertMessage is a DerivedField
			type is Alpha 30 
			restricted
			return "ReadyToStart"
		
		TaskOverdueMsg is a MessageField
			restricted
			"Overdue;ScheduledDueDateWas<DerivedTaskScheduleDueDate>__<DerivedTaskScheduleDueTime>"
			
		TaskInJeopardyMsg is a MessageField
			restricted
			"InJeopardy;ScheduledDueDateIs<DerivedTaskScheduleDueDate>__<DerivedTaskScheduleDueTime>"
			
		TaskEarlyCompletionMsg is a MessageField
			restricted
			"EstimatedEarlyCompletion;ScheduledDueDateIs<DerivedTaskScheduleDueDate>__<DerivedTaskScheduleDueTime>"







		
		MessageType is a ConditionalField
			type is Alpha 150
			restricted
			if (IsReminder)
				ReminderMessage
			else
			if (IsWorkRequest)
				WorkRequestMessage
			else
				blank

		ReminderMessage is a StringField
			type is Alpha 150
			restricted
			"Reminder:  "
			Description
		
		WorkRequestMessage is a StringField
			type is Alpha 150
			restricted
			"Work Request:  "
			Description
			
		FinancialFormLabel is a DerivedField
			type is Alpha 21
			restricted
			return "Financial System Form"
			
		AttachmentLabel is a DerivedField
			type is Alpha 10

			return "Attachment"
			
		WebsiteLinkLabel is a DerivedField
			type is Alpha 7

			return "Website"

		PeriodOpen is a ConditionalField
			type is Alpha 3
			restricted
			if (!ClosePeriodTaskAssignment)
				"NA"
			else
			if (ClosePeriodOpenForTask)
				"Yes"
			else
				"No"
			
		RejectReopenOneTimeAlertMessage is a DerivedField
			type is Alpha 30
			restricted
			return (NavigateToThis(ClosePeriodTask).RejectReopenOneTimeAlertMessage)








		DerivedParentTask is a ConditionalField
			type is Alpha 100
			restricted
			if (ClosePeriodTaskAssignment)
				NavigateToThis(ClosePeriodTask).ParentClosePeriodTask.TaskName
			else
				"NA"

		DerivedTaskClosePeriod is a StringField
			type is Alpha 7
			restricted
			NavigateToThis(ClosePeriodTask).ClosePeriod.Year
			"-"
			NavigateToThis(ClosePeriodTask).ClosePeriod.Period
			
		DerivedTaskScheduleDueDate is a DerivedField
			type is Date
			restricted
			return (NavigateToThis(ClosePeriodTask).ScheduleForTask.DueDate)
		
		DerivedTaskScheduleDueTime is a DerivedField
			type is Time
			restricted
			return (NavigateToThis(ClosePeriodTask).ScheduleForTask.DueTime)
			
		DerivedTaskLevelOfEffort is a DerivedField
			type is Unsigned Decimal size 10.2
			restricted
			return (NavigateToThis(ClosePeriodTask).ScheduleForTask.LevelOfEffort)

		TimeSensitiveFormMessage is a MessageField
			restricted
			"*TimeSensitive"

		NumberOfDaysNote is a MessageField
			"(_Note:_UnlessTheSpecificDayOfTheMonthIsBeingUsed,Leaving_BeginNumberOfDaysBlankMeansTaskWillBeScheduledToBeginOnThePeriodEndDateAndLeaving_DueNumberOfDaysBlankMeansTaskWillBeScheduledToBeDueOnThePeriodEndDate)"

	Conditions
		RaiseRedAlert			
			restricted
			when (CloseRedAlert or ResourceRedAlert or ReminderRedAlert)
		NewRaiseRedAlert
			when (DueDate < current corporate date)
		RaiseYellowAlert
			restricted
			when (CloseYellowAlert or ReminderYellowAlert)
		RaiseGreenAlert
			restricted
			when (false) 
		SimpleReminder
			restricted
			when (IsReminder
			and   !ReminderAboutAResource
			and   !ReminderAboutAPeriod
			and   !ReminderAboutATask)
		ReminderRedAlert
			restricted
			when ((CreatedBy.Reminder or CreatedBy.WorkRequest)
			and   DueDate < current corporate date)
		ReminderYellowAlert
			restricted
			when ((CreatedBy.Reminder or CreatedBy.WorkRequest)
			and   DueDate = current corporate date)
		IsReminder
			restricted
			when (CreatedBy.Reminder)
		IsWorkRequest
			restricted
			when (CreatedBy.WorkRequest)
		ReminderOrWorkRequest
			restricted
			when (IsReminder
			or    IsWorkRequest)

		ReminderAboutAPeriod
			restricted
			when (NavigateToThis.BusinessClassName = "ClosePeriod"
			and   CreatedBy.Reminder)

		ReminderAboutAResource
			restricted
			when (NavigateToThis.BusinessClassName = "FinanceResource"
			and   CreatedBy.Reminder)
		ResourceRedAlert
			restricted
			when (ReminderAboutAResource
			and   NavigateToThis(FinanceResource).NotActive)

		ClosePeriodTaskAssignment				
			restricted
			when (NavigateToThis.BusinessClassName = "ClosePeriodTask"
			and  (CreatedBy.Task
			or    CreatedBy.OneTimeTask))
		ReminderAboutATask
			restricted
			when (NavigateToThis.BusinessClassName = "ClosePeriodTask"
			and   CreatedBy.Reminder)
		CloseRedAlert
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).RedAlert)
		CloseYellowAlert
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).YellowAlert)			
		CloseGreenAlert
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).GreenAlert)			
		ClosePeriodOpenForTask
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).PeriodIsOpen)
		ClosePeriodScheduledForTask
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).PeriodIsScheduled)
		ToDoCriteria
			restricted
			when (IsReminder
			or    IsWorkRequest
			or    ClosePeriodOpenForTask)




		AllowMarkComplete
			restricted
			when ((HROrganization = actor.agent(Employee).HROrganization
			and    FinanceResource = actor.agent(Employee).Employee)
			and   (WorkStatus.Scheduled or WorkStatus.InProcess)
			and   (CreatedBy.Reminder or CreatedBy.WorkRequest))  
		AllowReschedule
			restricted
			when ((HROrganization = actor.agent(Employee).HROrganization
			and    FinanceResource = actor.agent(Employee).Employee)
			and   (WorkStatus.Scheduled or WorkStatus.InProcess)
			and   (CreatedBy.Reminder or CreatedBy.WorkRequest)) 


		AllowStart
			restricted
			when ((HROrganization = actor.agent(Employee).HROrganization
			and    FinanceResource = actor.agent(Employee).Employee)
			and   WorkStatus.Scheduled
			and   (CreatedBy.Reminder or CreatedBy.WorkRequest)) 


		AllowDelete	
			restricted
			when ((HROrganization = actor.agent(Employee).HROrganization
			and    FinanceResource = actor.agent(Employee).Employee)
			and   (CreatedBy.Reminder or CreatedBy.WorkRequest)) 


		AllowResourceReassign
			restricted
			when (ClosePeriodOpenForTask
			and   NavigateToThis(ClosePeriodTask).AllowResourceReassign)
		AllowReassignToTeam
			restricted
			when (ClosePeriodOpenForTask
			and   NavigateToThis(ClosePeriodTask).AllowReassignToTeam)
		AllowStartTask
			restricted
			when (ClosePeriodOpenForTask
			and   NavigateToThis(ClosePeriodTask).TaskStatus.Scheduled
			and   !NavigateToThis(ClosePeriodTask).StartingLate
			and   NavigateToThis(ClosePeriodTask).IsNotTaskForPeriodReconciliations
			and   !OneTimeTaskNeedsApproval)
		AllowStartTaskLate
			restricted
			when (ClosePeriodOpenForTask
			and   NavigateToThis(ClosePeriodTask).TaskStatus.Scheduled
			and   NavigateToThis(ClosePeriodTask).StartingLate
			and   NavigateToThis(ClosePeriodTask).IsNotTaskForPeriodReconciliations
			and   !OneTimeTaskNeedsApproval)
		AllowVoidTask
			restricted
			when (ClosePeriodOpenForTask
			and	  FinanceResource.CloseManagementGroupRel.AllowResourceToVoidTasks
			and   NavigateToThis(ClosePeriodTask).IsNotTaskForPeriodReconciliations
			and   NavigateToThis(ClosePeriodTask).TaskStatus < 7) 
		AllowCompleteTask
			restricted
			when (ClosePeriodOpenForTask
			and   NavigateToThis(ClosePeriodTask).AllowTaskToBeClosed) 
		AllowUpdateEstimate
			restricted
			when (ClosePeriodOpenForTask
			and   NavigateToThis(ClosePeriodTask).AllowToUpdateEstimate)







		TaskClosingLate
			restricted
			when (NavigateToThis(ClosePeriodTask).ClosingLate)
		TaskRequireSupportingDoco
			restricted
			when (NavigateToThis(ClosePeriodTask).RequireSupportingDoco)
		TaskSupportingDocoIsOptional
			restricted
			when (NavigateToThis(ClosePeriodTask).SupportingDocoIsOptional)
		TaskRejectedOrReopened
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).RejectedReopenedOneTimeStatus)
		OneTimeTask
			restricted
			when (CreatedBy.OneTimeTask)
		OneTimeTaskNeedsApproval
			restricted
			when (OneTimeTask
			and   NavigateToThis(ClosePeriodTask).OneTimeTaskRequiresApproval
			and   !NavigateToThis(ClosePeriodTask).OneTimeTaskApproved)
		AllowCreationOfOneTimeTasks
			restricted
			when (FinanceResource.CloseManagementGroupRel.AllowOneTimeTasks
			and   OpenClosePeriodRel exists)
		OneTimeTaskNeedsApprovalCode
			restricted
			when (FinanceResource.CloseManagementGroupRel.RequireApprovalOfOneTimeTasks)
		TaskIsTimeSensitive
			restricted
			when (ClosePeriodTaskAssignment
			and   NavigateToThis(ClosePeriodTask).DueTimeSensitive)
		TaskScheduled
			restricted
			when (ClosePeriodTaskAssignment
			and   WorkStatus.Scheduled)
		TaskInProcess
			restricted
			when (ClosePeriodTaskAssignment
			and   WorkStatus.InProcess)
		TaskHasEstimate
			restricted
			when (NavigateToThis(ClosePeriodTask).EstimateForTask entered)
			

		HasAttachment
			restricted
			when (Attachment entered)
		HasUrl
			restricted
			when (WebsiteLink entered)
		HasAttachmentOrUrl
			restricted
			when (HasAttachment
			or    HasUrl)




		HasAdditionalInfo
			restricted
			when (FinancialForm entered
			or    HasAttachment
			or    HasUrl)
		DueToday	
			restricted
			when ((ClosePeriodTaskAssignment
			and    NavigateToThis(ClosePeriodTask).DueToday)
			or   (!ClosePeriodTaskAssignment
			and    WorkStatus < WorkStatus.Complete
			and    DueDate = current corporate date))
		Overdue
			restricted
			when ((ClosePeriodTaskAssignment
			and    NavigateToThis(ClosePeriodTask).Overdue)
			or   (!ClosePeriodTaskAssignment
			and    WorkStatus < WorkStatus.Complete
			and    DueDate < current corporate date))
		EstimateReasonCodeRequired
			restricted
			when (FinanceResource.CloseManagementGroupRel.EstimateReasonCodeRequired)
			
	Relations
		MyRemindersRel is a FinanceResourceSchedule set
			Instance Selection
				where (related.CreatedBy.Reminder)

		SendToTeamMembersRel
			one-to-many relation to FinanceTeamMember 
			Field Mapping uses ByFinanceTeam
				related.FinanceEnterpriseGroup 	= HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
				related.FinanceTeam 			= LocalTeam				

		OpenClosePeriodRel
			one-to-many relation to ClosePeriod
			Field Mapping uses ByOpenPeriod
				related.CloseManagementGroup = HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup

		FinanceTeamMemberRel
			one-to-many relation to FinanceTeamMember
			Field Mapping uses ByResource
				related.FinanceEnterpriseGroup			= HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
				related.FinanceTeamMember.TeamMember	= FinanceResource

		CloseTaskTypeDefaultPrimaryRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
			Instance Selection
				where (related.IsPrimary
				and    related.IsDefault)

		CloseTaskTypeDefaultSecondaryRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
			Instance Selection
				where (related.IsSecondary
				and    related.IsDefault)

		CloseTaskTypeDefaultTertiaryRel
			one-to-many relation to CloseTaskType
			Field Mapping uses symbolic key
				related.CloseManagementGroup = HROrganization.FinanceEnterpriseGroupRel.FinanceEnterpriseGroup
			Instance Selection
				where (related.IsTertiary
				and    related.IsDefault)

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Sets
		ByBeginDate
			Sort Order
				HROrganization
				BeginDate
				DueDate
				FinanceResource
				FinanceResourceSchedule
		ByDueDate
			Sort Order
				HROrganization
				DueDate
				BeginDate
				FinanceResource
				FinanceResourceSchedule
		ByWorkStatus
			Sort Order
				HROrganization
				WorkStatus
				DueDate
				BeginDate
				FinanceResource
				FinanceResourceSchedule
		ByNavigateToThis
			Sort Order
				HROrganization
				NavigateToThis
				FinanceResource
				CreatedBy
				WorkStatus
				BeginDate
				DueDate
				FinanceResourceSchedule	
							

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
	
		Delete is a Purge Action
			valid when (AllowDelete)
			confirmation required

		Purge is a Purge Action
			restricted

		Start is an Instance Action
			valid when (AllowStart)
			Action Rules
				WorkStatus = 1 



		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
														
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)
			
			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop

		MarkComplete is an Instance Action
			default label is "Complete"
			valid when (AllowMarkComplete) 

			Action Rules
				WorkStatus = 2 


								
		Reschedule is an Instance Action 
			valid when (AllowReschedule)
			completion message is "ScheduleHasBeenUpdated"
			Parameters
				NewBegin			is Date
				NewDue				is Date
				NewDescription		is Alpha 100
				NewNotes			is Text
				AddAttachment		is Boolean
				NewAttachment		is an Attachment
				NewLink				is an URL
			Parameter Rules
				NewBegin
					initial value is BeginDate
				NewDue
					initial value is DueDate
				NewDescription
					initial value is Description
				NewNotes
					initial value is Notes
				NewAttachment
					initial value is Attachment
				NewLink
					initial value is WebsiteLink

			Action Rules
				constraint (NewDue >= NewBegin)
						"DueDateMustBeLaterThanBeginDate"

				BeginDate			= NewBegin
				DueDate				= NewDue
				Description			= NewDescription
				Notes				= NewNotes
				Attachment			= NewAttachment
				WebsiteLink			= NewLink

		Reminder is an Instance Action 		
			Parameters
				ReminderBeginDate				is Date
				ReminderDueDate					is Date
				ReminderDescription				is Alpha 100
				ReminderNotes					is Text
				ReminderAttachment				is an Attachment
				ReminderLink					is an URL
				FinancialForm					is Alpha 6
			Parameter Rules
				ReminderBeginDate
					initial value is current corporate date
					default to current corporate date
					constraint (ReminderBeginDate >= current corporate date)
						"SelectFutureDateOrLeaveBlank"
				ReminderDueDate
					initial value is current corporate date
					default to ReminderDueDate
					initial value is ReminderBeginDate
					constraint (ReminderDueDate >= ReminderBeginDate)
						"BeginDateCanNotBeBeforeDueDate"
				ReminderDescription
					required
			Action Rules
				invoke Create 
					invoked.HROrganization = HROrganization
					invoked.FinanceResource = actor.agent(Employee).Employee
					invoked.BeginDate = ReminderBeginDate		
					invoked.DueDate = ReminderDueDate		
					invoked.Description = ReminderDescription
					invoked.Notes = ReminderNotes		
					invoked.NavigateToThis = reference to FinanceResourceSchedule  
					invoked.Attachment = ReminderAttachment
					invoked.WebsiteLink = ReminderLink
					invoked.CreatedBy = 0 
					invoked.FinancialForm = FinancialForm
	
		WorkRequest is an Instance Action 		
			Parameters
				EnterpriseGroup
				ClosePeriod
				RequestTeam						is a FinanceTeam
				RequestTeamMember				is a FinanceTeamMember
				RequestPerson					is a FinanceResource
				RequestBeginDate				is Date
				RequestBeginTime				is Time
				RequestDueDate					is Date
				RequestDueTime					is Time
				RequestDescription				is Alpha 100
				RequestNotes					is Text
				RequestAttachment				is an Attachment
				RequestLink						is an URL
				FinancialForm					is Alpha 6
				CreateOneTimeTask				is Boolean
				OneTimeParentTask				is a ClosePeriodTask
				OneTimeApprovalCode				is an ApprovalCode
				AddOneTimeTaskToProcess			is Boolean
				OneTimeCompany					is a CompanyGroupField
				OneTimePriority					is Numeric 1
					States
						Low							value is 0
						Medium						value is 1
						High						value is 2
				OneTimeRisk						is Numeric 1
					States	
						Low							value is 0
						Medium						value is 1
						High						value is 2
				OneTimeRequireSupportingDoco	is Boolean
				OneTimeRequireJournalEntry      is Boolean
				OneTimeRequiresApproval			is Boolean
				OneTimeCompletionApprovalCode	is an ApprovalCode
				OneTimeLevelOfEffort			is Unsigned Decimal 10.2
				OneTimeCalendar					is a SystemCalendar
				OneTimePeriodGroup				is a SystemCalendarPeriodGroup
				OneTimePrimaryType				is a CloseTaskType
				OneTimeSecondaryType			is a CloseTaskType
				OneTimeTertiaryType				is a CloseTaskType
				OneTimeDueTimeSensitive			is Boolean
				OneTimeTaskAddOption			is Numeric 1
					States
						JustTheSelectedPeriod			value is 0
						AllOpenPeriodsForParentTask		value is 1
				OneTimeStartDays				is Numeric 3
				OneTimeStartDayOfMonth			is Unsigned Decimal 2
				OneTimeStartLastDayOfMonth		is Boolean
				OneTimeStartNextMonth			is Boolean
				OneTimeStartTime				is Time
				OneTimeEndDays					is Numeric 3
				OneTimeEndDayOfMonth			is Unsigned Decimal 2
				OneTimeEndLastDayOfMonth		is Boolean
				OneTimeEndNextMonth				is Boolean
				OneTimeEndTime					is Time
				OneTimeHours					is Unsigned Decimal size 10.2
				OneTimeWorkDaysOnly				is Boolean
				
			Parameter Rules
				EnterpriseGroup
					force default to actor.context.EnterpriseGroup
					
				ClosePeriod
					initial value is first OpenClosePeriodRel.ClosePeriod
					if (CreateOneTimeTask)
						constraint (ClosePeriod exists)
							"PeriodDoesNotExist"
						constraint (ClosePeriod.CloseManagementStatus.Open)
							"PeriodMustBeOpenToAddAOneTimeTask"
				RequestTeam
					if (CreateOneTimeTask)
						required
							"TeamRequiredForOneTimeTask"
					initial value is first FinanceTeamMemberRel.FinanceTeam
				RequestTeamMember
					initial value is FinanceResource
				RequestPerson
					if (!CreateOneTimeTask)
						required
							"PersonResponsibleRequiredForWorkRequest"
				RequestBeginDate
					initial value is current corporate date
					default to current corporate date
					constraint (RequestBeginDate >= current corporate date)
						"SelectFutureDateOrLeaveBlank"
				RequestBeginTime
					initial value is 28800 
				RequestDueDate
					initial value is current corporate date
					default to RequestDueDate
					initial value is RequestBeginDate
					constraint (RequestDueDate >= RequestBeginDate)
						"BeginDateCanNotBeBeforeDueDate"
				RequestDueTime
					initial value is 61200 
				RequestDescription
					required
				OneTimeParentTask
					if (CreateOneTimeTask)
						required
							"ParentTaskRequiredForAOneTimeTask"
						constraint (OneTimeParentTask.TaskLevel.Summary)
							"ParentCloseTaskMustBeSummaryLevel"
				OneTimeApprovalCode
					initial value is FinanceResource.CloseManagementGroupRel.OneTimeTaskDefaultApprovalCode
					if (CreateOneTimeTask
					and FinanceResource.CloseManagementGroupRel.RequireApprovalOfOneTimeTasks)
						required
							"ApprovalCodeRequiredForOneTimeTask"
				OneTimeCompany
					initial value is OneTimeParentTask.TaskCompany
				OneTimeCompletionApprovalCode
					if (OneTimeRequiresApproval)
						required
							"OneTimeTaskRequiresApprovalUponCompletion;CompletionApprovalCodeRequired"
				OneTimeCalendar
					initial value is OneTimeParentTask.CreatedFrom.DefaultCalendar
				OneTimePeriodGroup
					initial value is OneTimeParentTask.CreatedFrom.DefaultPeriodGroup
				OneTimePrimaryType
					initial value is first CloseTaskTypeDefaultPrimaryRel.CloseTaskType
				OneTimeSecondaryType
					initial value is first CloseTaskTypeDefaultSecondaryRel.CloseTaskType
				OneTimeTertiaryType
					initial value is first CloseTaskTypeDefaultTertiaryRel.CloseTaskType
				OneTimeStartTime
					initial value is 28800
					default to 28800
				OneTimeEndTime
					initial value is 61200
					default to 61200
				OneTimeHours
					initial value is OneTimeLevelOfEffort

			Action Rules
				if (CreateOneTimeTask)
					invoke WorkRequest OneTimeParentTask
						invoked.PrmClosePeriod					= ClosePeriod
						invoked.RequestTeam						= RequestTeam
						invoked.RequestTeamMember				= RequestTeamMember
						invoked.RequestPerson					= RequestPerson
						invoked.RequestBeginDate				= RequestBeginDate
						invoked.RequestBeginTime				= RequestBeginTime
						invoked.RequestDueDate					= RequestDueDate
						invoked.RequestDueTime					= RequestDueTime
						invoked.RequestDescription				= RequestDescription
						invoked.RequestNotes					= RequestNotes
						invoked.RequestAttachment				= RequestAttachment
						invoked.RequestLink						= RequestLink
						invoked.FinancialForm					= FinancialForm
						invoked.CreateOneTimeTask				= CreateOneTimeTask
						invoked.OneTimeParentTask				= OneTimeParentTask
						invoked.OneTimeApprovalCode				= OneTimeApprovalCode
						invoked.AddOneTimeTaskToProcess			= AddOneTimeTaskToProcess
						invoked.OneTimeCompany					= OneTimeCompany
						invoked.OneTimePriority					= OneTimePriority
						invoked.OneTimeRisk						= OneTimeRisk
						invoked.OneTimeRequireSupportingDoco	= OneTimeRequireSupportingDoco
						invoked.OneTimeRequireJournalEntry      = OneTimeRequireJournalEntry
						invoked.OneTimeRequiresApproval			= OneTimeRequiresApproval
						invoked.OneTimeCompletionApprovalCode	= OneTimeCompletionApprovalCode
						invoked.OneTimeLevelOfEffort			= OneTimeLevelOfEffort
						invoked.OneTimeCalendar					= OneTimeCalendar
						invoked.OneTimePeriodGroup				= OneTimePeriodGroup
	         			invoked.OneTimePrimaryType				= OneTimePrimaryType
	         			invoked.OneTimeSecondaryType			= OneTimeSecondaryType
	         			invoked.OneTimeTertiaryType				= OneTimeTertiaryType
	         			invoked.OneTimeDueTimeSensitive			= OneTimeDueTimeSensitive
						invoked.OneTimeTaskAddOption			= OneTimeTaskAddOption
						invoked.OneTimeStartDays				= OneTimeStartDays
						invoked.OneTimeStartDayOfMonth			= OneTimeStartDayOfMonth
						invoked.OneTimeStartLastDayOfMonth		= OneTimeStartLastDayOfMonth
						invoked.OneTimeStartNextMonth			= OneTimeStartNextMonth
						invoked.OneTimeStartTime				= OneTimeStartTime
						invoked.OneTimeEndDays					= OneTimeEndDays
						invoked.OneTimeEndDayOfMonth			= OneTimeEndDayOfMonth
						invoked.OneTimeEndLastDayOfMonth		= OneTimeEndLastDayOfMonth
						invoked.OneTimeEndNextMonth				= OneTimeEndNextMonth
						invoked.OneTimeEndTime					= OneTimeEndTime
						invoked.OneTimeHours					= OneTimeHours
						invoked.OneTimeWorkDaysOnly				= OneTimeWorkDaysOnly

				else
					invoke Create FinanceResourceSchedule
						invoked.HROrganization		= HROrganization
						invoked.FinanceResource		= RequestPerson
						invoked.BeginDate			= RequestBeginDate
						invoked.BeginTime			= RequestBeginTime		
						invoked.DueDate				= RequestDueDate
						invoked.DueTime				= RequestDueTime
						invoked.Description			= RequestDescription
						invoked.Notes				= RequestNotes		
						invoked.Attachment			= RequestAttachment
						invoked.WebsiteLink			= RequestLink
						invoked.FinancialForm		= FinancialForm
						if (RequestPerson = actor.agent(Employee).Employee)
							invoked.CreatedBy		= 0 
						else
							invoked.CreatedBy		= 2 
							invoked.WorkRequestedBy	= actor.agent(Employee).Employee

		SendMessage is an Instance Action
			Parameters
				MessageFor						is Alpha 08
					States
						Resource				value is "Resource"
						Team					value is "Team"
				MessageTeam					is a FinanceTeam
				MessagePerson					is a FinanceResource
				DialogueTitle					is Alpha 60
				Dialogue 						is Text 
				DialogueAttachment				is an Attachment
				DialogueLink					is an URL
				ResponseRequired				is Boolean
				PrmExternalToEmailAddress		is EmailAddressField with multiple addresses 
					holds pii
				PrmCcEmailAddress				is EmailAddressField with multiple addresses 
					holds pii
				PrmBccEmailAddress				is EmailAddressField with multiple addresses 
					holds pii
				PrmAddCcEmail					is Boolean
				PrmAddBccEmail					is Boolean
				PrmSendingLandmarkNotifications is Boolean
				PrmLocalActor					is an Actor

			Parameter Rules
				PrmExternalToEmailAddress
					if (PrmExternalToEmailAddress not entered)
						constraint(MessagePerson entered or MessageTeam entered)
							"MustEnter_a_Resource,_Team_and/or_Email_Address."
				PrmCcEmailAddress
					if (PrmCcEmailAddress entered)
						constraint(PrmExternalToEmailAddress entered)
							"MustEnter_To_E-mail_Address,_when_Cc_email_is_entered."
				PrmBccEmailAddress
					if (PrmBccEmailAddress entered)
						constraint(PrmExternalToEmailAddress entered)
							"MustEnter_To_E-mail_Address,_when_Bcc_email_is_entered."
				PrmSendingLandmarkNotifications
					if (MessagePerson not entered and MessageTeam not entered)
						constraint (not PrmSendingLandmarkNotifications entered)
							"MustEnterEither_Resource_and/or_TeamToSend_Notifications"
				DialogueTitle
					required
						"PleaseEnterASubject"
				Dialogue
					required
						"PleaseEnterYourMessage"
			Action Rules	
				if (MessagePerson entered)
					invoke Create FinanceResourceDialogue
						invoked.NavigateToThis = reference to FinanceResourceSchedule				
						invoked.HROrganization = HROrganization
						invoked.FinanceResource = MessagePerson
						invoked.DialogueTitle =	DialogueTitle
						invoked.Dialogue = Dialogue
						invoked.DialogueAttachment = DialogueAttachment
						invoked.SendToContact = MessagePerson
						invoked.Author = actor.agent(Employee).Employee
						invoked.DialogueLink = DialogueLink
						invoked.ResponseRequired = ResponseRequired
						invoked.MessageType = 0 


					invoke Create FinanceResourceDialogue 
						invoked.NavigateToThis = reference to FinanceResourceSchedule				
						invoked.HROrganization = HROrganization
						invoked.FinanceResource = actor.agent(Employee).Employee
						invoked.DialogueTitle =	DialogueTitle
						invoked.Dialogue = Dialogue
						invoked.DialogueAttachment = DialogueAttachment
						invoked.SendToContact = MessagePerson
						invoked.Author = actor.agent(Employee).Employee
						invoked.DialogueLink = DialogueLink
						invoked.ResponseRequired = ResponseRequired
						invoked.MessageType = 1 

					if (PrmSendingLandmarkNotifications)
						PrmLocalActor = MessagePerson.FinanceResourceActor
						send notification
							to PrmLocalActor
							description is "<DialogueTitle>"
							priority is high
							detail is "<Dialogue>"

				if (MessageTeam entered) 



					LocalTeam = MessageTeam	
					for each SendToTeamMembersRel  
						if ((each.FinanceTeamMember.TeamMember != actor.agent(Employee).Employee)
						and (each.FinanceTeamMember.TeamMember != MessagePerson))
							invoke Create FinanceResourceDialogue
								invoked.NavigateToThis = reference to FinanceResourceSchedule				
								invoked.HROrganization = HROrganization
								invoked.FinanceResource = each.FinanceTeamMember.TeamMember
								invoked.DialogueTitle =	DialogueTitle
								invoked.Dialogue = Dialogue
								invoked.DialogueAttachment = DialogueAttachment
								invoked.SendToTeam = MessageTeam
								invoked.SendToContact = each.FinanceTeamMember.TeamMember
								invoked.Author = actor.agent(Employee).Employee
								invoked.DialogueLink = DialogueLink
								invoked.GroupingForTeam = LocalGroupingForTeam	
								invoked.ResponseRequired = ResponseRequired	
								invoked.MessageType = 0 


							invoke Create FinanceResourceDialogue
								invoked.NavigateToThis = reference to FinanceResourceSchedule				
								invoked.HROrganization = HROrganization
								invoked.FinanceResource = actor.agent(Employee).Employee
								invoked.DialogueTitle =	DialogueTitle
								invoked.Dialogue = Dialogue
								invoked.DialogueAttachment = DialogueAttachment
								invoked.SendToTeam = MessageTeam
								invoked.SendToContact = each.FinanceTeamMember.TeamMember
								invoked.Author = actor.agent(Employee).Employee
								invoked.DialogueLink = DialogueLink
								invoked.GroupingForTeam = LocalGroupingForTeam	
								invoked.ResponseRequired = ResponseRequired	
								invoked.MessageType = 1 

							if (PrmSendingLandmarkNotifications)
								PrmLocalActor = each.FinanceTeamMember.TeamMember.FinanceResourceActor															
								send notification
									to PrmLocalActor
									description is "<DialogueTitle>"
									priority is high
									detail is "<Dialogue>"

				if (PrmExternalToEmailAddress entered)
					send email
						to PrmExternalToEmailAddress
						cc PrmCcEmailAddress
						bcc PrmBccEmailAddress
						from actor.agent(Employee).EmployeeWorkEmailAddress
						subject "<DialogueTitle>"
						Attachments
							attachment DialogueAttachment.File
						Contents
								"<Dialogue>"
								blank line
								"<DialogueLink>"		


		ReassignForResource is an Instance Action
			valid when (AllowResourceReassign)
			default label is "Reassign"
			Parameters
				CloseManagementGroup 							
				PrmFinanceTeam								is a FinanceTeam
				PrmTaskOwner								is a FinanceTeamMember
			Parameter Rules
				PrmFinanceTeam
					initial value is NavigateToThis(ClosePeriodTask).FinanceTeam
					required
						"TeamIsRequired"				
				PrmTaskOwner
					initial value is NavigateToThis(ClosePeriodTask).TaskOwner

			Action Rules
				invoke ReassignForResource NavigateToThis(ClosePeriodTask)
					invoked.PrmFinanceTeam	= PrmFinanceTeam
					invoked.PrmTaskOwner	= PrmTaskOwner

		ReassignToTeam is an Instance Action
			valid when (AllowReassignToTeam)
			Action Rules
				invoke ReassignToTeam NavigateToThis(ClosePeriodTask)

		StartTask is an Instance Action
			default label is "Start"
			valid when (AllowStartTask)
			completion message is "<NavigateToThis(ClosePeriodTask).TaskName>IsNowInProcess"
			Action Rules
				invoke StartWork NavigateToThis(ClosePeriodTask)
			
		StartTaskLate is an Instance Action
			default label is "Start"
			valid when (AllowStartTaskLate)
			completion message is "<NavigateToThis(ClosePeriodTask).TaskName>IsNowInProcess"
			subject is StartTaskLate

			Parameters
				PrmComment		is Alpha size up to 500

			Action Rules
				invoke StartWorkLate NavigateToThis(ClosePeriodTask)
					invoked.PrmComment	= PrmComment
					invoked.reason code	= reason code

		VoidTask is an Instance Action
            default label is "Void"
            valid when (AllowVoidTask)
            completion message is "<NavigateToThis(ClosePeriodTask).TaskName>IsNowInProcess"
            subject is VoidTask

            Parameters
                PrmComment        is Alpha size up to 500
                
            Action Rules
                if (FinanceResource.CloseManagementGroupRel.VoidReasonCodeRequired)            
                    constraint (reason code entered)            
                        "ReasonCodeIsRequiredForVoidingTheTask"
                        
                invoke VoidTask NavigateToThis(ClosePeriodTask)
                    invoked.PrmComment    = PrmComment
                    invoked.reason code    = reason code

		CompleteTask is an Instance Action
			default label is "Complete"
			subject is CloseTaskLate 
			valid when (AllowCompleteTask)
			Parameters
				PrmActualHours				is Unsigned Decimal size 10.2
				PrmDialogue					is Text
				PrmAttachment				is a Attachment
				PrmLinkToDocumentation		is an URL
				PrmLateComment				is Alpha size up to 500
			Parameter Rules
				PrmActualHours
					initial value is NavigateToThis(ClosePeriodTask).DisplayLevelOfEffort
			Action Rules
				invoke CloseTask NavigateToThis(ClosePeriodTask)
					invoked.PrmActualHours			= PrmActualHours
					invoked.PrmDialogue				= PrmDialogue
					invoked.PrmAttachment			= PrmAttachment
					invoked.PrmLinkToDocumentation	= PrmLinkToDocumentation
					invoked.PrmLateComment			= PrmLateComment
					invoked.reason code				= reason code

		UpdateEstimate is an Instance Action
			valid when (AllowUpdateEstimate)	
			subject is UpdatingTaskEstimate
			completion message is "UpdateComplete"

			Parameters
				ParmPercentComplete				is Percent 2
				ParmComment						is Alpha size up to 250
				ParmNewBeginDate				is Date
				ParmNewBeginTime				is Time
				ParmNewDueDate					is Date
				ParmNewDueTime					is Time
				ParmNewLevelOfEffort			is Unsigned Decimal size 10.2
				ParmReason						is Alpha size up to 500 
				
			Action Rules
				invoke UpdateEstimate NavigateToThis(ClosePeriodTask)
					invoked.PrmPercentComplete	= ParmPercentComplete
					invoked.PrmComment			= ParmComment
					invoked.PrmNewBeginDate		= ParmNewBeginDate
					invoked.PrmNewBeginTime		= ParmNewBeginTime
					invoked.PrmNewDueDate		= ParmNewDueDate
					invoked.PrmNewDueTime		= ParmNewDueTime
					invoked.PrmNewLevelOfEffort	= ParmNewLevelOfEffort
					invoked.PrmReason			= ParmReason
					invoked.reason code			= reason code
				
