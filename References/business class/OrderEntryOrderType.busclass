OrderEntryOrderType is a BusinessClass
    owned by oe
    prefix is OTP
    classic name is ORDERTYPE

    Ontology
        symbolic key is OrderEntryOrderType
            classic set name is OTPSET1
            classic name is ORDER-TYPE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
      	CreatePurchaseOrder  is Boolean  
      		classic name is CREATE-PO
      	Dropship 			 is Boolean
            classic name is DROPSHIP-FL
      	Pick                 is Boolean  
      		classic name is PICK-FL
      	Pack				 is Boolean
      		classic name is PACK-FL  
      	ShipImmediate        is Boolean
      		classic name is IMMEDIATE-FL
      	PrintPickList		 is Boolean
      		classic name is PICK-LIST-FL
      	PrintPackList 		 is Boolean
      		classic name is PACK-LIST-FL
      	PrintInvoice		 is Boolean
      		classic name is INVOICE-FL
        WarehouseProcessType
            classic name is PROCESS-TYPE
        BillingInvoiceType
            classic name is INVC-TYPE
		Active				 is Boolean 
			classic name is ACTIVE-STATUS               
      	InUse				 is Boolean  
      		classic name is USED-FL      
		RushOrder			 is Boolean
			classic name is RUSH-FL        

	Attach Rules
		constraint (Active)
			"OrderType<OrderEntryOrderType>MustBeActive"

    Relations
        BillingCompanyRel
            one-to-one relation to BillingCompany
            Field Mapping uses symbolic key
                related.Company = Company

    Field Rules
		Active
			default to true
			
        Description
            required

        BillingInvoiceType
            required
            constraint (!BillingInvoiceType.Credit)
            	"CannotEnterInvoiceTypeWhereCreditIsSet"   				//OE51.116"

        WarehouseProcessType
        	if (!Dropship)
            	required    											
      	
      	Dropship 
      		if (!CreatePurchaseOrder)
      			constraint (!Dropship)
      				"CannotSetDropshipWhenCreatePurchaseOrderIsNotSet" 			//OE51.250"			 
  			
  			constraint (CreatePurchaseOrder)
  				"DropshipOrderTypeMustAlsoBeCreatePOType"				//OE51.118"       	 
      	
      	ShipImmediate  
      		if (Dropship)
      			constraint (!ShipImmediate)
      				"DropshipOrderTypeCannotBeShipImmediate"			//OE51.109"      
      		
      		if (CreatePurchaseOrder)
	  			constraint (!ShipImmediate)
	      				"CreatePOOrderTypeCannotBeShipImmediate"		//OE51.117"  
	      	
	      	constraint (!WarehouseProcessType.RequirePickingFeedback)
	      		"RequirePickingAndShipImmediateCannotBeOnOrderType"		//OE51.113"
	      	
	      	constraint (!WarehouseProcessType.RequirePackingFeedback)
	      		"RequirePackingAndShipImmediateCannotBeOnOrderType"		//OE51.114"	      		
	      		
      	PrintPickList	
      		if (!ShipImmediate)
      			constraint (!PrintPickList)
      				"CannotSetPrintPickListWhenNotShipImmediate"		//OE51.110"	 
      	
      	PrintPackList 
      		if (!ShipImmediate)
      			constraint (!PrintPackList)
      				"CannotSetPrintPackListWhenNotShipImmediate"		//OE51.111"	       			
      	
      	PrintInvoice	
      		if (!ShipImmediate)
      			constraint (!PrintInvoice)
      				"CannotSetPrintInvoiceWhenNotShipImmediate"			//OE51.112"	  
	Conditions

		RecordExits 
			when (OrderEntryOrderType exists)

	Derived Fields	

		OrderEntryOrderTypeFormTitle is a DerivedField
			type is MessageField
			if (RecordExits)
				return OrderEntryOrderTypeTitle
			else
				return OrderEntryOrderTypeTitleNoRecord

		OrderEntryOrderTypeTitle is a LabelField
			"OrderType_-_<OrderEntryOrderType>"
		
		OrderEntryOrderTypeTitleNoRecord is a LabelField
			"OrderType"

		OrderEntryOrderTypeDescriptionID is a MessageField
			"<OrderEntryOrderType>_-_<Description>"
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
