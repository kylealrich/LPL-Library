PfiUserProfile is a BusinessClass
    owned by pfi
    prefix is PfiUR
    default label is "UserProfile"
	framework type is ProcessFlow

    Ontology
        symbolic key is PfiUserProfile

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements CompoundDocument
            Document Components
                PfiUserPreference set
		implements AuditLogEntryActions

    Persistent Fields

        FilterCategory           is Boolean
            default label is "FilterIsEnabled"
        Reminders      is Boolean
            default label is "SendSummaryEmailReminder"
        NotifyOption             is a PfiNotifyOption
        DisableUserNotifications is Boolean
        PortalOptions            is a PfiPortalOptions

    Derived Fields

        TaskDescription is a DerivedField
            type is Alpha size 100
            if (PfiActorRel.PersonName.GivenName != blank and PfiActorRel.PersonName.FamilyName != blank)
                return PfiActorRel.PersonName.FirstAndLastName
            else
                return PfiUserProfile

        EmailAddress is a DerivedField
            type is EmailAddressField
            return PfiActorRel.ContactInfo.EmailAddress

        LastName is a DerivedField
            type is Alpha up to 50
            return PfiActorRel.PersonName.FamilyName

        FirstName is a DerivedField
            type is Alpha up to 50
            return PfiActorRel.PersonName.GivenName

        IsoCountry is a DerivedField
            type is Alpha size up to 100
            default label is "IsoCountry/\Jurisdiction"
            return PfiActorRel.IsoLocale.IsoCountry

        IsoLanguage is a DerivedField
            type is Alpha size up to 100
            return PfiActorRel.IsoLocale.IsoLanguage

        Variant is a DerivedField
            type is Alpha size up to 100
            return PfiActorRel.IsoLocale.Variant

        MobileNumber is a DerivedField
            type is Alpha size 21
            return PfiActorRel.ContactInfo.MobilePhone.MobilePhoneNumber.InternationalPrefix + PfiActorRel.ContactInfo.MobilePhone.MobilePhoneNumber.SubscriberNumber

    Sets

        ByFilterCategoryUserProfile
            sql name is PfiUR01
            indexed
            Sort Order
                FilterCategory
                PfiUserProfile

        RemindersEnabledUsers
            sql name is PfiUR02
            indexed
            Sort Order
                PfiUserProfile
            Instance Selection
                where (Reminders)

        ByInbasketLandingPageUserProfile
            sql name is PfiUR03
            indexed
            Sort Order
                PortalOptions.InbasketLandingPage
                PfiUserProfile

        ByRemindersUserProfile
            sql name is PfiUR04
            indexed
            Sort Order
                Reminders
                PfiUserProfile

        ByNotifyOptionUserProfile
            sql name is PfiUR05
            indexed
            Sort Order
                NotifyOption
                PfiUserProfile

        ByDisableUserNotificationsUserProfile
            sql name is PfiUR06
            indexed
            Sort Order
                DisableUserNotifications
                PfiUserProfile

    Rule Blocks

        CreateTaskAndUserTask
            if (not PfiTaskRel exists)
                invoke Create PfiTask
                    invoked.PfiTask.TaskName = PfiUserProfile
                    invoked.PfiTask.TaskType = LocalTaskType.User
            if (not PfiUserTaskForUserRel exists)
                invoke Create PfiUserTask
                    invoked.PfiUserProfile   = PfiUserProfile
                    invoked.PfiTask.TaskName = PfiUserProfile
                    invoked.PfiTask.TaskType = LocalTaskType.User

    Field Rules

        FilterCategory
            default to false
            initial value is false

            if (FilterCategory changed)
                for each PfiUserTask set
                    invoke Update each
                        invoked.UserProfileFilterCategory = FilterCategory

        Reminders
            default to false
            initial value is false

        DisableUserNotifications
            default to false
            initial value is false

    Relations

        PfiActorRel
            one-to-one relation to Actor
            Field Mapping uses symbolic key
                related.Actor = PfiUserProfile

        PfiUserTaskForUserRel
            one-to-one relation to PfiUserTask
            delete cascades
            Field Mapping uses symbolic key
                related.PfiUserProfile   = PfiUserProfile
                related.PfiTask.TaskName = PfiUserProfile
                related.PfiTask.TaskType = 1 

        PfiValidUserTaskRel
            one-to-many relation to PfiUserTask
            delete cascades 
            Field Mapping uses ByUserProfileStartDateStopDateTask
                related.PfiUserProfile = PfiUserProfile
            Instance Selection
                where (related.IsUserTaskValid)
                
        PfiTaskRel
            one-to-one relation to PfiTask
            delete cascades
            Field Mapping uses symbolic key
                related.PfiTask.TaskName = PfiUserProfile
                related.PfiTask.TaskType = 1 
        
   
   
    Actions

        Create is an Action

            Local Fields
                LocalTaskType is a PfiTaskType

            Exit Rules
                include CreateTaskAndUserTask

        Update is an Action

            Local Fields
                LocalTaskType is a PfiTaskType

            Exit Rules
                include CreateTaskAndUserTask

        Delete is an Action
