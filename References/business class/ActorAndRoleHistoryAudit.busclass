ActorAndRoleHistoryAudit is a BusinessClass
    owned by security
    prefix is ACARH
    stored in environment
    framework type is Security 

    Ontology
        symbolic key is ActorAndRoleHistoryAudit 

    Patterns
        implements CRUD
        disable Auditing
        disable EffectiveDated
        disable AsOfDateProcessing

    Persistent Fields
      	IsActorDisabled is Boolean
      		default label is "ActorIsDisabled"	
		Action is Alpha up to 100
		ActionType is Alpha up to 50
	    UpdateActor is Alpha up to 100
	    	default label is "ChangedBy"
    	BusinessObjectRef is BusinessObjectReference
		    		
	Local Fields
		LocDateForStart is Date
		
	Derived Fields
		StartOfDay is a NativeField
			type is TimeStamp
			restricted
				    
	Sets	
		ByUpdateTimeStamp
			duplicates
			Sort Order
			    ActorAndRoleHistoryAudit.HistoryStamp

		
							    
	Conditions
        ActorIsDeleted
            when (ActionType like "Delete" and BusinessObjectRef.BusinessClassName = "Actor")
        RoleIsDeleted
            when (ActionType like "Delete" and BusinessObjectRef.BusinessClassName = "ActorRole")
        
      	ActorRelExists
      		when (ActorRel exists)


      		
    Relations
        ActorRel
            one-to-one relation to Actor
            include deleted record
            Field Mapping uses symbolic key
                related.Actor = BusinessObjectRef(Actor).Actor







                             
	Actions

		Create is a Create Action
			restricted
			Action Rules
				autosequence ActorAndRoleHistoryAudit.Sequence using ByUpdateTimeStamp
					minimize contention
					
		Delete is a Delete Action
			restricted
	
 		Purge is a Purge Action
		
		PurgeAll is a Set Action
			run in background
			Action Rules
				Instance Rules
					invoke Purge

		PurgeInRange is a Set Action
			run in background
			Parameters
				ThruDate 			is Date
				PurgeOffsetDays 	is Numeric size 3

			Local Fields
				LocalThruTime 	is TimeStamp

			Parameter Rules
				ThruDate   
					if (ThruDate entered)
						LocDateForStart = (ThruDate + 1 day)
						LocalThruTime = StartOfDay
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThroughEndDateOrOffset"
					else
						LocDateForStart  = current date - (PurgeOffsetDays - 1)
						LocalThruTime = StartOfDay
				
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThroughEndDateAndOffset"

			Instance Selection
				where (ActorAndRoleHistoryAudit.HistoryStamp < LocalThruTime) 
									
			Action Rules
				Instance Rules
					invoke Purge
					            
