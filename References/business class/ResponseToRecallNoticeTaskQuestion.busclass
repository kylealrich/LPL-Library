ResponseToRecallNoticeTaskQuestion is a BusinessClass
    sql name is TaskQuestionResponse
    owned by recall
    prefix is RMTQR

    Ontology
    	part of RecallNoticeTask
    		delete cascades
    		relative key is RecallNoticeTaskQuestion
    			
   	Patterns 
        implements TemplateDriven by RecallNoticeTaskQuestion
        	create instance
        		when (Response entered)
		implements ContextualParent
   			    			
    Persistent Fields
    	Response 		is a RecallNoticeTaskQuestionAnswer
   		Attachment 				
    
    Derived Fields
    
    	TaskQuestionResponse is a ConditionalField
    		type is Alpha size 250
    		if (RecallNoticeTaskQuestion.ResponseType.Text)
    			Response.TextValue
    		else
    		if (RecallNoticeTaskQuestion.ResponseType.Number)
    			Response.NumericValue
    		else 
    		if (RecallNoticeTaskQuestion.ResponseType.Date)
    			DerivedDate
    		else
    		if (RecallNoticeTaskQuestion.ResponseType.List)
    			Response.ListValue.Answer
    		else
    		if (RecallNoticeTaskQuestion.ResponseType.YesNo)
    			if (Response.YesNoValue.Yes)
    				YesText
    			else 
    				NoText
    		else
    			YesNoTextValue
    			    		
		YesNoTextValue is a StringField
			type is Alpha size 260
			restricted
			YesNoText
			"-"
			Response.YesNoText.Text

		YesNoText is a ConditionalField
			type is Text
			restricted
			if (Response.YesNoText.YesNo = 1)
				YesText
			else
				NoText

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"	
			
		DerivedDate is a StringField
			type is Alpha 10
			restricted
			Response.DateValue month
			"/"
			Response.DateValue day
			"/"
			Response.DateValue year
    		    
    	AnswerMessage is a MessageField
    		restricted
    		"Answer"
    	
    	ResponseLabelRequired is a ConditionalField
    		type is Alpha 10
    		restricted
    		if (RecallNoticeTaskQuestion.ResponseRequired)
    			"*"
    		else
    			blank
    			
    	ResponseLabel is a StringField
    		type is Text
    		restricted
    		ResponseLabelRequired AnswerMessage
    		
    	AttachmentMessage is a MessageField
    		restricted
    		"Attachment"
    		
    	AttachmentRequiredLabel is a ConditionalField
    		type is Alpha 15
			restricted
    		if (RecallNoticeTaskQuestion.AlwaysRequireResponseAttachment)
    			"*"
    		else
    			blank
    			
    	AttachmentLabel is a StringField
    		type is Text
    		restricted
    		AttachmentRequiredLabel AttachmentMessage
    		
    	ResponseRequiredMessage is a MessageField
    		restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
        	restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
        	restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsYes"
            
        NoAttachmentMessage is a MessageField
        	restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsNo"
            
        YesOrNoAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsYes"
            
        NoAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsNo"
            
        AttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsYes"
            
        NoAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsNo"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
            restricted
            if (RecallNoticeTaskQuestion.ResponseRules.ResponseRequired
            or  RecallNoticeTaskQuestion.YesNoResponseRules.ResponseRequired)
                ResponseRequiredMessage        
            else
            if (RecallNoticeTaskQuestion.ResponseRules.ResponseRequiredAndAttachmentRequired
            or  RecallNoticeTaskQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
                ResponseAndAttachmentRequiredMessage
            else
            if (RecallNoticeTaskQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
                YesAttachmentMessage
            else
            if (RecallNoticeTaskQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                NoAttachmentMessage
            else
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
                YesOrNoAndTextMessage
            else
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
                AttachmentOrTextMessage
            else
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                YesAttachmentOrTextMessage
            else
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
                NoAttachmentOrTextMessage
            else 
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
                AttachmentAndTextMessage
            else
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                YesAttachmentAndTextMessage
            else
            if (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
                NoAttachmentAndTextMessage
            else
                blank
                            
    		    			
    Conditions
    	HasAttachment
    		restricted
    		when (RecallNoticeTaskQuestion.Attachment entered)
    		
    	AttachmentUploadAllowed
    		restricted
    		when (RecallNoticeTaskQuestion.AllowResponseAttachment)
    	    	
    	HasResponseAttachment
    		restricted
    		when (Attachment entered)
    		
    	AttachmentAlwaysRequired   
    		restricted
            when (RecallNoticeTaskQuestion.AlwaysRequireResponseAttachment
            and  (RecallNoticeTaskQuestion.NonYesNoResponseType
            or    RecallNoticeTaskQuestion.ResponseType.YesNo))

		CanAnswerQuestion 
			restricted 
			when ((RecallNoticeTask.IsOwner
			or     RecallNotice.MyCoordinatorNotices)
			and    RecallNoticeTask.Status.Accepted)

    Field Rules
	    Response
            if (RecallNoticeTaskQuestion.ResponseType.YesNoText
            and !HasResponseAttachment
            and (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
            or  (Response.YesNoText.YesNo.Yes
            and  RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
            or  (Response.YesNoText.YesNo.No
            and  RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
                constraint (Response.YesNoText.Text entered)
                    "MustEnterEitherTextOrAnAttachmentForQuestion<RecallNoticeTaskQuestion.Description>"
                    
            if (RecallNoticeTaskQuestion.ResponseType.YesNo
            and (Response.YesNoValue.Yes
            and  RecallNoticeTaskQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
            or  (Response.YesNoValue.No
            and  RecallNoticeTaskQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<RecallNoticeTaskQuestion.Description>"    
            
            if (AttachmentAlwaysRequired
            and !RecallNoticeTaskQuestion.ResponseType.YesNoText)
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<RecallNoticeTaskQuestion.Description>"
                       
            if  (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
            or   RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (Response.YesNoText.YesNo.Yes
            and  RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (Response.YesNoText.YesNo.No
            and  RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (Response.YesNoText.Text entered)
                    "MustEnterTextForQuestion<RecallNoticeTaskQuestion.Description>"
                    
            if  (RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (Response.YesNoText.YesNo.Yes
            and  RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (Response.YesNoText.YesNo.No
            and  RecallNoticeTaskQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<RecallNoticeTaskQuestion.Description>" 
	Actions
    	Create is a Create Action
			valid when (CanAnswerQuestion)
			Action Rules
				
				if (RecallNoticeTask.Status.New)
					constraint (false)
						"MustAcceptTheTaskBeforeAnsweringQuestions"
				
				if (RecallNoticeTask.Status.Completed
				and RecallNoticeTask.NotProposedItem)
					constraint (false)
						"CanOnlyCreateResponsesForActiveTasks"
							
		Update is an Update Action
			valid when (CanAnswerQuestion)
			Action Rules
				if (RecallNoticeTask.NotProposedItem)
					constraint (RecallNotice.Status.Active)
						"CanOnlyUpdateResponsesForActiveNotices"
				
					constraint (RecallNoticeTask.Status.Accepted)
						"CanOnlyUpdateResponsesForActiveTasks"
					
				constraint (!RecallNoticeTask.ItemProposalApprovedOrRejected)
					"CannotUpdateResponseForNewItemProposalReviewThatIsApprovedOrRejected"
	
		Delete is a Delete Action
			valid when (CanAnswerQuestion)
			Action Rules
				constraint (RecallNotice.Status.Active
				and         RecallNoticeTask.NotProposedItem)
					"CanOnlyDeleteResponsesForActiveNotices"
					
				constraint (!RecallNoticeTask.ItemProposalApprovedOrRejected)
					"CannotDeleteResponseForNewItemProposalReviewThatIsApprovedOrRejected"				
					
		Purge is a Purge Action
			restricted
