CashForecastAccount is a BusinessClass
	owned by cashmgmt
	prefix is CMCFA

	Ontology
		symbolic key is CashForecastAccount
				
 	Persistent Fields
 		ForecastCalendar				is a SystemCalendar
 		AccountType						is a snapshot of CashForecastAccount.CashManagementAccount.AccountType
 		NumberOfCalendarDays			is Numeric 4
 			default label is "NumberOfDays" 
		NumberOfCalendarWeeks			is Numeric 4
			default label is "NumberOfWeeks"
		NumberOfCalendarMonths			is Numeric 4
			default label is "NumberOfMonths"
		CorporateBaseCurrencyRate		is a CurrencyExchangeRate
		LocationCurrencyRate			is a CurrencyExchangeRate
				
  	Local Fields
  		LocalCashTransactionCategory	is a CashTransactionCategory
		LocalNumberOfDays			    is Numeric 4
		LocalNumberOfWeeks			    is Numeric 4
		LocalNumberOfMonths				is Numeric 4
		LocalWeek			   			is Numeric 4
		LocalMonth					    is Numeric 4
		LocalRollingDateRange			is a DateRange
		LocalBankDayNumber				is Numeric 4

	Context Fields
		CashForecastPublishDate
			
	Derived Fields
  		SummarizedTransactionMessage is a MessageField
  			restricted
			"SummarizedTransaction"
		
		CurrentDate is a DerivedField
			type is Date
			restricted
			return CashForecast.ForecastDateRange.Begin
			
		CurrentPeriod is a DerivedField
			type is Numeric 3
			return CurrentDate year day
		
		CurrentWeek is a DerivedField
			type is Numeric 2
			restricted
			return CurrentDate week
			
		CurrentMonth is a DerivedField
			type is Numeric 2
			restricted
			return CurrentDate month
		
		CurrentYear is a DerivedField
			type is Numeric 4
			return CurrentDate year
		
		MatrixPositionToDate is a DerivedField
			type is Date
			if (CashForecast.ForecastDateRange.End < current corporate date)
				return CashForecast.ForecastDateRange.End
			else
				return current corporate date
				
  	Field Rules
  		ForecastCalendar
  			initial value is CashManagementGroup.CorporateCalendar
  			default to CashManagementGroup.CorporateCalendar
  			required
  			
  			if (HaveBuiltForecast)
  				cannot be changed
  					"ForecastCalendarCannotBeChangedAfterForecastHasBeenBuilt"
 
  		CorporateBaseCurrencyRate
			if (CashForecastAccount.CashManagementAccount.Currency = CashManagementGroup.Currency)
   				force default to 1
   			
  		LocationCurrencyRate
   			if (CashManagementGroup.Currency != CashForecastAccount.CashManagementAccount.Currency
   			and CashManagementGroup.Currency = CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
				default to CorporateBaseCurrencyRate  	
  				constraint (CorporateBaseCurrencyRate = LocationCurrencyRate)
   					"CurrencyExchangeRateForLocationAndCorporateBaseCannotBeDifferentForSameCurrency"
 
 			if (CashForecastAccount.CashManagementAccount.Currency = CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
   				force default to 1
   				
  	Conditions
  		HaveBuiltForecast
  			restricted
  			when (CashForecast.HaveBuiltForecast)
  		
  		CanSelectAccount
  			restricted
  			when (!HaveBuiltForecast
  			and   CashForecast.Status.New)
  			
  		CanAddAccount
  			restricted
  			when (HaveBuiltForecast
  			and   CashForecast.Status.New)
  		
  		CanAddForecastTransaction
  			restricted
  			when (HaveBuiltForecast
  			and   CashForecast.IsUpdatable)
  			
  		CanMaintain
  			restricted
  			when (!HaveBuiltForecast
  			and   CashForecast.IsUpdatable)
  				
  		CanDelete
  			restricted
  			when (CashForecast.Status.New)
  		
		AccountCurrencyNotCorporateBaseCurrency
			restricted
			when (CashForecastAccount.CashManagementAccount.Currency != CashManagementGroup.Currency)
			
		AccountCurrencyNotLocationCurrency
			restricted
			when (CashForecastAccount.CashManagementAccount.Currency != CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)

		RequiresCurrencyExchangeRates
			restricted
			when (AccountCurrencyNotCorporateBaseCurrency
			or    AccountCurrencyNotLocationCurrency)

		RequiredCurrencyRateNotEntered
			restricted
			when ((AccountCurrencyNotCorporateBaseCurrency
			and    CorporateBaseCurrencyRate	!entered)	
			or    (AccountCurrencyNotLocationCurrency
			and    LocationCurrencyRate			!entered))	

		RequiredCurrencyRateEntered
			restricted
			when ((AccountCurrencyNotCorporateBaseCurrency
			and    CorporateBaseCurrencyRate entered)	
			or    (AccountCurrencyNotLocationCurrency
			and    LocationCurrencyRate entered))

  	Relations
  		CashForecastCategoryByDisplayOrderRel
			one-to-many relation to CashForecastCategory
		 	Field Mapping uses ByDisplayOrder
		 		related.CashManagementGroup	= CashManagementGroup			
		 		related.CashForecast		= CashForecast
		 	Instance Selection
		 		where (!related.HasChild)
		 		
		CashForecastCategoryRel
			one-to-one relation to CashForecastCategory
			Field Mapping uses symbolic key
				related.CashManagementGroup							 = CashManagementGroup
				related.CashForecast								 = CashForecast
				related.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory
				
		CashForecastPeriodByAccountRel
			one-to-many relation to CashForecastPeriod
			delete cascades
			Field Mapping uses symbolic key
				related.CashManagementGroup	= CashManagementGroup
				related.CashForecast		= CashForecast
  				related.CashForecastAccount = CashForecastAccount
  		
  		CashForecastPeriodAmountByCategoryRel
			one-to-many relation to CashForecastPeriodAmount
			Field Mapping uses ByCashTransactionCategory
				related.CashManagementGroup		  					 = CashManagementGroup
				related.CashForecast		  						 = CashForecast
				related.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory
				related.CashForecastAccount 	 					 = CashForecastAccount
						
		ForecastCalendarBankDayRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup	= CashManagementGroup.EnterpriseGroup
				related.SystemCalendar 	= ForecastCalendar
			Instance Selection
				where (related.IsBankDay
				and	   related.SystemCalendarDate within CashForecast.ForecastDateRange)

		RollingForecastCalendarBankDayRel
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup	= CashManagementGroup.EnterpriseGroup
				related.SystemCalendar 	= ForecastCalendar
			Instance Selection
				where (related.IsBankDay
				and	   related.SystemCalendarDate within LocalRollingDateRange)
		
		CashForecastAccountDateOverlapRel
			one-to-many relation to CashForecastAccount
			Field Mapping uses ByCashManagementAccount
				related.CashManagementGroup = CashManagementGroup
				related.CashForecastAccount = CashForecastAccount
			Instance Selection
				where (related.CashForecast != CashForecast
				and    related.CashForecast.Status.Active
				and   !related.CashForecast.Simulated
				and	  ((CashForecast.ForecastDateRange.Begin within related.CashForecast.ForecastDateRange
				or      CashForecast.ForecastDateRange.End within related.CashForecast.ForecastDateRange)
				or     (related.CashForecast.ForecastDateRange.Begin within CashForecast.ForecastDateRange
				or      related.CashForecast.ForecastDateRange.End within CashForecast.ForecastDateRange)))

		PublishedCashForecastPeriodRel
			one-to-many relation to CashForecastPeriod
			Field Mapping uses symbolic key
				related.CashManagementGroup	= CashManagementGroup
				related.CashForecast		= CashForecast
  				related.CashForecastAccount = CashForecastAccount
  			Instance Selection
  				where (related.CashForecastPeriod within CashForecastPublishDate.PublishDateRange)
  		
  		CashForecastPeriodAmountCubeRel
			one-to-one relation to AnalyticCube
	        Field Mapping uses AnalyticCubeSet
	        	related.BusinessClass = "CashForecastPeriodAmount"
	        			
  	Matrix Forms
		CashForecastMatrix
			rows are CashForecastCategoryByDisplayOrderRel
			columns are CashForecastPeriod set
			cell is CashForecastPeriodAmount
				create instance
                	when (ForecastAmount entered
                	or    CashForecastPeriodAmount exists)
		
  	Sets
  		ByCashManagementAccount
 			Sort Order
 				CashManagementGroup
 				CashForecastAccount
 				CashForecast
  	
  	Rule Blocks
  		CreateCashForecastPeriod
			LocalNumberOfWeeks = 1
			LocalNumberOfMonths = 1
			LocalWeek = first ForecastCalendarBankDayRel.SystemCalendarDate week
			LocalMonth = first ForecastCalendarBankDayRel.SystemCalendarDate month
			LocalBankDayNumber = 1
			for each ForecastCalendarBankDayRel
				invoke Create CashForecastPeriod
					invoked.CashManagementGroup	= CashManagementGroup
					invoked.CashForecast		= CashForecast
					invoked.CashForecastAccount = CashForecastAccount
					invoked.CashForecastPeriod	= each.SystemCalendarDate
					invoked.BankDayNumber		= LocalBankDayNumber
				
				LocalNumberOfDays +=1
				LocalBankDayNumber +=1
				
				if (LocalWeek != each.SystemCalendarDate week)
					LocalNumberOfWeeks +=1
					LocalWeek = each.SystemCalendarDate week
					
				if (LocalMonth != each.SystemCalendarDate month)
					LocalNumberOfMonths +=1
					LocalMonth = each.SystemCalendarDate month
			
			NumberOfCalendarDays = LocalNumberOfDays
			NumberOfCalendarWeeks = LocalNumberOfWeeks
			NumberOfCalendarMonths = LocalNumberOfMonths

		CreateRollingCashForecastPeriod
			
			LocalRollingDateRange = PrmRollingDateRange
			LocalBankDayNumber = last CashForecastPeriodByAccountRel.BankDayNumber + 1
			for each RollingForecastCalendarBankDayRel
				constraint (each.SystemCalendarDate >= current corporate date)
					"CannotMaintainForecastForPeriodsPriorToToday"
				invoke Create CashForecastPeriod
					invoked.CashManagementGroup	= CashManagementGroup
					invoked.CashForecast		= CashForecast
					invoked.CashForecastAccount = CashForecastAccount
					invoked.CashForecastPeriod	= each.SystemCalendarDate
					invoked.BankDayNumber		= LocalBankDayNumber

				LocalBankDayNumber +=1

  	Actions
  		Create is a Create Action
			valid when (CanMaintain)
			
		AddAccount is a Create Action 
			valid when (CanAddAccount)
			confirmation required
				"ForecastCalculationsForAccount<CashForecastAccount.CashManagementAccount>WillBePerformedForEachCategoryDefinedOnTheCashForecast;WouldYouLikeToContinue?"
			completion message is "AccountForecastCalculationHasBeenSubmittedToTheQueueForProcessing"
			Field Rules
				CorporateBaseCurrencyRate
			   		if (CashForecastAccount.CashManagementAccount.Currency != CashManagementGroup.Currency)
 						required
 						
 				LocationCurrencyRate
 					if (CashForecastAccount.CashManagementAccount.Currency != CashForecastAccount.CashManagementAccount.CashManagementLocation.Currency)
 						required		

			Exit Rules
				include CreateCashForecastPeriod 
										
				invoke CalculateForecastAmountForAllCategories CashForecastCategory
					invoked.PrmCashManagementGroup	 = CashManagementGroup
					invoked.PrmCashForecast			 = CashForecast	
					invoked.PrmCashManagementAccount = CashForecastAccount.CashManagementAccount
					invoked.PrmForecastCalendar		 = ForecastCalendar
								
  		Update is an Update Action
  			valid when (CanMaintain)

		UpdateCurrencyExchangeRates is an Update Action
			valid when (CanMaintain)
			Field Rules
		 		ForecastCalendar
		 			cannot be changed
		 		NumberOfCalendarDays
		 			cannot be changed
				NumberOfCalendarWeeks
		 			cannot be changed
				NumberOfCalendarMonths
		 			cannot be changed

  		CreateCashForecastPeriod is an Instance Action
  			default label is untranslatable
  			restricted
  			Action Rules
  				include CreateCashForecastPeriod

		CreateRollingCashForecastPeriod is an Instance Action
  			default label is untranslatable
  			restricted
  			Parameters
				PrmRollingDateRange	is a DateRange
			
			Parameter Rules
				PrmRollingDateRange
					required
				
			Action Rules
  				include CreateRollingCashForecastPeriod

  		AddForecastTransaction is an Instance Action 
  			valid when (CanAddForecastTransaction)
			completion message is "AddForecastTransactionHasBeenSubmittedToTheQueueForProcessing"
			Parameters
				PrmCashTransactionCategory		is a CashTransactionCategory
				PrmForecastCreationParameter	is a ForecastCreationParameter
				
			Parameter Rules
				PrmCashTransactionCategory		required
				PrmForecastCreationParameter	required
				
			Entrance Rules
				LocalCashTransactionCategory = PrmCashTransactionCategory
				
				if (CashForecastCategoryRel exists)
					confirmation required
						"ForecastAmountsForAccount<CashForecastAccount.CashManagementAccount>,Category<PrmCashTransactionCategory>WillBeRecalculated;WouldYouLikeToContinue?"
						
					invoke Delete CashForecastPeriodAmountByCategoryRel
					
					invoke Update CashForecastCategoryRel 
						invoked.CashManagementGroup							 = CashManagementGroup
						invoked.CashForecast								 = CashForecast
						invoked.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory
						invoked.ForecastCreationParameter					 = PrmForecastCreationParameter
						invoked.LocalCashManagementAccount  				 = CashForecastAccount.CashManagementAccount
						invoked.BypassAmountCalculation						 = true
						
				if (!CashForecastCategoryRel exists)
					invoke Create CashForecastCategory
						invoked.CashManagementGroup							   = CashManagementGroup
						invoked.CashForecast								   = CashForecast
						invoked.CashForecastCategory.CashTransactionCategory   = LocalCashTransactionCategory
						invoked.ParentForecastCategory.CashTransactionCategory = LocalCashTransactionCategory.ParentTransactionCategory
						invoked.ForecastCreationParameter					   = PrmForecastCreationParameter
								
			Action Rules
				invoke CalculateForecastAmountByCategory CashForecastCategoryRel
					invoked.PrmCashManagementGroup	 = CashManagementGroup
					invoked.PrmCashManagementAccount = CashForecastAccount.CashManagementAccount
					invoked.PrmForecastCalendar		 = ForecastCalendar
			
			Exit Rules
				if (CashForecast.HaveBuiltForecast)
					invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
		
  		Delete is a Delete Action
  			valid when (CanDelete)
  			Action Rules
  				if (CashForecast.HaveBuiltForecast)
  					invoke Delete CashForecastPeriod set
  			
  			Exit Rules
  				if (CashForecast.HaveBuiltForecast)
  					invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
  	
  		Purge is a Purge Action
  			restricted
