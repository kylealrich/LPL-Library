PurchaseOrderReceiptImport is a BusinessClass
    owned by ma
    prefix is MRH
    classic name is MACRECHDR

    Ontology
        symbolic key is PurchaseOrderReceiptImport
            classic set name is MRHSET0
            classic name is REFERENCE-NO

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	disable DataTranslations

    Persistent Fields

        RunGroup
        SequenceNumber         is a SeqNbr
            classic name is SEQ-NBR
        PurchaseOrderReceipt
			delete ignored          
            classic name is REC-NUMBER
        PurchaseOrder
			delete ignored          
        InterfacedPurchaseOrder is like PurchaseOrderImport
        PORelease
        POCode
        Location               is an InventoryLocation
        OperatorID             is an Operator 
        	holds pii
            classic name is OPER-ID
        Buyer
            classic name is BUYER-CODE
        BillOfLading
            classic name is BOL-NBR
        ReceivedDate           is TimeStamp
            classic name is REC-DATE
        MerchandiseCarrier     is like Vendor
            classic name is CARRIER
        Vendor
        InterfacedVendor is a OldVendor
        PurchaseFromLocation
            classic name is PURCH-FR-LOC
        ReceiptComments            is an ErsComments
        Status                 is Numeric size 1
            States
                Unreleased value is 0
                Released   value is 1
        ReceiptImportLoggedFl  is AlphaUpper size 1
            classic name is LOGGED-FL
            States
                Yes value is "Y"
                No  value is "N"
        ErsComments
        ERSStatus
        Invoice
        InvoiceReference       is a PayablesReferenceType	
            classic name is INVC-REF-TYPE












        PrintCode
		MatchReferenceNumber
        Currency
            classic name is CURRENCY-CODE
        ProcessLevel           is a PayablesProcessLevel
        TermCode			   is a TermsCode
            classic name is TERMS-CD
        Notes
        RecordInError is AlphaUpper size 1
			classic name is RECORD-ERROR
			States
		      	ErrorsExist value is "Y"
		      	Released    value is "N"
		     	Unreleased  value is blank
        CrossReferenceVendor
            classic name is XREF-VENDOR
        ShipToArriveDate       is Date
            classic name is SHIP-TO-ARRIVE
        RetailFlag             is Numeric size 1
        LocationGLN            is like GlobalLocationNumber
        BuyerGLN               is like GlobalLocationNumber
        VendorGLN              is like GlobalLocationNumber
        FreightTotal		   is an InternationalAmount
            classic name is FREIGHT-TOT
        InsuranceTotal		   is an InternationalAmount
            classic name is INSURANCE-TOT
        CustomsTotal		   is an InternationalAmount
            classic name is CSTMS-TOT
        OtherAddOnChargesTotal is an InternationalAmount
            classic name is OTHER-AOC-TOT
        GlobalDocumentType
            classic name is GLBL-DOC-TYPE
		ErrorMessage				  is Alpha 150
        ErrorLevel                       is Numeric 2
        	States
        		Header                   value is 1
        		Line                     value is 2
        		HeaderAOC                value is 3
        		LineAOC                  value is 4
        NewPurchaseOrderReceipt	is a PurchaseOrderReceipt
			delete ignored          
		InterfaceRun			is a PurchaseOrderReceiptInterfaceResult
			delete ignored
		SenderLogicalID				
		SenderComponentID			
		SenderCreationDateTime 		
		SenderBODID					
		SenderOriginalBOD			        
	
	Local Fields
    	InterfacedReceipt				is a PurchaseOrderReceipt view
		LocalInterfaceResult 			is a PurchaseOrderReceiptInterfaceResult

		LocalRunGroup					is like RunGroup
		LocalCompany					is like Company
				
		ErrorOccurred					is Boolean
		LocalErrorMessage				is Alpha 150
		LocalErrorRunGroup				is a RunGroup

		


		LocalVendor						is like Vendor
		LocalFinanceEnterpriseGroup 	is like FinanceEnterpriseGroup 
		LocalPurchaseOrderLine			is like PurchaseOrderLine
		LocalConfigurationParameter		is Alpha size up to 200
		
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforReceiveDelivery"

		ReceiptLinesWithDifferentLocationsMsg is a MessageField
			restricted 
			"CannotHaveMultipleLocationsOnReceiverLinesForPoolOptionCompanyVendorPurchaseOrderShipToLocation"

		ContextMessageText is a MessageField
			restricted
			"PurchaseOrderReceipt<PurchaseOrderReceipt>ForPurchaseOrder<PurchaseOrder>"
	
		Affiliate is a DerivedField
			type is Boolean
			return first Vendor.VendorCompanyDefault set.Affiliate 
		
		NoReceiptLinesMsg is a MessageField
			restricted
			"NoLinesExistForReceipt"

		UnreleasedReceiptMsg is a MessageField
			restricted
			"UnreleasedReceiptExistsForPurchaseOrder<PurchaseOrder>"

		UnreleasedReceiptLineMsg is a MessageField
			restricted
			"UnreleasedReceiptExistsForPO<PurchaseOrder>AndPOLine<LocalPurchaseOrderLine>"

		HasInvalidDetailRecordsMsg is a MessageField
			"LinesOrAddOnCostsExistForThisReceiptWithADifferentRunGroup"
			
		DateIsBeforeClosingControlDateMsg is a MessageField 
			"ReceivedDateIsBeforeClosingControlDate"
			
		DateIsAfterClosingControlDateMsg is a MessageField 
			"ReceivedDateIsAfterClosingControlDate"
			
		DateNotValidCompanySystemClosingControlDateMsg	is a DerivedField 
			type is MessageField
			restricted
			if (IsDateNotValidCompanySystemClosingControlBeginDate)
				return DateIsBeforeClosingControlDateMsg
			else
			if (IsDateNotValidCompanySystemClosingControlEndDate)
				return DateIsAfterClosingControlDateMsg
						
		DerivedVendor is a DerivedField
			type is like Vendor
			if  (Vendor not entered
			and  InterfacedVendor entered)
				if (InterfaceMappingVendorRel exists)
					return first InterfaceMappingVendorRel.Vendor
				else
				if  (VendorForLegacyVendorRel exists)
					return first VendorForLegacyVendorRel.Vendor 
			else
				return Vendor

		DerivedVendorPoolOption is a DerivedField
			type is like PoolOption
			LocalVendor = DerivedVendor
			if (!LocalVendorRel.PoolOption.UseCompany)
				return LocalVendorRel.PoolOption
			return MatchCompanyRel.PoolOption

		LineCountForAReceipt is a DerivedField
			type is Numeric 10 
			return instance count of LinesForCountRel 
		
		AOCCountForAReceipt is a DerivedField
			type is Numeric 10
			return instance count of AOCsForCountRel

#ifdef module integration	
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
	
		DerivedBODID is a DerivedField
			type is Alpha 100
			restricted 
			return	"infor-nid:" + DerivedTenantID +":"+ RunGroup + "PO Number" + PurchaseOrder +"?PurchaseOrderReceiptImport&verb=Process"
#endif					
		POReceiptImportAlertDescription	is a DerivedField
			type is Alpha 200
			restricted
			return "Unprocessed PurchaseOrderReceipt Detail RunGroup:" +" "+RunGroup +" "+",PO Number:" + " "+PurchaseOrder		
		
		POReceiptImportAlertXML	is a DerivedField
			type is XMLDocument
			restricted 
			POReceiptImportAlertXML = template.IONPurchaseOrderReceiptImport_PurchaseOrderReceiptImport_ST document for this instance	

		DerivedLastReceiptImportLineNumber is a DerivedField
			type is Numeric size 6
			restricted
			return (last PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber)				
	Conditions

		IsPartiallyImported
			when (NewPurchaseOrderReceipt entered)

		HasInterfacedReceiptWithLines
			when (NewPurchaseOrderReceipt entered
			and   NewPurchaseOrderReceipt.PurchaseOrderReceiptLinesRel exists)

		HasLinesWithInvalidRunGroup        	
			when (LinesWithInvalidRunGroupRel exists)			

		HasHeaderAddOnCostsWithInvalidRunGroup        	
			when (HeaderAddOnCostsWithInvalidRunGroupRel exists)			

		HasInvalidDetailRecords
			when (HasLinesWithInvalidRunGroup
			or    HasHeaderAddOnCostsWithInvalidRunGroup)
			
		IsDateNotValidCompanySystemClosingControlBeginDate 
			restricted
			when (CompanySystemClosingControlForPORel.ValidEntryDate.Begin > ReceivedDate)
			
		IsDateNotValidCompanySystemClosingControlEndDate 
			restricted
			when (CompanySystemClosingControlForPORel.ValidEntryDate.End   < ReceivedDate)
				
		IsDateNotWithinCompanySystemClosingControlDate 
			restricted
			when (CompanySystemClosingControlForPORel.Control
			and  (IsDateNotValidCompanySystemClosingControlBeginDate
			or    IsDateNotValidCompanySystemClosingControlEndDate))

		HasCatchWeightItem
			restricted
			when (CatchWeightLinesRel exists)

		RecordExists
			restricted
			when (PurchaseOrderReceiptImport exists)   	
    Relations

		CompanySystemClosingControlForPORel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses symbolic key
				related.Company							= Company
				related.GeneralLedgerSystemCode			= "PO"

		PayablesCompanyRel
            one-to-many relation to PayablesCompany
            Field Mapping uses symbolic key
				related.Company							= Company
			
		VendorForLegacyVendorRel
            one-to-many relation to Vendor
            Field Mapping uses ByLegacyVendor
				related.VendorGroup						= PayablesCompanyRel.VendorGroup
				related.LegacyVendor					= InterfacedVendor
			
		LinesForCountRel
			one-to-many relation to PurchaseOrderReceiptLineImport
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport

		ReceiptLinesWithDifferentLocationsRel
			one-to-many relation to PurchaseOrderReceiptLineImport
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport
			Instance Selection
				where (related.Location != Location)

		CatchWeightLinesRel
			one-to-many relation to PurchaseOrderReceiptLineImport
			Field Mapping uses ByRunGroup
				related.RunGroup                    = RunGroup
				related.Company						= Company
				related.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport
			Instance Selection
				where (related.IsCatchWeightItem)

		LocalVendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup					= PayablesCompanyRel.VendorGroup
				related.Vendor						= LocalVendor

		MatchCompanyRel
            one-to-one relation to MatchCompany
            Field Mapping uses symbolic key
				related.Company								= Company

		AOCsForCountRel
			one-to-many relation to PurchaseOrderReceiptLineAOCImport
			Field Mapping uses ByRunGroup
				related.RunGroup                    = LocalRunGroup
				related.Company						= Company
				related.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport

		InterfaceMappingVendorRel
            one-to-many relation to VendorMappingTableInterface
            Field Mapping uses symbolic key
				related.VendorGroup								= PayablesCompanyRel.VendorGroup
			Instance Selection
				where (related.VendorMappingTableInterface.OldVendor	= InterfacedVendor)
			
		InterfacedPurchaseOrderRel 
			one-to-many relation to InterfacedPurchaseOrder
            Field Mapping uses ByImport
                related.Company                    	= Company
                related.PurchaseOrderImport 		= InterfacedPurchaseOrder
                related.POCode						= POCode
			
        PurchaseOrderReceiptLineImportRel
            one-to-many relation to PurchaseOrderReceiptLineImport
            Field Mapping uses ByRunGroup
            	related.RunGroup					= RunGroup
                related.Company                     = Company
                related.PurchaseOrderReceiptImport  = PurchaseOrderReceiptImport

        PurchaseOrderReceiptLineAOCImportRel
            one-to-many relation to PurchaseOrderReceiptLineAOCImport
            Field Mapping uses ByRunGroup
            	related.RunGroup					= RunGroup
                related.Company                     = Company
                related.PurchaseOrderReceiptImport  = PurchaseOrderReceiptImport

        LinesWithInvalidRunGroupRel
            one-to-many relation to PurchaseOrderReceiptLineImport
            Field Mapping uses symbolic key
                related.Company                     = Company
                related.PurchaseOrderReceiptImport  = PurchaseOrderReceiptImport
			Instance Selection
            	where (related.RunGroup					!= RunGroup)

        HeaderAddOnCostsRel
            one-to-many relation to PurchaseOrderReceiptLineAOCImport
            Field Mapping uses ByRunGroup
            	related.RunGroup					= RunGroup
                related.Company                    = Company
                related.PurchaseOrderReceiptImport = PurchaseOrderReceiptImport
            Instance Selection
            	where (related.PurchaseOrderReceiptLineAOCImport.LineNumber not entered
            	and    related.PurchaseOrderReceiptLineAOCImport.SeqNbr not entered)

        HeaderAddOnCostsWithInvalidRunGroupRel
            one-to-many relation to PurchaseOrderReceiptLineAOCImport
            Field Mapping uses symbolic key
                related.Company                     = Company
                related.PurchaseOrderReceiptImport  = PurchaseOrderReceiptImport
            Instance Selection
            	where (related.PurchaseOrderReceiptLineAOCImport.LineNumber not entered
            	and    related.PurchaseOrderReceiptLineAOCImport.SeqNbr not entered
            	and    related.RunGroup					!= RunGroup)

		LocalInterfaceResultRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= LocalFinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult		= LocalInterfaceResult

        LocalReceiptsInRunGroupRel
            one-to-many relation to PurchaseOrderReceiptImport
            Field Mapping uses ByRunGroup
				related.RunGroup						= LocalRunGroup

        LocalReceiptsInRunGroupCompanyRel
            one-to-many relation to PurchaseOrderReceiptImport
            Field Mapping uses ByRunGroup
				related.RunGroup						= LocalRunGroup
				related.Company							= LocalCompany

		UnreleasedReceiptRel
			one-to-many relation to PurchaseOrderReceipt
			Field Mapping uses Set3
				related.Company							= PurchasingCompanyRel.Company
				related.PurchaseOrder					= PurchaseOrder
			Instance Selection
				where (related.Status.Unreleased
				or	   related.Status.CreateInProgress) 

		PurchasingCompanyRel
			one-to-one relation to PurchasingCompany
			Field Mapping uses symbolic key
				related.Company							= Company

       	VendorLocationRel			
       		one-to-one relation to VendorLocation
			Field Mapping uses symbolic key
				related.VendorGroup				= Vendor.VendorGroup
				related.Vendor					= DerivedVendor
				related.VendorLocation			= PurchaseFromLocation
#ifdef module integration		
		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 2
            	related.FSMBODConfiguration.Noun 		= "PulseAlert"
            	related.FSMBODConfiguration.Direction 	= 1
        
        FSMBODConfigurationDetailRel
        	one-to-many relation to FSMBODConfigurationDetail
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 			= 2	
            	related.FSMBODConfiguration.Noun 			= "PulseAlert"
            	related.FSMBODConfiguration.Direction 		= 1
            Instance Selection
				where (related.Alert						= "FSM_ION_PurchaseOrderReceiptImportAlert"
				and	   related.Enable)
#endif
    Sets

        Set2
            indexed
            Sort Order
                RunGroup
                RecordInError
                Company
                Vendor
                PurchaseOrderReceiptImport
                SequenceNumber

        Set1
            not indexed
            Sort Order
                Company
                PurchaseOrderReceiptImport
                SequenceNumber

        ByRunGroup
            Sort Order
				RunGroup
                Company
                PurchaseOrderReceiptImport

        ByInterfaceRun
            Sort Order
            	InterfaceRun
				RunGroup
                Company
                PurchaseOrderReceiptImport
                
        ByPurchaseOrder
        	Sort Order
        		Company
        		PurchaseOrder
        		PurchaseOrderReceiptImport

	Field Rules
		PurchaseOrderReceiptImport
			required
				"ReferenceNumberIsRequired"

		PurchaseOrder
			if  (InterfacedPurchaseOrder not entered)
				required
					"PurchaseOrderOrInterfacedPurchaseOrderRequired"

			if (PurchaseOrder changed)
				if (NewPurchaseOrderReceipt entered)
					constraint (NewPurchaseOrderReceipt.first PurchaseOrderReceiptLinesRel not exists)
						"CannotChangePurchaseOrder,LinesWereCreatedForTheReceipt"
 
					confirmation required
						"ChangingThe_Purchase_OrderWillDeleteTheCreatedReceiptAndResetTheError.Continue?"

					invoke ResetErrorMessage

					if (NewPurchaseOrderReceipt.Status.CreateInProgress)
						invoke TransitionToUnreleased CreateInProgress NewPurchaseOrderReceipt
					invoke Delete Unreleased NewPurchaseOrderReceipt
					initialize NewPurchaseOrderReceipt
				else
					if (ErrorMessage entered)
						confirmation required
							"ChangingThe_Purchase_OrderWillResetTheError.Continue?"

						invoke ResetErrorMessage

				for each PurchaseOrderReceiptLineImportRel
					invoke FastUpdate each
						invoked.PurchaseOrder = PurchaseOrder
				for each PurchaseOrderReceiptLineAOCImportRel
					if (each.PurchaseOrder entered)
						invoke FastUpdate each
							invoked.PurchaseOrder = PurchaseOrder


		InterfacedPurchaseOrder
			if  (PurchaseOrder not entered)
				required
					"PurchaseOrderOrInterfacedPurchaseOrderRequired"
				
		RunGroup
			required
			
		OperatorID
			default to actor

	Rule Blocks

	Actions
		Create is a Create Action
			Action Rules

		CreateNoRules is a Create Action
			restricted
			bypass field rules

		Update is an Update Action
			Action Rules
				if (RunGroup changed)
					invoke ChangeRunGroup
						invoked.NewRunGroup = RunGroup

		ChangeRunGroup is an Instance Action
			restricted
			Parameters
				NewRunGroup is a RunGroup
			Action Rules

				for each PurchaseOrderReceiptLineAOCImport set
					if  (each.Company                    	= Company
    		        and  each.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport)
				        invoke ChangeRunGroup each
							invoked.NewRunGroup			= NewRunGroup
        				
				for each PurchaseOrderReceiptLineImport set
					if  (each.Company                    	= Company
    		        and  each.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport)
				        invoke ChangeRunGroup each
							invoked.NewRunGroup			= NewRunGroup

				for each POReceiptInventoryDetailImport set
					if  (each.Company                    	= Company
    		        and  each.PurchaseOrderReceiptImport 	= PurchaseOrderReceiptImport)
				        invoke ChangeRunGroup each
							invoked.NewRunGroup			= NewRunGroup

				RunGroup								= NewRunGroup

		FastUpdate is an Update Action
			restricted
			bypass field rules

		RemoveInvalidLinesAndAddOnCosts is an Instance Action
			valid when (HasInvalidDetailRecords)
			Action Rules
				invoke Delete LinesWithInvalidRunGroupRel
				invoke Delete HeaderAddOnCostsWithInvalidRunGroupRel
				invoke RemoveInvalidAddOnCosts PurchaseOrderReceiptLineImportRel
							
		Delete is a Delete Action
			Entrance Rules
				if  (NewPurchaseOrderReceipt entered)
					confirmation required
						"ReceiptIsPartiallyInterfacedAndThoseRecordsWillAlsoBeDeleted"
						
					if (NewPurchaseOrderReceipt.Status.CreateInProgress)
						invoke CreateInProgress.TransitionToUnreleased NewPurchaseOrderReceipt
						
					invoke Unreleased.Delete NewPurchaseOrderReceipt

				invoke RemoveInvalidLinesAndAddOnCosts
				invoke Delete HeaderAddOnCostsRel
				invoke Delete PurchaseOrderReceiptLineImportRel


		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		DeleteAllTransactionsForRunGroup is a Set Action
			confirmation required
		
			Parameters
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup 
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup				  is AlphaUpper 30
					default label is "RunGroup"
			
			Parameter Rules 
				PrmFinanceEnterpriseGroup 
					default to actor.context.FinanceEnterpriseGroup
					
			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
				and    NewPurchaseOrderReceipt not entered)
				
			Action Rules
			
				Instance Rules
					invoke FastDelete HeaderAddOnCostsRel
					invoke FastDelete PurchaseOrderReceiptLineImportRel
				    invoke FastDelete

 		ResetErrorMessage is an Instance Action
			Action Rules
	 			initialize ErrorMessage
	 			RecordInError = blank

 		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmInterfaceResult		is like PurchaseOrderReceiptInterfaceResult
 				PrmErrorLevel           is Numeric 2
			Action Rules


				
				RecordInError					= "Y"
				ErrorMessage					= PrmErrorMessage
				ErrorLevel                      = PrmErrorLevel
				
				LocalInterfaceResult = PrmInterfaceResult
				
				invoke Update LocalInterfaceResultRel
					invoked.Status = 2
				

				if  (LocalInterfaceResultRel.MoveErrorsToNewRunGroup)
				
					if  (LocalInterfaceResultRel.ErrorRunGroup not entered)
						LocalErrorRunGroup					= LocalInterfaceResultRel.ErrorRunGroupPrefix + "ERRORS_" + RunGroup

						invoke Update LocalInterfaceResultRel
							invoked.ErrorRunGroup			= LocalErrorRunGroup
					else
						LocalErrorRunGroup					= LocalInterfaceResultRel.ErrorRunGroup

					invoke ChangeRunGroup
						invoked.NewRunGroup					= LocalErrorRunGroup
					

		InterfaceReceipts is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup   is an FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PurchasingCompany
				ReleaseReceipts				is Boolean
				ReleaseOption
				AllowDropshipReceivers		is Boolean
				AllowMatchItemOrVendorItem  is Boolean
					default label is "AllowMatchOnEitherItemOrVendorItem"


				IncludeRecordsInError		is Boolean
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"
				ReleaseReceipts
					initial value is true
					
			Local Fields
				LocalInstanceCount				is Numeric 10
				LocalSetInterfaceResult			is a PurchaseOrderReceiptInterfaceResult view 	
				LocalLineCount                is Numeric 12
				LocalCount                    is Numeric 12   
				LocalAOCCount                 is Numeric 12
								
			Instance Selection			
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = PrmFinanceEnterpriseGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany)
				and    (IncludeRecordsInError
				or      !RecordInError.ErrorsExist))

			Sort Order
				RunGroup
				Company
                PurchaseOrderReceiptImport

			Action Rules

				Empty Set Rules
					invoke Create PurchaseOrderReceiptInterfaceResult
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.RunTime						= current timestamp
						invoked.RunGroup					= PrmRunGroup
						invoked.RunType						= 1
						invoked.Company						= PrmCompany
						invoked.IncludeRecordsInError		= IncludeRecordsInError
						invoked.ReleaseReceipts				= ReleaseReceipts
						invoked.ReleaseOption				= ReleaseOption
				        invoked.AllowDropshipReceivers		= AllowDropshipReceivers
				        invoked.AllowMatchItemOrVendorItem  = AllowMatchItemOrVendorItem
						invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
						invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix
						invoked.Status						= 1 
				
				RunGroup Set Rules
					Entrance Rules
						ErrorOccurred							= false
						
						invoke Create PurchaseOrderReceiptInterfaceResult
							assign result to LocalSetInterfaceResult
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.RunType						= 1
							invoked.Company						= PrmCompany
							invoked.IncludeRecordsInError		= IncludeRecordsInError
							invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.ReleaseOption				= ReleaseOption
					        invoked.AllowDropshipReceivers		= AllowDropshipReceivers
					        invoked.AllowMatchItemOrVendorItem  = AllowMatchItemOrVendorItem
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix

					Exit Rules
						invoke Update LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
							invoked.RecordsProcessed							= LocalInstanceCount
							invoked.InterfaceCounts.PassedHeaderCount          	= LocalCount         
							invoked.InterfaceCounts.FailedHeaderCount           = (LocalInstanceCount - LocalCount)
							invoked.InterfaceCounts.DetailCount          		= LocalLineCount
							invoked.InterfaceCounts.AddOnChargeCount     		= LocalAOCCount

						invoke ReceiptLineLoad PurchaseOrderReceiptLineImport
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmRunGroup					= PrmRunGroup
							invoked.PrmCompany					= PrmCompany
							invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.ReleaseOption				= ReleaseOption
							invoked.AllowDropshipReceivers		= AllowDropshipReceivers
					        invoked.AllowMatchItemOrVendorItem  = AllowMatchItemOrVendorItem
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.PrmErrorRunGroupPrefix		= PrmErrorRunGroupPrefix
							invoked.PrmInterfaceRun				= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
					
						invoke AcknowledgeForFailureRunGroupBOD
							invoked.PrmRunGroup	= RunGroup	
					
				Instance Rules
					if   (IncludeRecordsInError
					or    !RecordInError.ErrorsExist)
						
						LocalFinanceEnterpriseGroup						= PrmFinanceEnterpriseGroup
						LocalRunGroup                                   = PrmRunGroup
						RecordInError = RecordInError.Released
						Status = Status.Released
									
						initialize ErrorMessage
						InterfaceRun									= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
						RecordInError									= blank
						ErrorOccurred									= false
						LocalInterfaceResult 							= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
						LocalCount	    				    			+= 1	
						LocalLineCount	                				+= LineCountForAReceipt 
						LocalAOCCount   	              				+= AOCCountForAReceipt

						initialize ErrorOccurred

						if (NewPurchaseOrderReceipt entered)
							if (NewPurchaseOrderReceipt.Status.Unreleased)
								invoke Unreleased.FastUpdate NewPurchaseOrderReceipt
									invoked.OriginatingInterfaceRun			= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
							else
							if (NewPurchaseOrderReceipt.Status.CreateInProgress)
								invoke CreateInProgress.FastUpdate NewPurchaseOrderReceipt
									invoked.OriginatingInterfaceRun			= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
						else
						if (UnreleasedReceiptRel exists
						and not PurchasingCompanyRel.AllowMultipleUnreleasedReceipts)
							ErrorOccurred								= true
							LocalErrorMessage							= UnreleasedReceiptMsg
						else
						if  (!PurchaseOrderReceiptLineImportRel exists)
							ErrorOccurred								= true
							LocalErrorMessage							= NoReceiptLinesMsg
						else 
						if (PurchaseOrderReceiptLineImportRel.HasUnreleasedReceiptLine)
							LocalPurchaseOrderLine						= PurchaseOrderReceiptLineImportRel.PurchaseOrderReceiptLineImport.LineNumber
							ErrorOccurred								= true
							LocalErrorMessage							= UnreleasedReceiptLineMsg
						else
						if (ReceiptLinesWithDifferentLocationsRel exists
						and DerivedVendorPoolOption = 3)
							ErrorOccurred								= true
							LocalErrorMessage							= ReceiptLinesWithDifferentLocationsMsg
						else
						if  (HasInvalidDetailRecords)
							ErrorOccurred								= true
							LocalErrorMessage							= HasInvalidDetailRecordsMsg
						else
							for each PurchaseOrderReceiptLineImportRel
								if (each.HasLineAddOnCostsWithInvalidRunGroup)
									ErrorOccurred						= true
									LocalErrorMessage					= HasInvalidDetailRecordsMsg

						if (!ErrorOccurred																						
						and NewPurchaseOrderReceipt not entered)
							invoke Unreleased.CreateFromInterface PurchaseOrderReceipt
								assign result to InterfacedReceipt
								resume on error
									ErrorOccurred		= true
									LocalErrorMessage	= error message

								fill in fields from this instance
									except invoked.PurchaseOrderReceipt
									except invoked.Status
									
								invoked.Company								= Company
								invoked.ReferenceNumber						= PurchaseOrderReceiptImport
								invoked.Vendor								= DerivedVendor

								if (InterfacedPurchaseOrder entered
								and PurchaseOrder not entered)
									invoked.PurchaseOrder					= first InterfacedPurchaseOrderRel.PurchaseOrder
								else
									invoked.PurchaseOrder					= PurchaseOrder

								invoked.TransientInterfaceStatus			= Status
								invoked.InterfaceInProcess					= true
								invoked.OriginatingInterfaceRun				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
								if  (AllowDropshipReceivers)
									invoked.BypassDropshipEdit				= true
								
							if (!ErrorOccurred)
								NewPurchaseOrderReceipt						= InterfacedReceipt.PurchaseOrderReceipt

						if (ErrorOccurred)


							invoke SetError
								invoked.PrmErrorMessage					= LocalErrorMessage
								invoked.PrmInterfaceResult				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
								invoked.PrmErrorLevel   				= 1
							LocalCount              -= 1 
							
						LocalInstanceCount			+= 1
						
						if (!RecordInError.ErrorsExist
						and !PurchaseOrderReceiptLineImportRel exists
						and !PurchaseOrderReceiptLineAOCImportRel exists)
							if (AllowDropshipReceivers
							and NewPurchaseOrderReceipt.PurchaseOrder.IsDropship)
								invoke CreateInProgress.TransitionToDropship NewPurchaseOrderReceipt
							else
								if (NewPurchaseOrderReceipt.Status.CreateInProgress)
									invoke CreateInProgress.TransitionToUnreleased NewPurchaseOrderReceipt
							
								invoke Unreleased.FastUpdate NewPurchaseOrderReceipt
									invoked.InterfaceInProcess				= false
								if (ReleaseReceipts)
									if  (ReleaseOption.ReleaseOnly)
										invoke Unreleased.Release NewPurchaseOrderReceipt
											resume on error
									else
									if (ReleaseOption.ReleaseAndDeliver)
										invoke Unreleased.ReleaseAndPrintLPL NewPurchaseOrderReceipt
											resume on error
									else
									if (ReleaseOption.ReleaseAndDeliverNoBackorders)
										invoke Unreleased.ReleaseAndPrintNoBackordersLPL NewPurchaseOrderReceipt
											resume on error
							invoke FastDelete

#ifdef module integration
		PurchaseOrderReceiptImportAlert is an Instance Action
			restricted
			Parameters
			Action Rules	
				invoke TriggerPulseAlert FSMBODConfigurationRel
					invoked.PrmActorGroup 	= "PORECEIPTFAILUREGROUP" 
					invoked.PrmMainXML 		= POReceiptImportAlertXML	
					invoked.PrmDescription	= POReceiptImportAlertDescription
					invoked.PrmBODID		= DerivedBODID
#endif						
		AcknowledgeForFailureRunGroupBOD is a Set Action
			restricted
			Parameters
				PrmRunGroup				  is AlphaUpper 30
					default label is "RunGroup"
			Instance Selection
				where (RunGroup	= PrmRunGroup
				and	   SenderLogicalID entered 
				and    ErrorMessage entered)
			Action Rules
				Instance Rules
					if (Company.FinanceEnterpriseGroup.BODTrigger)
						if(SenderLogicalID entered or SenderComponentID entered or SenderBODID entered)
	                		trigger "PurchaseOrderReceiptImportService" PA service 
	                   	 		resume on error
	                      	 	title is "EG:<Company.FinanceEnterpriseGroup>CO:<Company>PO:<PurchaseOrder>"
	                        	Criteria
	                        		Company.FinanceEnterpriseGroup      
	                            	Company      
	                        	Variables
	                        		SenderLogicalID		
									SenderComponentID	
									SenderCreationDateTime 	
									SenderBODID	
									SenderOriginalBOD		
									Company 
									RunGroup	
									PurchaseOrder
									PurchaseOrderReceipt
	                            	Company.FinanceEnterpriseGroup
	                            		variable name is FinanceEnterpriseGroup
#ifdef module integration
	       					if (FSMBODConfigurationRel.Enable)
          						if(FSMBODConfigurationDetailRel.Enable)
			   						invoke PurchaseOrderReceiptImportAlert
#endif
