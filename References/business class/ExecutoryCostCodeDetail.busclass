ExecutoryCostCodeDetail is a BusinessClass
    owned by lm
    prefix is EXC                                        
    classic name is LMEXECCODE
    default label is "NonLeaseComponent" 

    Ontology
        symbolic key is ExecutoryCostCodeDetail
            classic set name is EXCSET1
            classic name is EXEC-CODE                    

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Taxable				is Boolean															
        TaxCode																
        TaxUsageCode														
            classic name is TAX-USAGE-CD
        DistributionAccount is a FinanceCodeBlock
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for DistributionAccount.Project is ACTIVITY

	Local Fields
		CalculateTax												
		LocalCalculateTaxExecuted			is Alpha 1				

	Relations
		AnotherExecutoryCostCodeDetailsRel
			one-to-many relation to ExecutoryCostCodeDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 							= FinanceEnterpriseGroup
				related.ExecutoryCostCode    							= ExecutoryCostCode
				related.Company											= Company
			Instance Selection
				where (related.ExecutoryCostCodeDetail  				!= ExecutoryCostCodeDetail
				and    related.DistributionAccount						=  DistributionAccount)

    Field Rules

        Taxable
        	default to "N"             												
          
        TaxCode
        	if (Taxable)
        		required
        			"RecordIsTaxable;TaxCodeIsRequired"
        		
        	if (Taxable not entered)											
				cannot be entered
					"CannotEnterTaxCode,LineIsNotTaxable"                        

			initialize CalculateTax
			CalculateTax.TaxEntity			= ExecutoryCostCodeDetail.Company.AccountingEntity
			CalculateTax.TaxCode			= TaxCode
			CalculateTax.Function			= "I"
			CalculateTax.System				= "LM"
			LocalCalculateTaxExecuted       = CalculateTax.ExecuteCalculateTax
			constraint (CalculateTax.OutputErrorNumber not entered)
				"<CalculateTax.OutputErrorMessage>"								
					
		TaxUsageCode
	        if (Taxable not entered)		
				cannot be entered
					"CannotEnterTaxUsageCode,LineIsNotTaxable"					
 
 		DistributionAccount
			required
			
			constraint (AnotherExecutoryCostCodeDetailsRel not exists)
				"DistributionAccountAlreadyAssociatedWithLeaseAllocationCodeDetail"
          
  	Actions
  	
        Create is a Create Action

        Update is an Update Action
	
        Delete is a Delete Action

