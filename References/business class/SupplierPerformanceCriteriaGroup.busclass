SupplierPerformanceCriteriaGroup is a BusinessClass
    owned by procurement
    prefix is SPCGP
    sql name is SPPFCRGP

    Ontology
    	symbolic key is SupplierPerformanceCriteriaGroup
    	
	Persistent Fields
		Category					 	is a Description
		CategoryWeighting				is a Percent

	Transient Fields

	Derived Fields
		NumberOfPerformanceCriteriaResponses is a ComputeField
			type is Numeric 6
			restricted
			(instance count of CriteriaResponsesExcludingNotApplicable)

		TotalNumberOfPerformanceCriteriaResponses is a ComputeField
			type is Numeric 6
			restricted
			(instance count of SupplierPerformanceCriteriaRel)

		NumberOfPerformanceCriteriaWithScores is a ComputeField
			type is Numeric 6
			restricted
			(instance count of CriteriaResponsesWithScores)

		CategoryWeightingConv is a ConditionalField
			type is Percent size 6.3
			restricted
			if (CategoryWeighting entered
			and TotalCategoryWeightingApplicable < 1
			and NumberOfPerformanceCriteriaResponses > 0)
				CategoryWeighting + ((1 - TotalCategoryWeightingApplicable)/SupplierPerformanceEvaluation.NumberOfPerformanceCategories)
			else 
			if (CategoryWeighting entered
			and NumberOfPerformanceCriteriaResponses > 0)
				CategoryWeighting				
			else
			if (NumberOfPerformanceCriteriaResponses > 0)
				(1 / SupplierPerformanceEvaluation.NumberOfPerformanceCategories)
			else 
				0
					
		TotalCategoryWeightingApplicable is a DerivedField 
			type is like InternationalAmount 
			restricted 
			return (sum SupplierPerformanceEvaluation.SupplierPerformanceCriteriaGroupApplicableRel.PerformanceCriteriaGroup.CategoryWeighting)
					
		TotalCategoryScore is a DerivedField
			type is like InternationalAmount
			restricted
			return (sum CriteriaResponsesExcludingNotApplicable.PerformanceRatingScore)

		AverageCategoryScore is a DerivedField
			type is like InternationalAmount
			return (TotalCategoryScore / NumberOfPerformanceCriteriaResponses)

		WeightedCategoryScore is a DerivedField
			type is like InternationalAmount
			restricted
			return (AverageCategoryScore * CategoryWeightingConv)

	Sets
 	
 	Conditions
		HasMissingResponses
			restricted
			when (NumberOfPerformanceCriteriaWithScores < TotalNumberOfPerformanceCriteriaResponses)

 	Field Rules
 		Category
 			required

	Relations
		SupplierPerformanceCriteriaRel is a SupplierPerformanceCriteria set

		CriteriaResponsesExcludingNotApplicable is a SupplierPerformanceCriteria set
			Instance Selection
				where (related.PerformanceRatingScore > 0
				and	   related.PerformanceRatingScore < 6)

		CriteriaResponsesWithScores is a SupplierPerformanceCriteria set
			Instance Selection
				where (related.HasResponse)

		SupplierPerformanceCriteriaResponseRel is a SupplierPerformanceCriteriaResponse set

	Actions
		Create is a Create Action
			Exit Rules
				for each PerformanceCriteriaGroup.ActivePerformanceCriteriaRel
					invoke Create SupplierPerformanceCriteria
						invoked.SupplierGroup 						= this instance.SupplierGroup
						invoked.Supplier 							= this instance.Supplier
						invoked.PerformanceEvaluationTemplate		= this instance.PerformanceEvaluationTemplate
						invoked.SupplierPerformanceEvaluation 		= this instance.SupplierPerformanceEvaluation
						invoked.PerformanceCriteriaGroup 			= this instance.PerformanceCriteriaGroup
						invoked.SupplierPerformanceCriteriaGroup 	= SupplierPerformanceCriteriaGroup
						invoked.PerformanceCriteria					= each.PerformanceCriteria
						invoked.Criteria							= each.Criteria
						invoked.Description							= each.Description
						invoked.Contract							= Contract

		Update is an Action
			valid when (!SupplierPerformanceEvaluation.IsEvaluationComplete)

		Delete is a Delete Action
