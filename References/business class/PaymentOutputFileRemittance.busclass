PaymentOutputFileRemittance is a BusinessClass
    owned by cb
    prefix is POFR

    Ontology
        symbolic key is PaymentOutputFileRemittance

    Patterns
        implements StaticJava
        disable AuditIndex
		implements InlineUserFields
			size is 1000

    Persistent Fields
		BankClientIdentifier				is AlphaUpper 50
		CashLedgerTransactionIdentifier
        TransactionNumber
		PaidVendor                          is like Vendor
        InvoiceVendor                       is like Vendor
        InvoiceVendorName                   is a VendorName 
        	holds pii
		Invoice								is an InvoiceNum
		InvoiceSuffix						is a Suffix
		InvoiceDate							is Date
		PayablesCompany						is like PayablesCompany
		PayablesCompanyName					is a Name	 
			holds pii
		NetPaymentAmount					is an InternationalAmount
		PaymentAmount						is an InternationalAmount
		DiscountAmount						is an InternationalAmount
		InvoiceCurrency	                    is a Currency 
		PurchaseOrder						is like PurchaseOrder
		VoucherNumber                       is like VoucherNumber
		ProcessLevel	                    is like PayablesProcessLevel
		ReasonCode							is like ProcurementReasonCode
		ReasonCodeDescription				is a Description
		InvoiceDescription                  is a Description
        InvoiceComment                      is a CommentText
		TransactionData1					is a BankInstruct1	
		TransactionData2					is a BankInstruct2	
		TransactionData3					is a BankInstruct3	
		TransactionData4					is a BankInstruct4	
		InvoiceVendorEmailAddress			is an EmailAddressMulti 
			holds pii
		AttachedWithCheck					is Boolean
		VATSplitAmount						is an InternationalAmount		
		PayablesInvoice						is like PayablesInvoice			

	Transient Fields
				
	Rule Blocks

    Derived Fields
		UnformattedComment is a NativeField
			type is Text
			restricted
			
        DoubleQuote        is a StringField
            type is Alpha 2048
            restricted
            "\""

		DerivedDelimiter is a DerivedField
			type is Alpha 1
			if (PaymentOutputFileHeader.PaymentOutputMapping entered)
				return PaymentOutputFileHeader.PaymentOutputMapping.Delimiter

		DerivedLikeDelimiter is a DerivedField
			type is Alpha 3
			restricted
			if (PaymentOutputFileHeader.PaymentOutputMapping entered)
				return "*" + DerivedDelimiter + "*"

        VoucherNumberDisplay		is a DerivedField
        	type is Alpha 15
        	default label is "VoucherNumber"
        	if (VoucherNumber.Prefix entered)
        		return VoucherNumber
        	else
        		return VoucherNumber.Sequence

		UnsignedNetPaymentAmount	is a DerivedField		
			type is like InternationalAmount
			if (NetPaymentAmount < 0)
				return (NetPaymentAmount * -1)
			return NetPaymentAmount

		UnsignedPaymentAmount	is a DerivedField			
			type is like InternationalAmount
			if (PaymentAmount < 0)
				return (PaymentAmount * -1)
			return PaymentAmount

		UnsignedDiscountAmount	is a DerivedField			
			type is like InternationalAmount
			if (DiscountAmount < 0)
				return (DiscountAmount * -1)
			return DiscountAmount

		UnsignedVATSplitAmount	is a DerivedField			
			type is like InternationalAmount
			if (VATSplitAmount < 0)
				return (VATSplitAmount * -1)
			return VATSplitAmount

		CreditMemoIndicator		is a DerivedField			
			type is Numeric 1
			if (PaymentAmount < 0)
				return 1

		DerivedRemoveAfter is a StringField								
			type is Alpha up to 30
            restricted
			"\Q"
			"using"
			"\E"
			".*$"
		
		DerivedRemoveString is a StringField							
			type is Alpha up to 30
            restricted
			"\Q"
			"using"
			"\E"

		DerivedCsvHeader is a DerivedField
            type is Text
            restricted
            if (PaymentOutputFileHeader.PaymentOutputMapping entered)
            	return CustomCsvHeader
            else
            	return CsvHeader
            	
		CustomCsvHeader is a DerivedField
            type is Text
            restricted
            if (PaymentOutputFileHeader.PaymentOutputMapping entered)
	        	initialize LocalCustomCsvHeader
	        	LastPaymentOutputMappingRemittance = PaymentOutputFileHeader.PaymentOutputMapping.last PaymentOutputMappingRemittanceRel.PaymentOutputMappingRemittance
	        	for each PaymentOutputFileHeader.PaymentOutputMapping.PaymentOutputMappingRemittanceRel
					if (each.MappingType.Field)
						if (each.OverrideFieldName entered)
               				LocalFieldName = each.OverrideFieldName
          				else
							LocalFieldName = each.Field
							LocalFieldName -= DerivedRemoveAfter					
							LocalFieldName -= DerivedRemoveString					
					else
					if (each.MappingType.Literal)
						LocalFieldName = each.LiteralName
					else
					if (each.MappingType.Blank)
						LocalFieldName = each.BlankName

					if (PaymentOutputFileHeader.PaymentOutputMapping.RemoveQuotes)
						if (LocalFieldName like DerivedLikeDelimiter)
							LocalContainsDelimiterValue = true
						if (LocalContainsDelimiterValue)
							if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
								LocalCustomCsvHeader += DoubleQuote + LocalFieldName + DoubleQuote
							else
								LocalCustomCsvHeader += DoubleQuote + LocalFieldName + DoubleQuote + DerivedDelimiter 	
						else
						if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
							LocalCustomCsvHeader += LocalFieldName
						else
							LocalCustomCsvHeader += LocalFieldName + DerivedDelimiter
					else	
						if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
							LocalCustomCsvHeader += DoubleQuote + LocalFieldName + DoubleQuote
						else
							LocalCustomCsvHeader += DoubleQuote + LocalFieldName + DoubleQuote + DerivedDelimiter
					initialize LocalContainsDelimiterValue

				return LocalCustomCsvHeader
				
        CsvHeader is a StringField 
            type is Text
            restricted
			DoubleQuote
			BankClientIdentifier label
			DoubleQuote
			","
			CashLedgerTransactionIdentifier label
			","
			DoubleQuote
			TransactionNumber label
			DoubleQuote
			","
			PaidVendor label
			","
			DoubleQuote
			Invoice label
			DoubleQuote
			","
			InvoiceSuffix label
			","
			InvoiceDate label
			","
			PayablesCompany label
			","
			PayablesCompanyName label
			","
			NetPaymentAmount label
			","
			PaymentAmount label
			","
			DiscountAmount label
			","
			DoubleQuote
			InvoiceCurrency label
			DoubleQuote
			","
			PurchaseOrder label
			","
			DoubleQuote
			VoucherNumber label
			DoubleQuote
			","
			DoubleQuote
			ProcessLevel label
			DoubleQuote
			","
			DoubleQuote
			ReasonCode label
			DoubleQuote
			","
			DoubleQuote
			ReasonCodeDescription label
			DoubleQuote
			","
			DoubleQuote
			InvoiceDescription label
			DoubleQuote
			","
			DoubleQuote
			"Invoice Comments" 
			DoubleQuote
			","
			DoubleQuote
			TransactionData1 label
			DoubleQuote
			","
			DoubleQuote
			TransactionData2 label
			DoubleQuote
			","
			DoubleQuote
			TransactionData3 label
			DoubleQuote
			","
			DoubleQuote
			TransactionData4 label
			DoubleQuote
			","
			DoubleQuote
			InvoiceVendor label
			DoubleQuote
			","
			DoubleQuote
			InvoiceVendorName label
			DoubleQuote
			","
			DoubleQuote
			InvoiceVendorEmailAddress label		
			DoubleQuote
			","
			DoubleQuote
			VATSplitAmount label		
			DoubleQuote

		CsvFieldNames is a StringField 
            type is Text
            restricted
			BankClientIdentifier full name
			","
			CashLedgerTransactionIdentifier full name
			","
			TransactionNumber full name
			","
			PaidVendor full name
			","
			Invoice full name
			","
			InvoiceSuffix full name
			","
			InvoiceDate full name
			","
			PayablesCompany full name
			","
			PayablesCompanyName full name
			","
			NetPaymentAmount full name
			","
			PaymentAmount full name
			","
			DiscountAmount full name
			","
			InvoiceCurrency full name
			","
			PurchaseOrder full name
			","
			VoucherNumberDisplay full name
			","
			ProcessLevel full name
			","
			ReasonCode full name
			","
			ReasonCodeDescription full name
			","
			InvoiceDescription full name
			","


			TransactionData1 full name
			","
			TransactionData2 full name
			","
			TransactionData3 full name
			","
			TransactionData4 full name
			","
			InvoiceVendor full name
			","
			InvoiceVendorName full name
			","
			InvoiceVendorEmailAddress full name
			","
			VATSplitAmount full name
			","
			UnsignedNetPaymentAmount full name
			","
			UnsignedPaymentAmount full name
			","
			UnsignedDiscountAmount full name
			","
			UnsignedVATSplitAmount full name
			","
			CreditMemoIndicator full name
			
		NumberOfDecimals					is a StringField
            type is Alpha 4
			"%."
			InvoiceCurrency.NumberOfDecimals
			"f"

		DerivedCsvOutput is a DerivedField
            type is Text
            restricted
            if (PaymentOutputFileHeader.PaymentOutputMapping entered)
            	return CustomCsvOutput
            else
            	return CsvOutput
            	
		CustomCsvOutput is a DerivedField
            type is Text
            holds pii
            restricted
            if (PaymentOutputFileHeader.PaymentOutputMapping entered)
	        	initialize LocalCustomCsvOutput
	        	LastPaymentOutputMappingRemittance = PaymentOutputFileHeader.PaymentOutputMapping.last PaymentOutputMappingRemittanceRel.PaymentOutputMappingRemittance
	        	for each PaymentOutputFileHeader.PaymentOutputMapping.PaymentOutputMappingRemittanceRel
					if (each.MappingType.Field)
						LocalOutputField = each.Field
						LocalFieldValue = LocalOutputField compute value
					else
					if (each.MappingType.Literal)
						LocalFieldValue = each.LiteralValue
					else
					if (each.MappingType.Blank)
						LocalFieldValue = ""
					
					if (PaymentOutputFileHeader.PaymentOutputMapping.RemoveQuotes)
						if (LocalFieldValue like DerivedLikeDelimiter)
							LocalContainsDelimiterValue = true
						if (LocalContainsDelimiterValue)
							if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
								LocalCustomCsvOutput += DoubleQuote + LocalFieldValue + DoubleQuote
							else
								LocalCustomCsvOutput += DoubleQuote + LocalFieldValue + DoubleQuote + DerivedDelimiter 	
						else
						if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
							LocalCustomCsvOutput += LocalFieldValue
						else
							LocalCustomCsvOutput += LocalFieldValue + DerivedDelimiter
					else	
						if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
							LocalCustomCsvOutput += DoubleQuote + LocalFieldValue + DoubleQuote
						else
							LocalCustomCsvOutput += DoubleQuote + LocalFieldValue + DoubleQuote + DerivedDelimiter
					initialize LocalContainsDelimiterValue

				return LocalCustomCsvOutput
		
        CsvOutput is a StringField 
            type is Text
            holds pii
            restricted
			DoubleQuote
			BankClientIdentifier
			DoubleQuote
			","
			CashLedgerTransactionIdentifier
			","
			DoubleQuote
			TransactionNumber
			DoubleQuote
			","
			PaidVendor
			","
			DoubleQuote
			Invoice
			DoubleQuote
			","
			InvoiceSuffix
			","
			InvoiceDate
			","
			PayablesCompany
			","
			DoubleQuote
			PayablesCompanyName
			DoubleQuote
			","
			NetPaymentAmount using "<NumberOfDecimals>"
			","
			PaymentAmount using "<NumberOfDecimals>"
			","
			DiscountAmount using "<NumberOfDecimals>"
			","
			DoubleQuote
			InvoiceCurrency
			DoubleQuote
			","
			PurchaseOrder
			","
			DoubleQuote
			VoucherNumberDisplay
			DoubleQuote
			","
			DoubleQuote
			ProcessLevel
			DoubleQuote
			","
			DoubleQuote
			ReasonCode
			DoubleQuote
			","
			DoubleQuote
			ReasonCodeDescription
			DoubleQuote
			","
			DoubleQuote
			InvoiceDescription
			DoubleQuote
			","
			DoubleQuote
			UnformattedComment 
			DoubleQuote
			","
			DoubleQuote
			TransactionData1
			DoubleQuote
			","
			DoubleQuote
			TransactionData2
			DoubleQuote
			","
			DoubleQuote
			TransactionData3
			DoubleQuote
			","
			DoubleQuote
			TransactionData4
			DoubleQuote
			","
			DoubleQuote
			InvoiceVendor
			DoubleQuote
			","
			DoubleQuote
			InvoiceVendorName
			DoubleQuote
			","
			DoubleQuote
			InvoiceVendorEmailAddress		
			DoubleQuote
			","
			DoubleQuote
			VATSplitAmount using "<NumberOfDecimals>"		
			DoubleQuote

		PipeOutput is a DerivedField					
            type is Text
            restricted
            if (PaymentOutputFileHeader.PaymentOutputMapping entered)
            	return CustomPipeOutput
            else
            	return StandardPipeOutput
            	
		CustomPipeOutput is a DerivedField				
            type is Text
            holds pii
            restricted
            if (PaymentOutputFileHeader.PaymentOutputMapping entered)
	        	initialize LocalCustomCsvOutput
	        	LastPaymentOutputMappingRemittance = PaymentOutputFileHeader.PaymentOutputMapping.last PaymentOutputMappingRemittanceRel.PaymentOutputMappingRemittance
	        	for each PaymentOutputFileHeader.PaymentOutputMapping.PaymentOutputMappingRemittanceRel
					if (each.MappingType.Field)
						LocalOutputField = each.Field
						LocalFieldValue = LocalOutputField compute value
					else
					if (each.MappingType.Literal)
						LocalFieldValue = each.LiteralValue
					else
					if (each.MappingType.Blank)
						LocalFieldValue = ""
					if (each.PaymentOutputMappingRemittance = LastPaymentOutputMappingRemittance)
						LocalCustomCsvOutput += LocalFieldValue
					else
						LocalCustomCsvOutput += LocalFieldValue + "|"
				return LocalCustomCsvOutput
		
        StandardPipeOutput        is a StringField		
            type is Text
            restricted
			BankClientIdentifier
			"|"
			CashLedgerTransactionIdentifier
			"|"
			TransactionNumber
			"|"
			PaidVendor
			"|"
			Invoice
			"|"
			InvoiceSuffix
			"|"
			InvoiceDate
			"|"
			PayablesCompany
			"|"
			PayablesCompanyName
			"|"
			NetPaymentAmount using "<NumberOfDecimals>"
			"|"
			PaymentAmount using "<NumberOfDecimals>"
			"|"
			DiscountAmount using "<NumberOfDecimals>"
			"|"
			InvoiceCurrency
			"|"
			PurchaseOrder
			"|"
			VoucherNumberDisplay
			"|"
			ProcessLevel
			"|"
			ReasonCode
			"|"
			ReasonCodeDescription
			"|"
			InvoiceDescription
			"|"
			UnformattedComment 
			"|"
			TransactionData1
			"|"
			TransactionData2
			"|"
			TransactionData3
			"|"
			TransactionData4
			"|"
			InvoiceVendor
			"|"
			InvoiceVendorName
			"|"
			InvoiceVendorEmailAddress		
			"|"
			VATSplitAmount using "<NumberOfDecimals>"					
			"|"
			UnsignedNetPaymentAmount using "<NumberOfDecimals>"			
			"|"
			UnsignedPaymentAmount using "<NumberOfDecimals>"			
			"|"
			UnsignedDiscountAmount using "<NumberOfDecimals>"			
			"|"
			UnsignedVATSplitAmount using "<NumberOfDecimals>"			
			"|"
			CreditMemoIndicator				

		DerivedCsvConsolidatedHeader is a DerivedField
            type is Text
            restricted
			if (PaymentOutputFileHeader.PaymentOutputMapping entered)
            	return CustomCsvConsolidatedHeader
            else
            	return CsvConsolidatedHeader
			
		CustomCsvConsolidatedHeader is a StringField
            type is Text
            restricted
			PaymentOutputFileDetail.CustomCsvHeader
			","
			CustomCsvHeader

		CsvConsolidatedHeader is a StringField
            type is Text
            restricted
			PaymentOutputFileDetail.CsvHeader
			","
			CsvHeader

		DerivedCsvConsolidatedOutput is a DerivedField
            type is Text
            restricted
			if (PaymentOutputFileHeader.PaymentOutputMapping entered)
            	return CustomCsvConsolidatedOutput
            else
            	return CsvConsolidatedOutput
            	
		CustomCsvConsolidatedOutput is a StringField
            type is Text
            restricted
			PaymentOutputFileDetail.CustomCsvOutput
			","
			CustomCsvOutput
			
        CsvConsolidatedOutput is a StringField
            type is Text
            restricted
			PaymentOutputFileDetail.CsvOutput
			","
			CsvOutput
		
        DerivedWithholdingAmount is a DerivedField
            type is like InternationalAmount
			return first PayablesInvoicePaymentRel.WithholdingAmount


		DerivedCashCodeCurrency is a DerivedField
			type is like Currency
			return first PayablesInvoicePaymentRel.CashCode.Currency


        DerivedComputeWithholdingAmount is a DerivedField 
            type is like InternationalAmount
			return 0

		DerivedTotalWithholdingAmount is a DerivedField 
            type is like InternationalAmount
			return 0

	
        
	Local Fields
		LocalFieldName						is LPLName
		LocalOutputField					is Alpha up to 200
			is related value for "PaymentOutputFileRemittance"
		LocalFieldValue						is Alpha up to 20000 
		LocalCustomCsvHeader				is Text
		LocalCustomCsvOutput				is Text
		LastPaymentOutputMappingRemittance	is like PaymentOutputMappingRemittance
		LocalContainsDelimiterValue			is Boolean
					
	Context Fields

    Conditions

    Relations
		PayablesInvoiceRel					
			one-to-one relation to PayablesInvoice
			Field Mapping uses symbolic key
				related.Company 		= PayablesCompany
				related.PayablesInvoice	= PayablesInvoice

		PayablesInvoiceDistributionRel
			one-to-many relation to PayablesInvoiceDistribution
			Field Mapping uses Set9		
				related.Company			 = PayablesCompany
				related.Vendor			 = InvoiceVendor
				related.PayablesInvoice	 = PayablesInvoice
			Instance Selection
				where (related.DistributionType	= "D"
				and   !related.VATReverse
				and    related.TaxSequenceNumber not entered)	

		CashLedgerPaymentRel				
			one-to-many relation to CashLedgerPayment
			Field Mapping uses ByCashLedgerTransactionIdentifier
				related.CashManagementGroup				= CashManagementGroup
				related.CashLedgerTransactionIdentifier	= CashLedgerTransactionIdentifier

		PayablesInvoicePaymentRel
		    one-to-many relation to PayablesInvoicePayment
		    Field Mapping uses symbolic key
		        related.Company			= PayablesCompany
		        related.PayablesInvoice = PayablesInvoice
		    Instance Selection
		        where (related.PayablesInvoice.InvoiceDate	= InvoiceDate)
		        
		PayablesInvoiceWithholdingRel
		    one-to-many relation to PayablesInvoiceWithholding
		    Field Mapping uses symbolic key
		        related.Company         = PayablesCompany
		        related.PayablesInvoice = PayablesInvoice

    Sets
		ByPaidVendor
			duplicates
			Sort Order
				CashManagementGroup
				CashManagementAccount
				PaymentOutputFileHeader
				PaymentOutputFileDetail				
				PaymentOutputFileRemittance
				PayablesCompany
				InvoiceVendor
				TransactionNumber				

    Field Rules
		BankClientIdentifier
			default to PaymentOutputFileDetail.BankClientIdentifier

		CashLedgerTransactionIdentifier
			default to PaymentOutputFileDetail.CashLedgerTransactionIdentifier

        TransactionNumber
			default to PaymentOutputFileDetail.TransactionNumber

		PaidVendor
			default to PaymentOutputFileDetail.PaidVendor
			
	Actions
        Create is a Create Action
			restricted
			
		CreateFromPaymentRun is a Create Action				
			restricted
			Action Rules
				invoke Create this instance
					if (PayablesInvoice entered)
						fill in user fields from PayablesInvoiceRel
					else
						fill in user fields from first CashLedgerPaymentRel
					
        Update is an Update Action
			restricted
							
        Delete is a Delete Action
			restricted
			
