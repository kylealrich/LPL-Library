ContractQuestion is a BusinessClass
	owned by po
	prefix is CAQU

	Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Ontology
		symbolic key is ContractQuestion

	Persistent Fields
		QuestionText					is Text
			sql name is CONQUE
			text searchable
		ResponseType					is Numeric size 2
	 		States
				Text		value is 1
				Number		value is 2
				Date		value is 3
				List		value is 4
				YesNo		value is 5
				YesNoText	value is 6
				Rebate		value is 7
		Attachment						is an AlternateAttachment
		DisplayOrder
		LastQuestionValueDisplayOrder	is Numeric 6
		Question
			default label is "RepositoryQuestion"
		CreatedFromQuestion             is like Question
		SSQuestion						is Boolean
		QuestionSource					is Numeric size 2
			States
				Interview		value is 1
				Manual			value is 2
				SourcingEvent	value is 3
				Conditional     value is 4
		ResponseRules			is Numeric size 1
			States
				NotRequired								value is 1
				ResponseRequired						value is 2
				ResponseRequiredAndAttachmentRequired	value is 3
					default label is "ResponseRequired_andAttachmentRequired"
		YesNoResponseRules		is Numeric size 1
			default label is "ResponseRules"
			States
				NotRequired									value is 1
				ResponseRequired							value is 2
				ResponseRequiredAndAttachmentRequired		value is 3
					default label is "ResponseRequired_andAttachmentRequired"
				ResponseRequiredAndAttachmentRequiredIfYes	value is 4
					default label is "ResponseRequired_andAttachmentRequired_ifYes"
				ResponseRequiredAndAttachmentRequiredIfNo	value is 5
					default label is "ResponseRequired_andAttachmentRequired_ifNo"
		YesNoTextResponseRules	is Numeric size 1
			default label is "ResponseRules"
			States
				NotRequired											value is 1
				ResponseRequiredAndTextResponseRequired				value is 2
					default label is "ResponseRequired_andTextResponseRequired"
				ResponseRequiredAndTextOrAttachmentRequired			value is 3
					default label is "ResponseRequired_andText_orAttachmentRequired"
				ResponseRequiredAndTextOrAttachmentRequiredIfYes	value is 4
					default label is "ResponseRequired_andText_orAttachmentRequired_ifYes"
				ResponseRequiredAndTextOrAttachmentRequiredIfNo		value is 5
					default label is "ResponseRequired_andText_orAttachmentRequired_ifNo"
				ResponseRequiredAndTextAndAttachmentRequired		value is 6
					default label is "ResponseRequired_andText_andAttachmentRequired"
				ResponseRequiredAndTextAndAttachmentRequiredIfYes	value is 7
					default label is "ResponseRequired_andText_andAttachmentRequired_ifYes"
				ResponseRequiredAndTextAndAttachmentRequiredIfNo	value is 8
					default label is "ResponseRequired_andText_andAttachmentRequired_ifNo"
		AllowResponseAttachment			is Boolean

	Transient Fields
		NewDisplayOrder			is a DisplayOrder
		TransientListAnswer1	is Alpha 50
		TransientListAnswer2	is Alpha 50
		TransientListAnswer3	is Alpha 50
		TransientListAnswer4	is Alpha 50
		TransientListAnswer5	is Alpha 50

	Local Fields
		LocalConditionalQuestion is like Question
		LocalListValue           is like ContractQuestionListValue 
		LocalYesOrNo             is Numeric 1 
           	States
           		Yes	value is 1
           		No	value is 2 
		FromRepository           is Boolean 
		FromConditional          is Boolean		
		LocalFromConditional     is Boolean
		LocalAttributeCtr		 is Numeric 2

	Field Groups
		RevisionControlled
			QuestionText
			ResponseType
			Attachment
			ResponseRules
			YesNoResponseRules
			YesNoTextResponseRules

	Derived Fields
		IsQuestionRequired is a ConditionalField
			type is Alpha size 2
			restricted




			if (ResponseRequired
			and Question.DefineCorrectAnswer)
				"**"
			else
				blank

		AnswerMessage is a MessageField
			restricted
			"Answer"

		AnswerTextWithRequired is a StringField
			type is Text
			restricted
			IsQuestionRequired AnswerMessage

		IsAttachmentRequired is a ConditionalField
			type is Alpha size 2
			restricted
			if (!AlwaysRequireResponseAttachment)
				blank
			else
				"*"

		AttachDocumentMessage is a MessageField
			restricted
			"AttachDocument"

		AttachmentTextWithRequired is a StringField
			type is Text
			restricted
			IsAttachmentRequired AttachDocumentMessage

		IsManualQuestion is a ConditionalField
			type is Boolean
			restricted
			if (QuestionSource.Manual)
				true
			else
				false

	Conditions
		ResponseRequired
			restricted
			when (ResponseRules.ResponseRequiredAndAttachmentRequired
			or    ResponseRules.ResponseRequired
			or    YesNoResponseRules.ResponseRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)

		MayRequireResponseAttachment
			restricted
			when (ResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)

		AlwaysRequireResponseAttachment
			restricted
			when (ResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired
			or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)

		YesNoResponseType
			restricted 
			when (ResponseType.YesNo
			or    ResponseType.YesNoText)
		
		YesNoOrList 
			restricted
			when (YesNoResponseType
			or    ResponseType.List)
		
		CanCreateConditionalQuestion
			restricted 
			when (YesNoResponseType
			and  !QuestionIsAnswered)
		
		CanCreateConditionalListQuestion
			restricted 
			when (ResponseType.List
			and  !QuestionIsAnswered)

		HasContractConditionalQuestions 
			restricted 
			when (ContractConditionalYesNoQuestionRel exists)

		HasListConditionalQuestions 
			restricted 
			when (ContractConditionalListQuestionRel exists)
		
		HasAnyContractConditionalQuestions 
			when (HasContractConditionalQuestions
			or    HasListConditionalQuestions)
		
		RepositoryQuestion 
			restricted 
			when (Question entered)

		RepositoryHasConditionalQuestions	
			restricted 
			when (RepositoryConditionalQuestionRel exists)

		CreatedFromQuestionEntered	
			restricted 
			when (CreatedFromQuestion entered)
		
		NonYesNoResponseType
			restricted
			when (ResponseType.Date
			or    ResponseType.List
			or    ResponseType.Text
			or    ResponseType.Number)

		QuestionIsAnswered
			restricted
			when (ContractQuestionAnswerRel exists)

		DisplayAnswerRequiredText 
			restricted
			when (RequiredQuestionNoAnswer
			or    MayRequireAttachmentNoAttachment)

		RequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and  !QuestionIsAnswered)

		MayRequireAttachmentNoAttachment 
			restricted 
			when (MayRequireResponseAttachment
			and  !AnswerAttachmentExists)

		QuestionHasCorrectAnswer
			restricted
			when (CorrectAnswerRel exists)

		QuestionIsAnsweredIncorrectly
			restricted
			when (ContractQuestionAnswerRel exists
			and !ContractQuestionAnswerRel.QuestionAnswerRel.CorrectAnswer)

		IsRebateQuestion
			restricted
			when (ResponseType.Rebate)

		IsListOrRebateQuestion
			restricted
			when (Question.ResponseType.List
			or Question.ResponseType.Rebate)

		IsTextNumberDateQuestion
			restricted
			when (Question.ResponseType.Date
			or Question.ResponseType.Number
			or Question.ResponseType.Text)

		EligibleForSourcing
			restricted
			when (Contract.SourcingEligible
			and	  !IsRebateQuestion)

		HasAttachment
			restricted
			when (Attachment entered)

		HasListValues
			restricted
			when (ContractQuestionListValue set exists)

		QuestionAnswerHasTerms
			restricted
			when (ContractQuestionAnswerTermsRel exists)

		QuestionAnswerTermExists
			restricted
			when (Question.QuestionAnswerTermRel exists)

		AnswerAttachmentExists
			restricted
			when (ContractQuestionAnswerAttachmentRel exists)

		AnyQuestionAnswers
			restricted
			when (AnyQuestionAnswersRel exists)

	Relations
		AnyQuestionAnswersRel is a ContractQuestionAnswer set

		ContractListOrRebateQuestionAnswerRel
			one-to-many relation to QuestionAnswer
			valid when (IsListOrRebateQuestion)
			Field Mapping uses symbolic key
				related.ProcurementGroup	= ContractGroup
				related.Question			= Question

		SameRepositoryQuestionRel 
			one-to-many relation to ContractQuestion 
			Field Mapping uses ByQuestion
				related.ContractGroup       = ContractGroup 
				related.Question            = LocalConditionalQuestion 
				related.Contract            = Contract 

		CreatedFromQuestionRel 
			one-to-one relation to ContractQuestion 
			Field Mapping uses symbolic key 
				related.ContractGroup		= ContractGroup
				related.Contract            = Contract 
				related.ContractQuestion	= CreatedFromQuestion

		LocalConditionalQuestionRel 
			one-to-one relation to ContractConditionalYesNoQuestion
			Field Mapping uses symbolic key 
				related.ContractGroup   										= ContractGroup
				related.Contract                        						= Contract
				related.ContractQuestion    									= ContractQuestion
				related.ContractConditionalYesNoQuestion.YesOrNo       			= LocalYesOrNo
				related.ContractConditionalYesNoQuestion.ConditionalQuestion	= LocalConditionalQuestion	

		ContractConditionalYesNoQuestionRel
			one-to-many relation to ContractConditionalYesNoQuestion 
			delete cascades 
			Field Mapping uses symbolic key 
				related.ContractGroup   										= ContractGroup
				related.Contract                        						= Contract
				related.ContractQuestion    									= ContractQuestion				
		
		ContractConditionalListQuestionRel
			one-to-many relation to ContractConditionalListQuestion 
			delete cascades 
			Field Mapping uses symbolic key 
				related.ContractGroup   										= ContractGroup
				related.Contract                        						= Contract
				related.ContractQuestion    									= ContractQuestion

		LocalConditionalListQuestionRel
			one-to-one relation to ContractConditionalListQuestion 
			delete cascades 
			Field Mapping uses symbolic key 
				related.ContractGroup   										= ContractGroup
				related.Contract                        						= Contract
				related.ContractQuestion    									= ContractQuestion	
				related.ContractQuestionListValue                               = LocalListValue 
				related.ContractConditionalListQuestion                         = LocalConditionalQuestion	

		RepositoryConditionalQuestionRel    
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup 						= ContractGroup 
				related.Question                                = Question 

		RepositoryListValuesRel
			one-to-many relation to QuestionListValue 
			Field Mapping uses symbolic key 
				related.ProcurementGroup	= ContractGroup 
				related.Question            = Question 
		

		ContractQuestionAnswerRel
			one-to-one relation to ContractQuestionAnswer
			Field Mapping uses ContractQuestionSet
				related.ContractGroup		= ContractGroup
				related.Contract			= Contract
				related.Question			= Question
				related.ContractQuestion	= ContractQuestion

		ContractQuestionAnswerAttachmentRel is a ContractQuestionAnswer set
			Instance Selection
				where (related.Attachment entered)

		ContractQuestionAnswerTermsRel
			one-to-many relation to ContractQuestionAnswer
			Field Mapping uses ContractQuestionSet
				related.ContractGroup		= ContractGroup
				related.Contract			= Contract
				related.Question			= Question
				related.ContractQuestion	= ContractQuestion
			Instance Selection
				where (related.AnswerHasTerms)

		CorrectAnswerRel
			one-to-one relation to QuestionAnswer
			Field Mapping uses CorrectAnswerSet
				related.ProcurementGroup	= ContractGroup
				related.Question			= Question

		ContractRebateTypeRel
			one-to-many relation to ContractRebateType
			Field Mapping uses symbolic key
				related.ContractGroup = ContractGroup
			Instance Selection
				where (related.Active)

	Sets
		ByQuestion
			duplicates
			Sort Order
				ContractGroup
				Question
				Contract

		ByDisplayOrder
			duplicates
			Sort Order
				ContractGroup
				Contract
				DisplayOrder
				Question

	Field Rules
		QuestionText
			required
		ResponseType
			required
		QuestionSource
			default to 2
		ResponseRules
			if (NonYesNoResponseType)
				required
					"MustSelectAResponseRule"
				YesNoResponseRules = 0
				YesNoTextResponseRules = 0

		YesNoResponseRules
			if (ResponseType.YesNo)
				required
					"MustSelectAResponseRule"
				ResponseRules= 0
				YesNoTextResponseRules= 0

		YesNoTextResponseRules
			if (ResponseType.YesNoText)
				required
					"MustSelectAResponseRule"
				ResponseRules= 0
				YesNoResponseRules= 0
		AllowResponseAttachment
			if (AllowResponseAttachment changed
			and AllowResponseAttachment = false)
				constraint (!AnswerAttachmentExists)
					"CannotChangeAllowResponseAttachmentFlag;QuestionAnswersExistWithAttachments"
			if (MayRequireResponseAttachment)
				AllowResponseAttachment = true

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Actions
		Create is a Create Action
			valid when (Contract.DraftOrAddendumOrAmendment)
			Field Rules
				DisplayOrder
					if (QuestionSource.SourcingEvent
					or	!Contract.CreateByCopy)
						autosequence using Contract.LastQuestionDisplayOrder

			Action Rules
				LocalFromConditional = FromConditional 
				SSQuestion = true

			Exit Rules
				if (ResponseType.List)
					
					if (TransientListAnswer1 entered)
						invoke Create ContractQuestionListValue
							fill in fields from this instance
							invoked.ContractQuestionListValue    = TransientListAnswer1

					if (TransientListAnswer2 entered)
						invoke Create ContractQuestionListValue
							fill in fields from this instance
							invoked.ContractQuestionListValue    = TransientListAnswer2

					if (TransientListAnswer3 entered)
						invoke Create ContractQuestionListValue
							fill in fields from this instance
							invoked.ContractQuestionListValue    = TransientListAnswer3

					if (TransientListAnswer4 entered)
						invoke Create ContractQuestionListValue
							fill in fields from this instance
							invoked.ContractQuestionListValue    = TransientListAnswer4

					if (TransientListAnswer5 entered)
						invoke Create ContractQuestionListValue
							fill in fields from this instance
							invoked.ContractQuestionListValue    = TransientListAnswer5

		CreateFromInterview is a Create Action   
			restricted
			Field Rules
				DisplayOrder
					if (!Contract.CreateByCopy)
						autosequence using Contract.LastQuestionDisplayOrder

			Exit Rules
				if (Question.ResponseType.List)
					for each Question.QuestionListValue set
						invoke CreateDisplay ContractQuestionListValue
							invoked.ContractGroup             = ContractGroup
							invoked.Contract                  = Contract
							invoked.ContractQuestion          = ContractQuestion
							invoked.ContractQuestionListValue = each.QuestionListValue
							fill in fields from each

		CreateConditionalQuestion is an Instance Action
			valid when (CanCreateConditionalQuestion)
			Parameters 
            	ParmYesOrNo     		  is Numeric size 1
					default label is "YesOrNo"
					States
            			Yes	value is 1
            			No	value is 2   
				ParmConditionalQuestion is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmYesOrNo 
					required 
				ParmConditionalQuestion 
					required
			
			Action Rules

				constraint (ParmConditionalQuestion.Active)
					"CannotUseAnInactiveQuestion"
				LocalConditionalQuestion	= ParmConditionalQuestion
				LocalYesOrNo                = ParmYesOrNo
				constraint (SameRepositoryQuestionRel !exists)
					"ConditionalQuestionAlreadyExistsAsAContractQuestion"
				constraint (LocalConditionalQuestionRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (ContractQuestion.Question entered)
					constraint (ParmConditionalQuestion != ContractQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsQuestion"

				invoke Create ContractConditionalYesNoQuestion
					invoked.ContractGroup											= ContractGroup 
					invoked.Contract                        						= Contract 
					invoked.ContractQuestion										= ContractQuestion
					invoked.ContractConditionalYesNoQuestion.YesOrNo				= ParmYesOrNo
					invoked.ContractConditionalYesNoQuestion.ConditionalQuestion	= ParmConditionalQuestion	

		CreateConditionalListQuestion is an Instance Action
			valid when (CanCreateConditionalListQuestion)
			Parameters 
            	ParmListValue     		  is a ContractQuestionListValue 
					default label is "ListValue"
				ParmConditionalQuestion 	is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmListValue 
					required 
				ParmConditionalQuestion 
					required
			
			Action Rules

				LocalConditionalQuestion	= ParmConditionalQuestion
				LocalListValue              = ParmListValue
				constraint (LocalConditionalListQuestionRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (ContractQuestion.Question entered)
					constraint (ParmConditionalQuestion != ContractQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsRepositoryQuestion"

				invoke Create ContractConditionalListQuestion
					invoked.ContractGroup					= ContractGroup 
					invoked.Contract                        = Contract 
					invoked.ContractQuestion				= ContractQuestion
					invoked.ContractQuestionListValue		= ParmListValue
					invoked.ContractConditionalListQuestion	= ParmConditionalQuestion	

		Update is an Update Action
			valid when (Contract.DraftOrAddendumOrAmendment)
			Action Rules
				constraint (!QuestionSource.Interview)
					"QuestionsFromAnInterviewCannotBeUpdated"

				if (Question entered
				and RevisionControlled changed)
					initialize Question
					SSQuestion = false

				if (!ResponseType.List
				and ContractQuestionListValue set exists)
					invoke Delete ContractQuestionListValue set

				if (ResponseType changed)
					initialize Question
					constraint (!AnyQuestionAnswers)
						"CannotChangeQuestionTypeWhenAnswersExist"

		Delete is a Delete Action
			valid when (Contract.ContractStatus.Draft)
			Action Rules
				constraint (!QuestionSource.Interview)
					"QuestionsFromAnInterviewCannotBeDeleted"

		DeleteRestricted is a Delete Action
			restricted
			Entrance Rules
				invoke DeleteRestricted ContractQuestionAnswerRel

		DeleteConditionalQuestions is a Set Action  
			restricted 
			Parameters 
			 	ParmContractGroup		is a ContractGroup 
			 	ParmContract            is a Contract 
			 	ParmOriginalQuestion    is a ContractQuestion
				ParmLastOldQuestion     is a ContractQuestion   

			Instance Selection 
			 	where (ParmContractGroup	= ContractGroup
			 	and    ParmContract         = Contract 
			 	and    ParmOriginalQuestion = CreatedFromQuestion
				and    ContractQuestion    <= ParmLastOldQuestion)

			Action Rules
			 	Instance Rules 
					invoke Delete		

		Purge is a Purge Action
			restricted

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
														
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
