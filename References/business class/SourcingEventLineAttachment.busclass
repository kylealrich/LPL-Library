SourcingEventLineAttachment is a BusinessClass
    owned by ss
    prefix is ELA

    Ontology
    	part of SourcingEventLine
    		relative key is SeqNumber is a Sequence
    	
    Persistent Fields
		Attachment
		Name
		Description     is a Description3
		CMAttachment	is Boolean
		SupplierCanView 
		
	Field Rules
		SeqNumber
			autosequence 
		Attachment
			required
		SupplierCanView
			default to true
		Name
			default to Attachment.Title
	
	Conditions
		EligibleForCM
			when (SourcingEventLine.ContractOutput)

		ActionsAreValid
			restricted
			when ((SourcingEvent.InEditableState
			and	   SourcingEventLine.InLineActionableState)
			or     Company.CanAlwaysCreateEventAttachment)
		
		HasAttachment
			when (Attachment entered)

	Actions
		Create is a Create Action
			default label is "Upload"
			valid when (ActionsAreValid)
			Field Rules
				CMAttachment
					if (!SourcingEvent.CreateByCopy
					and SourcingEvent.InEditableState)
						default to true
					
			Action Rules
				if (!Company.CanAlwaysCreateEventAttachment)
					constraint (SourcingEvent.InActionableState)
						"CannotAddAttachmentWhileEventIsPendingAward,Cancelled,OrClosed"
					constraint (SourcingEventLine.InLineActionableState)
						"CannotAddAttachmentWhileLineIsPendingAward,Cancelled,OrAwarded"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddAttachmentWhileEventIsPendingEventApproval"
				if (CMAttachment = true)
					constraint (SourcingEvent.InEditableState)
						"CannotSetSendAttachmentToContractManagementToTrue;ContractHasAlreadyBeenCreated" 
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddAttachment)
					invoke Amend Open.Notified SourcingEvent 
					
		Update is an Update Action
			valid when (ActionsAreValid)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotUpdateAttachmentWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotUpdateAttachmentWhileLineIsPendingAward,Cancelled,OrAwarded"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotUpdateAttachmentWhileEventIsPendingEventApproval"
				if (CMAttachment = true
				and CMAttachment changed)
					constraint (SourcingEvent.InEditableState)
						"CannotSetSendAttachmentToContractManagementToTrue;ContractHasAlreadyBeenCreated" 
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateAttachment)
					invoke Amend Open.Notified SourcingEvent 

		Delete is a Delete Action
			valid when (ActionsAreValid)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteAttachmentWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (SourcingEventLine.InLineActionableState)
					"CannotDeleteAttachmentWhileLineIsPendingAward,Cancelled,OrAwarded"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteAttachmentWhileEventIsPendingEventApproval"

				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.DeleteAttachment)
					invoke Amend Open.Notified SourcingEvent
