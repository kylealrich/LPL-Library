InventoryReportGroupLocation is a BusinessClass
    owned by ic
    prefix is IRG
    classic name is ICRPTGRP

    Ontology
        symbolic key is InventoryReportGroupLocation

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        TotalGroup is Numeric size 3

    Local Fields
		LocalProcurementGroup	is like ProcurementGroup
		LocalParticipantGroup   is like ParticipantGroup
	
	Conditions
    
    	LocationActive
    		restricted
    		when (InventoryLocation.Status.Active)
    		
    	CartParLocation
    		restricted
    		when (InventoryLocation.ParOrExchangeCart)
   
    Relations
    	InventoryLocationRel
    		one-to-many relation to InventoryLocation
    		Field Mapping uses symbolic key
    			related.Company	= Company

        ParticipantGroupLineRel 
            one-to-many relation to ParticipantGroupLine  
            Field Mapping uses ByLocReqLocSet
                related.ProcurementGroup                                    = LocalProcurementGroup
                related.ParticipantGroup                                    = LocalParticipantGroup 
                related.ParticipantGroupLine.ParticipantLocation.Company    = Company 
                related.ParticipantGroupLine.ParticipantLocation.Location   = InventoryLocation

        ParticipantGroupLineReqLocation
            one-to-many relation to ParticipantGroupLine  
            Field Mapping uses ByLocReqLocSet
                related.ProcurementGroup                                            = LocalProcurementGroup
                related.ParticipantGroup                                            = LocalParticipantGroup 
                related.ParticipantGroupLine.ParticipantLocation.Company            = Company 
                related.ParticipantGroupLine.ParticipantLocation.Location           = blank 
                related.ParticipantGroupLine.ParticipantLocation.RequestingLocation = InventoryLocation

    Sets

        Set2
            indexed
            Sort Order
                Company
                InventoryReportGroup
                TotalGroup
                InventoryLocation

	Field Rules
		TotalGroup
			constraint (TotalGroup > 0)
				"TotalGroupShouldBeGreaterThanZero"

	Actions
		Create is a Create Action

			Action Rules 

				if (InventoryReportGroup.ParticipantGroupReportGroupRel exists)
					for each InventoryReportGroup.ParticipantGroupReportGroupRel
						LocalProcurementGroup	= each.ProcurementGroup 
						LocalParticipantGroup   = each.ParticipantGroup
						if (ParticipantGroupLineRel !exists
						and ParticipantGroupLineReqLocation !exists)
							invoke Create ParticipantGroupLine 
                            	invoked.ProcurementGroup	                              	= LocalProcurementGroup
								invoked.ParticipantGroup								 	= LocalParticipantGroup
								invoked.ParticipantGroupLine.ParticipantLocation.Company	= Company	
								invoked.ParticipantGroupLine.ParticipantLocation.Location 	= InventoryLocation
	
		Update is an Update Action
	
        Delete is a Delete Action
        
		FreezeLocationsInReportGroup is a Set Action
        	restricted
        	Parameters
        		PrmCompany						is an InventoryCompany
        		PrmSelectId						is a PhysicalInventorySelect
        		
        	Parameter Rules
        		PrmCompany
        			required
        			
        		PrmSelectId
        			required
        			constraint (PrmSelectId.ReportGroup entered)
        				"PhysicalInventorySelectIdDoesNotContainAReportGroup"
        				
        	Instance Selection
        		where (Company					= PrmCompany
        		and    InventoryReportGroup		= PrmSelectId.ReportGroup)
        
        	Action Rules
        		Instance Rules
					invoke FreezeItemsForLocation ItemLocation in background
						invoked.PrmCompany				= PrmCompany
						invoked.PrmLocation				= InventoryLocation
						invoked.PrmSelectId				= PrmSelectId
