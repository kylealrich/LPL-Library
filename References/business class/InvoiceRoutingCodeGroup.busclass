InvoiceRoutingCodeGroup is a BusinessClass
	owned by apautomation
	prefix is APRCG

	Ontology
		symbolic key is InvoiceRoutingCodeGroup

	Patterns

	Persistent Fields
		Name
			default label is "ApprovalCodeGroup"
		Description3
			default label is "Description"
		Active
		ProcessApprovalsForAllGroupLevels is Boolean
		
	Local Fields		
			LocalMaxApprovalAmount is an InternationalAmount

	Derived Fields
		DerivedFirstActiveInvoiceRoutingCodeGroupLevel is a DerivedField
			type is like InvoiceRoutingCode
			restricted
			return first ActiveInvoiceRoutingCodeGroupLevelRel.InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel

		DerivedStatus is a DerivedField
			type is Alpha 10
			if (Active)
				return "Active"
			return "Inactive"
		DerivedMaxApprovalAmount is an DerivedField		
			type is like InternationalAmount
			restricted
			initialize LocalMaxApprovalAmount
			for each ActiveInvoiceRoutingCodeGroupLevelRel
				if (each.InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel.DerivedMaxApprovalAmount > LocalMaxApprovalAmount)
					LocalMaxApprovalAmount = each.InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel.DerivedMaxApprovalAmount
			return LocalMaxApprovalAmount

	Conditions
		ActiveGroupWithAllLevelsInactive
			restricted
			when (Active
			and   ActiveInvoiceRoutingCodeGroupLevelRel !exists)

		UsesTeamMemberAssignment		
			restricted
			when (InvoiceRoutingCodeGroupLevelRel.InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel.UsesTeamMemberAssignment)

		FinalLevelUsesTeamMemberAssignment		
			restricted
			when (last InvoiceRoutingCodeGroupLevelRel.InvoiceRoutingCodeGroupLevel.ApprovalCodeGroupLevel.UsesTeamMemberAssignment)

	Relations
		InvoiceRoutingCodeGroupLevelRel is a InvoiceRoutingCodeGroupLevel set

		ActiveInvoiceRoutingCodeGroupLevelRel
			one-to-many relation to InvoiceRoutingCodeGroupLevel
			Field Mapping uses ByCodeGroupLevel
				related.VendorGroup				= VendorGroup
				related.InvoiceRoutingCodeGroup	= InvoiceRoutingCodeGroup
			Instance Selection
				where (related.Active)

	Sets
		ByCodeGroupNameAscending
			Sort Order
				Name
				Description3
				InvoiceRoutingCodeGroup

	Field Rules
		Name
			required

		Description3
			required

	Attach Rules
		constraint (Active)
			"ApprovalCodeGroup<Name>IsInactive"

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action

