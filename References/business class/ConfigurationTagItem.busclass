ConfigurationTagItem is a BusinessClass
	owned by repository
 	prefix is ctit
 	default label is "TagItem"
 	disable data area copy
 	
    Ontology
        symbolic key is ConfigurationTagItem

	Patterns
		disable AsOfDateProcessing
        disable EffectiveDated
        disable Auditing
        implements CreateStamp
        
    Conditions
    	DeletedRecord
    		when (not ConfigEntityRel.ConfigEntityExists)
    	
    	OrphanedRecord 
    		when (ConfigEntityRel.ConfigEntity = blank)
    		
    	InvalidRecord
    		when (ConfigEntityRel.Status.Invalid)
    		
    	InactiveRecord
    		when (ConfigEntityRel.Status.Inactive)
    		
    	IsFolder
    	    default label is "Folder"
    		when (ConfigEntityRel.IsFolder)
    		
    Local Fields
    	OtherReference is BusinessObjectReference
    	
    Derived Fields
    	ReportMessage is a MessageField
			restricted
			"ForReport<ConfigEntityRel.UserFolder.Name>"
        
        FolderMessage is a MessageField
			restricted
			"ForFolder<ConfigEntityRel.UserFolder.Name>"
		
		FolderTypeMessage is a DerivedField
    		type is MessageField
    		
    		if (ConfigEntityRel.UserFolder.IsReport)
    			return ReportMessage
    		else
    		if (ConfigEntityRel.UserFolder.IsFolder)
    			return FolderMessage
    		else
    			return ""
			
    Create Rules
    	constraint (ConfigurationTag.Active)
    		"<ConfigurationTag>IsInactive"
    		
	Actions
		Create is a Create Action
		Delete is a Delete Action
			Exit Rules 
				if (ConfigEntityRel.EntityType.BusinessClass)
					OtherReference = reference to ConfigEntityRel.RelUserBusinessClass.UserKeyField
					invoke Delete OtherTagItem
						resume on error
				else
				if (ConfigEntityRel.EntityType.KeyField)
					for each ConfigEntityRel.RelUserKeyField.UserBusinessClass set
						OtherReference = reference to each
						invoke Delete OtherTagItem
							resume on error
				else
				if (ConfigEntityRel.EntityType.SecurityClass)
					for each ConfigEntityRel.RelSecurityClass.ParentChildRel
						OtherReference = reference to each
						constraint (not OtherTagItem exists)
							"OtherSecurityClassesThatExtendThisClassHaveBeenTaggedWithThisSameTag.TagItemCannotDeletedUnlessThoseAreRemoved."
							
		TagExtendedSecurityClasses is a Set Action
			restricted
			default label is untranslatable
			
			Instance Selection
				where (ConfigEntityRel.EntityType.SecurityClass and ConfigEntityRel.RelSecurityClass.Extends and not ConfigEntityRel.RelSecurityClass.BaseClass.StandardTemplate)
			
			Sort Order is primary
			
			Action Rules
				Instance Rules
					OtherReference = reference to ConfigEntityRel.RelSecurityClass.BaseClass
					
					if (not OtherTagItem exists)
						invoke Create ConfigurationTagItem
							invoked.ConfigurationTag = ConfigurationTag
							invoked.ConfigurationTagItem.Reference = OtherReference
						
	Relations
		ConfigEntityRel
			default label is "ConfigurationEntity"
			one-to-one relation to ConfigEntity
			include deleted record
			Field Mapping uses symbolic key
				related.ConfigEntity = ConfigurationTagItem.Reference.BusinessObjectKey
				
		OtherTagItem
			one-to-one relation to ConfigurationTagItem
			Field Mapping uses symbolic key
				related.ConfigurationTag = ConfigurationTag
				related.ConfigurationTagItem.Reference = OtherReference
				
	Sets
		ByReference
			indexed
			Sort Order
				ConfigurationTagItem.Reference
				ConfigurationTag
