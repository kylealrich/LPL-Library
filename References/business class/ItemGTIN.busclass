ItemGTIN is a BusinessClass
    owned by ic
    prefix is GTI
    classic name is ICITEMGTIN

    Ontology
        symbolic key is ItemGTIN
            classic set name is GTISET1
            classic name is GTIN

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Active                 is Boolean
            classic name is STATUS
		UseAsDefaultPerUOM		is Boolean	
			default label is "UseAsDefaultPerMfgInfoAnd_\U\O\M"
        UnitOfMeasure          is a UnitOfMeasureEdit
            classic name for UnitOfMeasure.UnitOfMeasure is UOM
        GTINStructure          is Numeric size 1
            States
                GTIN8  value is 0
                GTIN12 value is 1
                GTIN13 value is 2
                GTIN14 value is 3
        CountryCode				is a Country
        GPCSegment
        GPCFamily
        GPCClass
        GPCBrick
        UNSPSCCode
            classic name for UNSPSCCode.UNSPSCSegment is ICSEGMENT
            classic name for UNSPSCCode.UNSPSCFamily is ICFAMILY
            classic name for UNSPSCCode.UNSPSCClass is ICCLASS
            classic name for UNSPSCCode.UNSPSCCommodity is ICCOMMODITY
        BrandName
        FunctionalName         is Alpha size 35
            classic name is FUNCTIONAL-NME
        AbbreviatedDescription is Alpha size 30
            classic name is ABBREV-DESC
        FullDescription        is Alpha size 40
            classic name is FULL-DESC
        HeightMeasurement      is a Measurement
            classic name is HEIGHT-MEASURE
        HeightUOM              is a UnitOfMeasure
        WidthMeasurement       is a Measurement
            classic name is WIDTH-MEASURE
        WidthUOM               is a UnitOfMeasure
        DepthMeasurement       is a Measurement
            classic name is DEPTH-MEASURE
        DepthUOM               is a UnitOfMeasure
        NetContent             is a Measurement
        NetContentUOM          is a UnitOfMeasure
            classic name is CONTENT-UOM
        GrossWeight            is a Measurement
        GrossWeightUOM         is a UnitOfMeasure
            classic name is GR-WEIGHT-UOM
        NetWeight              is a Measurement
        NetWeightUOM           is a UnitOfMeasure
        ConsumerUnit           is Boolean
        OrderableUnit          is Boolean
        InvoiceUnit            is Boolean
        ShippingUnit           is Boolean
        BaseUnit               is Boolean
        VariableUnit           is Boolean
        BarCodeType            is Numeric size 2
            States
                NoBarcode                   value is 0
                CompositeComponentA         value is 1
                CompositeComponentB         value is 2
                CompositeComponentC         value is 3
                EANUCC13Symbol              value is 4
                EANUCC14Symbol              value is 5
                EANUCC8Symbol               value is 6
                ITF14Symbol                 value is 7
                RSS14                       value is 8
                RSS14Stacked                value is 9
                RSS14StackedOmniDirectional value is 10
                RSS14Truncated              value is 11
                RSSExpanded                 value is 12
                RSSExpandedStacked          value is 13
                RSSLimited                  value is 14
                UCCEAN128Symbol             value is 15
                UPCASymbol                  value is 16
                UPCESymbol                  value is 17
                Other                       value is 99
        BatchOrLotRequired     is Numeric size 1
            classic name is BATCH-LOT-REQ
            States
                NoBatchOrLotNumberAllocated	value is 0
                RequiredBatchOrLotAllocated	value is 1
                BatchOrLotAllocated			value is 2
        Returnable             is Boolean
        DietOrAllergen         is Numeric size 2
            classic name is DIET-ALLERGEN
            States
				None							value is 0
				ApprovedForTubeFeeding			value is 1
				ContainsPVC						value is 2
				ContainsPVCWithPhthalates		value is 3
				ContainsPVCWithoutPhthalates	value is 4
				DietProduct450800KCal			value is 5
				DietProduct8001200KCal			value is 6
				LactateEnzyme					value is 7
				LowOnPhenylalanine				value is 8
				LowSugar						value is 9
				MothersMilkSubstitute			value is 10
				NutritionSupplement				value is 11
				ContainsLatex					value is 12
				Other							value is 99
        SterileIndicator       is Numeric size 2
            classic name is STERILE-IND
            States
                None           value is 0
                Sterile        value is 1
                ExtremelyClean value is 2
                Other          value is 99
        ParentGTIN             is an ItemGTIN
        DateAvailable          is TimeStamp
        EffectiveDate          is TimeStamp
        PublicationDate        is TimeStamp
            classic name is PUBLICATION-DT
        BrandOwnerGLN          is a GlobalLocationNumber
            classic name is BRAND-OWNR-GLN
        ProviderGLN            is a GlobalLocationNumber
        Manufacturer
            classic name for Manufacturer.ManufacturerCode is MANUF-CODE
            classic name for Manufacturer.ManufacturerDivision is MANUF-DIVISION
        ManufacturerNumber
            classic name is MANUF-NBR
        ProductShelfLife       is a ShelfLife
            classic name is SHELF-LIFE-PRD
        ShelfLifeAfterDelivery is a ShelfLife
            classic name is SHELF-LIFE-DEL
        Recyclable             is Boolean
        ReturnUnsold           is Boolean
        LatexIndicator         is Numeric size 1
            classic name is LATEX-IND
            States
            	Unspecified					value is 0
            	PackageIndicatesLatex		value is 1
            	PackageDoesNotIndicateLatex	value is 2
            	NotApplicable				value is 3
        ReusabilityCode        is Numeric size 1
            classic name is REUSABILITY-CD
            States
                Undefined              value is 0
                SingleUse              value is 1
                Reusable               value is 2
                LimitedReuse           value is 3
                ReusableForSamePatient value is 4
        LongDesc               is a Description3
        FromWorksheetResult       is an ItemInformationWorksheetResult

	Local Fields
		
		GTINCheckDigit
		LocalValidCheckDigit	is Boolean
		LocalManufacturer       is like Manufacturer
		LocalManufacturerNumber is like ManufacturerNumber
		FromAttributeUpdate     is Boolean
		LocalFromUpdateActiveFromItem is Boolean 

	Transient Fields 
		FromNewConsolidatedUOM  is Boolean 
	
	Attach Rules
		if (not BypassRuleForTheseFiles)
			constraint (Active)
				"GTIN<ItemGTIN>ForItem<Item>MustBeActive"

	Derived Fields
		
		RepresentativeText is a StringField
			type is Text
			default label is "GTIN"
			ItemGTIN " - " FullDescription
		UOMNotValidForSelling			is a MessageField
			"UnitOfMeasureIsNotValidForSelling"
		UOMNotValidForBuying			is a MessageField
			"UnitOfMeasureIsNotValidForBuying"
		ItemNotCatchWeight				is a MessageField
			"ItemIsNotDefinedAsCatchWeight"

		NumberOfActiveMatchingGTINs is a ComputeField
			type is Numeric size 6
			restricted
			(instance count of ActiveDuplicateGTINMfgPerUOMRel)

		ActiveCardView 			is a LabelField
			"Active"
		InactiveCardView 		is a LabelField
			"Inactive"
		ActiveCardViewDisplay 	is a DerivedField
			type is MessageField
			if (Active)
				return ActiveCardView
			else
				return InactiveCardView

		ManufacturerCardViewDisplay		is a DerivedField
			type is MessageField
			return Manufacturer.ManufacturerCode + " " + Manufacturer.ManufacturerDivision

    Conditions
		ItemGTINExists
			restricted
			when (ItemGTIN exists)
			
		IsValidGTINSize
			restricted
			when (ItemGTIN size = 8
			or	  ItemGTIN size = 12
			or    ItemGTIN size = 13
			or    ItemGTIN size = 14)

        IsActive
        	restricted
            when (Active)

        ItemInactive
        	restricted
        	when (Item.Active = false)

		ItemActive
			restricted
			when (Item.Active)
        
        HasBrandOwnerGLN
            classic name is BRAND-OWNR-GLN
            restricted
            when (BrandOwnerGLN entered)

        HasGpcSegment
            classic name is GPC-SEGMENT
            restricted
            when (GPCSegment entered)

        HasLongDesc
        	restricted
            when (LongDesc entered)

        HasManufacturerInfo
            classic name is HAS-MANUF-INFO
            restricted
            when (Manufacturer.ManufacturerCode entered
            and   ManufacturerNumber entered)

        HasParentGTIN
            classic name is HAS-PARENT
            restricted
            when (ParentGTIN entered)

        HasUNSPSCSegment
            classic name is ICSEGMENT
            restricted
            when (UNSPSCCode.UNSPSCSegment entered)

        IsInactive
        	restricted
            when (not Active)

        IsOrderableUnit
        	restricted
            when (OrderableUnit)

        HasProviderGLN
            classic name is PROVIDER-GLN
            restricted
            when (ProviderGLN entered)

		NotValidForSelling
			restricted
			when (ConsumerUnit
			and   !UnitOfMeasure.ValidForSelling)
		NotValidForBuying
			restricted
			when (OrderableUnit
			and   !UnitOfMeasure.ValidForBuying)
		NotCatchWeight
			restricted
			when (VariableUnit
			and   Item.CatchWeightCode.NotCatchWeight)
		GTINDuplicated
			restricted
			when (DuplicateGTINRel exists)
		HasDuplicateGTIN
			restricted
			when (SameCodeGTINRel exists)
		HasActiveGTINOnAnotherItem
			restricted
			when (ActiveGTINOnAnotherItemRel exists)
		GTINDetailsDuplicated
			restricted
			when (DuplicateGTINDetailsRel exists)
		HasDuplicateGTINPerUOM
			restricted
			when (DuplicateGTINMfgPerUOMRel exists)
		HasActiveDuplicateGTINPerUOM
			restricted
			when (ActiveDuplicateGTINMfgPerUOMRel exists)
		HasOldDuplicateGTINPerUOM
			restricted
			when (OldDuplicateGTINMfgPerUOMRel exists)
		GTINIsParent
			restricted
			when (ParentGTINRel exists)
		ConsolidatedItemMasterExists
			restricted 
			when (ConsolidatedItemMasterRel exists)         
		InactiveConsolidatedItemMasterExists
			restricted 
			when (InactiveConsolidatedItemMasterRel exists)   

		InactiveOrProposedConsolidatedItemMasterExists
			restricted 
			when (InactiveOrProposedConsolidatedItemMasterRel exists)

		BypassRuleForTheseFiles
			restricted
       		when (parentcontext.name = "SourcingEventLineResponse"
       		or    parentcontext.name = "PayablesInvoiceDetail"
       		or    parentcontext.name = "PayablesInvoiceDetailHistory"
       		or    parentcontext.name = "PayablesInvoiceDetailImport"
       		or    parentcontext.name = "MatchInvoiceMessage"
       		or    parentcontext.name = "ContractLine"
			or 	  parentcontext.name = "PurchaseOrderEDIOutput"
			or    parentcontext.name = "RecallProductItem"
			or    parentcontext.name = "PurchaseOrderReceiptLine"
			or 	  parentcontext.name = "VendorReturnLine"
			or    parentcontext.name = "PurchaseOrderLine"
			or	  parentcontext.name = "InventoryTransactionLine")			
    Relations

        ItemGPCsRel
            classic name is ICITEMGPC
            one-to-many relation to ItemGPC
            valid when (HasGpcSegment)
            Field Mapping uses symbolic key
                related.ItemGroup = ItemGroup
            Instance Selection
                where (related.ItemGPC.GPCSegment = GPCSegment
                and   related.ItemGPC.GPCFamily = GPCFamily
                and   related.ItemGPC.GPCClass = GPCClass
                and   related.ItemGPC.GPCBrick = GPCBrick)

        ParentGTINRel
            classic name is PARENTGTIN
            one-to-one relation to ItemGTIN
            delete cascades
            Field Mapping uses Set4
                related.ItemGroup  = ItemGroup
                related.ParentGTIN = ParentGTIN
                related.ItemGTIN   = ItemGTIN
                related.Item       = Item

        VendorItemsRel
            classic name is POITEMVEN
            one-to-many relation to VendorItem
            valid when (HasManufacturerInfo)
            Field Mapping uses Set6
                related.ProcurementGroup = ItemGroup
            Instance Selection
                where (related.Manufacturer.ManufacturerCode = Manufacturer.ManufacturerCode
                and   related.Manufacturer.ManufacturerDivision = Manufacturer.ManufacturerDivision
                and   related.ManufacturerNumber = ManufacturerNumber
                and   related.Item = Item)

		DuplicateGTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup	= ItemGroup
				related.ItemGTIN	= ItemGTIN
			Instance Selection
				where (related.Item	!= Item
				and    related.Active
				and    Active)
		
		SameCodeGTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup	= ItemGroup
				related.ItemGTIN	= ItemGTIN
			Instance Selection
				where (related.Item	!= Item)

		ActiveGTINOnAnotherItemRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set2
				related.ItemGroup	= ItemGroup
				related.ItemGTIN	= ItemGTIN
			Instance Selection
				where (related.Item	!= Item
				and    related.Active)

		DuplicateGTINDetailsRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set7
				related.ItemGroup			= ItemGroup
				related.Manufacturer		= Manufacturer
				related.ManufacturerNumber	= ManufacturerNumber
				related.Item				= Item
				related.UnitOfMeasure		= UnitOfMeasure
			Instance Selection
				where (related.ItemGTIN != ItemGTIN
				and    related.Active
				and    Active)

		DuplicateGTINMfgPerUOMRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set7
				related.ItemGroup			= ItemGroup
				related.Manufacturer		= Manufacturer
				related.ManufacturerNumber	= ManufacturerNumber
				related.Item				= Item
				related.UnitOfMeasure		= UnitOfMeasure
			Instance Selection
				where (related.ItemGTIN != ItemGTIN)

		ActiveDuplicateGTINMfgPerUOMRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set7
				related.ItemGroup			= ItemGroup
				related.Manufacturer		= Manufacturer
				related.ManufacturerNumber	= ManufacturerNumber
				related.Item				= Item
				related.UnitOfMeasure		= UnitOfMeasure
			Instance Selection
				where (related.ItemGTIN != ItemGTIN
				and    related.Active)

		OldDuplicateGTINMfgPerUOMRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set7
				related.ItemGroup			= ItemGroup
				related.Manufacturer		= Manufacturer
				related.ManufacturerNumber	= ManufacturerNumber
				related.Item				= Item
				related.UnitOfMeasure		= old UnitOfMeasure
			Instance Selection
				where (related.ItemGTIN != ItemGTIN)

		DuplicateGTINDetailsLocalRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set7
				related.ItemGroup			= ItemGroup
				related.Manufacturer		= LocalManufacturer
				related.ManufacturerNumber	= LocalManufacturerNumber
				related.Item				= Item
				related.UnitOfMeasure		= UnitOfMeasure
			Instance Selection
				where (related.ItemGTIN != ItemGTIN
				and    related.Active
				and    Active)

		ParentGTINsRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set4
				related.ItemGroup	= ItemGroup
				related.ParentGTIN	= ItemGTIN
				
		ItemUOMRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup     = ItemGroup
				related.Item          = Item
				related.UnitOfMeasure = UnitOfMeasure
				

		ContractLineLowUOMGTINRel
			one-to-many relation to ContractLine
			Field Mapping uses ByGTIN     
				related.ContractGroup			= ItemGroup
			Instance Selection 
				where (related.LowUOMCodeGTIN      	= ItemGTIN
				and    related.LineNotClosed)

		ContractLineLowUOMGTINAllRel
			one-to-many relation to ContractLine
			Field Mapping uses ByGTIN     
				related.ContractGroup			= ItemGroup
			Instance Selection 
				where (related.LowUOMCodeGTIN      	= ItemGTIN)

		ContractLineForLowUOMRel    
            one-to-many relation to ContractLine 
            Field Mapping uses ByItemContractLine
                related.ContractGroup       = ItemGroup 
                related.ItemNumber          = Item  
            Instance Selection 
                where (related.UOM.UnitOfMeasure 	!= UnitOfMeasure
				and    Item.StockUOM                 = UnitOfMeasure
				and    related.Manufacturer      	 = Manufacturer
				and    related.ManufacturerNumber	 = ManufacturerNumber
				and    related.LineNotClosed)    

		ContractLineGTINRel
			one-to-many relation to ContractLine
			Field Mapping uses ByGTIN     
				related.ContractGroup			= ItemGroup
				related.GlobalTradeItemNumber	= ItemGTIN
		ContractLineForItemUOMRel
            one-to-many relation to ContractLine 
            Field Mapping uses ByItemContractLine
                related.ContractGroup       = ItemGroup 
                related.ItemNumber          = Item  
            Instance Selection 
                where (related.UOM.UnitOfMeasure 	= UnitOfMeasure
				and    related.Manufacturer      	= Manufacturer
				and    related.ManufacturerNumber	= ManufacturerNumber
				and    related.LineNotClosed)    
		KeywordShadowRel
			one-to-one relation to KeywordShadow
			Field Mapping uses ByShadowItem
				related.ItemGroup	= ItemGroup
				related.Item		= Item

		RecallProductItemByGTINRel
			one-to-many relation to RecallProductItem
			Field Mapping uses ByGTIN 
				related.RecallGroup		= ItemGroup 
				related.GTIN            = ItemGTIN  

		DefaultGTINPerUOMRel
			one-to-many relation to ItemGTIN
			Field Mapping uses Set3
				related.ItemGroup					= ItemGroup
				related.Item						= Item
				related.UnitOfMeasure				= UnitOfMeasure
			Instance Selection
				where (related.ItemGTIN 	   		!= ItemGTIN
				and    related.Manufacturer			= Manufacturer
				and    related.ManufacturerNumber	= ManufacturerNumber
				and    related.UseAsDefaultPerUOM)

		ConsolidatedItemMasterRel 
			one-to-many relation to ConsolidatedItemMaster 
			Field Mapping uses ByInternalItem 
				related.ContractGroup               = ItemGroup 
				related.InternalItem                = Item 

		InactiveConsolidatedItemMasterRel 
			one-to-many relation to ConsolidatedItemMaster 
			Field Mapping uses ByInternalItem 
				related.ContractGroup               = ItemGroup 
				related.InternalItem                = Item 
			Instance Selection 
				where (related.HasDeletedOrInactiveUnitsOfMeasure)

		InactiveOrProposedConsolidatedItemMasterRel		
			one-to-many relation to ConsolidatedItemMaster 
			Field Mapping uses ByInternalItem 
				related.ContractGroup               = ItemGroup 
				related.InternalItem                = Item 
			Instance Selection 
				where (related.HasInactiveOrProposedUnitsOfMeasure)

    Sets

        Set2
            indexed
            Sort Order
                ItemGroup
                ItemGTIN
                Item

        Set3
            indexed
            Sort Order
                ItemGroup
                Item
                UnitOfMeasure
                ItemGTIN

        Set4
            indexed
            Instance Selection
                where (HasParentGTIN)
            Sort Order
                ItemGroup
                ParentGTIN
                ItemGTIN
                Item

        Set5
            indexed
            Instance Selection
                where (HasUNSPSCSegment)
            Sort Order
                ItemGroup
                UNSPSCCode
                ItemGTIN
                Item

        Set6
            indexed
            Instance Selection
                where (HasGpcSegment)
            Sort Order
                ItemGroup
                GPCSegment
                GPCFamily
                GPCClass
                GPCBrick
                Item
                ItemGTIN

        Set7
            indexed
            Instance Selection
                where (HasManufacturerInfo)
            Sort Order
                ItemGroup
                Manufacturer
                ManufacturerNumber
                Item
                UnitOfMeasure
                ItemGTIN

		FromWorksheet
			Sort Order
				FromWorksheetResult
				ItemGroup
				Item
				ItemGTIN

	Rule Blocks
		ValidateGTIN
			
			if (GTINStructure not entered) 
				constraint (IsValidGTINSize)
					"GTINLengthIsInvalid"


				if (ItemGTIN size = 8)
					default GTINStructure to GTINStructure.GTIN8
				else 
				if (ItemGTIN size = 12)
					default GTINStructure to GTINStructure.GTIN12
				else 
				if (ItemGTIN size = 13)
					default GTINStructure to GTINStructure.GTIN13
				else 
					default GTINStructure to GTINStructure.GTIN14
			else							

				if (GTINStructure.GTIN12)
					constraint (ItemGTIN size = 12)
						"GTINLengthMustBe12ForTheDefinedGTINStructure"
				else
				if (GTINStructure.GTIN13)
					constraint (ItemGTIN size = 13)
						"GTINLengthMustBe13ForTheDefinedGTINStructure"
				else
				if (GTINStructure.GTIN14)
					constraint (ItemGTIN size = 14)
						"GTINLengthMustBe14ForTheDefinedGTINStructure"
			
			initialize GTINCheckDigit
			GTINCheckDigit.ItemGTIN		= ItemGTIN
			LocalValidCheckDigit        = GTINCheckDigit.ValidCheckDigit
			constraint (LocalValidCheckDigit)
				"GTINCheckDigitIsInvalid"


	Field Rules
		ItemGTIN
			constraint (ItemGTIN is numeric)
				"GTINMustBeNumeric"
			constraint (not HasActiveGTINOnAnotherItem)
				"GTINAlreadyExistsForAnotherItem:<first ActiveGTINOnAnotherItemRel.Item>"

			include ValidateGTIN
			
		GTINStructure
			cannot be changed
			
		UnitOfMeasure
			required
				"UnitOfMeasureIsRequired"

			if (ItemGroup.AllowMultipleGTINs)
				if (UseAsDefaultPerUOM changed)
					if (not UseAsDefaultPerUOM)
						if (UnitOfMeasure changed)
							constraint (not HasOldDuplicateGTINPerUOM)
								"NewDefault_\G\T\I\NFor_\U\O\M<old UnitOfMeasure>MustBeSetFirst"
					else
						if (DefaultGTINPerUOMRel exists)
							confirmation required
								"Default_\G\T\I\NForUOM<UnitOfMeasure>WillBeChangedFrom<first DefaultGTINPerUOMRel.ItemGTIN>To<ItemGTIN>.Proceed?"
							invoke FastUpdate DefaultGTINPerUOMRel
								invoked.UseAsDefaultPerUOM = false
				else
					if (UseAsDefaultPerUOM)
						if (UnitOfMeasure changed)
							constraint (not HasOldDuplicateGTINPerUOM)
								"CannotUpdate;_\G\T\I\NIsSetAsTheDefaultFor_\U\O\M<old UnitOfMeasure>"

						if(DefaultGTINPerUOMRel exists)
							confirmation required
								"Default_\G\T\I\NForUOM<UnitOfMeasure>WillBeChangedFrom<first DefaultGTINPerUOMRel.ItemGTIN>To<ItemGTIN>.Proceed?"
							invoke FastUpdate DefaultGTINPerUOMRel
								invoked.UseAsDefaultPerUOM = false
			
		FullDescription
			required
				"DescriptionIsRequired"

		AbbreviatedDescription
			default to FullDescription

		Active
			initial value is true
			if (not Active and Active changed)
				constraint (not HasActiveGTINOnAnotherItem)
					"GTINAlreadyExistsForAnotherItem:<first ActiveGTINOnAnotherItemRel.Item>"
					
			if (ItemGroup.AllowMultipleGTINs
			and Active changed 
			and not Active
			and UseAsDefaultPerUOM)
				constraint (not HasActiveDuplicateGTINPerUOM)
					"CannotInactivate;_\G\T\I\N<ItemGTIN>IsSetAsDefault.PleaseAssignAnotherDefault_\G\T\I\N"

		Manufacturer
			initial value is Item.Manufacturer
			default to Item.Manufacturer
			required
				"ManufacturerInformationIsRequired"

		ManufacturerNumber
			required
				"ManufacturerNumberIsRequired"
			if (not ItemGroup.AllowMultipleGTINs)
				constraint (not HasActiveDuplicateGTINPerUOM)
					"AnotherGTIN<first DuplicateGTINMfgPerUOMRel.ItemGTIN>ExistsWithTheSameManufacturerInformationAndUOM"

		HeightUOM
			if (HeightMeasurement entered)
				required
					"HeightUOMIsRequiredWhenHeightMeasurementIsEntered"
			else
				cannot be entered
					"HeightUOMCanBeEnteredOnlyWhenHeightMeasurementIsEntered"

		WidthUOM
			if (WidthMeasurement entered)
				required
					"WidthUOMIsRequiredWhenWidthMeasurementIsEntered"
			else
				cannot be entered
					"WidthUOMCanBeEnteredOnlyWhenWidthMeasurementIsEntered"

		DepthUOM
			if (DepthMeasurement entered)
				required
					"DepthUOMIsRequiredWhenDepthMeasurementIsEntered"
			else
				cannot be entered
					"DepthUOMCanBeEnteredOnlyWhenDepthMeasurementIsEntered"

		GrossWeightUOM
			if (GrossWeight entered)
				required
					"GrossWeightUOMIsRequiredWhenGrossWeightIsEntered"
			else
				cannot be entered
					"GrossWeightUOMCanBeEnteredOnlyWhenGrossWeightIsEntered"

		NetWeightUOM
			if (NetWeight entered)
				required
					"NetWeightUOMIsRequiredWhenNetWeightIsEntered"
			else
				cannot be entered
					"NetWeightUOMCanBeEnteredOnlyWhenNetWeightIsEntered"

		NetContentUOM
			if (NetContent entered)
				required
					"NetContentUOMIsRequiredWhenNetContentIsEntered"
			else
				cannot be entered
					"NetContentUOMCanBeEnteredOnlyWhenNetContentIsEntered"

		ParentGTIN
			constraint (ParentGTIN != ItemGTIN)
				"ParentGTINCannotBeSameAsGTIN"
			constraint (!ParentGTIN.BaseUnit)
				"ParentGTINCannotBeInBaseUnit"

		BaseUnit
			if (GTINIsParent)
				constraint (!BaseUnit)
					"CannotSetBaseUnitWhenGTINHasAChildGTIN"

		UseAsDefaultPerUOM
			if (Active and DefaultGTINPerUOMRel not exists and !FromNewConsolidatedUOM)
				default to true

			if (ItemGroup.AllowMultipleGTINs
			and (action type.Create or UseAsDefaultPerUOM changed)
			and  UseAsDefaultPerUOM)
				constraint (Active)
					"Inactive_\G\T\I\NCannotBeSetAsDefault"
		
			
	Actions
		Create is a Create Action
			valid when (ItemActive)
			Field Rules
			
				Active 
					default to true

				ManufacturerNumber 
					if (ItemGroup.DefaultManufacturerNumberOnGTIN)
						LocalManufacturer		= Item.Manufacturer
						LocalManufacturerNumber	= Item.ManufacturerNumber
						if (DuplicateGTINDetailsLocalRel !exists)
							default to Item.ManufacturerNumber 
				
			Exit Rules 
                if (ContractLineForItemUOMRel exists)
					for each ContractLineForItemUOMRel 
						if  (each.GlobalTradeItemNumber !entered
						or  (each.GlobalTradeItemNumber entered
						and  each.GlobalTradeItemNumber != ItemGTIN
						and  UseAsDefaultPerUOM = true)) 
							invoke FastUpdateWithoutEdits each 
								invoked.EnterGTIN                = true
								invoked.GTINToEnter              = ItemGTIN
				
				if (ContractLineGTINRel exists)
					for each ContractLineGTINRel
						if (each.SpecialItem)
							invoke FastUpdateWithoutEdits each 
								invoked.DefaultItemFromGTIN = true
								invoked.GTINToEnter         = ItemGTIN 

				
				if (ContractLineLowUOMGTINRel exists)				  
					for each ContractLineLowUOMGTINRel
						if (each.SpecialItem)
						 	invoke FastUpdateWithoutEdits each 
						 		invoked.DefaultItemFromLowUOMCodeGTIN = true 
						 		invoked.LowUOMCodeGTINToEnter         = ItemGTIN 

				if (RecallProductItemByGTINRel exists)
					for each RecallProductItemByGTINRel 
						if (each.Item !entered
						and each.CanUpdateItemOnNotice)
							invoke UpdateItemFromExternal each 
								invoked.ParmItem = Item 

				if (InactiveOrProposedConsolidatedItemMasterExists)
					invoke UpdateUOMDetails ConsolidatedItemMasterRel 
						invoked.ParmGTIN		= ItemGTIN 
						invoked.ParmAdded       = true  				
				
				invoke BuildTextIndex Item	

		UpdateActiveFromItem is an Instance Action
			restricted
			Parameters
				SetToActive        				is Boolean
				SetToInactive      				is Boolean 
				SetUseAsDefaultPerUOMToFalse	is Boolean
			Action Rules
			
				LocalFromUpdateActiveFromItem = true 
				if (SetToActive)
					Active = true
				if (SetToInactive)
					Active = false
				if (SetUseAsDefaultPerUOMToFalse)
					UseAsDefaultPerUOM = false
		
		UpdateContractLineForAttributeUpdate is a Set Action 
			restricted 
			Parameters 
				ParmItemGroup	is an ItemGroup 

			Instance Selection 
				where (Active = true)

			Action Rules
				Instance Rules 

					if (ContractLineForItemUOMRel exists)
						for each ContractLineForItemUOMRel 
							if  (each.GlobalTradeItemNumber !entered
							or  (each.GlobalTradeItemNumber entered
							and  each.GlobalTradeItemNumber != ItemGTIN
							and  UseAsDefaultPerUOM = true 
							and  ItemGTIN.Active))
								invoke FastUpdateWithoutEdits each 
									invoked.EnterGTIN                = true
									invoked.GTINToEnter              = ItemGTIN
				
					if (ContractLineGTINRel exists)
						for each ContractLineGTINRel
							if (each.SpecialItem)
								invoke FastUpdateWithoutEdits each 
									invoked.DefaultItemFromGTIN = true
									invoked.GTINToEnter         = ItemGTIN 

		Update is an Update Action
		
			Action Rules

				if (ItemGroup.AllowMultipleGTINs
				and UseAsDefaultPerUOM changed
				and not UnitOfMeasure changed
				and HasDuplicateGTINPerUOM)
					constraint (not old UseAsDefaultPerUOM)
						"NewDefault_\G\T\I\NFor_\U\O\M<UnitOfMeasure>MustBeSetFirst"

				if ((Manufacturer changed
				or  ManufacturerNumber changed)
				and ContractLineGTINRel exists)
					for each ContractLineGTINRel
						invoke FastUpdateWithoutEdits each
							invoked.UpdateManufacturerInformation					= true						
							invoked.NewManufacturer			= Manufacturer			
							invoked.NewManufacturerNumber	= ManufacturerNumber						
				
				if (UnitOfMeasure changed)
					if (ContractLineGTINRel exists)
						for each ContractLineGTINRel
							invoke FastUpdateWithoutEdits each 
								invoked.EnterGTIN           = true
								invoked.GTINToEnter         = blank 		

					if (ContractLineLowUOMGTINRel exists)
						for each ContractLineLowUOMGTINRel 
							if (each.SpecialItem)
								invoke FastUpdateWithoutEdits each 
									invoked.LowUOMCodeGTINToEnter	= blank
									invoked.EnterLowUOMCodeGTIN     = true 								

					if (ContractLineForItemUOMRel exists)
						for each ContractLineForItemUOMRel 
							if  (each.GlobalTradeItemNumber !entered
							or  (each.GlobalTradeItemNumber entered
							and  each.GlobalTradeItemNumber != ItemGTIN
							and  UseAsDefaultPerUOM = true 
							and  ItemGTIN.Active))
								invoke FastUpdateWithoutEdits each 
									invoked.EnterGTIN           = true
									invoked.GTINToEnter         = ItemGTIN


				if (Active changed)
					if (IsActive)
						constraint (ItemActive)
							"CannotSetGTINToActiveWhenItemIsInactive"
						for each ContractLineGTINRel
							invoke RemoveInactiveGTINError each
						if (InactiveConsolidatedItemMasterExists)
							invoke UpdateUOMDetails first ConsolidatedItemMasterRel 
								invoked.ParmGTIN		= ItemGTIN 
								invoked.ParmActivated 	= true  	
						if (ActiveDuplicateGTINMfgPerUOMRel !exists)
							UseAsDefaultPerUOM          = true										
					if (IsInactive)
						for each ContractLineGTINRel
							invoke CreateInactiveGTINError each
						if (ConsolidatedItemMasterExists)
							invoke UpdateUOMDetails first ConsolidatedItemMasterRel 
								invoked.ParmGTIN		= ItemGTIN 
								invoked.ParmUOM         = UnitOfMeasure.UnitOfMeasure
								invoked.ParmInactivated = true  		
						if (UseAsDefaultPerUOM)
							UseAsDefaultPerUOM              = false
							if (ItemGroup.AllowMultipleGTINs)
								if (ActiveDuplicateGTINMfgPerUOMRel exists)
									constraint (NumberOfActiveMatchingGTINs = 1)
										"NewDefault_\G\T\I\NFor_\U\O\M<UnitOfMeasure>MustBeSetFirst" 	
									if (NumberOfActiveMatchingGTINs = 1)
										invoke Update first ActiveDuplicateGTINMfgPerUOMRel
											invoked.UseAsDefaultPerUOM = true 	

			Exit Rules 
			
				invoke BuildTextIndex Item
		
		FastUpdate is an Update Action
			restricted
			bypass field rules

		UpdateManufacturerInfo is an Instance Action			
			restricted											
			Parameters											
				PrmManufacturer 		is a Manufacturer		
				PrmManufacturerNumber	is a ManufacturerNumber	
			Action Rules										
				
				if (PrmManufacturer entered)
					LocalManufacturer  		= PrmManufacturer
				else
					LocalManufacturer       = Manufacturer
				if (PrmManufacturerNumber entered)
					LocalManufacturerNumber = PrmManufacturerNumber
				else
					LocalManufacturerNumber = ManufacturerNumber
					
				if (DuplicateGTINDetailsLocalRel !exists)
					if (PrmManufacturer entered)					
						Manufacturer 		= PrmManufacturer		
					if (PrmManufacturerNumber entered)				
						ManufacturerNumber	= PrmManufacturerNumber

		Delete is a Delete Action
			Entrance Rules
				if (ItemGroup.AllowMultipleGTINs
				and HasDuplicateGTINPerUOM)
						constraint (not UseAsDefaultPerUOM)
							"CannotDelete;_\G\T\I\N<ItemGTIN>IsSetAsDefault.PleaseAssignAnotherDefault_\G\T\I\N"

				if (ContractLineGTINRel exists)
					for each ContractLineGTINRel 
						invoke FastUpdateWithoutEdits each 
							invoked.EnterGTIN	= true 
							invoked.GTINToEnter = blank	

				if (ContractLineLowUOMGTINAllRel exists)
					for each ContractLineLowUOMGTINAllRel 
						invoke FastUpdateWithoutEdits each 
							invoked.LowUOMCodeGTINToEnter	= blank
							invoked.EnterLowUOMCodeGTIN     = true 	

				if (ConsolidatedItemMasterExists)
					invoke UpdateUOMDetails first ConsolidatedItemMasterRel 
						invoked.ParmGTIN		= ItemGTIN 
						invoked.ParmUOM         = UnitOfMeasure.UnitOfMeasure
						invoked.ParmDeleted     = true  
				
				invoke Update KeywordShadowRel
					initialize invoked.ItemGTIN

			Exit Rules
				invoke BuildTextIndex Item

		SetDefaultGTINPerUOM is a Set Action
			restricted
			Action Rules
				Instance Rules
					invoke FastUpdate
						invoked.UseAsDefaultPerUOM = true

	Action Exit Rules
		invoke ItemDetailChanged Item 		

		if  (ConsolidatedItemMasterExists
		and  not LocalFromUpdateActiveFromItem)
			invoke Update first ConsolidatedItemMasterRel    		
