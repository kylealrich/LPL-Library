PreferenceCardProvider is a BusinessClass
    owned by ic
    prefix is PCP

    Ontology
        symbolic key is PreferenceCardProvider

    Patterns

        implements StaticJava
        implements DynamicCreation

    Persistent Fields
        Provider
        Active is Boolean

    Derived Fields
        ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforPreferenceCardProvider"

        ContextMessageText is a MessageField
            restricted
            "PreferenceCardProvider<PreferenceCardProvider>"


        ActiveMessage is a LabelField
            "Active"
        
        InactiveMessage is a LabelField
            "Inactive"

        ActiveDisplay is a DerivedField
            type is MessageField
            default label is "Active"
            if (Active)
                return ActiveMessage
            else
                return InactiveMessage
        
        InactiveProviderMessage is a MessageField
            "ProviderIsInactive"

        ProviderNotEnteredMessage is a MessageField
            restricted
            "ProviderIsRequired"

        ProviderDoesNotExistMessage is a MessageField
            restricted
            "ProviderDoesNotExist"

        ProviderAlertDisplayMessage is a DerivedField
            type is MessageField
            if (Provider not entered)
                return ProviderNotEnteredMessage
            else
                if (Provider not exists)
                    return ProviderDoesNotExistMessage
                else
                    if (not Provider.Active)
                        return InactiveProviderMessage

    Relations
        PreferenceCardProviderErrorRel
            one-to-many relation to PreferenceCardError
            Field Mapping uses symbolic key
                related.ItemGroup                       = ItemGroup
                related.PreferenceCard                  = PreferenceCardProvider.PreferenceCard
            Instance Selection
                where (related.ErrorRecordType.Provider
                and    related.ErrorRecordNumber        = PreferenceCardProvider)

        SameProviderRel
            one-to-many relation to PreferenceCardProvider
            Field Mapping uses ByProvider
                related.ItemGroup                       = ItemGroup
                related.Provider                        = Provider
                related.PreferenceCard                  = PreferenceCardProvider.PreferenceCard
            Instance Selection
                where (related.PreferenceCardProvider   != PreferenceCardProvider)
                

    Sets
        ByProvider
            duplicates
            Sort Order
                ItemGroup
                Provider
                PreferenceCard

    Rule Blocks
        CreateOrUpdateEdits
            invoke Purge PreferenceCardProviderErrorRel

            if (Provider not entered)
                invoke Create PreferenceCardError
                    fill in fields from this instance
                    invoked.ErrorRecordType                         = 3 
                    invoked.ErrorMessageNumber                      = 11
                    invoked.ErrorPreferenceCard                     = PreferenceCard
                    invoked.ErrorRecordNumber                       = PreferenceCardProvider
            else 
                if (Provider not exists)
                    invoke Create PreferenceCardError
                        fill in fields from this instance
                        invoked.ErrorRecordType                     = 3 
                        invoked.ErrorMessageNumber                  = 12
                        invoked.ErrorPreferenceCard                 = PreferenceCard
                        invoked.ErrorRecordNumber                   = PreferenceCardProvider
                        invoked.ErrorProvider                       = Provider
                else 
                    if (SameProviderRel exists)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType                 = 3 
                            invoked.ErrorMessageNumber              = 13
                            invoked.ErrorPreferenceCard             = PreferenceCard
                            invoked.ErrorRecordNumber               = PreferenceCardProvider
                            invoked.ErrorProvider                   = Provider

                    if (not Provider.Active)
                        invoke Create PreferenceCardError
                            fill in fields from this instance
                            invoked.ErrorRecordType                 = 3 
                            invoked.ErrorMessageNumber              = 14
                            invoked.ErrorPreferenceCard             = PreferenceCard
                            invoked.ErrorRecordNumber               = PreferenceCardProvider
                            invoked.ErrorProvider                   = Provider

    Conditions
        ValidRecordExists
            restricted
            when (PreferenceCardProvider exists
            and   ValidProviderExists)

        ValidProviderExists
            restricted
            when (Provider entered
            and   Provider exists)

        DisplayAlert
            restricted
            when (not ValidProviderExists
            or    not Provider.Active)

    Field Rules

    Actions
        Create is a Create Action
            Exit Rules
                include CreateOrUpdateEdits

        Update is an Update Action
            Entrance Rules
                include CreateOrUpdateEdits

        Delete is a Delete Action
            Action Rules
                invoke Purge PreferenceCardProviderErrorRel

        ReviewErrors is an Instance Action
            restricted
            Action Rules
                include CreateOrUpdateEdits
