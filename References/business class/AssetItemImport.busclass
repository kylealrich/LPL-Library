AssetItemImport is a BusinessClass
    owned by am
    prefix is ITC
    classic name is AMASTITCNV

    Ontology
        symbolic key is AssetItemImport
            classic set name is ITCSET1
            classic name is SEQ-NUMBER
            classic name for AssetImport is CONVERSION-NBR

    Patterns
        implements ContextualParent
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
		RunGroup
        RecordInError                 		is Boolean
		ErrorMessage						is Alpha 150
		Asset
		AssetItem
		AssetManagementInterfaceResult

        ItemNumber          is an AssetItemNumber
            classic name is ITEM-NBR
        Description
        PurchaseDate        is Date
        ItemQuantity
        BaseItemCost        is an InternationalAmount
            classic name is ITEM-COST-BASE
        BaseItemTax         is an InternationalAmount
            classic name is ITEM-TAX-BASE
        TransactionItemCost is an InternationalAmount
            classic name is ITEM-COST-TRAN
        TransactionItemTax  is an InternationalAmount
            classic name is ITEM-TAX-TRAN
        InvoiceCompany      is a PayablesCompany
        Vendor
        PayablesInvoice
        PayablesInvoiceDistribution
        PurchaseOrder

        PONumber			is like PONumber
            classic name is PO-NBR
        PORelease			is like PORelease
        POCode				is like POCode
        VendorName 
        Invoice				is like Invoice 

        ModelNumber		
        SerialNumber
        Project				is a FinanceCodeBlockProjectOnly
            classic name is ACTIVITY
        LocationDetail      is an AssetLocationDetail
            classic name is ITEM-LOC-DTL
        BarCode             is an AssetBarCode
        InventoryDate       is TimeStamp
            classic name is INV-DATE
        ItemCondition
            classic name is INV-ITEM-COND
		AssetItemReferenceFields


	Transient Fields
		AssetCurrency	is a FromCurrency
			derive value from AssetImport.Currency
			
	Local Fields
    	InterfacedAssetItem				is an AssetItem view
    	LocalFund 						is an AssetFundField
    	LocalBookImportCount			is Numeric size 6
    	LocalBookCount					is Numeric size 6

    Conditions

        IsAstCnvExist
        	restricted
            when (AssetImport exists)

        IsBookCnvExist
        	restricted
            when (first AssetBookImportRel exists)

        IsCnvAstExist
        	restricted
            when (ImportedAssetRel exists)

        IsTranCnvExist
        	restricted
            when (first AssetTransactionImportRel exists)

		FundAccounting
			when (FinanceEnterpriseGroup.FundAccounting
			and not Asset.AssetType.AllocateDepreciationExpense)			
			
		FundAllocationComplete	
			when (sum AssetItemImportFundsRel.PercentContribution = 1)
		
		AssetImportFundsExist
			when (FundAccounting
			and AssetImportFundsRel exists)

		DisplayItemImportFunds
			when (FundAccounting
			and !AssetImportFundsRel exists)	
			
		AssetItemImportFundsExist
			when (AssetItemImportFundsRel exists)			
			
    Relations
        AssetBookImportRel
            classic name is AMASTBKCNV
            one-to-many relation to AssetBookImport
            Field Mapping uses Set1
                related.AssetImport = AssetImport
		
		AssetBookImportErrorsRel
            one-to-many relation to AssetBookImport
            Field Mapping uses Set1
                related.AssetImport = AssetImport
           	Instance Selection
           		where (related.RecordInError = true)  
		
		AssetBookRel
			one-to-many relation to AssetBook
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.Asset					= Asset
        
        AssetTransactionImportRel
            classic name is AMASTTRCNV
            one-to-many relation to AssetTransactionImport
            Field Mapping uses Set1
                related.AssetTransactionImport.ConversionNumber = AssetImport

        ImportedAssetRel
            classic name is AMCNVASSET
            one-to-one relation to ImportedAsset
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup            
                related.AssetImport 			= AssetImport
				related.Asset					= Asset
				
        NewImportedAssetRel
            one-to-many relation to ImportedAsset
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup            
                related.AssetImport 			= AssetImport
                
		AssetItemImportFundsRel 
			one-to-many relation to AssetItemImportFund
			delete cascades
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.AssetImport				= AssetImport
				related.AssetItemImport			= AssetItemImport
				
		AssetImportFundsRel
			one-to-many relation to AssetImportFund
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.AssetImport				= AssetImport
					
		AssetItemRel
			one-to-one relation to AssetItem
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
                related.Asset 					= Asset
                related.AssetItem				= AssetItem
					
				
				
				
    Field Rules
    	RunGroup
    		default to AssetImport.RunGroup

        ItemQuantity
            required

        PurchaseDate
            required

		TransactionItemCost

			
        ItemCondition
            initial value is "G"
            default to "G"

		Vendor
			if (Vendor entered)
				constraint (InvoiceCompany entered)
					"VendorRequiresAPayablesCompanyToBeEntered"
				constraint (PayablesInvoice entered)
					"VendorRequiresAPayablesInvoiceToBeEntered"
						
		PayablesInvoice
			if (PayablesInvoice entered)
				constraint (Vendor entered)
					"PayablesInvoiceRequiredAVendorToBeEntered"	
					

	Sets
		ByPurchaseDateDecending
			Sort Order
				AssetImport
				PurchaseDate			descending	
				AssetItemImport

	Rule Blocks

		InterfaceThisAssetItem

	
			initialize RecordInError
			initialize ErrorMessage

			if (AssetItemRel exists
			and Asset.InterfaceInProgress)
				invoke NoEditUpdate AssetItemRel
					fill in fields from this instance
					invoked.AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
			else
				invoke CreateFromImport AssetItem
					assign result to InterfacedAssetItem
					resume on error
						RecordInError	= true
						ErrorMessage	= error message
					fill in fields from this instance
	
					if (BaseItemCost entered)	
						invoked.PreserveImportedBaseAmount 							= true
					if (BaseItemTax entered)	
						invoked.PreserveImportedBaseTax 							= true					
					invoked.Asset													= AssetImport.Asset
					invoked.AssetManagementInterfaceResult							= PrmAssetManagementInterfaceResult
					invoked.InvoiceAndPurchaseOrderInformation.InvoiceCompany		= InvoiceCompany
					invoked.InvoiceAndPurchaseOrderInformation.Vendor				= Vendor
					invoked.InvoiceAndPurchaseOrderInformation.PayablesInvoice		= PayablesInvoice
					invoked.InvoiceAndPurchaseOrderInformation.DistributionSequence	= PayablesInvoiceDistribution
					invoked.InvoiceAndPurchaseOrderInformation.PurchaseOrder		= PurchaseOrder
					invoked.AssetProject											= Project

			if (!RecordInError)
				Asset							= AssetImport.Asset
				AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
				if (InterfacedAssetItem.AssetItem entered)
					Asset							= AssetImport.Asset
					AssetItem						= InterfacedAssetItem.AssetItem
					AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
					for each AssetItemImportFundsRel
						invoke InterfaceAssetItemFund each
							invoked.PrmAssetManagementInterfaceResult = PrmAssetManagementInterfaceResult





								
			else
				invoke IncrementErrorCount PrmAssetManagementInterfaceResult
				invoke SetRecordInError AssetImport

	Actions
		Create is a Create Action
		Update is an Update Action
		Delete is a Delete Action

		InterfaceAssetItems is a Set Action
			restricted
			completion message is "InterfaceAssetItemsSubmitted"
			Parameters
				PrmRunGroup							is a RunGroup
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
		        PrmFromRange						is like AssetImport
		        PrmToRange							is like AssetImport
				PrmAssetManagementInterfaceResult	is a AssetManagementInterfaceResult

			Instance Selection			
				where (RunGroup							= PrmRunGroup
				and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
				and    AssetImport.Asset				entered
				and   !AssetImport.RecordInError

				and   (PrmFromRange						<= AssetImport
				and   (AssetImport						<= PrmToRange
				or     PrmToRange						not entered)))

			Local Fields
				CommitNow						is Boolean

			Sort Order
				FinanceEnterpriseGroup
				AssetImport
				AssetItemImport
				
			Action Rules

				Instance Rules

					if (PrmAssetManagementInterfaceResult.ErrorCount < PrmAssetManagementInterfaceResult.ErrorLimit)
						include InterfaceThisAssetItem
					
		FixAssetItems is a Set Action
			restricted

			completion message is "FixAssetItemsSubmitted"
			disable checkpoint
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmAssetManagementInterfaceResult	is an AssetManagementInterfaceResult	
			Instance Selection			
				where (RunGroup							not entered
				and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
				and    AssetImport.AssetManagementInterfaceResult = PrmAssetManagementInterfaceResult
				and    AssetImport.Asset				entered
				and   !AssetImport.RecordInError
				and    AssetItem						not entered)

			Sort Order
				FinanceEnterpriseGroup
				AssetImport
				AssetItemImport
				
			Action Rules
				Instance Rules
	    			RunGroup  = AssetImport.RunGroup
					AssetManagementInterfaceResult = PrmAssetManagementInterfaceResult
					
		FastDelete is a Purge Action
			restricted
			bypass relational integrity rules

		DeleteImportedRecords is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmRunGroup				  			is a RunGroup
				PrmAssetManagementInterfaceResult	is an AssetManagementInterfaceResult

			Instance Selection
				where (RunGroup							= PrmRunGroup
                and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
                and    AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
				and    Asset entered
				and   !RecordInError
				and   !AssetImport.RecordInError)
				
			Action Rules
			
				Instance Rules
					if (PrmAssetManagementInterfaceResult.ErrorCount entered
					and PrmAssetManagementInterfaceResult.ErrorLimit entered)
						if (PrmAssetManagementInterfaceResult.ErrorCount <= PrmAssetManagementInterfaceResult.ErrorLimit)


							if ((AssetBookRel exists
							and AssetBookImportErrorsRel not exists)
							or (AssetBookRel not exists
							and AssetBookImportRel not exists))
								invoke Delete AssetItemImportFundsRel
							    invoke FastDelete
					else
						invoke Delete AssetItemImportFundsRel
						invoke FastDelete
						
		ResetAsset is an Instance Action
			restricted
			Action Rules
				Asset		= blank
				AssetItem	= blank
					
