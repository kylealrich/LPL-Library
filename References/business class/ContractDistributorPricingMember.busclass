ContractDistributorPricingMember is a BusinessClass
    owned by po
    prefix is CDPM
    sql name is CMDistributorPricingMember

    Ontology
    	symbolic key is ContractDistributorPricingMember    

	Persistent Fields
		PricingIdentifier				is a ContractDistributorPricing
		Released						is Boolean
		MfgContractTierInfo
				
	Transient Fields
		FromImport                  		is Boolean
		OverrideNetCost             		is Boolean
	
	Local Fields
		LocalMfgContract			is Numeric size 15
		LocalMfgContractLine		is like ContractLine
		LocalCompany				is like InventoryCompany
		LocalContractGroup          is like ContractGroup
		LocalInventoryLocation      is like InventoryLocation
		LocalRequestingLocation     is like RequestingLocation
		LocalPurchaseFrom           is AlphaUpper size 15
		LocalPricingIdentifierChanged is Boolean
		LocalTierChanged            is Boolean
		LocalOldCompany 			is like InventoryCompany
		LocalOldInventoryLocation  	is like InventoryLocation
		LocalOldRequestingLocation 	is like RequestingLocation
		LocalOldPricingGroup        is like PricingGroup
		LocalOldManufacturerContract is like Contract
		FromTierMember              is Boolean
		LocalParticipantGroup		is a ParticipantGroup
		LocalPricingGroup           is like PricingGroup 
			
	Context Fields
		ContractTierMember
			
    Derived Fields
    	CompanyName is a StringField 
     		type is like Name
    		holds pii
			ContractDistributorPricingMember.Company.Name
    	LocationName is a StringField 
     		type is like Name
    		holds pii
			ContractDistributorPricingMember.Location.Name
    	RequestingLocationName is a StringField 
     		type is like Name
    		holds pii
			ContractDistributorPricingMember.RequestingLocation.Name
		PricingGroupDescription is a StringField
			type is Alpha size 60
			ContractDistributorPricingMember.PricingGroup.Description		
		LongCompletionMessage is a MessageField
			restricted
			"UpdateOfContractDistributorPricingMemberComplete;CheckScheduledActionsForCompletionOf'UpdateTierOnLineMembers'SetAction"
		
		ShortCompletionMessage is a MessageField
			restricted
			"UpdateOfContractDistributorPricingMemberComplete"
		
		UpdateCompletionMessage is a DerivedField
			type is Text
			restricted
			if (PricingIdentifier changed
			and MemberHdToLineRel exists)
				return LongCompletionMessage
			else
				return ShortCompletionMessage
				
	Conditions
		CompanyOnly
			when (ContractDistributorPricingMember.Location = blank
			and	  ContractDistributorPricingMember.RequestingLocation = blank
			and   ContractDistributorPricingMember.Company entered)
			
		HasLocOrReqLoc
			restricted
			when (ContractDistributorPricingMember.Location != blank
			or    ContractDistributorPricingMember.RequestingLocation != blank)
			
		HasInventoryLocation
			restricted
			when (ContractDistributorPricingMember.Location entered)
			
		HasRequestingLocation
			restricted
			when (ContractDistributorPricingMember.RequestingLocation entered)
		
		HasPricingGroup
			restricted
			when (ContractDistributorPricingMember.PricingGroup entered)
		
		LocationInactive
			restricted
			when (HasInventoryLocation
			and   !InventoryLocationRel.IsActive)
			
		RequestingLocationInactive
			restricted
			when (HasRequestingLocation
			and   ContractDistributorPricingMember.RequestingLocation.Active = false)
			
		PricingGroupInactive
			restricted
			when (HasPricingGroup
			and   ContractDistributorPricingMember.PricingGroup.Active = false)
		
		LocationType
			restricted
			when (HasLocOrReqLoc
			or    ContractDistributorPricingMember.PricingGroup.LocationPricingGroup)
		CompanyType
			restricted
			when (CompanyOnly
			or    ContractDistributorPricingMember.PricingGroup.CompanyPricingGroup)
		
		UnReleased
			restricted
			when (!Released)
			
		MemberReleased
			restricted
			when (Released)
			
		FromMfgContract
			restricted
			when (MfgContractTierInfo entered)
			
		TierMismatch
			restricted
			when (MfgContractTierInfo.Tier != TierMemberRel.Tier)

		ManufacturerContractNotClosed
			restricted
			when (!MfgContractTierInfo.MfgContract.ContractStatus.Closed)
			
		ContractNotClosed
			restricted
			when (Contract.NotClosed)
		
		CanUpdate
			restricted
			when (ManufacturerContractNotClosed
			and   Contract.InProgressOrActive)
		
		CanCreate
			restricted 
			when  (Contract.IsDistributorContract
			and    Contract.InProgressOrActive)
		
		NeedsDistributorMember
			restricted
			when  (Contract.InProgress
			and   Contract.IsDistributorContract
			and   FromMfgContract
			and   !PricingMemberForDistRelExists
			and   DistOnlyContractLineRelExists)
			
		ManufacturerMemberNotReleased
			restricted
			when (!TierMemberRel.Released)

		ManufacturerMemberReleased
			restricted
			when (TierMemberRel.Released)

		NoOtherUnreleasedMembers
			restricted
			when (OtherManufacturerPricingMemberUnreleasedRel !exists)
		
		PricingMemberForDistRelExists
			restricted
			when (PricingMemberForDistRel exists)
			
		HasLinesForManufacturerContract
			restricted
			when (ManufacturerContractLineRel exists)

		DistOnlyContractLineRelExists
			restricted
			when (DistOnlyContractLineRel exists)
							
	Sets
		ByLocReqLocSet
      		Sort Order
           		ContractGroup
            	Contract
            	ContractDistributorPricingMember.Company
            	ContractDistributorPricingMember.Location
            	ContractDistributorPricingMember.RequestingLocation
            	ContractDistributorPricingMember.PricingGroup
            	ContractDistributorPricingMember.ManufacturerContract
            Instance Selection
            	where (HasLocOrReqLoc)

		ByPricingID
			duplicates
      		Sort Order
           		ContractGroup
            	Contract
            	PricingIdentifier

		ByMfgTier
			duplicates		
      		Sort Order
           		ContractGroup
            	MfgContractTierInfo.MfgContract
            	ContractDistributorPricingMember.Company
            	ContractDistributorPricingMember.Location
            	ContractDistributorPricingMember.RequestingLocation
            	ContractDistributorPricingMember.PricingGroup
            	ContractDistributorPricingMember.ManufacturerContract
            	MfgContractTierInfo.Tier
            	Contract

		ByUnReleased
			duplicates
      		Sort Order
           		ContractGroup
            	Contract
            	MfgContractTierInfo.MfgContract
            	ContractDistributorPricingMember.Company
            	ContractDistributorPricingMember.Location
            	ContractDistributorPricingMember.RequestingLocation
            	ContractDistributorPricingMember.PricingGroup
            	ContractDistributorPricingMember.ManufacturerContract
            	MfgContractTierInfo.Tier
            Instance Selection
            	where (UnReleased)

		ByReleased
			duplicates
      		Sort Order
           		ContractGroup
            	Contract
            Instance Selection
            	where (MemberReleased)
            	
        ByPricingGroup
        	Sort Order
        		ContractGroup
        		ContractDistributorPricingMember.PricingGroup
        		Contract
        		ContractDistributorPricingMember.ManufacturerContract
        	Instance Selection
        		where (HasPricingGroup)
            	
		ByCompanyFirst 
			Sort Order 
				ContractGroup 
				ContractDistributorPricingMember.Company
				ContractDistributorPricingMember.Location
				ContractDistributorPricingMember.RequestingLocation
				ContractDistributorPricingMember.PricingGroup 
				ContractDistributorPricingMember.ManufacturerContract 
				Contract 

		ByLocation
			Sort Order
				ContractGroup
				Contract
            	ContractDistributorPricingMember.Company
            	ContractDistributorPricingMember.Location
            	ContractDistributorPricingMember.RequestingLocation				
				ContractDistributorPricingMember.ManufacturerContract
			Instance Selection
				where (!HasPricingGroup)

    Relations
    	CompanyOnlyRel
    		one-to-one relation to ContractDistributorPricingMember
            Field Mapping uses symbolic key
          		related.ContractGroup											= ContractGroup
           		related.Contract												= Contract
           		related.ContractDistributorPricingMember.Company				= ContractDistributorPricingMember.Company
           		related.ContractDistributorPricingMember.Location				= blank
           		related.ContractDistributorPricingMember.RequestingLocation		= blank
           		related.ContractDistributorPricingMember.PricingGroup           = blank
           		related.ContractDistributorPricingMember.ManufacturerContract	= ContractDistributorPricingMember.ManufacturerContract
          		
		LocalPricingGroupRel 
			one-to-one relation to PricingGroup 
			Field Mapping uses symbolic key 
				related.ContractGroup   = ContractGroup
				related.PricingGroup    = LocalPricingGroup		

    	OldPricingMemberRel
    		one-to-one relation to ContractDistributorPricingMember
            Field Mapping uses symbolic key
          		related.ContractGroup											= ContractGroup
           		related.Contract												= Contract
           		related.ContractDistributorPricingMember.Company				= LocalOldCompany
           		related.ContractDistributorPricingMember.Location				= LocalOldInventoryLocation
           		related.ContractDistributorPricingMember.RequestingLocation		= LocalOldRequestingLocation
           		related.ContractDistributorPricingMember.PricingGroup           = LocalOldPricingGroup
           		related.ContractDistributorPricingMember.ManufacturerContract	= LocalOldManufacturerContract

		CompanyActiveLocationRel 
			one-to-many relation to InventoryLocation 
			Field Mapping uses symbolic key 
				related.Company 									= ContractDistributorPricingMember.Company 
			Instance Selection 
				where (related.Status != 1)	

		CompanyActiveRequestingLocationRel 
			one-to-many relation to RequestingLocation 
			Field Mapping uses symbolic key 
				related.Company 									= ContractDistributorPricingMember.Company 
			Instance Selection 
				where (related.Active)

		LocalInventoryCompanyRel 
			one-to-one relation to InventoryCompany 
			Field Mapping uses symbolic key 
				related.Company 		= LocalCompany

		LocalContractLocationRel 
			one-to-one relation to ContractAndLineLocation
			Field Mapping uses ByLocationOnly 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
				related.ContractAndLineLocation.Company				= ContractDistributorPricingMember.Company
				related.ContractAndLineLocation.Location 			= LocalInventoryLocation
	
		LocalContractRequestingLocationRel 
			one-to-one relation to ContractAndLineLocation
			Field Mapping uses ByRequestingLocationOnly 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
				related.ContractAndLineLocation.Company				= ContractDistributorPricingMember.Company
				related.ContractAndLineLocation.RequestingLocation  = LocalRequestingLocation

		OtherManufacturerPricingMemberUnreleasedRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup                                           = ContractGroup
				related.Contract                                                = Contract
			Instance Selection
				where (related.UnReleased
				and    related.MfgContractTierInfo.MfgContract                 != MfgContractTierInfo.MfgContract)

		ParticipantGroupRel
			one-to-one relation to ParticipantGroup
			Field Mapping uses symbolic key
				related.ProcurementGroup 				= LocalContractGroup
				related.ParticipantGroup				= LocalParticipantGroup

    	LocReqLocRel
    		one-to-many relation to ContractDistributorPricingMember
            Field Mapping uses ByLocReqLocSet
          		related.ContractGroup								 = ContractGroup
           		related.Contract									 = Contract
           		related.ContractDistributorPricingMember.Company	 = ContractDistributorPricingMember.Company
           	Instance Selection
           		where (related.ContractDistributorPricingMember.ManufacturerContract = LocalMfgContract)
    
    	OtherContractDistributorPricingMemberRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup = ContractGroup
				related.Contract      = Contract
    		Instance Selection
    			where (related.UniqueID  != UniqueID
    			and    related.ContractDistributorPricingMember.ManufacturerContract = LocalMfgContract
    			and    related.ContractDistributorPricingMember.Company              = ContractDistributorPricingMember.Company)
    	
    	PricingMemberForDistRel
    		one-to-one relation to ContractDistributorPricingMember
            Field Mapping uses ByLocReqLocSet
          		related.ContractGroup											= ContractGroup
           		related.Contract												= Contract
           		related.ContractDistributorPricingMember.Company	 			= ContractDistributorPricingMember.Company
           		related.ContractDistributorPricingMember.Location	 			= ContractDistributorPricingMember.Location
           		related.ContractDistributorPricingMember.RequestingLocation	    = ContractDistributorPricingMember.RequestingLocation
           		related.ContractDistributorPricingMember.PricingGroup           = blank
           		related.ContractDistributorPricingMember.ManufacturerContract 	= Contract
    		
    	ManufacturerContractLineRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgContractLine
				related.ContractGroup        = ContractGroup
				related.Contract             = Contract
				related.ManufacturerContract = ContractDistributorPricingMember.ManufacturerContract    		
    	
    	ManufacturerContractRel
			one-to-many relation to ManufacturerContract
			Field Mapping uses ByPivotId
				related.ContractGroup        		= ContractGroup
				related.Contract             		= Contract
				related.ManufacturerContractNumber 	= ContractDistributorPricingMember.ManufacturerContract  

    	DistOnlyContractLineRel
			one-to-many relation to ContractLine
			Field Mapping uses ByMfgContractLine
				related.ContractGroup        = ContractGroup
				related.Contract             = Contract
				related.ManufacturerContract = Contract
    	
    	MemberToPricingRel
    		one-to-one relation to ContractDistributorPricing
            Field Mapping uses symbolic key
          		related.ContractGroup				= ContractGroup
           		related.Contract					= Contract
           		related.ContractDistributorPricing	= PricingIdentifier
    
    	DefaultPricingRel
    		one-to-many relation to ContractDistributorPricing
    		Field Mapping uses ByDefault
    			related.ContractGroup				= ContractGroup
           		related.Contract					= Contract
    	
    	InventoryLocationRel
    		one-to-one relation to InventoryLocation
    		Field Mapping uses symbolic key
    			related.Company                                 = ContractDistributorPricingMember.Company
    			related.InventoryLocation                       = ContractDistributorPricingMember.Location
    	
    	MemberToTierRel
    		one-to-one relation to ContractTier
            Field Mapping uses symbolic key
          		related.ContractGroup = ContractGroup
           		related.Contract	  = MfgContractTierInfo.MfgContract
           		related.ContractTier  = MfgContractTierInfo.Tier          		
          		    
    	MemberHdToLineRel
    		one-to-many relation to ContractLineMember
            Field Mapping uses symbolic key
          		related.ContractGroup						  = ContractGroup
           		related.Contract							  = Contract
           	Instance Selection
           		where (related.ContractLineMember.Company	  			= ContractDistributorPricingMember.Company
           		and    related.ContractLineMember.Location  			= ContractDistributorPricingMember.Location           		
           		and    related.ContractLineMember.RequestingLocation	= ContractDistributorPricingMember.RequestingLocation 
           		and    related.ContractLineMember.PricingGroup          = ContractDistributorPricingMember.PricingGroup
           		and    related.ContractLineMember.ManufacturerContract 	= ContractDistributorPricingMember.ManufacturerContract) 

    	ContractRel
    		one-to-one relation to Contract
            Field Mapping uses symbolic key
          		related.ContractGroup = ContractGroup
           		related.Contract      = ContractDistributorPricingMember.ManufacturerContract 
           		
           		
		TierMemberRel
			one-to-one relation to ContractTierMember
			Field Mapping uses symbolic key
				related.ContractGroup 				  			= ContractGroup
				related.Contract      				  			= ContractDistributorPricingMember.MfgContractTierInfo.MfgContract
           		related.ContractTierMember.Company  			= ContractDistributorPricingMember.Company
           		related.ContractTierMember.Location 			= ContractDistributorPricingMember.Location
           		related.ContractTierMember.RequestingLocation	= ContractDistributorPricingMember.RequestingLocation
				related.ContractTierMember.PricingGroup         = ContractDistributorPricingMember.PricingGroup
				
    	MfgContractLineRel
    		one-to-one relation to ContractLine
            Field Mapping uses symbolic key
          		related.ContractGroup = ContractGroup
           		related.Contract	  = LocalMfgContract
           		related.ContractLine  = LocalMfgContractLine
           		
        PurchaseOrderLineByLocationRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses ByContractCompanyLocation
				related.ProcurementGroup                = ContractGroup
				related.Contract                        = Contract
				related.Company                         = ContractDistributorPricingMember.Company
				related.ShipToLocation                  = ContractDistributorPricingMember.Location
				
		PurchaseOrderLineByCompanyRel
			one-to-many relation to PurchaseOrderLine
			Field Mapping uses ByContractCompanyLocation
				related.ProcurementGroup                = ContractGroup
				related.Contract                        = Contract
				related.Company                         = ContractDistributorPricingMember.Company

		DirectPurchaseFromRel
			one-to-one relation to ContractPurchaseFrom
			Field Mapping uses symbolic key
				related.ContractGroup 													= ContractGroup
				related.Contract														= Contract
				related.ContractPurchaseFrom.ParticipantLocation.Company  				= ContractDistributorPricingMember.Company
				related.ContractPurchaseFrom.ParticipantLocation.Location 				= ContractDistributorPricingMember.Location
				related.ContractPurchaseFrom.ParticipantLocation.RequestingLocation		= ContractDistributorPricingMember.RequestingLocation
				related.ContractPurchaseFrom.ParticipantLocation.PricingGroup           = blank
				
		PurchaseFromRel
			one-to-one relation to ContractPurchaseFrom
			Field Mapping uses symbolic key
				related.ContractGroup 													= ContractGroup
				related.Contract														= Contract
				related.ContractPurchaseFrom.ParticipantLocation.Company  				= LocalCompany
				related.ContractPurchaseFrom.ParticipantLocation.Location 				= LocalInventoryLocation
				related.ContractPurchaseFrom.ParticipantLocation.RequestingLocation		= LocalRequestingLocation
				related.ContractPurchaseFrom.ParticipantLocation.PricingGroup           = blank

		SupplierPurchaseFromRel
			one-to-many relation to SupplierPurchaseFrom
			Field Mapping uses ByLocationRequestingLocation
				related.SupplierGroup											= ContractGroup
				related.Supplier 												= Contract.Supplier
				related.SupplierPurchaseFrom.ParticipantLocation.Company		= ContractDistributorPricingMember.Company
	        	related.SupplierPurchaseFrom.ParticipantLocation.Location		= ContractDistributorPricingMember.Location
	        	related.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation	= ContractDistributorPricingMember.RequestingLocation
	        Instance Selection
            	where (related.PayablesVendorLocationActive)
            	
		CompanySupplierPurchaseFromRels
			one-to-many relation to SupplierPurchaseFrom
			Field Mapping uses ByCompanyOnly
				related.SupplierGroup											= ContractGroup
				related.Supplier 												= Contract.Supplier
				related.SupplierPurchaseFrom.ParticipantLocation.Company		= LocalCompany
			Instance Selection
            	where (related.PayablesVendorLocationActive)
            	
        SupplierPurchFromRel
        	one-to-many relation to SupplierPurchaseFrom
    		Field Mapping uses symbolic key
    			related.SupplierGroup											= ContractGroup
				related.Supplier 												= Contract.Supplier
				related.SupplierPurchaseFrom.ParticipantLocation.Company		= LocalCompany
				related.SupplierPurchaseFrom.ParticipantLocation.Location		= LocalInventoryLocation
				related.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation	= LocalRequestingLocation
				related.SupplierPurchaseFrom.ParticipantLocation.PricingGroup   = blank
			Instance Selection
				where (related.PayablesVendorLocationActive)
				
		ContractPurchFromRel
			one-to-one relation to ContractPurchaseFrom
			Field Mapping uses ByPurchaseFrom
				related.ContractGroup											= ContractGroup
				related.Contract												= Contract
				related.ContractPurchaseFrom.ParticipantLocation.Company 		= LocalCompany
				related.ContractPurchaseFrom.ParticipantLocation.Location		= LocalInventoryLocation
				related.ContractPurchaseFrom.ParticipantLocation.RequestingLocation	= LocalRequestingLocation
	
		ContractAndLineLocationRel 
			one-to-many relation to ContractAndLineLocation
			Field Mapping uses ByContractOnly 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
				related.ContractAndLineLocation.Company				= ContractDistributorPricingMember.Company
				related.ContractAndLineLocation.Location 			= ContractDistributorPricingMember.Location
				related.ContractAndLineLocation.RequestingLocation 	= ContractDistributorPricingMember.RequestingLocation

		PricingGroupRel
    		one-to-one relation to PricingGroup
    		Field Mapping uses symbolic key
    			related.ContractGroup = ContractGroup
    			related.PricingGroup  = ContractDistributorPricingMember.PricingGroup	
    	
	Rule Blocks
	
		CreateContractPurchaseFrom
			if (HasPricingGroup)
				if (PricingGroupRel.LocationPricingGroup)
					for each PricingGroupRel.PricingGroupRequestingLocationRel
						LocalCompany 			= each.Company
						LocalRequestingLocation	= each.RequestingLocation
						initialize LocalInventoryLocation
						LocalPurchaseFrom   	= first SupplierPurchFromRel.PurchaseFrom
						
						if (LocalPurchaseFrom entered 
						and !PurchaseFromRel exists)
							invoke Create ContractPurchaseFrom 
								invoked.AutoCreate													= true
								invoked.ContractGroup 												= ContractGroup
								invoked.Contract	  												= Contract
								invoked.ContractPurchaseFrom.ParticipantLocation.Company    	  	= LocalCompany
								invoked.ContractPurchaseFrom.ParticipantLocation.RequestingLocation	= LocalRequestingLocation
								invoked.SupplierVendor												= Contract.Vendor
								invoked.PurchaseFrom												= LocalPurchaseFrom
								invoked.AllItemTypes												= first SupplierPurchFromRel.AllItemTypes
					for each PricingGroupRel.PricingGroupLocationRel
						LocalCompany 			= each.Company
						LocalInventoryLocation	= each.InventoryLocation
						initialize LocalRequestingLocation 
						LocalPurchaseFrom 		= first SupplierPurchFromRel.PurchaseFrom
						
						if (LocalPurchaseFrom entered 
						and !PurchaseFromRel exists)	
							invoke Create ContractPurchaseFrom 
								invoked.AutoCreate													= true
								invoked.ContractGroup 												= ContractGroup
								invoked.Contract	  												= Contract
								invoked.ContractPurchaseFrom.ParticipantLocation.Company    	  	= LocalCompany
								invoked.ContractPurchaseFrom.ParticipantLocation.Location			= LocalInventoryLocation
								invoked.SupplierVendor												= Contract.Vendor
								invoked.PurchaseFrom												= LocalPurchaseFrom
								invoked.AllItemTypes												= first SupplierPurchFromRel.AllItemTypes
			
				else	
				if (PricingGroupRel.CompanyPricingGroup)
					for each PricingGroupRel.PricingGroupCompanyRel
						LocalCompany 	= each.Company
						for each CompanySupplierPurchaseFromRels
							LocalInventoryLocation	= each.SupplierPurchaseFrom.ParticipantLocation.Location
							LocalRequestingLocation	= each.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation
							LocalPurchaseFrom   	= each.PurchaseFrom 	
						
							if (LocalPurchaseFrom entered 
							and !ContractPurchFromRel exists)
								invoke Create ContractPurchaseFrom 
									invoked.AutoCreate													= true
									invoked.ContractGroup 												= ContractGroup
									invoked.Contract	  												= Contract
									invoked.ContractPurchaseFrom.ParticipantLocation.Company    	  	= LocalCompany
									invoked.ContractPurchaseFrom.ParticipantLocation.Location			= LocalInventoryLocation
									invoked.ContractPurchaseFrom.ParticipantLocation.RequestingLocation	= LocalRequestingLocation
									invoked.SupplierVendor												= Contract.Vendor
									invoked.PurchaseFrom												= LocalPurchaseFrom
									invoked.AllItemTypes												= each.AllItemTypes
			else
			if (CompanyOnly)		
				LocalCompany = ContractDistributorPricingMember.Company
				for each CompanySupplierPurchaseFromRels 
					LocalInventoryLocation 		= each.SupplierPurchaseFrom.ParticipantLocation.Location
					LocalRequestingLocation		= each.SupplierPurchaseFrom.ParticipantLocation.RequestingLocation
					LocalPurchaseFrom 			= each.PurchaseFrom 	
					
					if (LocalPurchaseFrom entered 
					and !ContractPurchFromRel exists)
						invoke Create ContractPurchaseFrom 
							invoked.AutoCreate													= true
							invoked.ContractGroup 												= ContractGroup
							invoked.Contract	  												= Contract
							invoked.ContractPurchaseFrom.ParticipantLocation.Company  	  		= ContractDistributorPricingMember.Company
							invoked.ContractPurchaseFrom.ParticipantLocation.Location			= LocalInventoryLocation
							invoked.ContractPurchaseFrom.ParticipantLocation.RequestingLocation	= LocalRequestingLocation
							invoked.SupplierVendor												= Contract.Vendor
							invoked.PurchaseFrom												= LocalPurchaseFrom
							invoked.AllItemTypes												= each.AllItemTypes
				
			else 
				LocalPurchaseFrom   	= SupplierPurchaseFromRel.PurchaseFrom
				if (!DirectPurchaseFromRel exists)
					if (SupplierPurchaseFromRel exists)
						invoke Create ContractPurchaseFrom 
							invoked.AutoCreate													= true
							invoked.ContractGroup 												= ContractGroup
							invoked.Contract	  												= Contract
							invoked.ContractPurchaseFrom.ParticipantLocation.Company  	  	    = ContractDistributorPricingMember.Company
							invoked.ContractPurchaseFrom.ParticipantLocation.Location			= ContractDistributorPricingMember.Location
							invoked.ContractPurchaseFrom.ParticipantLocation.RequestingLocation = ContractDistributorPricingMember.RequestingLocation
							invoked.SupplierVendor												= Contract.Vendor
							invoked.PurchaseFrom												= LocalPurchaseFrom
							invoked.AllItemTypes												= SupplierPurchaseFromRel.AllItemTypes		
	
	Field Rules
			
		Released
			default to false
			
		PricingIdentifier
			default to first DefaultPricingRel.ContractDistributorPricing
			
	Create Exit Rules

		if (Contract.EnableClinicalSystemUseLocations
		and !FromMfgContract)
			if (HasInventoryLocation)
				LocalInventoryLocation = ContractDistributorPricingMember.Location
				if (LocalContractLocationRel !exists)
					invoke Create ContractAndLineLocation 
						invoked.ContractGroup 								= ContractGroup 
						invoked.Contract                					= Contract 
						invoked.ContractAndLineLocation.Company 			= ContractDistributorPricingMember.Company
						invoked.ContractAndLineLocation.Location			= ContractDistributorPricingMember.Location 
						invoked.ClinicalSystemUse                   		= true	
			else 
			if (HasRequestingLocation)
				if (ContractGroup.DoNotUseRequestingLocations = false)
					LocalRequestingLocation = ContractDistributorPricingMember.RequestingLocation 
					if (LocalContractRequestingLocationRel !exists)
						invoke Create ContractAndLineLocation
							invoked.ContractGroup 								= ContractGroup 
							invoked.Contract                					= Contract 
							invoked.ContractAndLineLocation.Company 			= ContractDistributorPricingMember.Company
							invoked.ContractAndLineLocation.RequestingLocation	= ContractDistributorPricingMember.RequestingLocation
							invoked.ClinicalSystemUse                   		= true					
			else
			if (CompanyOnly)
				LocalCompany	= ContractDistributorPricingMember.Company
				invoke CreateLocationsForAContractInstance LocalInventoryCompanyRel 
					invoked.ParmContractGroup   		= ContractGroup
					invoked.ParmContract 				= Contract
			else
			if (HasPricingGroup)
				LocalPricingGroup = ContractDistributorPricingMember.PricingGroup
				invoke CreateLocationsForAContractInstance LocalPricingGroupRel 
					invoked.ParmProcurementGroup	= ContractGroup
					invoked.ParmContract            = Contract
	
	Actions
    	Create is a Create Action
    		valid when (CanCreate)
    		Action Rules
          		if (ContractDistributorPricingMember.PricingGroup !entered)
                	constraint (ContractDistributorPricingMember.Company entered)
                   		"EitherCompanyOrPricingGroupIsRequired"				
				if (Contract.ContractStatus.Active)
					confirmation required
						"MustRunReleaseMembersAfterRecordIsAdded"
   		    	LocalMfgContract = ContractDistributorPricingMember.ManufacturerContract 
    			if (!FromImport)
	    			if (OtherContractDistributorPricingMemberRel exists)
		    			if (CompanyType)
		    				constraint (first OtherContractDistributorPricingMemberRel.CompanyType)
		    					"CannotAdd;MembersExistByLocationOrRequestingLocation"
		    			if (LocationType)
		    				constraint (first OtherContractDistributorPricingMemberRel.LocationType)
		    					"CannotAdd;MembersExistForWholeCompany"
				if (Contract.Stockless)
					if (HasLocOrReqLoc)
						constraint (ContractDistributorPricingMember.RequestingLocation.Stockless)
							"MustEnterAStocklessRequestingLocationForAStocklessContract"
					constraint (!HasPricingGroup)
						"CannotEnterAPricingGroupForAStocklessContract" 
    			if (ContractDistributorPricingMember.PricingGroup entered)
    				constraint (!ContractDistributorPricingMember.PricingGroup.NoDetails)
    					"CannotEnter;PricingGroupHasNoCompaniesOrLocationsAttached"
				if (!MfgContractTierInfo.Tier entered)
				   	if (TierMemberRel exists)
				   		MfgContractTierInfo.Tier = TierMemberRel.Tier
				if (ContractDistributorPricingMember.ManufacturerContract !entered)
					ContractDistributorPricingMember.ManufacturerContract = Contract
      					
    		Exit Rules
    			if (Contract.CanCreatePurchaseFroms)
    				include CreateContractPurchaseFrom
    	
    	CreateMemberPurchaseFrom is a Set Action 
			restricted
			Parameters
				PrmContractGroup is a ContractGroup
				PrmContract		 is a Contract
			
			Instance Selection
				where (ContractGroup = PrmContractGroup
				and    Contract 	 = PrmContract)
		
			Action Rules
				Instance Rules
					include CreateContractPurchaseFrom
    	
    	CreateFromGroup is a Create Action    
			restricted
    	
		SetToReleased is a Set Action
			restricted
			Parameters
				PrmContractGroup		is like ContractGroup
				PrmContract         	is like Contract
				PrmManufacturerContract is like Contract
			
			Instance Selection
				where (ContractGroup = PrmContractGroup
				and    Contract 	 = PrmContract
				and    Released      = false
				and   (PrmManufacturerContract !entered
				or    (PrmManufacturerContract entered
				and    PrmManufacturerContract = ContractDistributorPricingMember.ManufacturerContract)))
				
			Action Rules
				Instance Rules
				
					if (FromMfgContract)
						if (ManufacturerContractRel.HasLines)
							Released = true
					else 
						Released = true

    	UpdateTierFromManufacturer is an Instance Action
    		restricted
    		Parameters
    			NewTier        		is like ContractTier
    			ParmOverrideNetCost	is Boolean
				ParmTierModified	is Boolean
    		Action Rules
    		
    			MfgContractTierInfo.Tier = NewTier
    			
				if (MemberHdToLineRel exists)
	    			invoke UpdateTierOnLineMembers ContractLineMember
	    				invoked.PrmContractGroup					= ContractGroup
	    				invoked.PrmContractInFocus					= Contract
	    				invoked.PrmManufacturerContract				= ContractDistributorPricingMember.ManufacturerContract
	    				invoked.PrmTier								= NewTier
	    				invoked.PrmTierCompany						= ContractDistributorPricingMember.Company
	    				invoked.PrmTierLocation						= ContractDistributorPricingMember.Location
	    				invoked.PrmTierReqLocation					= ContractDistributorPricingMember.RequestingLocation
	    				invoked.PrmTierPricingGroup                 = ContractDistributorPricingMember.PricingGroup
	    				invoked.PrmContractDistributorPricingMember	= ContractDistributorPricingMember
						invoked.PrmTierMarkupDiscount 				= MemberToTierRel.TierMarkupDiscount
						invoked.PrmLineTierPricingAmtPct 			= MemberToTierRel.TierPricingAmtPct
						invoked.PrmFreightMarkupDiscount 			= MemberToTierRel.FreightMarkupDiscount
						invoked.PrmLineFreightPricingAmtPct 		= MemberToTierRel.FreightPricingAmtPct
	 					invoked.PrmPricingIdentifier				= PricingIdentifier
						invoked.PrmLineDistributorPricingCode		= MemberToPricingRel.DistributorPricing.PricingMarkupDiscount
                        invoked.PrmLineDistributorPricingAmt	    = MemberToPricingRel.DistributorPricing.DistributorPricingAmtPct.PricingCost
						invoked.PrmLineDistributorPricingPct        = MemberToPricingRel.DistributorPricing.DistributorPricingAmtPct.PricingPercent
						invoked.PrmLineDistributorFreightPricingCode	= MemberToPricingRel.DistributorFreightPricing.FreightPricingMarkupDiscount
						invoked.PrmLineDistributorFreightPricingAmt		= MemberToPricingRel.DistributorFreightPricing.DistributorFreightPricingAmtPct.FreightCost
						invoked.PrmLineDistributorFreightPricingPct		= MemberToPricingRel.DistributorFreightPricing.DistributorFreightPricingAmtPct.FreightPercent
						invoked.PrmOverrideNetCost              	= ParmOverrideNetCost
						invoked.PrmTierModified						= ParmTierModified
    	
    	Update is an Update Action
    		valid when (CanUpdate)
			completion message is "<UpdateCompletionMessage>"
    		Action Rules
				if (!MfgContractTierInfo.Tier entered)
				   	if (TierMemberRel exists)
				   		MfgContractTierInfo.Tier = TierMemberRel.Tier
				   		
    			if (PricingIdentifier changed)
    			    constraint (MemberToPricingRel exists)
    					"ContractDistributorPricingDoesNotExist"
					LocalPricingIdentifierChanged = true

				if (MfgContractTierInfo.Tier changed)
					LocalTierChanged              = true
					
    		Exit Rules
				if ((LocalPricingIdentifierChanged
				or   LocalTierChanged)
				and  MemberHdToLineRel exists)
	    			invoke UpdateTierOnLineMembers ContractLineMember
	    				invoked.PrmContractGroup					= ContractGroup
	    				invoked.PrmContractInFocus					= Contract
	    				invoked.PrmManufacturerContract				= ContractDistributorPricingMember.ManufacturerContract
	    				invoked.PrmTier								= MfgContractTierInfo.Tier
	    				invoked.PrmTierCompany						= ContractDistributorPricingMember.Company
	    				invoked.PrmTierLocation						= ContractDistributorPricingMember.Location
	    				invoked.PrmTierReqLocation					= ContractDistributorPricingMember.RequestingLocation
	    				invoked.PrmTierPricingGroup                 = ContractDistributorPricingMember.PricingGroup
	    				invoked.PrmContractDistributorPricingMember	= ContractDistributorPricingMember
						invoked.PrmTierMarkupDiscount 				= MemberToTierRel.TierMarkupDiscount
						invoked.PrmLineTierPricingAmtPct 			= MemberToTierRel.TierPricingAmtPct
						invoked.PrmFreightMarkupDiscount 			= MemberToTierRel.FreightMarkupDiscount
						invoked.PrmLineFreightPricingAmtPct 		= MemberToTierRel.FreightPricingAmtPct
	 					invoked.PrmPricingIdentifier				= PricingIdentifier
						invoked.PrmLineDistributorPricingCode		= MemberToPricingRel.DistributorPricing.PricingMarkupDiscount
                        invoked.PrmLineDistributorPricingAmt	    = MemberToPricingRel.DistributorPricing.DistributorPricingAmtPct.PricingCost
						invoked.PrmLineDistributorPricingPct        = MemberToPricingRel.DistributorPricing.DistributorPricingAmtPct.PricingPercent
						invoked.PrmLineDistributorFreightPricingCode	= MemberToPricingRel.DistributorFreightPricing.FreightPricingMarkupDiscount
						invoked.PrmLineDistributorFreightPricingAmt		= MemberToPricingRel.DistributorFreightPricing.DistributorFreightPricingAmtPct.FreightCost
						invoked.PrmLineDistributorFreightPricingPct		= MemberToPricingRel.DistributorFreightPricing.DistributorFreightPricingAmtPct.FreightPercent
						if (LocalTierChanged)
							invoked.PrmOverrideNetCost              = OverrideNetCost
						else
						if (LocalPricingIdentifierChanged)
							invoked.PrmOverrideNetCost              = true
					
		DeleteFromTierMember is an Instance Action
			restricted
			Action Rules
			
				FromTierMember = true
				invoke Delete

    	DeleteFromManufacturerContract is an Instance Action
    		restricted
    		Action Rules
    			FromTierMember = true
    			invoke Delete
    	
    	Delete is a Delete Action
    		valid when (CanUpdate)
    		Entrance Rules
    			invoke Delete DirectPurchaseFromRel

				if (Contract.EnableClinicalSystemUseLocations
				and !FromMfgContract)
					if (HasInventoryLocation
					or  HasRequestingLocation)
						invoke DeleteClinical ContractAndLineLocationRel
					else
					if (HasPricingGroup)
						invoke DeleteLocationsForAContract PricingGroup
							invoked.ParmProcurementGroup	= ContractGroup
							invoked.ParmContract            = Contract
							invoked.ParmPricingGroup        = ContractDistributorPricingMember.PricingGroup
					else 
					if (CompanyOnly)
						for each CompanyActiveLocationRel
							LocalInventoryLocation = each.InventoryLocation
							if (LocalContractLocationRel exists) 
								invoke DeleteClinical LocalContractLocationRel
						for each CompanyActiveRequestingLocationRel 													
							LocalRequestingLocation = each.RequestingLocation
							if (LocalContractRequestingLocationRel exists) 
								invoke DeleteClinical LocalContractRequestingLocationRel

    		Action Rules

    			if (!FromTierMember)
	    			invoke MassDelete ContractLineMember
						invoked.ParmContractGroup  					= ContractGroup
						invoked.ParmContract       					= Contract
						invoked.ParmCompany      					= ContractDistributorPricingMember.Company
						invoked.ParmLocation                        = ContractDistributorPricingMember.Location 
						invoked.ParmRequestingLocation              = ContractDistributorPricingMember.RequestingLocation
						invoked.ParmPricingGroup					= ContractDistributorPricingMember.PricingGroup
						invoked.ParmManufacturerContract            = ContractDistributorPricingMember.ManufacturerContract
 		
    	FastUpdateWithoutEdits is an Instance Action
			restricted
			Parameters
				UpdateReleased is AlphaUpper size 1
			Action Rules
				Released = UpdateReleased
					
		ResetContractForDefaulting is an Instance Action
			restricted 
			Action Rules
				invoke CreateAmendment Active Contract
				
		CreateContractAndLineLocationsForRequestingLocationOrLocation is a Set Action 
			restricted 
			Parameters
				PrmContractGroup			is a ContractGroup
				PrmContract					is a Contract
				PrmHasCompanyMembers        is Boolean 
				PrmHasPricingGroups         is Boolean
				
			Instance Selection
				where (ContractGroup		= PrmContractGroup
				and    Contract				= PrmContract
				and   !FromMfgContract
				and   (ContractDistributorPricingMember.Location entered
				or     ContractDistributorPricingMember.RequestingLocation entered))

			Sort Order 
				ContractGroup 
            	Contract
            	ContractDistributorPricingMember.Company
            	ContractDistributorPricingMember.Location
            	ContractDistributorPricingMember.RequestingLocation		

			Action Rules 
				
				Contract Set Rules 
					Exit Rules 

						if (PrmHasCompanyMembers)
							invoke CreateContractAndLineLocationsForCompany
								invoked.PrmContractGroup		= PrmContractGroup 
								invoked.PrmContract             = PrmContract 
								invoked.PrmHasPricingGroups     = PrmHasPricingGroups
						else 
						if (PrmHasPricingGroups)
							invoke CreateContractAndLineLocationsForPricingGroup 
								invoked.PrmContractGroup		= PrmContractGroup 
								invoked.PrmContract             = PrmContract 							
				
				ContractDistributorPricingMember.Location Set Rules

					Entrance Rules 

						if (HasInventoryLocation)
							LocalInventoryLocation = ContractDistributorPricingMember.Location
							if (LocalContractLocationRel !exists)
								invoke Create ContractAndLineLocation 
									invoked.ContractGroup 								= ContractGroup 
									invoked.Contract                					= Contract 
									invoked.ContractAndLineLocation.Company 			= ContractDistributorPricingMember.Company
									invoked.ContractAndLineLocation.Location			= ContractDistributorPricingMember.Location 
									invoked.ClinicalSystemUse                   		= true	

				ContractDistributorPricingMember.RequestingLocation Set Rules 
					Entrance Rules 

						if (HasRequestingLocation)
							LocalRequestingLocation = ContractDistributorPricingMember.RequestingLocation 
							if (LocalContractRequestingLocationRel !exists)
								invoke Create ContractAndLineLocation
									invoked.ContractGroup 								= ContractGroup 
									invoked.Contract                					= Contract 
									invoked.ContractAndLineLocation.Company 			= ContractDistributorPricingMember.Company
									invoked.ContractAndLineLocation.RequestingLocation	= ContractDistributorPricingMember.RequestingLocation
									invoked.ClinicalSystemUse                   		= true				

		CreateContractAndLineLocationsForCompany is a Set Action 
			restricted 
			Parameters
				PrmContractGroup			is a ContractGroup
				PrmContract					is a Contract
				PrmHasPricingGroups         is Boolean
				
			Instance Selection
				where (ContractGroup		= PrmContractGroup
				and    Contract				= PrmContract
				and   !FromMfgContract
				and    ContractDistributorPricingMember.Company entered 
				and    ContractDistributorPricingMember.Location !entered
				and    ContractDistributorPricingMember.RequestingLocation !entered)

			Sort Order 
				ContractGroup 
            	Contract
            	ContractDistributorPricingMember.Company

			Action Rules 
				
				Contract Set Rules 
					Exit Rules 

						if (PrmHasPricingGroups)
							invoke CreateContractAndLineLocationsForPricingGroup 
								invoked.PrmContractGroup		= PrmContractGroup 
								invoked.PrmContract             = PrmContract 	

				ContractDistributorPricingMember.Company Set Rules 

					Entrance Rules 
						for each CompanyActiveLocationRel
							LocalInventoryLocation = each.InventoryLocation 
							if (LocalContractLocationRel !exists)
								invoke Create ContractAndLineLocation 
									invoked.ContractGroup 						= ContractGroup 
									invoked.Contract                			= Contract 
									invoked.ContractAndLineLocation.Company 	= ContractDistributorPricingMember.Company
									invoked.ContractAndLineLocation.Location	= each.InventoryLocation 
									invoked.ClinicalSystemUse                   = true	

						for each CompanyActiveRequestingLocationRel  
							if (each.InventoryTransactionType.Issue)
								LocalRequestingLocation = each.RequestingLocation
								if (LocalContractRequestingLocationRel !exists)
									invoke Create ContractAndLineLocation 
										invoked.ContractGroup 								= ContractGroup 
										invoked.Contract                					= Contract 
										invoked.ContractAndLineLocation.Company 			= ContractDistributorPricingMember.Company
										invoked.ContractAndLineLocation.RequestingLocation	= each.RequestingLocation 
										invoked.ClinicalSystemUse                   		= true	

		CreateContractAndLineLocationsForPricingGroup is a Set Action 
			restricted 
			
			Parameters
				PrmContractGroup			is a ContractGroup
				PrmContract					is a Contract
				
			Instance Selection
				where (ContractGroup		= PrmContractGroup
				and    Contract				= PrmContract
				and    ContractDistributorPricingMember.PricingGroup entered
				and   !FromMfgContract)

			Sort Order 
				ContractGroup 
            	ContractDistributorPricingMember.PricingGroup 
				Contract

			Action Rules 

				ContractDistributorPricingMember.PricingGroup Set Rules 

					Entrance Rules 

						LocalPricingGroup = ContractDistributorPricingMember.PricingGroup
						invoke CreateLocationsForAContractInstance LocalPricingGroupRel 
							invoked.ParmProcurementGroup	= ContractGroup
							invoked.ParmContract            = Contract										
		
		CreatePricingMembersForDirectBuy is a Set Action
			restricted
			Parameters
				PrmContractGroup			is a ContractGroup
				PrmContract					is a Contract
				
			Instance Selection
				where (ContractGroup		= PrmContractGroup
				and    Contract				= PrmContract)
				
			Action Rules
				
				Instance Rules
					
					invoke CreatePricingMemberForDirectBuy ContractDistributorPricingMember
		
		CreatePricingMemberForDirectBuy is an Instance Action 
			valid when (NeedsDistributorMember)
										
			Action Rules
				if (!PricingMemberForDistRel exists) 
					invoke Create ContractDistributorPricingMember
						invoked.ContractGroup            							= ContractGroup
						invoked.Contract                                        	= Contract
						invoked.ContractDistributorPricingMember.Company        	= ContractDistributorPricingMember.Company
						invoked.ContractDistributorPricingMember.Location	 		= ContractDistributorPricingMember.Location
						invoked.ContractDistributorPricingMember.RequestingLocation = ContractDistributorPricingMember.RequestingLocation
						invoked.ContractDistributorPricingMember.PricingGroup       = ContractDistributorPricingMember.PricingGroup
						invoked.ContractDistributorPricingMember.ManufacturerContract = Contract
						invoked.PricingIdentifier                                   = PricingIdentifier
				

		
