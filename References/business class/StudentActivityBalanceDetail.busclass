StudentActivityBalanceDetail is a BusinessClass
    owned by studentactivities
    prefix is SABD

    Ontology
    	symbolic key is StudentActivityBalanceDetail

 	Patterns
		implements ContextualParent
 		implements DynamicCreation

    Persistent Fields
		StudentActivityType
		ParentStudentActivityAccount	is a StudentActivityAccount
		BeginningBalance				is an InternationalAmount
		PeriodReceipts					is an InternationalAmount
		PeriodDisbursements				is an InternationalAmount
		PeriodTransfers					is an InternationalAmount
		BalanceAdjustment				is an InternationalAmount
		BeginningPostedBalance			is an InternationalAmount
		PostedReceipts					is an InternationalAmount
		PostedDisbursements				is an InternationalAmount
		PostedTransfers					is an InternationalAmount
		PostedBalanceAdjustment			is an InternationalAmount

	Transient Fields
		TransactionCurrencyCode			is a Currency
			derive value from StudentActivitySchool.DefaultAccountingEntity.FunctionalCurrency


	Derived Fields
		PeriodEndingBalance is a DerivedField
			type is like InternationalAmount
			return (BeginningBalance + PeriodReceipts - PeriodDisbursements + PeriodTransfers)

		PostedEndingBalance is a DerivedField
			type is like InternationalAmount
			return (BeginningPostedBalance + PostedReceipts + PostedDisbursements + PostedTransfers)

		SchoolActivityDescription is a DerivedField
			type is Alpha 60
			return SchoolActivityAccountRel.Description

	Context Fields
		StudentActivitySchoolType

	Sets
		ByActivityType
			Sort Order
				StudentActivityDistrict
				StudentActivityClosedPeriod
				StudentActivityType
				StudentActivityAccount
				StudentActivitySchool

		ByDescendingDate
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				StudentActivityAccount
				StudentActivityClosedPeriod	descending

		ByDistrictDescendingDate
			Sort Order
				StudentActivityDistrict
				StudentActivityAccount
				StudentActivityClosedPeriod	descending
				StudentActivitySchool

		ByPeriod
			duplicates
			Sort Order
				StudentActivityDistrict
				StudentActivitySchool
				StudentActivityClosedPeriod	descending

		BySchoolActivityType
			duplicates
			Sort Order
				StudentActivityDistrict
				StudentActivityClosedPeriod
				StudentActivitySchool
				StudentActivityType
				StudentActivityAccount
			
		ByParentActivity
			duplicates
			Sort Order
				StudentActivityDistrict
				StudentActivityClosedPeriod
				ParentStudentActivityAccount
				StudentActivitySchool


	Relations
		PreviousStudentActivityBalanceDetailRel
			one-to-many relation to StudentActivityBalanceDetail
			Field Mapping uses ByDescendingDate
				related.StudentActivityDistrict			= StudentActivityDistrict
				related.StudentActivitySchool			= StudentActivitySchool
				related.StudentActivityAccount			= StudentActivityAccount
			Instance Selection
				where (related.StudentActivityClosedPeriod < StudentActivityClosedPeriod)
			
		NextStudentActivityBalanceDetailRel
			one-to-many relation to StudentActivityBalanceDetail
			Field Mapping uses ByDescendingDate
				related.StudentActivityDistrict			= StudentActivityDistrict
				related.StudentActivitySchool			= StudentActivitySchool
				related.StudentActivityAccount			= StudentActivityAccount
			Instance Selection
				where (related.StudentActivityClosedPeriod > StudentActivityClosedPeriod)
			
		SchoolActivityAccountBalanceRel
			one-to-many relation to SchoolActivityAccountBalance
			Field Mapping uses ByStudentActivityAccount
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.StudentActivitySchool							= StudentActivitySchool
				related.StudentActivityClosedPeriod						= StudentActivityClosedPeriod
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount

		SchoolActivityAccountRel
			one-to-one relation to SchoolActivityAccount
			Field Mapping uses symbolic key
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.StudentActivitySchool							= StudentActivitySchool
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount
				related.SchoolActivityAccount.SubActivity				= blank

		StudentActivityReceiptDetailRel
			one-to-many relation to StudentActivityReceiptDetail
			Field Mapping uses ByClosedPeriodBySchool
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.StudentActivitySchool							= StudentActivitySchool
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount
			
		StudentActivityBankReceiptDetailRel
			one-to-many relation to StudentActivityBankTransDetail
			Field Mapping uses ByClosedPeriodBySchool
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.StudentActivitySchool							= StudentActivitySchool
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount
			Instance Selection
				where (!related.BankTransactionType.Transfer
				and     related.StudentActivityBankTransaction.AmountType.BankCredit)

		StudentActivityDisbursementDetailRel
			one-to-many relation to StudentActivityBankTransDetail
			Field Mapping uses ByClosedPeriodBySchool
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.StudentActivitySchool							= StudentActivitySchool
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount
			Instance Selection
				where (!related.BankTransactionType.Transfer
				and     related.StudentActivityBankTransaction.AmountType.BankDebit)
			
		StudentActivityTransferDetailRel
			one-to-many relation to StudentActivityTransferDetail
			Field Mapping uses ByClosedPeriodBySchool
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.StudentActivitySchool							= StudentActivitySchool
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount
			
		StudentActivityBankTransferDetailRel
			one-to-many relation to StudentActivityBankTransDetail
			Field Mapping uses ByClosedPeriodBySchool
				related.StudentActivityDistrict							= StudentActivityDistrict
				related.ClosedPeriod									= StudentActivityClosedPeriod
				related.StudentActivitySchool							= StudentActivitySchool
				related.SchoolActivityAccount.StudentActivityAccount	= StudentActivityAccount
			Instance Selection
				where (related.BankTransactionType.Transfer)









	Conditions
		HasPostedBalances
			when (BeginningPostedBalance	entered
			or    PostedReceipts			entered
			or    PostedDisbursements		entered
			or    PostedTransfers			entered)

		HasBalanceAdjustment
			when (BalanceAdjustment		entered
			and   PreviousStudentActivityBalanceDetailRel exists)
			
		HasPostedBalanceAdjustment
			when (PostedBalanceAdjustment	entered
			and   PreviousStudentActivityBalanceDetailRel exists)
	
		HasReceiptDetails
			when (StudentActivityReceiptDetailRel exists)
			
		HasBankReceiptDetails	
			when (StudentActivityBankReceiptDetailRel exists)

		HasDisbursementDetails
			when (StudentActivityDisbursementDetailRel exists)
			
		HasTransferDetails
			when (StudentActivityTransferDetailRel exists)
		
		HasBankTransferDetails
			when (StudentActivityBankTransferDetailRel exists)

		HasTransactionDetails
			when (HasReceiptDetails
			or    HasBankReceiptDetails
			or    HasDisbursementDetails
			or    HasTransferDetails
			or    HasBankTransferDetails)



			
			
	Field Rules
 		StudentActivityType
  			default to StudentActivityAccount.StudentActivityType

		BeginningBalance
			BeginningBalance	= (first PreviousStudentActivityBalanceDetailRel.PeriodEndingBalance + BalanceAdjustment)

		BeginningPostedBalance
			BeginningPostedBalance	= (first PreviousStudentActivityBalanceDetailRel.PostedEndingBalance + PostedBalanceAdjustment)

		ParentStudentActivityAccount
			default to StudentActivityAccount.ParentStudentActivityAccount
			
	Actions
		Create is a Create Action
			restricted
			Exit Rules
				if (NextStudentActivityBalanceDetailRel exists)
					invoke Update last NextStudentActivityBalanceDetailRel
				
		Update is an Update Action
			restricted
			Action Rules
			Exit Rules
				if (NextStudentActivityBalanceDetailRel exists)
					invoke Update last NextStudentActivityBalanceDetailRel
				
		Delete is a Delete Action 
			restricted

  		DeleteIfNoAdjustmentsExist is an Instance Action
  			restricted
  			Action Rules
  				if (BalanceAdjustment 			!entered
				and PostedBalanceAdjustment		!entered)
  					invoke Delete
				else
					initialize	PeriodReceipts
					initialize	PeriodDisbursements
					initialize	PeriodTransfers
					initialize	PostedReceipts
					initialize	PostedDisbursements
					initialize	PostedTransfers
