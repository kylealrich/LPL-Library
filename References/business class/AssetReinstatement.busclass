AssetReinstatement is a BusinessClass
    owned by am
    prefix is RIN

    Ontology
        symbolic key is AssetReinstatement
            classic set name is RINSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        DisposalStatus
            classic name is DSP-STATUS
        DisposalDate                is Date
            classic name is DSP-DATE
        DisposalMethod
            classic name is DSP-METHOD
        DisposalProperty
            classic name is DSP-PROPERTY
        DisposalQuantity
            classic name is DSP-QUANTITY
        AssetLease
        AssetGroup
        FullOrPartial
            classic name is FULL-PARTIAL
        ProceedsCurrency            is a Currency
            classic name is PROCEEDS-CURR
        Proceeds                    is an InternationalAmount
                precision is Asset.TransactionNumberOfDecimals
        CostOfSaleCurrency          is a Currency
            classic name is COST-SALE-CURR
        CostOfSale                  is an InternationalAmount
            classic name is COST-SALE
                precision is Asset.TransactionNumberOfDecimals
        TotalItemQuantity
            classic name is ITEM-TOT-QTY
        ItemTotalBaseCost           is an InternationalAmount
            classic name is IT-TOT-CSTBASE
                precision is Asset.BaseNumberOfDecimals
        ItemTotalTransactionCost    is an InternationalAmount
            classic name is IT-TOT-CSTTRAN
                precision is Asset.TransactionNumberOfDecimals
        ItemTotalBaseTax            is an InternationalAmount
            classic name is IT-TOT-TAXBASE
                precision is Asset.BaseNumberOfDecimals
        ItemTotalTransactionTax     is an InternationalAmount
            classic name is IT-TOT-TAXTRAN
                precision is Asset.TransactionNumberOfDecimals
        BaseNumberOfDecimals
            classic name is BASE-ND
        TransactionNumberOfDecimals is a CurrencyNumberOfDecimals
            classic name is TRAN-ND
        Currency
            classic name is CURRENCY-CODE
        LastAssetItemPurchaseDate   is Date
            classic name is ITEM-LAST-DATE
        LastSequenceNumber          is a Sequence
        	disable Auditing
            classic name is ITEM-LAST-SEQ
        CreatedBy                   is an Operator 
        	holds pii
            classic name is CREATOR-ID
        ReinstatementDate           is Date
            classic name is REINSTATE-DATE
        PostingDate					is Date
        Company                     is an AssetCompany
		AssetDisposal
			delete ignored
	
	Local Fields
		LocalProcessSeq				is like Sequence
		LocalStartSeq				is like Sequence
		LocalWSSeq					is like Sequence
		LocalBook					is like Book
		LocalAsset					is an Asset
		LocalCreationDate			is TimeStamp

		LocalTranStatus				is AlphaUpper size 1
		LocalAssetProcess			is like AssetProcess
		LocalPostBook				is like Book



		LocalBalanceAmount			is an InternationalAmount
		LocalAmount					is an InternationalAmount
		LocalBasis					is an InternationalAmount
		LocalDisposalSequence		is like Sequence 
		LocalDisposalTranDate		is Date
		LocalTransactionDate		is Date
		LocalReinstatementDate		is Date
		BookDisposalReinstatement	is Boolean
		ReinstateBook 				is like Book

	Transient Fields
		TransientBook				is like Book

    Derived Fields
    
    	ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforAssetReinstatement"

		ContextMessageText is a MessageField
			restricted
			"AssetReinstatement<AssetReinstatement>"

        DisposedAsset                is a StringField
            type is Alpha size 10
            restricted
            classic name is AST-DSP-ASSET
            Asset.DisposedAsset
		
		DerivedDisposalDate	         is a DerivedField   
		    type is Date
		    if(IsDispExists)
		    	return AssetDisposalRel.DisposalDate
		    else
		    	return first PartialAssetDisposalRel.DisposalDate
		    	
		DerivedDisposalStatus       is a DerivedField
			type is like DisposalStatus
			if(IsDispExists)
		    	return AssetDisposalRel.DisposalStatus
		    else
		    	return first PartialAssetDisposalRel.DisposalStatus
    		
        AssetTagNumber               is a StringField
            type is Alpha size 12
            restricted
            classic name is AST-TAG-NBR
            Asset.TagNumber

        AssetReinstatementAssetGroup is a StringField
            type is Alpha size 10
            restricted
            classic name is AST-ASSET-GRP
            Asset.AssetGroup

        AssetDescription             is a StringField
            type is Alpha size 30
            restricted
            classic name is AST-DESCRIP
            Asset.Description
            
		AdjustmentMessage			is a MessageField
			restricted
			"AdjustmentProcess"
			
		TransferMessage				is a MessageField
			restricted
			"TransferProcess"
			
		DisposalMessage				is a MessageField
			restricted
			"DisposalProcess"
			
		ReinstatementMessage		is a MessageField
			restricted
			"ActiveReinstatement"
			
		AdditionMessage				is a MessageField
			restricted
			"AdditionProcess"
			
		AssetProcessMessage is a DerivedField
			type is Alpha size 25
			restricted
			if (Asset.Status.PostRelease)
				if (Asset.AssetProcess.Adjustment)
					return AdjustmentMessage
				else
				if (Asset.AssetProcess.Transfer)
					return TransferMessage
				else
				if (Asset.AssetProcess.Disposal)
					return DisposalMessage
				else
				if (Asset.AssetProcess.Reinstatement)
					return ReinstatementMessage
				else
				if (Asset.AssetProcess.Addition)
					return AdditionMessage    
					
		LastDisposalProcessSequence is a DerivedField
			type is Numeric 6
			restricted
			return first LastBookDisposalTransactionRel.AssetBookTransaction
		   
		LastBookDisposalTransactionDate is a DerivedField   
		    type is Date
		    restricted
		    return first LastBookDisposalTransactionRel.TransactionDate	    

    Conditions

        IsDispExists
        	restricted
            when (AssetDisposalRel exists)

        IsDspbooksExist
        	restricted
            when (first AssetBookDisposalReinstatementsRel exists)

        IsItemsExist
        	restricted
            when (first AssetItemReinstatementsRel exists)
            
        CanBeReinstated
        	restricted
        	when (Asset.Status.Disposed
        	and Asset.AssetProcess.Disposal)
        	
        ReAddUTransExist
        	restricted
        	when (AssetTransactionURAD2XRel exists
        	and AssetCompanyRel.DisplayJournalEntries)
        
        ReAddUBookTransExist
        	restricted
        	when (AssetBookTransactionURADRel exists)
        	
		ReAddMTransExist
			restricted
        	when (AssetCompanyRel.DisplayJournalEntries        	
        	and AssetTransactionURAD2XRel not exists)
        	
        ReinstatmentStatusNotReleased
        	restricted
            when (DisposalStatus.Unreleased)        	
        
       	FundAccounting
			restricted
			when (FinanceEnterpriseGroup.FundAccounting
			and not Asset.AssetType.AllocateDepreciationExpense)				        	
        	
    Relations

        AssetDisposalRel
            classic name is AMASSETDSP
            one-to-one relation to AssetDisposal
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
				related.Asset							= Asset
				related.AssetDisposal 					= Asset.LastAssetSequence
				
	  	PartialAssetDisposalRel
            one-to-many relation to AssetDisposal
            Field Mapping uses ByDisposalAsset
            	related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
            	related.DisposalAsset 					= Asset
				related.Asset							= Asset.DisposedAssetOriginatingAsset
	
		BookTypeAssetDisposalRel
            one-to-one relation to AssetDisposal
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
				related.Asset							= Asset
				related.AssetDisposal 					= LocalDisposalSequence		

		AssetBookDisposalsRel	
			one-to-many relation to AssetBookDisposal
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
			Instance Selection
				where (related.AssetDisposal 			= AssetDisposal)

		TransientBookDisposalRel	
			one-to-many relation to AssetBookDisposal
			Field Mapping uses Set2
				related.Book							= TransientBook
				related.Asset							= Asset
			Instance Selection
				where (related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				and    related.AssetDisposal 			= AssetDisposal)

		AssetItemDisposalRel	
			one-to-many relation to AssetItemDisposal
            	Field Mapping uses symbolic key
            		related.FinanceEnterpriseGroup		= Asset.FinanceEnterpriseGroup
                	related.Asset 						= Asset
                	related.AssetDisposal 				= Asset.LastAssetSequence
                	
        PartialAssetItemDisposalRel	
			one-to-many relation to AssetItemDisposal
            	Field Mapping uses symbolic key
            		related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
                	related.Asset 						= Asset.DisposedAssetOriginatingAsset
                	related.AssetDisposal 				= PartialAssetDisposalRel.AssetDisposal     	       	

        AssetBookDisposalReinstatementsRel
            classic name is AMASTBKRIN
            one-to-many relation to AssetBookReinstatement
            Field Mapping uses Set3
                related.Asset            		 		= Asset
                related.ReinstatementDate 				= ReinstatementDate

        AssetItemReinstatementsRel
            classic name is AMASTITRIN
            one-to-many relation to AssetItemReinstatement
            Field Mapping uses Set2
                related.Asset             				= Asset
                related.ReinstatementDate 				= ReinstatementDate
                
        BookTypeAssetBookDisposalsRel
			one-to-one relation to AssetBookDisposal
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
				related.Book							= TransientBook

		AssetBookDisposalRel
			one-to-many relation to AssetBookDisposal
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
			Instance Selection
				where (related.AssetDisposalRel.AssetDisposal = Asset.LastAssetSequence)	
		
		AssetTransactionRel
			one-to-many relation to AssetTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
				related.Book							= LocalBook
				related.Company							= Asset.Company
			Instance Selection
				where (related.AssetAccountType.Asset)
		
		AssetBookTransactionRel
			one-to-many relation to AssetBookTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
				related.Book							= LocalBook
				related.Company							= Asset.Company
			Instance Selection
				where (related.AssetAccountType.Asset)
				
		AssetTransaction10Rel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set10
                related.Asset             				= Asset
                
		AssetBookTransaction3Rel
			one-to-many relation to AssetBookTransaction
            Field Mapping uses Set3
            	related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
            	related.Company							= Asset.Company
                related.TransactionStatus				= "U"
                related.AssetProcess					= LocalAssetProcess
            Instance Selection
            	where (related.Asset					= Asset)
                                
		AssetBookTransaction5Rel
			one-to-many relation to AssetBookTransaction
            Field Mapping uses Set5
            	related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
                related.Asset             				= Asset                
                
		LastBookDisposalTransactionRel
			one-to-many relation to AssetBookTransaction
			Field Mapping uses ByDisposalSequenceDescending	
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
                related.Asset							= Asset

		BookReinstatementBookTransRel
			one-to-many relation to AssetBookTransaction
			Field Mapping uses Set6
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.TransactionStatus				= "H"
				related.AssetProcess					= "DSP"
				related.Asset							= Asset
				related.Book							= LocalBook
			Instance Selection
				where (related.ProcessSequence = AssetDisposal)

		AssetBookTransactionHDSPRel
			one-to-many relation to AssetBookTransaction
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
                related.Asset            				= Asset
			Instance Selection
				where (related.TransactionStatus 		= "H"
				and related.AssetProcess 				= "DSP")
					
		AssetBookTransactionURINRel
			one-to-many relation to AssetBookTransaction
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
                related.Asset            				= Asset
			Instance Selection
				where (related.TransactionStatus 		= "U"
				and related.AssetProcess 				= "RIN")
				
		AssetBookTransactionURADRel
			one-to-many relation to AssetBookTransaction
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
                related.Asset            				= Asset
			Instance Selection
				where (related.TransactionStatus 		= "U"
				and related.AssetProcess 				= "RAD")			
		
		AssetTransactionRDSPRel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set1
                related.Asset							= Asset
                related.TransactionStatus				= "R"
            	related.Company							= Asset.Company
			Instance Selection
				where (related.AssetProcess 			= "DSP"
				and related.ProcessSequence				= LocalProcessSeq)     

		CompanySystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode 
				related.GeneralLedgerSystemCode			= "AM"
				related.Company							= Asset.Company
				
                	

















                
     	AssetTransactionURAD2XRel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set1
                related.Asset							= Asset  
                related.TransactionStatus				= "U"
            	related.Company							= Asset.Company
			Instance Selection
                where (related.AssetProcess				= "RAD")
    
                
		AssetTransactionUnReleasedRel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set1
                related.Asset							= Asset   
                related.TransactionStatus				= "U"
            	related.Company							= Asset.Company
			Instance Selection
                where (related.AssetProcess				= "DSP")
             
                
		AssetTransactionURINRel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set1
                related.Asset							= Asset 
            	related.TransactionStatus				= "U"
            	related.Company							= Asset.Company
			Instance Selection
                where (related.AssetProcess				= "RIN")

                
                        
        AssetTransaction2DelRel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set1
                related.Asset							= Asset
                related.TransactionStatus				= LocalTranStatus
            	related.Company							= Asset.Company
			Instance Selection
                where (related.AssetProcess				= LocalAssetProcess)

                
        AssetTransaction4Rel
			one-to-many relation to AssetTransaction
            Field Mapping uses Set4
             	related.ProcessAsset					= Asset
                related.ProcessSequence					= LocalProcessSeq
                related.TransactionStatus				= "U"
                				
















		AssetTransactionURADRel
			one-to-many relation to AssetTransaction
			Field Mapping uses Set1
				related.Asset							= Asset
				related.TransactionStatus				= "U"
				related.Company							= Asset.Company
			Instance Selection	
				where (related.AssetProcess				= "RAD")

				
		AssetTransactionMemoUnreleasedRel
			one-to-many relation to AssetTransaction
			Field Mapping uses Set4
				related.ProcessAsset					= Asset	
				related.ProcessSequence					= Asset.LastAssetSequence
				
        AssetControlTransactionRel
			one-to-one relation to AssetControlTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
				related.Asset							= Asset
					        
		AssetCompanyRel
			one-to-one relation to AssetCompany
			Field Mapping uses symbolic key
				related.Company							= Asset.Company 
				
		AssetReinstatementRel
			one-to-many relation to AssetReinstatement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
				related.Asset							= Asset
								
		AssetBookReinstatement2Rel
			one-to-many relation to AssetBookReinstatement
			Field Mapping uses Set2
				related.Book							= LocalBook
				related.Asset							= LocalAsset
				
		AssetBookReinstatementRel
			one-to-one relation to AssetBookReinstatement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
				related.Book							= LocalBook
				related.AssetBookReinstatement 			= AssetReinstatement

		AssetBookRel
			one-to-many relation to AssetBook
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
				related.Asset							= Asset

		TransientAssetBookRel
			one-to-one relation to AssetBook
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= Asset.FinanceEnterpriseGroup
				related.Asset							= Asset	
				related.Book							= TransientBook
				
		CurrentBookCalendarsRel
			one-to-one relation to BookCalendar
			Field Mapping uses Set4
				related.Company							= Asset.Company
				related.Book							= LocalBook			
			
		LocalBookRel
			one-to-one relation to Book
				Field Mapping uses symbolic key		
					related.FinanceEnterpriseGroup		= Asset.FinanceEnterpriseGroup
					related.Book						= LocalBook		

		PurgeAssetItemReinstatementRel
			one-to-many relation to AssetItemReinstatement
			Field Mapping uses Set2
				related.Asset							= Asset
			Instance Selection
				include deleted records
				
		PurgeBookReinstatementRel
			one-to-many relation to AssetBookReinstatement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.Asset							= Asset
				related.Book							= LocalBook
				related.AssetBookReinstatement 			= AssetReinstatement
			Instance Selection
				include deleted records

	Rule Blocks
		CreateRules
			constraint (!Asset.Status.Unreleased)
				"CannotDisposeInProcessAsset"				

			if (Asset.Status.PostRelease)
				constraint (Asset.AssetProcess.ReAdd
				or			Asset.AssetProcess.Depreciation)
					"CannotReinstate;AssetIsIn<AssetProcessMessage>"	
					
		DeleteAndReleaseRules
			constraint (Asset.Status.PostRelease
			or			Asset.AssetProcess.Reinstatement)
				"CannotDeleteOrRelease;<AssetProcessMessage>"					
				
		CommonEditRules
			if (Asset.Disposed)
				constraint (AssetDisposalRel.Full)
					"CannotReinstatePartiallyDisposedAsset"				
			
		CheckAMControl
			if (AssetCompanyRel exists)
				constraint (AssetCompanyRel.PendingYearEndCount not entered)
					"YearEndPending,CanNotUpdate"  
				
			if (AssetCompanyRel exists)
				constraint (AssetCompanyRel.PeriodCloseRelease != "X")
					"PeriodOrYearEndCloseInProcess"

		ReinstatementCalendarEdits	



			constraint (ReinstatementDate <= CurrentBookCalendarsRel.YearEndDate
			and ReinstatementDate >= CurrentBookCalendarsRel.YearBeginDate)
				"ReinstateDateMustBeInTheCurrentYearOfAllBooks" 
			constraint(DerivedDisposalDate <= CurrentBookCalendarsRel.YearEndDate
			and DerivedDisposalDate >= CurrentBookCalendarsRel.YearBeginDate)
				"ReinstatementDateMustBeInTheSameCalendarYearAsTheDisposal" 
			constraint (ReinstatementDate >= each.InServiceDate)
				"ReinstateDateCannotBePriorToInServiceDate"	

		ReInstateAsset
			invoke TransistiontoPostRelease Asset
				invoked.PrmAssetProcess			= "RIN"
				invoked.PrmProcessDate 			= ReinstatementDate
				invoked.PrmReInstateFlag		= true
				invoked.PrmReInstateDate		= ReinstatementDate
				invoked.PrmDisposedAsset		= Asset
				
			invoke ReinstateDisposalTransactions Asset
			if (IsDispExists)
				LocalProcessSeq	= Asset.LastAssetSequence
			else
				LocalProcessSeq = first PartialAssetDisposalRel.AssetDisposal

		ReinstateBookDisposalAsset
			invoke TransistiontoPostRelease Asset
				invoked.PrmAssetProcess			= "RIN"
				invoked.PrmProcessDate 			= ReinstatementDate
				invoked.PrmReInstateFlag		= true
				invoked.PrmReInstateDate		= ReinstatementDate
				invoked.PrmDisposedAsset		= Asset
			invoke ReinstateDisposalTransactions Asset
			LocalProcessSeq = AssetDisposal


		ReInstateAssetItemBook
			if (AssetItemDisposalRel exists)
				for each AssetItemDisposalRel
					invoke Create AssetItemReinstatement 
						fill in fields from each.AssetItemDisposal
						invoked.ReinstatementDate						= ReinstatementDate  
						invoked.AssetItemReinstatement.ProcessSequence 	= each.AssetItemDisposal.ProcessSequence
						invoked.AssetItemReinstatement.CreationDate		= AssetReinstatement.CreationDate
			else
				for each PartialAssetItemDisposalRel 
					invoke Create AssetItemReinstatement 
						fill in fields from each.AssetItemDisposal
						invoked.Asset									= Asset
						invoked.ReinstatementDate						= ReinstatementDate
						invoked.AssetItemReinstatement.ProcessSequence 	= each.AssetItemDisposal.ProcessSequence
						invoked.AssetItemReinstatement.CreationDate		= AssetReinstatement.CreationDate
						
						
						







			if (TransientBook entered)
				LocalBook	= TransientBook
				if (BookTypeAssetBookDisposalsRel exists)

					if (BookTypeAssetBookDisposalsRel.AssetDisposal = AssetDisposal)					
						invoke Create AssetBookReinstatement 
							fill in fields from BookTypeAssetBookDisposalsRel
							invoked.ReinstatementDate						= ReinstatementDate
							invoked.AssetBookReinstatement.CreationDate		= AssetReinstatement.CreationDate

				invoke UpdateFromReinstatement TransientAssetBookRel
					if (this instance.TransientAssetBookRel.AssetTaxCreditTable entered)
						if (AssetTransactionRel exists)
							LocalBasis = sum AssetTransactionRel.AssetTransaction.TransactionAmount
						else
							if (AssetBookTransactionRel exists)
								LocalBasis = sum AssetBookTransactionRel.AssetBookTransaction.TransactionAmount
						invoked.PrmBasis 					= this instance.TransientAssetBookRel.Basis + LocalBasis
					invoked.PrmYearToDateDepreciation		= AssetBookReinstatementRel.ReinstatementYearToDateDepreciation
					invoked.PrmLifeToDateDepreciation		= AssetBookReinstatementRel.ReinstatementLifeToDateDepreciation
					invoked.PrmReinstateBookDisposal		= true	
				LocalDisposalTranDate = LastBookDisposalTransactionDate 
				for each BookReinstatementBookTransRel
					LocalAmount		= each.TransactionAmount
					invoke Update each
						invoked.AssetProcess					= "RIN"
						invoked.TransactionStatus				= "U"
						invoked.Reference						= "Disposal"
						invoked.ProcessSequence					=  AssetDisposal
					invoke Create AssetBookTransaction
						fill in fields from each
							except invoked.AssetTransactionAmount
						if (each.Book.PostCurrency.Asset
						and Asset.Currency != each.Book.Currency)
							LocalTransactionDate 				= Asset.LastAssetItemPurchaseDate
						else
							LocalTransactionDate				= ReinstatementDate
						invoked.TransactionDate 				= LocalTransactionDate						
						if (PostingDate entered)					
							invoked.PostingDate					= PostingDate
						else
							invoked.PostingDate					= ReinstatementDate
						invoked.ProcessDate						= ReinstatementDate
						invoked.AssetProcess					= "RAD"
						invoked.TransactionStatus				= "U"
						invoked.Reference						= "Reinstatement"
						invoked.TransactionAmount				= each.TransactionAmount * -1
						if (LocalBookRel.PostCurrency.Book)
							invoked.TransientBookCurrency						= LocalBookRel.Currency
							invoked.TransactionNumberOfDecimals 				= LocalBookRel.Currency.NumberOfDecimals
						else
							invoked.TransientBookCurrency						= Asset.Currency 
							invoked.TransactionNumberOfDecimals					= Asset.Currency.NumberOfDecimals
						invoked.AssetTransactionAmount.TransactionAmount 		= each.TransactionAmount * -1
						invoked.AssetTransactionAmount.BaseAmount.ToCurrency 	= LocalBookRel.Currency
						invoked.AssetTransactionAmount.ExchangeDate	  			= LocalTransactionDate
				
				LocalProcessSeq	= AssetDisposal

				for each AssetTransactionRDSPRel
					LocalBook 			= each.Book
					if (each.ProcessSequence = LocalProcessSeq)
						invoke Update each
							invoked.AssetProcess					= "RIN"
							invoked.TransactionStatus				= "U"
							invoked.Reference						= "Disposal"
							invoked.ProcessSequence                 = LocalProcessSeq
				for each AssetTransactionURINRel
					LocalBook 			= each.Book
					if (each.ProcessSequence = LocalProcessSeq)
						invoke Create AssetTransactionURINRel
							fill in fields from each
								except invoked.AssetTransactionAmount
							invoked.OnlineSystemCreate				= true				
							invoked.Reference						= "Reinstatement"
							if (each.Book.PostCurrency.Asset
							and Asset.Currency != each.Book.Currency)
								LocalTransactionDate 				= Asset.LastAssetItemPurchaseDate
							else
								LocalTransactionDate				= ReinstatementDate
							invoked.TransactionDate 				= LocalTransactionDate			
							if (PostingDate entered)					
								invoked.PostingDate					= PostingDate
							else
								invoked.PostingDate					= ReinstatementDate
							invoked.ProcessDate						= ReinstatementDate
							invoked.AssetProcess					= "RAD"
							invoked.TransactionStatus 				= "U"
							invoked.TransactionAmount				= each.TransactionAmount * -1
							if (each.Book.PostCurrency.Book)
								invoked.TransientBookCurrency						= each.Book.Currency
								invoked.TransactionNumberOfDecimals 				= each.Book.Currency.NumberOfDecimals
							else
								invoked.TransientBookCurrency						= Asset.Currency 
								invoked.TransactionNumberOfDecimals					= Asset.Currency.NumberOfDecimals
							invoked.AssetTransactionAmount.TransactionAmount 		= each.TransactionAmount * -1
							invoked.AssetTransactionAmount.BaseAmount.ToCurrency 	= each.Book.Currency
							invoked.AssetTransactionAmount.ExchangeDate	  			= LocalTransactionDate
			else				
				if (AssetBookDisposalRel exists)
					for each AssetBookDisposalRel
						LocalBook 			= each.Book
						if ((each.AssetDisposal = Asset.LastAssetSequence)
						or (each.AssetDisposal not entered))
							invoke Create AssetBookReinstatement 
								fill in fields from each.AssetBookDisposal
								invoked.ReinstatementDate						= ReinstatementDate
								invoked.AssetBookReinstatement.CreationDate		= AssetReinstatement.CreationDate
				for each AssetBookRel
					LocalBook 			= each.Book
					if ((each.DerivedBookDisposal = Asset.LastAssetSequence)
					or (each.DerivedBookDisposal not entered))
						invoke UpdateFromReinstatement each
							if (each.AssetTaxCreditTable entered)
								if (AssetTransactionRel exists)
									LocalBasis = sum AssetTransactionRel.AssetTransaction.TransactionAmount
								else
									if (AssetBookTransactionRel exists)
										LocalBasis = sum AssetBookTransactionRel.AssetBookTransaction.TransactionAmount
								invoked.PrmBasis 						= each.Basis + LocalBasis
							invoked.PrmYearToDateDepreciation		= AssetBookReinstatementRel.ReinstatementYearToDateDepreciation
							invoked.PrmLifeToDateDepreciation		= AssetBookReinstatementRel.ReinstatementLifeToDateDepreciation
						
				if (AssetTransaction10Rel exists)
					LocalStartSeq						= AssetTransaction10Rel.AssetTransaction
				else
					initialize LocalStartSeq
					
				if (AssetBookTransaction5Rel exists)
					LocalWSSeq							= AssetBookTransaction5Rel.AssetBookTransaction
				else
					initialize LocalWSSeq
					

				LocalDisposalTranDate = LastBookDisposalTransactionDate 
				for each AssetBookTransactionHDSPRel

					LocalBook 			= each.Book
					if ((AssetDisposalRel exists 
					and each.ProcessSequence = Asset.LastAssetSequence)
					or (each.ProcessSequence = first PartialAssetDisposalRel.AssetDisposal))
						LocalAmount		= each.TransactionAmount
						invoke Update each
							invoked.AssetProcess					= "RIN"
							invoked.TransactionStatus				= "U"
							invoked.Reference						= "Disposal"
							invoked.ProcessSequence					=  Asset.LastAssetSequence
						invoke Create AssetBookTransaction
							fill in fields from each
								except invoked.AssetTransactionAmount
							if (each.Book.PostCurrency.Asset
							and Asset.Currency != each.Book.Currency)
								LocalTransactionDate 				= Asset.LastAssetItemPurchaseDate
							else
								LocalTransactionDate				= ReinstatementDate
							invoked.TransactionDate 				= LocalTransactionDate						
							if (PostingDate entered)					
								invoked.PostingDate					= PostingDate
							else
								invoked.PostingDate					= ReinstatementDate
							invoked.ProcessDate						= ReinstatementDate
							invoked.AssetProcess					= "RAD"
							invoked.TransactionStatus				= "U"
							invoked.Reference						= "Reinstatement"
							invoked.TransactionAmount				= each.TransactionAmount * -1
							if (LocalBookRel.PostCurrency.Book)
								invoked.TransientBookCurrency						= LocalBookRel.Currency
								invoked.TransactionNumberOfDecimals 				= LocalBookRel.Currency.NumberOfDecimals
							else
								invoked.TransientBookCurrency						= Asset.Currency 
								invoked.TransactionNumberOfDecimals					= Asset.Currency.NumberOfDecimals
							invoked.AssetTransactionAmount.TransactionAmount 		= each.TransactionAmount * -1
							invoked.AssetTransactionAmount.BaseAmount.ToCurrency 	= LocalBookRel.Currency
							invoked.AssetTransactionAmount.ExchangeDate	  			= LocalTransactionDate
				
				if (IsDispExists)
					LocalProcessSeq	= Asset.LastAssetSequence
				else
					LocalProcessSeq	= first PartialAssetDisposalRel.AssetDisposal
				for each AssetTransactionRDSPRel
					LocalBook 			= each.Book
					if (each.ProcessSequence = LocalProcessSeq)
						invoke Update each
							invoked.AssetProcess					= "RIN"
							invoked.TransactionStatus				= "U"
							invoked.Reference						= "Disposal"
							invoked.ProcessSequence                 = Asset.LastAssetSequence
				for each AssetTransactionURINRel
					LocalBook 			= each.Book
					if (each.ProcessSequence = Asset.LastAssetSequence)
						invoke Create AssetTransactionURINRel
							fill in fields from each
								except invoked.AssetTransactionAmount
							invoked.OnlineSystemCreate				= true				
							invoked.Reference						= "Reinstatement"
							if (each.Book.PostCurrency.Asset
							and Asset.Currency != each.Book.Currency)
								LocalTransactionDate 				= Asset.LastAssetItemPurchaseDate
							else
								LocalTransactionDate				= ReinstatementDate
							invoked.TransactionDate 				= LocalTransactionDate			
							if (PostingDate entered)					
								invoked.PostingDate						= PostingDate
							else
								invoked.PostingDate						= ReinstatementDate
							invoked.ProcessDate						= ReinstatementDate
							invoked.AssetProcess					= "RAD"
							invoked.TransactionStatus 				= "U"
							invoked.TransactionAmount				= each.TransactionAmount * -1
							if (each.Book.PostCurrency.Book)
								invoked.TransientBookCurrency						= each.Book.Currency
								invoked.TransactionNumberOfDecimals 				= each.Book.Currency.NumberOfDecimals
							else
								invoked.TransientBookCurrency						= Asset.Currency 
								invoked.TransactionNumberOfDecimals					= Asset.Currency.NumberOfDecimals
							invoked.AssetTransactionAmount.TransactionAmount 		= each.TransactionAmount * -1
							invoked.AssetTransactionAmount.BaseAmount.ToCurrency 	= each.Book.Currency
							invoked.AssetTransactionAmount.ExchangeDate	  			= LocalTransactionDate
				               
	Field Rules
		AssetReinstatement.CreationDate
			default to current timestamp
		ReinstatementDate
			required
			constraint (DerivedDisposalDate <= ReinstatementDate)
				"ReinstateDateCanNotBePriorToDisposalDate"  
			for each Asset.AssetBooksRel
				LocalBook 			= each.Book
				if (AssetDisposal entered)
					if (AssetDisposal.BookDisposal
					and each.AssetBookDisposalRel.SelectedForReinstatement)
						include ReinstatementCalendarEdits	
				else	
					if (!AssetDisposal.BookDisposal)
						include ReinstatementCalendarEdits	
		
		PostingDate
			default to ReinstatementDate
			if (CompanySystemClosingControlRel exists
			and CompanySystemClosingControlRel.Control = true)
				constraint (PostingDate within CompanySystemClosingControlRel.ValidEntryDate)
					"PostingDateIsOutsideSystemControlValidEntryDateRange"

		Company
			default to Asset.Company
			
		AssetGroup
			default to Asset.AssetGroup
			
		AssetLease
			default to Asset.AssetLease
			
    Sets
		Set7
			indexed
            Instance Selection
				where (ReinstatmentStatusNotReleased)
            Sort Order
                Asset
                AssetReinstatement
														
	Actions
		Create is a Create Action
			Entrance Rules
				include CheckAMControl
				include ReInstateAsset
			Action Rules
				if (AssetDisposalRel exists)
					DisposalStatus						= "U"
					AssetReinstatement.CreationDate		= current timestamp
					DisposalDate 						= AssetDisposalRel.DisposalDate		
        			DisposalMethod						= AssetDisposalRel.DisposalMethod
        			DisposalProperty					= AssetDisposalRel.DisposalProperty
        			if (AssetDisposalRel.FullOrPartial.Partial)
			        	DisposalQuantity				= AssetDisposalRel.DisposalQuantity
			        else
			        	DisposalQuantity				= Asset.TotalItemQuantity	
        			FullOrPartial						= AssetDisposalRel.FullOrPartial
        		else
					DisposalStatus						= "U"
					AssetReinstatement.CreationDate		= current timestamp
					DisposalDate 						= PartialAssetDisposalRel.DisposalDate		
					DisposalMethod						= PartialAssetDisposalRel.DisposalMethod
					DisposalProperty					= PartialAssetDisposalRel.DisposalProperty
					DisposalQuantity				    = PartialAssetDisposalRel.DisposalQuantity
					FullOrPartial						= PartialAssetDisposalRel.FullOrPartial
			Exit Rules
				
				include ReInstateAssetItemBook

		CreateBookDisposalReinstatement is a Create Action
			Entrance Rules
				include ReinstateBookDisposalAsset
				LocalDisposalSequence = AssetDisposal
			Action Rules
				DisposalStatus						= "U"
				AssetReinstatement.CreationDate		= current timestamp
				DisposalDate 						= BookTypeAssetDisposalRel.DisposalDate		
				DisposalMethod						= BookTypeAssetDisposalRel.DisposalMethod
				DisposalProperty					= BookTypeAssetDisposalRel.DisposalProperty
				DisposalQuantity					= Asset.TotalItemQuantity	
				FullOrPartial						= BookTypeAssetDisposalRel.FullOrPartial
				AssetDisposal						= BookTypeAssetDisposalRel.AssetDisposal

			Exit Rules		
				include ReInstateAssetItemBook
				invoke Delete TransientBookDisposalRel


		MCreateAssetReinstatement is a Create Action
			restricted
			Action Rules
				invoke Create this instance				


		Update is an Update Action
			Entrance Rules
				include CheckAMControl
				if (PostingDate entered
				and PostingDate changed)
					if (AssetTransactionURADRel exist)
						for each AssetTransactionURADRel
							invoke UpdateTransactionDate each
								invoked.PostingDate = PostingDate
	
					if (AssetBookTransactionURADRel exist)
						for each AssetBookTransactionURADRel
							invoke UpdateTransactionDate each
								invoked.PostingDate = PostingDate


		Delete is a Delete Action
			Entrance Rules
				include CheckAMControl

				invoke RestoreAsset Asset
					invoked.PrmAssetProcess		= "DSP"
					initialize invoked.PrmRestoreDate

				for each AssetReinstatementRel
					if (each.DisposalStatus 	= "U")
					 	initialize LocalBook
					 	LocalAsset				= each.Asset
					 	LocalCreationDate		= each.AssetReinstatement.CreationDate
					    for each AssetBookRel
							LocalBook 			= each.Book
							if ((each.DerivedBookDisposal = Asset.LastAssetSequence)
							or (each.DerivedBookDisposal not entered))
								invoke UpdateFromReinstatement each
									invoked.PrmBasis 						= AssetBookReinstatementRel.Basis
									invoked.PrmYearToDateDepreciation		= AssetBookReinstatementRel.YearToDateDepreciation
									invoked.PrmLifeToDateDepreciation		= AssetBookReinstatementRel.LifeToDateDepreciation
					 		if (AssetBookReinstatement2Rel exists
					 		and AssetBookReinstatement2Rel.Asset = Asset)
					 			for each AssetBookReinstatement2Rel
					 				if (each.AssetBookReinstatement.CreationDate date 	= LocalCreationDate date
					 				and each.AssetBookReinstatement.CreationDate time 	= LocalCreationDate time)
					 					invoke Delete AssetBookReinstatement2Rel				 				
					 
						LocalAsset				= each.Asset
						for each AssetItemReinstatementsRel
							if (each.AssetItemReinstatement.CreationDate date 		= LocalCreationDate date
							and each.AssetItemReinstatement.CreationDate time 		= LocalCreationDate time)
								invoke Delete AssetItemReinstatementsRel
									
			Action Rules					

				for each AssetTransactionURINRel
					if (each.ProcessSequence = Asset.LastAssetSequence)					
						invoke ReinstatementUpdate each
							invoked.TransactionStatus 					= "R"
							invoked.Reference							= "Disposal"
							invoked.AssetProcess						= "DSP"
							if (PartialAssetDisposalRel exists)
								invoked.ProcessSequence                     = first PartialAssetDisposalRel.AssetDisposal
			

				for each AssetTransactionURADRel
					if (each.ProcessSequence = Asset.LastAssetSequence)				
						invoke DecrementLastTransactionSequence Asset
						invoke Delete AssetTransactionURADRel


				
				LocalAssetProcess	= "RIN"
				for each AssetBookTransaction3Rel
					if (each.ProcessSequence = Asset.LastAssetSequence)				
						invoke Update AssetBookTransaction3Rel
							fill in fields from each
							invoked.AssetProcess						= "DSP"
							invoked.TransactionStatus					= "H"
							invoked.Reference							= "Disposal"
							if (PartialAssetDisposalRel exists)
								invoked.ProcessSequence                 = first PartialAssetDisposalRel.AssetDisposal
						
				LocalAssetProcess	= "RAD"
				for each AssetBookTransaction3Rel
					if (each.ProcessSequence = Asset.LastAssetSequence)				
						invoke DecrementLastTransactionSequence Asset
							
						invoke Delete each
						



		Release is an Instance Action
			valid when (ReinstatmentStatusNotReleased)
			Parameters
				PrmAssetBatchUpdateResult is like AssetBatchUpdateResult
				PrmBookDisposal	is Boolean
			Entrance Rules
				initialize LocalBalanceAmount
				for each AssetTransactionMemoUnreleasedRel
					LocalBalanceAmount += each.TransactionAmount
				constraint (LocalBalanceAmount = 0)
					"JournalEntriesDoNotBalance"  
			Action Rules
				invoke SetBackToActive Asset
				if (AssetDisposal.BookDisposal)

					for each AssetBookDisposalsRel	

						if (each.Disposed
						and each.SelectedForReinstatement)
							invoke UpdateFromReinstatement each
								invoked.PrmYearToDateDepreciation		= AssetBookReinstatementRel.ReinstatementYearToDateDepreciation
								invoked.PrmLifeToDateDepreciation		= AssetBookReinstatementRel.ReinstatementLifeToDateDepreciation	
					if (!AssetBookDisposalsRel exists)
						invoke BookReinstateDelete AssetDisposal
							invoked.PrmAssetDisposal = AssetDisposal

				else
					for each AssetBookRel
						LocalBook 			= each.Book
						if ((each.DerivedBookDisposal = Asset.LastAssetSequence)
						or (each.DerivedBookDisposal not entered))
							invoke UpdateFromReinstatement each
								invoked.PrmYearToDateDepreciation		= AssetBookReinstatementRel.ReinstatementYearToDateDepreciation
								invoked.PrmLifeToDateDepreciation		= AssetBookReinstatementRel.ReinstatementLifeToDateDepreciation	
				DisposalStatus	= "R"
				if (AssetDisposal.BookDisposal)
					for each AssetBookDisposalsRel
						ReinstateBook = each.Book
						if (!each.Disposed)
							invoke Delete each							
					if (AssetBookDisposalsRel exists)
						invoke Released.Delete AssetDisposal
				else
					if (AssetDisposalRel exists)
						for each AssetBookDisposalRel	
							ReinstateBook = each.Book
							if (!each.IsBookDisposal)
								invoke Delete each

						if (AssetItemDisposalRel exists)
							invoke Delete AssetItemDisposalRel
		
						invoke Released.Delete AssetDisposalRel
					else	
						if(PartialAssetDisposalRel exists)
							invoke Delete AssetBookDisposalRel
							invoke DisposalDelete PartialAssetItemDisposalRel
							invoke Released.Delete PartialAssetDisposalRel
				for each AssetTransactionURINRel
					invoke Release each
						invoked.PrmAssetBatchUpdateResult	= PrmAssetBatchUpdateResult

				for each AssetBookTransactionURINRel
					invoke UpdateStatus AssetBookTransactionURINRel
						invoked.PrmStatus	= "H"
						invoked.PrmAssetBatchUpdateResult	= PrmAssetBatchUpdateResult

				for each AssetBookTransactionURADRel
					invoke UpdateStatus AssetBookTransactionURADRel
						invoked.PrmStatus	= "H"
						invoked.PrmAssetBatchUpdateResult	= PrmAssetBatchUpdateResult
						
		AssetReleaseReinstatement is an Instance Action
			restricted
			Action Rules
				invoke PostRelease.ReleaseReinstatement Asset
		
		AssetResetReinstatement is an Instance Action
			restricted
			Action Rules
				invoke PostRelease.ResetReinstatement Asset

		SubmitForApproval is an Instance Action
			restricted
			Action Rules
				invoke SubmitForApproval Asset
				
		PurgeAssetReinstatements is a Set Action
			restricted
			
			Parameters
				PrmCompany				is an AssetCompany
				PrmAsset				is like Asset
				PrmFromDate				is Date
				PrmToDate				is Date
				PrmAssetGroup			is like AssetGroup
				PrmAssetType			is like AssetType
				PrmAssetSubType			is like AssetType
				
			Instance Selection

				where (Asset.Company = PrmCompany
				and    (PrmFromDate not entered or DisposalDate >=	PrmFromDate) 
				and    (PrmToDate not entered or DisposalDate <= PrmToDate)
				and    (PrmAssetGroup not entered or Asset.AssetGroup = PrmAssetGroup)
				and    (PrmAssetType not entered or Asset.AssetType.Type = PrmAssetType)
				and    (PrmAssetSubType not entered or Asset.AssetType.SubType = PrmAssetSubType)
				and    DisposalStatus.Released)
				
			Action Rules
				Instance Rules
					invoke Purge
		
		PurgeDeletedAssetReinstatements is a Set Action
			restricted
			
			Parameters
				PrmCompany				is an AssetCompany
				PrmAsset				is like Asset
				PrmFromDate				is Date
				PrmToDate				is Date
				PrmAssetGroup			is like AssetGroup
				PrmAssetType			is like AssetType
				PrmAssetSubType			is like AssetType
				
			Instance Selection
				include only deleted records
				where (Asset.Company = PrmCompany
				and    (PrmFromDate not entered or DisposalDate >=	PrmFromDate) 
				and    (PrmToDate not entered or DisposalDate <= PrmToDate)
				and    (PrmAssetGroup not entered or Asset.AssetGroup = PrmAssetGroup)
				and    (PrmAssetType not entered or Asset.AssetType.Type = PrmAssetType)
				and    (PrmAssetSubType not entered or Asset.AssetType.SubType = PrmAssetSubType))
				
			Action Rules
				Instance Rules
					invoke Purge
					
		PurgeDisposedAssetReinstatements is a Set Action
			restricted
			Parameters
				PrmCompany					is an AssetCompany
				PrmAsset					is an Asset
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
				PrmLeaseCompany				is like GeneralLedgerCompany
				PrmLocationName				is like AssetLocation
				PrmDivision					is like AssetDivision
				PrmAssetGroup				is like AssetGroup
				PrmFromDate					is Date
				PrmToDate					is Date
				
			Instance Selection
				include deleted records
				where (Asset.Company = PrmCompany
				and   (PrmAsset not entered or (Asset = PrmAsset and Asset.Company.FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup))
				and   (PrmLeaseCompany not entered or Asset.AssetLease.LeaseCompany = PrmLeaseCompany)
				and   (PrmLocationName not entered or Asset.AssetLocation = PrmLocationName)
				and   (PrmDivision not entered or Asset.AssetDivision = PrmDivision)
				and   (PrmAssetGroup not entered or Asset.AssetGroup = PrmAssetGroup)
				and   (PrmFromDate not entered or Asset.AssetDisposalDate >= PrmFromDate)
				and   (PrmToDate not entered or Asset.AssetDisposalDate <= PrmToDate)
				and   Asset.Status.Disposed)
				
			Action Rules
				Instance Rules
					invoke Purge

		Purge is a Purge Action  
			restricted
			bypass relational integrity rules
			Entrance Rules
				invoke Purge PurgeAssetItemReinstatementRel
				
				for each AssetBookRel
					LocalBook	= each.Book
					invoke Purge PurgeBookReinstatementRel

		RemovePurgedLeaseID is an Update Action  
			restricted
			bypass field rules










				
			Action Rules
	        	initialize AssetLease

		NewPostingDateUpdate is an Update Action
	    	restricted

					
					
