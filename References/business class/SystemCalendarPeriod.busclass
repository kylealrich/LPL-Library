SystemCalendarPeriod is a BusinessClass
	owned by sc
	prefix is Syst

	Ontology
		symbolic key is SystemCalendarPeriod

    Patterns
        implements StaticJava

	Context Fields
		SystemCalendarPeriodYear

	Persistent Fields
		DateRange		is a SystemCalendarDateRange
		Name			is Alpha size 50
				
	Field Rules

		DateRange
			required
			constraint (!OverlappingPeriodRel exists)
				"PeriodCannotOverlapAnotherPeriod"
				
	Relations
		OverlappingPeriodRel
			one-to-many relation to SystemCalendarPeriod
			Field Mapping uses symbolic key
				related.EnterpriseGroup			= EnterpriseGroup
				related.SystemCalendar          = SystemCalendar
				related.SystemCalendarPeriodGroup = SystemCalendarPeriodGroup
			Instance Selection
				where (related.SystemCalendarPeriod != SystemCalendarPeriod
				and    related.DateRange            overlaps DateRange)
				
		DatesForRange
			one-to-many relation to SystemCalendarDate
			Field Mapping uses symbolic key
				related.EnterpriseGroup = EnterpriseGroup
				related.SystemCalendar  = SystemCalendar
			Instance Selection
				where (related.SystemCalendarDate within DateRange)				
			
	Transient Fields
		PeriodNameTemplate is Alpha size 50
			Text Variables
				PeriodNumber 	value is SystemCalendarPeriod.Period
				FromDate		value is DateRange.FromDate
				ToDate			value is DateRange.ToDate
				Year			value is SystemCalendarPeriod.Year
						
	Derived Fields
		RenderedNameTemplate	is a DerivedField
			type is Alpha size 50
			return PeriodNameTemplate text
			
		PeriodWithinRequestedYear is a DerivedField
			type is Boolean
			if (!SystemCalendarPeriodYear entered)
				return true
			if (SystemCalendarPeriod.Year = SystemCalendarPeriodYear)
				return true
						
	Actions
		Create is a Create Action
			Exit Rules
				if (PeriodNameTemplate entered)
					invoke Update
						invoked.Name = RenderedNameTemplate					
		
		Update is an Update Action
		
		Delete is a Delete Action
			Exit Rules
				if (!SystemCalendarPeriodGroup.SystemCalendarPeriod set exists)
					invoke Update SystemCalendarPeriodGroup
						invoked.LastPeriod = 0
									
				
