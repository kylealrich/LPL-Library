FileCreationSortThread is a BusinessClass
    owned by filecreation
    prefix is FCUST


    Patterns
        disable EffectiveDated
        disable AsOfDateProcessing

    Persistent Fields
    	FinanceEnterpriseGroup
		FileCreationSetup
    	Key 							is Alpha 240
    	Thread 							is Numeric 2
		FileSystemTimeStamp				is TimeStamp
		FileSequenceNumber				is Numeric 4
		Processed						is Boolean

	Local Fields
	
		LocalFileCreationSetup is like FileCreationSetup
		LocalFinanceEnterpriseGroup    is like FinanceEnterpriseGroup
		
		
	Transient Fields
				
	Derived Fields
	
		NumberOfUniqueSortKeys is a DerivedField
			type is Numeric 6
			return instance count of UnprocessedFileCreationSortThreadRel

	Conditions
			
	Relations
	
		UnprocessedFileCreationSortThreadRel
			one-to-many relation to FileCreationSortThread
			Field Mapping uses ByKey
				related.FinanceEnterpriseGroup 		= LocalFinanceEnterpriseGroup
				related.FileCreationSetup	= LocalFileCreationSetup
			Instance Selection
				where (related.FileSystemTimeStamp not entered
				and    related.FileSequenceNumber not entered)
				
		ProcessedSortThreadRel
			one-to-one relation to FileCreationSortThread
			Field Mapping uses ByKey
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.FileCreationSetup	= FileCreationSetup
				related.Key					= Key
				related.FileSystemTimeStamp = FileSystemTimeStamp
				related.FileSequenceNumber  = FileSequenceNumber
				related.Processed			= true

	Field Rules

	Sets
	
		ByKey
			primary
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				Key
				FileSystemTimeStamp
				FileSequenceNumber
				Processed

	Rule Blocks

	Actions
		Create is a Create Action
		
		Update is an Update Action
	
		Delete is a Delete Action
		
		Purge is a Purge Action
		
		PurgeRecords is a Set Action
		
			Parameters
				PrmFileCreationSetup 				is like FileCreationSetup
				PrmFinanceEnterpriseGroup  	 				is like FinanceEnterpriseGroup
				PrmFileSystemTimeStamp				is TimeStamp
				PrmFileSequenceNumber				is Numeric 4
			
			Instance Selection
				where (FileCreationSetup = PrmFileCreationSetup
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileSystemTimeStamp = PrmFileSystemTimeStamp
				and    FileSequenceNumber = PrmFileSequenceNumber
				and    Processed)
				
			Action Rules
					
				Instance Rules
					invoke Purge
		
		SetThread is a Set Action
		
			Parameters
				PrmFileCreationSetup is like FileCreationSetup
				PrmFinanceEnterpriseGroup  	 is like FinanceEnterpriseGroup
				PrmOverrideNumberOfThreads  is Numeric 3
			
			Local Fields
				I1    is Numeric 9
				ThreadNumber is Numeric 2
				SortKeysPerThread is Numeric 9
				LocalNumberOfThreads is Numeric 3
			
			Instance Selection
				where (FileCreationSetup = PrmFileCreationSetup
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileSystemTimeStamp not entered
				and    FileSequenceNumber not entered
				and    not Processed)
				
			Action Rules
				Set Rules
					Entrance Rules
						LocalFileCreationSetup = PrmFileCreationSetup
						LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						if (PrmOverrideNumberOfThreads entered)
							SortKeysPerThread = NumberOfUniqueSortKeys / PrmOverrideNumberOfThreads
							LocalNumberOfThreads = PrmOverrideNumberOfThreads
						else
							SortKeysPerThread = NumberOfUniqueSortKeys / 2
							LocalNumberOfThreads = 2
						if (SortKeysPerThread = 0)
							SortKeysPerThread = 1
						ThreadNumber = 0
					
				Instance Rules
					Thread = ThreadNumber
					invoke Purge ProcessedSortThreadRel
					Processed = true
					I1 += 1
					if (I1 = SortKeysPerThread)
						initialize I1
						if (ThreadNumber < LocalNumberOfThreads - 1)
							ThreadNumber += 1
						
		SetFileForThread is a Set Action
		
			Parameters
				PrmFileCreationSetup 				is like FileCreationSetup
				PrmFinanceEnterpriseGroup  	 				is like FinanceEnterpriseGroup
				PrmFileSystemTimeStamp				is TimeStamp
				PrmFileSequenceNumber				is Numeric 4
				PrmThread							is Numeric 2
			
			Instance Selection
				where (FileCreationSetup = PrmFileCreationSetup
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileSystemTimeStamp not entered
				and    FileSequenceNumber not entered
				and    Processed
				and    Thread = PrmThread)
				
			Action Rules
					
				Instance Rules
					FileSystemTimeStamp = PrmFileSystemTimeStamp
					FileSequenceNumber  = PrmFileSequenceNumber
					
		SetFileForThreadRegenerated is a Set Action
		
			Parameters
				PrmFileCreationSetup 				is like FileCreationSetup
				PrmFinanceEnterpriseGroup  	 				is like FinanceEnterpriseGroup
				PrmFileSystemTimeStamp				is TimeStamp
				PrmFileSequenceNumber				is Numeric 4
				PrmThread							is Numeric 2
				SystemTimeStampPrm				is TimeStamp
				SequenceNumberPrm				is Numeric 4
			
			Instance Selection
				where (FileCreationSetup = PrmFileCreationSetup
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileSystemTimeStamp = SystemTimeStampPrm
				and    FileSequenceNumber = SequenceNumberPrm
				and    Processed
				and    Thread = PrmThread)
				
			Action Rules
					
				Instance Rules
					FileSystemTimeStamp = PrmFileSystemTimeStamp
					FileSequenceNumber  = PrmFileSequenceNumber
						
		SetFile is a Set Action
		
			Parameters
				PrmFileCreationSetup 				is like FileCreationSetup
				PrmFinanceEnterpriseGroup  	 				is like FinanceEnterpriseGroup
				PrmFileSystemTimeStamp				is TimeStamp
				PrmFileSequenceNumber				is Numeric 4
				
			
			Instance Selection
				where (FileCreationSetup = PrmFileCreationSetup
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileSystemTimeStamp not entered
				and    FileSequenceNumber not entered
				and    Processed)
				
			Action Rules
					
				Instance Rules
					FileSystemTimeStamp = PrmFileSystemTimeStamp
					FileSequenceNumber  = PrmFileSequenceNumber
					
		SetFileRegenerated is a Set Action
		
			Parameters
				PrmFileCreationSetup 				is like FileCreationSetup
				PrmFinanceEnterpriseGroup  	 				is like FinanceEnterpriseGroup
				PrmFileSystemTimeStamp				is TimeStamp
				PrmFileSequenceNumber				is Numeric 4
				SystemTimeStampPrm				is TimeStamp
				SequenceNumberPrm				is Numeric 4
			
			Instance Selection
				where (FileCreationSetup = PrmFileCreationSetup
				and    FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileSystemTimeStamp = SystemTimeStampPrm
				and    FileSequenceNumber = SequenceNumberPrm
				and    Processed)
				
			Action Rules
					
				Instance Rules
					FileSystemTimeStamp = PrmFileSystemTimeStamp
					FileSequenceNumber  = PrmFileSequenceNumber
