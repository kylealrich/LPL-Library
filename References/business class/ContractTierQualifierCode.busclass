ContractTierQualifierCode is a BusinessClass
    owned by po
    prefix is CTQC

    Ontology
    	symbolic key is ContractTierQualifierCode    
    		    	
    Persistent Fields
    	ManufacturerInfo			is a Manufacturer  			 
    	CommodityCode    	
    	MajorPurchasingClass		 	
    	MinorPurchasingClass		
    	UnspscCode					is a UNSPSCCode
    	GMDNPTCode
		ItemCategory				is Numeric size 1
    		States
    			CommodityCode			value is 1
    			ManufacturerInformation	value is 2
				PurchasingClass			value is 3
    			UNSPSCCode				value is 4
				GMDNPTCode              value is 5
    
    Derived Fields
    	PurchClassMajorMinor is a StringField
    		type is Alpha 50
    		restricted
    		MajorPurchasingClass
    		MinorPurchasingClass

    Conditions
    
    	NoLineCodesUsed
    		restricted
    		when (!ContractLineTierMemberQualCodesUsedRel exists
    		and    ContractLineTierMemberQualRel exists)

		ManufacturerQualifier
            when (ManufacturerInfo exists)
        CommodityQualifier
            when (CommodityCode exists)
        UnspscCodeQualifier
            when (UnspscCode exists)
        MajorClassQualifier
            when (MajorPurchasingClass exists)
		ContractLinesForManufacturerExist 
			restricted
			when (ManufacturerQualifier
			and   ContractLinesByManufacturerRel exists)
		ContractLinesForPurchasingClassExist 
			restricted
			when (MajorClassQualifier
			and   ContractLinesByPurchasingClassRel exists)
		ContractLinesForCommodityCodeExist 
			restricted
			when (CommodityQualifier
			and   ContractLinesByCommodityCodeRel exists)
		ContractLinesForGMDNTermExist 
			restricted
			when (GMDNTermQualifier
			and   ContractLinesByGMDNTermRel exists)
		ContractLinesForUNSPSCCodeExist 
			restricted
			when (UnspscCodeQualifier
			and   ContractLinesByUNSPSCCodeRel exists)
		GMDNTermQualifier 
			restricted
			when (GMDNPTCode exists)
		UnspscCodeQualifierHasItems 
			restricted
			when (UnspscCodeQualifier
			and   UnspscCode.ItemsRel exists)
		GMDNTermQualifierHasItems 
			restricted
			when (GMDNTermQualifier
			and   GMDNPTCode.ItemRel exists)
		ManufacturerQualifierHasItems 
			restricted
			when (ManufacturerQualifier
			and   ManufacturerInfo.ActiveItemsRel exists)
		CommodityCodeQualifierHasItems
			restricted
			when (CommodityQualifier
			and   CommodityCode.ItemsRel exists) 
		MajorPurchasingClassQualifierHasItems 
			restricted
			when (MajorClassQualifier
			and   MajorPurchasingClass.ActiveItemInPurchasingMajorClassRel exists)

    Relations
    	ContractLineTierMemberQualCodesUsedRel
    		one-to-many relation to ContractLineTierMemberQual
            Field Mapping uses ByTierQual
          		related.ContractGroup		  = ContractGroup
           		related.Contract			  = Contract
           		related.Tier		  		  = ContractTier
           		related.ContractTierQualifier = ContractTierQualifier
           	Instance Selection
           		where (related.ItemCodesUsed)
           		
        ContractLineTierMemberQualRel
    		one-to-many relation to ContractLineTierMemberQual
            Field Mapping uses ByTierQual
          		related.ContractGroup		  = ContractGroup
           		related.Contract			  = Contract
           		related.Tier		  		  = ContractTier
           		related.ContractTierQualifier = ContractTierQualifier

		DuplicateCodeRel 
			one-to-many relation to ContractTierQualifierCode 
			Field Mapping uses ByCodeForAContract 
				related.ContractGroup 				= Contract 
				related.Contract                    = Contract 
    			related.ManufacturerInfo			= ManufacturerInfo   			 
    			related.CommodityCode    	        = CommodityCode 
    			related.MajorPurchasingClass		= MajorPurchasingClass 	
    			related.MinorPurchasingClass		= MinorPurchasingClass
    			related.UnspscCode					= UnspscCode
    			related.GMDNPTCode				    = GMDNPTCode

		ContractLinesByManufacturerRel 
			one-to-many relation to ContractLine 
			Field Mapping uses ByMfgInfoVenItem
				related.ContractGroup						= ContractGroup 
				related.Manufacturer.ManufacturerCode		= ManufacturerInfo.ManufacturerCode
				related.Manufacturer.ManufacturerDivision	= ManufacturerInfo.ManufacturerDivision
			Instance Selection 
				where (related.ItemType.Special)

		ContractLinesByPurchasingClassRel 
			one-to-many relation to ContractLine 
			Field Mapping uses ByPurchMinorClass
				related.ContractGroup						= ContractGroup 
				related.MajorPurchasingClass                = MajorPurchasingClass
			Instance Selection 
				where (related.ItemType.Special)				

		ContractLinesByCommodityCodeRel 
			one-to-many relation to ContractLine 
			Field Mapping uses ByCommodityCode
				related.ContractGroup						= ContractGroup 
				related.CommodityCode                       = CommodityCode 
			Instance Selection 
				where (related.ItemType.Special)
		ContractLinesByUNSPSCCodeRel 
			one-to-many relation to ContractLine 
			Field Mapping uses ByUnspscCode
				related.ContractGroup						= ContractGroup 
				related.UNSPSCCode                       	= UnspscCode 
			Instance Selection 
				where (related.ItemType.Special)
		ContractLinesByGMDNTermRel 
			one-to-many relation to ContractLine 
			Field Mapping uses ByGMDNTerm
				related.ContractGroup                       = ContractGroup 
				related.GMDNPTCode							= GMDNPTCode 
			Instance Selection 
				where (related.ItemType.Special)
	Sets
		ByItemCategory
			Sort Order
				ContractGroup
				Contract
				ContractTier
				ContractTierQualifier
				ItemCategory
				ContractTierQualifierCode

		ByCodeForAContract 
			Sort Order 
				ContractGroup 
				Contract 
    			ManufacturerInfo			  			 
    			CommodityCode    	
    			MajorPurchasingClass		 	
    			MinorPurchasingClass		
    			UnspscCode					
    			GMDNPTCode				
				ContractTierQualifierCode 

	Field Rules
			
	Actions
		Create is a Create Action
			restricted
			Action Rules
				constraint (DuplicateCodeRel !exists)
					"QualifierCodeAlreadyExistsForThisContract"
				
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
    		valid when (Contract.InProgress)
			Action Rules
				constraint (Contract.InProgress)
    				"ContractStatusCannotBeActive,InactiveOrClosedToDeleteTierQualifierItems"   	

