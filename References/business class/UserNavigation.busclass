UserNavigation is a BusinessClass
    owned by repository
    prefix is URNG
    framework type is LPLConfiguration

	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing
		implements DynamicCreation
		implements ConfigurationMetadata
		implements AuditLogEntryActions

    Ontology
        symbolic key is UserNavigation

    Persistent Fields
    	BusinessView
    	BusinessNavigation 	is LPLName
    		default label is "NavigationName"
		Status 			is a UserObjectStatus
    	LPL             is LPL
    	Description		is a ConfigDescription
    	UserEditorType
    	Independent		is Boolean 
    	Owner 			is like Actor 
    	LPLIndex		is Text  
    	ErrorText		is Text
    	





    Derived Fields
    	DerivedBusinessNavigation is a DerivedField
    		type is LPLName
    		restricted
    		if (BusinessNavigationRel exists)
    			return BusinessNavigation

    		return blank
    	
		ParseText is a NativeField
			type is Text
			
		BusinessClassViewMF is a MessageField
            "Business_Class"

        BusinessTaskViewMF is a MessageField
            "Business_Task"

        BusinessViewDefaultMF is a MessageField
            "View"

        BusinessViewLabel is a ConditionalField
            type is Alpha 20
            if (BusinessView.Type.BusinessClass) 
                BusinessClassViewMF
            else
            if (BusinessView.Type.BusinessTask)
                BusinessTaskViewMF
            else
                BusinessViewDefaultMF

		NewInitialLPL is a NativeField
		    type is Text
		    
		UpdIndIndexStamp is a NativeField
			type is Text
			restricted
			default label is "UpdateIndexStamp"


	Local Fields
		LocParseText 	    is Text
        LocBusinessView     is LPLName
        LocName             is LPLName
		
    Field Rules
    	BusinessView
    		required

    	BusinessNavigation
    		required
    			
		LPL
			required

			if (LPL changed)
				LPLIndex = blank

			if (Status.Active)
				LocParseText = ParseText

				if (not LocParseText = blank)
					confirmation required
						"<LocParseText>:_User_NavigationIsInvalid.__DoYouWantToSaveAnyway?"

					Status = Status.Invalid
					ErrorText = LocParseText

		UserEditorType
			default to UserEditorType.LPL
			
		Owner
			default to actor

		Status
			if (Status.Active)
				initialize ErrorText

    Conditions
		IsForUserBusinessView
			default label is untranslatable
			when (BusinessView.IsUserClass)
		
		IsNavigationDependent
			restricted
			when (BusinessNavigationRel exists
			and   not BusinessNavigationRel.Independent)

		IsNavigationIndependent
			restricted
			when (not BusinessNavigationRel exists
			or    BusinessNavigationRel.Independent)

		HasErrorText
			default label is "ErrorTextEntered"
			when (ErrorText entered)

	Relations
		BusinessNavigationRel
			one-to-one relation to BusinessNavigation
			Field Mapping uses symbolic key
				related.BusinessView = BusinessView
				related.BusinessNavigation = BusinessNavigation
				
		ConfigEntityRel
            one-to-one relation to ConfigEntity
            Field Mapping uses symbolic key
                related.ConfigEntity = UserNavigation

		LocalDuplicateRel
		    one-to-many relation to UserNavigation
			Field Mapping uses ByViewNavigation
		        related.BusinessView        = LocBusinessView
				related.BusinessNavigation  = LocName

	Actions
    	Create is a Create Action
    		Action Rules









				
				constraint (not BusinessNavigationRel exists)
					"Navigation<BusinessNavigation>AlreadyExists"
					
				Independent = true 

    		Exit Rules


			    if (Independent
    			and Status.Active)
					LocParseText = ParseText

				invoke FireEvent
    			
    	CreateFromSnapshot is a Create Action
			restricted
			bypass field rules

				
			Exit Rules

			    if (Independent
    			and Status.Active)
					LPLIndex = UpdIndIndexStamp
					
    	Update is an Update Action
    		Action Rules








				Independent = IsNavigationIndependent
				
				constraint (Independent)
					"CannotConfigureDeliveredNavigations"

     		Exit Rules


			    if (Independent
    			and Status.Active)
					LocParseText = ParseText

				invoke FireEvent
    			
    	UpdateFromSnapshot is an Update Action
			restricted
			bypass field rules

				
			Exit Rules

			    if (Independent
    			and Status.Active)
					LPLIndex = UpdIndIndexStamp
			
    	Delete is a Delete Action
    		Exit Rules
    			invoke FireEvent
    			
    	DeleteFromSnapshot is a Delete Action
			restricted
			bypass relational integrity rules

			
		Purge is a Purge Action
			restricted
    		Exit Rules
    			invoke FireEvent

    	Activate is an Instance Action
    		valid when (not Status.Active)
    		Action Rules

    			LocParseText = ParseText

				if (not LocParseText = blank)
					constraint(LocParseText = blank)
						"<LocParseText>:_User_NavigationIsInvalid._Cannot_Activate."

				Status = Status.Active
				initialize ErrorText

			Exit Rules



				if (Independent)
					LocParseText = ParseText 

    			invoke FireEvent

    	Deactivate is an Instance Action
    		valid when (not Status.Inactive)
    		Action Rules
    			Status = Status.Inactive

			Exit Rules
    			invoke FireEvent

    	Invalidate is an Instance Action
    		valid when (not Status.Invalid)
    		restricted
    		Action Rules
    			Status = Status.Invalid

			Exit Rules
    			invoke FireEvent

		FireEvent is an Instance Action
			restricted





















        CreateNewNavigation is a Create Action
            default label is "AddNewNavigation"
        	completion message is "NewNavigationCreated"
            Parameters
                BusinessView
                NavigationName is LPLName

			Parameter Rules
				BusinessView
					required
				NavigationName
					required

			Entrance Rules
				constraint (BusinessView.Type.BusinessClass)
					"CanOnlyCreateNewNavigationsForBusinessClasses"

                LocBusinessView = BusinessView
                LocName = NavigationName
                constraint (!LocalDuplicateRel exists)
                    "NavigationNameInUse_'<LocName>'"

            Action Rules
                invoke Create this instance
                    invoked.BusinessView 		= BusinessView
                    invoked.BusinessNavigation 	= NavigationName
                    invoked.Independent  		= true
                    invoked.LPL 				= this instance.NewInitialLPL

		TagItem is an Instance Action
			Parameters
				ParamConfigurationTag is a ConfigurationTag
					default label is "Tag"
			
			Entrance Rules
				if (Status.Inactive)
					confirmation required
						"UserNavigationIsInactive,TagAnyway?"
							
				if (Status.Invalid)
					confirmation required
						"UserNavigationIsInvalid,TagAnyway?"
							
			Action Rules
				invoke Create ConfigurationTagItem
					invoked.ConfigurationTag = ParamConfigurationTag
					invoked.ConfigurationTagItem.Reference = reference to this instance		  

    Sets
		ByViewNavigation
			sql name is UNVBYVNA
			indexed
			Sort Order
				BusinessView
				BusinessNavigation

