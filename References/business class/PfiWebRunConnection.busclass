PfiWebRunConnection is a BusinessClass
    owned by pfi
    prefix is PfiWR
	contains environment data
	disable data area copy
		preserve target data
    default label is "WebRunConnection"

    Ontology
        symbolic key is PfiWebRunConnection

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated

    Persistent Fields

        WebRoot  is Alpha size up to 1000
        User     is Alpha size 100
        Password is Password
        ConnTimeOut is Numeric size 2
            default label is "ConnectionTimeout_(Minutes)"
        TimeOut  is Numeric size 2
            default label is "RequestTimeout_(Minutes)"
        DataArea is a PfiLandmarkDataArea
        RetryCount is Numeric size 2
        RetryPauseTime is Numeric size 6
            default label is "PauseTime_(Milliseconds)"
        EnableTLS    is Boolean
            default label is "External_(ApplicableOnlyForTestConnection)"
        ResultSize is Numeric size 4
        	default label is "ResultSize_(Mi\B)"

        AuthType            is a PfiAuthType
            default label is "AuthorizationType"

        ServerType          is Numeric size 1
            States
                Landmark        value is 0
                External        value is 1
                InforLawson     value is 2

        ConsumerKey         is Password
        SecretKey           is Password
        LawsonConsumerKey   is Password
            default label is "ConsumerKey"
        LawsonSecretKey     is Password
            default label is "SecretKey"
        ServiceName         is AlphaUpper size 100
        RunAsUser           is a Actor
        LawsonRunAsUser     is Alpha size up to 200
        	default label is "RunAsUser"
        		
    Derived Fields

        AdminResultSize is a NativeField
    	    type is Numeric 4
            default label is "AdminResultSize_(Mi\B)"
        	
        IsInforServer is a DerivedField
            type is Boolean
            default label is "InforServer"
            if (ServerType = ServerType.External)
                return false
            return true
        	
        IsOAuth1aLandmark is a DerivedField
            type is Boolean
            default label is "O\Auth1aLandmark"
            if (AuthType = AuthType.OAuth10a and ServerType = ServerType.Landmark)
                return true
            return false
            
    Conditions
        CanDisplayWebRoot
            default label is "DisplayWebRoot"
            when (AuthType.Basic or (AuthType.OAuth10a and not ServerType.Landmark))
            
        CanDisplayUser
            default label is "DisplayUser"
            when (AuthType.Basic)
             
        CanDisplayRunAsUser
            default label is "DisplayRunAsUser"
            when (AuthType.OAuth10a and not ServerType.External)


    Field Rules
    
        ResultSize
        	default to 15
        	initial value is 15
        	constraint (PfiWebRunConnection.ResultSize > 0)
                "ValueMustBePositive"
			constraint (PfiWebRunConnection.AdminResultSize >= ResultSize)
				"ResultSizeCannotBeGreaterThanAdminResultSize"
				
        ConnTimeOut
            default to 1
            initial value is 1
            constraint (PfiWebRunConnection.ConnTimeOut > 0)
                        "ValueMustBePositive"
            
        TimeOut
            default to 2
            initial value is 2
            constraint (PfiWebRunConnection.TimeOut > 0)
                        "ValueMustBePositive"
            
        RetryCount
            default to 0
            initial value is 3
            constraint (PfiWebRunConnection.RetryCount >= 0)
                        "ValueMustBeGreaterOrEqualTo0"

        RetryPauseTime
            default to 1000
            initial value is 1000
            constraint (PfiWebRunConnection.RetryPauseTime > 0)
                        "ValueMustBePositive"
            
        WebRoot
            constraint (WebRoot[1:7] = "http://" or WebRoot[1:8] = "https://")
                        "InvalidProtocolSpecified"

        ConsumerKey
        	if (AuthType = AuthType.OAuth10a and ServerType.External)
        		required

        SecretKey
        	if (AuthType = AuthType.OAuth10a and ServerType.External)
        		required
        		
        ServiceName
        	if (AuthType = AuthType.OAuth10a and ServerType.InforLawson)
        		required
        		
        LawsonConsumerKey
        	if (AuthType = AuthType.OAuth10a and ServerType.InforLawson)
        		required
        		
        LawsonSecretKey
        	if (AuthType = AuthType.OAuth10a and ServerType.InforLawson)
        		required
        		
        RunAsUser
        	if (AuthType = AuthType.OAuth10a and not ServerType.External)
        		required
        
    Actions
        
        Create is a Create Action

        Update is an Update Action
        
        Delete is a Delete Action             
        
        TestConnection is an Instance Action
