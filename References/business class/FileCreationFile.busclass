FileCreationFile is a BusinessClass
    owned by filecreation
    prefix is FCFIL

    Patterns
        disable EffectiveDated
        disable AsOfDateProcessing
        disable Auditing

    Persistent Fields
    	FinanceEnterpriseGroup
    	FileCreationSetup
  			delete cascades
		SystemTimeStamp				is TimeStamp
		SequenceNumber				is Numeric 4
    	ExportFile					is a FileCreationAttachment
    	FileOutput					is Text
    	RowsOutputted				is Numeric 12
    	RecordsOutputted			is Numeric 12
    	RegeneratedFileTimeStamp	is TimeStamp
    	RegeneratedSequenceNumber	is Numeric 4
    	GroupedSystemTimeStamp		is TimeStamp
    	GroupedSequenceNumber 		is Numeric 4
    	Message						is Text
    	HeaderText					is Text
    	TrailerText					is Text
    	SectionTextHeader			is Text
    	SectionTextTrailer			is Text
    	TemporaryText				is Text	
    	RecordCount1				is Numeric 12
    	RecordCount2				is Numeric 12
    	RecordCount3				is Numeric 12
    	RecordCount4				is Numeric 12
    	RecordCount5				is Numeric 12
    	RecordCount6				is Numeric 12
    	ThreadGroupID				is UniqueID
    	ThreadNumber				is Numeric 2
    	NumberOfThreads				is Numeric 2
		MergedFile					is Boolean
		BaselineTest				is Boolean
		SubtotalExists				is Boolean
		FileCreationTotalArray
		GeneratedWithSortThread     is Boolean
		Count1						is Numeric 12
			restricted
		Count2						is Numeric 12
			restricted
		Count3						is Numeric 12
			restricted
		Count4						is Numeric 12
			restricted
		Count5						is Numeric 12
			restricted
		Count6						is Numeric 12
			restricted

	Local Fields
		LocalIONSource		is Alpha 300
			Text Variables
				FileName	value is ExportFile.Title
		LocalIONDocumentID	is Alpha 300
			Text Variables
				FileName	value is ExportFile.Title
		ErrorMessage			is Text
		IsError					is Boolean
		LocalCommitCounter		is Numeric 3
		LocalKey1				is Alpha 40
		LocalKey2				is Alpha 40
		
	Derived Fields
		NoOutputExistsMessage	is a MessageField
			restricted
			"NoOutputExists"
			
		OriginalBaselineDeletedMessage is a MessageField
			restricted
			"BaselineFileDeleted"

		NoRecordsMessage is a MessageField	
			restricted
			"NoRecordsToProcess"

	Conditions
		FileExists
			when (ExportFile.File entered)
		
		Regenerated
			when (RegeneratedFileTimeStamp entered
			and	  RegeneratedSequenceNumber entered)
		
			
		RegenerateThreadedActionValid
			when ((FileExists
			or     FileCreationSetup.UseForTesting)
			and   FileCreationRecordRel exists)
			
		SendToIONIsValid
			restricted
			when (ExportFile.File entered
			and   FileCreationSetup.AutomaticallySendFileToION
			and   FileCreationSetup.IONDocumentName entered)

	Relations
		FileCreationRecordRel
			one-to-many relation to FileCreationRecord
			Field Mapping uses ByRunSystemTimeStamp
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FileCreationSetup	= FileCreationSetup
				related.RunSystemTimeStamp	= SystemTimeStamp	        	 	        	

		RegeneratedFileRel
			one-to-one relation to FileCreationFile
			Field Mapping uses ByTimeStamp
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FileCreationSetup	= FileCreationSetup
				related.SystemTimeStamp		= RegeneratedFileTimeStamp
				related.SequenceNumber		= RegeneratedSequenceNumber
				
		GroupedFilesRel
			one-to-many relation to FileCreationFile
			Field Mapping uses ByGroupedTimeStamp
				related.FinanceEnterpriseGroup		    = FinanceEnterpriseGroup
				related.FileCreationSetup	    = FileCreationSetup
				related.GroupedSystemTimeStamp	= GroupedSystemTimeStamp
				related.GroupedSequenceNumber   = GroupedSequenceNumber
				
		BaselineTestFileRel
			one-to-many relation to FileCreationFile
			Field Mapping uses ByTimeStamp
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.FileCreationSetup		= FileCreationSetup
			Instance Selection
				where (related.BaselineTest		= true
				and    (related.SystemTimeStamp  != SystemTimeStamp
				or     (related.SystemTimeStamp   = SystemTimeStamp
				and     related.SequenceNumber   != SequenceNumber)))
				
		FileCreationTestBaselineRel
			one-to-many relation to FileCreationTest
			Field Mapping uses ByBaselineSystemTimeStamp
				related.BaselineTimeStamp		    = SystemTimeStamp
				related.BaselineSequenceNumber		= SequenceNumber 
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FileCreationSetup			= FileCreationSetup
			
		FileCreationTestBaselineFailedRel
			one-to-many relation to FileCreationTest
			Field Mapping uses ByBaselineSystemTimeStamp
				related.BaselineTimeStamp		    = SystemTimeStamp
				related.BaselineSequenceNumber		= SequenceNumber 
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FileCreationSetup			= FileCreationSetup
			Instance Selection
				where (related.Status  			= 1)
				
		FileCreationTestRel
			one-to-many relation to FileCreationTest
			Field Mapping uses BySystemTimeStamp
				related.SystemTimeStamp       = SystemTimeStamp
				related.SequenceNumber		  = SequenceNumber
				related.FinanceEnterpriseGroup        = FinanceEnterpriseGroup
				related.FileCreationSetup     = FileCreationSetup
				
		FileCreationTestFailedRel
			one-to-many relation to FileCreationTest
			Field Mapping uses BySystemTimeStamp
				related.SystemTimeStamp       = SystemTimeStamp
				related.SequenceNumber		  = SequenceNumber
				related.FinanceEnterpriseGroup        = FinanceEnterpriseGroup
				related.FileCreationSetup     = FileCreationSetup
			Instance Selection
				where (related.Status  			= 1)

		FileCreationSortThreadRel
			one-to-many relation to FileCreationSortThread
			Field Mapping uses ByKey
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.FileCreationSetup = FileCreationSetup
			Instance Selection
				where (related.FileSystemTimeStamp = SystemTimeStamp
				and    related.FileSequenceNumber  = SequenceNumber)

	Field Rules
		SequenceNumber
			autosequence using ByTimeStamp
				minimize contention	
			
		BaselineTest
			constraint (BaselineTestFileRel not exists)
				"OnlyOneFileCanBeDefinedAsTheBaseline"

	Sets
		ByTimeStamp
			primary
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				SystemTimeStamp
				SequenceNumber
				
		ByGroupedTimeStamp
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				GroupedSystemTimeStamp
				GroupedSequenceNumber
				SystemTimeStamp
				SequenceNumber
				
		ByThreadGroupID
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				ThreadGroupID
				ThreadNumber
				SystemTimeStamp
				SequenceNumber
				
		ByBaselineTest
			indexed
			Sort Order
				FinanceEnterpriseGroup
				FileCreationSetup
				BaselineTest
				SystemTimeStamp
				SequenceNumber
				
	Actions
		Create is a Create Action

		Update is an Update Action
																																																										
		Delete is a Delete Action
			valid when (not FileCreationSetup.UseForTesting)
			
			Action Rules
				
				if (FileCreationSortThreadRel exists)
				
					invoke PurgeRecords FileCreationSortThread
						invoked.PrmFileCreationSetup 				= FileCreationSetup
						invoked.PrmFinanceEnterpriseGroup  	 				= FinanceEnterpriseGroup
						invoked.PrmFileSystemTimeStamp				= SystemTimeStamp
						invoked.PrmFileSequenceNumber				= SequenceNumber
		
		Purge is a Purge Action
			restricted
			
			Action Rules
			
				if (FileCreationSortThreadRel exists)
			
					invoke PurgeRecords FileCreationSortThread
						invoked.PrmFileCreationSetup 				= FileCreationSetup
						invoked.PrmFinanceEnterpriseGroup  	 				= FinanceEnterpriseGroup
						invoked.PrmFileSystemTimeStamp				= SystemTimeStamp
						invoked.PrmFileSequenceNumber				= SequenceNumber
					
			Exit Rules
				if (FileCreationTestBaselineRel exists)
					invoke Update first FileCreationTestBaselineRel
						invoked.Message     += OriginalBaselineDeletedMessage
						
				if (FileCreationTestRel exists)
					invoke Purge first FileCreationTestRel
					
		SendToION is an Instance Action
			valid when (SendToIONIsValid)
			
			Parameters
				InstanceCountPrm	is Numeric 12
					default label is "InstanceCount"
					
			Parameter Rules
				InstanceCountPrm
					if (FileCreationSetup.IONInstanceCount.RecordCount)
						default to RecordsOutputted
					if (FileCreationSetup.IONInstanceCount.RowCount)
						default to RowsOutputted
			Local Fields
				BODXML is XMLDocument
				LocalIONSourceText		is Alpha 300
				LocalIonDocumentIDText	is Alpha 300
			Action Rules
				if (FileCreationSetup.IONSource entered)
					LocalIONSource = FileCreationSetup.IONSource
				else
					LocalIONSource = ExportFile.Title
				if (FileCreationSetup.IONDocumentID entered)
					LocalIONDocumentID = FileCreationSetup.IONDocumentID
				else
					LocalIONDocumentID = ExportFile.Title
				LocalIONSourceText      = LocalIONSource text
				LocalIonDocumentIDText	= LocalIONDocumentID text
				BODXML = ExportFile.File
				if (BODXML entered)
					send ion bod
						bod is BODXML
						bod type is FileCreationSetup.IONDocumentName
						document id is LocalIonDocumentIDText
						instance count is InstanceCountPrm
						source is LocalIONSourceText
						
		CreateIONFileAudit is an Instance Action
			restricted
			Parameters
				InstanceCountPrm	is Numeric 12
					default label is "InstanceCount"
				
			Parameter Rules
				InstanceCountPrm
					if (FileCreationSetup.IONInstanceCount.RecordCount)
						default to RecordsOutputted
					if (FileCreationSetup.IONInstanceCount.RowCount)
						default to RowsOutputted
			
			Local Fields
				LocalIONSourceText		is Alpha 300
				LocalIonDocumentIDText	is Alpha 300
			
			Action Rules
				if (FileOutput entered)
					if (FileCreationSetup.IONSource entered)
						LocalIONSource = FileCreationSetup.IONSource
					else
						LocalIONSource = ExportFile.Title
					if (FileCreationSetup.IONDocumentID entered)
						LocalIONDocumentID = FileCreationSetup.IONDocumentID
					else
						LocalIONDocumentID = ExportFile.Title
					LocalIONSourceText      = LocalIONSource text
					LocalIonDocumentIDText	= LocalIONDocumentID text
				
		AppendToFile is an Instance Action
			restricted
			Parameters
				PrmNewLine is Text
				PrmAppendTarget is Numeric 1
					States
						FileOutput 			value is 0
						SectionTextHeader 	value is 1
						SectionTextTrailer	value is 2
				PrmAppendSection is Boolean
				
			Local Fields
				LocalOutputExists  is Boolean
				
			Action Rules
				if (PrmAppendTarget.FileOutput)
					if (SubtotalExists)
						TemporaryText += PrmNewLine
					else
						FileOutput += PrmNewLine
				if (PrmAppendTarget.SectionTextHeader)
					SectionTextHeader += PrmNewLine
				if (PrmAppendTarget.SectionTextTrailer)
					SectionTextTrailer += PrmNewLine
				if (PrmAppendSection)
					initialize LocalOutputExists
					if (TemporaryText entered)
						LocalOutputExists = true
					if (LocalOutputExists)
						FileOutput += SectionTextHeader
					initialize SectionTextHeader
					FileOutput += TemporaryText
					initialize TemporaryText
					if (LocalOutputExists)
						FileOutput += SectionTextTrailer
					initialize SectionTextTrailer						
						
		UpdateRecordCount is an Instance Action
			restricted
			Parameters
				TotalRowCount			is Numeric 12
				TotalRecordCount		is Numeric 12
				FileName				is Alpha 150
				FileMimeType			is Text
				Regenerated				is Boolean
				ProcessRegeneratedFileWithProcessFlow	is Boolean
				SendRegeneratedFileToION				is Boolean
				PrmKey1					is Alpha 40
				PrmKey2					is Alpha 40
	
			Local Fields
				NewFile		 			is BinaryDocument
				LocalText				is Text
				LocalSetStatus			is Alpha 1
			
			Rule Blocks
				UpdateSupportExternallyLinkedFiles
					invoke Update {Relation}
						invoked.{PrintTime}			= SystemTimeStamp
						invoked.{SequenceNumber}	= SequenceNumber
						if (LocalSetStatus entered)
							invoked.{Status}		= LocalSetStatus

			Action Rules
				RowsOutputted						= TotalRowCount
				RecordsOutputted					= TotalRecordCount
				if (FileCreationSetup.GenerateBlankDocument
				and FileOutput not entered)
					FileOutput 						= FileCreationSetup.OutputTextForBlankDocument
					Message							= NoRecordsMessage
				
				if (HeaderText	entered
				or TrailerText  entered)
					LocalText   = HeaderText
					LocalText   += FileOutput
					LocalText   += TrailerText
					FileOutput 	 =  LocalText
					initialize HeaderText
					initialize TrailerText 
				NewFile 							= FileOutput
				if (not FileCreationSetup.UseForTesting)												
					if (FileOutput entered
					or  FileCreationSetup.GenerationBehaviorForNoOutput.GenerateFileRecordWithDocument)
						ExportFile.File						= NewFile 				
						ExportFile.MimeType					= FileMimeType
						ExportFile.Title					= FileName
						ExportFile.CurrentSystemTimeStamp 	= SystemTimeStamp	
						
						if (not FileOutput entered
						and Message not entered)
							Message = NoOutputExistsMessage
									
						if (FileCreationSetup.FlowToProcessFile entered
						and (not Regenerated
						or	 ProcessRegeneratedFileWithProcessFlow))
							trigger FileCreationSetup.FlowToProcessFile PA service
								Variables
									FinanceEnterpriseGroup
									FileCreationSetup
									SystemTimeStamp
									SequenceNumber
								title is "<FileCreationSetup.Description>:<FileCreationSetup>"
						else 
							if (FileCreationSetup.AutomaticallySendFileToION entered
							and (not Regenerated
							or	 SendRegeneratedFileToION))
								invoke SendToION
									if (FileCreationSetup.IONInstanceCount.RecordCount)
										invoked.InstanceCountPrm = RecordsOutputted
									if (FileCreationSetup.IONInstanceCount.RowCount)
										invoked.InstanceCountPrm = RowsOutputted

					else
						Message = NoOutputExistsMessage

				else
					if (BaselineTestFileRel exists
					and Regenerated)
						invoke Create FileCreationTest
							invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
							invoked.FileCreationSetup 	= FileCreationSetup
							invoked.SystemTimeStamp		= SystemTimeStamp
							invoked.SequenceNumber		= SequenceNumber
							invoked.TestOutput			= FileOutput
							
			Exit Rules
				invoke UpdateLastGeneration FileCreationSetup
					invoked.PrmSystemTimeStamp			= SystemTimeStamp
					invoked.PrmSequenceNumber		    = SequenceNumber
						
						

		UpdateRegenerationFields is an Instance Action
			restricted

			Parameters
				NewFileTimeStamp 		is TimeStamp
				NewSequenceNumber		is Numeric 4

			Action Rules
				RegeneratedFileTimeStamp	= NewFileTimeStamp
				RegeneratedSequenceNumber	= NewSequenceNumber

		RegenerateFileThreaded is an Instance Action
			default label is "Regenerate"
			valid when (RegenerateThreadedActionValid)
			
			Parameters
				ProcessRegeneratedFileWithProcessFlow	is Boolean
				SendRegeneratedFileToION				is Boolean
				GroupedSystemTimeStampPrm				is TimeStamp
				GroupedSequenceNumberPrm				is Numeric 4
			
			Parameter Rules
				ProcessRegeneratedFileWithProcessFlow
					constraint (FileCreationSetup.SendToProcessFlow)
						"AFlowIsNotDefinedOnThisSetup"
						
				SendRegeneratedFileToION
					constraint (FileCreationSetup.AutomaticallySendFileToION)
						"IONIsNotDefinedOnThisSetup"

			Local Fields
				ActionBackgroundGroup is UniqueID
				LocalActionRequestID 	is UniqueID
				LocalActionRequestID2 	is UniqueID
				LocalBackgroundGroup	is Alpha 200
						
			Action Rules
				constraint (NumberOfThreads entered)
					"ThisFileWasGeneratedUsingAction_Generate_File;PleaseUseAction_RegenerateInsteadOfAction_Regenerate_Threaded"
				if (first FileCreationRecordRel.ThreadingKey = blank)
					if (FileCreationSetup.UseDynamicRowProcessing)
						if (FileCreationSetup.RegenerateMappingsFlag.Needed)	
							invoke Update FileCreationSetup
								invoked.RegenerateMappingsFlag = 1
							invoke PurgeMappings FileCreationRowDynamicMapping
								assign async action request id to LocalActionRequestID 
								invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
								invoked.FileCreationSetupPrm = FileCreationSetup
							for each FileCreationSetup.FileCreationRowDetail set
								if (!each.Header
								and !each.Trailer
								and !each.Subtotal)
									if (each.LinkedSetup not entered
									and	each.LinkedRow not entered)
										invoke CreateDynamicRowLPL FileCreationColumnDetail in background group (LocalBackgroundGroup)
											run after LocalActionRequestID
											invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
											invoked.FileCreationSetupPrm = FileCreationSetup
											invoked.FileCreationRowDetailPrm = each.FileCreationRowDetail
											invoked.TargetFileCreationSetupPrm	= FileCreationSetup
											invoked.TargetFileCreationRowDetailPrm	= each.FileCreationRowDetail
									else
										invoke CreateDynamicRowLPL FileCreationColumnDetail in background group (LocalBackgroundGroup)
											run after LocalActionRequestID
											invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
											invoked.FileCreationSetupPrm = each.LinkedSetup
											invoked.FileCreationRowDetailPrm = each.LinkedRow
											invoked.TargetFileCreationSetupPrm	= FileCreationSetup
											invoked.TargetFileCreationRowDetailPrm	= each.FileCreationRowDetail
							invoke SetRegenerateMappingsFlagToNotNeeded FileCreationSetup in background
								assign async action request id to LocalActionRequestID2
								run after background group(LocalBackgroundGroup)
					invoke SetThreadingKeyOnRegenerate FileCreationRecord
						assign async action request id to ActionBackgroundGroup 
						run after LocalActionRequestID2
						invoked.FinanceEnterpriseGroupPrm    = FinanceEnterpriseGroup
						invoked.FileCreationSetupPrm 		 = FileCreationSetup
						invoked.SystemTimeStampPrm   		 = SystemTimeStamp
				if (not FileCreationSetup.UseForTesting)
					if (not MergedFile)
						if (FileCreationSetup.UseDynamicRowProcessing
						and not first FileCreationRecordRel.ThreadingKey = blank)
							if (FileCreationSetup.RegenerateMappingsFlag.Needed)	
								invoke Update FileCreationSetup
									invoked.RegenerateMappingsFlag = 1	
								invoke PurgeMappings FileCreationRowDynamicMapping
									assign async action request id to LocalActionRequestID 
									invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
									invoked.FileCreationSetupPrm = FileCreationSetup
								for each FileCreationSetup.FileCreationRowDetail set
									if (!each.Header
									and !each.Trailer
									and !each.Subtotal)
										if (each.LinkedSetup not entered
										and	each.LinkedRow not entered)
											invoke CreateDynamicRowLPL FileCreationColumnDetail in background group (LocalBackgroundGroup)
												run after LocalActionRequestID
												invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
												invoked.FileCreationSetupPrm = FileCreationSetup
												invoked.FileCreationRowDetailPrm = each.FileCreationRowDetail
												invoked.TargetFileCreationSetupPrm	= FileCreationSetup
												invoked.TargetFileCreationRowDetailPrm	= each.FileCreationRowDetail
										else
											invoke CreateDynamicRowLPL FileCreationColumnDetail in background group (LocalBackgroundGroup)
												run after LocalActionRequestID
												invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
												invoked.FileCreationSetupPrm = each.LinkedSetup
												invoked.FileCreationRowDetailPrm = each.LinkedRow
												invoked.TargetFileCreationSetupPrm	= FileCreationSetup
												invoked.TargetFileCreationRowDetailPrm	= each.FileCreationRowDetail
								invoke SetRegenerateMappingsFlagToNotNeeded FileCreationSetup in background
									assign async action request id to ActionBackgroundGroup
									run after background group(LocalBackgroundGroup)
						invoke GenerateFileThread FileCreationRecord in background
							run after ActionBackgroundGroup
							invoked.FinanceEnterpriseGroupPrm						= FinanceEnterpriseGroup
							invoked.FileCreationSetupPrm					= FileCreationSetup
							invoked.SystemTimeStampPrm						= SystemTimeStamp
							invoked.SequenceNumberPrm						= SequenceNumber
							invoked.ProcessRegeneratedFileWithProcessFlow	= ProcessRegeneratedFileWithProcessFlow
							invoked.SendRegeneratedFileToION				= SendRegeneratedFileToION	
							invoked.GroupedSystemTimeStampRegenPrm 			= GroupedSystemTimeStamp
							invoked.GroupedSequenceNumberRegenPrm 			= GroupedSequenceNumber
							invoked.GroupedSystemTimeStampPrm      			= GroupedSystemTimeStampPrm
							invoked.GroupedSequenceNumberPrm       			= GroupedSequenceNumberPrm
							invoked.PrmNumberOfThreads	    				= NumberOfThreads
							invoked.PrmThread	   							= ThreadNumber
							invoked.PrmGeneratedWithSortThread              = GeneratedWithSortThread
					else
						invoke GenerateFileThreaded FileCreationSetup in background
							run after ActionBackgroundGroup
							invoked.Regenerated								= true
							invoked.SystemTimeStampPrm						= SystemTimeStamp
							invoked.SequenceNumberPrm						= SequenceNumber
							invoked.ProcessRegeneratedFileWithProcessFlow	= ProcessRegeneratedFileWithProcessFlow
							invoked.SendRegeneratedFileToION				= SendRegeneratedFileToION	
							invoked.GroupedSystemTimeStampRegenPrm 			= GroupedSystemTimeStamp
							invoked.GroupedSequenceNumberRegenPrm  			= GroupedSequenceNumber
							invoked.GroupedSystemTimeStampPrm      			= GroupedSystemTimeStampPrm
							invoked.GroupedSequenceNumberPrm       			= GroupedSequenceNumberPrm
							invoked.PrmNumberOfThreads						= NumberOfThreads
							invoked.PrmGeneratedWithSortThread              = GeneratedWithSortThread
				else	
					if (not MergedFile)
						if (FileCreationSetup.UseDynamicRowProcessing
						and not first FileCreationRecordRel.ThreadingKey = blank)
							if (FileCreationSetup.RegenerateMappingsFlag.Needed)	
								invoke Update FileCreationSetup
									invoked.RegenerateMappingsFlag = 1	
								invoke PurgeMappings FileCreationRowDynamicMapping
									assign async action request id to LocalActionRequestID 
									invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
									invoked.FileCreationSetupPrm = FileCreationSetup
								for each FileCreationSetup.FileCreationRowDetail set
									if (!each.Header
									and !each.Trailer
									and !each.Subtotal)
										if (each.LinkedSetup not entered
										and	each.LinkedRow not entered)
											invoke CreateDynamicRowLPL FileCreationColumnDetail in background group (LocalBackgroundGroup)
												run after LocalActionRequestID
												invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
												invoked.FileCreationSetupPrm = FileCreationSetup
												invoked.FileCreationRowDetailPrm = each.FileCreationRowDetail
												invoked.TargetFileCreationSetupPrm	= FileCreationSetup
												invoked.TargetFileCreationRowDetailPrm	= each.FileCreationRowDetail
										else
											invoke CreateDynamicRowLPL FileCreationColumnDetail in background group (LocalBackgroundGroup)
												run after LocalActionRequestID
												invoked.FinanceEnterpriseGroupPrm = FinanceEnterpriseGroup
												invoked.FileCreationSetupPrm = each.LinkedSetup
												invoked.FileCreationRowDetailPrm = each.LinkedRow
												invoked.TargetFileCreationSetupPrm	= FileCreationSetup
												invoked.TargetFileCreationRowDetailPrm	= each.FileCreationRowDetail
								invoke SetRegenerateMappingsFlagToNotNeeded FileCreationSetup in background
									assign async action request id to ActionBackgroundGroup
									run after background group(LocalBackgroundGroup)
						invoke GenerateFileThread FileCreationRecord in background
							run after ActionBackgroundGroup
							resume on error
								ErrorMessage = error message
								IsError		 = true
							invoked.FinanceEnterpriseGroupPrm						= FinanceEnterpriseGroup
							invoked.FileCreationSetupPrm					= FileCreationSetup
							invoked.SystemTimeStampPrm						= SystemTimeStamp
							invoked.SequenceNumberPrm						= SequenceNumber
							invoked.ProcessRegeneratedFileWithProcessFlow	= ProcessRegeneratedFileWithProcessFlow
							invoked.SendRegeneratedFileToION				= SendRegeneratedFileToION	
							invoked.GroupedSystemTimeStampRegenPrm 			= GroupedSystemTimeStamp
							invoked.GroupedSequenceNumberRegenPrm 			= GroupedSequenceNumber
							invoked.GroupedSystemTimeStampPrm      			= GroupedSystemTimeStampPrm
							invoked.GroupedSequenceNumberPrm       			= GroupedSequenceNumberPrm
							invoked.PrmNumberOfThreads	    				= NumberOfThreads
							invoked.PrmThread	   							= ThreadNumber
							invoked.PrmGeneratedWithSortThread              = GeneratedWithSortThread
					else
						invoke GenerateFileThreaded FileCreationSetup in background
							run after ActionBackgroundGroup
							resume on error
								ErrorMessage = error message
								IsError		 = true
							invoked.Regenerated								= true
							invoked.SystemTimeStampPrm						= SystemTimeStamp
							invoked.SequenceNumberPrm						= SequenceNumber
							invoked.ProcessRegeneratedFileWithProcessFlow	= ProcessRegeneratedFileWithProcessFlow
							invoked.SendRegeneratedFileToION				= SendRegeneratedFileToION	
							invoked.GroupedSystemTimeStampRegenPrm 			= GroupedSystemTimeStamp
							invoked.GroupedSequenceNumberRegenPrm  			= GroupedSequenceNumber
							invoked.GroupedSystemTimeStampPrm      			= GroupedSystemTimeStampPrm
							invoked.GroupedSequenceNumberPrm       			= GroupedSequenceNumberPrm
							invoked.PrmNumberOfThreads						= NumberOfThreads
							invoked.PrmGeneratedWithSortThread              = GeneratedWithSortThread
					if (IsError)
						invoke Create FileCreationTest
							invoked.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
							invoked.FileCreationSetup 	= FileCreationSetup
							invoked.SystemTimeStamp		= system current timestamp
							invoked.Message 			= ErrorMessage
							invoked.Status       		= 2 
		
		PurgeFiles is a Set Action
			restricted
			run in background
			synchronized on untranslatable:"FileCreationSetup_PurgeFileCreationRecordOrFile_<PrmFinanceEnterpriseGroup>_<Thread>"
			
			Parameters
				PrmFinanceEnterpriseGroup		is like FinanceEnterpriseGroup
				PrmFileCreationSetup	is like FileCreationSetup
				PrmPurgeDate			is TimeStamp
				NumberOfThreads			is Numeric 3
				Thread					is Numeric 3
				
			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and    FileCreationSetup = PrmFileCreationSetup
				and	   SystemTimeStamp <= PrmPurgeDate
				and   (NumberOfThreads <= 1
				or	  (SequenceNumber % NumberOfThreads) = Thread))
				
			Action Rules
				Instance Rules
					invoke Purge		
			
		
		PurgeByPass is a Purge Action
			restricted
			bypass relational integrity rules	 
			Action Rules
				
				if (FileCreationSortThreadRel exists)
					invoke PurgeRecords FileCreationSortThread
						invoked.PrmFileCreationSetup 				= FileCreationSetup
						invoked.PrmFinanceEnterpriseGroup  	 				= FinanceEnterpriseGroup
						invoked.PrmFileSystemTimeStamp				= SystemTimeStamp
						invoked.PrmFileSequenceNumber				= SequenceNumber
			
		MarkAsBaselineForTest is an Instance Action
			default label is "MarkAsBaseline"
			valid when (FileCreationSetup.UseForTesting)
			Action Rules
				if (BaselineTestFileRel exists)
					confirmation required
						"CurrentBaselineFileWillBeDisabled;Continue?"
					invoke Update BaselineTestFileRel
						invoked.BaselineTest = false
			Exit Rules
				invoke UpdateBypass
					invoked.BaselineTest = true
					
		UpdateBypass is an Update Action
			restricted
			bypass field rules
			
		PurgeTestFileCreationFile is a Set Action
			restricted
			
			Parameters
				PurgeDatePrm					is TimeStamp
				
			Instance Selection
				where (FileCreationSetup.UseForTesting
				and    FileCreationSetup.BaselineFileCreationFileRel exists
				and    SystemTimeStamp <= PurgeDatePrm
				and    not BaselineTest
				and    FileCreationTestFailedRel not exists
				and    FileCreationTestBaselineFailedRel not exists)
														
			Action Rules
				
				Instance Rules
					invoke Purge 
					
					
			
					
			
				
					
		
