ContractImportTier is a BusinessClass
    owned by po
    prefix is CTI

    Ontology
    	symbolic key is ContractImportTier
    			    			
    Patterns
		disable Auditing 
       	disable EffectiveDated
                      
    Persistent Fields
    	TierTitle					is a Description
    	Description					is a Description4
		TierMarkupDiscount			is AlphaUpper size 1
			States
    			Markup      			value is "M"
    			Discount      			value is "D"
    			Fixed					value is "F"    			    			
		TierPricingAmtPct
		FreightPricingAmtPct
    	LastContractTierQualifier	is Numeric size 6
    		disable Auditing
		FreightMarkupDiscount		is AlphaUpper size 1
			States
    			Markup      			value is "M"
    			Discount      			value is "D"
    	GPOContractNumber 
				    			   			
	Local Fields
		FromGHX                     is Boolean
		
	Transient Fields
    	
	Conditions
		NoTierPricing
			restricted
			when (TierPricingAmtPct.TierPercent = 0
			and   TierPricingAmtPct.TierCost = 0)			
		NoFreightPricing
			restricted
			when (FreightPricingAmtPct.FreightPercent = 0
			and   FreightPricingAmtPct.FreightCost = 0)			
		TierPricingEntered
			restricted
			when (TierPricingAmtPct.TierPercent > 0
			or    TierPricingAmtPct.TierCost > 0)			
		FreightPricingEntered
			restricted
			when (FreightPricingAmtPct.FreightPercent > 0
			or    FreightPricingAmtPct.FreightCost > 0)			
		TierMarkupDiscountEntered
			restricted
			when (TierMarkupDiscount.Markup
			or    TierMarkupDiscount.Discount)			
		FreightMarkupDiscountEntered
			restricted
			when (FreightMarkupDiscount.Markup
			or    FreightMarkupDiscount.Discount)
		FixedTier
			restricted
			when (TierMarkupDiscount.Fixed)
		TierMembersExist
			restricted
			when (TierMembersRel exists)
    	NoQualifier
    		restricted
    		when (ContractImportTierQualifier set !exists)
   		GPOContractNumberEntered
   			restricted
   			when (GPOContractNumber entered)
		GPOIsVizient
			restricted
			when (ContractImport.GPOIsVizient)
		ToContractExists
			restricted
			when (ContractImport.DerivedToContractForGPO entered)
   			 	
	Relations            	 

  		TierMembersRel
    		one-to-many relation to ContractImportTierMember
            Field Mapping uses  symbolic key            
          		related.ContractGroup					= ContractGroup
           		related.ContractImport					= ContractImport
           	Instance Selection
           		where (related.Tier						= ContractImportTier)     
		
		GPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber               = GPOContractNumber
			Instance Selection
				where (related.TierLevel = ContractImportTier)
		
		TierContractRel 
			one-to-many relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup   = ContractGroup
				related.ContractImport  = ContractImport
				
    	ContractTierFixedRel
    		one-to-many relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup   = ContractGroup
				related.ContractImport  = ContractImport
			Instance Selection
				where (related.TierMarkupDiscount.Fixed)	
				
    	ContractTierNonFixedRel
    		one-to-many relation to ContractImportTier
			Field Mapping uses symbolic key
				related.ContractGroup   = ContractGroup
				related.ContractImport  = ContractImport
			Instance Selection
				where (related.TierMarkupDiscountEntered)
				
		ContractTierRel
			one-to-one relation to ContractTier
			Field Mapping uses symbolic key
				related.ContractGroup   = ContractGroup
				related.Contract        = ContractImport.DerivedToContractForGPO
				related.ContractTier    = ContractImportTier
    	
	Sets
		
		ByGPOContractNumber
			Sort Order
				ContractGroup
				GPOContractNumber
				ContractImportTier
				ContractImport 
			Instance Selection
				where (GPOContractNumberEntered)

		ByTierName 
			Sort Order
				ContractGroup
				GPOContractNumber
				TierTitle
				ContractImportTier
				ContractImport 
			Instance Selection
				where (GPOContractNumberEntered)

	Field Rules
		TierTitle
			required
				"TitleIsRequired"    			    				
		TierMarkupDiscount
			default to TierMarkupDiscount.Fixed
			if (TierMarkupDiscount.Fixed)
				initialize TierPricingAmtPct
			if (TierMarkupDiscountEntered)
				constraint (TierPricingEntered)
					"TierCostOrPercentIsRequired"
			if (TierMarkupDiscount.Fixed)
    			constraint (NoTierPricing)
    				"CannotEnterTierCostOrPercentWhenFixedIsSelected"
    		else			
				if (TierPricingEntered)
					constraint (TierMarkupDiscountEntered)
						"TierCostOrPercentCannotBeEnteredUnlessTypeIsMarkupOrDiscount"
		TierPricingAmtPct
			
			if (TierMarkupDiscount.Markup
			or  TierMarkupDiscount.Discount)
    			constraint (TierPricingEntered)
    				"TierCostOrPercentIsRequired"
			if (TierPricingEntered)
				constraint (TierMarkupDiscountEntered)
					"TierCostOrPercentCannotBeEnteredUnlessTypeIsMarkupOrDiscount"
		FreightMarkupDiscount
			if (FreightMarkupDiscountEntered)
				constraint (FreightPricingEntered)
					"FreightCostOrPercentIsRequired"
		FreightPricingAmtPct
			if (FreightMarkupDiscount.Markup
			or  FreightMarkupDiscount.Discount)
    			constraint (FreightPricingEntered)
    				"FreightCostOrPercentIsRequired"
			if (FreightPricingEntered)
				constraint (FreightMarkupDiscountEntered)
					"FreightCostOrPercentCannotBeEnteredUnlessTypeIsMarkupOrDiscount"
    				
	Actions
    	Create is a Create Action
    		Action Rules
    			if (TierMarkupDiscount.Discount)
    				constraint (TierPricingAmtPct.TierCost = 0)
    					"DiscountMustBeByPercentForHeaderLevelDefaulting"
           			constraint (TierPricingAmtPct.TierPercent < 1.0)
                		"ForDiscounts,TierPercentMustBeLessThan100%"
    			if (FreightMarkupDiscount.Discount)
    				constraint (FreightPricingAmtPct.FreightCost = 0)
    					"DiscountMustBeByPercentForHeaderLevelDefaulting"    					
           			constraint (FreightPricingAmtPct.FreightPercent < 1.0)
               			"ForDiscounts,FreightPercentMustBeLessThan100%"

               	if (ToContractExists
               	and FromGHX = true)
               		invoke Create ContractTier
						fill in fields from this instance
						invoked.ContractGroup              = ContractGroup
						invoked.Contract		  		   = ContractImport.DerivedToContractForGPO
						invoked.ContractTier               = ContractImportTier               			

			Exit Rules
               	if (ContractImport.GPOIsHealthTrust2021)
               		if (ContractImportTier = TierTitle)
               			if (GPOParticipantRel exists)
               				TierTitle = first GPOParticipantRel.TierName 			

    	Update is an Update Action
    		Action Rules
    			if (TierMarkupDiscount.Discount)
    				constraint (TierPricingAmtPct.TierCost = 0)
    					"DiscountMustBeByPercentForHeaderLevelDefaulting"
           			constraint (TierPricingAmtPct.TierPercent < 1.0)
                		"ForDiscounts,TierPercentMustBeLessThan100%"
    			if (FreightMarkupDiscount.Discount)
    				constraint (FreightPricingAmtPct.FreightCost = 0)
    					"DiscountMustBeByPercentForHeaderLevelDefaulting"  					
           			constraint (FreightPricingAmtPct.FreightPercent < 1.0)
               			"ForDiscounts,FreightPercentMustBeLessThan100%"
 								
    	Delete is a Delete Action
								

			Action Rules
				constraint (!TierMembersExist)
					"CannotDelete;TierMembersExist"
