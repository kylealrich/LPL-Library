ReceivableProcessLevel is a BusinessClass
	owned by ar
	prefix is APV
	classic name is ARPROCLEVL

	Ontology
		symbolic key is ReceivableProcessLevel
			classic set name is APVSET1
			classic name is PROCESS-LEVEL

	Patterns
		implements StaticJava
		disable AuditIndex

	Attach Rules
		if (parentcontext.name != "ReceivableGLDistribution")	
			constraint (IsActive)
				"ReceivableProcessLevelIsNotActive"	


	Persistent Fields
		Name
		PostalAddress									is a PostalAddressV2	
			holds pii
			classic name for PostalAddress.DeliveryAddress.AddressLine1 is ADDR1
			classic name for PostalAddress.DeliveryAddress.AddressLine2 is ADDR2
			classic name for PostalAddress.DeliveryAddress.AddressLine3 is ADDR3
			classic name for PostalAddress.DeliveryAddress.AddressLine4 is ADDR4
			classic name for PostalAddress.Municipality is CITY
			classic name for PostalAddress.StateProvince is STATE
			classic name for PostalAddress.PostalCode is ZIP
			classic name for PostalAddress.Country is COUNTRY-CODE
			classic name for PostalAddress.Region is REGION
		ReceivableGeneralLedgerCode
			classic name is AR-CODE
			default label is "ReceivableGlobalLedgerCode"
		ReceivedNotAppliedReceivableGeneralLedgerCode	is a ReceivableGeneralLedgerCode
			sql name is RNAReceivableGeneralLedgerCode
			classic name is NOT-APPLD-CD
			default label is "ReceivedNotAppliedReceivableGlobalLedgerCode"
		PrepaymentReceivableGeneralLedgerCode			is a ReceivableGeneralLedgerCode
			sql name is PReceivableGeneralLedgerCode
			classic name is PRE-PAY-CODE
			default label is "PrepaymentReceivableGlobalLedgerCode"
		ProformaPrepaymentReceivableGeneralLedgerCode	is a ReceivableGeneralLedgerCode
			default label is "ProformaPrepaymentReceivableGlobalLedgerCode"		
		ProformaTaxInvoiceCode							is a ReceivableGeneralLedgerCode
		CashCode
		FinanceChargeReceivableGeneralLedgerCode		is a ReceivableGeneralLedgerCode
			sql name is FCReceivableGeneralLedgerCode
			classic name is FIN-GL-CODE
			default label is "FinanceChargeReceivableGlobalLedgerCode"
		BOEAcceptanceReceivableGeneralLedgerCode		is a ReceivableGeneralLedgerCode
			sql name is BOEARGeneralLedgerCode
			classic name is DT-ACCPT-CODE
			default label is "BOEAcceptanceReceivableGlobalLedgerCode"
		BOEFinancialRevenueReceivableGeneralLedgerCode	is a ReceivableGeneralLedgerCode
			sql name is BOEFRRGeneralLedgerCode
			classic name is FIN-REV-CODE
			default label is "BOEFinancialRevenueReceivableGlobalLedgerCode"
		BOEFinancialExpenseReceivableGeneralLedgerCode	is a ReceivableGeneralLedgerCode
			sql name is BOEFERGeneralLedgerCode
			classic name is FIN-EXP-CODE
			default label is "BOEFinancialExpenseReceivableGlobalLedgerCode"
		BOEDishonoredExpenseReceivableGeneralLedgerCode is a ReceivableGeneralLedgerCode
			sql name is BOEDERGeneralLedgerCode
			classic name is DIS-EXP-CODE
			default label is "BOEDishonoredExpenseReceivableGlobalLedgerCode"
		AccountingUnitFinanceCodeBlock					is a FinanceCodeBlock

		ActiveStatus									is AlphaUpper size 1
			States
				Active		value is "A"
				Inactive	value is "I"
		UsedFlag										is Boolean
			classic name is USED-FL
		CurrencyRoundingReceivableGeneralLedgerCode		is a ReceivableGeneralLedgerCode
			sql name is CRReceivableGeneralLedgerCode
			classic name is ROUNDING-CODE
			default label is "CurrencyRoundingReceivableGlobalLedgerCode"
		NatureOfTransactionCode							is a IntrastatNatureOfTransactionCode
			classic name is NOTC
		CompanyCashCode
		DefaultProcessLevel       						is Boolean



	Derived Fields
		ReceivableProcessLevelAndName is a MessageField	
			restricted
			default label is "ProcessLevel"
			"<Name>_(<ReceivableProcessLevel>)"
		RepresentativeText is a StringField
			type is Text
			default label is "ProcessLevel"
			ReceivableProcessLevel " - " Name

	Field Rules





		CompanyCashCode
			required

		ActiveStatus
			default to "A"
			if (CashManagementAccountBankStatementImportRelExists)	
				constraint (ActiveStatus.Active)	
					"CannotUpdate_\Active_\StatusTo_\Inactive.Receivable_\Process_\LevelExistsOn_\Cash_\Management_\Account<first CashManagementAccountBankStatementImportRel.CashManagementAccount>."	
			if (CashManagementAccountThirdPartyPaymentProcessingRelExists)	
				constraint (ActiveStatus.Active)	
					"CannotUpdate_\Active_\StatusTo_\Inactive.Receivable_\Process_\LevelExistsOn_\Cash_\Management_\Account<first CashManagementAccountThirdPartyPaymentProcessingRel.CashManagementAccount>."	

		Name
			default to Company.Name	

		ReceivableGeneralLedgerCode
			default to Company.ReceivableGeneralLedgerCode
			required	
				"Receivable_\Global_\Ledger_\CodeIsRequiredFor_\Receivable_\Company<Company>_\Receivable_\Process_\Level<ReceivableProcessLevel>."	

		ReceivedNotAppliedReceivableGeneralLedgerCode
			default to Company.ReceivedNotAppliedReceivableGeneralLedgerCode
			required	
				"Received_\Not_\Applied_\Receivable_\Global_\Ledger_\CodeIsRequiredFor_\Receivable_\Company<Company>_\Receivable_\Process_\Level<ReceivableProcessLevel>."	

		PrepaymentReceivableGeneralLedgerCode
			default to Company.PrepaymentReceivableGeneralLedgerCode
			required	
				"Prepayment_\Receivable_\Global_\Ledger_\CodeIsRequiredFor_\Receivable_\Company<Company>_\Receivable_\Process_\Level<ReceivableProcessLevel>."	

		ProformaPrepaymentReceivableGeneralLedgerCode
			default to Company.ProformaPrepaymentReceivableGeneralLedgerCode

		ProformaTaxInvoiceCode
			default to Company.ProformaTaxInvoiceCode

		FinanceChargeReceivableGeneralLedgerCode
			default to Company.FinanceChargeReceivableGeneralLedgerCode

		BOEAcceptanceReceivableGeneralLedgerCode
			default to Company.BOEAcceptanceReceivableGeneralLedgerCode
			if (IsBOECompany)
				required
					"FinancialRevenueCodeRequired"									

		BOEFinancialRevenueReceivableGeneralLedgerCode
			default to Company.BOEFinancialRevenueReceivableGeneralLedgerCode

		BOEFinancialExpenseReceivableGeneralLedgerCode
			default to Company.BOEFinancialExpenseReceivableGeneralLedgerCode

		BOEDishonoredExpenseReceivableGeneralLedgerCode
			default to Company.BOEDishonoredExpenseReceivableGeneralLedgerCode

		CurrencyRoundingReceivableGeneralLedgerCode
			if (Company.MultiCurrencyProcessing)
				constraint (CurrencyRoundingReceivableGeneralLedgerCode entered)
					"Rounding_\G\LCodeRequired;CompanyIsMultiCurrency"				

			if (!Company.MultiCurrencyProcessing)
				constraint (CurrencyRoundingReceivableGeneralLedgerCode not entered)
					"CannotAdd_\Rounding_\G\L_\Code;CompanyIsNotMultiCurrency"		

		AccountingUnitFinanceCodeBlock
			if (AccountingUnitFinanceCodeBlock.ToAccountingEntity entered)
				constraint (AccountingUnitFinanceCodeBlock.ToAccountingEntity = Company.AccountingEntity)
					"CodeBlockAccountingEntityMustBeTheSameAsTheCompanyAccountingEntityOf<Company.AccountingEntity>"

		DefaultProcessLevel
			if (DefaultProcessLevel)
				constraint (DefaultReceivableProcessLevelRel not exists)
					"Default_\Process_\Level<first DefaultReceivableProcessLevelRel.ReceivableProcessLevel>AlreadyExists"
				constraint (ActiveStatus.Active)
					"CannotUpdate_\Active_\StatusTo_\InactiveForThe_\Default_\Process_\Level"

	Create Rules


















		if (PostalAddress not entered)
			PostalAddress = Company.PostalAddress

	Relations

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company

		ArsummaryRel
			one-to-many relation to ReceivableTransactionSummary
			delete restricted
			Field Mapping uses symbolic key
				related.Company					 = Company
				related.ReceivableProcessLevel	 = ReceivableProcessLevel

		CompanyCashCodeRel
			one-to-one relation to CompanyCashCode
			required
			Field Mapping uses symbolic key
				related.Company					 = Company
				related.CashCode				 = CashCode

		CashManagementAccountBankStatementImportRel	
			one-to-many relation to CashManagementAccount							
			required																
			Field Mapping uses symbolic key											
				related.CashManagementGroup		= Company.FinanceEnterpriseGroup	
			Instance Selection														
				where (related.ReceivableProcessLevel	= ReceivableProcessLevel)	

		CashManagementAccountThirdPartyPaymentProcessingRel	
			one-to-many relation to CashManagementAccount	
			Field Mapping uses symbolic key	
				related.CashManagementGroup		= Company.FinanceEnterpriseGroup	
			Instance Selection	
				where (related.ThirdPartyPaymentProcessingReceivableProcessLevel	= ReceivableProcessLevel)	

		DefaultReceivableProcessLevelRel
			one-to-many relation to ReceivableProcessLevel
			Field Mapping uses symbolic key
				related.Company 						 = Company
			Instance Selection
				where (related.DefaultProcessLevel
				and    related.ReceivableProcessLevel	!= ReceivableProcessLevel)


	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)

		IsBOECompany
			restricted
			when (Company.BOEAcceptanceReceivableGeneralLedgerCode entered)

		IsActive
			restricted
			when (ActiveStatus.Active)

		CashManagementAccountBankStatementImportRelExists	
			when (CashManagementAccountBankStatementImportRel exists)	

		CashManagementAccountThirdPartyPaymentProcessingRelExists	
			when (CashManagementAccountThirdPartyPaymentProcessingRel exists)	



	Actions

		Create is a Create Action

		Update is an Update Action

		FastUpdate is an Update Action
			bypass field rules
			restricted

		Delete is a Delete Action


