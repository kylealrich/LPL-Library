IMSReceiver is a BusinessClass
    owned by la
    prefix is IMSR
    stored in environment
	default label is "IncomingIMS_Messages"
	
    Ontology
    	symbolic key is IMSReceiver

	Patterns
	    implements CreateStamp
		disable AsOfDateProcessing
		disable EffectiveDated

    Persistent Fields
    	MessageId				is an IONBODMessageId
    	DocumentName        	is an IONDocumentName    	
    	FromLogicalId       	is an IONLogicalId
    	ToLogicalId         	is an IONLogicalId
    	Document				is an IMSDocument
    	AdditionalProperties	is an IONAdditionalProperties
		LastProcessingTime	is TimeStamp
        FailureCount 		is Numeric size 6
        Error 				is Alpha size 300
        ErrorMessage 		is Text
        	disable Auditing
		Loopback            is Boolean
		State               is Numeric size 1
			States
				New       			value is 0
					default label is untranslatable
				Complete 			value is 3
					default label is untranslatable
				MissingProcessor 	value is 4
					default label is untranslatable
				Failed	  			value is 5
					default label is untranslatable
				Canceled			value is 6
					default label is untranslatable
				FailedConfirm		value is 7
					default label is untranslatable
				
		ResponseMessage is an IONOutboxQueue
		RawMessage is JSONObject
		AsyncActionRequest
			delete ignored

	Transient Fields
	    TransientShowEncodedValue is Boolean

	Conditions
		CanReset
			default label is "BODResettable"
			when (State.MissingProcessor or State.Failed)
			
		IsTargetDataArea
			default label is "TargetDataArea"
			when (UpperCaseDataArea = TargetDataArea)

		MissingAsyncRequest
            when (not AsyncActionRequest exists)
			
	Derived Fields
		UpperCaseDataArea is a DerivedField
			type is DataAreaName
			restricted
			return parentcontext.dataarea

		ProductLine is a NativeField
			type is ProductLineName
			restricted
			



			
		ConfirmBODType is a NativeField
			type is Alpha size 42
			restricted
			
		CanConfirm is a NativeField
			type is Boolean
			default label is "BODConfirmable"
			
		DataAreaLogicalId is a NativeField
			type is Alpha size 250

		HasLogicalIdMapping is a NativeField
			type is Boolean		
			default label is "LogicalIdRelationExists"

		TargetDataArea is a NativeField
			type is DataAreaName
			
		UseSweep is a NativeField
			type is Boolean

		DecodedValue is a NativeField
		    type is Text

		DerivedIsValueEncoded is a DerivedField
            type is Boolean
            if (Document.Encoding > 0)
                return true
            else
                return false

    Sets
		ByToLogicalId
			no duplicates
			indexed
			Sort Order
				ToLogicalId
				MessageId

		ByMessageId
			no duplicates
			indexed
			Sort Order
				MessageId
				ToLogicalId

		ByStamp
			indexed
			Sort Order
				create stamp
				ToLogicalId
				MessageId

		ByProcessingTime
			indexed
			Sort Order
				LastProcessingTime
				ToLogicalId
				MessageId

    Local Fields
		LocalAsyncActionRequest is an AsyncActionRequest
		LocalDataArea is a DataArea
		MissingProcessor is Boolean
		LocalErrorMessage is Alpha size 300
		LocalErrorStack is Text
		LocalState               is Numeric size 1
			States
				New       			value is 0
				Complete 			value is 3
				MissingProcessor 	value is 4
				Failed	  			value is 5
				Canceled			value is 6

	Field Rules
    		MessageId
    			required
    			default to IMSReceiver

    		DocumentName
    			required

    		ToLogicalId
    			required

	Relations
		AsyncActionRequestRel
			default label is "AsyncActionRequest"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction 
				related.DataArea = TargetDataArea
				related.ImplementingClass = "IMSReceiver"
				related.AsyncAction = "Process"	
				
		LogicalIdProductLineRel
			one-to-many relation to LogicalId
			Field Mapping uses symbolic key
			Instance Selection
				where (related.LogicalId.LogicalId = ToLogicalId
				and    related.LogicalId.DataAreaName = ProductLine) 
				
		LogicalIdDataAreaRel
			one-to-many relation to LogicalId
			Field Mapping uses symbolic key
			Instance Selection
				where (related.LogicalId.LogicalId = ToLogicalId
				and    related.LogicalId.DataAreaName = UpperCaseDataArea) 
				
		LogicalIdRel
			one-to-many relation to LogicalId
			Field Mapping uses symbolic key
			Instance Selection
				where (related.LogicalId.LogicalId = ToLogicalId)
	
	Actions
		Process is a Set Action
			run in foreground
			disable checkpoint
			
			Instance Selection
				where (State.New
				and   (LogicalIdProductLineRel exists
				or     LogicalIdDataAreaRel exists
				or     ToLogicalId = UpperCaseDataArea))
				
			Local Fields
				LocalErrorMessage	is Alpha size 300
				LocalErrorStack     is Text
				
			Action Rules
				Instance Rules
					invoke New.ProcessSweepInstance
						resume on error

		Update is an Update Action
			restricted
	
		DeleteInstance is a Delete Action
			restricted

				
		NewJSONMessage is an Import Action
			restricted
			
			Parameters
				Message is JSONObject
				
			Local Fields
			    LocalMessageId				is an IONBODMessageId
    			LocalDocumentName        	is an IONDocumentName
    			LocalFromLogicalId       	is an IONLogicalId
    			LocalToLogicalId         	is an IONLogicalId
    			LocalDocument				is an IMSDocument
    			LocalAdditionalProperties	is an IONAdditionalProperties
    			LocalRawMessage             is JSONObject

			Action Rules
				invoke Create IMSReceiver
					invoked.MessageId 				= LocalMessageId
					invoked.DocumentName 			= LocalDocumentName
					invoked.FromLogicalId 			= LocalFromLogicalId
					invoked.ToLogicalId 			= LocalToLogicalId
					invoked.Document				= LocalDocument
					invoked.AdditionalProperties	= LocalAdditionalProperties
					invoked.RawMessage				= LocalRawMessage




























					

		NewMultipartMessage is an Import Action
			restricted

			Parameters
				MessageParameters		is JSONObject
				MessageBody				is BinaryObject

			Local Fields
			    LocalMessageId				is an IONBODMessageId
    			LocalDocumentName        	is an IONDocumentName
    			LocalFromLogicalId       	is an IONLogicalId
    			LocalToLogicalId         	is an IONLogicalId
    			LocalDocument				is an IMSDocument
    			LocalAdditionalProperties	is an IONAdditionalProperties

			Action Rules
				invoke Create IMSReceiver
					invoked.MessageId 				= LocalMessageId
					invoked.DocumentName 			= LocalDocumentName
					invoked.FromLogicalId 			= LocalFromLogicalId
					invoked.ToLogicalId 			= LocalToLogicalId
					invoked.Document				= LocalDocument
					invoked.AdditionalProperties	= LocalAdditionalProperties
					invoked.Document.Payload		= MessageBody

			
		NewXMLMessage is an Import Action
			restricted
			Parameters
				Message is XMLDocument
				
			Action Rules
				invoke Create IMSReceiver
					invoked.MessageId 			= Message select "/messageRequest/messageId"
					invoked.DocumentName 		= Message select "/messageRequest/documentName"
					invoked.FromLogicalId 		= Message select "/messageRequest/fromLogicalId"
					invoked.ToLogicalId 		= Message select "/messageRequest/toLogicalId"
					if (Message select "/messageRequest/document/encoding" like "BASE64")
						invoked.Document.Encoding 	= 1
					else
					if (Message select "/messageRequest/document/encoding" like "GZIP64")
						invoked.Document.Encoding    = 2
					else
					if (Message select "/messageRequest/document/encoding" like "DEFLATE64")
						invoked.Document.Encoding    = 3
							
					invoked.Document.CharacterSet 					 = Message select "/messageRequest/document/characterSet"
					invoked.Document.Value 							 = Message select "/messageRequest/document/value"
					
					invoked.AdditionalProperties.AccountingEntity 	 = Message select "/messageRequest/additionalProperties/accountingEntity"
					invoked.AdditionalProperties.Location 			 = Message select "/messageRequest/additionalProperties/location"
					invoked.AdditionalProperties.DocumentId 		 = Message select "/messageRequest/additionalProperties/documentId"
					invoked.AdditionalProperties.VariationId 		 = Message select "/messageRequest/additionalProperties/variationId"
					invoked.AdditionalProperties.RevisionId 		 = Message select "/messageRequest/additionalProperties/revisionId"
					invoked.AdditionalProperties.BatchId 			 = Message select "/messageRequest/additionalProperties/batchId"
					invoked.AdditionalProperties.BatchSequence 		 = Message select "/messageRequest/additionalProperties/batchSequence"
					invoked.AdditionalProperties.BatchSize 			 = Message select "/messageRequest/additionalProperties/batchSize"
					invoked.AdditionalProperties.BatchRevision 		 = Message select "/messageRequest/additionalProperties/batchRevision"
					invoked.AdditionalProperties.BatchAbortIndicator = Message select "/messageRequest/additionalProperties/batchAbortIndicator"
		
		EndWorkUpdateAsyncRequest is an Instance Action
			restricted
			manual update

			
		UpdateAsyncRequest is an Instance Action
			restricted
			
			Action Rules
                if (first AsyncActionRequestRel exists)
                    invoke SetPendingScheduling first AsyncActionRequestRel.AsyncActionRequest
                        invoked.ParamPendingScheduling = true

		ValidateEncoding is an Instance Action
			Parameters
				OverrideEncoding is Boolean
				Encoding            is Numeric size 1
					States
						None   value is 0
						Base64 value is 1
						GZip64 value is 2
						Deflate64 value is 3
						
		Delete is an Import Action
			restricted
			Parameters
				ParamMessageId is a IONBODMessageId
				    default label is "MessageId"
				
			Action Rules
			
		PurgeCompletedMessages is a Set Action
			Parameters
				ThruDate   			is Date
				PurgeOffsetDays 	is Numeric size 3
				
			Local Fields
				LocalThruDate 		is Date
					value is ThruDate
					


			Parameter Rules
				ThruDate
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate  = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"

			Instance Selection
				include deleted records
				where (State.Complete
				and   LastProcessingTime < LocalThruDate)
				
			Sort Order
				State
				
			Action Rules

				Instance Rules
					invoke Complete.Purge
		
		Purge is a Set Action
			disable checkpoint
			Parameters
				ThruDate   			is Date
				PurgeOffsetDays 	is Numeric size 3
				
			Local Fields
				LocalThruDate 		is Date
					value is ThruDate
					


			Parameter Rules
				ThruDate
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else	 
					if (not PurgeOffsetDays entered)
						required	
							"MustEnterThruDateOrOffset"
					else
						LocalThruDate  = current date - (PurgeOffsetDays - 1)
			
				PurgeOffsetDays
					constraint (not ThruDate entered)
						"CannotEnterBothThruDateAndOffset"

			Instance Selection
				include deleted records
				where (((State.Complete)
				or (State.Failed) 
				or (State.FailedConfirm))
				and (LastProcessingTime < LocalThruDate))
				
			Sort Order
				State
				LastProcessingTime
				
			Action Rules

				Instance Rules
					if (State.Complete)
						invoke Complete.Purge
					else
					if (State.Failed)
						invoke Failed.Purge
					else
					if (State.FailedConfirm)
						invoke FailedConfirm.Purge

		PurgeDeleted is a Set Action
		    restricted
            disable checkpoint
            Parameters
                ThruDate   			is Date
                PurgeOffsetDays 	is Numeric size 3

            Local Fields
                LocalThruDate 		is Date
                    value is ThruDate



            Parameter Rules
                ThruDate
                    if (ThruDate entered)
                        LocalThruDate = (ThruDate + 1 day)
                    else
                    if (not PurgeOffsetDays entered)
                        required
                            "MustEnterThruDateOrOffset"
                    else
                        LocalThruDate  = current date - (PurgeOffsetDays - 1)

                PurgeOffsetDays
                    constraint (not ThruDate entered)
                        "CannotEnterBothThruDateAndOffset"

            Instance Selection
                include only deleted records
                where (LastProcessingTime < LocalThruDate)

            Sort Order
                State
                LastProcessingTime

            Action Rules

                Instance Rules
                    if (State.Complete)
                        invoke Complete.Purge
                    else
                    if (State.Failed)
                        invoke Failed.Purge
                    else
                    if (State.FailedConfirm)
                        invoke FailedConfirm.Purge
                    else
                    if (State.New)
                        invoke New.Purge
                    else
                    if (State.MissingProcessor)
                        invoke MissingProcessor.Purge
                    else
                    if (State.Canceled)
                        invoke Canceled.Purge

	StateCycles
    	MessageLifeCycle is a StateCycle
			state field is State
			initial state is New
	
			New is a State
				Entrance Rules
			        initialize FailureCount
			        initialize Error
			        initialize ErrorMessage
					invoke EndWorkUpdateAsyncRequest
			
				Create is a Create Action
					restricted

					Action Rules
						if (Loopback)
							constraint (HasLogicalIdMapping)
								"DataAreaDoesNotExist"
						else
							constraint (HasLogicalIdMapping)
								"LogicalIdMappingDoesNotExist"
								
				Cancel is an Instance Action
					default label is untranslatable
					Action Rules
						make transition to Canceled 

                ProcessSweepInstance is an Instance Action
                    restricted
                    default label is untranslatable

				ProcessInstance is an Instance Action
					default label is untranslatable
					valid when (IsTargetDataArea)

                Purge is a Purge Action
                    restricted

				ScheduleForProcessing is an Instance Action
                    default label is untranslatable
                    valid when (MissingAsyncRequest)
                    Action Rules
                        invoke EndWorkUpdateAsyncRequest

				Exit Rules
					LastProcessingTime = system current timestamp

			Canceled is a State	
			
				Entrance Rules
					initialize AsyncActionRequest
						
				Delete is a Delete Action
				
				Reset is an Instance Action
					default label is untranslatable
					Action Rules
						make transition to New

				Purge is a Purge Action
                    restricted

			Complete is a State
				Entrance Rules
					initialize AsyncActionRequest

				Delete is a Delete Action
				
				Purge is a Purge Action
					restricted
								 
			Failed is a State
				Entrance Rules
					initialize AsyncActionRequest

				Reset is an Instance Action
					default label is untranslatable
					Action Rules
						make transition to New
					
				Cancel is an Instance Action
					default label is untranslatable
					Action Rules
						make transition to Canceled
				
				Purge is a Purge Action
					restricted
						





















						
			FailedConfirm is a State
				Delete is a Delete Action
				
				Purge is a Purge Action
					restricted
				
			MissingProcessor is a State
				Entrance Rules
					initialize AsyncActionRequest

				Reset is an Instance Action
					default label is untranslatable
					Action Rules
						make transition to New
					
				Cancel is an Instance Action
					default label is untranslatable
					Action Rules
						make transition to Canceled
				
				Purge is a Purge Action
					restricted
						
