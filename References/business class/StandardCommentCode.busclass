StandardCommentCode is a BusinessClass
    owned by po
    prefix is SCM
    classic name is STCOMMCODE

    Ontology
        symbolic key is StandardCommentCode
            classic set name is SCMSET0
            classic name is COMMENT-CODE
            classic name for ProcurementGroup is PROCURE-GROUP

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        StandardCommentPrintCode is AlphaUpper size 1
            classic name is PRINT-CODE
            States
                PrintOnInternalDocuments	value is "I"
                PrintOnPurchaseOrder		value is "P"
                PrintOnPurchaseOrderTrailer	value is "T"
                PrintOnReceivingDocument	value is "R"
                PrintOnPickList				value is "L"
                PrintOnDeliveryTicket		value is "D"
                PrintOnRequisition			value is "Q"
                DisplayOnly					value is "O"
                ReturnDocument				value is "E"
                BillTo						value is "B"
                InvoiceComments				value is "N"
                NamedPlaceIncoterm			value is "M"
                Asset						value is "4"
		CommentName		is Alpha size 50 
		CommentText		is RichText
		Attachment		is an AlternateAttachment

	Local Fields
		LocalAttributeCtr   is Numeric 2	

	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforStandardCommentCode"   
			
		ContextMessageText is a MessageField
			restricted
			"ProcurementGroup<ProcurementGroup>StandardCommentCode<StandardCommentCode>"  
			
    Conditions

        IsContractComment
            classic name is AGRMT-HEADER
            restricted
            when (StandardCommentPrintCode.DisplayOnly)

        IsContractLineComment
            classic name is AGRMT-LINE
            restricted
            when (StandardCommentPrintCode.InvoiceComments
            or    StandardCommentPrintCode.DisplayOnly
            or    StandardCommentPrintCode.PrintOnPurchaseOrder)

        HasAttachment
			restricted
			when (Attachment entered)
        
        HasNoLanguage
            classic name is NO-LANGUAGE
            restricted

            when (IsoLocale not entered)

        IsPurchaseOrderComment
            classic name is PO-HEADER
            restricted
            when (StandardCommentPrintCode.PrintOnInternalDocuments
            or    StandardCommentPrintCode.PrintOnPurchaseOrder
            or    StandardCommentPrintCode.PrintOnPurchaseOrderTrailer
            or    StandardCommentPrintCode.PrintOnReceivingDocument
            or    StandardCommentPrintCode.DisplayOnly
            or    StandardCommentPrintCode.PrintOnDeliveryTicket
            or    StandardCommentPrintCode.InvoiceComments
            or    StandardCommentPrintCode.BillTo
            or    StandardCommentPrintCode.NamedPlaceIncoterm)

        IsPurchaseOrderLineComment
            classic name is PO-LINE
            restricted
            when (StandardCommentPrintCode.PrintOnInternalDocuments
            or    StandardCommentPrintCode.PrintOnPurchaseOrder
            or    StandardCommentPrintCode.PrintOnReceivingDocument
            or    StandardCommentPrintCode.DisplayOnly
            or    StandardCommentPrintCode.PrintOnDeliveryTicket
            or    StandardCommentPrintCode.InvoiceComments)

        IsRequisitionComment
            classic name is REQUISITIONS
            restricted
            when (not StandardCommentPrintCode.ReturnDocument)

        IsRequisitionLineComment
            classic name is RQ-LINE
            restricted
            when (StandardCommentPrintCode.PrintOnInternalDocuments
            or    StandardCommentPrintCode.PrintOnPurchaseOrder
            or    StandardCommentPrintCode.PrintOnReceivingDocument
            or    StandardCommentPrintCode.PrintOnPickList
            or    StandardCommentPrintCode.PrintOnDeliveryTicket
            or    StandardCommentPrintCode.PrintOnRequisition
            or    StandardCommentPrintCode.DisplayOnly
            or    StandardCommentPrintCode.InvoiceComments)

        IsVendorReturnComment
            classic name is VEN-RET-HDR
            restricted
            when (StandardCommentPrintCode.PrintOnPurchaseOrder
            or    StandardCommentPrintCode.DisplayOnly
            or    StandardCommentPrintCode.ReturnDocument)

        IsVendorReturnsComment
            classic name is VEN-RETURNS
            restricted
            when (StandardCommentPrintCode.ReturnDocument
            or    StandardCommentPrintCode.DisplayOnly
            or    StandardCommentPrintCode.PrintOnInternalDocuments
            or    StandardCommentPrintCode.PrintOnPurchaseOrder
            or    StandardCommentPrintCode.PrintOnReceivingDocument)
        
        IsValidPurchasingCompanyComment
            restricted
            when (StandardCommentPrintCode.PrintOnPurchaseOrder 
            or    StandardCommentPrintCode.PrintOnPurchaseOrderTrailer)
		
	Field Rules
        StandardCommentPrintCode
            required
            
        CommentName
        	required


	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment
	
    Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
				
	Actions
		Create is a Create Action
			Entrance Rules
				if (IsoLocale entered)

					constraint (DefaultLocaleStandardCommentCodeRel exists)
						"BaseLanguageCommentMustExistBeforeOtherLanguages"
				
		Update is an Update Action

		Delete is a Delete Action
			Entrance Rules
				if (IsoLocale not entered)
					constraint (NonBaseStandardCommentCodeRel not exists) 
						"MustDeleteOtherLanguagesFirstBeforeTheBaseLanguage"
 
 
 		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment
                        
                    commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
						
						
						
           
	Relations
		DefaultLocaleStandardCommentCodeRel
			one-to-many relation to StandardCommentCode
			Field Mapping uses Set1
				related.ProcurementGroup 	= ProcurementGroup
				related.StandardCommentCode = StandardCommentCode

			Instance Selection 
				where (related.IsoLocale not entered)
				
		NonBaseStandardCommentCodeRel
			one-to-many relation to StandardCommentCode
			Field Mapping uses Set1
				related.ProcurementGroup 	= ProcurementGroup
				related.StandardCommentCode = StandardCommentCode

			Instance Selection 
				where (related.IsoLocale entered)			
	Sets
        Set1
            not indexed
            Sort Order
                ProcurementGroup
                StandardCommentCode
                IsoLocale
				
