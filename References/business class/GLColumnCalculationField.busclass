GLColumnCalculationField is a BusinessClass
	owned by GeneralLedger
	prefix is GLCCD

	Ontology
		symbolic key is GLColumnCalculationField

	Patterns
		implements Resequence on ColumnOrder
			new sequence field is NewColumnOrder
			set is ByColumnOrder

	Persistent Fields
		Type				is Numeric 1
			States
				Standard		value is 0
				Numeric			value is 1
		CalculationColumn	is a GLColumn
		NumericValue		is Numeric 9
		ColumnOrder			is Numeric 2
			disable Auditing
		CalculationOrder	is Numeric 2
		ColumnOperator		is Numeric 1
			States
				None		value is 0
				Add			value is 1
				Subtract	value is 2
				Multiply	value is 3
				Divide		value is 4

	Transient Fields
		NewColumnOrder	is like GLColumn

	Derived Fields	
		GLColumnCalculationFieldCount is a DerivedField
			type is Numeric 2
			restricted
			return instance count of GLColumn.GLColumnCalculationFieldRel

		IsGLColumnCalculationFieldUpdated is a DerivedField
			type is Boolean
			restricted
			if(Type changed
			or CalculationColumn changed
			or NumericValue changed
			or ColumnOrder changed
			or CalculationOrder changed
			or ColumnOperator changed
			or NewColumnOrder changed)
				return true
			else
				return false

		IsNumber is a DerivedField
			type is Boolean
			if(NumericValue entered)
				return true
			else
				return false
	
	Field Rules
		ColumnOrder
			default to GLColumnCalculationField
		ColumnOperator
			if(ColumnOrder = 1)
				constraint(ColumnOperator.None)
					"ColumnOperatorMustBeNoneForFirstGLColumnCalculationField"
		NumericValue
			if(Type.Numeric)
				required
		CalculationColumn
			constraint(CalculationColumn.GLColumnType.Standard)
				"OnlyStandardColumnIsAllowed"
			constraint(GLColumn.DisplayOrder > CalculationColumn.DisplayOrder)
				"InvalidStandardColumnForCalculation"
				
	Rule Blocks
		UpdateExitRules
			if (IsGLColumnCalculationFieldUpdated)
				if (GLReportRel exists)
					for each GLReportRel
						if (each.IntegrationStatus = 1)
							invoke UpdateGLReport each
								invoked.PrmIntegrationStatus = 2
				if (SubReportRel exists)
					for each SubReportRel
						if(each.CompositeReportRel.IntegrationStatus = 1)
							invoke Update each.CompositeReportRel
								invoked.IntegrationStatus = 2

		UpdateIntegrationStatus
			if (GLReportRel exists)
				for each GLReportRel
					if (each.IntegrationStatus = 1)
						invoke UpdateGLReport each
							invoked.PrmIntegrationStatus = 2
				
			if (SubReportRel exists)
				for each SubReportRel
					if(each.CompositeReportRel.IntegrationStatus = 1)
						invoke Update each.CompositeReportRel
							invoked.IntegrationStatus = 2
				
			

	Actions
		Create is an Action
			valid when (GLColumn.ValidForCalculation)
			Exit Rules
				include UpdateIntegrationStatus

		Update is an Update Action
			Exit Rules
				include UpdateExitRules
		Purge is a Purge Action
			Exit Rules
				include UpdateIntegrationStatus
		Delete is a Delete Action
			Exit Rules
				include UpdateIntegrationStatus

	Sets
		ByColumnOrder
			Sort Order
				FinanceEnterpriseGroup
				GLColumnView
				GLColumn
				ColumnOrder
				GLColumnCalculationField
	
	Relations
		GLReportRel
			one-to-many relation to GLReport
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where(related.ColumnView				= GLColumnView)

		SubReportRel
			one-to-many relation to GLReportElement
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.SubReport.ColumnView		= GLColumn.GLColumnView)
