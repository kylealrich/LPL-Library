FinanceDimension6Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD6UP   
    
    Ontology
    	symbolic key is FinanceDimension6Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension6			is like FinanceDimension6
    	FinanceDimension6				is like FinanceDimension6
		SequenceDimension6				is like FinanceDimension6
		ParentSequenceDimension6		is like FinanceDimension6
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description	
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension6Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension6 is a DerivedField
			type is like FinanceDimension6
			restricted
			if (ParentFinanceDimension6 entered)
				return DerivedParentFinanceDimension6String
			else
				return TopNodeString
		
		DerivedSequenceParentDimension6 is a DerivedField
			type is like FinanceDimension6
			restricted
			if (ParentSequenceDimension6 entered)
				return ParentSequenceDimension6
			else
				return DerivedParentFinanceDimension6 
		
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension6Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension6String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension6Structure.StructureSequence
			"_"
			ParentFinanceDimension6
		
		DerivedSummaryFinanceDimension6 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension6Structure.StructureSequence
			"_"
			FinanceDimension6
	
		DerivedSummarySequenceDimension6 is a DerivedField
			type is like FinanceDimension6
			restricted
			if (SequenceDimension6 entered)
				return SequenceDimension6
			else 
				return DerivedSummaryFinanceDimension6

			
	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
 
 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension6Structure
				FinanceDimension6
				FinanceDimension6Upload.RunGroup
				FinanceDimension6Upload.Sequence
 	
	Relations
		FinanceDimension6HierarchyRel
			one-to-one relation to FinanceDimension6Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension6Structure = FinanceDimension6Structure
				related.FinanceDimension6		   = FinanceDimension6
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension6Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension6Structure = FinanceDimension6Structure
			Instance Selection
				where (related.FinanceDimension6Upload.RunGroup = FinanceDimension6Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 11
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 11
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension6
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"
							
    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension6Upload.Sequence entered)
					FinanceDimension6Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension6Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action

		Purge is a Purge Action
			restricted
			
		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension6Structure		is a FinanceDimension6Structure
					default label is "FinanceDimension6Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension6Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension6	is a FinanceDimension6
				DynamicRulesExist		is Boolean		
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension6Structure	    = PrmFinanceDimension6Structure
				and    FinanceDimension6Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension6Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension6Structure
				FinanceDimension6Upload.RunGroup
				FinanceDimension6Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
					
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension6 = FinanceDimension6
					else
						LocalFinanceDimension6 = DerivedSummarySequenceDimension6
					if (LocalFinanceDimension6 exists)
						if  (RecordType.Posting
						and !FinanceDimension6HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension6
								resume on error
									LocalErrorMessage		= error message	
								invoked.PrmStructure		= FinanceDimension6Structure				
								invoked.PrmParentDimension	= DerivedSequenceParentDimension6
						else					
							if (DerivedSequenceParentDimension6 != LocalFinanceDimension6.DerivedParentDimension)
								invoke Move LocalFinanceDimension6
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension6
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension6
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension6 
										except invoked.ParentFinanceDimension6
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension6
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension6
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension6
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension6 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension6
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension6Structure		is a FinanceDimension6Structure
					default label is "FinanceDimension6Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension6Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension6Structure	    = PrmFinanceDimension6Structure
				and    FinanceDimension6Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension6Structure   is a FinanceDimension6Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension6Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension6Structure	    = PrmFinanceDimension6Structure
				and    FinanceDimension6Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
