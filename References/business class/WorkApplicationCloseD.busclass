WorkApplicationCloseD is a BusinessClass
	owned by ar
	prefix is r190d

	Ontology
		symbolic key is WorkApplicationCloseD

	Patterns
		disable AuditIndex
		disable Auditing
		disable EffectiveDated
		disable DataTranslations
		disable StaticTranslations

	Persistent Fields
		FinanceEnterpriseGroup	is like FinanceEnterpriseGroup
		ApplicationCloseResult	is like ApplicationCloseResult
		RecKey					is a AR190War190dRecKeyGroup
		TranAppliedAmount		is like InternationalAmount
		BaseAppliedAmount		is like InternationalAmount
		OrigNd					is like OrigNd
		DocumentNbr				is like DocumentNumber
		Customer				is like Customer
		BatchNbr				is like BatchNumber
		AraOprCode				is like ReceivableOperator
		DepInstCode				is like BankTransactionCode
		BankInstCode			is like BankTransactionCode
		ProgramSequence

	Derived Fields
		ARCredMsg is a MessageField
			restricted
			"ARCreditAppliedOn"

	Relations
		ARCompanyRel
			one-to-one relation to ReceivableCompany
			Field Mapping uses symbolic key
				related.Company					 = RecKey.Company

		GLCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company					 = RecKey.Company

		ARCodeRel
			one-to-one relation to ReceivableGeneralLedgerCode
			Field Mapping uses symbolic key
				related.Company					 = RecKey.Company
				related.ReceivableGeneralLedgerCode = RecKey.ArCode

		CloseDistributionRel
			one-to-many relation to ReceivableGLDistribution
			Field Mapping uses ByApplicationCloseResult
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.ApplicationCloseResult				= ApplicationCloseResult

	Sets

		RecKey
			indexed
			no duplicates
			Sort Order
				FinanceEnterpriseGroup
				ApplicationCloseResult
				RecKey

	Actions

		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted

		FastUpdate is an Update Action
			restricted
			bypass field rules

		Delete is a Delete Action
			restricted

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules

		Process is a Set Action
			restricted

			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmApplicationCloseResult		is a ApplicationCloseResult
				PrmCreateDate					is TimeStamp

			Instance Selection
				where (FinanceEnterpriseGroup	 = PrmFinanceEnterpriseGroup
				and	ApplicationCloseResult		 = PrmApplicationCloseResult)

			Local Fields




			Action Rules
				Instance Rules
					display "WorkD:<RecKey.Invoice>"

					invoke CreateFromApplicationClose ReceivableGLDistribution
						invoked.FinanceEnterpriseGroup							= FinanceEnterpriseGroup
						invoked.ReceivableGLDistribution.ProgramSequence		= ProgramSequence
						invoked.LastDistributionSequence						= 1
						invoked.CreateDate										= PrmCreateDate

						invoked.ReceivableCompanyGroup.GlCompany				= RecKey.Company
						invoked.ReceivableCompanyDataGroup.TransactionCompany	= RecKey.Company

						invoked.GeneralLedgerAccount							= ARCodeRel.DistributionAccount
						invoked.GeneralLedgerDate								= RecKey.GlDate
						invoked.TransactionDate									= RecKey.GlDate
						invoked.ReceivableCompanyDataGroup.Customer				= Customer
						invoked.ReceivableCompanyDataGroup.ReceivableProcessLevel	= RecKey.ProcessLevel

						invoked.Origin											= RecKey.AppSource
						invoked.Status											= 2
						invoked.OriginalCurrency								= RecKey.AppCurrCd

						if  (RecKey.TransType = "C"
						or   RecKey.TransType = "P")
							invoked.DistributionAmount.CurrencyAmount							= TranAppliedAmount * -1
							invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount	= BaseAppliedAmount * -1
						else
							invoked.DistributionAmount.CurrencyAmount							= TranAppliedAmount
							invoked.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount	= BaseAppliedAmount

						invoked.Description										= ARCredMsg + " " + PrmCreateDate
						invoked.TransType										= RecKey.TransType
						invoked.Invoice											= RecKey.Invoice
						invoked.BatchNumber										= BatchNbr
						invoked.DocumentNumber									= DocumentNbr

						invoked.AuditSummaryIndicator							= "04"	

						if  (ARCompanyRel.DetailGLTransPerApplication = "Y")
							invoked.AccumulationType							= "D"
						else
							invoked.AccumulationType							= "S"
							invoked.ReceivableApplicationOjectID				= ProgramSequence
						invoked.ApplicationCloseResult							= PrmApplicationCloseResult

					invoke FastDelete

