AllocationSnapshot is a BusinessClass
    owned by Allocations
    prefix is ALCSS

    Ontology
    	symbolic key is AllocationSnapshot

    Persistent Fields
		Scenario
		Ledger
		Calendar					is a GeneralLedgerCalendar
		AllocationPeriod			is a GeneralLedgerCalendarPeriod
		Status						is Numeric size 1
			States
				CopyOngoing	     		value is 0
				CopyCompleted    		value is 1
		DateCreated					is Date
		LastCounter					is Numeric size 8
		
    Relations
		PeriodShadowEndDatesRel
			one-to-many relation to GeneralLedgerPeriodShadow
			Field Mapping uses BySummaryPeriod
				related.FinanceEnterpriseGroup             = FinanceEnterpriseGroup
				related.GeneralLedgerCalendar       	   = Calendar
				related.SummaryGeneralLedgerCalendarPeriod = AllocationPeriod
			Instance Selection
				where (related.GeneralLedgerCalendarPeriod.PeriodType.EndDate)
#ifdef module truecost
		TrueCostAllocationRunRel
			one-to-many relation to TrueCostAllocationRun
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
			Instance Selection
				where (related.TrueCostAllocationRun.AllocationSourceSystem	= AllocationSourceSystem
				and    related.TrueCostAllocationRun.AllocationSnapshot		= AllocationSnapshot)			
#endif

		AllocationSnapshotDetailRel is an AllocationSnapshotDetail set

	Sets
		ByPeriods
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				AllocationSourceSystem
				AllocationPeriod
				
		ByCreationDateDescending
			Sort Order
				FinanceEnterpriseGroup
				AllocationSourceSystem
				AllocationPeriod descending
				create stamp descending

	Field Rules
		Scenario
			required
		Ledger
			required
		Calendar
			required
		AllocationPeriod
			required
	Local Fields
		AsyncId										is an AsyncActionRequest
		
	Actions
		Create is an Action
			restricted
			
			Action Rules
				DateCreated = current corporate date
				
			Exit Rules
				AsyncId 									= current async action request id
				for each PeriodShadowEndDatesRel
					invoke CopyToAllocationSnapshot GeneralLedgerTotal
						run after AsyncId
						assign async action request id to AsyncId
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.PrmAllocationSourceSystem	= AllocationSourceSystem
						invoked.PrmAllocationSnapshot		= AllocationSnapshot
						invoked.PrmScenario					= Scenario
						invoked.PrmLedger					= Ledger
						invoked.PrmPeriod					= each.GeneralLedgerCalendarPeriod

				invoke UpdateStatus in background
					run after AsyncId
					assign async action request id to AsyncId

		Update is an Action
			restricted
			Action Rules
				DateCreated = current corporate date
				
		Delete is an Action
			restricted

		UpdateStatus is an Instance Action
			restricted
			Action Rules
				Status = Status.CopyCompleted
		
		CreateAllocationSnapshot is an Instance Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmAllocationSourceSystem	is a AllocationSourceSystem
				PrmScenario					is a Scenario
				PrmLedger					is a Ledger
				PrmCalendar					is a GeneralLedgerCalendar
				PrmAllocationPeriod			is a GeneralLedgerCalendarPeriod
			
			Action Rules
				FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				AllocationSourceSystem		= PrmAllocationSourceSystem
				Scenario					= PrmScenario
				Ledger						= PrmLedger
				Calendar					= PrmCalendar
				AllocationPeriod			= PrmAllocationPeriod

		
