FinanceDimension4Upload is a BusinessClass
	owned by GeneralLedger
	prefix is FD4UP   
    
    Ontology
    	symbolic key is FinanceDimension4Upload

    Patterns
 		disable Auditing 
 		disable EffectiveDated
 		disable AsOfDateProcessing
		implements InlineUserFields
			size is 3000
			
    Persistent Fields
    	ParentFinanceDimension4			is like FinanceDimension4
    	FinanceDimension4				is like FinanceDimension4
		SequenceDimension4				is like FinanceDimension4
		ParentSequenceDimension4		is like FinanceDimension4
		RecordType						is Numeric 1
		    States	    
		    	Summary	value is 2
		    	Posting	value is 1		
		Description
		Active
    	SystemDimension					is Boolean
    	BudgetIdentifier
    	IncludeInBudgetEdit				is Boolean     	
		ErrorMessage					is Alpha 150
		AddressCode
#ifdef module tx
		ProductTaxCategory		
#endif
#ifndef module tx
		ProductTaxCategory	is AlphaUpper size 15
#endif
		RevenuePartialAccount	is a FinanceCodeBlockNoProjectFD2
			default label is "PartialRevenueFinanceStructure"

   	Context Fields
   		
    Transient Fields

	Derived Fields
		SummaryPrefix			is a StringField
			type is AlphaUpper 15
			restricted
			FinanceDimension4Structure.StructureSequence
			"_*"
				
		DerivedParentFinanceDimension4 is a DerivedField
			type is like FinanceDimension4
			restricted
			if (ParentFinanceDimension4 entered)
				return DerivedParentFinanceDimension4String
			else
				return TopNodeString
		
		DerivedSequenceParentDimension4 is a DerivedField
			type is like FinanceDimension4
			restricted
			if (ParentSequenceDimension4 entered)
				return ParentSequenceDimension4
			else
				return DerivedParentFinanceDimension4
				
		TopNodeString			is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension4Structure.StructureSequence
			"_TOP_NODE"
							
		DerivedParentFinanceDimension4String is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension4Structure.StructureSequence
			"_"
			ParentFinanceDimension4
		
		DerivedSummaryFinanceDimension4 is a StringField
			type is AlphaUpper 25
			restricted
			FinanceDimension4Structure.StructureSequence
			"_"
			FinanceDimension4
	
		DerivedSummarySequenceDimension4 is a DerivedField
			type is like FinanceDimension4
			restricted
			if (SequenceDimension4 entered)
				return SequenceDimension4
			else 
				return DerivedSummaryFinanceDimension4

	Local Fields
		LocalCodeBlockRelation						is a CodeBlockRelation

					
	Conditions

 		ErrorTransaction
 			restricted
 			when (ErrorMessage entered)
 		FromOrToRelationExists
 			restricted
 			when (CodeBlockRelationFromRel exists
 			or    CodeBlockRelationToRel exists)
		PostingInEnterpriseStructureBOLDimension4
			restricted
			when (RecordType.Posting
			and   FinanceDimension4Structure = FinanceEnterpriseGroup.EnterpriseDim4Structure
			and   FinanceEnterpriseGroup.BillingOverrideLevel.Dimension4)

		InEnterpriseStructureBOLDimension4
			restricted
			when (FinanceDimension4Structure = FinanceEnterpriseGroup.EnterpriseDim3Structure
			and   FinanceEnterpriseGroup.BillingOverrideLevel.Dimension4)
 	Sets
		ByDimension
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension4Structure
				FinanceDimension4
				FinanceDimension4Upload.RunGroup
				FinanceDimension4Upload.Sequence
 	
	Relations
		FinanceDimension4HierarchyRel
			one-to-one relation to FinanceDimension4Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension4Structure = FinanceDimension4Structure
				related.FinanceDimension4		   = FinanceDimension4
		OtherRecordsInRunGroupRel
			one-to-many relation to FinanceDimension4Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup     = FinanceEnterpriseGroup
				related.FinanceDimension4Structure = FinanceDimension4Structure
			Instance Selection
				where (related.FinanceDimension4Upload.RunGroup = FinanceDimension4Upload.RunGroup)
		CodeBlockRelationFromRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByFromTo
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.FromField					= 9
		CodeBlockRelationToRel
			one-to-many relation to CodeBlockRelation
			Field Mapping uses ByToFrom
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ToField						= 9
		CodeBlockRelationRuleDynamicRel
			one-to-many relation to CodeBlockRelationRule
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.CodeBlockRelation				= LocalCodeBlockRelation
			Instance Selection
				where (related.Active
				and    related.DynamicCreate)
				
	Field Rules
    	FinanceDimension4
    		required
		RecordType
		   	required	
		Description
			required
		BudgetIdentifier
			constraint (RecordType.Summary)
				"NotValidForPostingDimension"
		IncludeInBudgetEdit
			constraint (RecordType.Posting)
				"NotValidForSummaryDimension"





    Actions
		Create is a Create Action
			Entrance Rules
				if (!FinanceDimension4Upload.Sequence entered)
					FinanceDimension4Upload.Sequence = (last OtherRecordsInRunGroupRel.FinanceDimension4Upload.Sequence + 1)
				
		Update is an Update Action
			Exit Rules

		Delete is a Delete Action

		Purge is a Purge Action
			restricted
			
		UploadHierarchy is a Set Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension4Structure		is a FinanceDimension4Structure
					default label is "FinanceDimension4Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension4Structure
					required
				PrmRunGroup
					required
					
			Local Fields	
				LocalRecordCreated		is Boolean
				LocalErrorMessage		is Alpha 150
				LocalFinanceDimension4	is a FinanceDimension4
				DynamicRulesExist		is Boolean		
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension4Structure	    = PrmFinanceDimension4Structure
				and    FinanceDimension4Upload.RunGroup = PrmRunGroup)

			Set Is
				PrmFinanceEnterpriseGroup
				PrmFinanceDimension4Structure
								
			Sort Order
				FinanceEnterpriseGroup
				FinanceDimension4Structure
				FinanceDimension4Upload.RunGroup
				FinanceDimension4Upload.Sequence

			Action Rules
				FinanceEnterpriseGroup Set Rules
					Entrance Rules
						DynamicRulesExist = false
						if (FromOrToRelationExists)
							if (CodeBlockRelationToRel exists)
								for each CodeBlockRelationToRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
							if (!DynamicRulesExist
							and CodeBlockRelationFromRel exists)
								for each CodeBlockRelationFromRel
									LocalCodeBlockRelation		= each.CodeBlockRelation
									if (CodeBlockRelationRuleDynamicRel exists)
										DynamicRulesExist = true
					
					Exit Rules



								
				Instance Rules
					initialize LocalErrorMessage
					if (RecordType.Posting)
						LocalFinanceDimension4 = FinanceDimension4
					else
						LocalFinanceDimension4 = DerivedSummarySequenceDimension4
					if (LocalFinanceDimension4 exists)
						if  (RecordType.Posting
						and !FinanceDimension4HierarchyRel exists)
							invoke AddPostingDimensionToStructure LocalFinanceDimension4
								resume on error
									LocalErrorMessage		= error message		
								invoked.PrmStructure		= FinanceDimension4Structure				
								invoked.PrmParentDimension  = DerivedSequenceParentDimension4
						else					
							if (DerivedSequenceParentDimension4 != LocalFinanceDimension4.DerivedParentDimension)
								invoke Move LocalFinanceDimension4
									resume on error
										LocalErrorMessage	= error message
									invoked.NewParent = DerivedParentFinanceDimension4
							if (!LocalErrorMessage entered)
								invoke Update LocalFinanceDimension4
									resume on error
										LocalErrorMessage	= error message							
									fill in fields from this instance
										except invoked.FinanceDimension4 
										except invoked.ParentFinanceDimension4
										except invoked.ParentDimension
									invoked.BypassUpdateStructure = true
								if (!LocalErrorMessage entered)
									invoke UpdateStructure LocalFinanceDimension4
										resume on error
											LocalErrorMessage	= error message											
					else
						LocalRecordCreated = true
						invoke Create FinanceDimension4
							resume on error
								LocalErrorMessage		= error message
							fill in fields from this instance
								except invoked.ParentFinanceDimension4
							invoked.DimensionType 				   = RecordType
							if (!DynamicRulesExist)
								invoked.BypassNewStructureNotification = true
							else
								invoked.BypassNewStructureNotification = false						
							if (ParentFinanceDimension4 entered)
								invoked.ParentDimension = DerivedParentFinanceDimension4
					if (!LocalErrorMessage entered)
						invoke Delete
						if (DynamicRulesExist)
							commit transaction
					else
						ErrorMessage				= LocalErrorMessage

		ResetAllErrors is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension4Structure		is a FinanceDimension4Structure
					default label is "FinanceDimension4Structure"
				PrmRunGroup							is AlphaUpper 30
					default label is "RunGroup"

			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				PrmFinanceDimension4Structure
					required
				PrmRunGroup
					required
														
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension4Structure	    = PrmFinanceDimension4Structure
				and    FinanceDimension4Upload.RunGroup = PrmRunGroup
				and    ErrorMessage entered)
				
			Action Rules
			
				Instance Rules
					initialize ErrorMessage

		DeleteAll is a Set Action
			restricted
		
			Parameters
				PrmFinanceEnterpriseGroup   	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmFinanceDimension4Structure   is a FinanceDimension4Structure
					default label is "<FinanceEnterpriseGroup.FinanceDimension4Label>Structure"
				PrmRunGroup						is AlphaUpper 30
				
			Instance Selection
				where (FinanceEnterpriseGroup		    = PrmFinanceEnterpriseGroup
				and	   FinanceDimension4Structure	    = PrmFinanceDimension4Structure
				and    FinanceDimension4Upload.RunGroup = PrmRunGroup)
				
			Action Rules
			
				Instance Rules
					invoke Purge								
