ClassDepreciation is a BusinessClass
    owned by am
    prefix is CLD
    classic name is AMCLASSDEP

    Ontology
        symbolic key is ClassDepreciation
            classic set name is CLDSET0
            classic name for ClassDepreciation.YearEndDate is YR-END-DATE
            classic name for AssetType.Type is ASSET-TYPE

    Patterns
        implements StaticJava
        disable AuditIndex
        disable Auditing

    Persistent Fields
        AssetAccountingUnit               		is a FinanceCodeBlockNoAccountFull
            default label is "Asset"
        DepreciationExpenseAccountingUnit     	is a FinanceCodeBlockNoAccountFull
            default label is "DepreciationExpense"            
        AccumulatedDepreciationAccountingUnit 	is a FinanceCodeBlockNoAccountFull
            default label is "AccumulatedDepreciation"
        CurrentPeriodDepreciation           	is an InternationalAmount
            classic name is DEPR-CP
        YearToDateDepreciation              	is an InternationalAmount
            classic name is DEPR-YTD
        LifeToDateDepreciation              	is an InternationalAmount
            classic name is DEPR-LTD
        CurrentPeriodDepreciationCalculated 	is Boolean
            sql name is CPeriodDepreciationCalculated
            classic name is STATUS-CP-CALC
            restricted
        ReportWriterComputedAmount          	is an InternationalCost
            classic name is RW-COMPUTE-AMT
            restricted
        DepreciationRate
            classic name is DEPR-RATE
        CreationDate                        	is TimeStamp
        	restricted
        CreatedBy                           	is an Operator 
        	holds pii
            classic name is CREATOR-ID
            restricted
		
	Local Fields
		LocalCurrentPeriodAmountCalculated		is Boolean
		LocalCurrentPeriodAmount				is like InternationalAmount
		LocalCurrentYearToDateAmountCalculated	is Boolean
		LocalCurrentYearToDateAmount			is like InternationalAmount
		LocalPriorYearToDateAmountCalculated	is Boolean
		LocalPriorYearToDateAmount				is like InternationalAmount
            
	Derived Fields
		DerivedYTDTopNodeCubeValue is a StringField
			type is AlphaUpper up to 30
			restricted
			Company.FinanceEnterpriseGroup
			"/"
			first GeneralLedgerPeriodShadowRel.SummaryGeneralLedgerCalendarPeriod
			"_YTD"
		
		DerivedPriorYTDTopNodeCubeValue is a StringField
			type is AlphaUpper up to 30
			restricted
			Company.FinanceEnterpriseGroup
			"/"
			first PriorGeneralLedgerPeriodShadowRel.SummaryGeneralLedgerCalendarPeriod
			"_YTD"
			
		GeneralLedgerTotalCubeMap is a DerivedField
			type is LPLText
			restricted
			if (Book.ReportingBasis.AccountingUnitTopNodeGroup.DimensionNode entered)
				if (AssetAccountingUnit.AccountingUnit entered)
					GeneralLedgerTotalCubeMap += "related.GeneralLedgerTotal.AccountingUnit = AssetAccountingUnit.AccountingUnit\n"
				else
					GeneralLedgerTotalCubeMap += "related.GeneralLedgerTotal.AccountingUnit = Book.ReportingBasis.AccountingUnitTopNodeCubeValue\n"
			
			if (Book.ReportingBasis.ProjectTopNode entered)	
				if (AssetAccountingUnit.Project entered)
					GeneralLedgerTotalCubeMap += "related.GeneralLedgerTotal.Project = AssetAccountingUnit.Project\n"
				else
					GeneralLedgerTotalCubeMap += "related.GeneralLedgerTotal.Project = Book.ReportingBasis.ProjectTopNodeCubeValue\n"
				
			if (Book.ReportingBasis.FinanceDimension1TopNode entered)
				if (AssetAccountingUnit.FinanceDimension1 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension1 = AssetAccountingUnit.FinanceDimension1\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension1 = Book.ReportingBasis.FinanceDimension1TopNodeCubeValue\n"
			
			if (Book.ReportingBasis.FinanceDimension2TopNode entered)	
				if (AssetAccountingUnit.FinanceDimension2 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension2 = AssetAccountingUnit.FinanceDimension2\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension2 = Book.ReportingBasis.FinanceDimension2TopNodeCubeValue\n"
				
			if (Book.ReportingBasis.FinanceDimension3TopNode entered)
				if (AssetAccountingUnit.FinanceDimension3 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension3 = AssetAccountingUnit.FinanceDimension3\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension3 = Book.ReportingBasis.FinanceDimension3TopNodeCubeValue\n"
				 
			if (Book.ReportingBasis.FinanceDimension4TopNode entered)
				if (AssetAccountingUnit.FinanceDimension4 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension4 = AssetAccountingUnit.FinanceDimension4\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension4 = Book.ReportingBasis.FinanceDimension4TopNodeCubeValue\n"
				
			if (Book.ReportingBasis.FinanceDimension5TopNode entered)
				if (AssetAccountingUnit.FinanceDimension5 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension5 = AssetAccountingUnit.FinanceDimension5\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension5 = Book.ReportingBasis.FinanceDimension5TopNodeCubeValue\n"
				 
			if (Book.ReportingBasis.FinanceDimension6TopNode entered)
				if (AssetAccountingUnit.FinanceDimension6 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension6 = AssetAccountingUnit.FinanceDimension6\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension6 = Book.ReportingBasis.FinanceDimension6TopNodeCubeValue\n"
				
			if (Book.ReportingBasis.FinanceDimension7TopNode entered)
				if (AssetAccountingUnit.FinanceDimension7 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension7 = AssetAccountingUnit.FinanceDimension7\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension7 = Book.ReportingBasis.FinanceDimension7TopNodeCubeValue\n"
				 
			if (Book.ReportingBasis.FinanceDimension8TopNode entered)
				if (AssetAccountingUnit.FinanceDimension8 entered) 
					GeneralLedgerTotalCubeMap += "related.FinanceDimension8 = AssetAccountingUnit.FinanceDimension8\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension8 = Book.ReportingBasis.FinanceDimension8TopNodeCubeValue\n"
				
			if (Book.ReportingBasis.FinanceDimension9TopNode entered)
				if (AssetAccountingUnit.FinanceDimension9 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension9 = AssetAccountingUnit.FinanceDimension9\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension9 = Book.ReportingBasis.FinanceDimension9TopNodeCubeValue\n"
				
			if (Book.ReportingBasis.FinanceDimension10TopNode entered)
				if (AssetAccountingUnit.FinanceDimension10 entered)
					GeneralLedgerTotalCubeMap += "related.FinanceDimension10 = AssetAccountingUnit.FinanceDimension10\n"
				else
					GeneralLedgerTotalCubeMap += "related.FinanceDimension10 = Book.ReportingBasis.FinanceDimension10TopNodeCubeValue\n"
						
		CurrentPeriodAmount is a DerivedField
			type is like InternationalAmount
			if (LocalCurrentPeriodAmountCalculated)
				return LocalCurrentPeriodAmount
			LocalCurrentPeriodAmountCalculated = true
			LocalCurrentPeriodAmount = GeneralLedgerTotalPeriodCubeRel.NetFunctionalAmount
			return LocalCurrentPeriodAmount
			
		CurrentPeriodYearToDateAmount is a DerivedField
			type is like InternationalAmount
			if (LocalCurrentYearToDateAmountCalculated)
				return LocalCurrentYearToDateAmount
			LocalCurrentYearToDateAmountCalculated = true
			LocalCurrentYearToDateAmount = GeneralLedgerTotalYTDCubeRel.NetFunctionalAmount
			return LocalCurrentYearToDateAmount
	
		PriorPeriodYearToDateAmount is a DerivedField
			type is like InternationalAmount
			if (LocalPriorYearToDateAmountCalculated)
				return LocalPriorYearToDateAmount
			LocalPriorYearToDateAmountCalculated = true
			LocalPriorYearToDateAmount = PriorGeneralLedgerTotalYTDCubeRel.NetFunctionalAmount
			return LocalPriorYearToDateAmount
			
		AveragePeriodYearToDateAmount is a DerivedField
			type is like InternationalAmount
			default label is "Average Year To Date Amount"
			return ((PriorPeriodYearToDateAmount + CurrentPeriodYearToDateAmount) / 2)
		
		ClassBalanceTypeAmount is a DerivedField
			type is like InternationalAmount
			if (Book.ClassBalanceType.AverageBalance)
				return AveragePeriodYearToDateAmount
			else
				return CurrentPeriodYearToDateAmount
			
	Cube Relations
		GeneralLedgerTotalPeriodCubeRel
			cell relation to GeneralLedgerTotal
			dynamic mapping is GeneralLedgerTotalCubeMap
        	Dimension Mapping
				related.Scenario					 				 = Company.FinanceEnterpriseGroup.ActualsScenario
				related.GeneralLedgerTotal.Ledger	 				 = Book.ReportingBasis.LedgerTopNode
				related.AccountingEntity 							 = AssetAccountingUnit.ToAccountingEntity
				related.GeneralLedgerTotal.GeneralLedgerChartAccount = AssetType.ASTDistributionAccount.GeneralLedgerChartAccount
				related.GeneralLedgerTotal.EntityYearPeriod 		 = first GeneralLedgerCalendarPeriodRel.GeneralLedgerCalendarPeriod
        	Preload Measures
        		NetFunctionalAmount
        		
        GeneralLedgerTotalYTDCubeRel
			cell relation to GeneralLedgerTotal
			dynamic mapping is GeneralLedgerTotalCubeMap
        	Dimension Mapping
				related.Scenario					 				 = Company.FinanceEnterpriseGroup.ActualsScenario
				related.GeneralLedgerTotal.Ledger	 				 = Book.ReportingBasis.LedgerTopNode
				related.AccountingEntity 							 = AssetAccountingUnit.ToAccountingEntity
				related.GeneralLedgerTotal.GeneralLedgerChartAccount = AssetType.ASTDistributionAccount.GeneralLedgerChartAccount
				related.GeneralLedgerTotal.EntityYearPeriod 		 = DerivedYTDTopNodeCubeValue
        	Preload Measures
        		NetFunctionalAmount
    
    	PriorGeneralLedgerTotalYTDCubeRel
    		cell relation to GeneralLedgerTotal
			dynamic mapping is GeneralLedgerTotalCubeMap
        	Dimension Mapping
				related.Scenario					 				 = Company.FinanceEnterpriseGroup.ActualsScenario
				related.GeneralLedgerTotal.Ledger	 				 = Book.ReportingBasis.LedgerTopNode
				related.AccountingEntity 							 = AssetAccountingUnit.ToAccountingEntity
				related.GeneralLedgerTotal.GeneralLedgerChartAccount = AssetType.ASTDistributionAccount.GeneralLedgerChartAccount
				related.GeneralLedgerTotal.EntityYearPeriod 		 = DerivedPriorYTDTopNodeCubeValue
        	Preload Measures
        		NetFunctionalAmount
        		
    Relations
		BookCalendarRel
 			one-to-one relation to BookCalendar
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.Book					= Book
				related.Company					= Company
				related.YearEndDate				= ClassDepreciation.YearEndDate
		
		GeneralLedgerCalendarPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
			Instance Selection
				where (related.Date >= BookCalendarRel.DepreciationPeriods.DepreciationPeriodEndDates.DepreciationPeriodEndDate[ClassDepreciation.Period])
		
		PriorGeneralLedgerCalendarPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
			Instance Selection
				where (related.Date >= BookCalendarRel.DepreciationPeriods.DepreciationPeriodEndDates.DepreciationPeriodEndDate[ClassDepreciation.Period - 1])
								
		GeneralLedgerPeriodShadowRel
			one-to-many relation to GeneralLedgerPeriodShadow
			Field Mapping uses BySummaryPeriodType
				related.FinanceEnterpriseGroup      = Company.FinanceEnterpriseGroup
				related.GeneralLedgerCalendar       = Book.ReportingBasis.GeneralLedgerCalendar
				related.GeneralLedgerCalendarPeriod	= first GeneralLedgerCalendarPeriodRel.GeneralLedgerCalendarPeriod
				related.SummaryPeriodType			= 3 
		
		PriorGeneralLedgerPeriodShadowRel
			one-to-many relation to GeneralLedgerPeriodShadow
			Field Mapping uses BySummaryPeriodType
				related.FinanceEnterpriseGroup      = Company.FinanceEnterpriseGroup
				related.GeneralLedgerCalendar       = Book.ReportingBasis.GeneralLedgerCalendar
				related.GeneralLedgerCalendarPeriod	= first PriorGeneralLedgerCalendarPeriodRel.GeneralLedgerCalendarPeriod
				related.SummaryPeriodType			= 3 
				
    Sets
        Set2
            indexed
            Sort Order
                Company
                Book
                AssetType
                ClassDepreciation.YearEndDate
                ClassDepreciation.Period
                ClassDepreciation.Sequence
                











        Set4
            indexed
            Sort Order
                Company
                Book
                ClassDepreciation.YearEndDate
                ClassDepreciation.Period
                AssetType
                ClassDepreciation.Sequence

        Set5
            indexed
            Sort Order
                Book
                AssetType
                Company
                ClassDepreciation

        Set6
            indexed
            Sort Order
                AssetType
                Book
                Company
                ClassDepreciation

        Set1 
            not indexed
            Sort Order
                Company
                Book
                AssetType
                ClassDepreciation
                      
                
	Field Rules
		CreationDate
			default to current timestamp

		CreatedBy
			default to actor

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
