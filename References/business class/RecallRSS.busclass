RecallRSS is a BusinessClass
    owned by recall
    prefix is RMRSS

	Ontology
		symbolic key is RecallRSS

	Patterns
        disable Auditing
        disable EffectiveDated
        implements WSI DataSource

	Persistent Fields
		Link			is a URL
		Description		is Alpha size up to 1000
		Guid			is Alpha size 200
		PubDate			is a Description

    Derived Fields
    
    	ExistingNotice is a ConditionalField
 			type is Alpha size 35
 			if (RecallNoticeCreated)
 				NoticeMessage
 			else
 				blank
    
    	NoticeMessage is a MessageField
    		restricted
    		"Notice<first RecallNoticeNameRel.RecallNotice>HasBeenCreated"
    
    DataSource Mapping
        set is RSS.ListItems
            RecallRSS		= result.Rss.Channel.Item.Title
            Link			= result.Rss.Channel.Item.Link
            Description		= result.Rss.Channel.Item.Description
            Guid			= result.Rss.Channel.Item.Guid
            PubDate			= result.Rss.Channel.Item.PubDate

	Conditions
		HasLink
			restricted
			when (Link entered)

		RecallNoticeCreated
			restricted
			when (RecallNoticeNameRel exists)
			
	Relations
		RecallNoticeNameRel
        	one-to-many relation to RecallNotice
        	Field Mapping uses ByName
        		related.Name			= RecallRSS
		
	Actions
		CreateNotice is an Instance Action
			completion message is "RecallNotice<LocalRecallNotice.RecallNotice>HasBeenCreated" 
			Parameters
				RecallGroup
				AnnouncementDate				is Date
				ReceivedDate					is Date
			    RecallType
		    	RecallTaskInstruction 
		    	FDAClassification				is Numeric size 2
		    		States
		    			ClassI						value is 1
		    			ClassII						value is 2
		    			ClassIII					value is 3
			Parameter Rules
				RecallGroup
					required
				AnnouncementDate
					required
			Local Fields
				LocalRecallNotice	is a RecallNotice view
					
			Action Rules
				invoke RecallRSSCreate Pending RecallNotice
					assign result to LocalRecallNotice
					invoked.RecallGroup  	   		= RecallGroup
					invoked.Name			   		= RecallRSS
					if (Description entered)
						invoked.Description	   		= Description
					else
						invoked.Description	   		= RecallRSS
					invoked.RecallDate	 	   		= AnnouncementDate
					invoked.RecallReceivedDate 		= ReceivedDate
				    invoked.RecallType				= RecallType
			    	invoked.RecallTaskInstruction	= RecallTaskInstruction 
			    	invoked.FDAClassification		= FDAClassification
				
				invoke Create LocalRecallNotice.RecallNotice.RecallNoticeReference set
					invoked.Link			   = Link
					invoked.Description 	   = "RSS article link"
					
		
