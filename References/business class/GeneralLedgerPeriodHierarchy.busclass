GeneralLedgerPeriodHierarchy is a BusinessClass
	default label is "GlobalLedgerPeriodHierarchy"
	owned by GeneralLedger
	
	prefix is GLPHy
	
	Ontology
		part of GeneralLedgerCalendarPeriod
			relative key is SubordinatePeriod is a GeneralLedgerCalendarPeriod
			
	Patterns
	
	Persistent Fields
		TopNode					is AlphaUpper 20
		Weight				 	is Percent 6.3
	
	Transient Fields
		TransientWeight			is Percent 6.3
	
	Context Fields
		GeneralLedgerCalendar
		CreatingWeeksWithinMonths
		
	Local Fields
		KeyReady				is Boolean
		I1						is Numeric 2
		I2						is Numeric 2
		AlphaArray
		LocalSubordinatePeriod  is like GeneralLedgerCalendarPeriod
		
	Relations
		Week52Rel
			one-to-one relation to GeneralLedgerPeriodHierarchy
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.GeneralLedgerCalendarPeriod = GeneralLedgerCalendarPeriod
				related.SubordinatePeriod			= LocalSubordinatePeriod

		NextParent1Rel
			one-to-one relation to GeneralLedgerPeriodHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
				related.TopNode                     = TopNode
				related.SubordinatePeriod           = GeneralLedgerCalendarPeriod
				related.GeneralLedgerCalendarPeriod >  blank

		NextParent2Rel
			one-to-one relation to GeneralLedgerPeriodHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
				related.TopNode                     = TopNode
				related.SubordinatePeriod           = NextParent1Rel.GeneralLedgerCalendarPeriod
				related.GeneralLedgerCalendarPeriod >  blank

		NextParent3Rel
			one-to-one relation to GeneralLedgerPeriodHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
				related.TopNode                     = TopNode
				related.SubordinatePeriod           = NextParent2Rel.GeneralLedgerCalendarPeriod
				related.GeneralLedgerCalendarPeriod >  blank

		NextParent4Rel
			one-to-one relation to GeneralLedgerPeriodHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
				related.TopNode                     = TopNode
				related.SubordinatePeriod           = NextParent3Rel.GeneralLedgerCalendarPeriod
				related.GeneralLedgerCalendarPeriod >  blank

		ParentsInTopNodeRel
			one-to-many relation to GeneralLedgerPeriodHierarchy
			Field Mapping uses SubordinatesByTopNode
				related.FinanceEnterpriseGroup	 	 = FinanceEnterpriseGroup
				related.TopNode			  			 = TopNode
				related.SubordinatePeriod			 = LocalSubordinatePeriod	

	Field Rules
		TopNode
			if (SubordinatePeriod.PeriodType.EndDate)
				Weight = 1
			else
			if (SubordinatePeriod.PeriodType.Year)
				if (SubordinatePeriod like "*_YTD"
				or  SubordinatePeriod like "*_LTD")
					Weight = 0
				else
					Weight = 1
			else
			if (SubordinatePeriod like "*_YTD"
			or  SubordinatePeriod like "*_LTD")		
				if (TransientWeight entered)
					Weight = TransientWeight
				else	
				if (SubordinatePeriod.PeriodType.Month)
					if (GeneralLedgerCalendar.CalendarType.4WeekPeriods)
						if (SubordinatePeriod.Month.Period13)
							Weight = 1
						else
							Weight = 0	
					else
					if (SubordinatePeriod.Month.Period3
					or  SubordinatePeriod.Month.Period6
					or  SubordinatePeriod.Month.Period9
					or  SubordinatePeriod.Month.Period12)
						Weight = 1
					else
						Weight = 0
				else
				if (SubordinatePeriod.PeriodType.Quarter)
					if (SubordinatePeriod.Quarter.Quarter4)
						Weight = 1
				else
				if (SubordinatePeriod.PeriodType.Week)
					if (GeneralLedgerCalendar.CalendarType.Weeks)
						if (SubordinatePeriod.Week.Week52)
							Weight = 1
						else
							Weight = 0
					else			
					if (GeneralLedgerCalendar.CalendarType.445
					or  GeneralLedgerCalendar.CalendarType.5253Week)
						if (SubordinatePeriod.Week.Week4
						or  SubordinatePeriod.Week.Week8
						or  SubordinatePeriod.Week.Week13
						or  SubordinatePeriod.Week.Week17
						or  SubordinatePeriod.Week.Week21
						or  SubordinatePeriod.Week.Week26
						or  SubordinatePeriod.Week.Week30
						or  SubordinatePeriod.Week.Week34
						or  SubordinatePeriod.Week.Week39
						or  SubordinatePeriod.Week.Week43
						or  SubordinatePeriod.Week.Week47						
						or  SubordinatePeriod.Week.Week52)
							Weight = 1
						else
							Weight = 0
					else
					if (GeneralLedgerCalendar.CalendarType.454)
						if (SubordinatePeriod.Week.Week4
						or  SubordinatePeriod.Week.Week9
						or  SubordinatePeriod.Week.Week13
						or  SubordinatePeriod.Week.Week17
						or  SubordinatePeriod.Week.Week22
						or  SubordinatePeriod.Week.Week26
						or  SubordinatePeriod.Week.Week30
						or  SubordinatePeriod.Week.Week35
						or  SubordinatePeriod.Week.Week39
						or  SubordinatePeriod.Week.Week43
						or  SubordinatePeriod.Week.Week48						
						or  SubordinatePeriod.Week.Week52)
							Weight = 1
						else
							Weight = 0
					else
					if (GeneralLedgerCalendar.CalendarType.544)
						if (SubordinatePeriod.Week.Week5
						or  SubordinatePeriod.Week.Week9
						or  SubordinatePeriod.Week.Week13
						or  SubordinatePeriod.Week.Week18
						or  SubordinatePeriod.Week.Week22
						or  SubordinatePeriod.Week.Week26
						or  SubordinatePeriod.Week.Week31
						or  SubordinatePeriod.Week.Week35
						or  SubordinatePeriod.Week.Week39
						or  SubordinatePeriod.Week.Week44
						or  SubordinatePeriod.Week.Week48						
						or  SubordinatePeriod.Week.Week52)
							Weight = 1
						else
							Weight = 0					
				else
					Weight = 1
				if (SubordinatePeriod.Week.Week53)
					KeyReady			   = false
					Weight 				   = 1
					AlphaArray			   = SubordinatePeriod
					I2					   = 20
					I1					   = 19
					while (!KeyReady)
						if (AlphaArray.AlphaElement[I1] = "5"
						and AlphaArray.AlphaElement[I2] = "3")
							AlphaArray.AlphaElement[I2] = "2"
							KeyReady = true
						else
							I1 -= 1
							I2 -= 1
					LocalSubordinatePeriod = AlphaArray
					invoke SetWeight Week52Rel
						invoked.PrmWeight = 0				
			else
				Weight = 1	

			if (TransientWeight entered)
				Weight = TransientWeight

				
	Derived Fields
			
	Sets
		ByParent
			Sort Order
				FinanceEnterpriseGroup
				GeneralLedgerCalendarPeriod
				SubordinatePeriod
		
		ByParentInTopNode
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				GeneralLedgerCalendarPeriod
				SubordinatePeriod
								
		BySubordinate
			Sort Order
				FinanceEnterpriseGroup
				SubordinatePeriod				
				GeneralLedgerCalendarPeriod
				
		SubordinatesByTopNode
			Sort Order
				FinanceEnterpriseGroup
				TopNode
				SubordinatePeriod
				GeneralLedgerCalendarPeriod

	Actions
		Create is an Action
		Update is an Action
		Delete is an Action
		
		SetWeight is an Instance Action
			Parameters
				PrmWeight		is Percent 6.3
					default label is "Weight"
			Action Rules
				Weight = PrmWeight

		BuildShadowFile	is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmGeneralLedgerCalendar		is a GeneralLedgerCalendar
				PrmGeneralLedgerCalendarPeriod	is a GeneralLedgerCalendarPeriod
			Set Is
				PrmFinanceEnterpriseGroup
				PrmGeneralLedgerCalendar
				PrmGeneralLedgerCalendarPeriod
			Instance Selection
				where(FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
				and TopNode = PrmGeneralLedgerCalendar.TopNode
				and (SubordinatePeriod.PeriodType.EndDate
				or	SubordinatePeriod.PeriodType.BeginningBalance)
				and (PrmGeneralLedgerCalendarPeriod not entered
				or GeneralLedgerCalendarPeriod = PrmGeneralLedgerCalendarPeriod))
			Sort Order is SubordinatesByTopNode
			Local Fields
				TopNodeFound				is Boolean
				LocalPeriod					is a GeneralLedgerCalendarPeriod
				ShadowPeriod				is a GeneralLedgerCalendarPeriod
			Action Rules
				Instance Rules
					TopNodeFound  = false
					LocalPeriod   = SubordinatePeriod
					ShadowPeriod  = LocalPeriod
					while (!TopNodeFound)
						if (LocalPeriod exists)
							LocalSubordinatePeriod = LocalPeriod
							if (!ParentsInTopNodeRel.GeneralLedgerCalendarPeriod.PeriodType.TopNode)		
								invoke Create GeneralLedgerPeriodShadow
									resume on error
									invoked.FinanceEnterpriseGroup 			   = FinanceEnterpriseGroup
									invoked.GeneralLedgerCalendar  			   = GeneralLedgerCalendar
									invoked.GeneralLedgerCalendarPeriod		   = ShadowPeriod
									invoked.SummaryGeneralLedgerCalendarPeriod = ParentsInTopNodeRel.GeneralLedgerCalendarPeriod
									invoked.SummaryPeriodType				   = ParentsInTopNodeRel.GeneralLedgerCalendarPeriod.PeriodType
								LocalPeriod = ParentsInTopNodeRel.GeneralLedgerCalendarPeriod
							else 
								TopNodeFound = true
						else
							TopNodeFound = true
