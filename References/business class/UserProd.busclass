UserProd is a BusinessClass
	default label is "DataAreaConfiguration"
    owned by repository
    prefix is uspd
    framework type is LPLConfiguration

	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing
		implements DynamicCreation
		implements ConfigurationMetadata
		implements AuditLogEntryActions
				
    Ontology
        symbolic key is UserProd
    
    Persistent Fields
    	Status 				is a UserObjectStatus
    	LPL             	is Text
    	Description			is a ConfigDescription
    	UserEditorType
    	ErrorText			is Text

	Derived Fields 
		ParseText is a NativeField
			type is Text	

    	SpecInfo is a NativeField
			type is Boolean
			restricted

		ImplementsDataTranslations is a DerivedField
			type is Boolean
			restricted

            if (!LocSpecInfoDerived)
                LocSpecInfoDerived = SpecInfo

            return LocImplementsDataTranslations

		DisableTranslations is a DerivedField
			type is Boolean
			restricted

            if (!LocSpecInfoDerived)
                LocSpecInfoDerived = SpecInfo

            return LocDisableTranslations

	Local Fields    		
		LocParseText is Text
		LocParsed is Boolean 

        LocSpecInfoDerived is Boolean
        LocImplementsDataTranslations is Boolean
        LocDisableTranslations is Boolean
    		
	Relations
		DuplicateRel 
    		one-to-many relation to UserProd 
			Field Mapping uses symbolic key
			Instance Selection		
				where (related.UserProd != UserProd)

        ConfigEntityRel
            one-to-one relation to ConfigEntity
            Field Mapping uses symbolic key
                related.ConfigEntity = UserProd

    Conditions
		ConfigGlobal
			default label is "GlobalConfiguration"
			when (true)
			
		RecordExists
			when (DuplicateRel exists)
			
		HasErrorText
			default label is "ErrorTextEntered"
			when (ErrorText entered)			
			
	Field Rules
		LPL    
			required
			
			if (Status.Active)
				LocParseText = ParseText
			
				if (not LocParseText = blank)




					confirmation required
						"<LocParseText>:_Data_AreaConfigurationIsInvalid.__DoYouWantToSaveAnyway?"
					
					Status = Status.Invalid	
					ErrorText = LocParseText				
		
		UserEditorType
			default to UserEditorType.LPL	
			
		Status
			if (Status.Active)
				initialize ErrorText			
			

   
   	Actions
   		Create is a Create Action
   			valid when (not RecordExists)
   			
   			Action Rules
   				constraint (not RecordExists)
   					"OnlyOneData_AreaConfigurationRecordCanExist"
   			
   			Exit Rules
    			invoke FireEvent
    			
    	CreateFromSnapshot is a Create Action
    		restricted
    		bypass field rules

    			
    		Action Rules
   				constraint (not RecordExists)
   					"OnlyOneData_AreaConfigurationRecordCanExist"
   					
    	Update is an Update Action
    		Exit Rules
    			invoke FireEvent
    			
    	UpdateFromSnapshot is an Update Action
    		restricted
    		bypass field rules


    	Delete is a Delete Action
    		Exit Rules
    			invoke FireEvent
    			
    	DeleteFromSnapshot is a Delete Action
    		restricted
    		bypass relational integrity rules
    	
    	Activate is an Instance Action
    		Action Rules
    			LocParseText = ParseText
				
				if (not LocParseText = blank)
					if (LocParsed)
						confirmation required
							"<LocParseText>:_Data_AreaConfigurationContainsErrors.__DoYouWantToActivateAnyway?"
					else
						constraint(LocParseText = blank)							
							"<LocParseText>:_Data_AreaConfigurationIsInvalid._Cannot_Activate."
    		
    			Status = Status.Active
    			initialize ErrorText
    			
    		Exit Rules
    			invoke FireEvent
    			
    	Deactivate is an Instance Action
    		Action Rules
    			Status =  Status.Inactive
    		Exit Rules
    			invoke FireEvent
    			
    	Invalidate is an Instance Action
    		Action Rules
    			Status = Status.Invalid
    		Exit Rules
    			invoke FireEvent
    			
    	FireEvent is an Instance Action
			restricted    			 
   
   		TagItem is an Instance Action
			Parameters
				ParamConfigurationTag is a ConfigurationTag
					default label is "Tag"
			
			Entrance Rules
				if (Status.Inactive)
					confirmation required
						"DataAreaConfigurationIsInactive,TagAnyway?"
							
				if (Status.Invalid)
					confirmation required
						"DataAreaConfigurationIsInvalid,TagAnyway?"
							
			Action Rules
				invoke Create ConfigurationTagItem
					invoked.ConfigurationTag = ParamConfigurationTag
					invoked.ConfigurationTagItem.Reference = reference to this instance		  

        CreateConfiguration is a Create Action
            Local Fields
                LocalLPLParam is LPL

            Action Rules
                invoke Create this instance
                    invoked.LPL = LocalLPLParam
