DistributionClerkZone is a BusinessClass
    owned by wh
    prefix is DCLKZ

    Ontology
        symbolic key is DistributionClerkZone

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
        Active          is Boolean

    Context Fields
        ContextDistributionClerk    is a DistributionClerk

    Derived Fields
        ActiveMessage is a MessageField
    		restricted
			"Active"

    	InactiveMessage is a MessageField
    		restricted
    		"Inactive"

        StatusMessage is a DerivedField
    		type is MessageField
    		default label is "Status"
    		if (Active)
    			return ActiveMessage
    		else
    			return InactiveMessage

    Relations
        DistributionClerkRel
            one-to-one relation to DistributionClerk
            Field Mapping uses symbolic key
                related.HROrganization                  = ContextDistributionClerk.HROrganization
                related.DistributionClerk               = ContextDistributionClerk

        WarehouseZonesForClerkRel
            one-to-many relation to WarehouseZone
            Field Mapping uses symbolic key
                related.Company                         = DistributionClerkRel.Company
            Instance Selection
                where (related.ZoneIsValidForDistributionClerk)

        WarehouseZoneRel
            one-to-one relation to WarehouseZone
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseZone                   = WarehouseZone
        
        InventoryLocationsRel
            one-to-many relation to InventoryLocation
            Field Mapping uses symbolic key
                related.Company                         = DistributionClerkRel.Company
            Instance Selection
                where (related.UseAsWarehouseLocation)

    Conditions
        RecordExists
            restricted
            when (DistributionClerkZone exists)

    Field Rules
        Active
            default to true

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action

        AssignDistributionClerkToZoneFromLocation is a Create Action
            default label is "Assign_Zone"
            completion message is "ClerkHasBeenAssignToZone/s"
            Parameters
                PrmDistributionClerk        is a DistributionClerk
                PrmInventoryCompany         is an InventoryCompany
                PrmInventoryLocation        is an InventoryLocation
                PrmWarehouseZone            is a WarehouseZone
                PrmAssignClerkTo            is Numeric size 1
                    States
                        AllZonesFromLocation          value is 0
                            default label is "Assign_to_all_Zones_fromLocation"
                        SingleZoneFromLocation        value is 1
                            default label is "Assign_to_a_Zone_fromLocation"
            Parameter Rules
                PrmDistributionClerk
                    initial value is DistributionClerkRel.DistributionClerk
                    default to DistributionClerkRel.DistributionClerk

                PrmInventoryCompany
                    initial value is DistributionClerkRel.Company
                    default to DistributionClerkRel.Company

                PrmInventoryLocation
                    required

                PrmWarehouseZone
                    if (PrmAssignClerkTo.SingleZoneFromLocation)
                        required

            Action Rules
                if (PrmAssignClerkTo.AllZonesFromLocation)
                    if (instance count of WarehouseZonesForClerkRel > 50)
                        invoke BatchCreateOfDistributionClerkZone WarehouseZone in background
                            invoked.PrmCompany                      = PrmInventoryCompany
                            invoked.PrmLocation                     = PrmInventoryLocation
                            invoked.PrmDMClerk                      = PrmDistributionClerk
                    else
                        invoke BatchCreateOfDistributionClerkZone WarehouseZone in foreground
                            invoked.PrmCompany                      = PrmInventoryCompany
                            invoked.PrmLocation                     = PrmInventoryLocation
                            invoked.PrmDMClerk                      = PrmDistributionClerk
                else
                    invoke Create this instance
                        invoked.Company                             = PrmInventoryCompany
                        invoked.InventoryLocation                   = PrmInventoryLocation
                        invoked.DistributionClerk                   = PrmDistributionClerk
                        invoked.WarehouseZone                       = PrmWarehouseZone
                        invoked.Active                              = true

        UnassignDistributionClerkFromZone is a Delete Action
            completion message is "ClerkHasBeenUnassignedFromZone/s"
            default label is "Unassign_Zone"
            confirmation required
                "Unassign_ClerkFromZone/s._Proceed?"
            Action Rules
                invoke Delete DistributionClerkZone
