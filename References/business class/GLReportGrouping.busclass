GLReportGrouping is a BusinessClass
	owned by GeneralLedger
	
	prefix is GLRG
	
	Ontology
		symbolic key is GLReportGrouping
	
	Patterns
	
	Persistent Fields
		Description			is Text
		Private				is Boolean
		Owner				is an Actor
		LastReportRunDate	is TimeStamp
	
	Field Rules
		Owner
			initial value is actor
			default to actor
	
	Conditions
		HasReportGroupAccess
			when((!Private)
			or (Private 
			and Owner = actor)
			or (ApplicationAdministratorActorRoleRel exist))
		
		HasPrivateAccess
			when((action type.Create )
			or (Owner = actor)
			or (ApplicationAdministratorActorRoleRel exist))
		
		HasEditAccess
			when((Owner = actor)
			or (ApplicationAdministratorActorRoleRel exist))
		
		HasOwnerAccess
			when(ApplicationAdministratorActorRoleRel exist)
		
		RunReportsInProcess
			when(RunReportsAsyncActionRequestProcessingRel exists)
		
		ShowRunReports	
			when(RunReportsIsValid 
			and !RunReportsInProcess
			and GLReportGroupingDetailRel exists)
		
		RunReportsIsValid 
			when(IsRunReportsValid)
		
		DisplayAlertMessage	
			when(!IsRunReportsValid or RunReportsInProcess)
	
	Derived Fields
		IsRunReportsValid is a DerivedField
			type is Boolean
			initialize LocalPreviousReportParameter
			LocalPreviousReportParameter = GLReportGroupingDetailRel.GLReport.DerivedReportParameters
			for each GLReportGroupingDetailRel
				if( each.GLReport.DerivedReportParameters = LocalPreviousReportParameter)
					LocalPreviousReportParameter = each.GLReport.DerivedReportParameters
				else
					return false
			return true
		
		DerivedReportsUnderReportGroup is a DerivedField
			type is Numeric 9
			restricted
			return instance count of GLReportGroupingDetailRel
		
		DerivedIsAllReportsGenerated is a DerivedField 
			type is Boolean	
			restricted
			for each GLReportGroupingDetailRel
				if(!(each.GLReport.IntegrationStatus.Generated))
					return false
			return true
		
		DerivedRunReportsMessage is a DerivedField
	    	type is Alpha 50
	    	if (RunReportsIsValid and RunReportsInProcess)
	    		return "Run Reports Action Is In-Progress"
	    	else 
	    		return "Report Parameters Do Not Match"	
		
		DerivedIsFiscalQuarter is a DerivedField
	    	type is Boolean
			if (GLReportGroupingDetailRel exists)
				for each GLReportGroupingDetailRel
					if(each.GLReport.DerivedIsFiscalQuarter)
						return true 
			return false
		
		DerivedIsFiscalMonth is a DerivedField
	    	type is Boolean
			if (GLReportGroupingDetailRel exists)
				for each GLReportGroupingDetailRel
					if(each.GLReport.DerivedIsFiscalMonth)
						return true 
			return false
		
		DerivedHideBalanceType is a DerivedField 
			type is Boolean 
			if (GLReportGroupingDetailRel exists)
				for each GLReportGroupingDetailRel
					if(!each.GLReport.HasMultiLevelAccountDimension)
						return true 
			return false
			
	Local Fields
		LocalPreviousReportParameter 			is Text	
		LocalAsyncId							is a AsyncActionRequest
	
	Actions
		Create is an Action

		Update is an Action
			valid when (HasEditAccess)

			Entrance Rules
				if(Owner changed)
					constraint(Owner changed and HasOwnerAccess)
						"OnlyAppAdminCanChangeTheOwner"
			
		Delete is an Action
			valid when (HasEditAccess)
		
		RunReports is an Instance Action
			Parameters
				ReportFormat			is Numeric 1
					States
						PDF		value is 1
						Excel	value is 2
						CSV		value is 3
				ReportDistributionGroup
					default label is "DistributionGroup_(Optional)"
				ShowTopNodes 			is Boolean
				FinanceEnterpriseGroup
				BalanceType				is Text
					States
						NaturalBalance			value is "Natural Balance"
						FinancialReportBalance 	value is "Financial Report Balance"
				ReportingBasis
				GeneralLedgerCalendar
				GeneralLedgerCalendarTopNode is like GeneralLedgerCalendarPeriod 
				FiscalYear				is Numeric 4
				FiscalMonth				is Numeric 2
				FiscalQuarter           is Numeric 1
				GLChartAccountTopNode	is a GeneralLedgerChartAccount
					default label is "<FinanceEnterpriseGroup.AccountLabel>Structure"
				GeneralLedgerChartAccount
					default label is "<FinanceEnterpriseGroup.AccountLabel>"
				AccountingEntity
				AccountingUnitStructure
				AccountingUnit
				Currency
				ProjectTopNode			is a Project
					default label is "<FinanceEnterpriseGroup.ProjectLabel>Structure"
				Project
					default label is "<FinanceEnterpriseGroup.ProjectLabel>"
				GeneralLedgerSystemCode
					default label is "System"
				Ledger
				Scenario
				FinanceDimension1TopNode is a FinanceDimension1
					default label is "<FinanceEnterpriseGroup.FinanceDimension1Label>Structure"
				FinanceDimension1
					default label is "<FinanceEnterpriseGroup.FinanceDimension1Label>"
				FinanceDimension2TopNode is a FinanceDimension2
					default label is "<FinanceEnterpriseGroup.FinanceDimension2Label>Structure"
				FinanceDimension2
					default label is "<FinanceEnterpriseGroup.FinanceDimension2Label>"
				FinanceDimension3TopNode is a FinanceDimension3
					default label is "<FinanceEnterpriseGroup.FinanceDimension3Label>Structure"			
				FinanceDimension3
					default label is "<FinanceEnterpriseGroup.FinanceDimension3Label>"
				FinanceDimension4TopNode is a FinanceDimension4
					default label is "<FinanceEnterpriseGroup.FinanceDimension4Label>Structure"			
				FinanceDimension4
					default label is "<FinanceEnterpriseGroup.FinanceDimension4Label>"
				FinanceDimension5TopNode is a FinanceDimension5
					default label is "<FinanceEnterpriseGroup.FinanceDimension5Label>Structure"			
				FinanceDimension5
					default label is "<FinanceEnterpriseGroup.FinanceDimension5Label>"
				FinanceDimension6TopNode is a FinanceDimension6
					default label is "<FinanceEnterpriseGroup.FinanceDimension6Label>Structure"			
				FinanceDimension6
					default label is "<FinanceEnterpriseGroup.FinanceDimension6Label>"
				FinanceDimension7TopNode is a FinanceDimension7
					default label is "<FinanceEnterpriseGroup.FinanceDimension7Label>Structure"
				FinanceDimension7
					default label is "<FinanceEnterpriseGroup.FinanceDimension7Label>"
				FinanceDimension8TopNode is a FinanceDimension8
					default label is "<FinanceEnterpriseGroup.FinanceDimension8Label>Structure"
				FinanceDimension8
					default label is "<FinanceEnterpriseGroup.FinanceDimension8Label>"
				FinanceDimension9TopNode is a FinanceDimension9
					default label is "<FinanceEnterpriseGroup.FinanceDimension9Label>Structure"
				FinanceDimension9
					default label is "<FinanceEnterpriseGroup.FinanceDimension9Label>"
				FinanceDimension10TopNode is a FinanceDimension10
					default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>Structure"
				FinanceDimension10
					default label is "<FinanceEnterpriseGroup.FinanceDimension10Label>"

			Parameter Rules
				ReportFormat
					initial value is ReportFormat.PDF
					required
				ReportingBasis
					required
				FiscalYear
					required
				FiscalMonth
					if(!DerivedIsFiscalQuarter)
						required
						constraint(FiscalMonth > 0 and FiscalMonth < 13)
							"FiscalMonthRange_1To_12Only"
				FiscalQuarter
					if(DerivedIsFiscalQuarter)
						required
						constraint(FiscalQuarter > 0 and FiscalQuarter < 5)
							"FiscalQuarterRange_1To_4Only"		
					
			Action Rules
				constraint (DerivedReportsUnderReportGroup <=10)
					"TheReportGroupMustHave10ReportsOrLess"
				constraint (DerivedIsAllReportsGenerated)
					"AllTheReportsUnderTheReportGroupAreNotGenerated"

				invoke RunReportForGroup GLReportGroupingDetail
					invoked.PrmGLReportGrouping		= GLReportGrouping
					invoked.ReportFormat 			= ReportFormat
					invoked.ReportDistributionGroup = ReportDistributionGroup
					invoked.ShowTopNodes			= ShowTopNodes
					invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
					invoked.BalanceType				= BalanceType
					invoked.ReportingBasis			= ReportingBasis
					invoked.GeneralLedgerCalendar	= GeneralLedgerCalendar
					invoked.GeneralLedgerCalendarTopNode = GeneralLedgerCalendarTopNode
					invoked.FiscalYear					    = FiscalYear
					invoked.FiscalMonth						= FiscalMonth
					invoked.FiscalQuarter                   = FiscalQuarter
					invoked.GLChartAccountTopNode			= GLChartAccountTopNode
					invoked.GeneralLedgerChartAccount		= GeneralLedgerChartAccount
					invoked.AccountingEntity				= AccountingEntity
					invoked.AccountingUnitStructure			= AccountingUnitStructure
					invoked.AccountingUnit					= AccountingUnit
					invoked.Currency						= Currency
					invoked.ProjectTopNode					= ProjectTopNode
					invoked.Project							= Project
					invoked.GeneralLedgerSystemCode			= GeneralLedgerSystemCode
					invoked.Ledger							= Ledger
					invoked.Scenario						= Scenario
					invoked.FinanceDimension1TopNode		= FinanceDimension1TopNode
					invoked.FinanceDimension1				= FinanceDimension1
					invoked.FinanceDimension2TopNode		= FinanceDimension2TopNode
					invoked.FinanceDimension2				= FinanceDimension2
					invoked.FinanceDimension3TopNode		= FinanceDimension3TopNode
					invoked.FinanceDimension3				= FinanceDimension3
					invoked.FinanceDimension4TopNode		= FinanceDimension4TopNode
					invoked.FinanceDimension4				= FinanceDimension4
					invoked.FinanceDimension5TopNode		= FinanceDimension5TopNode
					invoked.FinanceDimension5				= FinanceDimension5
					invoked.FinanceDimension6TopNode		= FinanceDimension6TopNode
					invoked.FinanceDimension6				= FinanceDimension6
					invoked.FinanceDimension7TopNode		= FinanceDimension7TopNode
					invoked.FinanceDimension7				= FinanceDimension7
					invoked.FinanceDimension8TopNode		= FinanceDimension8TopNode
					invoked.FinanceDimension8				= FinanceDimension8
					invoked.FinanceDimension9TopNode		= FinanceDimension9TopNode
					invoked.FinanceDimension9				= FinanceDimension9
					invoked.FinanceDimension10TopNode		= FinanceDimension10TopNode
					invoked.FinanceDimension10				= FinanceDimension10
		
	Relations
		GLReportGroupingDetailRel	
			one-to-many relation to GLReportGroupingDetail
			delete cascades
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.GLReportGrouping                = GLReportGrouping
		
		GLReportRel
			one-to-many relation to GLReport
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
			Instance Selection
				where (all GLReportGroupingDetailRel.GLReport != related.GLReport)
		
		ApplicationAdministratorActorRoleRel
            one-to-many relation to ActorRole
            Field Mapping uses symbolic key
                related.Actor           = actor
                related.ActorRole.Role  = "ApplicationAdministrator_ST"
		
		RunReportsAsyncActionRequestProcessingRel
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByClass
				related.ImplementingClass 	= "GLReportGroupingDetail"
				related.AsyncAction 		= "RunReportForGroup"
			Instance Selection
				where (related.MappingField1 = GLReportGrouping
				and   (related.NonFinishedNotInErrorTriggersExist))
