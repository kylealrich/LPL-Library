LandmarkExceptionLogEntry is a BusinessClass
    owned by admincon
    store using com.lawson.rdtech.framework.repository.LandmarkExceptionLogEntryStorage
    
    Ontology
    	symbolic key is LandmarkExceptionLogEntry
    	
    Persistent Fields   

		DataArea
		SystemId 			is Alpha size 100
		FileName			is Alpha size up to 500
		ExceptionDetails 	is Text      
		
	Derived Fields
	    ExceptionDetailsText is a DerivedField
	        type is BinaryDocument
	        return ExceptionDetails
	       
	Actions
		Inquire is a Set Action
			run in foreground
			manual update
			
			Parameters
				ExceptionId is UniqueID
				
			Local Fields
				LocLandmarkExceptionLogEntry is a LandmarkExceptionLogEntry			

			Parameter Rules
				ExceptionId
					required
						
					LocLandmarkExceptionLogEntry = ExceptionId
				
					constraint (LocLandmarkExceptionLogEntry exists)
						"LogEntryNotFoundForSuppliedValue"
						
			Instance Selection
				where (LandmarkExceptionLogEntry = ExceptionId)
			
		Share is an Instance Action		
			Parameters
				To		        is a MultipleEmailAddress
				Cc		        is a MultipleEmailAddress
				Subject         is Alpha size 500
				
			Parameter Rules
				To
					if (not To entered and not Cc entered)
						required
						
				Subject
					required
					initial value is "Exception details for id \"" + LandmarkExceptionLogEntry + "\""
					
			Action Rules
				send email
                	to		To
                	cc		Cc
                	from	actor.ContactInfo.EmailAddress
                	subject	"<Subject>"
                	Contents
                		"Id:<LandmarkExceptionLogEntry>"
                		"SystemId:<SystemId>"
						"FileName:<FileName>"
						"ExceptionDetails:"
						"<ExceptionDetails>"  
        
