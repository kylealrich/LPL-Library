ReportData is a BusinessClass
    owned by la
    prefix is RPDA
    disable data area copy

    Ontology
    	symbolic key is ReportData

    Patterns
    	implements CreateStamp
    	implements UpdateStamp
        disable Auditing
        disable EffectiveDated

    Persistent Fields
    	Name 					is Alpha size 80 	
    	MimeType				is MimeType
    		default label is "MIME_Type"
    	ActionResult
			delete cascades
		RptFile					is Text
			default label is "ReportSourceFile"
			enable alternate document location
		GroupFieldData			is Text
			enable alternate document location
		Description				is Text
		IsDefault               is Boolean
			default label is "Default" 		
        RootProgramCode         is Alpha size 50      
		ProgramCode             is Alpha size 50      

		PrintfileName           is Alpha size 50      


				
		IsUploadIneligible is Boolean 
			default label is "UploadIneligible"
		
    Conditions
    	HasName
    		default label is "NameEntered"
    		when (Name entered)
    		
    	HasGroupFieldData
			default label is untranslatable
			when (HasLocalGroupFieldData or GroupFieldDataIsStoredInCloud)
			
		HasReportSource
			default label is untranslatable
			when (HasLocalRptFile or RptFileIsStoredInCloud)


		HasActionResult
			default label is "ActionResultEntered"
			when (ActionResult entered)

		RptFileIsStoredInCloud
			default label is untranslatable
			when (not RptFileIsLocallyStored)

		GroupFieldDataIsStoredInCloud
			default label is untranslatable
			when (not GroupFieldDataIsLocallyStored)

		DerIsUploadEligible
			default label is untranslatable:"IsUploadEligible"
			when (HasLocalRptFile or HasLocalGroupFieldData)

		HasLocalGroupFieldData
			default label is untranslatable
			when (GroupFieldDataIsLocallyStored and GroupFieldData entered)

		HasLocalRptFile
			default label is untranslatable
			when (RptFileIsLocallyStored and RptFile entered)

		StillProcessing
		    restricted
		    default label is untranslatable
		    when (ActionResult exists
		    and   ActionResult.AsyncActionInvocation exists
		    and   not ActionResult.AsyncActionInvocation.AsyncActionInvocationStatus.Finished)

    Derived Fields
    	GroupFieldDataBinary is a DerivedField
			type is BinaryDocument
			return GroupFieldData
			
    	RptFileBinary is a DerivedField
			type is BinaryDocument
			return RptFile
			
        ReportParameters is a DerivedField
            type is DataView
            



       		return ActionResult.Data	
        
        SelectedReportDistributionGroup is a NativeField
			type is Alpha size 50
			
		ReportLabel is a NativeField
		    type is Alpha size up to 120
        		
        CreatedByInvoke is a DerivedField
            type is Boolean
            
            if (ProgramCode != RootProgramCode)
                return true
            else
                return false

		RptFileS3DownloadUrl is a NativeField
			type is Text
			default label is "ReportFileUrl"

		RptFileS3ObjectKey is a NativeField
			type is Text
			default label is "ReportFileS\3ObjectKey"

		GroupFieldDataS3DownloadUrl is a NativeField
			type is Text
			default label is "Group\Field\DataUrl"

		GroupFieldDataS3ObjectKey is a NativeField
			type is Text
			default label is "Group\Field\DataS\3ObjectKey"

		UploadBatchSize is a NativeField
			type is Numeric size 8
			restricted

		RemainingUploadEligible is a DerivedField
			type is Numeric size 10
			return instance count of UploadEligibleReportData

		UpperCaseDataArea is a DerivedField
			type is DataAreaName
			restricted
			return parentcontext.dataarea  

		RptFileIsLocallyStored is a NativeField
			type is Boolean

		GroupFieldDataIsLocallyStored is a NativeField
			type is Boolean

		UploadToS3Enabled is a NativeField
			type is Boolean
			default label is "UploadToS\3Enabled"

	Local Fields
		LocalAsyncActionRequest is an AsyncActionRequest

	Relations
		RemainingReportData
    		one-to-many relation to ReportData
			Field Mapping uses ByActionResult
				related.ActionResult = ActionResult
			Instance Selection
				where (related.ReportData != ReportData)

		UploadEligibleReportData
			one-to-many relation to ReportData
			Field Mapping uses ByIsUploadIneligible
				related.IsUploadIneligible = false
				related.create stamp > blank

		AsyncActionRequestRel
			default label is "AsyncActionRequest"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
				related.DataArea = UpperCaseDataArea
				related.ImplementingClass = "ReportData"
				related.AsyncAction = "UploadEligibleToS3"
				
		AutoDisableAsyncActionRequestRel
			default label is "AsyncActionRequest"
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
				related.DataArea = UpperCaseDataArea
				related.ImplementingClass = "ReportData"
				related.AsyncAction = "UploadEligibleToS3"
			Instance Selection
				where (related.SystemRequest.AutoDisable)






	Sets
        ByName
            no duplicates
            indexed
            Sort Order
                Name
                ReportData
            Instance Selection
                where (HasName)
				
		ByMimeType
			no duplicates
			indexed
			Sort Order
				MimeType
				ReportData	
				
		ByActorMimeType
			no duplicates
			indexed
			Sort Order
				create stamp.actor
				MimeType
				create stamp
				ReportData	
				
		ByActorName
			no duplicates
			indexed
			Sort Order
				create stamp.actor
				Name
				ReportData

		ByActorTime
			no duplicates
			indexed
			Sort Order
				create stamp.actor
				create stamp descending 
				ReportData
				
        ByTime
            no duplicates
            indexed
            Sort Order
                create stamp descending 
                ReportData  					
		
		ByActionResult
			no duplicates				
			indexed
			Sort Order
				ActionResult
				ReportData
			Instance Selection
				where (HasActionResult)











		ByIsUploadIneligible
			Sort Order
				IsUploadIneligible
				create stamp descending

	Actions
		Create is a Create Action

			Action Rules
				IsUploadIneligible         = not DerIsUploadEligible
				invoke EndWorkUpdateAsyncRequest

			restricted
			
		Update is an Update Action
	
		Delete is a Delete Action
			Entrance Rules
				invoke Create AmazonS3ObjectReference
					invoked.BusinessObjectReference = reference to this instance
					invoked.S3ObjectKey             = RptFileS3ObjectKey
					invoked.CheckLocalField         = "RptFileIsLocallyStored"
				invoke Create AmazonS3ObjectReference
					invoked.BusinessObjectReference = reference to this instance
					invoked.S3ObjectKey             = GroupFieldDataS3ObjectKey
					invoked.CheckLocalField         = "GroupFieldDataIsLocallyStored"

			Exit Rules
				if (not RemainingReportData exists)
					invoke Delete ActionResult
						resume on error 
						
        SendFailureNotification is an Instance Action
            restricted
            
            Local Fields
                FailNotificationMessage is Alpha size 100
            
            Parameters 

                AsyncActionRequestParam is a AsyncActionRequest
                	default label is "AsyncActionRequest"
                AsyncActionTriggerParam is a AsyncActionTrigger
                	default label is "AsyncActionTrigger"
                ExportType is Alpha size 5 
                
            Action Rules

                FailNotificationMessage = "Failure generating " + ExportType + " report for \"" + Name + "\"" 
                
                invoke Create UserNotification
                    invoked.SourceObject = reference to AsyncActionTriggerParam
                    invoked.Actor = create stamp.actor
                    invoked.SourceType = 6
                    invoked.NavigationName = "MyAsyncActionTriggerFormNav"
                    invoked.Description = FailNotificationMessage
                    
        						
		Convert is an Instance Action
			default label is "GenerateReport"
			Parameters				
                ReportDistributionGroup	
				ReportExportType
				
			Parameter Rules
				ReportDistributionGroup
					initial value is SelectedReportDistributionGroup
						
		ScheduleConvert is an Instance Action
			default label is "ScheduleReportGeneration"
			Parameters			
                ReportDistributionGroup
                ReportExportType
                
			Parameter Rules
				ReportDistributionGroup
					initial value is SelectedReportDistributionGroup

		PurgeData is a Set Action
			Parameters
				ThruDate              is Date
				PurgeOffsetDays       is Numeric size 3
				IncludeReportDocument is Boolean
			Parameter Rules
				ThruDate
					if (ThruDate entered)
						LocalThruDate = (ThruDate + 1 day)
					else
						if (PurgeOffsetDays not entered)
							required
								"MustEnterThruDateOrOffset"
						else
							LocalThruDate = (current date - (PurgeOffsetDays - 1))
				PurgeOffsetDays
					constraint (ThruDate not entered)
						"CannotEnterBothThruDateAndOffset"
			Local Fields
				LocalThruDate  is Date
					value is ThruDate
				LocalReportDoc is a ReportDocument
			Instance Selection
				where (create stamp <  LocalThruDate)
			Sort Order is ByTime
			Action Rules
				Instance Rules
					if (IncludeReportDocument)
						for each (RepDoc) ReportDocument set
							LocalReportDoc = each(RepDoc).ReportDocument
							for each(UserRep) LocalReportDoc.UserReport set
								invoke Delete each(UserRep)
					invoke Delete


		UploadRptFileToS3 is an Instance Action
			default label is "UploadReportFileToS\3"
			restricted

		UploadGroupFieldDataToS3 is an Instance Action
			default label is "UploadGroup\Field\DataToS\3"
			restricted

		UploadEligibleToS3 is a Set Action
			default label is "UploadEligibleToS\3"
			disable checkpoint
			Sort Order is ByIsUploadIneligible
			Instance Selection
				where (not IsUploadIneligible
				and    not StillProcessing)

			Local Fields
				Count                is Numeric size 8
					value is 0

				LocalUploadBatchSize is Numeric size 8
					value is UploadBatchSize

				TransferredSize is Numeric size 15
					value is 0

				TotalTransferredSize is Numeric size 25
					value is 0

				StartTime is TimeStamp

			Action Rules

				Set Rules
					Entrance Rules
						StartTime = current timestamp

					Exit Rules
						invoke SubmitUploadMetrics
							invoked.TransferredSize  = TotalTransferredSize
							invoked.TransferredCount = Count
							invoked.StartTime        = StartTime
							invoked.EndTime          = current timestamp

						if (UploadEligibleReportData exists)
							invoke EndWorkUpdateAsyncRequest

				Instance Rules
					if ((UploadBatchSize > 0
					and Count >= UploadBatchSize)
					or not UploadToS3Enabled)
						end set action instance loop

					if (HasLocalRptFile)
						TransferredSize = RptFile size
						invoke UploadRptFileToS3
							resume on error
								initialize TransferredSize
						TotalTransferredSize += TransferredSize

					if (HasLocalGroupFieldData)
						TransferredSize = GroupFieldData size
						invoke UploadGroupFieldDataToS3
							resume on error
								initialize TransferredSize
						TotalTransferredSize += TransferredSize

					IsUploadIneligible = not DerIsUploadEligible

					increment Count

		DownloadRptFileToDatabase is an Instance Action
			default label is "DownloadReportFileToDatabase"
			valid when (RptFileIsStoredInCloud)

			Exit Rules
				invoke Create AmazonS3ObjectReference
					invoked.BusinessObjectReference = reference to this instance
					invoked.S3ObjectKey             = RptFileS3ObjectKey
					invoked.CheckLocalField         = "RptFileIsLocallyStored"

		DownloadGroupFieldDataToDatabase is an Instance Action
			default label is "DownloadGroup\Field\DataToDatabase"
			valid when (GroupFieldDataIsStoredInCloud)

			Exit Rules
				invoke Create AmazonS3ObjectReference
					invoked.BusinessObjectReference = reference to this instance
					invoked.S3ObjectKey             = GroupFieldDataS3ObjectKey
					invoked.CheckLocalField         = "GroupFieldDataIsLocallyStored"

		SubmitUploadMetrics is an Instance Action
			restricted
			Parameters
				TransferredSize  is Numeric size 25
				TransferredCount is Numeric size 8
				StartTime        is TimeStamp
				EndTime          is TimeStamp

		EndWorkUpdateAsyncRequest is an Instance Action
			restricted
			manual update


		UpdateAsyncRequest is an Instance Action
			restricted

			Action Rules

				LocalAsyncActionRequest = first AutoDisableAsyncActionRequestRel.AsyncActionRequest
				if (not LocalAsyncActionRequest exists)
					LocalAsyncActionRequest = first AsyncActionRequestRel.AsyncActionRequest

				if (LocalAsyncActionRequest exists)
					invoke SetPendingScheduling LocalAsyncActionRequest
						invoked.ParamPendingScheduling = true
