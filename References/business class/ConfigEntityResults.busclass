ConfigEntityResults is a BusinessClass
    owned by repository
    prefix is coer
    default label is "ConfigurationEntityResults"

    Ontology
        symbolic key is ConfigEntityResults

    Patterns
    	disable Auditing
    	implements CreateStamp
    	implements UpdateStamp
    	implements DynamicCreation



    Persistent Fields
    	Results			is Text
    	Type            is a ConfigEntityResultsType

	Actions
		
		Create is a Create Action
			restricted
			
		Delete is a Delete Action
		
		ValidateConfigurations is a Set Action
			run in foreground
			no records message is "ProcessingComplete"
			Parameters
				UpdateConfigStatus is Boolean
				StrictParsing is Boolean
			Instance Selection
				where (false)
			Action Rules
				Empty Set Rules
					invoke ValidateConfigurations ValidateConfigsTask in foreground
						invoked.UpdateConfigStatus   = UpdateConfigStatus
						invoked.ConfigBundleSnapshot = blank
						invoked.ProcessRollback = false
						invoked.StrictParsing = StrictParsing
				
		ScheduleValidateConfigurations is a Set Action
			run in background
			no records message is "ProcessingComplete"
			Parameters
				UpdateConfigStatus is Boolean
				StrictParsing is Boolean
				EmailAddresses is MultiEmailAddressField
			Local Fields
				Results is Text
			Parameter Rules
				UpdateConfigStatus
					if (UpdateConfigStatus)
						confirmation required
							"UpdatingTheConfigurationStatusCouldCauseChangesWhenThisActionRuns.AreYouSure?"
				StrictParsing
					initial value is true
			Instance Selection
				where (false)
			Action Rules
				Empty Set Rules
					invoke ValidateConfigurations ValidateConfigsTask in foreground
						assign result to Results
						invoked.UpdateConfigStatus   = UpdateConfigStatus
						invoked.ConfigBundleSnapshot = blank
						invoked.ProcessRollback = false
						invoked.StrictParsing = StrictParsing
						
					if (EmailAddresses entered)
						send email
		                	to		EmailAddresses
		                	from	actor.ContactInfo.EmailAddress
		                	subject	"ConfigurationValidationResult"
		                	Contents	
		                		"ConfigurationValidationResultAttached"
	                		attachment Results
	                			name is "Results.txt"
	                			mime type is "text/plain"
										
    Sets
    
		ByCreateStamp
			Sort Order
			    create stamp descending
			    ConfigEntityResults

		ByUpdateStamp
			Sort Order
			    update stamp descending
			    ConfigEntityResults

