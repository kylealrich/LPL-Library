OAuth is a BusinessClass
    owned by ws
    prefix is OA
    contains environment data
	disable data area copy
		preserve target data
    default label is "O\Auth"
    
    Ontology
        symbolic key is OAuth

    Patterns
        disable Auditing
        disable EffectiveDated
        disable UniqueID

    Persistent Fields
		Type						is Numeric size 2	
			States
				Yammer 				value is 1 
				TalentTech			value is 2
				Generic10a          value is 3
				Generic10aSHA256    value is 4
				Generic20           value is 5
				
        RequestTokenURL             is Alpha size 1000
        AccessTokenURL              is Alpha size 1000
        AuthorizeURL                is Alpha size 1000

        ApplicationKey              is Alpha size 200
        ApplicationSecret           is Alpha size 200

        ResourceUser                is Alpha size 200
        ResourcePassword            is Alpha size 200

        AuthenticationURL           is Alpha size 1000
        PIN                         is Alpha size 100

        AuthenticationToken         is Alpha size 100
        AuthenticationTokenSecret   is Alpha size 100

        AuthorizationCode           is Alpha size 100

        AccessToken                 is Alpha size 100
        RefreshToken                is Alpha size 100

        RedirectURL                 is Alpha size 1000
        AuthorizeUsingBrowser       is Boolean

        TokenType                   is Alpha size 100
        TokenName                   is Alpha size 100

        Scope                       is Alpha size 100

        GrantType                   is Numeric size 2
            States
                AuthorizationCode 				value is 1
                ClientCredentials 				value is 2
                Password         				value is 3

        ClientAuthentication                    is Numeric size 2
            States
                SendClientCredentialsInBody		value is 1
                SendAsBasicAuthHeader			value is 2
                    default label is "SendAsBasicAuthenticationHeader"

        ThirdPartyApplicationName              is Alpha size 100

    Transient Fields
        ThirdPartyApplicationJSONResponse      is JSONObject
        DataAreaUrl                             is URL
        EnvironmentDataAreaUrl                  is URL
        DataAreaName                            is Alpha size 50

    Relations
    	ThirdPartyApplicationRel
    	    one-to-one relation to ThirdPartyApplication
                Field Mapping uses symbolic key
                    related.ThirdPartyApplication = ThirdPartyApplicationName

    Actions
        Create is a Create Action
        Update is an Update Action
        Delete is a Delete Action

		GetAuthenticationURL is an Instance Action
        GrantAccess is an Instance Action

        GetNewAccessToken is an Instance Action

        GetAuthorizationCode is an Instance Action
        ExchangeAuthorizationCodeForAccessToken is an Instance Action

        RefreshToken is an Instance Action

        ProvisionInterviewSchedulingApplication is an Instance Action
			Parameters
				PrmContactEmail         is Alpha size 100
					default label is "ContactEmail"

            Action Rules
                ThirdPartyApplicationName = DataAreaName + ":InterviewScheduling:Integration"
                if (ThirdPartyApplicationRel not exists)
                    invoke Create ThirdPartyApplication
                        invoked.ThirdPartyApplication	= ThirdPartyApplicationName
                        invoked.Name	                = ThirdPartyApplicationName
                        invoked.URL	                    = EnvironmentDataAreaUrl
                        invoked.ApplicationType         = 1
                        invoked.ContactEmail            = PrmContactEmail
                        invoked.RedirectURI1            = DataAreaUrl + "/soap/ldrest/OAuth/Update_OAuthRecord_FormOperation?OAuth=" + OAuth + "&AuthorizationCode=_url(code)&_out=CLOSE"
                        invoked.DisplayName             = ThirdPartyApplicationName

                ApplicationKey                  = ThirdPartyApplicationRel.OAuthKey
                ApplicationSecret               = ThirdPartyApplicationRel.OAuthSecret
                AuthorizeURL                    = "https://app.cronofy.com/oauth/authorize"
                AccessTokenURL                  = "https://api.cronofy.com/oauth/token"
                GrantType                       = 1
                Scope                           = "read_write"
                ClientAuthentication            = 1
                RedirectURL                     = DataAreaUrl + "/soap/ldrest/OAuth/Update_OAuthRecord_FormOperation?OAuth=" + OAuth + "&AuthorizationCode=_url(code)&_out=CLOSE"



