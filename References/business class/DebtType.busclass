DebtType is a BusinessClass
	owned by treasury
	prefix is DMDT	

	Ontology
		symbolic key is DebtType
		
	Persistent Fields								
		Description		  is Alpha up to 250
		Active

	Local Fields
		NewParentDebtType is a DebtType
		
	Derived Fields
		NewParentDebtTypeIsAChildDebtType is a DerivedField
			type is Boolean
			restricted
   			for each NewParentDebtType and ancestors
				if (each.ParentDebtType = DebtType)
					return true
					end for each
					
	Field Rules
		Description
			default to DebtType
		
		Active
			initial value is true
	
	Conditions
		HasParentDebtType
			restricted
    		when (ParentDebtType entered)
    		
    	HasChild
    		restricted
    		when (DebtType children exist)
    				
  	Attach Rules
		constraint (Active)
			"DebtTypeIsInactive"
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
			
		ReparentDebtType is an Instance Action
			Parameters
				PrmParentDebtType is a DebtType
				
			Parameter Rules
				PrmParentDebtType
					required
						"NewDebtTypeIsRequired"
						
					constraint (PrmParentDebtType != DebtType)
						"CannotSelectDebtTypeToBeItsOwnParent"
						
					constraint (PrmParentDebtType != ParentDebtType)
						"NewParentIsTheSameAsCurrentParent"
						
			Action Rules
				NewParentDebtType = PrmParentDebtType
				
				constraint (!NewParentDebtTypeIsAChildDebtType)
					"CannotChooseAChildDebtTypeToBeTheNewDebtType"
					
				ParentDebtType = NewParentDebtType
				
		RemoveParentDebtType is an Instance Action
			valid when (HasParentDebtType)
			Action Rules
				initialize ParentDebtType
				
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted	
