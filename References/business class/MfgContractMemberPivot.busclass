MfgContractMemberPivot is a BusinessClass
    owned by po
    prefix is MFCP
    
    Ontology
  		symbolic key is MfgContractMemberPivot

    Persistent Fields
    	PurchaseMethod 				
    	AvailableForDistribution 	is Boolean						
    	
    Local Fields
     	LocalMfgContract					is a Contract       
        LocalMfgCompany     				is like InventoryCompany
        LocalMfgLocation    				is like InventoryLocation
        LocalMfgReqLocation 				is like RequestingLocation
        LocalPricingGroup           		is like PricingGroup
        LocalDistributorContract 			is like Contract
		LocalNbrPricingMembers				is Numeric size 3 
		LocalOriginalManufacturerContract	is a Contract
		
	Context Fields
		ContextDistributorContract    is a Contract

	Derived Fields
			
	Conditions
		DistributorMembers
			restricted
			when (PurchaseMethod.FromDistributor
			or    PurchaseMethod.FromManufacturerAndDistributor)
			
		ContextContractMemberSelected
			restricted
			when (ContextContractMemberRel exists)

		ContextContractMemberNotSelected
			restricted
			when (ContextContractMemberRel !exists)

		TierEntered
			restricted
			when (TierMemberRel.Tier entered)
		
	Relations
		ContractRel
			one-to-one relation to Contract
			Field Mapping uses symbolic key
				related.ContractGroup = ContractGroup
				related.Contract      = ManufacturerContractPivot
				
		MfgContractRel
			one-to-one relation to ManufacturerContract
			Field Mapping uses ByPivotId
				related.ContractGroup 				= ContractGroup
				related.Contract      				= LocalDistributorContract
				related.ManufacturerContractNumber  = ManufacturerContractPivot
				
		TierMemberRel
			one-to-one relation to ContractTierMember
			Field Mapping uses symbolic key
				related.ContractGroup 				  = ContractGroup
				related.Contract      				  = ManufacturerContractPivot
				related.ContractTierMember.Company  = LocalMfgCompany
				related.ContractTierMember.Location = LocalMfgLocation
				related.ContractTierMember.RequestingLocation	  = LocalMfgReqLocation
				related.ContractTierMember.PricingGroup           = LocalPricingGroup
				
		TierMemberListRel
			one-to-one relation to ContractTierMember
			Field Mapping uses symbolic key
				related.ContractGroup 				   					= ContractGroup
				related.Contract      				   					= ManufacturerContractPivot
				related.ContractTierMember.Company  					= MfgContractMemberPivot.ParticipantLocation.Company
				related.ContractTierMember.Location 					= MfgContractMemberPivot.ParticipantLocation.Location
				related.ContractTierMember.RequestingLocation	   		= MfgContractMemberPivot.ParticipantLocation.RequestingLocation
				related.ContractTierMember.PricingGroup                 = MfgContractMemberPivot.ParticipantLocation.PricingGroup
	
		ContractLineMemberDistributorRel
			one-to-many relation to ContractLineMember
			Field Mapping uses symbolic key
				related.ContractGroup 				   					= ContractGroup
				related.Contract      				   					= ManufacturerContractPivot
			Instance Selection
				where (related.ContractLineMember.Company  			= MfgContractMemberPivot.ParticipantLocation.Company
				and    related.ContractLineMember.Location			= MfgContractMemberPivot.ParticipantLocation.Location
				and    related.ContractLineMember.RequestingLocation	= MfgContractMemberPivot.ParticipantLocation.RequestingLocation
				and    related.ContractLineMember.PricingGroup      = MfgContractMemberPivot.ParticipantLocation.PricingGroup
				and    related.CreateDistributorLineMember)
		
		ContextContractMemberRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup 											= ContractGroup
				related.Contract      											= ContextDistributorContract
				related.ContractDistributorPricingMember.Company	 			= MfgContractMemberPivot.ParticipantLocation.Company
				related.ContractDistributorPricingMember.Location	 			= MfgContractMemberPivot.ParticipantLocation.Location
				related.ContractDistributorPricingMember.RequestingLocation		= MfgContractMemberPivot.ParticipantLocation.RequestingLocation
				related.ContractDistributorPricingMember.PricingGroup       	= MfgContractMemberPivot.ParticipantLocation.PricingGroup
				related.ContractDistributorPricingMember.ManufacturerContract 	= ManufacturerContractPivot

		PricingMemberRel
			one-to-one relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup 								 		= ContractGroup
				related.Contract      								 		= LocalDistributorContract
				related.ContractDistributorPricingMember.Company	 		= LocalMfgCompany
				related.ContractDistributorPricingMember.Location	 		= LocalMfgLocation
				related.ContractDistributorPricingMember.RequestingLocation	= LocalMfgReqLocation
				related.ContractDistributorPricingMember.PricingGroup       = LocalPricingGroup
				related.ContractDistributorPricingMember.ManufacturerContract = LocalMfgContract
				
		OriginalPricingMemberRel
			one-to-one relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup 								 		= ContractGroup
				related.Contract      								 		= LocalDistributorContract
				related.ContractDistributorPricingMember.Company	 		= LocalMfgCompany
				related.ContractDistributorPricingMember.Location	 		= LocalMfgLocation
				related.ContractDistributorPricingMember.RequestingLocation	= LocalMfgReqLocation
				related.ContractDistributorPricingMember.PricingGroup       = LocalPricingGroup
				related.ContractDistributorPricingMember.ManufacturerContract = LocalOriginalManufacturerContract

		DefaultPricingRel
			one-to-many relation to ContractDistributorPricing
			Field Mapping uses symbolic key
				related.ContractGroup = ContractGroup
				related.Contract      = LocalDistributorContract
            Instance Selection
            	where (related.DefaultDistributorPricing = "Y")
            	
		PurchaseFromMfgAndDistributorContractLineRel
			one-to-many relation to ContractLine
			Field Mapping uses symbolic key
				related.ContractGroup = ContractGroup
				related.Contract      = ManufacturerContractPivot
			Instance Selection
				where  (related.CreateDistributorContractLine)
				
  	Field Rules
  			
  	
  	Actions     	 	
 		Create is a Create Action
 			restricted
 		
 		Update is an Update Action
 			restricted
 		
 		Delete is a Delete Action
 			restricted
 			
 		Purge is a Purge Action
 			restricted
							
		CreateDistributorPricingMembers is a Set Action
			restricted
			Parameters
				PrmContractGroup				is a ContractGroup
				PrmManufacturerContract			is a Contract
				PrmDistributorContract			is a Contract
				FromCreateFull              	is Boolean 
				OriginalManufacturerContract	is a Contract
				PrmCloseExistingLines           is Boolean 
				
			Instance Selection
				where (ContractGroup				= PrmContractGroup
				and    ManufacturerContractPivot	= PrmManufacturerContract
				and   (DistributorMembers
				or     AvailableForDistribution)
				and    TierMemberListRel.Tier entered)

			Sort Order
				ContractGroup
				ManufacturerContractPivot

			Action Rules
				
				ManufacturerContractPivot Set Rules 
					Exit Rules 
						if (FromCreateFull)
						 	invoke AddAllLinesToDistributorFromCopy MfgContractRel
								invoked.PrmCloseExistingLines	= PrmCloseExistingLines
				
				Instance Rules
					LocalDistributorContract 			= PrmDistributorContract
					LocalMfgContract		 			= ManufacturerContractPivot
	           		LocalMfgCompany		     			= MfgContractMemberPivot.ParticipantLocation.Company
	            	LocalMfgLocation		 			= MfgContractMemberPivot.ParticipantLocation.Location
	            	LocalMfgReqLocation		 			= MfgContractMemberPivot.ParticipantLocation.RequestingLocation
					LocalPricingGroup        			= MfgContractMemberPivot.ParticipantLocation.PricingGroup
					LocalOriginalManufacturerContract	= OriginalManufacturerContract
					if (!PricingMemberRel exists)
						invoke Create ContractDistributorPricingMember 
		            		invoked.ContractGroup								 			= ContractGroup
		            		invoked.Contract									 			= PrmDistributorContract
		            		invoked.ContractDistributorPricingMember.Company	 			= TierMemberListRel.ContractTierMember.Company
		            		invoked.ContractDistributorPricingMember.Location	 			= TierMemberListRel.ContractTierMember.Location
		            		invoked.ContractDistributorPricingMember.RequestingLocation		= TierMemberListRel.ContractTierMember.RequestingLocation
		            		invoked.ContractDistributorPricingMember.PricingGroup           = TierMemberListRel.ContractTierMember.PricingGroup
		            		invoked.ContractDistributorPricingMember.ManufacturerContract 	= TierMemberListRel.Contract
		            		invoked.MfgContractTierInfo.MfgContract				 			= TierMemberListRel.Contract
		            		invoked.MfgContractTierInfo.Tier					 			= TierMemberListRel.Tier
		            		if (OriginalPricingMemberRel exists)
								invoked.PricingIdentifier                                   = OriginalPricingMemberRel.PricingIdentifier
							if (DefaultPricingRel exists)
		            			for each DefaultPricingRel
		            				invoked.PricingIdentifier					 			= DefaultPricingRel.ContractDistributorPricing
		
		AddMembersToDistributor is an Instance Action
			completion message is "MemberRecordsCreated"		
			Parameters
				Contract
			Parameter Rules
				Contract
					required
			Action Rules
				LocalDistributorContract = Contract
				LocalMfgContract		 = ManufacturerContractPivot
	            LocalMfgCompany		     = MfgContractMemberPivot.ParticipantLocation.Company
	            LocalMfgLocation		 = MfgContractMemberPivot.ParticipantLocation.Location
	            LocalMfgReqLocation		 = MfgContractMemberPivot.ParticipantLocation.RequestingLocation
	            LocalPricingGroup        = MfgContractMemberPivot.ParticipantLocation.PricingGroup
				
				constraint (TierEntered)
					"MemberDoesNotHaveATier;CannotSelectForDistributorContract"
				
		      	constraint (!PricingMemberRel exists)
					"MemberAlreadyAttachedToDistributorContractForThisManufacturerContract"
            	
            	if (!PricingMemberRel exists
            	and TierMemberRel exists)
	            	invoke Create ContractDistributorPricingMember 
	            		invoked.ContractGroup								 			= ContractGroup
	            		invoked.Contract									 			= Contract
	            		invoked.ContractDistributorPricingMember.Company	 			= TierMemberRel.ContractTierMember.Company
	            		invoked.ContractDistributorPricingMember.Location	 			= TierMemberRel.ContractTierMember.Location
	            		invoked.ContractDistributorPricingMember.RequestingLocation		= TierMemberRel.ContractTierMember.RequestingLocation
	            		invoked.ContractDistributorPricingMember.PricingGroup           = TierMemberRel.ContractTierMember.PricingGroup
	            		invoked.ContractDistributorPricingMember.ManufacturerContract 	= TierMemberRel.Contract
	            		invoked.MfgContractTierInfo.MfgContract				 			= TierMemberRel.Contract
	            		invoked.MfgContractTierInfo.Tier					 			= TierMemberRel.Tier
	            		if (DefaultPricingRel exists)
	            			for each DefaultPricingRel
	            				invoked.PricingIdentifier					 			= DefaultPricingRel.ContractDistributorPricing
	            	LocalNbrPricingMembers += 1
	            		

		    					
