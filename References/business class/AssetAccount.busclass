AssetAccount is a BusinessClass
    owned by am
    prefix is AAC
    classic name is AMACCOUNT

    Ontology
        symbolic key is AssetAccount
            classic set name is AACSET1
            classic name for AssetAccount.AssetAccountType is ACCT-TYPE
            classic name for AssetAccountGroup is ACCT-GRP
            classic name for AssetType.Type is ASSET-TYPE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		DistributionAccount		is a FinanceCodeBlockAccountOnly 


	Context Fields
		AssetCompany







    Conditions
        IsAssetAccount
            classic name is ASSET-ACCT
            restricted
            when (AssetAccount.AssetAccountType.Asset)
            
	Field Rules
		AssetType
			if  (AssetType.SubType = blank
			and (AssetAccount.AssetAccountType = "AST"
			or   AssetAccount.AssetAccountType = "ACM"
			or   AssetAccount.AssetAccountType = "EXP")
			and (AssetAccount.AssetEvent = ""))
				constraint (AssetAccount entered)
					"AccountIsRequiredIfNoAssetSubTypeIsEntered"	
	
		AssetAccount.AssetAccountType
			if 	(AssetAccount.AssetAccountType = "PRO"
			or   AssetAccount.AssetAccountType = "GAN"			
			or   AssetAccount.AssetAccountType = "LOS"
			or   AssetAccount.AssetAccountType = "CLR"
			or   AssetAccount.AssetAccountType = "COS")
				constraint (DistributionAccount.GeneralLedgerChartAccount entered)
					"AccountIsRequired"	
				
    Sets

	Relations
		AssetRel is an Asset set

		AssetTypeRel
			one-to-one relation to AssetType
			Field Mapping uses symbolic key
				related.Company							= Company
				related.AssetType.Type					= AssetAccount.AssetType.Type
				related.AssetType.SubType				= AssetAccount.AssetType.SubType
				

	Rule Blocks
		
		AssetTypeEdits	
			if (AssetTypeRel.Property.Land
			or  AssetTypeRel.Property.Stock)
				constraint (!AssetTypeRel.DepreciableAssetEnteredRel.AssetCategory.Depreciable
				and			!AssetTypeRel.DepreciableAssetReleasedRel.AssetCategory.Depreciable
				and			!AssetTypeRel.DepreciableAssetPostReleaseRel.AssetCategory.Depreciable
				and			!AssetTypeRel.DepreciableAssetDisposedRel.AssetCategory.Depreciable)
					"PropertyType<AssetTypeRel.Property>IsNotValidWithDepreciableAsset"	
	
    Actions
        Create is a Create Action
			restricted
			Action Rules
				if (AssetType entered)        	
    	    		include AssetTypeEdits					

        Update is an Update Action
			restricted

        Delete is a Delete Action
			restricted


					


