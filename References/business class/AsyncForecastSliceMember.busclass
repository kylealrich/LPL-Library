AsyncForecastSliceMember is a BusinessClass
	owned by async
    prefix is AFSM
    stored in environment
	
	Ontology
		part of AsyncForecastSlice
			relative key is AsyncActionRequest is like AsyncActionRequest



	Patterns
		disable Auditing
        disable EffectiveDated
        disable AsOfDateProcessing
        implements UniqueID
        implements CreateStamp
        
    Persistent Fields
    	RequestCount is Numeric size 6
		
	Actions
		Create is a Create Action
			restricted
			Action Rules
				increment RequestCount 
			
			Exit Rules
				increment AsyncForecastSlice.MemberCount
		
		Update is an Update Action
			restricted
			
			Local Fields
				RequestCountChange is Numeric size 6
			
			Action Rules
				if (RequestCount changed)
					RequestCountChange = (RequestCount - old RequestCount)
					
					if (RequestCountChange > 0)
						increment AsyncForecastSlice.MemberCount by RequestCountChange
					else
						if (RequestCountChange < 0)
							decrement AsyncForecastSlice.MemberCount by RequestCountChange
					
		AdjustRequestCount is an Instance Action
			restricted
			Parameters
				AdjustCountValue is Numeric size 6
			
			Action Rules
				if (AdjustCountValue > 0)
					increment RequestCount by AdjustCountValue
					increment AsyncForecastSlice.MemberCount by AdjustCountValue
				else
					if (AdjustCountValue < 0)
						decrement RequestCount by AdjustCountValue
						decrement AsyncForecastSlice.MemberCount by AdjustCountValue		
		
		Delete is a Delete Action
			restricted
			
			Action Rules
				decrement AsyncForecastSlice.MemberCount by RequestCount
			
	Relations
		ActionRequestRel 
			one-to-one relation to AsyncActionRequest

			Field Mapping uses symbolic key
				related.AsyncActionRequest = AsyncActionRequest
	
	Sets
		ByAsyncRequest
			indexed
			Sort Order
				AsyncActionRequest
				AsyncSchedulingForecast
				SliceKey
