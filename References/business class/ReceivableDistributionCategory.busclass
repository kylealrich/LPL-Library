ReceivableDistributionCategory is a BusinessClass
	owned by ar
	prefix is ACA
	classic name is ARCATEGORY

	Ontology
		symbolic key is ReceivableDistributionCategory
			classic set name is ACASET1
			classic name is AR-CATEGORY

	Patterns
		implements StaticJava
		disable AuditIndex
		implements ContextualParent

	Persistent Fields


		Description
		TaxCode
		TaxExempt					is Boolean
			classic name is TAX-EXEMPT-CD
			default label is "PreventTaxCodeDefaulting"
		AccountingUnitOverrideFlag	is AlphaUpper size 1
			default label is "ProcessLevelFinanceCodeBlockOverride"
			classic name is ACCT-UNIT-FLAG
			States
				AllowProcessLevelFinanceCodeBlockOverride			 value is "N"
				PreventProcessLevelFinanceCodeBlockOverride 		 value is "Y"

		IntrastatLineType			is AlphaUpper size 1
			classic name is LINE-TYPE
			States
				Goods		value is "G"
				Service		value is "S"
		DistributionAccount			is a FinanceCodeBlockFull
			classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
			classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
			classic name for DistributionAccount.Project is ACTIVITY

	Local Fields
		CalculateTax
		LocalCalculateTaxExecuted		is Alpha 1

	Relations
	   	TaxEntityRel				 
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.TaxEntity 				= GeneralLedgerCompanyRel.AccountingEntity







		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company


	Field Rules
		Description
			required

		DistributionAccount
			required

		AccountingUnitOverrideFlag
			default to AccountingUnitOverrideFlag.AllowProcessLevelFinanceCodeBlockOverride

		TaxCode
			if (TaxExempt)
				cannot be entered
					"TaxCodeCannotBeEnteredIfPreventTaxCodeDefaultingIsSelected"						  
			if (TaxEntityRel.ThirdParty = blank

			or TaxEntityRel.ThirdParty = "O")	//"Q") - tlc
				constraint (!TaxExempt)
					"CannotPreventTaxCodeDefaultingWhenTaxCodeIsEntered"









	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)

		AllowProcessLevelFinanceCodeBlockOverride
			restricted
			when (AccountingUnitOverrideFlag.AllowProcessLevelFinanceCodeBlockOverride)	

		PreventProcessLevelFinanceCodeBlockOverride
			restricted
			when (AccountingUnitOverrideFlag.PreventProcessLevelFinanceCodeBlockOverride)	

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
