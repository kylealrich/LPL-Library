DebtReportGroup is a BusinessClass
	owned by treasury
	prefix is DMDRG	

	Ontology
		symbolic key is DebtReportGroup
		
	Persistent Fields								
		Description		  		 is Alpha up to 250
		Active

	Local Fields
		NewParentDebtReportGroup is a DebtReportGroup
	
	Derived Fields
		NewParentDebtReportGroupIsAChildDebtReportGroup is a DerivedField
			type is Boolean
			restricted
   			for each NewParentDebtReportGroup and ancestors
				if (each.ParentDebtReportGroup = DebtReportGroup)
					return true
					end for each
						
	Field Rules
		Description
			default to DebtReportGroup
		
		Active
			initial value is true
	
	Conditions
		HasParentDebtReportGroup
			restricted
    		when (ParentDebtReportGroup entered)
    		
    	HasChild
    		restricted
    		when (DebtReportGroup children exist)
    	
    	HasReportGroupRating
    		restricted
    		when (DebtReportGroupRating set exists)
    
    Relations
    	DebtReportGroupLatestRatingRel is a DebtReportGroupRating set
			Instance Selection
				where (related.IsLatestVersion)
							
  	Attach Rules
		constraint (Active)
			"ReportGroupIsInactive"
	
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		ReparentReportGroup is an Instance Action
			Parameters
				PrmParentDebtReportGroup is a DebtReportGroup
				
			Parameter Rules
				PrmParentDebtReportGroup
					required
						"NewReportGroupIsRequired"
						
					constraint (PrmParentDebtReportGroup != DebtReportGroup)
						"CannotSelectReportGroupToBeItsOwnParent"
						
					constraint (PrmParentDebtReportGroup != ParentDebtReportGroup)
						"NewParentIsTheSameAsCurrentParent"
						
			Action Rules
				NewParentDebtReportGroup = PrmParentDebtReportGroup
				
				constraint (!NewParentDebtReportGroupIsAChildDebtReportGroup)
					"CannotChooseAChildReportGroupToBeTheNewReportGroup"
					
				ParentDebtReportGroup = NewParentDebtReportGroup
				
		RemoveParentReportGroup is an Instance Action
			valid when (HasParentDebtReportGroup)
			Action Rules
				initialize ParentDebtReportGroup
					
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted	
