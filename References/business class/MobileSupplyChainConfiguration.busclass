MobileSupplyChainConfiguration is a BusinessClass
    default label is "MobileSupplyChain"
    owned by mscm
    prefix is MSCON
    
    Ontology
        symbolic key is MobileSupplyChainConfiguration
    
    Patterns
    
    Persistent Fields
        MobileSupplyChainEnabled                    is Boolean


        BlindCounting                               is Boolean
        ValidateCountAgainstFreezeQuantity          is Boolean


        DisplayFirstParItemDetail                   is Boolean
        ItemDisplayOrder                            is Numeric 1
            States
                SortByBinSequence           value is 0
	            SortByShelf                 value is 1
	   	DisplayParUtilizationLevel			        is Boolean
	   		default label is "DisplayAverageParUtilizationAndSuggestedParLevel"
        DisplayQuantityOnOrder      		        is Boolean
        AllowEditParLevel           		        is Boolean
        PrintParItemLabel					        is Boolean


        LastKanbanBinID                             is Numeric 10
            disable Auditing


        CarrierTrackingNumberOptions                        is a MobileSupplyChainDisplayOptions
        CarrierOptions                                      is a MobileSupplyChainDisplayOptions
        ReferenceNumberOptions                              is a MobileSupplyChainDisplayOptions
        BillOfLadingOptions                                 is a MobileSupplyChainDisplayOptions
        DisplayTrackingNumber                               is Boolean
        PrintDeliveryLabel                                  is Boolean
        PrintDeliveryTicket                                 is Boolean
        DisplayAddReceiptButton                             is Boolean
        CheckCatchWeightTolerance                           is Boolean


        DockLoggingAutoPrint                                is Boolean
        

        MiscellaneousReceivingCarrierOptions                is a MobileSupplyChainDisplayOptions
        MiscellaneousReceivingVendorOptions                 is a MobileSupplyChainDisplayOptions
        MiscellaneousReceivingPONumberOptions               is a MobileSupplyChainDisplayOptions
        MiscellaneousReceivingPackingSlipNumberOptions      is a MobileSupplyChainDisplayOptions
        MiscellaneousDisplayReceivingRejectDelivery         is Boolean
        MiscellaneousReceivingPrintDeliveryLabel            is Alpha 1
            States
                Enable                      value is blank
                EnabledAndChecked           value is "C"
                Disable                     value is "D"
        MiscellaneousReceivingPrintDeliveryTicket     is Alpha 1
            States
                Enable                      value is blank
                EnabledAndChecked           value is "C"
                Disable                     value is "D"
                

        ItemNumberValidation                        is Boolean
        TrackDeliveryOption                         is Alpha 1
            States
                Container                   value is "C"
                Shipment                    value is "S"
                ShipmentLine                value is "L"
        PrintPickingOptions                         is a MobileSupplyChainPrintingOptions             
        DownloadedShipmentOrder                       is AlphaUpper size 1
            States
                FIFO                        value is "F"
                LIFO                        value is "L"
        PrintPickingClusterOptions                  is a MobileSupplyChainPrintingOptions
        

        TrackingNumberDate                          is Date
            protected
            disable Auditing
        TrackingNumberSequence                      is like LastSequence
            protected
            disable Auditing
        

        PrintInventoryIssueTicket                   is Boolean
        DisplayIssueToFirstName                     is a MobileSupplyChainDisplayOptions
        DisplayIssueToLastName                      is a MobileSupplyChainDisplayOptions
        PrintInventoryTransferTicket                is Boolean
        PrintInventoryAdjustmentTicket              is Boolean   
        PrintInventoryReceivingTransferTicket       is Boolean


        DisplayInventoryReplenishmentFirstItemDefault   is Boolean
        DisplayInventoryReplenishmentItemDisplayOrder   is Numeric 1
            States
                SortByBin                   value is 0
	            SortByItem                  value is 1
        DisplayInventoryReplenishmentQOO                is Boolean
        DisplayInventoryReplenishmentMinimumOrder       is Boolean


        DeliveryInventoryItem                       is Boolean
        DeliveryAttemptedFunction                   is Boolean
        DeliveryReleaseFunction                     is Boolean
        DeliveryAttemptedTransferFunction           is Boolean
        DeliveryReleaseTransferFunction             is Boolean
        DeliveryConfirmTransferFunction             is Boolean
        DeliveryHandheldProcess                     is Boolean
        DeliveryHandheldEmployeeID                  is a MobileSupplyChainDisplayOptions
        DeliveryHandheldFirstName                   is a MobileSupplyChainDisplayOptions
        DeliveryHandheldLastName                    is a MobileSupplyChainDisplayOptions
        DeliverySignatureCapture                    is a MobileSupplyChainDisplayOptions
        DeliveryReleasedImageCapture                is a MobileSupplyChainDisplayOptions
        DeliveryConfirmedImageCapture               is a MobileSupplyChainDisplayOptions
    
    Local Fields
        LocalLocation                       is an InventoryLocation
        LocalCompany                        is an InventoryCompany
        LocalLocationType                   is AlphaUpper size 1
        LocalMonth                          is Numeric 2
        LocalDate                           is Numeric 2
        LocalZeroStringLength               is Numeric 2

    Derived Fields
        RequesterBaseURL is a DerivedField
            type is Alpha 300
            restricted
            return base url(webapp is Requester)


        AndroidShellInstallerURL is a DerivedField
            type is URL
            return RequesterBaseURL + "/Requester/mscminstallers/AndroidShell.apk"

        MPCInstallerURL is a DerivedField
            type is URL
            return RequesterBaseURL + "/Requester/mscminstallers/MPC.apk"

        RADInstallerURL is a DerivedField
            type is URL
            return RequesterBaseURL + "/Requester/mscminstallers/RAD.apk"

        POUInstallerURL is a DerivedField
            type is URL
            return RequesterBaseURL + "/Requester/mscminstallers/POU.apk"
        
        PrinterAgentURL is a DerivedField
            type is URL
            return RequesterBaseURL + "/Requester/mscminstallers/printagent.zip"

        CLFAddinURL is a DerivedField
            type is URL
            return RequesterBaseURL + "/Requester/mscminstallers/LmrkMSCMLabelFormatAddInSetup.zip"

        DerivedTrackingNumber is a StringField
            type is like TrackingNumber
            "IT"
            DerivedTrackingYearTwoDigit
            DerivedTrackingMonth
            DerivedTrackingDay 
            DerivedZeroSequenceNumber
        
        DerivedZeroString   is a StringField
            type is AlphaUpper 20
            restricted
            "00000"

        DerivedTrackingNumberSequnce            is a DerivedField
            type is like TrackingNumber
            restricted
            return TrackingNumberSequence


        DerivedZeroSequenceNumber               is a DerivedField
            type is like TrackingNumber
            restricted
            LocalZeroStringLength = (DerivedZeroString size - DerivedTrackingNumberSequnce size)
            if (LocalZeroStringLength > 0)
                return DerivedZeroString[1:LocalZeroStringLength] + DerivedTrackingNumberSequnce
        
        DerivedTrackingMonth                    is a DerivedField
            type is like TrackingNumber
            restricted
            LocalMonth              = TrackingNumberDate month
            if (LocalMonth <= 9)
                return "0" + LocalMonth
            else
                return LocalMonth
        
        DerivedTrackingDay                      is a DerivedField
            type is like TrackingNumber
            restricted
            LocalDate               = TrackingNumberDate day
            if (LocalDate <= 9)
                return "0" + LocalDate
            else
                return LocalDate
        
        DerivedTrackingYear                     is a DerivedField
            type is Alpha 4
            restricted  
            return TrackingNumberDate year
        
        DerivedTrackingYearTwoDigit             is a DerivedField
            type is like TrackingNumber
            restricted 
            return DerivedTrackingYear[3:4]

    Field Rules          
        ValidateCountAgainstFreezeQuantity
            if (not BlindCounting)
                force default to false
        CarrierTrackingNumberOptions
			initial value is CarrierTrackingNumberOptions.DisplayOnly
			default to CarrierTrackingNumberOptions.DisplayOnly
        CarrierOptions
			initial value is CarrierOptions.DisplayOnly
			default to CarrierOptions.DisplayOnly
        ReferenceNumberOptions
			initial value is ReferenceNumberOptions.DisplayOnly
			default to ReferenceNumberOptions.DisplayOnly
        BillOfLadingOptions
			initial value is BillOfLadingOptions.DisplayOnly
			default to BillOfLadingOptions.DisplayOnly
        DeliveryHandheldEmployeeID
            initial value is DeliveryHandheldEmployeeID.DisplayOnly
            default to DeliveryHandheldEmployeeID.DisplayOnly
        DeliveryHandheldFirstName
            initial value is DeliveryHandheldFirstName.DisplayOnly
            default to DeliveryHandheldFirstName.DisplayOnly
        DeliveryHandheldLastName
            initial value is DeliveryHandheldLastName.DisplayOnly
            default to DeliveryHandheldLastName.DisplayOnly
        DeliverySignatureCapture
            initial value is DeliverySignatureCapture.DisplayOnly
            default to DeliverySignatureCapture.DisplayOnly
        DeliveryReleasedImageCapture
            initial value is DeliveryReleasedImageCapture.Hide
            default to DeliveryReleasedImageCapture.Hide
        DeliveryConfirmedImageCapture
            initial value is DeliveryConfirmedImageCapture.Hide
            default to DeliveryConfirmedImageCapture.Hide
        DeliveryAttemptedFunction
            initial value is true
        DeliveryReleaseFunction
            initial value is true
        DeliveryAttemptedTransferFunction
            initial value is true
        DeliveryReleaseTransferFunction
            initial value is true
            if (DeliveryConfirmTransferFunction not entered)
                required
                    "EitherReleaseTransferFunctionOrConfirmTransferFunctionOrBothMustBeEnabled."
        DeliveryConfirmTransferFunction
            initial value is true
        DeliveryHandheldProcess
            initial value is true
    Conditions
        TransactionNotExists
            when(DockLogRel not exists 
            and MiscellaneousReceiptRel not exists
            and ParCountRel not exists
            and KanbanCountRel not exists
            and CycleCountRel not exists)
    Relations
        InventoryCompanyRel
            one-to-many relation to InventoryCompany
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)
        InventoryLocationRel
            one-to-many relation to InventoryLocation
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)
        
        RequestingLocationRel
            one-to-many relation to RequestingLocation
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)

        MobileSupplyChainLocationRel
            one-to-one relation to MobileSupplyChainLocation
            Field Mapping uses symbolic key
                related.Company                                 = LocalCompany
                related.MobileSupplyChainLocation               = LocalLocation
        
        MobileSupplyChainCompanyRel
            one-to-one relation to MobileSupplyChainCompany
            Field Mapping uses symbolic key
                related.Company                                 = LocalCompany

        MobileSupplyChainCompaniesRel
            one-to-many relation to MobileSupplyChainCompany
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)
        
        DeliveryNoteCodeRel
            one-to-many relation to DeliveryNoteCode
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup                  = MobileSupplyChainConfiguration
        
        DockLogRel
            one-to-many relation to DockLog
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)
        
        MiscellaneousReceiptRel
            one-to-many relation to MiscellaneousReceipt
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)
        
        ParCountRel
            one-to-many relation to MobileSupplyChainParCount
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)
        KanbanCountRel
            one-to-many relation to KanbanCount
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)

        CycleCountRel
            one-to-many relation to CycleCountUpload
            Field Mapping uses symbolic key
            Instance Selection
                where (related.Company.FinanceEnterpriseGroup = MobileSupplyChainConfiguration)

    Rule Blocks
        CreateMobileSupplyChainLocation
            if(MobileSupplyChainLocationRel not exists)
                invoke Create MobileSupplyChainLocation
                    invoked.MobileSupplyChainLocation       = LocalLocation
                    invoked.Company                         = LocalCompany
                    invoked.LocationType                    = LocalLocationType
	
    Actions
        Create is a Create Action 
            valid when (actor.context.FinanceEnterpriseGroup.ShowMobileSupplyChain)
            Action Rules
                for each InventoryCompanyRel
                    LocalCompany    = each.Company

                    if(MobileSupplyChainCompanyRel not exists)
                        invoke Create MobileSupplyChainCompany
                            invoked.Company                         = LocalCompany

                for each InventoryLocationRel
                    LocalLocation       = each.InventoryLocation
                    LocalCompany        = each.Company
                    if(each.LocationType.InventoryLocation)
                        LocalLocationType   = MobileSupplyChainLocation.LocationType.InventoryLocation
                    else
                        LocalLocationType   = MobileSupplyChainLocation.LocationType.ParAndCartLocation

                    include CreateMobileSupplyChainLocation
                    
                for each RequestingLocationRel
                    LocalLocation   = each.RequestingLocation
                    LocalCompany    = each.Company

                    LocalLocationType   = MobileSupplyChainLocation.LocationType.RequestingLocation

                    include CreateMobileSupplyChainLocation		
                
                invoke CreateMSCMPreDefinedLabelTemplate DataCreation
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration

                invoke CreateMSCMCarrierTrackingIdentifierAndFormat DataCreation
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration
        Update is an Update Action
            valid when (actor.context.FinanceEnterpriseGroup.ShowMobileSupplyChain)
                
        Delete is a Delete Action
            Entrance Rules
                constraint (TransactionNotExists)
                    "MobileSupplyChainTransactionsAlreadyExisting"

                if (MobileSupplyChainCompanyRel exists and MobileSupplyChainLocationRel exists)
                    confirmation required
                        "Warning:ThisWillPermanentlyDeleteTheSelectedRecordAndItsAssociatedConfigurationRecords.DoYouWantToContinue?"
                else
                    confirmation required
                        "Warning:ThisWillPermanentlyDeleteTheSelectedRecord.DoYouWantToContinue?"
                
                invoke DeleteData MobileSupplyChainResourceAccess
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration

                invoke DeleteData MobileSupplyChainResourceAccessPrinter
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration    
     
                invoke DeleteData MobileSupplyChainResourceParLocation
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration

                invoke DeleteData MobileSupplyChainCompany
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration
                
                invoke DeleteData MobileSupplyChainLocation
                    invoked.PrmFinanceEnterpriseGroup   = MobileSupplyChainConfiguration
        
        GenerateTrackingNumber is an Instance Action
            restricted
            refresh and lock this instance
            Action Rules
                if (TrackingNumberDate = current corporate date)
                    increment TrackingNumberSequence      
                else
                    TrackingNumberDate = current corporate date
                    TrackingNumberSequence = 1     

        MarkAllPurchasingVendorAsMiscReceivingVendor is an Instance Action
            valid when (MobileSupplyChainEnabled)
            Action Rules
                invoke CreateMobileSupplyChainVendor PurchasingVendor
                    invoked.PrmFinanceEnterpriseGroup  = MobileSupplyChainConfiguration

        SetDefaultDeliveryDisplayOptions is a Set Action
            restricted

            Sort Order is primary

            Action Rules
                Instance Rules
                    if (DeliveryHandheldEmployeeID not entered)
                        DeliveryHandheldEmployeeID        = DeliveryHandheldEmployeeID.DisplayOnly

                    if (DeliveryHandheldFirstName not entered)
                        DeliveryHandheldFirstName         = DeliveryHandheldFirstName.DisplayOnly

                    if (DeliveryHandheldLastName not entered)
                        DeliveryHandheldLastName          = DeliveryHandheldLastName.DisplayOnly

                    if (DeliverySignatureCapture not entered)
                        DeliverySignatureCapture          = DeliverySignatureCapture.DisplayOnly

                    if (DeliveryReleasedImageCapture not entered)
                        DeliveryReleasedImageCapture      = DeliveryReleasedImageCapture.Hide
                    
                    if (DeliveryConfirmedImageCapture not entered)
                        DeliveryConfirmedImageCapture     = DeliveryConfirmedImageCapture.Hide

                    if (not DeliveryAttemptedFunction)
                        DeliveryAttemptedFunction = true
                    
                    if (not DeliveryReleaseFunction)
                        DeliveryReleaseFunction = true

                    if (not DeliveryAttemptedTransferFunction)
                        DeliveryAttemptedTransferFunction = true
        
                    if (not DeliveryReleaseTransferFunction)
                        DeliveryReleaseTransferFunction = true
                    
                    if (not DeliveryConfirmTransferFunction)
                        DeliveryConfirmTransferFunction = true

                    if (not DeliveryHandheldProcess)
                        DeliveryHandheldProcess = true
