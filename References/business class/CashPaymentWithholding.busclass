CashPaymentWithholding is a BusinessClass
	owned by cb
	
	prefix is CPWT
	
	Ontology
		symbolic key is CashPaymentWithholding
		
    Patterns
        implements DynamicCreation
        implements StaticJava
        disable AuditIndex
        disable Auditing

	Persistent Fields
		PayGroup
		CashRequirementsResult
		Vendor
		Accumulated					is Boolean
		WithholdingScale
		ExemptAmount				is an InternationalAmount			
		PeriodPaidAmount			is an InternationalAmount
		PeriodWithheldAmount		is an InternationalAmount
		CurrentPaidAmount			is an InternationalAmount
		CurrentWithholdingAmount	is an InternationalAmount
		
	Transient Fields
		TransientPeriodPaidAmount			is an InternationalAmount
		TransientPeriodWithheldAmount		is an InternationalAmount
		PaymentDate							is Date
			derive value from CashLedgerPayablesPaymentRel.PaymentDate
		PaymentNumber						is a TransactionNumber
			derive value from CashLedgerPayablesPaymentRel.TransactionNumber

	Local Fields
		IDMXMLDefinition
		AttributeCtr				is Numeric 2
		LocalExecute				is Boolean	
					
	Derived Fields
		BasisForWithholding is a DerivedField
			type is like InternationalAmount
			return (PeriodPaidAmount + CurrentPaidAmount - ExemptAmount)
			
		TotalWithholdingAmount is a DerivedField
			type is like InternationalAmount
			return (PeriodWithheldAmount + CurrentWithholdingAmount)
			

		DerivedPayablesCompany is a DerivedField
			type is like Company
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany
			
		DerivedCompanyName is a DerivedField 
			type is like Name
			holds pii
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.Name
			
		DerivedCompanyAddressLine1	is a DerivedField
			type is like AddressLine
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.DeliveryAddress.AddressLine1
			
		DerivedCompanyAddressLine2	is a DerivedField
			type is like AddressLine
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.DeliveryAddress.AddressLine2
			
		DerivedCompanyAddressLine3	is a DerivedField
			type is like AddressLine
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.DeliveryAddress.AddressLine3
			
		DerivedCompanyAddressLine4	is a DerivedField
			type is like AddressLine
			return CashLedgerPayablesPaymentRel.PayablesCompanyRel.Company.AccountingEntity.AddressCode.PostalAddress.DeliveryAddress.AddressLine4
			
		DerivedCompanyAddressMunicipality	is a DerivedField
			type is like MunicipalityLarge
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.Municipality
			
		DerivedCompanyAddressState	is a DerivedField
			type is like StateProvince
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.StateProvince
			
		DerivedCompanyAddressPostal	is a DerivedField
			type is like PostalCode
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.PostalCode
			
		DerivedCompanyAddressCountry	is a DerivedField
			type is like Country
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.AccountingEntity.AddressCode.PostalAddress.Country
			
		DerivedCompanyTaxRegistrationNumber is a DerivedField 
			type is like VATRegistrationNumber
			holds pii
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.DerivedVATRegistrationNumber
			
		DerivedCompanyTaxID				is a DerivedField 
			type is like TaxID
			holds pii
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.DerivedTaxID
			
		DerivedIDMCurrentDate			is a DerivedField
			type is Date
			return current corporate date
			
		DerivedCompanyCurrency is a DerivedField
			type is like Currency
			return CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.Currency
			
		DerivedRetentionLinestotal is a DerivedField
			type is like InternationalAmount
			return sum PayablesInvoiceWithholdingRel.WithholdingAmount
				
		DerivedLetterOfRetentionFileName	is a StringField
			type is Alpha 100
			"Letter of Retention - "
			DerivedPayablesCompany
			"-"
			Vendor
			"-"
			IncomeWithholdingCode
			"-"
			DerivedIDMCurrentDate
		
	Field Rules
		Accumulated
			default to IncomeWithholdingCode.Accumulated
			
		TransientPeriodPaidAmount
			if (action type.Create)
				PeriodPaidAmount = TransientPeriodPaidAmount
			else
				if (TransientPeriodPaidAmount	< PeriodPaidAmount)
					PeriodPaidAmount = TransientPeriodPaidAmount
					
		TransientPeriodWithheldAmount
			if (action type.Create)
				PeriodWithheldAmount = TransientPeriodWithheldAmount
			else
				if (TransientPeriodWithheldAmount	< PeriodWithheldAmount)
					PeriodWithheldAmount = TransientPeriodWithheldAmount

	Conditions
		HasTotalWithholdingAmount
			restricted
			when (TotalWithholdingAmount > 0)
		
		IncomeWithholdingCodeIsNumeric
			when (IncomeWithholdingCode is numeric)
					
	Relations

		IDMAdditionalAttributesLinesRel
			one-to-many relation to IDMAdditionalAttributesLines
			Field Mapping uses symbolic key
				related.IDMAdditionalAttributesHeader = "FSM_LetterOfRetention"
			Instance Selection
				where(related.IDMAdditionalAttributesHeader.ActivateAdditionalAttributes
				and	  related.ActivateAdditionalAttributes.Active)

		PayablesInvoiceWithholdingRel
			one-to-many relation to PayablesInvoiceWithholding
			Field Mapping uses ByPaymentWithholdingCode
				related.CashCode                = CashCode
				related.BankTransactionCode		= BankTransactionCode
				related.TransactionIDNumber		= TransactionIDNumber
				related.IncomeWithholdingCode   = IncomeWithholdingCode
				related.WithholdingCategory		= WithholdingCategory

		CashLedgerPayablesPaymentRel
			one-to-one relation to CashLedgerPayablesPayment
			Field Mapping uses symbolic key
				related.CashManagementGroup 						  = CashCode.CashManagementGroup
				related.CashCode                        			  = CashCode
				related.CashLedgerSourceRecord						  = "CHK"
				related.CashLedgerPayablesPayment.BankTransactionCode = BankTransactionCode
				related.CashLedgerPayablesPayment.TransactionIDNumber = TransactionIDNumber


	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
		
		Delete is a Purge Action
			restricted			
			
				
		GenerateLetterOfRetentionInIDM is an Instance Action
			default label is "GenerateLetterOfRetention"
			valid when (HasTotalWithholdingAmount)
			Local Fields
				IDMAttributes
				IDMGenerateDocument
				  			
			Action Rules
			
				IDMGenerateDocument.IDMXMLDefinition.Busclass											= reference to this instance
				IDMGenerateDocument.IDMXMLDefinition.ListName											= "LetterOfRetentionIDMList"
				IDMGenerateDocument.IDMXMLDefinition.DocumentName										= "LetterOfRetention"
				
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName			= "PayablesInvoiceWithholdingRel"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName 			= "PayablesInvoiceWithholdingForIDMList"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1		= 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag				= "WithholdingLines"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag				= "WithholdingLine"
			
				initialize IDMAttributes
 				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName  	= "Company"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue  	= DerivedPayablesCompany
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName  	= "CompanyName"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue  	= DerivedCompanyName
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName  	= "VendorGroup"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue  	= Vendor.VendorGroup
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName  	= "Vendor"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue  	= Vendor
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName  	= "VendorName"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue  	= Vendor.VendorName
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName  	= "PayGroup"
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue  	= PayGroup
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeName  	= "PaymentDate"
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeDate  	= PaymentDate

				if (IDMAdditionalAttributesLinesRel exists)
					AttributeCtr = 8
					include IDM.IDMAdditionalAttributes
				
				IDMGenerateDocument.IDMAttributes		= IDMAttributes
				IDMGenerateDocument.TemplateUniqueId	= CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.LetterOfRetentionTemplate.IDMUniqueId
				IDMGenerateDocument.DocumentType		= "FSM_LetterOfRetention"
				IDMGenerateDocument.FileName			= DerivedLetterOfRetentionFileName + CashLedgerPayablesPaymentRel.PayablesCompanyProcessLevel.PayablesCompany.LetterOfRetentionTemplate.DerivedOutputFormat
				
				IDMGenerateDocument.IDMAccessControlList = "CSFDefined"
				
				invoke CreateFromGenerateDocument IDMJob
					invoked.Actor				= actor
					invoked.Description			= "Letter of Retention"
					invoked.IDMGenerateDocument	= IDMGenerateDocument
					
					
