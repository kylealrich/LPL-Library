SourcingEventTermAndCondition is a BusinessClass 
    owned by ss
    prefix is ETM

    Ontology
    	symbolic key is SourcingEventTermAndCondition
    	    			    			    			
    Patterns
        implements CompoundDocument 
        	Document Components
				SourcingEventTermAndConditionAttachment set
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

	Persistent Fields
	   	Title						is a Description
   		Description					is Text
   			sql name is EVTSRCHDOC
        	Text Variables
 				EventBuyer				value is SourcingEvent.Buyer.Name
 				EventBuyerPhoneNumber	value is SourcingEvent.Buyer.EmployeeWorkTelephone
 				EventBuyerEmailAddress	value is SourcingEvent.Buyer.EmployeeWorkEmailAddress
 				EventCloseDate			value is SourcingEvent.CloseDate
        		EventCommodities		value is Commodities
 				EventDescription		value is SourcingEvent.Description
	       		EventName				value is SourcingEvent.Name 
 				EventNumber				value is SourcingEvent    
         		EventOpenDate			value is SourcingEvent.OpenDate
         		EventType				value is SourcingEvent.SourcingEventType.Description
         		EventReference          value is SourcingEvent.EventReference
         		TAndCAttachmentReference value is SourcingEventTermAndConditionAttachment.AttachmentReference
         		
   		DisplayOrder
   		AllowModification			is Boolean
	    TermAndConditionNumber		is a TermAndCondition
	    CMTerm						is Boolean
	    RepositoryModified			is Boolean
		Article
        HeaderText     				is Alpha size 100
	    LastAttachmentDisplayOrder	is Numeric size 4
			disable Auditing
		LastChildTermAndConditionDisplayOrder is Numeric size 4
			disable Auditing
		TermLevel                   is Numeric size 1
		DisplayString               is Alpha size 8   
		DisplayString12             is Alpha size 12
			
	Transient Fields
		NewDisplayOrder				is a DisplayOrder
		Attachment            
		
	Local Fields
		LocalTermEvent				is Numeric size 8
		LocalTermAndCond			is Numeric size 6
		LocalArticle                is Numeric size 8
		LocalDisplayOrder			is a DisplayOrder
   		FirstLoop 					is Boolean
   		FromRepository              is Boolean
		LocalParentTermAndCondition is a TermAndCondition
		LocalAlphaTermDisplay       is Alpha size 2
    	LocalFullAlphaTermDisplay   is Alpha size 8
    	LocalTopLevelTermDisplay    is Alpha size 2
    	LocalSecondLevelTermDisplay is Alpha size 2
    	LocalThirdLevelTermDisplay  is Alpha size 2
    	LocalFourthLevelTermDisplay is Alpha size 2
    	LocalAlphaTermDisplay3          is Alpha size 3
    	LocalFullAlphaTermDisplay12     is Alpha size 12
    	LocalTopLevelTermDisplay3       is Alpha size 3
    	LocalSecondLevelTermDisplay3    is Alpha size 3
    	LocalThirdLevelTermDisplay3     is Alpha size 3
    	LocalFourthLevelTermDisplay3    is Alpha size 3
		LocalUserTemplate			is like UserTemplate
    	
	Derived Fields
		Commodities is a DerivedField
			type is Text
			restricted
			FirstLoop = true
			for each SourcingEvent.SourcingEventCommodities set
				if (FirstLoop)
					FirstLoop = false
				else
					Commodities += ", "
				Commodities += each.CommodityCode.Description
				
		MissingTemplateMsg is a MessageField
			"TheTemplateCouldNotBeFound"
			
		SourcingEventTermsAndConditionsAttachments is a DerivedField
			type is XMLDocument
			default label is untranslatable
			restricted
			for each SourcingEventTermAndConditionAttachment set
				SourcingEventTermsAndConditionsAttachments += each.AttachmentTemplate
			
		TermsTemplate is a DerivedField
			type is XMLDocument
			default label is untranslatable
			restricted
			LocalUserTemplate = "SourcingEventTermsAndConditionsTerms_ST"
			if (UserTemplateRel exists)
				TermsTemplate = template.SourcingEventTermsAndConditionsTerms_ST document for this instance
			else
				TermsTemplate = MissingTemplateMsg 
			return TermsTemplate
				
	Field Groups
		VerifiedFields
			Title 
			Description
			HeaderText 
			RepositoryModified
			Article
        	HeaderText
		    
	Field Rules
		Title
			required						
   		Description
   			required
		AllowModification
			cannot be changed
		CMTerm
			if (CMTerm)
				constraint (SourcingEventArticle.CMArticle)
					"CannotSendTermToContractManagement;AssociatedArticleIsNotBeingSent"
		Article
			default to SourcingEventArticle.Article

	Conditions
		AttachmentExists
			when (SourcingEventTermAndConditionAttachment set exists)
		EligibleForCM
			restricted
			when (SourcingEvent.ContractOutputExists
			or    SourcingEvent.ContractOutput)
		TermAndConditionExists
			restricted
			when (TermAndConditionNumber > 0)
		TermAndConditionIsActive
			restricted
			when (TermAndConditionNumber.Active
			or	  !TermAndConditionExists)
		ParentTermAndConditionExists
			restricted
			when (ParentTermAndCondition entered)
		HasChildren
			restricted
			when (ChildTermAndConditionRel exists)
		LevelFour
			restricted
			when (TermLevel = 4)
		NotLevelFour
			restricted
			when (TermLevel !=4)

	Sets
		ByTermAndCondition
			duplicates
			Sort Order
				Company
				SourcingEvent
				TermAndConditionNumber
				Article
	
		ByDisplayOrder
			duplicates
			Sort Order
 				Company
 				SourcingEvent
				SourcingEventArticle
 				DisplayOrder
 				SourcingEventTermAndCondition
 				
 		ByDisplayString
        	Sort Order
        		Company
        		SourcingEvent
        		SourcingEventArticle
        		DisplayString12
        		SourcingEventTermAndCondition
 		
 		
	Relations
		SourcingEventTermRel
			one-to-one relation to SourcingEventTermAndCondition
			Field Mapping uses symbolic key
				related.Company		    			  = Company
				related.SourcingEvent				  = LocalTermEvent
				related.SourcingEventArticle 		  = LocalArticle
				related.SourcingEventTermAndCondition = LocalTermAndCond

   		AnotherTermForArticleRel
   			one-to-many relation to SourcingEventTermAndCondition
   			Field Mapping uses symbolic key
				related.Company		    	 = Company
				related.SourcingEvent		 = SourcingEvent
				related.SourcingEventArticle = SourcingEventArticle

		SETermsAttachmentByDisplayOrderRel	
			one-to-many relation to SourcingEventTermAndConditionAttachment
			delete cascades
			Field Mapping uses ByDisplayOrder 
				related.Company		    			  = Company
				related.SourcingEvent				  = SourcingEvent
				related.SourcingEventArticle 		  = SourcingEventArticle
				related.SourcingEventTermAndCondition = SourcingEventTermAndCondition
				
		ParentTermAndConditionRel
			one-to-one relation to SourcingEventTermAndCondition
			Field Mapping uses symbolic key
				related.Company		    			  = Company
				related.SourcingEvent				  = SourcingEvent
				related.SourcingEventArticle 		  = SourcingEventArticle
				related.SourcingEventTermAndCondition = ParentTermAndCondition
   		
   		ChildTermAndConditionRel
			one-to-many relation to SourcingEventTermAndCondition
			Field Mapping uses symbolic key
				related.Company		    			  = Company
				related.SourcingEvent				  = SourcingEvent
				related.SourcingEventArticle 		  = SourcingEventArticle
			Instance Selection
				where (related.ParentTermAndCondition = SourcingEventTermAndCondition)
		
		DescendantTermAndConditionRel
			one-to-many relation to SourcingEventTermAndCondition
			Field Mapping uses symbolic key
				related.Company		    			  = Company
				related.SourcingEvent				  = SourcingEvent
				related.SourcingEventArticle 		  = SourcingEventArticle
				related.SourcingEventTermAndCondition = SourcingEventTermAndCondition descendants.SourcingEventTermAndCondition
		
		LocalParentTermAndConditionRel
   			one-to-many relation to SourcingEventTermAndCondition
   			Field Mapping uses symbolic key
   				related.Company		    			  = Company
				related.SourcingEvent				  = SourcingEvent
				related.SourcingEventArticle 		  = SourcingEventArticle
			Instance Selection
				where (related.TermAndConditionNumber = LocalParentTermAndCondition)

		UserTemplateRel
			one-to-one relation to UserTemplate
			Field Mapping uses symbolic key
		 		related.UserTemplate = LocalUserTemplate

	Context Fields
   		SourcingEventTermAndConditionAttachment
	
	Rule Blocks
		
		MaintainDisplayString12
			initialize LocalAlphaTermDisplay3
			initialize LocalFullAlphaTermDisplay12
			initialize LocalTopLevelTermDisplay3
			initialize LocalSecondLevelTermDisplay3
			initialize LocalThirdLevelTermDisplay3
			initialize LocalFourthLevelTermDisplay3
			if (TermLevel = 1)
				LocalAlphaTermDisplay3 = DisplayOrder
				if (LocalAlphaTermDisplay3[3] !entered
				and LocalAlphaTermDisplay3[2] !entered)
					LocalTopLevelTermDisplay3 = "00" + DisplayOrder
				else	
				if (LocalAlphaTermDisplay3[3] !entered
				and LocalAlphaTermDisplay3[2] entered)
					LocalTopLevelTermDisplay3 = "0" + DisplayOrder
				else 
					LocalTopLevelTermDisplay3 = DisplayOrder
				LocalSecondLevelTermDisplay3 = "000"
				LocalThirdLevelTermDisplay3  = "000"
				LocalFourthLevelTermDisplay3 = "000"
			if (TermLevel = 2)
				LocalFullAlphaTermDisplay12 	= ParentTermAndConditionRel.DisplayString12
				LocalTopLevelTermDisplay3 	    = LocalFullAlphaTermDisplay12[1] + LocalFullAlphaTermDisplay12[2] + LocalFullAlphaTermDisplay12[3]
				LocalAlphaTermDisplay3   		= DisplayOrder
				if (LocalAlphaTermDisplay3[2] !entered
				and LocalAlphaTermDisplay3[3] !entered) 
					LocalSecondLevelTermDisplay3 = "00" + DisplayOrder
				else
				if (LocalAlphaTermDisplay3[3] !entered
				and LocalAlphaTermDisplay3[2]  entered) 
					LocalSecondLevelTermDisplay3 = "0" + DisplayOrder 
				else 
					LocalSecondLevelTermDisplay3 = DisplayOrder
				LocalThirdLevelTermDisplay3  = "000"
				LocalFourthLevelTermDisplay3 = "000"	
			if (TermLevel = 3)
				LocalFullAlphaTermDisplay12 	= ParentTermAndConditionRel.DisplayString12
				LocalTopLevelTermDisplay3 	    = LocalFullAlphaTermDisplay12[1] + LocalFullAlphaTermDisplay12[2]+ LocalFullAlphaTermDisplay12[3]
				LocalSecondLevelTermDisplay3    = LocalFullAlphaTermDisplay12[4] + LocalFullAlphaTermDisplay12[5]+ LocalFullAlphaTermDisplay12[6]
				LocalAlphaTermDisplay3 		    = DisplayOrder
				if (LocalAlphaTermDisplay3[2] !entered
				and LocalAlphaTermDisplay3[3] !entered) 
					LocalThirdLevelTermDisplay3 = "00" + DisplayOrder
				else 
				if (LocalAlphaTermDisplay3[2]  entered
				and LocalAlphaTermDisplay3[3] !entered)
					LocalThirdLevelTermDisplay3 = "0" = DisplayOrder 
				else 
					LocalThirdLevelTermDisplay3 = DisplayOrder
				LocalFourthLevelTermDisplay3 = "000"
			if (TermLevel = 4)
				LocalFullAlphaTermDisplay12 	= ParentTermAndConditionRel.DisplayString12
				LocalTopLevelTermDisplay3       = LocalFullAlphaTermDisplay12[1] + LocalFullAlphaTermDisplay12[2]+ LocalFullAlphaTermDisplay12[3]
				LocalSecondLevelTermDisplay3    = LocalFullAlphaTermDisplay12[4] + LocalFullAlphaTermDisplay12[5]+ LocalFullAlphaTermDisplay12[6]
				LocalThirdLevelTermDisplay3     = LocalFullAlphaTermDisplay12[7] + LocalFullAlphaTermDisplay12[8]+ LocalFullAlphaTermDisplay12[9]
				LocalAlphaTermDisplay3 		    = DisplayOrder
				if  (LocalAlphaTermDisplay3[2] !entered
				and  LocalAlphaTermDisplay3[3] !entered) 
					LocalFourthLevelTermDisplay3 = "00" + DisplayOrder
				else
				if (LocalAlphaTermDisplay3[2] entered
				and LocalAlphaTermDisplay3[3] !entered) 
					LocalFourthLevelTermDisplay3 = "0" + DisplayOrder
				else 
					LocalFourthLevelTermDisplay3 = DisplayOrder	
			DisplayString12 = LocalTopLevelTermDisplay3 + LocalSecondLevelTermDisplay3 + LocalThirdLevelTermDisplay3 + LocalFourthLevelTermDisplay3

		MaintainDisplayString    
			initialize LocalAlphaTermDisplay
			initialize LocalFullAlphaTermDisplay
			initialize LocalTopLevelTermDisplay
			initialize LocalSecondLevelTermDisplay
			initialize LocalThirdLevelTermDisplay
			initialize LocalFourthLevelTermDisplay
			if (TermLevel = 1)
				LocalAlphaTermDisplay = DisplayOrder
				if (LocalAlphaTermDisplay[2] !entered) 
					LocalTopLevelTermDisplay = "0" + DisplayOrder
				else 
					LocalTopLevelTermDisplay = DisplayOrder
				LocalSecondLevelTermDisplay = "00"
				LocalThirdLevelTermDisplay  = "00"
				LocalFourthLevelTermDisplay = "00"
			if (TermLevel = 2)
				LocalFullAlphaTermDisplay 	= ParentTermAndConditionRel.DisplayString
				LocalTopLevelTermDisplay 	= LocalFullAlphaTermDisplay[1] + LocalFullAlphaTermDisplay[2]
				LocalAlphaTermDisplay 		= DisplayOrder
				if (LocalAlphaTermDisplay[2] !entered) 
					LocalSecondLevelTermDisplay = "0" + DisplayOrder
				else 
					LocalSecondLevelTermDisplay = DisplayOrder
				LocalThirdLevelTermDisplay  = "00"
				LocalFourthLevelTermDisplay = "00"	
			if (TermLevel = 3)
				LocalFullAlphaTermDisplay 	= ParentTermAndConditionRel.DisplayString
				LocalTopLevelTermDisplay 	= LocalFullAlphaTermDisplay[1] + LocalFullAlphaTermDisplay[2]
				LocalSecondLevelTermDisplay = LocalFullAlphaTermDisplay[3] + LocalFullAlphaTermDisplay[4]
				LocalAlphaTermDisplay 		= DisplayOrder
				if (LocalAlphaTermDisplay[2] !entered) 
					LocalThirdLevelTermDisplay = "0" + DisplayOrder
				else 
					LocalThirdLevelTermDisplay = DisplayOrder
				LocalFourthLevelTermDisplay = "00"
			if (TermLevel = 4)
				LocalFullAlphaTermDisplay 	= ParentTermAndConditionRel.DisplayString
				LocalTopLevelTermDisplay    = LocalFullAlphaTermDisplay[1] + LocalFullAlphaTermDisplay[2]
				LocalSecondLevelTermDisplay = LocalFullAlphaTermDisplay[3] + LocalFullAlphaTermDisplay[4]
				LocalThirdLevelTermDisplay  = LocalFullAlphaTermDisplay[5] + LocalFullAlphaTermDisplay[6]
				LocalAlphaTermDisplay 		= DisplayOrder
				if (LocalAlphaTermDisplay[2] !entered) 
					LocalFourthLevelTermDisplay = "0" + DisplayOrder
				else 
					LocalFourthLevelTermDisplay = DisplayOrder	
			DisplayString = LocalTopLevelTermDisplay + LocalSecondLevelTermDisplay + LocalThirdLevelTermDisplay + LocalFourthLevelTermDisplay
	
	Create Rules
	
		if (LocalParentTermAndCondition entered
		and FromRepository)
			ParentTermAndCondition = first LocalParentTermAndConditionRel.SourcingEventTermAndCondition
		if (!SourcingEvent.CreateByCopy
		and SourcingEventArticle.CMArticle)
			CMTerm = true
		if (!FromRepository)
			if (ParentTermAndConditionExists)
				constraint (ParentTermAndConditionRel.TermLevel < 4) 
					"CannotHaveMoreThanFourLevelsOfTermsAndConditions"
			if (!ParentTermAndConditionExists)
				TermLevel = 1
			else
				TermLevel = ParentTermAndConditionRel.TermLevel + 1
		if (!ParentTermAndConditionExists)
			autosequence DisplayOrder using SourcingEventArticle.LastTermAndConditionDisplayOrder
		else
			autosequence DisplayOrder using ParentTermAndConditionRel.LastChildTermAndConditionDisplayOrder
		
	Create Exit Rules
	
		include MaintainDisplayString12
	
	Actions
		Create is a Create Action
			valid when (SourcingEvent.InEditableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotAddTermWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddTermWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
		
			Field Rules
				
				AllowModification
					default to true
						
		CreateFromWizard is a Create Action
			valid when (SourcingEvent.InEditableState)
				
			Field Rules
				AllowModification
					default to true
					
			Action Rules
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
				
				if (Attachment entered)
					invoke Create SourcingEventTermAndConditionAttachment
						invoked.Company         		        = Company
						invoked.SourcingEvent           		= SourcingEvent
						invoked.SourcingEventArticle     		= SourcingEventArticle
						invoked.SourcingEventTermAndCondition	= SourcingEventTermAndCondition
						invoked.Attachment                      = Attachment
		
		CreateFromTerm is a Create Action
			restricted
			valid when (SourcingEvent.InEditableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotAddTermWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotAddTermWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.AddTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
			
		UpdateFromTermAttachment is an Update Action
			restricted
			valid when (SourcingEvent.InEditableState)
			Action Rules
				if (VerifiedFields changed)
					constraint (AllowModification)
						"TermCannotBeModified"
					RepositoryModified = true 
				constraint (SourcingEvent.InActionableState)
					"CannotUpdateTermWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotUpdateTermWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
							
				if (NewDisplayOrder entered)
					invoke Update SourcingEventArticle
						invoked.TermDisplayChange = true

		Update is an Update Action
			valid when (SourcingEvent.InEditableState)
			Action Rules
				if (VerifiedFields changed)
					constraint (AllowModification)
						"TermCannotBeModified"
					RepositoryModified = true 
				constraint (SourcingEvent.InActionableState)
					"CannotUpdateTermWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotUpdateTermWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.UpdateTermAndCondition)
					invoke Amend Open.Notified SourcingEvent
							
				if (NewDisplayOrder entered)
					if (!SourcingEventArticle.TermDisplayChange)
						confirmation required
							"Warning;AllAdditionalTermAndConditionsWillBeAppendedToEndOfList;DoYouWantToContinue?"
					invoke Update SourcingEventArticle
						invoked.TermDisplayChange = true
						
			Exit Rules
				include MaintainDisplayString12

		UpdateDisplayOrder is an Update Action
			restricted

		EventTermAndConditionUpdate is an Update Action
			restricted
			
		Delete is a Delete Action
			valid when (SourcingEvent.InEditableState)
			Action Rules
				constraint (SourcingEvent.InActionableState)
					"CannotDeleteTermWhileEventIsPendingAward,Cancelled,OrClosed"
				constraint (!SourcingEvent.NeedsApproval)
					"CannotDeleteTermWhileEventIsPendingEventApproval"
				if (SourcingEvent.InInitiateAmendmentState
				and Company.AmendmentCriteria.DeleteTermAndCondition)
					invoke Amend Open.Notified SourcingEvent      

				if (!AnotherTermForArticleRel exists)
					invoke Delete SourcingEventArticle
					
		MoveToNewParent is an Instance Action
			Parameters
				Company  
				SourcingEvent
				SourcingEventArticle
				SourcingEventTermAndCondition
				NewParentTermAndCondition  is a SourcingEventTermAndCondition
				
			Parameter Rules
				Company
					default to Company
					
				SourcingEvent
					default to SourcingEvent
				
				SourcingEventArticle
					default to SourcingEventArticle
					
				SourcingEventTermAndCondition
					default to SourcingEventTermAndCondition
				
				NewParentTermAndCondition
					required
					constraint (NewParentTermAndCondition != ParentTermAndCondition)
						"NewParentTermAndConditionMustBeDifferentThanCurrent"
					constraint (NewParentTermAndCondition != SourcingEventTermAndCondition)
						"TermAndConditionCannotBeItsOwnParent"
			
			Action Rules
				ParentTermAndCondition  	= NewParentTermAndCondition
	 		
	 		Exit Rules
	 			DisplayOrder        = ParentTermAndConditionRel.LastChildTermAndConditionDisplayOrder + 1
	 			increment ParentTermAndConditionRel.LastChildTermAndConditionDisplayOrder  
				TermLevel           = ParentTermAndConditionRel.TermLevel + 1
				include MaintainDisplayString12
				for each DescendantTermAndConditionRel
					invoke Update each
			 			invoked.TermLevel               = each.ParentTermAndConditionRel.TermLevel + 1
					invoke UpdateDisplayString each
		
		UpdateDisplayString is a Set Action
			restricted
			
			Action Rules
			
				Instance Rules
				
					include MaintainDisplayString12 

		UpdateDisplayStringValues is an Instance Action  
			restricted
			Action Rules
				include MaintainDisplayString
