CashRequirementsTemplate is a BusinessClass
    owned by ap
    prefix is CRT
    Ontology
        symbolic key is CashRequirementsTemplate
        
    Persistent Fields
        ReportDistributionGroup
        AddendaOption           is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        PayThroughCurrentDate   is Boolean
        PayThroughDays          is Numeric size 3
        PaymentCurrentDate      is Boolean 
        PaymentDays             is Numeric size 3
        DiscountOption          is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        ReportSequence          is AlphaUpper size 1
            States
                VendorNumber value is "N"
                SearchName   value is "S"
        ProcessFlag             is Numeric size 1
            States
                APPayments  value is 0
                    default label is "PayablesPayments"
                CBPayments  value is 1
                    default label is "CashLedgerPayments"
                BothAPAndCB value is 2
                    default label is "BothPayablesAndCashLedgerPayments"
        EditPaymentCode         is a BankTransactionCode
        Comment                 is Alpha size 80
        IncludeVoucherNumber    is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        CashCode
        CashCodeGroup
        OverrideCashCode        is a CashCode
        Company                 is a PayablesCompany
            disable surrogates
        ProcessLevel            is a PayablesProcessLevel
        PaymentCode             is a BankTransactionCode
        MaxCashOutlay           is Numeric size 16
        InvoiceGroup            is a PayablesInvoiceGroup       
        PayImmediatelyOnly      is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        InvoiceCurrency         is a Currency
        CurrencyOption          is AlphaUpper size 1
            States
                Include value is "I"
                Exclude value is "E"
        OverridePaymentRate      is a CurrencyExchangeRate      
        VendorClasses            is a AP150VenClassArray
        Vendor
        Vendors					is a AP146VendorArray
        SpecialCreditHandling   is AlphaUpper size 1
            States
                Yes value is "Y"
                No  value is "N"
        SortOption1              is AlphaUpper size 1
            States
                VendorName         value is "N"
                VendorNumber       value is "V"
                VendorClass        value is "C"
                PostalCode         value is "P"
                InvoiceGroup       value is "I"
                CounterSignature   value is "L"
                PaymentAmount      value is "A"
                Priority           value is "R"
                Enclosure          value is "E"
                OverflowRemittance value is "S"
        SortOption2              is AlphaUpper size 1
            States
                VendorName               value is "N"
                VendorNumber             value is "V"
                VendorClass              value is "C"
                PostalCode               value is "P"
                InvoiceGroup             value is "I"
                PaymentAmountWithCounterSignature    value is "L"
                PaymentAmountWithoutCounterSignature value is "A"
                Priority                 value is "R"
                Enclosure                value is "E"
                OverflowRemittance       value is "S"
        SortOption3              is AlphaUpper size 1
            States
                VendorName               value is "N"
                VendorNumber             value is "V"
                VendorClass              value is "C"
                PostalCode               value is "P"
                InvoiceGroup             value is "I"
                PaymentAmountWithCounterSignature    value is "L"
                PaymentAmountWithoutCounterSignature value is "A"
                Priority                 value is "R"
                Enclosure                value is "E"
                OverflowRemittance       value is "S"
        SignOver                is Numeric size 16
            default label is "AmountLimit"
		CashPaymentSortOption1              is Numeric 2                
            States
                ReferenceNumber             value is 1
                Description                 value is 2
                PaymentAmount               value is 3
                PayeeName                   value is 4
                PostalCode                  value is 5
                FirstPartyReference         value is 6
                SecondPartyReference        value is 7              
                TransactionData1            value is 8
                TransactionData2            value is 9
                TransactionData3            value is 10
                TransactionData4            value is 11
        CashPaymentSortOption2              is Numeric 3                
            States
                ReferenceNumber             value is 1
                Description                 value is 2
                PaymentAmount               value is 3
                PayeeName                   value is 4
                PostalCode                  value is 5
                FirstPartyReference         value is 6
                SecondPartyReference        value is 7              
                TransactionData1            value is 8
                TransactionData2            value is 9
                TransactionData3            value is 10
                TransactionData4            value is 11
        CashPaymentSortOption3              is Numeric 4                
            States
                ReferenceNumber             value is 1
                Description                 value is 2
                PaymentAmount               value is 3
                PayeeName                   value is 4
                PostalCode                  value is 5
                FirstPartyReference         value is 6
                SecondPartyReference        value is 7              
                TransactionData1            value is 8
                TransactionData2            value is 9
                TransactionData3            value is 10
                TransactionData4            value is 11 
        DisplaySearchName       is Boolean
        DefaultTemplate         is Boolean
        Active
        IncludeCommentOnCashLedgerPayments  is Boolean        

    Field Rules
        VendorGroup
            required

        PayGroup
            required
            constraint (PayGroup.Active)
                "PayGroupIsNotActive"
        
        PayThroughDays
            constraint (PayThroughCurrentDate)
                "MustSelectCurrentDateForPayThroughDateToEnterAdjustmentDays"

        PaymentDays
            constraint (PaymentCurrentDate)
                "MustSelectCurrentDateForPaymentDateToEnterAdjustmentDays"

        Company
            if (ProcessLevel entered)
                required
                    "CompanyRequiredWhenProcessLevelEntered"
            if (Company entered)
                constraint (PayGroupCompanyRelationshipRel exists)
                    "Company<Company>IsNotAssignedToPayGroup<PayGroup>"
        
        ProcessLevel
            if (ProcessLevel entered)
                constraint (PayGroupProcessLevelRelationshipRel exists)
                    "ProcessLevel<ProcessLevel>IsNotAssignedToPayGroup<PayGroup>"
        
        EditPaymentCode
            if (EditPaymentCode entered)
                constraint (AddendaOption.Yes)
                    "EnforceAddendaRecordLimitsMustBeYes"
                constraint (EditPaymentCode.PaymentOutputOption.ElectronicFile)
                    "EditPaymentCodeMustHaveAnOutputOptionOfElectronicFile"

        PaymentCode
            if (PaymentCode entered)
                if (!PaymentCode.BankTransactionType.CashPayment
                or  !PaymentCode.TransactionOrigin.Payable)
                    constraint (false)
                        "PaymentCode<PaymentCode>IsNotCreatedByPayablesOrIsNotACashPayment"
        
        InvoiceCurrency
            if (CurrencyOption.Exclude)
                required
        
        OverridePaymentRate
            constraint (InvoiceCurrency entered)
                "InvoiceCurrencyRequiredForOverridePaymentRate"
            constraint (CurrencyOption.Include)
                "CurrencyOptionMustBeSetToIncludeForOverridePaymentRate"
            constraint (CashCode entered)
                "CashCodeRequiredForOverridePaymentRate"
            constraint (CashCode.Currency != InvoiceCurrency)
                "CannotOverrideRateBetweenInvoiceAndCashCodeWithSameCurrency"

        Vendor
            if (VendorClasses.VenClass[1] entered
            or  VendorClasses.VenClass[2] entered
            or  VendorClasses.VenClass[3] entered
            or  VendorClasses.VenClass[4] entered
            or  VendorClasses.VenClass[5] entered
            or  VendorClasses.VenClass[6] entered
            or  VendorClasses.VenClass[7] entered
            or  VendorClasses.VenClass[8] entered
            or  VendorClasses.VenClass[9] entered)
                cannot be entered
                    "CannotEnterBothVendorAndVendorClass"

            if (PayablesProcessGroup entered)
                cannot be entered
                    "CannotEnterBothVendorAndProcessGroup"

            if (VendorEntered entered)      
                cannot be entered
                    "CannotEnterVendor"

        Vendors     
            if (VendorClasses.VenClass[1] entered
            or  VendorClasses.VenClass[2] entered
            or  VendorClasses.VenClass[3] entered
            or  VendorClasses.VenClass[4] entered
            or  VendorClasses.VenClass[5] entered
            or  VendorClasses.VenClass[6] entered
            or  VendorClasses.VenClass[7] entered
            or  VendorClasses.VenClass[8] entered
            or  VendorClasses.VenClass[9] entered)
                cannot be entered
                    "CannotEnterBothVendorAndVendorClass"

            if (PayablesProcessGroup entered)
                cannot be entered
                    "CannotEnterBothVendorAndProcessGroup"

        SortOption1
            if (SortOption2 entered)
                required
                    "MustSelectConsecutiveSortOptions"
            
        SortOption2
            if (SortOption3 entered)
                required
                    "MustSelectConsecutiveSortOptions"

		CashPaymentSortOption1

			if (CashPaymentSortOption2 entered)
				required
					"MustSelectConsecutiveSortOptions"
			
		CashPaymentSortOption2

			if (CashPaymentSortOption3 entered)
				required
					"MustSelectConsecutiveSortOptions"
        
        SignOver
            if (SortOption1.CounterSignature
            or  SortOption2.PaymentAmountWithCounterSignature
            or  SortOption3.PaymentAmountWithCounterSignature)
                required
                    "AmountLimitRequiredWithCounterSignatureSort"
            if (SignOver entered)
                constraint (CounterSignatureUsed)
                    "CounterSignatureSortRequiredWhenAmountLimitEntered"

        CashCode
            if (CashCode entered)
                if (CashCode.DefaultPostCompany entered)
                    LocalCompany    = CashCode.DefaultPostCompany
                else
                    LocalCompany    = PayGroup.PostCompany
                LocalCashCode   = CashCode
                constraint (CompanyCashCodeRel exists)
                    "CompanyCashCodeDoesNotExistForCompany<LocalCompany>AndCashCode<LocalCashCode>"
        
        OverrideCashCode
            if (OverrideCashCode entered)
                constraint (!VendorGroup.BusinessGroup.FinanceEnterpriseGroup.FundAccounting)   
                    "OverrideCashCodeNotAllowedWithFundAccounting"
                constraint (OverrideCashCode.Active)
                    "OverrideCashCodeIsNotActive"
                if (OverrideCashCode.DefaultPostCompany entered)
                    LocalCompany    = OverrideCashCode.DefaultPostCompany
                else
                    LocalCompany    = PayGroup.PostCompany
                LocalCashCode   = OverrideCashCode
                constraint (CompanyCashCodeRel exists)
                    "CompanyOverrideCashCodeDoesNotExistForCompany<LocalCompany>AndCashCode<LocalCashCode>"
        
        CashCodeGroup
            constraint (CashCodeGroup.HasCashCodeGroupDetails)
                "CashCodeGroup<CashCodeGroup>ContainsNoCashCodes"

            if  (PaymentCode entered
            and  PayablesProcessGroup entered
            and  PayablesProcessGroup != "NONE")
                for each CashCodeGroup.CashCodeGroupDetailsRel
                    initialize ValidateCashPaymentFormat
                    ValidateCashPaymentFormat.VendorGroup           = VendorGroup
                    ValidateCashPaymentFormat.CashCode              = each.CashCode
                    ValidateCashPaymentFormat.BankTransactionCode   = PaymentCode
                    ValidateCashPaymentFormat.PayablesProcessGroup  = PayablesProcessGroup
                    LocalBoolean = ValidateCashPaymentFormat.PerformValidateCashPaymentFormat
                    constraint (ValidateCashPaymentFormat.OutputErrorNumber not entered)
                        "<ValidateCashPaymentFormat.OutputErrorMessage>"
            
            for each CashCodeGroup.CashCodeGroupDetailsRel
                if (each.CashCode.DefaultPostCompany    entered)
                    LocalCompany    = each.CashCode.DefaultPostCompany
                else
                    LocalCompany    = PayGroup.PostCompany
                LocalCashCode   = each.CashCode
                constraint (CompanyCashCodeRel exists)
                    "CompanyCashCodeDoesNotExistForCompany<LocalCompany>AndCashCode<LocalCashCode>"

        Active
            initial value is true
        
        DefaultTemplate
            constraint (DefaultTemplateRel !exists)
                "DefaultTemplateAlreadyExistsFor<first DefaultTemplateRel.CashRequirementsTemplate>"
        
    Relations
        DefaultTemplateRel
            one-to-many relation to CashRequirementsTemplate
            Field Mapping uses symbolic key
                related.VendorGroup          = VendorGroup
                related.PayGroup             = PayGroup
                related.PayablesProcessGroup = PayablesProcessGroup
            Instance Selection 
                where (related.DefaultTemplate
                and    related.CashRequirementsTemplate != CashRequirementsTemplate)

        LocalCashPaymentFormatRel       
            one-to-one relation to CashPaymentFormat
            Field Mapping uses symbolic key
                related.CashManagementGroup                     = LocalCashCode.CashManagementGroup 
                related.CashCode                                = LocalCashCode
                related.CashPaymentFormat.BankTransactionCode   = LocalBankTransactionCode
                related.CashPaymentFormat.PayablesProcessGroup  = PayablesProcessGroup

        PayablesElectronicTransferIDRel     
            one-to-one relation to PayablesElectronicTransferID
            Field Mapping uses symbolic key
                related.VendorGroup         = VendorGroup
                related.PayGroup            = PayGroup
                related.BankTransactionCode = LocalBankTransactionCode

        PayGroupCompanyRelationshipRel
            one-to-many relation to PayGroupCompanyRelationship
            Field Mapping uses symbolic key
                related.VendorGroup = VendorGroup
                related.PayGroup    = PayGroup
                related.Company     = Company

        PayGroupProcessLevelRelationshipRel
            one-to-one relation to PayGroupCompanyRelationship
            Field Mapping uses symbolic key
                related.VendorGroup     = VendorGroup
                related.PayGroup        = PayGroup
                related.Company         = Company
                related.ProcessLevel    = ProcessLevel

        CompanyCashCodeRel
            one-to-one relation to CompanyCashCode
            Field Mapping uses symbolic key
                related.Company         = LocalCompany
                related.CashCode        = LocalCashCode

    Sets









    Conditions
        CounterSignatureUsed
			when (SortOption1.CounterSignature
			or    SortOption2.PaymentAmountWithCounterSignature
			or    SortOption3.PaymentAmountWithCounterSignature)
        
        VendorEntered		
			restricted
			when (Vendors.Vendor[1] entered
			or    Vendors.Vendor[2] entered
			or    Vendors.Vendor[3] entered
			or    Vendors.Vendor[4] entered
			or    Vendors.Vendor[5] entered
			or    Vendors.Vendor[6] entered
			or    Vendors.Vendor[7] entered
			or    Vendors.Vendor[8] entered
			or    Vendors.Vendor[9] entered)

		CBPaymentsInvolved                                                  
			when (ProcessFlag.CBPayments
			or    ProcessFlag.BothAPAndCB)

    Derived Fields
        DerivedPaymentDate is a DerivedField
            type is Date
            if (PaymentCurrentDate)
                return current corporate date + PaymentDays as days 
        
        DerivedPayThroughDate is a DerivedField
            type is Date
            if (PayThroughCurrentDate)
                return current corporate date + PayThroughDays as days

    Local Fields
        ValidateCashPaymentFormat
        LocalBoolean                is Boolean
        LocalCompany                is a Company
        LocalCashCode               is a CashCode
        LocalBankTransactionCode    is a BankTransactionCode    

    Attach Rules
        constraint (Active)
            "CashRequirementsTemplateIsNotActive"

    Rule Blocks
        EditParameters
            if  (PaymentCode entered
            and  CashCode entered)
                if (ProcessGroup entered
                and ProcessGroup != "NONE")
                    initialize ValidateCashPaymentFormat
                    ValidateCashPaymentFormat.VendorGroup           = VendorGroup
                    ValidateCashPaymentFormat.CashCode              = CashCode
                    ValidateCashPaymentFormat.BankTransactionCode   = PaymentCode
                    ValidateCashPaymentFormat.PayablesProcessGroup  = ProcessGroup
                    LocalBoolean = ValidateCashPaymentFormat.PerformValidateCashPaymentFormat
                    constraint (ValidateCashPaymentFormat.OutputErrorNumber not entered)
                        "<ValidateCashPaymentFormat.OutputErrorMessage>"

            if  (PaymentCode entered
            and  OverrideCashCode entered)
                if (ProcessGroup entered
                and ProcessGroup != "NONE")
                    initialize ValidateCashPaymentFormat
                    ValidateCashPaymentFormat.VendorGroup           = VendorGroup
                    ValidateCashPaymentFormat.CashCode              = OverrideCashCode
                    ValidateCashPaymentFormat.BankTransactionCode   = PaymentCode
                    ValidateCashPaymentFormat.PayablesProcessGroup  = ProcessGroup
                    LocalBoolean = ValidateCashPaymentFormat.PerformValidateCashPaymentFormat
                    constraint (ValidateCashPaymentFormat.OutputErrorNumber not entered)
                        "<ValidateCashPaymentFormat.OutputErrorMessage>"

            if (SortOption1 entered)
                if (SortOption1 = SortOption2
                or  SortOption1 = SortOption3)
                    constraint (false)
                        "CannotHaveDuplicateSortOptions"
            if (SortOption2 entered)
                if (SortOption1 = SortOption3
                or  SortOption2 = SortOption3)
                    constraint (false)
                        "CannotHaveDuplicateSortOptions"
            
            if (CashCode entered
            or  CashCodeGroup entered
            or  OverrideCashCode entered)
                if (CashCode entered
                and CashCodeGroup entered)
                    constraint (false)
                        "CannotEnterCashCodeAndCashCodeGroup"

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Purge Action

