TaxDistributionCode is a BusinessClass
    owned by tx
    prefix is TxDh
    Ontology
        symbolic key is TaxDistributionCode  

    Patterns
        implements StaticJava
        disable AuditIndex
					
    Persistent Fields
		Country								 
        Description		 	 
		MatchBy				is Alpha 1	
			restricted				 
			States
		        JurisdictionLevel				value is blank
		        ImpositionType					value is "I"
		       	BothJurisdictionAndImposition	value is "B"
		SummaryOrDetail		is Alpha 1						 
			restricted			 
			States
				Summary		value is blank	 			 
				Detail		value is "D"	
					default label is "Detail"
		DefaultAccountsFrom		is Alpha 1	
			restricted			 		 
			States
				Default			value is blank				 
				TaxEntity		value is "T"	 			 
				EntityTaxCode	value is "D"	
					default label is "EntityTaxCode"
		DefaultTaxCode			is a TaxCode				 
			restricted
		AccountValidationEdit	is Alpha 1					 
			restricted			 
			States
				HardStop		value is blank	 			 
				Warning			value is "W"	
				DontValidate	value is "D"
		Comment					is a CommentText
							
	Transient Fields	
		TransJurisLevel			is a TaxDistributionLevel
		TransDistType			is Alpha 20					
		TransientTaxCode		is a TaxCode				 
		ValidateTaxAccounts     is Boolean
		RequireAllInputOutputAccounts      is Boolean
		
    Field Rules
		FinanceEnterpriseGroup
			initial value is actor.context.FinanceEnterpriseGroup			
			default to actor.context.FinanceEnterpriseGroup	
		DefaultAccountsFrom
			if (TaxEntity.UseTaxCodeAccounts)
				DefaultAccountsFrom = "D"
			else
				DefaultAccountsFrom = "T"
		TransientTaxCode
			constraint (TaxEntity.UseTaxCodeAccounts)
				"DefaultTaxCodeOnlyValidIfUsingTaxCodeAccounts=true;AccountsWillDefaultFrom_\Tax\Entity"
			constraint (!TransientTaxCode.TaxType.TaxTableCode)
				"CannotUseATaxTable"
									
	Local Fields 
		LocalTaxDistributionCode 			is a TaxDistributionCode
		LocalNewTaxDistributionFEG 			is like FinanceEnterpriseGroup	
		LocalNewTaxDistributionCode 		is like TaxDistributionCode	 


    Derived Fields

		DerivedTaxCode is a DerivedField	
			type is like TaxCode
			restricted 
			if (TransientTaxCode entered)	 
				return TransientTaxCode
			if (DefaultTaxCode entered)	 
				return DefaultTaxCode
			return blank    
		
    Conditions
        EntityTaxCodeExists
        	restricted
            when (TaxEntity.UseTaxCodeAccounts
            and   TransientTaxCode entered
            and   DefaultEntityTaxCodeRel exist)
		UseTaxCodeAccountsTrue
			restricted
			when (TaxEntity.UseTaxCodeAccounts)          
		TaxDistributionoCodeDetailsExist
			restricted
			when (AllTaxDistributionoCodeDetailsRel exist)
			
    Relations










		ExistingTaxDistributionoCodeRel
			one-to-one relation to TaxDistributionCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.TaxEntity 			   = TaxEntity
				related.TaxDistributionCode	   = LocalTaxDistributionCode

		NewTaxDistributionoCodeRel
			one-to-one relation to TaxDistributionCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TaxEntity 				= TaxEntity
				related.TaxDistributionCode	   	= LocalNewTaxDistributionCode	 
			
 		CopyTaxDistributionoCodeDetailRel
			one-to-many relation to TaxDistributionCodeDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup	
				related.TaxEntity 				= TaxEntity		
				related.TaxDistributionCode	  	= LocalTaxDistributionCode			

 		DefaultTaxDistDetailRecordRel
			one-to-many relation to TaxDistributionCodeDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TaxEntity 				= TaxEntity
				related.TaxDistributionCode	   	= TaxDistributionCode
			Instance Selection
				where (related.TaxDistributionLevel = "DEFAULT")

 		AllTaxDistributionoCodeDetailsRel
			one-to-many relation to TaxDistributionCodeDetail
			delete cascades
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup	
				related.TaxEntity 				= TaxEntity		
				related.TaxDistributionCode	  	= TaxDistributionCode	
				
 		DefaultTaxDistributionLevelRecordRel
			one-to-one relation to TaxDistributionLevel
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup

				related.TaxDistributionLevel 	= "DEFAULT"

		TaxDistributionLevelsRel
			one-to-many relation to TaxDistributionLevel
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				
 		NewDefaultTaxDistDetailRecordRel
			one-to-one relation to TaxDistributionCodeDetail
			Field Mapping uses ByTaxDistributionLevel		 
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TaxEntity	   			= TaxEntity
				related.TaxDistributionCode	   	= LocalNewTaxDistributionCode
				related.TaxDistributionLevel   	= "DEFAULT"
				related.DistributionType 	= blank							
				
        DefaultEntityTaxCodeRel	 
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup   
                related.TaxEntity 				= TaxEntity
                related.TaxCode					= DerivedTaxCode
                				
	Rule Blocks	
	
	Sets


	Actions

		Create is a Create Action
			Entrance Rules
				constraint (TaxEntity.ThirdParty.VertexOSeries)
					"CanOnlyUse_\Tax\Distribution\CodeFor_\Vertex"
			Exit Rules
				if (DefaultTaxDistributionLevelRecordRel not exist)
					invoke Create TaxDistributionLevel 
						invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
						invoked.TaxDistributionLevel	= "DEFAULT"

				if (DefaultTaxDistDetailRecordRel not exist)
					invoke Create TaxDistributionCodeDetail 
						invoked.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
						invoked.TaxEntity				= TaxEntity
						invoked.TaxDistributionCode		= TaxDistributionCode
						invoked.TaxDistributionLevel	= "DEFAULT"
						invoked.DistributionType 	= blank						
						invoked.SystemGenerated			= true
						if (DerivedTaxCode entered
						and DefaultEntityTaxCodeRel exist)
							invoked.TaxInputAccount           		 = DefaultEntityTaxCodeRel.TaxInputAccount           	
					        invoked.TaxInputDueAccount        		 = DefaultEntityTaxCodeRel.TaxInputDueAccount        	
					        invoked.TaxOutputAccount          		 = DefaultEntityTaxCodeRel.TaxOutputAccount          	
					        invoked.TaxOutputDueAccount       		 = DefaultEntityTaxCodeRel.TaxOutputDueAccount       	





						else
							invoked.TaxInputAccount           		 = TaxEntity.TaxInputAccount           	
					        invoked.TaxInputDueAccount        		 = TaxEntity.TaxInputDueAccount        	
					        invoked.TaxOutputAccount          		 = TaxEntity.TaxOutputAccount          	
					        invoked.TaxOutputDueAccount       		 = TaxEntity.TaxOutputDueAccount       	
					        invoked.RecoverableAccount        		 = TaxEntity.RecoverableAccount        	
					        invoked.NonRecoverableAccount        	 = TaxEntity.NonRecoverableAccount       
					        invoked.ForeignRecoverableAccount  		 = TaxEntity.ForeignRecoverableAccount  	
					        invoked.ForeignNonRecoverableAccount     = TaxEntity.ForeignNonRecoverableAccount

							
		Update is an Update Action
			Action Rules
				if (TransientTaxCode entered
				or  ValidateTaxAccounts
				or  RequireAllInputOutputAccounts)
					for each AllTaxDistributionoCodeDetailsRel
						invoke Update each
							invoked.OverrideTaxCode = TransientTaxCode
							invoked.RequireAllInputOutputAccounts = RequireAllInputOutputAccounts
							invoked.LastValidated = current timestamp	
								
		Delete is a Delete Action
		Purge is a Purge Action
			
		CopyFromExisting is an Instance Action	 	 
			Parameters
				ExistingTaxDistributionCode	is a TaxDistributionCode
				NewTaxDistributionCodeFEG 	is like FinanceEnterpriseGroup
				NewTaxDistributionCode      is like TaxDistributionCode	 
				BypassAccountEdit			is Boolean	
			Parameter Rules
				ExistingTaxDistributionCode
					initial value is TaxDistributionCode.TaxDistributionCode
					required
				NewTaxDistributionCode
					required
				NewTaxDistributionCodeFEG
					initial value is FinanceEnterpriseGroup	
					default to FinanceEnterpriseGroup
			Action Rules
			    LocalNewTaxDistributionFEG  = NewTaxDistributionCodeFEG	
				LocalTaxDistributionCode	= ExistingTaxDistributionCode
				LocalNewTaxDistributionCode	= NewTaxDistributionCode
				constraint (NewTaxDistributionoCodeRel not exist)
					"NewTaxDistributionCodeAlreadyExist"
				if (NewTaxDistributionCodeFEG not entered)
					NewTaxDistributionCodeFEG = LocalNewTaxDistributionFEG
				if (ExistingTaxDistributionoCodeRel exist)
					invoke Create TaxDistributionCode
						fill in fields from this instance
						invoked.FinanceEnterpriseGroup 					= LocalNewTaxDistributionFEG	 	
						invoked.TaxEntity 								= TaxEntity
						invoked.TaxDistributionCode	   			   		= NewTaxDistributionCode
					for each CopyTaxDistributionoCodeDetailRel
						if (each.TaxDistributionLevel = "DEFAULT"
						and NewDefaultTaxDistDetailRecordRel exist)
							invoke Update NewDefaultTaxDistDetailRecordRel
								invoked.BypassAccountEdit				= BypassAccountEdit
								invoked.Description						= each.Description					
								invoked.Country                     	= each.Country                     
								invoked.TaxDistributionLevel			= each.TaxDistributionLevel			
								invoked.Jurisdiction					= each.Jurisdiction				
								invoked.DistributionType				= each.DistributionType				
								invoked.TaxInputAccount           		= each.TaxInputAccount           	
								invoked.TaxInputDueAccount        		= each.TaxInputDueAccount        	
								invoked.TaxOutputAccount          		= each.TaxOutputAccount          	
								invoked.TaxOutputDueAccount       		= each.TaxOutputDueAccount       	
								invoked.RecoverableAccount        		= each.RecoverableAccount        	
								invoked.NonRecoverableAccount       	= each.NonRecoverableAccount       
								invoked.ForeignRecoverableAccount  		= each.ForeignRecoverableAccount  	
								invoked.ForeignNonRecoverableAccount 	= each.ForeignNonRecoverableAccount
								invoked.VATSuspenseAccount			 	= each.VATSuspenseAccount
						else
						if (!each.TaxDistributionLevel = "DEFAULT")
							invoke Create TaxDistributionCodeDetail
								fill in fields from this instance
									except invoked.TaxDistributionCode
									except invoked.TaxDistributionCodeDetail
								invoked.BypassAccountEdit				= BypassAccountEdit
								invoked.FinanceEnterpriseGroup 			= LocalNewTaxDistributionFEG	 
								invoked.TaxDistributionCode 			= NewTaxDistributionCode	
								invoked.Description						= each.Description					
								invoked.Country                     	= each.Country                     
								invoked.TaxDistributionLevel			= each.TaxDistributionLevel			
								invoked.Jurisdiction					= each.Jurisdiction				
								invoked.DistributionType				= each.DistributionType				
								invoked.TaxInputAccount           		= each.TaxInputAccount           	
								invoked.TaxInputDueAccount        		= each.TaxInputDueAccount        	
								invoked.TaxOutputAccount          		= each.TaxOutputAccount          	
								invoked.TaxOutputDueAccount       		= each.TaxOutputDueAccount       	
								invoked.RecoverableAccount        		= each.RecoverableAccount        	
								invoked.NonRecoverableAccount       	= each.NonRecoverableAccount       
								invoked.ForeignRecoverableAccount  		= each.ForeignRecoverableAccount  	
								invoked.ForeignNonRecoverableAccount 	= each.ForeignNonRecoverableAccount
								invoked.VATSuspenseAccount			 	= each.VATSuspenseAccount

		ClearDefaultTaxCode is an Instance Action	
			restricted 			 
			Parameters
			Parameter Rules
			Action Rules
				invoke Update TaxDistributionCode
					invoked.DefaultTaxCode = blank	 	
