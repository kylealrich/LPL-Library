UserDefaultPrinter is a BusinessClass
	owned by idm
	
	prefix is IDMA
	
	Ontology
		symbolic key is UserDefaultPrinter
		
	
	Patterns
	
	Persistent Fields
		IDMPrinter
			default label is "DefaultPrinter"
			
	Local Fields
		LocalIDMPrinter				is an IDMPrinter
		
	Transient Fields
		TransientDefaultPrinter		is an IDMPrinter
		
	Conditions
		DefaultIDMPrinterExists
			restricted
			when (UserDefaultPrinterRel exists)
		
	Derived Fields
		DerivedDefaultPrinter is a DerivedField
			type is AlphaUpper size 20
			default label is "CurrentPrinter"
			return UserDefaultPrinterRel.IDMPrinter
				
	Field Rules
	
				
	Rule Blocks
		SetupPrinter
			if (UserDefaultPrinterRel exists)
				invoke Update UserDefaultPrinterRel
					invoked.IDMPrinter = LocalIDMPrinter
			else
				invoke Create UserDefaultPrinter
					invoked.UserDefaultPrinter		= actor
					invoked.IDMPrinter			= LocalIDMPrinter
				
				
	Relations
		UserDefaultPrinterRel
			one-to-one relation to UserDefaultPrinter
			Field Mapping uses symbolic key
				related.UserDefaultPrinter.Actor	= actor
				
		IDMPrinterRel
			one-to-many relation to IDMPrinter
			Field Mapping uses symbolic key
			Instance Selection
				where (related.IDMPrinter not = DerivedDefaultPrinter)
				
	Actions
		Create is a Create Action
			
		Update is an Update Action
			
		Delete is a Delete Action
		
		OverrideDefaultIDMPrinter is a Set Action
			restricted
			default label is untranslatable
			Parameters
				PrmIDMPrinter is an IDMPrinter
					default label is "NewPrinter"
		
			Parameter Rules
				PrmIDMPrinter
					required
					
			Action Rules
				Empty Set Rules
					LocalIDMPrinter = PrmIDMPrinter
					include SetupPrinter
					
				Set Rules
					Entrance Rules
						LocalIDMPrinter = PrmIDMPrinter
						include SetupPrinter	
					
				Instance Rules
	
