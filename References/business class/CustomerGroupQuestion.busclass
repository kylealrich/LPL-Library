CustomerGroupQuestion is a BusinessClass
    owned by ar
    prefix is CGQ

    Patterns
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder

    Ontology
		symbolic key is CustomerGroupQuestion 
		
    Persistent Fields
		QuestionText					is Text
			sql name is CGQUE
			text searchable
   		ResponseType					is Numeric size 1
   			States
   				Text 				value is 1 
   				Number 				value is 2 
   				Date 				value is 3 
   				List 				value is 4 
   				YesNo 				value is 5
   				YesNoText			value is 6  
   		Attachment
   		DisplayOrder
   		Active							is Boolean
    	ResponseRules            		is Numeric size 1
            States
                NotRequired                             			value is 1
                ResponseRequired                        			value is 2
                ResponseRequiredAndAttachmentRequired   			value is 3
        YesNoResponseRules       		is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                 		value is 1
                ResponseRequired                            		value is 2
                ResponseRequiredAndAttachmentRequired       		value is 3
                ResponseRequiredAndAttachmentRequiredIfYes  		value is 4
                ResponseRequiredAndAttachmentRequiredIfNo   		value is 5
        YesNoTextResponseRules   		is Numeric size 1
            default label is "ResponseRules"
            States
                NotRequired                                         value is 1
                ResponseRequiredAndTextResponseRequired             value is 2
                ResponseRequiredAndTextOrAttachmentRequired         value is 3
                ResponseRequiredAndTextOrAttachmentRequiredIfYes    value is 4
                ResponseRequiredAndTextOrAttachmentRequiredIfNo     value is 5
                ResponseRequiredAndTextAndAttachmentRequired        value is 6
                ResponseRequiredAndTextAndAttachmentRequiredIfYes   value is 7
                ResponseRequiredAndTextAndAttachmentRequiredIfNo    value is 8
		AllowResponseAttachment			is Boolean
		
	Transient Fields
		NewDisplayOrder					is a DisplayOrder

 	Derived Fields
 		IsQuestionRequired is a ConditionalField
 			type is Alpha size 2
 			restricted
   			if (!ResponseRequired)
   				blank
			else
				"*"

		AnswerMessage is a MessageField
			restricted
			"Answer"   		
   		
   		AnswerTextWithRequired is a StringField
 			type is Text
 			restricted
   			IsQuestionRequired AnswerMessage

		CustomerRepresentativeResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (HasAnswer)
				CustomerRepresentativeQuestionResponseRel.CustomerRepresentativeResponseAnswers   	
   			else
   				blank

 		UnansweredRequiredQuestion is a ConditionalField
 			type is Alpha size 2
 			restricted
   			if (RequiredQuestionNoAnswer)
   				"*"
			else
				blank

		DerivedMissingRequiredInformation is a ConditionalField
			type is Alpha 2
			if (MissingRequiredInformation)
				"*"
			else
				blank
				
 		IsAttachmentRequired is a ConditionalField
 			type is Alpha size 2
 			restricted
   			if (!AlwaysRequireResponseAttachment)
   				blank
			else
				"*"

   		AttachDocumentMessage is a MessageField
   			restricted
   			"AttachDocument"
   		
   		AttachmentTextWithRequired is a StringField
 			type is Text
 			restricted
   			IsAttachmentRequired AttachDocumentMessage

		RequiredQuestionMessage is a MessageField
			restricted
			"RequiredQuestionWithNoAnswer"
			
		QuestionMissingMessage is a MessageField
			restricted
			"QuestionMissingRequiredAttachment"
		
		DerivedMissingInformationAlert is a DerivedField
			type is like Description
			restricted
			if (RequiredQuestionNoAnswer)
				return RequiredQuestionMessage
			else
			if (MissingRequiredAttachment)
				return QuestionMissingMessage
			else
				return ""

   	Conditions
		IsYesNo
			restricted
			when (ResponseType.YesNo
			or	  ResponseType.YesNoText)
		
		HasAttachment
			restricted
			when (Attachment entered)

		HasAnswer
			restricted
			when (CustomerRepresentativeQuestionResponseRel exists)

		AnyResponses
			restricted
			when (CustomerRepresentativeQuestionResponse set exists)
		
		RequiredQuestionNoAnswer
			restricted
			when (ResponseRequired
			and   !HasAnswer)
			
		ResponseAttachmentExists
			restricted
			when (CustomerRepresentativeQuestionResponseAttachmentRel exists)

		MissingRequiredAttachment
			restricted
			when (AlwaysRequireResponseAttachment
			and	  !ResponseAttachmentExists)

		MissingRequiredInformation
			restricted
			when (RequiredQuestionNoAnswer
			or	  MissingRequiredAttachment)
			
		ResponseRequired
			restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    ResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        MayRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo    
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired            
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired       
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo 
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)    
        
        AlwaysRequireResponseAttachment
        	restricted
            when (ResponseRules.ResponseRequiredAndAttachmentRequired
            or    YesNoResponseRules.ResponseRequiredAndAttachmentRequired          
            or    YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
            
        NonYesNoResponseType
        	restricted
            when (ResponseType.Date
            or    ResponseType.List
            or    ResponseType.Text
            or    ResponseType.Number)     

  	Relations
  		CustomerRepresentativeQuestionResponseRel
  			one-to-one relation to CustomerRepresentativeQuestionResponse
            Field Mapping uses part of key
            	related.CustomerGroup			= CustomerGroup
            	related.CustomerRepresentative	= actor.agent(CustomerRepresentativeContact).CustomerRepresentative
            	related.CustomerGroupQuestion	= CustomerGroupQuestion

        CustomerRepresentativeQuestionResponseAttachmentRel
  			one-to-many relation to CustomerRepresentativeQuestionResponse
            Field Mapping uses part of key
            	related.CustomerGroup			= CustomerGroup
            	related.CustomerRepresentative	= actor.agent(CustomerRepresentativeContact).CustomerRepresentative
            	related.CustomerGroupQuestion	= CustomerGroupQuestion
        	Instance Selection
        		where (related.Attachment entered)
			
	Sets
		ByDisplayOrder
			duplicates
			Sort Order
 				CustomerGroup
 				DisplayOrder
 				CustomerGroupQuestion
 				
   	Field Rules
   		QuestionText
   			required
   			
   		ResponseType
   			required
   			
		Active
			default to true
			
		ResponseRules
            initial value is 1 
            if (NonYesNoResponseType)
                required
                    "MustSelectAResponseRule"
                YesNoResponseRules = 0
                YesNoTextResponseRules = 0
                    
        YesNoResponseRules
            if (ResponseType.YesNo)
                required
                    "MustSelectAResponseRule"
                ResponseRules = 0
                YesNoTextResponseRules = 0
        
        YesNoTextResponseRules
            if (ResponseType.YesNoText)
                required
                    "MustSelectAResponseRule"
                ResponseRules = 0
                YesNoResponseRules = 0
                
		AllowResponseAttachment
			if (AllowResponseAttachment changed
			and !AllowResponseAttachment)
				constraint (!ResponseAttachmentExists)
					"CannotChangeAllowAttachmentUploadWithAnswerFlag;CustomerRepresentativeQuestionAnswersExistWithAttachments"
			if (MayRequireResponseAttachment)
				AllowResponseAttachment = true

	Attach Rules
		constraint (Active)
			"QuestionIsInactive"	
			
	Actions 
		Create is a Create Action
			Field Rules
	 			DisplayOrder
	 				autosequence using ByDisplayOrder
	
		Update is an Update Action
			Action Rules
				if (!ResponseType.List
				and CustomerGroupQuestionValue set exists)
					invoke Delete CustomerGroupQuestionValue set
					
				if (ResponseType changed)
					constraint (!AnyResponses)
						"CannotChangeQuestionTypeWhenAnswersExist"
			
		Delete is a Delete Action
