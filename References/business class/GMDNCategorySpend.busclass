GMDNCategorySpend is a BusinessClass
    owned by ic
    prefix is GMDN5

    Ontology
        symbolic key is GMDNCategorySpend

	Patterns
        disable AuditIndex
	
	Persistent Fields
		DateRange        
		IncludesChildCategories	is Boolean
		InvoiceSpend            is an InternationalAmount
		TotalSpend          	is an InternationalAmount 
			default label is "POSpend"
		CreateDate              is Date 

	Local Fields
		LocalSpendTerm          	is like GMDNPTCode
		LocalCalculatePOSpend   	is Boolean 
		LocalCalculateInvoiceSpend	is Boolean 
	
	Field Rules
		
	Derived Fields

	Conditions
		
 	Relations

		CategoryPathRel 
			one-to-many relation to GMDNCategoryPath 
			Field Mapping uses ByCategory
				related.ItemGroup		= ItemGroup 
				related.GMDNCategory    = GMDNCategory 

		CategoryTermLinkRel
			one-to-many relation to GMDNCategoryTermLink 
			Field Mapping uses ByCategory
				related.ItemGroup		= ItemGroup 
				related.GMDNCategory    = GMDNCategory 

		GMDNCategorySpendTermRel
			one-to-one relation to GMDNCategorySpendTerm 
			Field Mapping uses symbolic key 
				related.ItemGroup				= ItemGroup 
				related.GMDNCategory    		= GMDNCategory 
				related.GMDNCategorySpend		= GMDNCategorySpend
				related.GMDNCategorySpendTerm	= LocalSpendTerm				

	Sets 

		ByCreateDate
			Sort Order 
				ItemGroup 
				CreateDate descending 
				GMDNCategory 
				GMDNCategorySpend 

		ByInvoiceSpend 
			Sort Order 
				ItemGroup 
				InvoiceSpend descending 
				GMDNCategory 
				GMDNCategorySpend 

		ByPOSpend 
			Sort Order 
				ItemGroup 
				TotalSpend descending
				GMDNCategory 
				GMDNCategorySpend 				
			
	Actions
		
		Create is a Create Action 
			restricted 

			Action Rules 
				CreateDate = current corporate date
			
			Exit Rules 

				invoke CalculateCategorySpend 
					invoked.CalculatePOSpend		= LocalCalculatePOSpend
					invoked.CalculateInvoiceSpend	= LocalCalculateInvoiceSpend

		Delete is a Delete Action 

		CalculateCategorySpend is an Instance Action 
			restricted 
			Parameters 
				CalculateInvoiceSpend	is Boolean 
				CalculatePOSpend        is Boolean 
			
			Action Rules 

				for each CategoryTermLinkRel
					LocalSpendTerm = each.GMDNPTCode
					if (GMDNCategorySpendTermRel !exists)
						invoke Create GMDNCategorySpendTerm 
							invoked.ItemGroup				= ItemGroup
							invoked.GMDNCategory        	= GMDNCategory
							invoked.GMDNCategorySpend   	= GMDNCategorySpend
							invoked.GMDNCategorySpendTerm	= each.GMDNPTCode 

						invoke CalculateSpend each 
							invoked.OriginatingCategory 	= GMDNCategory
							invoked.GMDNCategorySpend		= GMDNCategorySpend
							invoked.DateRange           	= DateRange 
							invoked.CalculateInvoiceSpend	= CalculateInvoiceSpend 
							invoked.CalculatePOSpend        = CalculatePOSpend

				if (IncludesChildCategories)
					for each CategoryPathRel

						invoke FindCategoriesForChildren each 
							invoked.OriginatingCategory 	= GMDNCategory
							invoked.GMDNCategorySpend		= GMDNCategorySpend
							invoked.DateRange           	= DateRange 
							invoked.CalculateInvoiceSpend	= CalculateInvoiceSpend 
							invoked.CalculatePOSpend        = CalculatePOSpend
				



     	
 
