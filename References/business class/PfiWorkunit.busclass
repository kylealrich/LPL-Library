PfiWorkunit is a BusinessClass
    owned by pfi
    prefix is PfiWU
    default label is "WorkUnit"
    disable data area copy

    Ontology
        symbolic key is PfiWorkunit

    Patterns
        implements CRUD
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated
        implements IncrementalReplication
            indicator field is IsReplicated
                replicate when false
                    then set to true

    Persistent Fields

        AppsDataArea         is a PfiLandmarkDataArea
        AppsKey              is a PfiAppsKey
        AppsValue            is a PfiAppsValue
        CheckPointState      is Alpha size 100
            default label is "CheckpointState"
        ServiceDefinition    is a PfiServiceDefinition
            default label is "Service"
        EventType            is a PfiEventType 
            default label is "RequestType"
        FlowDefinition       is a PfiFlowDefinition
            default label is "Process"
        FlowVersion          is a PfiFlowVersion
            default label is "ProcessVersion"
        Actor
            default label is "Originator"
        AuthenticatedActor   is an Actor
            default label is "AuthenticatedOriginator"
        WorkTitle            is a PfiWorkTitle
        PfiFilterKey
        FilterValue          is a PfiFilterValue
        WorkPriority         is a PfiWorkPriority
        ExecutionStartDate   is TimeStamp
            default label is "StartDate"
        StartDate            is TimeStamp
            default label is "CreationDate"
        CloseDate            is TimeStamp
        Status               is a PfiWorkunitStatus
        Criterion1           is a PfiCriterion
        Criterion2           is a PfiCriterion
        Criterion3           is a PfiCriterion
        SourceType           is a PfiSourceType
        Source               is a PfiSource
        ConfigurationName    is Alpha size 100
        Log                  is Text
            disable Auditing
        PersistentLogSize    is Numeric size 12
        LogStatus            is Numeric size 2
            States
                Database    value is 0
                AmazonS3    value is 1
                    default label is "AmazonS\3"
                Uploading   value is 2
                Uploaded    value is 3
                Downloading value is 4
                Downloaded  value is 5
                Empty       value is 6
                Deleted     value is 7
        LogDownloadedTimestamp is TimeStamp
        LogUploadedTimestamp is TimeStamp
        LastActivity         is Numeric size 6
        LastMessage          is Numeric size 4
        LastWorkunitFolder   is Numeric size 4
            default label is "LastWorkUnitFolder"
        LastVariableSeqNbr   is a PfiSeqNbr
        ParentWorkunit       is a PfiWorkunit
            default label is "ParentWorkUnit"
        ClassicWorkunit      is a PfiClassicWorkunit
            default label is "ClassicWorkUnit"
        Archived             is Boolean
        LogsCleaned          is Boolean 
        ActivitiesCleaned    is Boolean 
        WorkunitVarsCleaned  is Boolean 
            default label is "WorkUnitDataPruned"
        KeyField1Value       is Alpha size 100
        KeyField2Value       is Alpha size 100
        KeyField3Value       is Alpha size 100
        KeyField4Value       is Alpha size 100
        KeyField5Value       is Alpha size 100
        KeyField6Value       is Alpha size 100
        KeyField7Value       is Alpha size 100
        KeyField8Value       is Alpha size 100
        KeyField9Value       is Alpha size 100
        KeyField10Value      is Alpha size 100
        AutoRestartCount     is Numeric size 1
        RunId                is Numeric size 3
        Checkpoint           is Numeric size 6
        IsReplicated is Boolean
            default label is untranslatable
		RestartCount is Numeric size 1
		KeepWorkUnitActive is Boolean
		IsAutoRestart is Boolean
		
    Derived Fields

		PruneImmediate is a DerivedField
			type is Boolean
			if(FlowDefinition.PruneDefinition.PruneWorkunitData.Immediate)
				return true
			return false
			
		CanRestartActionRequestWorkUnit is a DerivedField
			type is Boolean
			if(ActionRequestRel exists)
				if(ActionRequestRel.Status = ActionRequestRel.Status.InProcess or ActionRequestRel.Status = ActionRequestRel.Status.NotInProcess)
					return true
				else
					return false
			else
				return true
				
		PauseTime is a DerivedField
			type is Numeric size 3
			return FlowDefinition.PauseTime
			
		IsWaitingToBeRestarted is a DerivedField
			type is Boolean
			if(Status.Failed and IsAutoRestart and AutoRestartCount < RestartCount and !KeepWorkUnitActive)
				return true
			return false
			
		IsAutoRestartable is a DerivedField
			type is Boolean
			if(IsAutoRestart and AutoRestartCount < RestartCount)
				return true
			return false
			
        QueueAlertText is a DerivedField
            type is Alpha size 50
            default label is untranslatable
            if(IsQueueSuspended)
                return "Queue is suspended"
            else
                return "Queue mapping is set to None"

        IsQueueSuspended is a DerivedField
            type is Boolean
            default label is untranslatable
            return PfiAsyncWorkUnitTriggerRel.IsQueueSuspended

        QueueName is a DerivedField
            type is Alpha size 50
            return PfiAsyncWorkUnitTriggerRel.AsyncQueueDefinition

        IsQueueMappingNone is a DerivedField
            type is Boolean
            default label is untranslatable
            if(PfiAsyncWorkUnitTriggerRel.Priority.None)
                return true
            else
                return false

        CheckpointExists is a DerivedField
            type is Boolean
            if (Checkpoint > 0)
                return true
            return false

        S3LogDownloadMessage is a DerivedField
            type is TimeStamp
            default label is "DownloadLinkWillExpireIn2Minutes\.PleaseRefreshThePageIfItIsExpired"
            return system current timestamp

        S3LogDownloadURL is a NativeField
            type is Text
            default label is "S\3LogDownloadURL"

        IsS3DirectDownload is a DerivedField
            type is Boolean
            default label is "S\3DirectDownload"
            if (IsAmazonS3Log and IsS3LogDownloadURL)
                return true
            return false

        IsDBDirectDownload is a DerivedField
            type is Boolean
            default label is "DBDirectDownload"
            if (IsAmazonS3Log and not IsS3LogDownloadURL)
                return true
            return false

        CanDisplayLog is a DerivedField
            type is Boolean
            default label is "DisplayLogEnabled"
            if (LogStatus.Downloaded or LogStatus.Database or LogStatus.AmazonS3 or LogStatus.Uploading)
                return true
            return false

        IsAmazonS3Log is a DerivedField
            type is Boolean
            default label is "AmazonS\3Log"
            if (LogStatus.Uploaded or LogStatus.Downloading or LogStatus.Downloaded)
                return true
            return false

        IsLogDownloaded is a DerivedField
            type is Boolean
            default label is "LogDownloaded"
            if (LogStatus.Downloaded)
                return true
            return false

        IsLogDownloading is a DerivedField
            type is Boolean
            default label is "LogDownloading"
            if (LogStatus.Downloading)
                return true
            return false

        LogElapsedTime is a DerivedField
            type is Numeric size 10
            return (system current timestamp - LogDownloadedTimestamp)

        DownloadedLogRemainingTime is a DerivedField
            type is Numeric size 10
            return (7200 - LogElapsedTime)

        DownloadedLogRemainingTimeInMinutes is a DerivedField
            type is Numeric size 10
            default label is "DownloadedLogCopyWillBeDeletedIn(Minutes)"
            if (DownloadedLogRemainingTime > 0)
                return DownloadedLogRemainingTime / 60
            return 0

        IsThereLogRemainingTime is a DerivedField
            type is Boolean
            default label is "LogRemainingTime"
            if (IsLogDownloaded and DownloadedLogRemainingTimeInMinutes > 0)
                return true
            return false

        NoLogRemainingTime is a DerivedField
            type is Boolean
            if (IsLogDownloaded and DownloadedLogRemainingTimeInMinutes = 0)
                return true
            return false

        LogAttachment is a DerivedField
            type is BinaryDocument
            return Log

        MimeType is a DerivedField
            type is MimeType
            default label is "MIME_Type"
            return "text/plain"

        LogDisplay is a NativeField
            type is Text

        InputDataStatus is a DerivedField
            type is Numeric 2
            if (PfiWorkunitInputDataRel exists)
                return PfiWorkunitInputDataRel.InputDataStatus
            return 0

        InputDataExists is a DerivedField
            type is Boolean
            if (PfiWorkunitInputDataRel exists)
                return true
            return false

        ActivityCount is a DerivedField
            type is Alpha size 20
            if (PfiActivity set.exists)
                return "(" + instance count of PfiActivity set + ")"
            else
                return ""
		ActivityCountNumeric is a DerivedField
			type is Numeric size 6
			return instance count of PfiActivity set
			
		ActivityVarCount is a DerivedField
			type is Numeric size 6
			return instance count of PfiActivityVariable set
			
		WorkunitVarCount is a DerivedField
			type is Numeric size 6
			return instance count of PfiWorkunitVariable set
			
		InboxUserCount is a DerivedField
			type is Numeric size 6
			return instance count of PfiMingleInboxUserTask set		
									
        InfoCount is a DerivedField
            type is Alpha size 20
            default label is "InformationCount"
            if (PfiWorkunitInfo set.exists)
                return "(" + instance count of PfiWorkunitInfo set + ")"
            else
                return ""

        ActivityFailedCount is a DerivedField
            type is Alpha size 20
            if (PfiFailedActivityRel.exists)
                return "(" + instance count of PfiFailedActivityRel + ")"
            else
                return ""

        MessageCount is a DerivedField
            type is Alpha size 20
            if (PfiMessage set.exists)
                return "(" + instance count of PfiMessage set + ")"
            else
                return ""

        FolderCount is a DerivedField
            type is Alpha size 20
            default label is "RelatedLinkCount"
            if (PfiWorkunitFolder set.exists)
                return "(" + instance count of PfiWorkunitFolder set + ")"
            else
                return ""

        VariableCount is a DerivedField
            type is Alpha size 20
            if (PfiWorkunitVariable set.exists)
                return "(" + instance count of PfiWorkunitVariable set + ")"
            else
                return ""

        ErrorMessageCount is a DerivedField
            type is Alpha size 20
            if (PfiErrorMessage set.exists)
                return "(" + instance count of PfiErrorMessage set + ")"
            else
                return ""

        ErrorMessageNewCount is a DerivedField
            type is Alpha size 20
            if (PfiErrorMessageRel exists)
                return "(" + first PfiErrorMessageRel.TotalErrorMessageCount + ")"
            else
                return ""

        QueueCount is a DerivedField
            type is Alpha size 20
            if (PfiQueueRel.exists)
                return "(" + instance count of PfiQueueRel + ")"
            else
                return ""
                
		IsUserActionWorkUnit is a DerivedField
			type is Boolean
			if (PfiQueueRel.exists)
				return true
			return false
			
        RelatedWorkunitCount is a DerivedField
            type is Alpha size 20
            default label is "RelatedWorkUnitCount"
            if (PfiWorkunitChildrenRel.exists)
                return "(" + instance count of PfiWorkunitChildrenRel + ")"
            else
                return ""

        IsActionPending is a DerivedField
            type is Boolean
            default label is "ActionIsPending"
            if (PfiQueueTaskRel exists)
                for each PfiQueueTaskRel
                    if (each.IsCurrentActionWaitingSnapshot)
                        return true
            return false

        IsActionPendingInServer is a DerivedField
            type is Boolean
            default label is "ActionIsPendingInServer"
            if (PfiQueueDispStatusRel exists)
                return true
            return false

        CurrentQueueAssignment is a DerivedField
            type is Numeric size 6
            if (PfiQueueActRel exists)
                return PfiQueueActRel.CurrentAssignment
            return 1

        MingleInboxId is a DerivedField
            type is Numeric size 12
            default label is "InboxId"
            if (PfiQueueAssignmentRel exists)
                return PfiQueueAssignmentRel.MingleInboxId
            return 0

        NotificationCenterStatus is a DerivedField
            type is Numeric size 1
            if (PfiQueueAssignmentRel exists)
                return PfiQueueAssignmentRel.NotificationCenterStatus
            return 0
            
        UAActivityElapsedTime is a DerivedField
            type is Decimal 10.0
            if (PfiLastActivityIsUserActionAndStartedRel exists)
                return PfiLastActivityIsUserActionAndStartedRel.ElapsedTime

        ActionPendingInServerElapsedTime is a DerivedField
            type is Decimal 10.0
            if (PfiUAMetricsRel exists)
                return last PfiUAMetricsRel.ElapsedTime

        ActionPendingInServerElapsedTimeAsString is a DerivedField
            type is Alpha size 25
            if (PfiUAMetricsRel exists)
                return last PfiUAMetricsRel.ElapsedTimeAsString

        ActionDate is a DerivedField
            type is TimeStamp
            if (PfiUAMetricsRel exists)
                return last PfiUAMetricsRel.ActionDate

        ActionPendingDisplayText is a DerivedField
            type is Alpha size 25
            if (IsActionPending)
                return "Action Awaiting"
            return "Action Pending In Server"

        CancelDisplayText is a DerivedField
            type is Alpha size 25
            if (Status.Canceled)
                return "Canceled"
            return "Cancel Pending In Server"

        IsActionPendingOrActionPendingInServer is a DerivedField
            type is Boolean
            default label is "ActionPendingOrActionPendingInServer"
            if (IsActionPending or IsActionPendingInServer)
                return true
            return false





        ElapsedTime is a DerivedField   
            type is Decimal 10.0 
            if (ExecutionStartDate = blank and StartDate = blank)
                return 0

            if (ExecutionStartDate != blank)
                if (CloseDate != blank)
                    return CloseDate - ExecutionStartDate
                else
                    return system current timestamp - ExecutionStartDate

            if (ExecutionStartDate = blank and StartDate != blank)
                if (CloseDate != blank)
                    return CloseDate - StartDate
                else
                    return system current timestamp - StartDate

        ElapsedTimeAsString is a NativeField
            type is Alpha size 25

        CancelingElapsedTime is a DerivedField   
            type is Decimal 10.0 
            if (CloseDate != blank)
                return system current timestamp - CloseDate
            return 0

        CancelingElapsedTimeAsString is a NativeField
            type is Alpha size 25

        EventTypeText is a DerivedField
            type is Alpha size 6
            if (EventType = "EVT_PFLOW")
                return "Single"
            else
            if (EventType = "EVT_MULTI")
                return "Multi"

        KeyField1Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[1]

        KeyField2Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[2]

        KeyField3Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[3]

        KeyField4Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[4]

        KeyField5Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[5]

        KeyField6Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[6]

        KeyField7Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[7]

        KeyField8Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[8]

        KeyField9Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[9]

        KeyField10Name is a DerivedField
            type is Alpha size 100
            return FlowDefinition.VariableNames.VariableName[10]

        StartDateAsInforGmtDateTime is a NativeField
            type is Alpha size 20

        LogSize is a NativeField
            type is Decimal size 12.0

        LogSizeDisplay is a NativeField
            type is Alpha size 10
            default label is "LogSize"

        UserActionRoutingAsJson is a NativeField
            type is Text
            default label is "UserActionRoutingAsJSON"

        UserActionRoutingAsHtml is a NativeField
            type is RichText

        WorkUnitCanBeCanceled is a DerivedField
            type is Boolean
            if (Status.NotReady or Status.Ready or Status.Processing or Status.Failed or Status.Waiting)
                return true
            else
                return false

        GetCurrentActivityDetails is a NativeField
            type is Boolean

        IsHoldingThread is a DerivedField
            type is Boolean
            default label is"HoldingThread"
            if ((Status.Processing and not IsActionPending and not IsActionPendingInServer) or Status.Canceling)
                return true
            return false

        IsHoldingThreadButNotCanceling is a DerivedField
            type is Boolean
            default label is "HoldingThreadButNotCanceling"
            if (Status.Processing and not IsActionPending and not IsActionPendingInServer)
                return true
            return false

        CurrentActivityNameDisplay is a DerivedField
            type is Alpha size 100
            if (GetCurrentActivityDetailsCounter = 0 and IsHoldingThread)
                IsGetCurrentActivityDetails = GetCurrentActivityDetails
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            else
            if (GetCurrentActivityDetailsCounter = 3)
                GetCurrentActivityDetailsCounter = 0
            else
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            return CurrentActivityName

        CurrentActivityTypeDisplay is a DerivedField
            type is Alpha size 25
            if (GetCurrentActivityDetailsCounter = 0 and IsHoldingThread)
                IsGetCurrentActivityDetails = GetCurrentActivityDetails
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            else
            if (GetCurrentActivityDetailsCounter = 3)
                GetCurrentActivityDetailsCounter = 0
            else
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            return CurrentActivityType

        CurrentActivityElapsedTimeDisplay is a DerivedField
            type is Alpha size 25
            if (GetCurrentActivityDetailsCounter = 0 and IsHoldingThread)
                IsGetCurrentActivityDetails = GetCurrentActivityDetails
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            else
            if (GetCurrentActivityDetailsCounter = 3)
                    GetCurrentActivityDetailsCounter = 0
            else
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            return CurrentActivityElapsedTime

        CurrentActivityStartTimeDisplay is a DerivedField
            type is TimeStamp
            if (GetCurrentActivityDetailsCounter = 0 and IsHoldingThread)
                IsGetCurrentActivityDetails = GetCurrentActivityDetails
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            else
            if (GetCurrentActivityDetailsCounter = 3)
                GetCurrentActivityDetailsCounter = 0
            else
                GetCurrentActivityDetailsCounter = GetCurrentActivityDetailsCounter + 1
            return CurrentActivityStartTime

        CancelMessage is a DerivedField
            type is Alpha size 120
            if(IsHoldingThread)
                return "This action will cancel the work unit when the current running activity is done. Do you want to continue?"
            return "This action will cancel the work unit. Do you want to continue?"

        CanDeleteAmazonS3Log is a DerivedField
            type is Boolean
            default label is "AmazonS\3LogCanBeDeleted"
            if(not LogStatus.Empty and not LogStatus.Database)
                return true
            return false

        RunIdExists is a DerivedField
            type is Boolean
            if (RunId > 0)
                return true
            return false

        ErrorMessageExists is a DerivedField
            type is Boolean
            if (PfiErrorMessageRel exists)
                return true
            return false

        ErrorMessageSize is a DerivedField
            type is Numeric size 12
            if (RunId > 0 and PfiErrorMessageRel exists)
                return first PfiErrorMessageRel.ErrorMessageSize
            return 0

        ErrorMessageStatus is a DerivedField
            type is Numeric size 2
            if (RunId > 0 and PfiErrorMessageRel exists)
                return first PfiErrorMessageRel.ErrorMessageStatus
            return 0

        HasProcessIndicator is a DerivedField
            type is Boolean
            default label is "ProcessIndicator"
            if (PfiProcessIndicatorRel exists)
                return true
            return false

    Local Fields

        LocalServiceDefinition           is a PfiServiceDefinition
        LocalCriterion1                  is a PfiCriterion
        LocalCriterion2                  is a PfiCriterion
        LocalCriterion3                  is a PfiCriterion
        CurrentActivityName              is Alpha size 100
        CurrentActivityType              is Alpha size 25
        CurrentActivityStartTime         is TimeStamp
        CurrentActivityElapsedTime       is Alpha size 25
        GetCurrentActivityDetailsCounter is Numeric size 1
        IsGetCurrentActivityDetails      is Boolean
        IsS3LogDownloadURL               is Boolean
        LocalBeginDate                   is TimeStamp
        LocalEndDate                     is TimeStamp
        LocalBeginWorkunit               is a PfiWorkunit
        LocalEndWorkunit                 is a PfiWorkunit

    Rule Blocks

        CreateChildWorkUnit
            invoke Create
                assign result to ChildWorkUnit
                invoked.AppsDataArea      = AppsDataArea
                invoked.AppsKey           = AppsKey
                invoked.AppsValue         = AppsValue
                invoked.ServiceDefinition = ServiceDefinition
                invoked.EventType         = "EVT_PFLOW"
                invoked.FlowDefinition    = each.FlowDefinition
                invoked.FlowVersion       = each.FlowDefinition.CurrentFlowVersion
                invoked.Actor             = Actor
                invoked.WorkTitle         = WorkTitle
                invoked.PfiFilterKey      = PfiFilterKey
                invoked.FilterValue       = FilterValue
                invoked.WorkPriority      = WorkPriority
                invoked.StartDate         = system current timestamp
                invoked.Status            = WorkunitStatus.Ready
                invoked.Criterion1        = Criterion1
                invoked.Criterion2        = Criterion2
                invoked.Criterion3        = Criterion3
                invoked.SourceType        = SourceType
                invoked.Source            = Source
                invoked.ParentWorkunit    = PfiWorkunit

        UpdateParentStatusRuleBlock
            if (ParentWorkunit entered and old Status != Status)
                invoke UpdateParentStatus

    Field Rules

		RestartCount
			default to 3
			initial value is 3
			
        CheckPointState
            default to "0"

        Status
            default to Status.NotReady
            initial value is Status.NotReady

            if (Status changed)
                if (Status.Ready)
                    CloseDate = blank
                    if (ParentWorkunit entered and not ParentWorkunit.Status.Processing)
                        invoke Update PfiWorkunit
                            invoked.PfiWorkunit = ParentWorkunit
                            invoked.Status      = Status.Processing

                if ((old Status.NotReady or old Status.Ready) and Status.Processing)
                    ExecutionStartDate = system current timestamp

                if (not Status.Canceled)
                    invoke UpdateParentStatus
                invoke UpdateQueueTaskIsCurrentActionWaitingSnapshot

        Actor
            default to actor
            initial value is actor

        AuthenticatedActor
            default to authenticated actor
            initial value is authenticated actor

        WorkTitle
            required

        LogsCleaned
            default to false
            initial value is false

        ActivitiesCleaned
            default to false
            initial value is false

        WorkunitVarsCleaned
            default to false
            initial value is false

        GetCurrentActivityDetailsCounter
            default to 0
            initial value is 0

        AutoRestartCount
            default to 0
            initial value is 0

    Conditions

		IsNonUAWorkUnit
			default label is "NonUA_WorkUnit"
			when (PfiQueueTaskRel not exists)
			
        CanPrune
            default label is "WorkUnitCanBePruned"
            when (Status.Completed
            and not WorkunitVarsCleaned)

        IsFailed
            default label is "Failed"
            when (Status.Failed or Status.CancelFailed)

        IsCanceled
            default label is "Canceled"
            when (Status.Canceled or Status.CancelRequested or Status.Canceling)

        IsCanceling
            default label is "Canceling"
            when (Status.Canceling)

        IsHistory
            default label is "History"
            when (Status.Completed
            or    Status.Canceled)

        IsInProgress
            default label is "InProgress"
            when (Status.Processing)

        IsProcess
            default label is "Process"
            when (Status.Ready or Status.CancelRequested)

        IsMyWorkunit
            default label is "MyWorkUnit"
            when (not Archived and Actor = actor)

        CanDeleteOrMove
            default label is "WorkUnitCanBeDeletedOrMoved"
            when (Status = Status.NotReady
            or    Status = Status.Completed
            or    Status = Status.Canceled)

        NoActionRequest
            when (not ActionRequestRel exists)

        ClassicWorkunitEntered
            default label is "ClassicWorkUnitEntered"
            when (ClassicWorkunit entered)

        ActivityFailedFlag
            when (PfiFailedActivityRel exists)

        CanMoveLog
            default label is "LogStatusMovable"
            when (Status = Status.Completed
            or    Status = Status.Canceled)

        IsDispatcher3Enabled
            default label is untranslatable
            when (stackconfig(ipa).enableDispatcher30 = blank or stackconfig(ipa).enableDispatcher30 = true)

        CanDisplayQueueAlert
            default label is untranslatable
            when (IsQueueSuspended or IsQueueMappingNone)

        CanDisplayQueue
            default label is untranslatable
            when (IsDispatcher3Enabled)

		IsPruningChangeDisabled
            default label is untranslatable
            when (stackconfig(ipa).disablePruningChange = true)
            
    Relations

        PfiAsyncWorkUnitTriggerRel
            one-to-one relation to PfiAsyncWorkUnitTrigger
            Field Mapping uses ByDataAreaWorkUnit
                related.DataArea = parentcontext.dataarea
                related.WorkUnit = PfiWorkunit

        PfiMetricsRel
            one-to-many relation to PfiMetrics
            Field Mapping uses ByWorkunitActivityQueueAssignmentActionTakenUserProfileTask
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiArchivedMetricsRel
            one-to-many relation to PfiMetrics
            Field Mapping uses ByWorkunitActivityQueueAssignmentActionTakenUserProfileTask
                related.PfiWorkunit = PfiWorkunit

        PfiUAMetricsRel
            one-to-many relation to PfiMetrics
            Field Mapping uses ByWorkunitActivityQueueAssignmentTaskNameTaskTypeUserProfile
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiMetricsSummaryRel
            one-to-many relation to PfiMetricsSummary
            Field Mapping uses ByWorkunitEndDateActivity
                related.PfiWorkunit = PfiWorkunit

        PfiQueueActRel
            one-to-one relation to PfiQueue
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiQueueAssignmentRel
            one-to-one relation to PfiQueueAssignment
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity
                related.PfiQueueAssignment = CurrentQueueAssignment

        PfiQueueRel
            one-to-many relation to PfiQueue
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit

        PfiQueueDispStatusRel
            one-to-one relation to PfiQueue
            Field Mapping uses ByDispStatusWorkunitActivity
                related.DispStatus = 3
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiQueueTaskRel
            one-to-many relation to PfiQueueTask
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit

        PfiServiceDefinitionEnabledRel
            one-to-one relation to PfiServiceDefinition
            Field Mapping uses ByServiceDefinitionEnabled
                related.PfiServiceDefinition = ServiceDefinition

        PfiServFlowDefCrit1Crit2Crit3Rel
            one-to-many relation to PfiServiceFlowDefinition
            Field Mapping uses ByServDefCrit1Crit2Crit3
                related.PfiServiceDefinition = LocalServiceDefinition
                related.Criterion1           = LocalCriterion1
                related.Criterion2           = LocalCriterion2
                related.Criterion3           = LocalCriterion3

        PfiServFlowDefCrit1Crit2Rel
            one-to-many relation to PfiServiceFlowDefinition
            Field Mapping uses ByServDefCrit1Crit2Crit3
                related.PfiServiceDefinition = LocalServiceDefinition
                related.Criterion1           = LocalCriterion1
                related.Criterion2           = LocalCriterion2
                related.Criterion3           = ""

        PfiServFlowDefCrit1Rel
            one-to-many relation to PfiServiceFlowDefinition
            Field Mapping uses ByServDefCrit1Crit2Crit3
                related.PfiServiceDefinition = LocalServiceDefinition
                related.Criterion1           = LocalCriterion1
                related.Criterion2           = ""
                related.Criterion3           = ""

        PfiServFlowDefRel
            one-to-many relation to PfiServiceFlowDefinition
            Field Mapping uses ByServDefCrit1Crit2Crit3
                related.PfiServiceDefinition = LocalServiceDefinition
                related.Criterion1           = ""
                related.Criterion2           = ""
                related.Criterion3           = ""

        PfiWorkunitChildrenRel
            one-to-many relation to PfiWorkunit
            Field Mapping uses ByParentWorkunitWorkunit
                related.ParentWorkunit = PfiWorkunit

        ActionRequestRel
            one-to-one relation to ActionRequest
            Field Mapping uses ByWorkUnit
                related.WorkUnit = PfiWorkunit

        PfiFailedActivityRel is a PfiActivity set
            Instance Selection
                where (related.ActStatus.Error)

        PfiCheckpointActivityRel is a PfiActivity set
            Instance Selection
                where (related.PfiActivity = Checkpoint)

        PfiLastActivityRel
            one-to-one relation to PfiActivity
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiLastActivityIsUserActionAndStartedRel
            one-to-one relation to PfiActivity
            Field Mapping uses ByWorkunitActivityIsUserActionAndStarted
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiActorRel
            one-to-one relation to Actor
            Field Mapping uses symbolic key
                related.Actor = Actor

        PfiProcessReportRel
            one-to-one relation to PfiProcessReport
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit

        PfiTimerQueueRel
            one-to-one relation to PfiTimerQueue
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = LastActivity

        PfiWorkunitInputDataRel
            one-to-one relation to PfiWorkunitInputData
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit

        PfiErrorMessageRel
            one-to-many relation to PfiErrorMessage
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit

        PfiProcessIndicatorRel
            one-to-one relation to PfiProcessIndicator
            Field Mapping uses symbolic key
                related.PfiProcessIndicator = PfiWorkunit

        PfiWorkunitByCloseDateIsHistoryRel
            one-to-many relation to PfiWorkunit
            Field Mapping uses ByCloseDateWorkunitWhereCompletedOrCanceled
            Instance Selection
                where (related.CloseDate >= LocalBeginDate and related.CloseDate <= LocalEndDate)

        PfiWorkunitByWorkunitIsHistoryRel
            one-to-many relation to PfiWorkunit
            Field Mapping uses ByWorkunitIsHistory
            Instance Selection
                where (related.PfiWorkunit >= LocalBeginWorkunit and related.PfiWorkunit <= LocalEndWorkunit)

        PfiWorkunitRel
            one-to-many relation to PfiWorkunit
            Field Mapping uses symbolic key

        PfiUserActionRoutingRel is a PfiUserActionRouting set

    Sets

        ArchivedWorkunits
            sql name is PfiWU01
            indexed
            Sort Order
                PfiWorkunit
            Instance Selection
                where (Archived)

        LiveWorkunits
            sql name is PfiWU02
            indexed
            Sort Order
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByStatusWorkunitArchived
            sql name is PfiWU03
            indexed
            Sort Order
                Status
                PfiWorkunit
            Instance Selection
                where (Archived)

        ByStatusWorkunitLive
            sql name is PfiWU04
            indexed
            Sort Order
                Status
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByAppsKeyAppsValueWorkunit
            sql name is PfiWU05
            indexed
            Sort Order
                AppsKey
                AppsValue
                PfiWorkunit descending

        ByEventTypeWorkunitIsProcess
            sql name is PfiWU06
            indexed
            Instance Selection
                where (IsProcess)
            Sort Order
                EventType
                PfiWorkunit

        ByWorkunitIsProcess
            sql name is PfiWU07
            indexed
            Instance Selection
                where (IsProcess)
            Sort Order
                PfiWorkunit

        ByWorkunitIsFailed
            sql name is PfiWU08
            indexed
            Instance Selection
                where (IsFailed)
            Sort Order
                PfiWorkunit

        ByWorkunitIsHistory
            sql name is PfiWU09
            indexed
            Instance Selection
                where (IsHistory)
            Sort Order
                PfiWorkunit

        ByWorkunitIsInProgress
            sql name is PfiWU10
            indexed
            Instance Selection
                where (IsInProgress)
            Sort Order
                PfiWorkunit

        ByServDefWorkunit
            sql name is PfiWU11
            indexed
            Sort Order
                ServiceDefinition
                PfiWorkunit

        ByEventTypeWorkunit
            sql name is PfiWU13
            indexed
            Sort Order
                EventType
                PfiWorkunit descending

        ByServDefFlowDefFlowVersionWorkunit
            sql name is PfiWU14
            indexed
            Sort Order
                ServiceDefinition
                FlowDefinition
                FlowVersion
                PfiWorkunit

        ByAppsValueWorkunit
            sql name is PfiWU15
            indexed
            Sort Order
                AppsValue
                PfiWorkunit

        ByDataAreaWorkunitLive
            sql name is PfiWU16
            not indexed
            Sort Order
                AppsDataArea
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByDataAreaWorkunitArchived
            sql name is PfiWU17
            not indexed
            Sort Order
                AppsDataArea
                PfiWorkunit
            Instance Selection
                where (Archived)

        ByWorkTitleWorkunitLive
            sql name is PfiWU18
            indexed
            Sort Order
                WorkTitle
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByWorkTitleWorkunitArchived
            sql name is PfiWU19
            indexed
            Sort Order
                WorkTitle
                PfiWorkunit
            Instance Selection
                where (Archived)

        ByServDefWorkunitLive
            sql name is PfiWU20
            not indexed
            Sort Order
                ServiceDefinition
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByServDefWorkunitArchived
            sql name is PfiWU21
            not indexed
            Sort Order
                ServiceDefinition
                PfiWorkunit
            Instance Selection
                where (Archived)

        ByStartDateWorkunitLive
            sql name is PfiWU22
            not indexed
            Sort Order
                StartDate
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByStartDateWorkunitArchived
            sql name is PfiWU23
            not indexed
            Sort Order
                StartDate
                PfiWorkunit
            Instance Selection
                where (Archived)

        ByParentWorkunitWorkunit
            sql name is PfiWU24
            indexed
            Sort Order
                ParentWorkunit
                PfiWorkunit

        ByFlowDefWorkunitLive
            sql name is PfiWU25
            not indexed
            Sort Order
                FlowDefinition
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByActorWorkunitLive
            sql name is PfiWU26
            not indexed
            Sort Order
                Actor
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByClassicWorkunit
            sql name is PfiWU27
            indexed
            Sort Order
                ClassicWorkunit
                PfiWorkunit
            Instance Selection
                where (ClassicWorkunitEntered)

        ByExecutionStartDateWorkunitLive
            sql name is PfiWU28
            not indexed
            Sort Order
                ExecutionStartDate
                PfiWorkunit
            Instance Selection
                where (not Archived)

        ByCloseDateWorkunitWhereCompletedOrCanceled
            sql name is PfiWU29
            indexed
            Sort Order
                CloseDate
                PfiWorkunit
            Instance Selection
                where (IsHistory)

        ByFlowDefinitionCloseDateWhereCompletedandNotPruned
            sql name is PfiWU30
            indexed
            Sort Order
                FlowDefinition
                CloseDate
                PfiWorkunit
            Instance Selection
                where (CanPrune)

        ByCloseDate
            sql name is PfiWU31
            indexed
            Sort Order
                CloseDate
                PfiWorkunit

        ByLogStatusLogDownloadedDate
            sql name is PfiWU32
            indexed
            Sort Order
                LogStatus
                LogDownloadedTimestamp
                PfiWorkunit
            Instance Selection
                where (CanMoveLog)

        ByLogStatusLogUploadedDate
            sql name is PfiWU33
            indexed
            Sort Order
                LogStatus
                LogUploadedTimestamp
                PfiWorkunit
            Instance Selection
                where (CanMoveLog)

        ByIsReplicatedUniqueID
            sql name is PfiWU34
            indexed
            Sort Order
                IsReplicated
                UniqueID

    Delete Rules
        for each PfiQueueTaskRel
            invoke Delete each

    Actions

        Create is a Create Action

            Entrance Rules
                if (not PfiActorRel.exists)
                    Actor = AuthenticatedActor
            Action Rules
                RunId = 1
                RestartCount = FlowDefinition.RestartCount
                KeepWorkUnitActive = FlowDefinition.KeepWorkUnitActive
                IsAutoRestart = FlowDefinition.IsAutoRestartValid
            Exit Rules
                if (ParentWorkunit entered)
                    invoke CopyVariables PfiWorkunitVariable in foreground
                        invoked.SourceWorkUnit      = ParentWorkunit
                        invoked.DestinationWorkUnit = PfiWorkunit
                    invoke CopyFolders PfiWorkunitFolder in foreground
                        invoked.SourceWorkUnit      = ParentWorkunit
                        invoked.DestinationWorkUnit = PfiWorkunit
                    if (ParentWorkunit.PfiWorkunitInputDataRel exists)
                        invoke CopyInputData ParentWorkunit.PfiWorkunitInputDataRel
                            invoked.DestinationWorkUnit = PfiWorkunit
                if (Status.Ready)
                    invoke QueueWorkUnit

        Update is an Update Action

            Exit Rules

                if (!old Status.Ready and Status.Ready)
                    invoke QueueWorkUnit
                IsReplicated = false
                
                if (Status.Completed or Status.Canceled)
                	AutoRestartCount = 0
                
		AutoRestart is an Instance Action
			restricted
			Entrance Rules
				AutoRestartCount = AutoRestartCount + 1
			Action Rules
				invoke Restart        

        Restart is an Instance Action

            Local Fields
                WorkunitStatus is a PfiWorkunitStatus

            Action Rules
                constraint (EventType = "EVT_PFLOW" or EventType = "EVT_MULTI")
                    "InvalidEventTypeOnWorkUnit"
                constraint (Status != WorkunitStatus.Processing)
                    "CannotRestartWorkUnitsThatAreInProgress"

			    if(AppsKey = "ACTIONREQUEST")
				    constraint(CanRestartActionRequestWorkUnit)
                	    "ThisWorkUnitCannotBeRestartedBecauseItWasCreatedByAnActionRequestWhichIsAlreadyCompleteOrRejected\.PleaseCreateANewActionRequestInstead."
                    confirmation required
                	    "ThisIsAnActionRequestWorkUnit.DoYouReallyWantToContinue?"

                if (EventType = "EVT_PFLOW")
                    CloseDate = blank
                    Status = WorkunitStatus.Ready
                    if (RunId > 0 or not ErrorMessageExists)
                        RunId = RunId + 1

                    invoke QueueWorkUnit
                    invoke UpdateParentStatus
                else
                if (EventType = "EVT_MULTI")
                    Status = WorkunitStatus.Ready
                    CloseDate = blank
                    if (RunId > 0 or not ErrorMessageExists)
                        RunId = RunId + 1
                    for each PfiWorkunitChildrenRel
                        invoke Update each
                            invoked.CloseDate = blank
                            invoked.Status = Status.Ready
                            if (invoked.RunId > 0 or not ErrorMessageExists)
                                invoked.RunId = RunId + 1

            Exit Rules
                invoke UpdateQueueTaskIsCurrentActionWaitingSnapshot
                IsReplicated = false
                WorkunitVarsCleaned = false 
                invoke LogWorkUnitRestart

        DownloadLogFile is an Instance Action
            restricted

        ClearCheckpoint is an Instance Action
            valid when (CheckpointExists)
            Action Rules
                Checkpoint = -1
            Exit Rules
                IsReplicated = false

        Proceed is an Instance Action
            default label is "ProceedNow"
            manual update

        EnableLogging is an Instance Action
            valid when (IsHoldingThreadButNotCanceling)

        DisableLogging is an Instance Action
            valid when (IsHoldingThreadButNotCanceling)

        SetNonActiveWorkUnitsToFailed is a Set Action
            run in foreground
            no records message is "ActionSubmittedSuccessfully"

            Instance Selection
                where (false)

            Action Rules

                Empty Set Rules
                    invoke SetNonActiveWorkUnitsToFailed PfiWorkunitUtils in background

        SendCancelMessageToMingle is an Instance Action
            default label is "SendCancelMessageToO\S"

        SendCancelMessageToServer is an Instance Action
            valid when (IsCanceling)

        Terminate is an Instance Action
            restricted

            Action Rules
                invoke Cancel
                Status = Status.Failed

            Exit Rules
                IsReplicated = false

        Cancel is an Instance Action
            valid when (WorkUnitCanBeCanceled)
            confirmation required
                "<CancelMessage>"
            completion message is "CancelStatusSet;InitiatedServerRequest"

            Local Fields
                WorkunitStatus is a PfiWorkunitStatus

            Action Rules

                constraint (EventType = "EVT_PFLOW" or EventType = "EVT_MULTI")
                    "InvalidEventTypeOnWorkUnit"

                if (WorkUnitCanBeCanceled)
                    if (EventType = "EVT_PFLOW")
                        if (Status.Processing and not IsActionPending and not IsActionPendingInServer)
                            Status =  WorkunitStatus.Canceling
                        else
                            Status = WorkunitStatus.Canceled

                        if ((MingleInboxId > 0 or NotificationCenterStatus = 2) and IsActionPending)
                            invoke SendCancelMessageToMingle
                        if (CloseDate = blank)
                            CloseDate = system current timestamp
                        include UpdateParentStatusRuleBlock
                        if (PfiLastActivityRel exists)
                            invoke Cancel PfiLastActivityRel
                    else
                    if (EventType = "EVT_MULTI")
                        for each PfiWorkunitChildrenRel
                            invoke Cancel each

                    if (IsDispatcher3Enabled)
                        invoke Delete PfiAsyncWorkUnitTriggerRel

            Exit Rules
                invoke Create PfiWorkunitInfo
                    invoked.PfiWorkunit = PfiWorkunit
                    invoked.Info = "Work Unit canceled by " + actor
                if (ParentWorkunit entered)
                    invoke UpdateParentStatus
                else
                    invoke UpdateQueueTaskIsCurrentActionWaitingSnapshot
                IsReplicated = false
            	AutoRestartCount = 0
                					
        CancelServerRequest is an Instance Action
            restricted
            manual update

        Release is an Instance Action

            Local Fields
                WorkunitStatus is a PfiWorkunitStatus
                ChildWorkUnit  is a PfiWorkunit view

            Action Rules
                constraint (Status.NotReady)
                    "OnlyWorkUnitsInNotReadyStatusCanBeReleased"
                if (EventType = "EVT_PFLOW")
                    CloseDate = blank
                    Status = Status.Ready
                    invoke QueueWorkUnit
                else
                if (EventType = "EVT_MULTI")
                    Status = Status.Processing
                    if (PfiServiceDefinitionEnabledRel exists)


                        LocalServiceDefinition = ServiceDefinition
                        LocalCriterion1        = Criterion1
                        LocalCriterion2        = Criterion2
                        LocalCriterion3        = Criterion3

                        if (PfiServFlowDefCrit1Crit2Crit3Rel exists)
                            for each PfiServFlowDefCrit1Crit2Crit3Rel
                                include CreateChildWorkUnit
                        else
                        if (PfiServFlowDefCrit1Crit2Rel exists)
                            for each PfiServFlowDefCrit1Crit2Rel
                                include CreateChildWorkUnit
                        else
                        if (PfiServFlowDefCrit1Rel exists)
                            for each PfiServFlowDefCrit1Rel
                                include CreateChildWorkUnit
                        else
                        if (PfiServFlowDefRel exists)
                            for each PfiServFlowDefRel
                                include CreateChildWorkUnit
                        else
                            constraint (false)
                                "NoServiceProcessDefinitionsFoundForCriteriaForServiceDefinition<ServiceDefinition>"
                    else
                        constraint (false)
                            "ServiceDefinition<ServiceDefinition>DoesNotExistOrIsNotEnabled"
                else
                    constraint (false)
                        "InvalidEventTypeOnWorkUnit"

            Exit Rules
                invoke UpdateQueueTaskIsCurrentActionWaitingSnapshot
                IsReplicated = false






        CreateWorkunitByFlow is a Create Action
            default label is "CreateWorkUnitByFlow"
            restricted
            Parameters
                AppsKey         is a PfiAppsKey
                AppsValue       is a PfiAppsValue
                FlowDefinition  is a PfiFlowDefinition
                WorkTitle       is a PfiWorkTitle
                FilterKey       is Alpha size 100 
                FilterValue     is a PfiFilterValue
                SourceType      is a PfiSourceType
                Source          is a PfiSource
                ClassicWorkunit is a PfiClassicWorkunit
                    default label is "ClassicWorkUnit"

            Local Fields
                Status is a PfiWorkunitStatus

            Parameter Rules
                FlowDefinition
                    required
                        "ProcessDefinitionIsRequired"
                    constraint (FlowDefinition.IsFlowEnabled)
                        "ProcessDefinition<FlowDefinition>IsNotEnabled"
                WorkTitle
                    required

            Action Rules
                invoke Create this instance
                    invoked.AppsKey         = AppsKey
                    invoked.AppsValue       = AppsValue
                    invoked.EventType       = "EVT_PFLOW"
                    invoked.FlowDefinition  = FlowDefinition
                    invoked.FlowVersion     = FlowDefinition.CurrentFlowVersion
                    invoked.Actor           = actor
                    invoked.WorkTitle       = WorkTitle
                    invoked.PfiFilterKey    = FilterKey
                    invoked.FilterValue     = FilterValue
                    invoked.WorkPriority    = 50
                    invoked.StartDate       = system current timestamp
                    invoked.Status          = Status.NotReady
                    invoked.SourceType      = SourceType
                    invoked.Source          = Source
                    invoked.ParentWorkunit  = ""
                    invoked.ClassicWorkunit = ClassicWorkunit
                	invoked.RestartCount 	= FlowDefinition.RestartCount
                	invoked.KeepWorkUnitActive = FlowDefinition.KeepWorkUnitActive
                	invoked.IsAutoRestart 	= FlowDefinition.IsAutoRestartValid                    






        CreateWorkunitByService is a Create Action
            default label is "CreateWorkUnitByService"
            restricted
            Parameters
                AppsKey           is a PfiAppsKey
                AppsValue         is a PfiAppsValue
                PrmServiceDefinition is a PfiServiceDefinition
                    default label is "Service"
                WorkTitle         is a PfiWorkTitle
                FilterKey         is Alpha size 100 
                FilterValue       is a PfiFilterValue
                Criterion1        is a PfiCriterion
                Criterion2        is a PfiCriterion
                Criterion3        is a PfiCriterion
                SourceType        is a PfiSourceType
                Source            is a PfiSource
                ClassicWorkunit   is a PfiClassicWorkunit
                    default label is "ClassicWorkUnit"

            Local Fields
                Status is a PfiWorkunitStatus

            Parameter Rules
                PrmServiceDefinition
                    required
                        "ServiceDefinitionIsRequired"
                    constraint (PrmServiceDefinition.IsServiceEnabled)
                        "ServiceDefinition<PrmServiceDefinition>IsNotEnabled"
                WorkTitle
                    required

            Action Rules




                LocalServiceDefinition = PrmServiceDefinition
                LocalCriterion1        = Criterion1 
                LocalCriterion2        = Criterion2
                LocalCriterion3        = Criterion3

                constraint (PfiServFlowDefRel exists
                or          PfiServFlowDefCrit1Rel exists
                or          PfiServFlowDefCrit1Crit2Rel exists
                or          PfiServFlowDefCrit1Crit2Crit3Rel exists)
                    "NoServiceDefinitionProcessesFoundForCriteriaForService<PrmServiceDefinition>"

                invoke Create this instance
                    invoked.AppsKey           = AppsKey
                    invoked.AppsValue         = AppsValue
                    invoked.ServiceDefinition = PrmServiceDefinition
                    invoked.Actor             = actor
                    invoked.WorkTitle         = WorkTitle
                    invoked.PfiFilterKey      = FilterKey
                    invoked.FilterValue       = FilterValue
                    invoked.WorkPriority      = 50
                    invoked.StartDate         = system current timestamp
                    invoked.Status            = Status.NotReady
                    invoked.Criterion1        = Criterion1
                    invoked.Criterion2        = Criterion2
                    invoked.Criterion3        = Criterion3
                    invoked.SourceType        = SourceType
                    invoked.Source            = Source
                    invoked.ClassicWorkunit   = ClassicWorkunit

                if (PfiServFlowDefCrit1Crit2Crit3Rel exists)
                    if (instance count of PfiServFlowDefCrit1Crit2Crit3Rel = 1)
                        EventType      = "EVT_PFLOW"
                        FlowDefinition = first PfiServFlowDefCrit1Crit2Crit3Rel.FlowDefinition
                        FlowVersion    = first PfiServFlowDefCrit1Crit2Crit3Rel.FlowDefinition.CurrentFlowVersion
                    else
                        EventType      = "EVT_MULTI"
                        FlowDefinition = blank
                        FlowVersion    = blank
                else
                if (PfiServFlowDefCrit1Crit2Rel exists)
                    if (instance count of PfiServFlowDefCrit1Crit2Rel = 1)
                        EventType      = "EVT_PFLOW"
                        FlowDefinition = first PfiServFlowDefCrit1Crit2Rel.FlowDefinition
                        FlowVersion    = first PfiServFlowDefCrit1Crit2Rel.FlowDefinition.CurrentFlowVersion
                    else
                        EventType      = "EVT_MULTI"
                        FlowDefinition = blank
                        FlowVersion    = blank
                else
                if (PfiServFlowDefCrit1Rel exists)
                    if (instance count of PfiServFlowDefCrit1Rel = 1)
                        EventType      = "EVT_PFLOW"
                        FlowDefinition = first PfiServFlowDefCrit1Rel.FlowDefinition
                        FlowVersion    = first PfiServFlowDefCrit1Rel.FlowDefinition.CurrentFlowVersion
                    else
                        EventType      = "EVT_MULTI"
                        FlowDefinition = blank
                        FlowVersion    = blank
                else
                if (PfiServFlowDefRel exists)
                    if (instance count of PfiServFlowDefRel = 1)
                        EventType      = "EVT_PFLOW"
                        FlowDefinition = first PfiServFlowDefRel.FlowDefinition
                        FlowVersion    = first PfiServFlowDefRel.FlowDefinition.CurrentFlowVersion
                    else
                        EventType      = "EVT_MULTI"
                        FlowDefinition = blank
                        FlowVersion    = blank
                        
				if (FlowDefinition != blank)
                	RestartCount = FlowDefinition.RestartCount
                	KeepWorkUnitActive = FlowDefinition.KeepWorkUnitActive
                	IsAutoRestart = FlowDefinition.IsAutoRestartValid					
					




        MoveWorkunitToHistory is an Instance Action
            default label is "MoveWorkUnitToHistory"

            Local Fields
                WorkunitStatus is a PfiWorkunitStatus

            Action Rules
                constraint (CanDeleteOrMove)
                    "CannotMoveWorkUnitToHistoryUnlessProcessingIsCompleteOrCancellationIsComplete"
                if (PfiWorkunitChildrenRel exists)
                    for each PfiWorkunitChildrenRel
                        invoke MoveWorkunitToHistory each
                Archived = true
                IsReplicated = false





        ScheduleMoveToWorkUnitHistory is a Set Action
            run in background
            completion message is "TheSelectedWorkUnitsHaveBeenMovedToHistory"

            Parameters
                BeginDate       is TimeStamp
                EndDate         is TimeStamp
                OffsetDays      is Numeric size 3
                BeginOffsetDays is Numeric size 3
                BeginWorkunit   is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit     is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName     is a PfiServiceDefinition
                FlowName        is a PfiFlowDefinition

            Local Fields
                CalculatedBeginDate is TimeStamp
                CalculatedEndDate   is TimeStamp

            Parameter Rules
                BeginDate
                    constraint (BeginDate = blank or EndDate != blank)
                        "CannotSpecifyABeginDateWithoutAnEndDate"
                EndDate
                    constraint (EndDate = blank or BeginDate != blank)
                        "CannotSpecifyAnEndDateWithoutABeginDate"
                ServiceName
                    constraint (ServiceName = blank or FlowName = blank)
                        "CannotEnterBothServiceNameAndProcessName"
                OffsetDays
                    constraint (OffsetDays not entered or EndDate not entered)
                        "CannotEnterBothEndDateAndDaysPriorToCurrentDate"

                    if (OffsetDays entered)
                        CalculatedEndDate = (system current timestamp - OffsetDays as days)
                        if (BeginDate entered)
                            constraint (CalculatedEndDate >= BeginDate)
                                "DaysPriorToCurrentDateMustResultInADateEqualToOrGreaterThanBeginDate"

                BeginOffsetDays
                    constraint (BeginOffsetDays not entered or OffsetDays entered)
                        "CannotEnterBeginDaysWithoutEndDays"

                    constraint (BeginOffsetDays not entered or StartDate not entered)
                        "CannotEnterBothEndDateAndBeginDays"

                    if (BeginOffsetDays entered)
                        CalculatedBeginDate = (system current timestamp - BeginOffsetDays as days)
                        constraint (BeginOffsetDays >= OffsetDays)
                            "BeginDaysCannotBeLessThanEndDays"

                BeginWorkunit
                    constraint (BeginWorkunit not entered or EndWorkunit entered)
                        "CannotSpecifyABeginWorkUnitWithoutAnEndWorkUnit"

                EndWorkunit
                    constraint (EndWorkunit not entered or BeginWorkunit entered)
                        "CannotSpecifyAnEndWorkUnitWithoutABeginWorkUnit"

                    constraint (EndWorkunit >= BeginWorkunit)
                        "BeginWorkUnitCannotBeGreaterThanEndWorkUnit"

                    if ((BeginDate entered or EndDate entered) and (BeginWorkunit entered or EndWorkunit entered))
                        constraint (false)
                            "CannotSpecifyBothDateRangeAndWorkUnitRange"

                    if (not BeginDate entered and not BeginWorkunit entered)
                        constraint (false)
                            "ADateRangeOrAWorkUnitRangeMustBeSpecified"

            Instance Selection
                where (false)

            Action Rules

                Empty Set Rules
                    if (OffsetDays entered)
                        EndDate = CalculatedEndDate
                    if (BeginOffsetDays entered)
                        BeginDate = CalculatedBeginDate

                    if (EndDate entered and BeginWorkunit not entered)
                        invoke UpdateAllWorkunitsByDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (BeginWorkunit entered and EndDate not entered)
                        invoke UpdateAllWorkunitsByWorkunitRange
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (BeginWorkunit entered and EndDate entered)
                        invoke UpdateAllWorkunitsByWorkunitAndDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName





        UpdateAllWorkunitsByDateRange is a Set Action
            restricted
            run in background
            default label is "UpdateAllWorkUnitsByDateRange"

            Parameters
                BeginDate   is TimeStamp
                EndDate     is TimeStamp
                ServiceName is a PfiServiceDefinition
                FlowName    is a PfiFlowDefinition

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    CanDeleteOrMove and not Archived)

            Action Rules

                Instance Rules
                    invoke MoveWorkunitToHistory





        UpdateAllWorkunitsByWorkunitRange is a Set Action
            restricted
            run in background
            default label is "UpdateAllWorkUnitsByWorkUnitRange"

            Parameters
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition

            Instance Selection
                where (((PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                or      ((Status = Status.NotReady)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    CanDeleteOrMove and not Archived)

            Action Rules

                Instance Rules
                    invoke MoveWorkunitToHistory

        UpdateAllWorkunitsByWorkunitAndDateRange is a Set Action
            restricted
            run in background
            default label is "UpdateAllWorkUnitsByWorkUnitAndDateRange"

            Parameters
                BeginDate     is TimeStamp
                EndDate       is TimeStamp
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and     (PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    CanDeleteOrMove and not Archived)

            Action Rules

                Instance Rules
                    invoke MoveWorkunitToHistory





        Delete is a Delete Action
            default label is "DeleteSelectedWorkUnits"
            valid when (CanDeleteOrMove)

            Local Fields
                CanDeleteErrorMessage is Boolean
                CanDeleteInputData    is Boolean

            Action Rules
                constraint (CanDeleteOrMove)
                    "CannotDeleteWorkUnitUnlessNotReadyOrProcessingIsCompleteOrCancellationIsComplete"

                constraint (NoActionRequest)
                    "CannotDeleteWorkUnitWhenLinkedToAnActionRequest"

                if (RunId > 0 and ErrorMessageExists)
                    CanDeleteErrorMessage = true

                if (InputDataExists)
                    CanDeleteInputData = true

                if (PfiWorkunitChildrenRel exists)
                    for each PfiWorkunitChildrenRel
                        invoke Delete each

            Entrance Rules
                if (PfiProcessIndicatorRel.exists)
                    invoke Delete PfiProcessIndicatorRel
				invoke DeleteChildren
            Exit Rules
                if (CanDeleteAmazonS3Log or CanDeleteInputData or CanDeleteErrorMessage)
                    invoke Create PfiAmazonS3DeleteQueue
                        fill in fields from this instance
                        invoked.PfiAmazonS3DeleteQueue.WorkUnit = PfiWorkunit
                        if (CanDeleteInputData)
                            invoked.InputData =  true
                        if (CanDeleteAmazonS3Log)
                            invoked.Log = true
                        if (CanDeleteErrorMessage)
                            invoked.ErrorMessage = true

        DeleteChildren is an Instance Action
            restricted
            manual update
            
        PruneChildren is an Instance Action
            restricted
            manual update

        PruneNonUAActivities is an Instance Action
            restricted
            manual update

        PruneUAActivities is an Instance Action
            restricted
            manual update                              




        ScheduleWorkUnitDeletion is a Set Action
            run in background
            completion message is "TheSelectedWorkUnitsHaveBeenDeleted"

            Parameters
                BeginDate       is TimeStamp
                EndDate         is TimeStamp
                OffsetDays      is Numeric size 3
                BeginOffsetDays is Numeric size 3
                BeginWorkunit   is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit     is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName     is a PfiServiceDefinition
                FlowName        is a PfiFlowDefinition
                Options         is Numeric size 1
                    States
                        Historical value is 1
                        Current    value is 2
                        Both       value is 3

            Local Fields
                CalculatedBeginDate is TimeStamp
                CalculatedEndDate   is TimeStamp

            Parameter Rules

                Options
                    default to Options.Both
                    initial value is Options.Both

                BeginDate
                    constraint (BeginDate not entered or (EndDate entered or OffsetDays entered))
                        "CannotSpecifyABeginDateWithoutAnEndDateOrDaysPriorToCurrentDate"

                    constraint (EndDate not entered or BeginDate <= EndDate)
                        "BeginDateCannotBeAfterEndDate"

                OffsetDays
                    constraint (OffsetDays not entered or EndDate not entered)
                        "CannotEnterBothEndDateAndDaysPriorToCurrentDate"

                    if (OffsetDays entered)
                        CalculatedEndDate = (system current timestamp - OffsetDays as days)
                        if (BeginDate entered)
                            constraint (CalculatedEndDate >= BeginDate)
                                "DaysPriorToCurrentDateMustResultInADateEqualToOrGreaterThanBeginDate"

                BeginOffsetDays
                    constraint (BeginOffsetDays not entered or OffsetDays entered)
                        "CannotEnterBeginDaysWithoutEndDays"

                    constraint (BeginOffsetDays not entered or StartDate not entered)
                        "CannotEnterBothEndDateAndBeginDays"

                    if (BeginOffsetDays entered)
                        CalculatedBeginDate = (system current timestamp - BeginOffsetDays as days)
                        constraint (BeginOffsetDays >= OffsetDays)
                            "BeginDaysCannotBeLessThanEndDays"

                BeginWorkunit
                    constraint (BeginWorkunit not entered or EndWorkunit entered)
                        "CannotSpecifyABeginWorkUnitWithoutAnEndWorkUnit"

                EndWorkunit
                    constraint (EndWorkunit not entered or BeginWorkunit entered)
                        "CannotSpecifyAnEndWorkUnitWithoutABeginWorkUnit"

                    constraint (EndWorkunit >= BeginWorkunit)
                        "BeginWorkUnitCannotBeGreaterThanEndWorkUnit"

                    if ((BeginDate entered or EndDate entered) and (BeginWorkunit entered or EndWorkunit entered))
                        constraint (false)
                            "CannotSpecifyBothDateRangeAndWorkUnitRange"

                    if (not BeginDate entered and not BeginWorkunit entered)
                        constraint (false)
                            "ADateRangeOrAWorkUnitRangeMustBeSpecified"

                ServiceName
                    constraint (ServiceName = blank or FlowName = blank)
                        "CannotEnterBothServiceNameAndProcessName"

            Instance Selection
                where (false)

            Action Rules
                Empty Set Rules
                    if (OffsetDays entered)
                        EndDate = CalculatedEndDate
                    if (BeginOffsetDays entered)
                        BeginDate = CalculatedBeginDate

                    if (EndDate entered and BeginWorkunit not entered and Options = Options.Both)
                        invoke DeleteAllWorkunitsByDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (BeginWorkunit entered and EndDate not entered and Options = Options.Both)
                        invoke DeleteAllWorkunitsByWorkunitRange
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (BeginWorkunit entered and EndDate entered and Options = Options.Both)
                        invoke DeleteAllWorkunitsByWorkunitAndDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (EndDate entered and BeginWorkunit not entered and Options = Options.Historical)
                        invoke DeleteWorkunitsByDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.IsArchived              = true

                    if (BeginWorkunit entered and EndDate not entered and Options = Options.Historical)
                        invoke DeleteWorkunitsByWorkunitRange
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.IsArchived              = true

                    if (BeginWorkunit entered and EndDate entered and Options = Options.Historical)
                        invoke DeleteWorkunitsByWorkunitAndDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.IsArchived              = true

                    if (EndDate entered and BeginWorkunit not entered and Options = Options.Current)
                        invoke DeleteWorkunitsByDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.IsArchived              = false

                    if (BeginWorkunit entered and EndDate not entered and Options = Options.Current)
                        invoke DeleteWorkunitsByWorkunitRange
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.IsArchived              = false

                    if (BeginWorkunit entered and EndDate entered and Options = Options.Current)
                        invoke DeleteWorkunitsByWorkunitAndDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.IsArchived              = false





        ScheduleNonUAWorkUnitDeletion is a Set Action
            run in background
            completion message is "TheSelectedNonUAWorkUnitsHaveBeenDeleted"

            Parameters
                OffsetDays is Numeric size 3
            Local Fields
                CalculatedBeginDate is TimeStamp

            Parameter Rules
                OffsetDays
                    initial value is 90
                    constraint (OffsetDays entered)
                        "OffsetDaysCannotBeEmpty"
                    if (OffsetDays entered)
                        CalculatedBeginDate = (system current timestamp - OffsetDays as days)

            Instance Selection
                where (((CloseDate <= CalculatedBeginDate)
                    or      ((Status = Status.NotReady)
                    and     (StartDate <= CalculatedBeginDate)))
                    and    CanDeleteOrMove
                    and    NoActionRequest
                    and    IsNonUAWorkUnit)

            Action Rules
                Instance Rules
                    invoke Delete





        DeleteAllWorkunitsByDateRange is a Set Action
            restricted
            run in background
            default label is "DeleteAllWorkUnitsByDateRange"

            Parameters
                BeginDate   is TimeStamp
                EndDate     is TimeStamp
                ServiceName is a PfiServiceDefinition
                FlowName    is a PfiFlowDefinition

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    CanDeleteOrMove
                and    NoActionRequest
                and (ParentWorkunit = 0 or FlowName!=""))

            Action Rules

                Instance Rules
                    invoke Delete





        DeleteAllWorkunitsByWorkunitRange is a Set Action
            restricted
            run in background
            default label is "DeleteAllWorkUnitsByWorkUnitRange"

            Parameters
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition

            Instance Selection
                where (((PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                or      ((Status = Status.NotReady)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    CanDeleteOrMove
                and    NoActionRequest
                and (ParentWorkunit = 0 or FlowName!=""))

            Action Rules

                Instance Rules
                    invoke Delete





        DeleteAllWorkunitsByWorkunitAndDateRange is a Set Action
            restricted
            run in background
            default label is "DeleteAllWorkUnitsByWorkUnitAndDateRange"

            Parameters
                BeginDate     is TimeStamp
                EndDate       is TimeStamp
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and     (PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    CanDeleteOrMove
                and    NoActionRequest
                and (ParentWorkunit = 0 or FlowName !="" ))

            Action Rules

                Instance Rules
                    invoke Delete





        DeleteWorkunitsByDateRange is a Set Action
            restricted
            run in background
            default label is "DeleteWorkUnitsByDateRange"

            Parameters
                BeginDate   is TimeStamp
                EndDate     is TimeStamp
                ServiceName is a PfiServiceDefinition
                FlowName    is a PfiFlowDefinition
                IsArchived  is Boolean
                    default label is "Archived"

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    (CanDeleteOrMove)
                and    (NoActionRequest)
                and    Archived = IsArchived
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Delete





        DeleteWorkunitsByWorkunitRange is a Set Action
            restricted
            run in background
            default label is "DeleteWorkUnitsByWorkUnitRange"

            Parameters
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition
                IsArchived    is Boolean
                    default label is "Archived"

            Instance Selection
                where (((PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                or      ((Status = Status.NotReady)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    (CanDeleteOrMove)
                and    (NoActionRequest)
                and    Archived = IsArchived
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Delete





        DeleteWorkunitsByWorkunitAndDateRange is a Set Action
            restricted
            run in background
            default label is "DeleteWorkUnitsByWorkUnitAndDateRange"

            Parameters
                BeginDate     is TimeStamp
                EndDate       is TimeStamp
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition
                IsArchived    is Boolean
                    default label is "Archived"

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and     (PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    (CanDeleteOrMove)
                and    (NoActionRequest)
                and     Archived = IsArchived
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Delete

        UpdateParentStatus is an Instance Action
            restricted
            manual update

        CreateComparisonWorkunit is an Instance Action
            default label is "CreateComparisonWorkUnit"

            Action Rules
                invoke Create PfiComparisonWorkunit
                    fill in fields from this instance
                    invoked.Actor = actor    

        UpdateQueueTaskIsCurrentActionWaitingSnapshot is an Instance Action
            default label is untranslatable
            restricted

            Action Rules
                for each PfiQueueTaskRel
                    invoke BuildIsCurrentActionWaitingSnapshot each










        DeleteActivitiesAndVariables is an Instance Action
            default label is "DeleteSelectedActivitiesAndVariables"

            Action Rules
                constraint (IsHistory)
                    "CannotDeleteActivitiesAndVariablesUnlessStatusCompletedOrCanceled"
                for each PfiWorkunitChildrenRel
                    if (each.IsHistory)
                        invoke DeleteActivitiesAndVariables PfiActivity
                            invoked.Workunit = each.PfiWorkunit
                invoke DeleteActivitiesAndVariables PfiActivity
                    invoked.Workunit = PfiWorkunit














        ScheduleLogsAndActivitiesDeletion is a Set Action
            run in background

            completion message is "TheSelectedLogsAndActivitiesHaveBeenDeleted"

            Parameters
                ClearLogs             is Boolean
                DeleteActivityAndVars is Boolean
                DeleteWorkunitVars    is Boolean
                    default label is "DeleteWorkUnitVars"
                BeginDate             is TimeStamp
                EndDate               is TimeStamp
                OffsetDays            is Numeric size 3
                BeginOffsetDays       is Numeric size 3
                BeginWorkunit         is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit           is a PfiWorkunit
                    default label is "EndWorkUnit"

            Local Fields
                CalculatedBeginDate is TimeStamp
                CalculatedEndDate   is TimeStamp

            Parameter Rules

                ClearLogs
                    default to false
                    constraint (ClearLogs or DeleteActivityAndVars or DeleteWorkunitVars)
                        "OneOrMoreOptionsMustBeSelected"

                DeleteActivityAndVars
                    default to false

                DeleteWorkunitVars
                    default to false

                BeginDate
                    constraint (BeginDate not entered or (EndDate entered or OffsetDays entered))
                        "CannotSpecifyABeginDateWithoutAnEndDateOrDaysPriorToCurrentDate"

                    constraint (EndDate not entered or BeginDate <= EndDate)
                        "BeginDateCannotBeAfterEndDate"

                OffsetDays
                    constraint (OffsetDays not entered or EndDate not entered)
                        "CannotEnterBothEndDateAndDaysPriorToCurrentDate"

                    if (OffsetDays entered)
                        CalculatedEndDate = (system current timestamp - OffsetDays as days)
                        if (BeginDate entered)
                            constraint (CalculatedEndDate >= BeginDate)
                                "DaysPriorToCurrentDateMustResultInADateEqualToOrGreaterThanBeginDate"

                BeginOffsetDays
                    constraint (BeginOffsetDays not entered or OffsetDays entered)
                        "CannotEnterBeginDaysWithoutEndDays"

                    constraint (BeginOffsetDays not entered or StartDate not entered)
                        "CannotEnterBothEndDateAndBeginDays"

                    if (BeginOffsetDays entered)
                        CalculatedBeginDate = (system current timestamp - BeginOffsetDays as days)
                        constraint (BeginOffsetDays >= OffsetDays)
                            "BeginDaysCannotBeLessThanEndDays"

                BeginWorkunit
                    constraint (BeginWorkunit not entered or EndWorkunit entered)
                        "CannotSpecifyABeginWorkUnitWithoutAnEndWorkUnit"

                EndWorkunit
                    constraint (EndWorkunit not entered or BeginWorkunit entered)
                        "CannotSpecifyAnEndWorkUnitWithoutABeginWorkUnit"

                    constraint (EndWorkunit >= BeginWorkunit)
                        "BeginWorkUnitCannotBeGreaterThanEndWorkUnit"

                    if ((BeginDate entered or EndDate entered) and (BeginWorkunit entered or EndWorkunit entered))
                        constraint (false)
                            "CannotSpecifyBothDateRangeAndWorkUnitRange"

                    if (not BeginDate entered and not BeginWorkunit entered)
                        constraint (false)
                            "ADateRangeOrAWorkUnitRangeMustBeSpecified"

            Instance Selection
                where (false)

            Action Rules

                Empty Set Rules

                    constraint (ClearLogs or DeleteActivityAndVars or DeleteWorkunitVars)
                        "OneOrMoreOptionsMustBeSelected"

                    if (BeginDate entered and BeginOffsetDays entered)
                        constraint (false)
                            "CannotSpecifyBothBeginDateAndBeginDays"

                    if (BeginOffsetDays entered)
                        BeginDate = (system current timestamp - BeginOffsetDays as days)

                    if (EndDate entered and OffsetDays entered)
                        constraint (false)
                            "CannotSpecifyBothEndDateAndEndDays"

                    if (OffsetDays entered)
                        EndDate = (system current timestamp - OffsetDays as days)

                    if ((BeginDate entered and not EndDate entered)
                    or  (not BeginDate entered and EndDate entered))
                        constraint (false)
                            "IncompleteDateRangeSpecified"

                    if (BeginDate entered)
                        constraint (BeginDate <= EndDate)
                            "InvalidDateRangeSpecified"

                    if ((BeginWorkunit entered and not EndWorkunit entered)
                    or  (not BeginWorkunit entered and EndWorkunit entered))
                        constraint (false)
                            "IncompleteWorkUnitRangeSpecified"

                    if (BeginWorkunit entered)
                        constraint (BeginWorkunit <= EndWorkunit)
                            "InvalidWorkUnitRangeSpecified"

                    if ((BeginDate entered or EndDate entered) and (BeginWorkunit entered or EndWorkunit entered))
                        constraint (false)
                            "CannotSpecifyBothDateRangeAndWorkUnitRange"

                    if (not BeginDate entered and not BeginWorkunit entered)
                        constraint (false)
                            "ADateRangeOrAWorkUnitRangeMustBeSpecified"

                    if (BeginDate entered)
                        invoke ClearLogsAndActivitiesByDateRange first PfiWorkunitRel in background
                            invoked.ClearLogs               = ClearLogs
                            invoked.DeleteActivityAndVars   = DeleteActivityAndVars
                            invoked.DeleteWorkunitVars      = DeleteWorkunitVars
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                    else
                    if (BeginWorkunit entered)
                        invoke ClearLogsAndActivitiesByWorkunitRange first PfiWorkunitRel in background
                            invoked.ClearLogs               = ClearLogs
                            invoked.DeleteActivityAndVars   = DeleteActivityAndVars
                            invoked.DeleteWorkunitVars      = DeleteWorkunitVars
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                    else
                        constraint (false)
                            "InvalidParametersSpecified"






        ClearLogsAndActivitiesByDateRange is an Instance Action
            restricted
            run in background

            Parameters
                ClearLogs             is Boolean
                DeleteActivityAndVars is Boolean
                DeleteWorkunitVars    is Boolean
                    default label is "DeleteWorkUnitVars"
                BeginDate             is TimeStamp
                EndDate               is TimeStamp

            Local Fields
                RecordCount is Numeric size 6 

            Entrance Rules
                LocalBeginDate = BeginDate
                LocalEndDate = EndDate

            Action Rules

                RecordCount = 0
                for each(WU) PfiWorkunitByCloseDateIsHistoryRel
                    if (ClearLogs)
                        invoke Update each(WU)
                            each(WU).Log = blank
                            each(WU).LogStatus = LogStatus.Deleted
                            each(WU).PersistentLogSize = 0
                    if (DeleteActivityAndVars)
                        invoke DeleteActivitiesAndActivityVariables each(WU)
                    if (DeleteWorkunitVars)
                        for each(WV) each(WU).PfiWorkunitVariable set
                            invoke Delete each(WV)
                    RecordCount = RecordCount + 1
                    if (RecordCount >= 50)
                        commit transaction
                        RecordCount = 0






        ClearLogsAndActivitiesByWorkunitRange is an Instance Action
            restricted
            run in background
            default label is "ClearLogsAndActivitiesByWorkUnitRange"

            Parameters
                ClearLogs             is Boolean
                DeleteActivityAndVars is Boolean
                DeleteWorkunitVars    is Boolean
                    default label is "DeleteWorkUnitVars"
                BeginWorkunit         is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit           is a PfiWorkunit
                    default label is "EndWorkUnit"

            Local Fields
                RecordCount is Numeric size 6 

            Entrance Rules
                LocalBeginWorkunit = BeginWorkunit
                LocalEndWorkunit = EndWorkunit

            Action Rules

                RecordCount = 0
                for each(WU) PfiWorkunitByWorkunitIsHistoryRel
                    if (ClearLogs)
                        invoke Update each(WU)
                            each(WU).Log = blank
                            each(WU).LogStatus = LogStatus.Deleted
                            each(WU).PersistentLogSize = 0
                    if (DeleteActivityAndVars)
                        invoke DeleteActivitiesAndActivityVariables each(WU)
                    if (DeleteWorkunitVars)
                        for each(WV) each(WU).PfiWorkunitVariable set
                            invoke Delete each(WV)
                    RecordCount = RecordCount + 1
                    if (RecordCount >= 50)
                        commit transaction
                        RecordCount = 0






        DeleteActivitiesAndActivityVariables is an Instance Action
            restricted

            Local Fields
                RecordCount is Numeric size 6 

            Action Rules
                RecordCount = 0
                for each PfiActivity set
                    if (not each.IsUserActionOrHRUserAction)
                        invoke DeleteIncludingComparisonActivities each
                    else
                        invoke DeleteActivityVariablesAndComparisonActivities each
                    RecordCount = RecordCount + 1
                    if (RecordCount >= 50)
                        commit transaction
                        RecordCount = 0










        DeleteWorkunitVariables is an Instance Action
            restricted
            default label is "DeleteWorkUnitVariables"

            Action Rules
                for each PfiWorkunitVariable set
                    invoke Delete each

        UpdateFlowVersionOnActionPendingWorkunit is an Instance Action
            restricted
            default label is "UpdateFlowVersionOnActionPendingWorkUnit"

            Parameters
                PrmFlowDefinition is a PfiFlowDefinition
                    default label is "Process"
                PrmFlowVersion    is a PfiFlowVersion
                    default label is "ProcessVersion"

            Action Rules
                if (PfiLastActivityIsUserActionAndStartedRel.exists)
                    FlowVersion = PrmFlowVersion
                    IsReplicated = false

        TakeAction is an Instance Action
            manual update

            Parameters
                Action  is a PfiActionTaken
                Subject is a PfiComment
                    default label is "Subject_(Optional)"
                Message is Text
                    default label is "Message_(Optional)"

            Action Rules
                constraint (IsInProgress)
                    "CannotTakeActionOnWorkUnitUnlessStatusIsProcessing"
                if (PfiLastActivityRel exists)
                    invoke TakeAction PfiLastActivityRel
                        invoked.Action  = Action
                        invoked.Subject = Subject
                        invoked.Message = Message





        GenerateProcessReportData is a Set Action
            restricted
            run in foreground
            completion message is "ProcessReportDataGenerated"

            Parameters
                BeginDate  is TimeStamp
                EndDate    is TimeStamp
                Process    is a PfiFlowDefinition
                Originator is an Actor

            Parameter Rules
                BeginDate
                    required
                EndDate
                    required

            Instance Selection
                where ((StartDate >= BeginDate)
                and    (StartDate <= EndDate)
                and     ((Process = blank) or (FlowDefinition = Process))
                and     ((Originator = blank) or (Originator = Actor)))

            Action Rules

                Instance Rules
                    if (PfiProcessReportRel.exists)
                        invoke Delete PfiProcessReportRel
                    invoke Create PfiProcessReport
                        invoked.PfiWorkunit       = PfiWorkunit
                        invoked.AppsKey           = AppsKey
                        invoked.AppsValue         = AppsValue
                        invoked.ProcessDefinition = FlowDefinition
                        invoked.ProcessVersion    = FlowVersion
                        invoked.Originator        = Actor
                        invoked.FilterKey         = PfiFilterKey
                        invoked.FilterValue       = FilterValue
                        invoked.StartDate         = StartDate
                        invoked.CloseDate         = CloseDate
                        invoked.Status            = Status
                        invoked.Criterion1        = Criterion1
                        invoked.Criterion2        = Criterion2
                        invoked.Criterion3        = Criterion3
                        invoked.ElapsedTime       = ElapsedTime
                        invoked.KeyField1Name     = KeyField1Name
                        invoked.KeyField1Value    = KeyField1Value
                        invoked.KeyField2Name     = KeyField2Name
                        invoked.KeyField2Value    = KeyField2Value
                        invoked.KeyField3Name     = KeyField3Name
                        invoked.KeyField3Value    = KeyField3Value
                        invoked.KeyField4Name     = KeyField4Name
                        invoked.KeyField4Value    = KeyField4Value
                        invoked.KeyField5Name     = KeyField5Name
                        invoked.KeyField5Value    = KeyField5Value
                        invoked.KeyField6Name     = KeyField6Name
                        invoked.KeyField6Value    = KeyField6Value
                        invoked.KeyField7Name     = KeyField7Name
                        invoked.KeyField7Value    = KeyField7Value
                        invoked.KeyField8Name     = KeyField8Name
                        invoked.KeyField8Value    = KeyField8Value
                        invoked.KeyField9Name     = KeyField9Name
                        invoked.KeyField9Value    = KeyField9Value
                        invoked.KeyField10Name    = KeyField10Name
                        invoked.KeyField10Value   = KeyField10Value





        GenerateTaskReportData is a Set Action
            restricted
            run in foreground
            completion message is "TaskReportDataGenerated"

            Parameters
                BeginDate  is TimeStamp
                EndDate    is TimeStamp
                Process    is a PfiFlowDefinition
                Originator is an Actor

            Parameter Rules
                BeginDate
                    required
                EndDate
                    required

            Instance Selection
                where ((StartDate >= BeginDate)
                and    (StartDate <= EndDate)
                and     ((Process = blank) or (FlowDefinition = Process))
                and     ((Originator = blank) or (Originator = Actor)))

            Action Rules

                Instance Rules
                    for each PfiQueueTaskRel
                        invoke GenerateTaskReportData each





        PruneWorkunit is a Set Action
            restricted
            run in background
            default label is "PruneWorkUnit"

            Parameters
                ProcessName                      is a PfiFlowDefinition
                DaysToRetainBeforePruning        is Numeric size 3
                PruneDate                        is TimeStamp

            Instance Selection
                where (FlowDefinition = ProcessName and CloseDate <= PruneDate and Status = Status.Completed and WorkunitVarsCleaned = false )

            Parameter Rules
                PruneDate
                    PruneDate = system current timestamp - DaysToRetainBeforePruning as days

            Action Rules

                Instance Rules
                    if(ActivityCountNumeric > 0)
	                    if (IsUserActionWorkUnit)
	                    	invoke PruneUAActivities
	                    else
	                    	invoke PruneNonUAActivities
                    invoke PruneChildren
                	Log = blank
                	LogStatus = LogStatus.Deleted
                	PersistentLogSize = 0
                    WorkunitVarsCleaned = true





		EndWorkunit is an Instance Action
            restricted
            default label is "EndWorkUnit"
            Action Rules
            	if (IsPruningChangeDisabled)
            		invoke EndWorkunitOld
            	else
            		invoke EndWorkunitNew
            		
        EndWorkunitNew is an Instance Action
            restricted
            default label is "EndWorkUnitNew"
            Action Rules
                if (FlowDefinition.PruneDefinition.PruneWorkunitData.Immediate)
                    Log = blank
                    if(ActivityCountNumeric > 0)
	                    if (IsUserActionWorkUnit)
	                    	invoke PruneUAActivities
	                    else
	                    	invoke PruneNonUAActivities
                    invoke PruneChildren
                    WorkunitVarsCleaned = true
                    IsReplicated = false

        PruneWorkunitOld is a Set Action
            restricted
            run in background
            default label is "PruneWorkUnitOld"

            Parameters
                ProcessName                      is a PfiFlowDefinition
                DaysToRetainBeforePruning        is Numeric size 3
                PruneDate                        is TimeStamp

            Instance Selection
                where (FlowDefinition = ProcessName and CloseDate <= PruneDate and Status = Status.Completed and WorkunitVarsCleaned = false )

            Parameter Rules
                PruneDate
                    PruneDate = system current timestamp - DaysToRetainBeforePruning as days

            Action Rules

                Instance Rules
                	Log = blank
                	LogStatus = LogStatus.Deleted
                	PersistentLogSize = 0
                    invoke PruneActivities PfiActivity
                        invoked.Workunit = PfiWorkunit
                    for each PfiErrorMessage set
                        invoke Delete each
                    for each PfiProcessReport set
                        invoke Delete each
                    for each PfiComparisonWorkunit set
                        invoke Delete each
                    WorkunitVarsCleaned = true





        EndWorkunitOld is an Instance Action
            restricted
            default label is "EndWorkUnitOld"
            Action Rules
                if (FlowDefinition.PruneDefinition.PruneWorkunitData.Immediate)
                    Log = blank
                    invoke PruneActivities PfiActivity
                        invoked.Workunit = PfiWorkunit
                    for each PfiErrorMessage set
                        invoke Delete each
                    for each PfiProcessReport set
                        invoke Delete each
                    for each PfiComparisonWorkunit set
                        invoke Delete each
                    WorkunitVarsCleaned = true
                    IsReplicated = false







        ResetPruneFlags is a Set Action
            restricted
            run in background

            Action Rules

                Instance Rules
                    WorkunitVarsCleaned = false

        ScheduleWorkUnitCancellation is a Set Action
            run in background
            completion message is "TheSelectedWorkUnitsHaveBeenCancelled"

            Parameters
                BeginDate       is TimeStamp
                EndDate         is TimeStamp
                OffsetDays      is Numeric size 3
                BeginOffsetDays is Numeric size 3
                BeginWorkunit   is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit     is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName     is a PfiServiceDefinition
                FlowName        is a PfiFlowDefinition
                Status         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10
                        All             value is 99

            Local Fields
                CalculatedBeginDate is TimeStamp
                CalculatedEndDate   is TimeStamp

            Parameter Rules

                Status
                    default to Status.Ready
                    initial value is Status.Ready

                BeginDate
                    constraint (BeginDate not entered or (EndDate entered or OffsetDays entered))
                        "CannotSpecifyABeginDateWithoutAnEndDateOrDaysPriorToCurrentDate"

                    constraint (EndDate not entered or BeginDate <= EndDate)
                        "BeginDateCannotBeAfterEndDate"

                OffsetDays
                    constraint (OffsetDays not entered or EndDate not entered)
                        "CannotEnterBothEndDateAndDaysPriorToCurrentDate"

                    if (OffsetDays entered)
                        CalculatedEndDate = (system current timestamp - OffsetDays as days)
                        if (BeginDate entered)
                            constraint (CalculatedEndDate >= BeginDate)
                                "DaysPriorToCurrentDateMustResultInADateEqualToOrGreaterThanBeginDate"

                BeginOffsetDays
                    constraint (BeginOffsetDays not entered or OffsetDays entered)
                        "CannotEnterBeginDaysWithoutEndDays"

                    constraint (BeginOffsetDays not entered or StartDate not entered)
                        "CannotEnterBothEndDateAndBeginDays"

                    if (BeginOffsetDays entered)
                        CalculatedBeginDate = (system current timestamp - BeginOffsetDays as days)
                        constraint (BeginOffsetDays >= OffsetDays)
                            "BeginDaysCannotBeLessThanEndDays"

                BeginWorkunit
                    constraint (BeginWorkunit not entered or EndWorkunit entered)
                        "CannotSpecifyABeginWorkUnitWithoutAnEndWorkUnit"

                EndWorkunit
                    constraint (EndWorkunit not entered or BeginWorkunit entered)
                        "CannotSpecifyAnEndWorkUnitWithoutABeginWorkUnit"

                    constraint (EndWorkunit >= BeginWorkunit)
                        "BeginWorkUnitCannotBeGreaterThanEndWorkUnit"

                    if ((BeginDate entered or EndDate entered) and (BeginWorkunit entered or EndWorkunit entered))
                        constraint (false)
                            "CannotSpecifyBothDateRangeAndWorkUnitRange"

                    if (not BeginDate entered and not BeginWorkunit entered)
                        constraint (false)
                            "ADateRangeOrAWorkUnitRangeMustBeSpecified"

                ServiceName
                    constraint (ServiceName = blank or FlowName = blank)
                        "CannotEnterBothServiceNameAndProcessName"

            Instance Selection
                where (false)

            Action Rules
                Empty Set Rules
                    if (OffsetDays entered)
                        EndDate = CalculatedEndDate
                    if (BeginOffsetDays entered)
                        BeginDate = CalculatedBeginDate

                    if (EndDate entered and BeginWorkunit not entered and Status = Status.All)
                        invoke CancelAllWorkunitsByDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (BeginWorkunit entered and EndDate not entered and Status = Status.All)
                        invoke CancelAllWorkunitsByWorkunitRange
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (BeginWorkunit entered and EndDate entered and Status = Status.All)
                        invoke CancelAllWorkunitsByWorkunitAndDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName

                    if (EndDate entered and BeginWorkunit not entered and Status != Status.All)
                        invoke CancelWorkunitsByDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.CancelStatus            = Status

                    if (BeginWorkunit entered and EndDate not entered and Status != Status.All)
                        invoke CancelWorkunitsByWorkunitRange
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.CancelStatus            = Status

                    if (BeginWorkunit entered and EndDate entered and Status != Status.All)
                        invoke CancelWorkunitsByWorkunitAndDateRange
                            invoked.BeginDate               = BeginDate
                            invoked.EndDate                 = EndDate
                            invoked.BeginWorkunit           = BeginWorkunit
                            invoked.EndWorkunit             = EndWorkunit
                            invoked.ServiceName             = ServiceName
                            invoked.FlowName                = FlowName
                            invoked.CancelStatus            = Status




        CancelWorkunitsByDateRange is a Set Action
            restricted
            run in background
            default label is "CancelWorkUnitsByDateRange"

            Parameters
                BeginDate   is TimeStamp
                EndDate     is TimeStamp
                ServiceName is a PfiServiceDefinition
                FlowName    is a PfiFlowDefinition
                CancelStatus         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    Status = CancelStatus
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Cancel





        CancelWorkunitsByWorkunitRange is a Set Action
            restricted
            run in background
            default label is "CancelWorkUnitsByWorkUnitRange"

            Parameters
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition
                CancelStatus         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10

            Instance Selection
                where (((PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                or      ((Status = Status.NotReady)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    Status = CancelStatus
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Cancel





        CancelWorkunitsByWorkunitAndDateRange is a Set Action
            restricted
            run in background
            default label is "CancelWorkUnitsByWorkUnitAndDateRange"

            Parameters
                BeginDate     is TimeStamp
                EndDate       is TimeStamp
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition
                CancelStatus         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and     (PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and     Status = CancelStatus
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Cancel








        CancelAllWorkunitsByWorkunitAndDateRange is a Set Action
            restricted
            run in background
            default label is "CancelWorkUnitsByWorkUnitAndDateRange"

            Parameters
                BeginDate     is TimeStamp
                EndDate       is TimeStamp
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition
                CancelStatus         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and     (PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    (WorkUnitCanBeCanceled)
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Cancel


        CancelAllWorkunitsByWorkunitRange is a Set Action
            restricted
            run in background
            default label is "CancelWorkUnitsByWorkUnitRange"

            Parameters
                BeginWorkunit is a PfiWorkunit
                    default label is "BeginWorkUnit"
                EndWorkunit   is a PfiWorkunit
                    default label is "EndWorkUnit"
                ServiceName   is a PfiServiceDefinition
                FlowName      is a PfiFlowDefinition
                CancelStatus         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10

            Instance Selection
                where (((PfiWorkunit >= BeginWorkunit and PfiWorkunit <= EndWorkunit)
                or      ((Status = Status.NotReady)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    (WorkUnitCanBeCanceled)
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Cancel


        CancelAllWorkunitsByDateRange is a Set Action
            restricted
            run in background
            default label is "CancelWorkUnitsByDateRange"

            Parameters
                BeginDate   is TimeStamp
                EndDate     is TimeStamp
                ServiceName is a PfiServiceDefinition
                FlowName    is a PfiFlowDefinition
                CancelStatus         is Numeric size 2
                    States
                        NotReady        value is 9
                        Ready           value is 1
                        Processing      value is 2
                        Failed          value is 3
                        Waiting         value is 10

            Instance Selection
                where (((CloseDate >= BeginDate and CloseDate <= EndDate)
                or      ((Status = Status.NotReady)
                and     (StartDate >= BeginDate and StartDate <= EndDate)))
                and    ((not ServiceName entered and not FlowName entered)
                or      (ServiceName entered and ServiceDefinition = ServiceName)
                or      (FlowName entered and FlowDefinition = FlowName))
                and    (WorkUnitCanBeCanceled)
                and (ParentWorkunit = 0 or FlowName !=""))

            Action Rules

                Instance Rules
                    invoke Cancel

        QueueWorkUnit is an Instance Action
            restricted

            Local Fields
                Name is Alpha size 100

            Action Rules

                if (ServiceDefinition != blank)
                    Name = ServiceDefinition
                else
                    Name = FlowDefinition
                if (IsDispatcher3Enabled)
                    invoke Create PfiAsyncWorkUnitTrigger
                        invoked.WorkUnit = PfiWorkunit
                        invoked.Actor = Actor
                        invoked.DataArea = parentcontext.dataarea
                        invoked.FlowDefinition = FlowDefinition
                        invoked.Type = ProcessFlowQueue.Type.Trigger
                else
                    invoke Create ProcessFlowQueue
                        invoked.Name = Name
                        invoked.Title = WorkTitle
                        invoked.Type = ProcessFlowQueue.Type.Trigger
                        invoked.WorkUnitID = PfiWorkunit

            Exit Rules
                IsReplicated = false

        InterruptWorkUnit is an Instance Action
            valid when (Status.Canceling)
            confirmation required
                "WARNING:_\InterruptingAWorkUnitMightLeaveApplicationDataInAnIndeterminateState.AreYouSureYouWantToContinue?"

        LogWorkUnitRestart is an Instance Action
            restricted
