FinanceDimension5Structure is a BusinessClass
	owned by GeneralLedger
	
	prefix is FD5St
	
	Ontology
		symbolic key is FinanceDimension5Structure
		
	Patterns
	
    Persistent Fields
    	Description
		DimensionNode				is a FinanceDimension5
			delete ignored
		StructureSequence			is Numeric 4
		IsEnterpriseStructure		is Boolean
		UnusedDimensionStructure	is Boolean
			restricted
		Active				

   	Context Fields
		FinanceDimension5
		
    Derived Fields
		TopNodeString	    is a StringField
			type is AlphaUpper 15
			restricted
			StructureSequence
			"_TOP_NODE"

		DerivedOldPrefix	is a StringField
			type is AlphaUpper 15
			restricted
			StructureSequence
			"_"
			
		DerivedNewPrefix    is a StringField
			type is AlphaUpper 15
			restricted
			LocalNewStructureSequence
			"_"
			
		DerivedNewDimension   is a DerivedField
			type is like FinanceDimension5
			restricted
			I1 = DerivedOldPrefix size
			I1 += 1
			DerivedNewDimension = DerivedNewPrefix + LocalCopyDimension[I1:15]

		DerivedSystemDimension is a DerivedField
			type is like FinanceDimension5
			if (SystemFinanceDimension5Rel exists)
				return SystemFinanceDimension5Rel.FinanceDimension5
			else
				return UndefinedMsg
									
		UndefinedMsg is a LabelField
			restricted
			"NotDefined"

		InactiveMF is a MessageField			
			restricted
			"Inactive"

		ActiveMF is a MessageField
			restricted
			"Active"
						
		StatusMessage is a ConditionalField
			type is Alpha size 20
			if (Active)
				ActiveMF
			else
				InactiveMF

		IsEnterpriseStructureMF is a MessageField
			restricted
			"Enterprise_Structure"
						
		IsEnterpriseStructureMessage is a ConditionalField
			type is Alpha size 20
			if (IsEnterpriseStructure)
				IsEnterpriseStructureMF
			else
				blank

	Local Fields
		LocalNewStructure	   		is a FinanceDimension5Structure
		LocalCopyDimension        	is a FinanceDimension5
		LocalNewDimension         	is a FinanceDimension5
		LocalNewStructureSequence   is Numeric 4
		LocalParentDimension	    is a FinanceDimension5
		LocalSubordinateDimension 	is like FinanceDimension5
		I1					    	is Numeric 2		
 		LocalDimensionNode			is like FinanceDimension5
		InvokedByUnusedStructure
		LocalRunGroup				is AlphaUpper 30
		LocalCounter				is Numeric 3	
		 
	Relations
		StructureDimensionRel
			one-to-many relation to FinanceDimension5
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.FinanceDimension5	!= StructureDimensionHierarchyRel.FinanceDimension5)
		StructureDimensionHierarchyRel 
			one-to-many relation to FinanceDimension5Hierarchy
			Field Mapping uses ByParentDimension
				related.FinanceEnterpriseGroup	    = FinanceEnterpriseGroup
				related.ParentDimension				= DimensionNode
		DimensionsInStructureRel
			one-to-many relation to FinanceDimension5
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.DimensionStructure			= FinanceDimension5Structure
		EnterpriseDimensionStructureRel
			one-to-many relation to FinanceDimension5Structure
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.IsEnterpriseStructure
			    and    related.UniqueID != UniqueID)
		NotInDimensionStructureRel
			one-to-many relation to FinanceDimension5
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.NotUsedInHierarchy)
		SystemFinanceDimension5Rel
			one-to-one relation to FinanceDimension5Hierarchy
			Field Mapping uses BySystemDimensionInStructure
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension5Structure			= FinanceDimension5Structure
		UploadErrorsRel is a FinanceDimension5Upload set
			Instance Selection
				where (related.ErrorMessage entered)
		RunGroupRel
			one-to-many relation to FinanceDimension5Upload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension5Structure			= FinanceDimension5Structure
			Instance Selection
				where (related.FinanceDimension5Upload.RunGroup = LocalRunGroup)
		ContextDimensionHierarchyRel
			one-to-many relation to FinanceDimension5Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
				related.FinanceDimension5Structure			= FinanceDimension5Structure
				related.FinanceDimension5					= FinanceDimension5

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 7
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
		SecurityGroupMemberRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 7
				related.FinanceDimensionStructure     = FinanceDimension5Structure	
		SecurityGroupsRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 7
				related.FinanceDimensionStructure     = FinanceDimension5Structure
			Instance Selection
				where (related.RequiresRebuild)
		SecurityGroupsNotRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 7
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
			Instance Selection
				where (!related.RequiresRebuild)
														
    Conditions
		UploadRecordsExist
			restricted
			when (FinanceDimension5Upload set exists)
		UploadErrorsExist
			restricted
			when (UploadErrorsRel exists)
		EligibleForExport
			restricted
			when ((!UploadRecordsExist)
			and   (!UnusedDimensionStructure))
		AvailableStructure
			restricted
			when ((!UnusedDimensionStructure)
			and   (!ContextDimensionHierarchyRel exists))
		UsedInASecurityGroup
			when (SecurityGroupMemberRel exists)
		SecurityGroupRequiresRebuild
			when (SecurityGroupsRequiringRebuildRel exists)
												
	Sets
		ByStructureSequence
			Sort Order
				FinanceEnterpriseGroup
				StructureSequence
		





	
		ByEnterpriseStructure
			Sort Order
				FinanceEnterpriseGroup
				IsEnterpriseStructure descending
				FinanceDimension5Structure
		
	Attach Rules
		if (!parentcontext.name = "FinanceDimension5"
		and !parentcontext.name = "FinanceDimension5Upload"
		and !parentcontext.name = "FinanceDimension5Shadow"
		and !parentcontext.name = "FinanceDimension5Hierarchy"
		and !parentcontext.name = "FinanceEnterpriseGroup"
		and !parentcontext.name = "AccountAnalysisSettings"
		and !parentcontext.name = "ReportingBasis")	
			constraint (Active)
				"StructureIsNotActive"
									
    Field Rules
		Active
			if (UnusedDimensionStructure)
				Active = false  
		Description
			required
		StructureSequence
			autosequence using ByStructureSequence
			if (!StructureSequence = old StructureSequence)
				cannot be changed
					"SequenceStructureCannotBeChanged"
		IsEnterpriseStructure
			constraint (Active)
				"EnterpriseDimensionMustBeActive"
			constraint (!UnusedDimensionStructure)
				"TheUnusedDimensionStructureCannotBeTheEnterpriseStructure"				
			constraint (!EnterpriseDimensionStructureRel exists)
				"<first EnterpriseDimensionStructureRel.FinanceDimension5Structure>AlreadyDesignatedAsEnterpriseStructure"

	Rule Blocks
		UpdateFinanceEnterpriseGroupDim5Structure
			if (action type.Create)
				if (IsEnterpriseStructure)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						invoked.PrmEnterpriseDim5Structure = FinanceDimension5Structure
			else								
				if (IsEnterpriseStructure changed)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						if (!IsEnterpriseStructure)
							invoked.PrmClearDim5Structure = true
						else
							invoked.PrmEnterpriseDim5Structure = FinanceDimension5Structure

	Actions
		Create is a Create Action
			Entrance Rules



			Exit Rules
				InvokedByUnusedStructure = true
				
				invoke Create FinanceDimension5
					invoked.FinanceEnterpriseGroup  	= FinanceEnterpriseGroup
					invoked.FinanceDimension5			= TopNodeString
					invoked.DisplayDimension			= "TOP_NODE"
					invoked.DimensionStructure			= FinanceDimension5Structure
					invoked.Description					= Description + " Top Level"

					invoked.DimensionType				= DimensionType.Node
					invoked.SkipHierarchy				= true
					invoked.IsTopNode					= true				
			
				DimensionNode = TopNodeString
				include UpdateFinanceEnterpriseGroupDim5Structure
							
		T2VCreate is a Create Action
			restricted
			default label is untranslatable

		Update is an Update Action
		
			Action Rules
				include UpdateFinanceEnterpriseGroupDim5Structure
				invoke Update DimensionNode
					invoked.Description					= Description + " Top Level"

		Delete is a Delete Action
			confirmation required
				"AreYouSureYouWantToPerformTheFollowingAction:_Delete_Structure?"
			Entrance Rules
				constraint (!IsEnterpriseStructure)
					"CannotDeleteEnterpriseStructure"
				constraint (!UnusedDimensionStructure)
					"CannotDeleteUnusedDimensionStructure"
				constraint (!Active)
					"CannotDeleteActiveStructure"	


				for each FinanceDimension5Hierarchy set		
					increment LocalCounter
					invoke Delete each
					if (LocalCounter = 50)
						commit transaction
						initialize LocalCounter

			Exit Rules
				invoke DeleteAll DimensionsInStructureRel						

		RestrictedDelete is a Delete Action
			restricted
			
			Entrance Rules

			Exit Rules
				invoke DeleteAll DimensionsInStructureRel
										
		UploadHierarchy is an Instance Action
			valid when (UploadRecordsExist)
			completion message is "UploadInitiated"	
			run in background
			
			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"	
			
			Action Rules
				invoke UploadHierarchy FinanceDimension5Upload in background
					invoked.PrmFinanceEnterpriseGroup		= FinanceEnterpriseGroup
					invoked.PrmFinanceDimension5Structure	= FinanceDimension5Structure
					invoked.PrmRunGroup						= PrmRunGroup

		ResetUploadErrors is an Instance Action

			completion message is "ResetInitiated"

			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"
									
			Action Rules
				invoke ResetAllErrors FinanceDimension5Upload in background
					invoked.PrmFinanceEnterpriseGroup  		= FinanceEnterpriseGroup
					invoked.PrmFinanceDimension5Structure	= FinanceDimension5Structure
					invoked.PrmRunGroup						= PrmRunGroup
											
		CopyStructure is an Instance Action
			valid when (!UnusedDimensionStructure)
			run in background
			completion message is "CopyInitiated"		
			
			Parameters
				NewStructure		is like FinanceDimension5Structure
				NewDescription		is like Description
				MakeNewStructureActive is Boolean
							
			Parameter Rules
				NewStructure
					required
					LocalNewStructure = NewStructure
					constraint (!LocalNewStructure exists)
						"DimensionStructureAlreadyExists"
				NewDescription
					required

			Local Fields
				AsyncId			is an AsyncActionRequest
															
			Action Rules
				invoke Create FinanceDimension5Structure
					invoked.FinanceEnterpriseGroup	   = FinanceEnterpriseGroup
					invoked.FinanceDimension5Structure = NewStructure
					invoked.Description			   	   = NewDescription
					invoked.IsEnterpriseStructure      = false
					invoked.Active					   = MakeNewStructureActive

			Exit Rules
				
				invoke CopyDimensions FinanceDimension5Hierarchy in background
					assign async action request id to AsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmStructure	 		  = FinanceDimension5Structure
					invoked.PrmNewStructure	 		  = NewStructure
					
				invoke BuildShadowFile LocalNewStructure in background
					run after AsyncId
									
		BuildShadowFile is an Instance Action
			restricted
					
			Local Fields
				LocalAsyncId				is an AsyncActionRequest
						
			Action Rules
				invoke DeleteShadowForStructure FinanceDimension5Shadow in background
					assign async action request id to LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension5Structure				
					
				invoke BuildShadowFile FinanceDimension5Hierarchy in background
					run after LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension5Structure
					
		BuildShadowFileForDimension is an Instance Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmDimension					is a FinanceDimension5
					default label is "Dimension"
		
			Action Rules
				invoke BuildShadowFile FinanceDimension5Hierarchy in foreground
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = FinanceDimension5Structure
					invoked.PrmDimension			  = PrmDimension
		
		ExportToUpload is an Instance Action


			completion message is "ExportInitiated"
						
			Parameters
				PrmRunGroup					is AlphaUpper 30
					default label is "RunGroup"
				PrmDimensionGroup   		is a FinanceDimension5 group
					default label is "CustomGroup"
				PrmSummaryDimension		    is a FinanceDimension5
					default label is "Summary<FinanceEnterpriseGroup.FinanceDimension5Label>"	
				PrmPostingDimension			is a FinanceDimension5
					default label is "Posting<FinanceEnterpriseGroup.FinanceDimension5Label>"					

			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (!RunGroupRel exists)
						"UploadRecordsAlreadyExistForThisRunGroup"	
									
				PrmDimensionGroup
					constraint (!PrmSummaryDimension entered
					and			!PrmPostingDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
						
				PrmSummaryDimension
					constraint (!PrmDimensionGroup entered
					and         !PrmPostingDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
										
				PrmPostingDimension
					constraint (!PrmDimensionGroup entered
					and         !PrmSummaryDimension entered)
						"CannotBeUsedWithOtherSpecifiedParameters"					
			
			Action Rules
			
				if ((!PrmDimensionGroup entered)
				and (!PrmSummaryDimension entered)
				and (!PrmPostingDimension entered))
					invoke ExportToUpload DimensionNode in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension5Structure	  = FinanceDimension5Structure
						invoked.PrmRunGroup						  = PrmRunGroup			
				else
				if (PrmSummaryDimension entered)
					invoke ExportToUpload PrmSummaryDimension in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension5Structure	  = FinanceDimension5Structure
						invoked.PrmRunGroup						  = PrmRunGroup
				else
				if (PrmPostingDimension entered)														
					invoke ExportToUpload PrmPostingDimension in background
						invoked.PrmFinanceEnterpriseGroup         = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension5Structure	  = FinanceDimension5Structure
						invoked.PrmRunGroup						  = PrmRunGroup
				else																		
					invoke ExportToUpload FinanceDimension5Hierarchy in background
						invoked.PrmFinanceEnterpriseGroup  		  = FinanceEnterpriseGroup
						invoked.PrmFinanceDimension5Structure     = FinanceDimension5Structure
						invoked.PrmRunGroup				   	      = PrmRunGroup	
						invoked.PrmFinanceDimension5Group	   	  = PrmDimensionGroup

		DeleteUploadRecordsForRunGroup is an Instance Action
			valid when (UploadRecordsExist)
			confirmation required
			completion message is "DeleteSubmitted"
			
			Parameters
				PrmRunGroup			is AlphaUpper 30
					default label is "RunGroup"
					
			Parameter Rules
				PrmRunGroup
					required
					
			Action Rules
				invoke DeleteAll FinanceDimension5Upload in background
					invoked.PrmFinanceEnterpriseGroup  	  = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension5Structure = FinanceDimension5Structure
					invoked.PrmRunGroup				   	  = PrmRunGroup
						
		CleanupUnusedDimensions is an Instance Action
			restricted
			
			Action Rules
				InvokedByUnusedStructure = true
				invoke CleanupUnusedDimensions FinanceDimension5 in background
					invoked.PrmFinanceEnterpriseGroup	  = FinanceEnterpriseGroup
					invoked.PrmFinanceDimension5Structure = FinanceDimension5Structure
					
		AddDimensionToUnusedStructure is an Instance Action
			restricted
			
			Parameters
				FinanceDimension5
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<FinanceDimension5Structure>"
					
				InvokedByUnusedStructure = true
				invoke AddPostingDimensionToStructure FinanceDimension5
					invoked.PrmStructure	   = FinanceDimension5Structure
					invoked.PrmParentDimension = DimensionNode
					
		RemoveDimensionFromUnusedStructure is an Instance Action
			restricted
			
			Parameters
				FinanceDimension5
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<FinanceDimension5Structure>"
					
				InvokedByUnusedStructure = true
				invoke RemoveFromStructure FinanceDimension5				
