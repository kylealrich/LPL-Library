ContractRebateQualifierCode is a BusinessClass
    owned by po
    prefix is CRQC

    Ontology
    	symbolic key is ContractRebateQualifierCode    
    		    	
    Persistent Fields
    	ManufacturerInfo			is a Manufacturer  			 
    	CommodityCode   			 	
    	MajorPurchasingClass		 	
    	MinorPurchasingClass		
    	UnspscCode					is an UNSPSCCode
    	ItemCategory				is Numeric size 1
    		States
    			CommodityCode			value is 1
    			ManufacturerInformation	value is 2
				PurchasingClass			value is 3
    			UNSPSCCode				value is 4
    
	Transient Fields
		TransientTransactionAmount			is a CurrencyAmount
		TransientExchangeDate				is an ExchangeDate
		TransientFromCurrency				is a FromCurrency
		TransientContextToCurrency			is a ToCurrency
		TransientCurrencyTable				is a CurrencyTable              
				
    Local Fields
    	LocalManufacturerContract				is Numeric size 15
    	LocalInvoiceDte							is Date
    	LocalCompany							is like Company
		LocalContractRebateDue					is a ContractRebateDue
		MultipleUpdatesForAnInvoiceLine			is Boolean
		LocalHasContractOnRebate				is Boolean
		LocalItemUsedForQualification			is Boolean
		LocalContractCurrencyExchange			is a CurrencyExchange
		LocalInvoiceCurrencyCode				is a Currency
		LocalInvoiceCurrencyExchangeRate		is Decimal 14.7
		LocalCurrencyUnitCost					is an InternationalCost
		LocalMatchedQuantity					is like Quantity
		LocalInvoiceDate						is Date
		LocalCurrencyTable						is like CurrencyTable
		LocalItemNumber							is AlphaUpper size 32
		LocalPayablesCompany					is like PayablesCompany
		LocalPayablesInvoice					is like PayablesInvoice
		LocalPurchaseOrder						is like PurchaseOrder
		LocalPurchaseOrderLine					is like PurchaseOrderLine
		LocalPayablesInvoiceDetail				is like PayablesInvoiceDetail
		LocalContractRebateDueInvoiceLine		is like ContractRebateDueInvoiceLine
		LocalContractLine                       is like ContractLine   
	Derived Fields
		DerivedCurrencyUnitCost is a DerivedField
			type is like InternationalCost
			restricted
						
			TransientTransactionAmount 		= (LocalCurrencyUnitCost * LocalMatchedQuantity)
			TransientFromCurrency			= Contract.CurrencyCode
			TransientExchangeDate			= LocalInvoiceDate
			TransientCurrencyTable			= LocalCurrencyTable

			if (TransientFromCurrency		!= LocalInvoiceCurrencyCode)
				LocalContractCurrencyExchange.ToCurrency			= LocalInvoiceCurrencyCode
				LocalContractCurrencyExchange.EnteredCurrencyRate	= LocalInvoiceCurrencyExchangeRate
				LocalContractCurrencyExchange.EnteredCurrencyAmount	= 0
				return (LocalContractCurrencyExchange.OutputCurrencyAmount / LocalMatchedQuantity)
			else
				return LocalCurrencyUnitCost

	Conditions
		RebateEligibleContract
			restricted
			when (Contract.ContractStatus != 1 
			and   Contract.ContractStatus != 7 
			and   Contract.ContractStatus !=10) 
		
		QualifierCodeNotOnAnotherQualifierForThisRebate
			restricted
			when ((ItemCategory.CommodityCode
			and   !ContractRebateQualifierCommCodeCodesRel exists)
			or    (ItemCategory.ManufacturerInformation
			and   !ContractRebateQualifierManufInfoCodesRel exists)	
			or    (ItemCategory.PurchasingClass
			and   !ContractRebateQualifierPurchClassCodesRel exists)	
			or    (ItemCategory.UNSPSCCode
			and   !ContractRebateQualifierUnspscCodeCodesRel exists))	

		ContractNotClosed
			restricted
			when (Contract.ContractStatus !=10) 

		NotSupplementalRebateAndCanAmend
			when (!ContractRebateType.RebateType.SupplementalRebate
			and   Contract.CanAmend)
			
    	ManufacturerQual
    		when (ManufacturerInfo exists)

    	CommodityQual
    		when (CommodityCode exists)

		UnspscCodeQual
			when (UnspscCode exists)

		MajorClassQual
			when (MajorPurchasingClass exists)

	Relations
		RebateQualifierContractsByContractRel
			one-to-many relation to RebateQualifierContract 
			Field Mapping uses symbolic key
				related.ContractGroup			    = ContractGroup
				related.Contract					= Contract
				related.ContractRebateType			= ContractRebateType
				related.ContractRebate				= blank
				related.ContractRebateQualifier		= ContractRebateQualifier
			Instance Selection
				where (related.OriginalContractNumber		= LocalManufacturerContract)

		ContractRebateDuesRel
			one-to-many relation to ContractRebateDue
			Field Mapping uses symbolic key
				related.ContractGroup			= ContractGroup
				related.Contract				= Contract
				related.ContractRebateType		= ContractRebateType
				related.ContractRebate			= blank
			Instance Selection
				where (related.ContractRebateDue	>= LocalInvoiceDte)
				
		ContractRebateDuesReverseRel
			one-to-many relation to ContractRebateDue
			Field Mapping uses symbolic key
				related.ContractGroup			= ContractGroup
				related.Contract				= Contract
				related.ContractRebateType		= ContractRebateType
				related.ContractRebate			= blank
			Instance Selection
				where (related.ContractRebateDue	< LocalInvoiceDte)
				
		ContractRebateDueInvoiceLineRel
			one-to-one relation to ContractRebateDueInvoiceLine
			Field Mapping uses symbolic key
				related.ContractGroup										= ContractGroup
				related.Contract											= Contract
				related.ContractRebateType									= ContractRebateType
				related.ContractRebate										= blank
				related.ContractRebateDue 									= LocalContractRebateDue
				related.ContractRebateQualifier								= ContractRebateQualifier
				related.Company												= LocalPayablesCompany
				related.PayablesInvoice										= LocalPayablesInvoice
				related.PurchaseOrder										= LocalPurchaseOrder
				related.PurchaseOrderLine									= LocalPurchaseOrderLine
				related.ContractLine                                        = LocalContractLine 
				related.ContractRebateDueInvoiceLine						= LocalContractRebateDueInvoiceLine
				
		PayablesInvoiceDetailForChargebackRel
			one-to-one relation to PayablesInvoiceDetail
			Field Mapping uses symbolic key
				related.Company								= LocalCompany
				related.PayablesInvoice						= LocalPayablesInvoice
				related.PurchaseOrder						= LocalPurchaseOrder
				related.PurchaseOrderLine					= LocalPurchaseOrderLine
				related.Contract							= last PayablesInvoiceDetailsRel.Contract
				related.ContractLine						= last PayablesInvoiceDetailsRel.ContractLine
				related.PayablesInvoiceDetail               = LocalPayablesInvoiceDetail

		PayablesInvoiceDetailsRel
			one-to-many relation to PayablesInvoiceDetail
			Field Mapping uses symbolic key
				related.Company					= LocalCompany
				related.PayablesInvoice			= LocalPayablesInvoice
				related.PurchaseOrder			= LocalPurchaseOrder
				related.PurchaseOrderLine		= LocalPurchaseOrderLine
						
		ContractRebateDueInvoiceLinesByQualifierRel
			one-to-many relation to ContractRebateDueInvoiceLine
			Field Mapping uses ByInvoiceLine
				related.Company							= LocalPayablesCompany
				related.PayablesInvoice					= LocalPayablesInvoice
				related.PurchaseOrder					= LocalPurchaseOrder
				related.PurchaseOrderLine				= LocalPurchaseOrderLine
				related.ContractRebateDueInvoiceLine	= LocalContractRebateDueInvoiceLine
				related.ContractGroup					= ContractGroup
				related.Contract						= Contract
				related.ContractLine                    = LocalContractLine  
				related.ContractRebateType				= ContractRebateType
				related.ContractRebateDue				= LocalContractRebateDue

		LinesForPurchasingClassRel
			one-to-many relation to ContractLine
			Field Mapping uses ByPurchMinorClass
				related.ContractGroup						= ContractGroup
				related.MajorPurchasingClass				= MajorPurchasingClass
				related.MinorPurchasingClass				= MinorPurchasingClass
				related.Contract 							= Contract
				
		LinesForCommodityCodeRel
			one-to-many relation to ContractLine
			Field Mapping uses ByCommodityCode
				related.ContractGroup						= ContractGroup
				related.CommodityCode						= CommodityCode
				related.Contract 							= Contract
				
		LinesForUnspscCodeRel
			one-to-many relation to ContractLine
			Field Mapping uses ByUnspscCode
				related.ContractGroup						= ContractGroup
				related.UNSPSCCode							= UnspscCode
				related.Contract 							= Contract
				
		LinesForManufacturerInfoRel
			one-to-many relation to ContractLine
			Field Mapping uses ByManufacturerInformationCode
				related.ContractGroup						= ContractGroup
				related.Manufacturer						= ManufacturerInfo
				related.Contract 							= Contract

		ContractRebateQualifierManufInfoCodesRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByManufInfo
				related.ManufacturerInfo					= ManufacturerInfo
				related.ContractGroup						= ContractGroup	
				related.Contract							= Contract
				related.ContractRebateType					= ContractRebateType
				related.ContractRebate						= blank
											
		ContractRebateQualifierCommCodeCodesRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByCommCode
				related.CommodityCode						= CommodityCode
				related.ContractGroup						= ContractGroup	
				related.Contract							= Contract
				related.ContractRebateType					= ContractRebateType
				related.ContractRebate						= blank
											
		ContractRebateQualifierPurchClassCodesRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByPurchClass
				related.MajorPurchasingClass				= MajorPurchasingClass
				related.MinorPurchasingClass				= MinorPurchasingClass
				related.ContractGroup						= ContractGroup	
				related.Contract							= Contract
				related.ContractRebateType					= ContractRebateType
				related.ContractRebate						= blank
											
		ContractRebateQualifierUnspscCodeCodesRel
			one-to-many relation to ContractRebateQualifierCode
			Field Mapping uses ByUnspscCode
				related.UnspscCode							= UnspscCode
				related.ContractGroup						= ContractGroup	
				related.Contract							= Contract
				related.ContractRebateType					= ContractRebateType
				related.ContractRebate						= blank
											
		ContractRebateQualifierItemsForItemRel
			one-to-many relation to ContractRebateQualifierItem
			Field Mapping uses ByItemNumber
				related.ItemNumber							= LocalItemNumber
				related.ContractGroup						= ContractGroup	
				related.Contract							= Contract
				related.ContractRebate						= blank
				related.ContractRebateQualifier				= ContractRebateQualifier
			Instance Selection
				where (!related.UseThisItemForQualificationPurposes)
											
	Sets
		ByManufInfo
			duplicates
			indexed
			Sort Order	
				ManufacturerInfo
				ContractGroup
				Contract
				ContractRebateType
				ContractRebate
				ContractRebateQualifier
		
		ByCommCode
			duplicates
			indexed
			Sort Order
				CommodityCode
				ContractGroup
				Contract
				ContractRebateType
				ContractRebate
				ContractRebateQualifier
				
		ByPurchClass
			duplicates
			indexed
			Sort Order
    			MajorPurchasingClass		
    			MinorPurchasingClass				
				ContractGroup
				Contract
				ContractRebateType
				ContractRebate
				ContractRebateQualifier
		
		ByUnspscCode
			duplicates
			indexed
			Sort Order
				UnspscCode
				ContractGroup
				Contract
				ContractRebateType
				ContractRebate
				ContractRebateQualifier
    
	Field Rules
	
	Rule Blocks

		RebateDueDateCheck
			LocalInvoiceDte				= ParameterInvoiceDte
			if (ContractRebateDuesRel exists)
				LocalContractRebateDue		= first ContractRebateDuesRel.ContractRebateDue
			else
				LocalContractRebateDue		= last ContractRebateDuesReverseRel.ContractRebateDue
		
		LoadLocalFields		
			LocalPayablesCompany							= ParameterPayablesCompany
			LocalPayablesInvoice							= ParameterPayablesInvoice
			LocalPurchaseOrder								= ParameterPurchaseOrder
			if (ParameterPurchaseOrderLine !entered) 
				LocalContractLine                      		= ParameterLineNbr
			LocalPurchaseOrderLine							= ParameterPurchaseOrderLine
			LocalPayablesInvoiceDetail						= ParameterPayablesInvoiceDetail
			LocalContractRebateDueInvoiceLine				= ParameterPayablesInvoiceDetail
			if (!ContractRebateDueInvoiceLinesByQualifierRel exists
			or   ContractRebateQualifier			= first ContractRebateDueInvoiceLinesByQualifierRel.ContractRebateQualifier)
				MultipleUpdatesForAnInvoiceLine	= false
			else
				MultipleUpdatesForAnInvoiceLine = true
		
		BuildContractRebateDueInvoiceLines
			LocalMatchedQuantity					= ParameterMatchedQty
			LocalInvoiceCurrencyCode				= ParameterCurrencyCode
			LocalInvoiceCurrencyExchangeRate		= ParameterExchangeRate
			LocalInvoiceDate						= ParameterInvoiceDte
			LocalCurrencyTable						= ParameterCurrencyTable
			invoke Update ContractRebateDueInvoiceLineRel
				invoked.HasContractOnRebate			= LocalHasContractOnRebate
				invoked.ItemUsedForQualification	= LocalItemUsedForQualification
				invoked.ManufacturerUnitCst			= DerivedCurrencyUnitCost
				invoked.MatchedQty  				= ParameterMatchedQty
				invoked.InvoiceDte    				= ParameterInvoiceDte
				invoked.CmContract					= ParameterCmContract
				invoked.ProcureGroup				= ParameterProcureGroup
				invoked.MfgContract					= ParameterMfgContract
				invoked.Invoice						= ParameterInvoice
				invoked.Vendor						= ParameterVendor
				invoked.Location					= ParameterLocation
				invoked.ManufCode					= ParameterManufCode
				invoked.ManufDivision				= ParameterManufDivision
				invoked.MultipleQualifierUpdate		= MultipleUpdatesForAnInvoiceLine
				initialize invoked.InvoiceAmount
				invoked.SupplierRebate				= false
				invoked.InvoiceStatus				= ParameterInvoiceStatus
				invoked.IsForQualifierItem			= false
				invoked.QualifierItemCategory		= ItemCategory
    			invoked.CommCodes					= ParameterCommCodes
    			invoked.MajorPurchasingClass		= ParameterPurchMajorClass
    			invoked.MinorPurchasingClass		= ParameterPurchMinorClass
    			invoked.UNSPSCSegment				= ParameterUNSPSCSegment
    			invoked.UNSPSCFamily				= ParameterUNSPSCFamily
    			invoked.UNSPSCClass					= ParameterUNSPSCClass
    			invoked.UNSPSCCommodity				= ParameterUNSPSCCommodity
				if (ContractRebateType.RebateType.SupplementalRebate)
					invoked.IsForSupplementalRebate	= true
				else
					invoked.IsForSupplementalRebate = false			

																			
	Actions
		Create is a Create Action
			restricted
			Action Rules
    			constraint (Contract.CanAmend)
    				"ContractMustBeInDraftOrAddendumOrAmendmentStatusToAddRebateQualifierCodes"
    			constraint (ContractRebate.Active)
    				"ContractRebateMustBeActiveToAddRebateQualifierCodes"
    				
			Exit Rules
				if (ContractRebate.TotalExpectedAmount entered)
 	    			invoke SetQualifierChangedFlagToYes ContractRebate		
    			invoke DeleteNonQualifyingContractRebateQualifierItems
        			
		Update is an Update Action
			restricted
			Action Rules
    			constraint (Contract.CanAmend)
    				"ContractMustBeInDraftOrAddendumOrAmendmentStatusToModifyRebateQualifierCodes"   	
    			constraint (ContractRebate.Active)
    				"ContractRebateMustBeActiveToModifyRebateQualifierCodes"
		
    	UpdateContractRebateDueInvoiceLine	is an Instance Action
    		restricted
    		Parameters
		    	ParameterManufacturerContract			is Numeric size 15
		    	ParameterInvoiceDte						is Date
		    	ParameterCompany						is a PurchasingCompany
		    	ParameterVendor							is like Vendor
		    	ParameterInvoice 						is AlphaUpper size 22
		    	ParameterSuffix							is Numeric size 3
		        ParameterPoNumber  						is a PurchaseOrder
		        ParameterLineNbr   						is Numeric size 6
    			ParameterManufacturerUnitCost	    	is an InternationalCost
    			ParameterMatchUnitCost					is an InternationalCost
    			ParameterMatchedQty						is Decimal size 13.4
    			ParameterCmContract						is Numeric size 15
    			ParameterProcureGroup					is a ProcurementGroup
    			ParameterMfgContract					is Numeric size 15
    			ParameterLocation						is AlphaUpper size 5
    			ParameterManufCode						is AlphaUpper size 4
    			ParameterManufDivision					is AlphaUpper size 4
    			ParameterEntUnitCst						is an InternationalCost
    			ParameterServiceTypeContract			is Boolean
    			ParameterPoDate							is Date
    			ParameterVendorReturn					is Boolean
    			ParameterCurrencyCode					is like Currency
    			ParameterExchangeRate					is Decimal 14.7
    			ParameterCurrencyTable					is like CurrencyTable
    			ParameterPurchFrLocation				is like VendorLocation
				ParameterInvoiceStatus					is Numeric size 1
					States 
						Released							value is 0
						Unreleased							value is 1
				ParameterPayablesCompany				is like PayablesCompany
				ParameterPayablesInvoice				is like PayablesInvoice
				ParameterPurchaseOrder					is like PurchaseOrder
				ParameterPurchaseOrderLine				is like PurchaseOrderLine
				ParameterPayablesInvoiceDetail			is like PayablesInvoiceDetail
    			ParameterCommCodes						is Alpha size 35
    			ParameterPurchMajorClass				is AlphaUpper size 4
    			ParameterPurchMinorClass				is AlphaUpper size 4
    			ParameterUNSPSCSegment					is AlphaUpper size 2
    			ParameterUNSPSCFamily					is AlphaUpper size 2
    			ParameterUNSPSCClass					is AlphaUpper size 2
    			ParameterUNSPSCCommodity				is AlphaUpper size 2
    			
    		Action Rules
    			LocalManufacturerContract			= ParameterManufacturerContract
				if (RebateQualifierContractsByContractRel	exists)
					if (ContractRebateQualifier.PurchaseType.TargetedPurchases)
						if (ParameterInvoiceDte		>= ContractRebateQualifier.TargetedPurchasesFromDate
						and ParameterInvoiceDte		<= ContractRebateQualifier.TargetedPurchasesToDate)
							include RebateDueDateCheck
							LocalHasContractOnRebate		= true
							LocalItemUsedForQualification	= true
							include LoadLocalFields
							
							if ((ParameterMatchedQty				>= 0
							and  ParameterEntUnitCst				>= 0)
							or  (ParameterMatchedQty				< 0
							and  ParameterSuffix					= 111
							and  PayablesInvoiceDetailForChargebackRel			exists
							and  ParameterEntUnitCst				= PayablesInvoiceDetailForChargebackRel.MatchUnitCost)
							or  (ParameterMatchedQty 				< 0
							and  ParameterSuffix					not entered)
							or  (ParameterMatchedQty				< 0
							and  ParameterServiceTypeContract)
							or  (ParameterEntUnitCst				< 0
							and  ParameterServiceTypeContract)
							or  (ParameterVendorReturn))
								LocalCurrencyUnitCost				= ParameterManufacturerUnitCost
								include BuildContractRebateDueInvoiceLines
					else
						include RebateDueDateCheck
						LocalHasContractOnRebate		= true
						LocalItemUsedForQualification	= true
						include LoadLocalFields
						
						if ((ParameterMatchedQty				>= 0
						and  ParameterEntUnitCst				>= 0)
						or  (ParameterMatchedQty				< 0
						and  ParameterSuffix					= 111
						and  PayablesInvoiceDetailForChargebackRel			exists
						and  ParameterEntUnitCst				= PayablesInvoiceDetailForChargebackRel.MatchUnitCost)
						or  (ParameterMatchedQty 				< 0
						and  ParameterSuffix					= 0)
						or  (ParameterMatchedQty				< 0
						and  ParameterServiceTypeContract)
						or  (ParameterEntUnitCst				< 0
						and  ParameterServiceTypeContract)
						or  (ParameterVendorReturn))
							LocalCurrencyUnitCost				= ParameterManufacturerUnitCost
							include BuildContractRebateDueInvoiceLines
				else
				if (ContractRebateQualifier.RebatePurchaseAmtPct.PurchasePercent entered)
					if (ContractRebateQualifier.PurchaseType.TargetedPurchases)
						if (ParameterPoDate		>= ContractRebateQualifier.TargetedPurchasesFromDate
						and ParameterPoDate		<= ContractRebateQualifier.TargetedPurchasesToDate)
							include RebateDueDateCheck
							LocalHasContractOnRebate		= false
							LocalItemUsedForQualification	= false
							include LoadLocalFields
							
							if ((ParameterCmContract				not entered)
							or  (ParameterMatchedQty				>= 0
							and  ParameterEntUnitCst				>= 0)
							or  (ParameterMatchedQty				< 0
							and  ParameterSuffix					= 111
							and  PayablesInvoiceDetailForChargebackRel			exists
							and  ParameterEntUnitCst				= PayablesInvoiceDetailForChargebackRel.MatchUnitCost)
							or  (ParameterMatchedQty 				< 0
							and  ParameterSuffix					= 0)
							or  (ParameterMatchedQty				< 0
							and  ParameterServiceTypeContract)
							or  (ParameterEntUnitCst				< 0
							and  ParameterServiceTypeContract))
								LocalCurrencyUnitCost				= ParameterMatchUnitCost
								include BuildContractRebateDueInvoiceLines
					else
						include RebateDueDateCheck
						LocalHasContractOnRebate		= false
						LocalItemUsedForQualification	= false
						include LoadLocalFields
						
						if ((ParameterCmContract				not entered)
						or  (ParameterMatchedQty				>= 0
						and  ParameterEntUnitCst				>= 0)
						or  (ParameterMatchedQty				< 0
						and  ParameterSuffix					= 111
						and  PayablesInvoiceDetailForChargebackRel			exists
						and  ParameterEntUnitCst				= PayablesInvoiceDetailForChargebackRel.MatchUnitCost)
						or  (ParameterMatchedQty 				< 0
						and  ParameterSuffix					= 0)
						or  (ParameterMatchedQty				< 0
						and  ParameterServiceTypeContract)
						or  (ParameterEntUnitCst				< 0
						and  ParameterServiceTypeContract))
							if (ParameterPoDate				>= Contract.EffectiveDate)
								if (Contract.LastInvoiceAllowedDate	entered
								and ParameterPoDate			<= Contract.LastInvoiceAllowedDate)
									LocalCurrencyUnitCost				= ParameterMatchUnitCost
									include BuildContractRebateDueInvoiceLines
								else 
								if (Contract.ExpirationDate	entered
    							and ParameterPoDate				<= Contract.ExpirationDate)
									LocalCurrencyUnitCost				= ParameterMatchUnitCost
									include BuildContractRebateDueInvoiceLines
								else
								if (Contract.ExpirationDate	not entered)									
									LocalCurrencyUnitCost				= ParameterMatchUnitCost
									include BuildContractRebateDueInvoiceLines

		Delete is a Delete Action
			valid when (NotSupplementalRebateAndCanAmend)
			Action Rules
    			constraint (Contract.CanAmend)
    				"ContractMustBeInDraftOrAddendumOrAmendmentStatusToDeleteRebateQualifierCodes"   	
    			constraint (ContractRebate.Active)
    				"ContractRebateMustBeActiveToDeleteRebateQualifierCodes"
    			constraint (!ContractRebate.RebateMoniesReceived)
    				"CannotDeleteRebateQualifierCodesAfterRebateMoniesHaveBeenReceived"   	
			Exit Rules
				if (ContractRebate.TotalExpectedAmount entered)
	    			invoke SetQualifierChangedFlagToYes ContractRebate		
    			
		DeleteNonQualifyingContractRebateQualifierItems is an Instance Action
			restricted
			Action Rules
				if (ItemCategory.CommodityCode)
					for each LinesForCommodityCodeRel
						LocalItemNumber				= each.ItemNumber
						invoke Delete ContractRebateQualifierItemsForItemRel
				else
				if (ItemCategory.ManufacturerInformation)
					for each LinesForManufacturerInfoRel
						LocalItemNumber				= each.ItemNumber
						invoke Delete ContractRebateQualifierItemsForItemRel
				else
				if (ItemCategory.PurchasingClass)
					for each LinesForPurchasingClassRel
						LocalItemNumber				= each.ItemNumber
						invoke Delete ContractRebateQualifierItemsForItemRel
				else
				if (ItemCategory.UNSPSCCode)
					for each LinesForUnspscCodeRel
						LocalItemNumber				= each.ItemNumber
						invoke Delete ContractRebateQualifierItemsForItemRel
																		
									    			
