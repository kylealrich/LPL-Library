IntercompanyBillingSettlementFee is a BusinessClass
	owned by intercobilling
	prefix is ICBSF
	sql name is "ICBSettlementFee"

	Ontology
		symbolic key is IntercompanyBillingSettlementFee

	Persistent Fields
		Description
		Type							is Numeric 1
			States
				Payable			value is 0
				Receivable		value is 1
		CashCode
		BankTransactionCode
		CashCodeTransactionCode
		Account							is a TransactionCodeBlock
		Currency						is a FromCurrency
		Amount							is a CurrencyAmount
		CashLedgerSourceRecord
		CashLedgerTransaction
		PayableCurrencyTable			is a CurrencyTable
		PayableExchangeRate				is a CurrencyExchangeRate
		PayableCurrencyAmount			is a CurrencyAmount
		ReceivableCurrencyTable			is a CurrencyTable
		ReceivableExchangeRate			is a CurrencyExchangeRate
		ReceivableCurrencyAmount		is a CurrencyAmount
		

	Field Rules
		Currency
			initial value is DerivedCurrency
			default to DerivedCurrency

		CashCode
			initial value is DerivedCashCode
			default to DerivedCashCode
			
		BankTransactionCode
			initial value is DerivedBankTransactionCode
			default to DerivedBankTransactionCode
			
			required

			if (CashCodeTransactionCodeRel exists)
				constraint (CashCodeTransactionCode exists
				and CashCodeTransactionCode.Active)
					"TransactionCode<BankTransactionCode>NotAuthorizedForCashCode<CashCode>"

			constraint (BankTransactionCode.BankTransactionType.BankService)
				"BankFeeCodeMustBeBankServiceType"
			constraint (BankTransactionCode.TransactionType.DebitTransaction)				
				"BankFeeCodeMustBeDebitTransactionType"

		Amount		
			required
				"AmountIsRequired"
			constraint (Amount > 0)
				"AmountmustBeGreaterThanZero"

		Description		
			default to "SettlementFee"
			required
				"DescriptionIsRequired"

		Account
			required
				"AccountIsRequired"
			











		PayableCurrencyTable
			initial value is IntercompanyBillingSettlementHeader.PayablesCompany.DerivedCurrencyTable
			default to IntercompanyBillingSettlementHeader.PayablesCompany.DerivedCurrencyTable
			
		ReceivableCurrencyTable
			initial value is IntercompanyBillingSettlementHeader.ReceivableCompany.DerivedCurrencyTable
			default to IntercompanyBillingSettlementHeader.ReceivableCompany.DerivedCurrencyTable


	Local Fields
		CurrencyWork					is a CurrencyExchange
		LocalIsRateDefined				is Boolean
		LocalTransactionNumber			is Numeric 12
		LocalCashLedgerTransaction		is a CashLedgerTransaction view		
		LocalBankTransactionCode		is a BankTransactionCode			


	Transient Fields
		AccountingEntity
			derive value from DerivedAccountingEntity
		CurrencyTable
		TransactionAmount					is a CurrencyAmount
		ExchangeDate
		TransientGeneralLedgerSystemCode	is a GeneralLedgerSystemCode

				
	Derived Fields
		DerivedCurrency is a DerivedField
			type is like FromCurrency
			if (Type.Payable)
				return IntercompanyBillingSettlementHeader.PayablesCashCode.Currency
			else
				return IntercompanyBillingSettlementHeader.ReceivableCashCode.Currency

		DerivedBankTransactionCode is a DerivedField
			type is like BankTransactionCode
			if (Type.Payable)
				if (IntercompanyBillingSettlementHeader.PayablesCompany.ICBillingDefaultBankFeeCode entered)
					return IntercompanyBillingSettlementHeader.PayablesCompany.ICBillingDefaultBankFeeCode
				else
					return IntercompanyBillingSettlementHeader.PayablesBankTransactionCode
			else
				if (IntercompanyBillingSettlementHeader.ReceivableCompany.ICBillingDefaultBankFeeCode entered)
					return IntercompanyBillingSettlementHeader.ReceivableCompany.ICBillingDefaultBankFeeCode
				else
					return IntercompanyBillingSettlementHeader.ReceivableBankTransactionCode

		DerivedAccountingEntity is a DerivedField
			type is like AccountingEntity
			if (Type.Payable)
				return IntercompanyBillingSettlementHeader.PayablesCompany.GeneralLedgerCompany.AccountingEntity
			else
				return IntercompanyBillingSettlementHeader.ReceivableCompany.GeneralLedgerCompany.AccountingEntity

		DerivedCashCode is a DerivedField
			type is like CashCode
			if (Type.Payable)
				return IntercompanyBillingSettlementHeader.PayablesCashCode
			else
				return IntercompanyBillingSettlementHeader.ReceivableCashCode
				
		DerivedCompany is a DerivedField
			type is like GeneralLedgerCompany
			if (Type.Payable)
				return IntercompanyBillingSettlementHeader.PayablesCompany
			else
				return IntercompanyBillingSettlementHeader.ReceivableCompany


	Conditions
		HeaderUnreleased
			when (IntercompanyBillingSettlementHeader.Status.Unreleased)

		HeaderReleased		
			when (IntercompanyBillingSettlementHeader.Status.Released)

		HasCashLedgerTransaction
			when (CashLedgerTransaction entered)


	Relations
		CashCodeTransactionCodeRel
			one-to-many relation to CashCodeTransactionCode
			Field Mapping uses symbolic key
				related.CashManagementGroup	= IntercompanyBillingGroup
				related.CashCode			= CashCode
			Instance Selection
				where (related.CashCodeTransactionCode.Active	 	= true)

        CashLedgerTransactionsRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set6
				related.CashManagementGroup		= IntercompanyBillingGroup	
                related.CashCode				= CashCode
                related.BankTransactionCode		= BankTransactionCode
				related.TransactionNumber		= LocalTransactionNumber

		ReversalCashLedgerTransactionsRel		
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set6
				related.CashManagementGroup		= IntercompanyBillingGroup
				related.CashCode				= CashCode
				related.BankTransactionCode		= LocalBankTransactionCode
				related.TransactionNumber		= LocalTransactionNumber

		CBCompanySystemClosingControlRel 
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode	= "CB"
				related.Company					= DerivedCompany

	Sets
		
	Rule Blocks
		DoCurrencyWork
			if (Type.Payable)
				if (IntercompanyBillingSettlementHeader.PayablesCashCode.Currency = IntercompanyBillingSettlementHeader.PayablesCompany.Currency
				and PayableExchangeRate entered)
					constraint (PayableExchangeRate = 1)
						"PayableCashCodeCurrencyIsTheSameAsPayableCompanyBaseCurrency,TheExchangeRateMustBe1"

				if (PayableExchangeRate not entered)
					CurrencyTable					= PayableCurrencyTable	
					TransactionAmount				= Amount
					ExchangeDate					= IntercompanyBillingSettlementHeader.PaymentDate
					initialize CurrencyWork
					CurrencyWork.ToCurrency			= IntercompanyBillingSettlementHeader.PayablesCompany.Currency
					LocalIsRateDefined				= CurrencyWork.IsRateDefined
					PayableExchangeRate				= CurrencyWork.OutputCurrencyRate
					PayableCurrencyAmount			= CurrencyWork.OutputCurrencyAmount

				if (Amount changed
				or  PayableExchangeRate changed)
					CurrencyTable						= PayableCurrencyTable
					TransactionAmount					= Amount
					initialize CurrencyWork
					CurrencyWork.ToCurrency				= IntercompanyBillingSettlementHeader.PayablesCompany.Currency
					CurrencyWork.EnteredCurrencyRate	= PayableExchangeRate
					PayableCurrencyAmount				= CurrencyWork.OutputCurrencyAmount

			if (Type.Receivable)
				if (IntercompanyBillingSettlementHeader.ReceivableCashCode.Currency = IntercompanyBillingSettlementHeader.ReceivableCompany.Currency
				and ReceivableExchangeRate entered)
					constraint (ReceivableExchangeRate = 1)
						"ReceivableCashCodeCurrencyIsTheSameAsReceivableCompanyBaseCurrency,TheExchangeRateMustBe1"
	
				if (ReceivableExchangeRate not entered)
					CurrencyTable					= ReceivableCurrencyTable
					TransactionAmount				= Amount
					ExchangeDate					= IntercompanyBillingSettlementHeader.PaymentDate
					initialize CurrencyWork
					CurrencyWork.ToCurrency			= IntercompanyBillingSettlementHeader.ReceivableCompany.Currency
					LocalIsRateDefined				= CurrencyWork.IsRateDefined
					ReceivableExchangeRate			= CurrencyWork.OutputCurrencyRate
					ReceivableCurrencyAmount		= CurrencyWork.OutputCurrencyAmount
			
				if (Amount changed
				or  ReceivableExchangeRate changed)
					CurrencyTable						= ReceivableCurrencyTable
					TransactionAmount					= Amount
					initialize CurrencyWork
					CurrencyWork.ToCurrency				= IntercompanyBillingSettlementHeader.ReceivableCompany.Currency
					CurrencyWork.EnteredCurrencyRate	= ReceivableExchangeRate
					ReceivableCurrencyAmount			= CurrencyWork.OutputCurrencyAmount


	Actions
        Create is a Create Action
			valid when (HeaderUnreleased)
			Action Rules
				if (CBCompanySystemClosingControlRel.Control)
					constraint (IntercompanyBillingSettlementHeader.PostDate within CBCompanySystemClosingControlRel.ValidEntryDate)
						"SettlementPostDateIsNotWithinValidEntryDatesForSystemCodeCBAndCompany<DerivedCompany>;ValidDateRangeIs<CBCompanySystemClosingControlRel.ValidEntryDate.Begin>-<CBCompanySystemClosingControlRel.ValidEntryDate.End>"
				ExchangeDate						= IntercompanyBillingSettlementHeader.PaymentDate
				TransientGeneralLedgerSystemCode	= "CB"
			Exit Rules
				include DoCurrencyWork
				
										        	
		Update is an Update Action
			valid when (HeaderUnreleased)
			Action Rules
				if (CBCompanySystemClosingControlRel.Control)
					constraint (IntercompanyBillingSettlementHeader.PostDate within CBCompanySystemClosingControlRel.ValidEntryDate)
						"SettlementPostDateIsNotWithinValidEntryDatesForSystemCodeCBAndCompany<DerivedCompany>;ValidDateRangeIs<CBCompanySystemClosingControlRel.ValidEntryDate.Begin>-<CBCompanySystemClosingControlRel.ValidEntryDate.End>"
				ExchangeDate						= IntercompanyBillingSettlementHeader.PaymentDate
				TransientGeneralLedgerSystemCode	= "CB"
			Exit Rules
				include DoCurrencyWork

		UpdateReceivableCurrency is an Instance Action
			restricted
			Parameters
				PrmCashCode is like CashCode
				PrmCurrency is like FromCurrency
			Action Rules
				CashCode = PrmCashCode
				Currency = PrmCurrency
				include DoCurrencyWork

		Delete is a Purge Action
			valid when (HeaderUnreleased)


		ProcessBankFees is a Set Action
			restricted
			Parameters
				PrmIntercompanyBillingGroup				is a IntercompanyBillingGroup
				PrmIntercompanyBillingSettlementHeader	is a IntercompanyBillingSettlementHeader

			Sort Order
				IntercompanyBillingGroup
				IntercompanyBillingSettlementHeader
				Type
				IntercompanyBillingSettlementFee

			Instance Selection
				where (IntercompanyBillingGroup				= PrmIntercompanyBillingGroup
				and	   IntercompanyBillingSettlementHeader	= PrmIntercompanyBillingSettlementHeader)

			Action Rules
				
				Instance Rules
					increment IntercompanyBillingGroup.LastBankFeeTransactionNumber
					LocalTransactionNumber = IntercompanyBillingGroup.LastBankFeeTransactionNumber
					while (CashLedgerTransactionsRel exists)
						increment IntercompanyBillingGroup.LastBankFeeTransactionNumber
						LocalTransactionNumber = IntercompanyBillingGroup.LastBankFeeTransactionNumber
					
					CashLedgerSourceRecord	= "CBT"
					if (Type.Payable)
		                invoke Unreleased.Create CashLedgerTransaction
		                    assign result to CashLedgerTransaction
		                    invoked.CashManagementGroup						= IntercompanyBillingGroup
		                    invoked.CashCode                   				= CashCode
		                    invoked.BankTransactionCode                 	= BankTransactionCode
		                    invoked.Company									= IntercompanyBillingSettlementHeader.PayablesCompany
		                    invoked.Description								= Description
		                    
		                    invoked.TransactionNumber	                   	= LocalTransactionNumber 
		                    invoked.TransactionNumberSuffix		    		= 0
		                    invoked.IssueDate                       	    = IntercompanyBillingSettlementHeader.PaymentDate
		                    invoked.IssuedBankAmount 				    	= Amount
		                    invoked.GeneralLedgerPostDate           	    = IntercompanyBillingSettlementHeader.PostDate
							invoked.Reference								= IntercompanyBillingSettlementHeader.SettlementID
							invoked.CurrencyTable							= PayableCurrencyTable
							invoked.IssuedBaseAmount.FunctionalAmount.EnteredCurrencyRate = PayableExchangeRate
							invoked.AutoNumberingAction						= 2 
							
						invoke Create CashLedgerGLDistribution
							fill in fields from CashLedgerTransaction
		                    invoked.CashManagementGroup								= IntercompanyBillingGroup
		                    invoked.CashCode                   						= CashCode
		                    invoked.CashLedgerSourceRecord							= "CBT"
							invoked.CashLedgerGLDistribution.BankTransactionCode	= BankTransactionCode
							invoked.CashLedgerGLDistribution.TransactionIDNumber	= CashLedgerTransaction
							invoked.CashLedgerGLDistribution.DistributionType		= "E"
							invoked.CashLedgerGLDistribution.SequenceNumber			= 1
							invoked.Company											= IntercompanyBillingSettlementHeader.PayablesCompany
							invoked.CompanyAccountingEntity							= IntercompanyBillingSettlementHeader.PayablesCompany.GeneralLedgerCompany.AccountingEntity
							invoked.LocalExchangeDate								= IntercompanyBillingSettlementHeader.PaymentDate
							invoked.PostDate										= IntercompanyBillingSettlementHeader.PostDate
							invoked.GLFinanceCodeBlock							 	= Account
							invoked.GLTransactionAmount								= Amount

					if (Type.Receivable)
		                invoke Unreleased.Create CashLedgerTransaction
		                    assign result to CashLedgerTransaction
		                    invoked.CashManagementGroup						= IntercompanyBillingGroup
		                    invoked.CashCode                   				= CashCode
		                    invoked.BankTransactionCode                 	= BankTransactionCode
		                    invoked.Company									= IntercompanyBillingSettlementHeader.ReceivableCompany
		                    invoked.Description								= Description
			                    
		                    invoked.TransactionNumber	                   	= LocalTransactionNumber 
		                    invoked.TransactionNumberSuffix		    		= 0
		                    invoked.IssueDate                       	    = IntercompanyBillingSettlementHeader.PaymentDate
		                    invoked.IssuedBankAmount 				    	= Amount
		                    invoked.GeneralLedgerPostDate           	    = IntercompanyBillingSettlementHeader.PostDate
							invoked.Reference								= IntercompanyBillingSettlementHeader.SettlementID
							invoked.CurrencyTable							= ReceivableCurrencyTable
							invoked.IssuedBaseAmount.FunctionalAmount.EnteredCurrencyRate = ReceivableExchangeRate
							invoked.AutoNumberingAction						= 2 

						invoke Create CashLedgerGLDistribution
							fill in fields from CashLedgerTransaction
		                    invoked.CashManagementGroup								= IntercompanyBillingGroup
			                invoked.CashCode  		                 				= CashCode
			                invoked.CashLedgerSourceRecord							= "CBT"
							invoked.CashLedgerGLDistribution.BankTransactionCode	= BankTransactionCode
							invoked.CashLedgerGLDistribution.TransactionIDNumber	= CashLedgerTransaction
							invoked.CashLedgerGLDistribution.DistributionType		= "E"
							invoked.CashLedgerGLDistribution.SequenceNumber			= 1
							invoked.Company											= IntercompanyBillingSettlementHeader.ReceivableCompany
							invoked.CompanyAccountingEntity							= IntercompanyBillingSettlementHeader.ReceivableCompany.GeneralLedgerCompany.AccountingEntity
							invoked.LocalExchangeDate								= IntercompanyBillingSettlementHeader.PaymentDate
							invoked.PostDate										= IntercompanyBillingSettlementHeader.PostDate
							invoked.GLFinanceCodeBlock							 	= Account
							invoked.GLTransactionAmount								= Amount
							
					invoke Unreleased.Release CashLedgerTransaction

					invoke Update

		ReverseBankFees is a Set Action		
			restricted
			Parameters
				PrmIntercompanyBillingGroup				is a IntercompanyBillingGroup
				PrmIntercompanyBillingSettlementHeader	is a IntercompanyBillingSettlementHeader
				PrmReversalDate							is Date

			Sort Order
				IntercompanyBillingGroup
				IntercompanyBillingSettlementHeader
				Type
				IntercompanyBillingSettlementFee

			Instance Selection
				where (IntercompanyBillingGroup				= PrmIntercompanyBillingGroup
				and    IntercompanyBillingSettlementHeader	= PrmIntercompanyBillingSettlementHeader)

			Action Rules

				Instance Rules
					if (Type.Payable)
						increment IntercompanyBillingGroup.LastBankFeeTransactionNumber
						LocalTransactionNumber = IntercompanyBillingGroup.LastBankFeeTransactionNumber
						LocalBankTransactionCode = IntercompanyBillingSettlementHeader.PayablesCompany.ICBillingReverseBankFeeCode
						while (ReversalCashLedgerTransactionsRel exists)
							increment IntercompanyBillingGroup.LastBankFeeTransactionNumber
							LocalTransactionNumber = IntercompanyBillingGroup.LastBankFeeTransactionNumber
						CashLedgerSourceRecord = "CBT"
						invoke Unreleased.Create CashLedgerTransaction
							assign result to LocalCashLedgerTransaction
							invoked.CashManagementGroup						= IntercompanyBillingGroup
							invoked.CashCode								= CashCode
							invoked.BankTransactionCode						= LocalBankTransactionCode
							invoked.Company									= IntercompanyBillingSettlementHeader.PayablesCompany
							invoked.Description								= "IntercompanyBillingSettlementReversal"
							invoked.TransactionNumber						= LocalTransactionNumber
							invoked.TransactionNumberSuffix					= 0
							invoked.IssueDate								= IntercompanyBillingSettlementHeader.PaymentDate
							invoked.IssuedBankAmount						= Amount
							invoked.GeneralLedgerPostDate					= PrmReversalDate
							invoked.Reference								= IntercompanyBillingSettlementHeader.SettlementID
							invoked.CurrencyTable							= PayableCurrencyTable
							invoked.IssuedBaseAmount.FunctionalAmount.EnteredCurrencyRate = PayableExchangeRate
							invoked.AutoNumberingAction						= 2
						invoke Create CashLedgerGLDistribution
							fill in fields from LocalCashLedgerTransaction.CashLedgerTransaction
							invoked.CashManagementGroup								= IntercompanyBillingGroup
							invoked.CashCode										= CashCode
							invoked.CashLedgerSourceRecord							= "CBT"
							invoked.CashLedgerGLDistribution.BankTransactionCode	= LocalBankTransactionCode
							invoked.CashLedgerGLDistribution.TransactionIDNumber	= LocalCashLedgerTransaction.CashLedgerTransaction
							invoked.CashLedgerGLDistribution.DistributionType		= "E"
							invoked.CashLedgerGLDistribution.SequenceNumber			= 1
							invoked.Company											= IntercompanyBillingSettlementHeader.PayablesCompany
							invoked.CompanyAccountingEntity							= IntercompanyBillingSettlementHeader.PayablesCompany.GeneralLedgerCompany.AccountingEntity
							invoked.PostDate										= PrmReversalDate
							invoked.GLFinanceCodeBlock								= Account
							invoked.GLTransactionAmount								= Amount * -1
						invoke Unreleased.Release LocalCashLedgerTransaction.CashLedgerTransaction

					if (Type.Receivable)
						increment IntercompanyBillingGroup.LastBankFeeTransactionNumber
						LocalTransactionNumber = IntercompanyBillingGroup.LastBankFeeTransactionNumber
						LocalBankTransactionCode = IntercompanyBillingSettlementHeader.ReceivableCompany.ICBillingReverseBankFeeCode
						while (ReversalCashLedgerTransactionsRel exists)
							increment IntercompanyBillingGroup.LastBankFeeTransactionNumber
							LocalTransactionNumber = IntercompanyBillingGroup.LastBankFeeTransactionNumber
						CashLedgerSourceRecord = "CBT"
						invoke Unreleased.Create CashLedgerTransaction
							assign result to LocalCashLedgerTransaction
							invoked.CashManagementGroup						= IntercompanyBillingGroup
							invoked.CashCode								= CashCode
							invoked.BankTransactionCode						= LocalBankTransactionCode
							invoked.Company									= IntercompanyBillingSettlementHeader.ReceivableCompany
							invoked.Description								= "Intercompany Billing Settlement Fee Reversal"
							invoked.TransactionNumber						= LocalTransactionNumber
							invoked.TransactionNumberSuffix					= 0
							invoked.IssueDate								= IntercompanyBillingSettlementHeader.PaymentDate
							invoked.IssuedBankAmount 						= Amount
							invoked.GeneralLedgerPostDate					= PrmReversalDate
							invoked.Reference								= IntercompanyBillingSettlementHeader.SettlementID
							invoked.CurrencyTable							= ReceivableCurrencyTable
							invoked.IssuedBaseAmount.FunctionalAmount.EnteredCurrencyRate = ReceivableExchangeRate
							invoked.AutoNumberingAction						= 2
						invoke Create CashLedgerGLDistribution
							fill in fields from LocalCashLedgerTransaction.CashLedgerTransaction
							invoked.CashManagementGroup								= IntercompanyBillingGroup
							invoked.CashCode										= CashCode
							invoked.CashLedgerSourceRecord							= "CBT"
							invoked.CashLedgerGLDistribution.BankTransactionCode	= LocalBankTransactionCode
							invoked.CashLedgerGLDistribution.TransactionIDNumber	= LocalCashLedgerTransaction.CashLedgerTransaction
							invoked.CashLedgerGLDistribution.DistributionType		= "E"
							invoked.CashLedgerGLDistribution.SequenceNumber			= 1
							invoked.Company											= IntercompanyBillingSettlementHeader.ReceivableCompany
							invoked.CompanyAccountingEntity							= IntercompanyBillingSettlementHeader.ReceivableCompany.GeneralLedgerCompany.AccountingEntity
							invoked.PostDate										= PrmReversalDate
							invoked.GLFinanceCodeBlock								= Account
							invoked.GLTransactionAmount								= Amount * -1
						invoke Unreleased.Release LocalCashLedgerTransaction.CashLedgerTransaction


