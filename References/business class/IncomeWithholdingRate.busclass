IncomeWithholdingRate is a BusinessClass
	owned by ap
	prefix is CWHR

	Ontology
		symbolic key is IncomeWithholdingRate
			name for EffectiveDate is EffectiveDate

	Patterns

	Persistent Fields
		Rate								is a ChrgRate
		WithholdingScale
		EnrolledRate						is a ChrgRate
		MinimumWithholding					is an InternationalAmount
		EnrolledMinimumWithholding			is an InternationalAmount
		MinimumInvoicedAmount				is an InternationalAmount
		ExemptAmount						is an InternationalAmount
		PeriodInvoicedThreshold				is an InternationalAmount

	Actions
		Create is a Create Action
		Update is an Update Action
		Delete is a Delete Action 

	Field Rules



		WithholdingScale
			if (EnrolledRate entered)
				constraint (WithholdingScale !entered)
					"CannotEnterBothEnrolledRateAndScale"
								
			constraint (IncomeWithholdingCode.Type.Payment)
				"WithholdingScaleOnlyValidForPaymentTypeWithholdingCodes"			

			constraint (WithholdingScale.Currency = IncomeWithholdingCode.Currency)
				"ScaleCurrencyMustEqualWithholdingCodeCurrency"

			constraint (IncomeWithholdingCode.Accumulated)
				"IncomeCodeMustBeAccumulatedTypeToEnterAWithholdingScale"
				
		EnrolledRate
			constraint (IncomeWithholdingCode.Type.Payment)
				"EnrolledRateOnlyValidForPaymentTypeWithholdingCodes"			

			constraint (IncomeWithholdingCode.Currency entered)
				"CurrencyRequiredOnWithholdingCode"

		MinimumWithholding
			constraint (IncomeWithholdingCode.Currency entered)
				"CurrencyRequiredOnWithholdingCode"

			constraint (MinimumWithholding >= 0)
				"MinimumWithholdingCannotBeNegative"

		EnrolledMinimumWithholding
			constraint (IncomeWithholdingCode.Type.Payment)
				"EnrolledMinimumWithholdingOnlyValidForPaymentTypeWithholdingCodes"			

			constraint (IncomeWithholdingCode.Currency entered)
				"CurrencyRequiredOnWithholdingCode"
				
			constraint (EnrolledRate		entered
			or	        WithholdingScale	entered)
				"EnrolledRateOrWithholdingScaleRequiredForEnrolledMinimumWithholding"

			constraint (EnrolledMinimumWithholding >= 0)
				"EnrolledMinimumWithholdingCannotBeNegative"

		MinimumInvoicedAmount


				
			if (IncomeWithholdingCode.Accumulated)						
				constraint (PeriodInvoicedThreshold	entered)
					"MinimumInvoiceAmountMustBeUsedInConjunctionWithPeriodInvoicedThresholdWhenWithholdingCodeIsSetToAccumulate"
				
			constraint (MinimumWithholding	!entered)
				"CannotEnterBothMinimumWithholdingAndMinimumInvoicedAmount"
			
			constraint (IncomeWithholdingCode.Currency entered)
				"CurrencyRequiredOnWithholdingCode"

			constraint (MinimumInvoicedAmount >= 0)
				"MinimumInvoicedAmountCannotBeNegative"

		ExemptAmount
			constraint (IncomeWithholdingCode.Type.Payment)
				"ExemptAmountOnlyValidForPaymentTypeWithholdingCodes"			

			constraint (IncomeWithholdingCode.Accumulated)
				"IncomeCodeMustBeAccumulatedTypeToEnterAnExemptAmount"
				
			constraint (IncomeWithholdingCode.Currency entered)
				"CurrencyRequiredOnWithholdingCode"

			constraint (ExemptAmount >= 0)
				"ExemptAmountCannotBeNegative"

		PeriodInvoicedThreshold					
			constraint (IncomeWithholdingCode.Accumulated)
				"IncomeCodeMustBeAccumulatedTypeToEnterPeriodInvoicedThreshold"
				
			constraint (IncomeWithholdingCode.Currency entered)
				"CurrencyRequiredOnWithholdingCode"

			constraint (PeriodInvoicedThreshold >= 0)
				"PeriodInvoicedThresholdCannotBeNegative"

			constraint (WithholdingScale !entered)
				"CannotEnterPeriodInvoicedThresholdAndWithholdingScale"		
				
			constraint (EnrolledRate !entered)
				"EnrolledRateNotValidWithPeriodInvoicedThreshold"
				
			constraint (MinimumWithholding !entered)
				"CannotEnterPeriodInvoicedThresholdAndMinimumWithholding"
				
			constraint (EnrolledMinimumWithholding !entered)
				"CannotEnterPeriodInvoicedThresholdAndEnrolledMinimumWithholding"
				
			constraint (ExemptAmount !entered)
				"CannotEnterPeriodInvoicedThresholdAndEnrolledExemptAmount"		

			constraint (PeriodInvoicedThreshold > MinimumInvoicedAmount)
				"PeriodInvoiceThreshholdMustBeGreaterThanMinimumInvoicedAmount"	
