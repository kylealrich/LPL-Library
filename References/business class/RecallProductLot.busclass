RecallProductLot is a BusinessClass
    owned by recall
    prefix is RMRPL

    Ontology
    	symbolic key is RecallProductLot
    	
    Persistent Fields
   		ExpirationDate	is Alpha 20
   		ThruLot        	is AlphaUpper size 16
   		RecallProductLotStripped is AlphaUpper size 16
   	
	Derived Fields
		
		ThruMessage is a MessageField
			restricted
			"Thru"
		
		DisplayLot is a DerivedField
			type is Alpha size 40
			if (ThruLot != blank)
				return RecallProductLot + "_" + ThruMessage + "_" + ThruLot
			else
				return RecallProductLot

		LotComparisonNote is a MessageField
			restricted
			"Note:ForProductQueries,LotNumbersAreComparedAlphabeticallyRatherThanNumerically"
			
		NumberOfCharacters is a DerivedField
			type is Numeric size 3
			restricted
			return RecallProductLot size 
			
		DerivedStrippedLot is a DerivedField
			type is AlphaUpper size 16
			return com.lawson.apps.procurement.base.StringUtils.stripNonAlphaNumeric(RecallProductLot)
			
	Conditions
		HasRange
			restricted
			when (ThruLot != blank)

		SingleLot
			restricted
			when (RecallProductLot entered
			and   ThruLot not entered)
						
   	Sets
   		
   		ByStrippedLot
			Sort Order
				RecallGroup
			 	RecallNotice
			 	RecallProduct
			 	RecallProductLotStripped
			 	RecallProductLot
   	
   	Field Rules
   	
   		RecallProductLotStripped
   		
   			default to DerivedStrippedLot
   	
   	Actions
    	Create is a Create Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			Action Rules
				invoke FastUpdate RecallNotice
					invoked.PrmLastUpdateToProducts = current timestamp
						
		EnterLotNumberRange is a Create Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			Parameters
				RecallGroup
				RecallNotice
				RecallProduct
				Prefix				 is AlphaUpper size up to 5
				SerialNumberLotRange
				Suffix				 is AlphaUpper size up to 5
				ExpirationDate		 is Alpha 20
				
			Parameter Rules
				RecallGroup 
					required
				RecallNotice	
					required
				RecallProduct	
					required
					
			Local Fields
   				LocalLotNumber 		is Numeric size 10
   				LocalLotCheck		is AlphaUpper size 20
   				 
   			Action Rules
   				if (Prefix entered
   				or	Suffix entered)
   					LocalLotCheck = Prefix + SerialNumberLotRange.EndingNumber + Suffix
   					constraint (!LocalLotCheck[17] entered)
   						"LotNumberWithPrefixAndSuffixMustBeLessThan16Characters"
   			
   				invoke Create this instance
					invoked.RecallGroup		 	= RecallGroup
					invoked.RecallNotice 		= RecallNotice
					invoked.RecallProduct 		= RecallProduct
					invoked.ExpirationDate		= ExpirationDate
					invoked.RecallProductLot	= Prefix + SerialNumberLotRange.BeginningNumber + Suffix
					invoked.ThruLot				= Prefix + SerialNumberLotRange.EndingNumber + Suffix
				
						
		Update is an Update Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			
		Delete is a Delete Action
			valid when (RecallNotice.RecallNoticeUpdatable)
			Action Rules
				invoke FastUpdate RecallNotice
					invoked.PrmLastUpdateToProducts = current timestamp
						
		Purge is a Purge Action
			restricted
			
			
