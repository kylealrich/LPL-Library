PickForParTicket is a BusinessClass
    owned by mscm
    prefix is MSPPT

    Ontology
        symbolic key is PickForParTicket

	Persistent Fields
        RequestingCompany                       is like InventoryCompany
        RequestingLocation                      is like InventoryLocation
        MobileSupplyChainParCount               is like MobileSupplyChainParCount
        MobileSupplyChainPrinter                is like MobileSupplyChainPrinter
    	Status									is Numeric size 1
			protected
			States
				Open			    value is 0
				InProgress		    value is 1
                Downloaded          value is 5
				Cancelled		    value is 7
                ProcessedWithErrors value is 8
				Processed		    value is 9
        InventoryTransaction                    is like InventoryTransaction
        AdditionalInventoryTransaction          is like InventoryTransaction
        TimeSubmitted                           is TimeStamp
        TimeStarted                             is TimeStamp
        LastDownloadedTime                      is TimeStamp

    Derived Fields
        DerivedDuration is a DerivedField
            type is Numeric size 6
            return TimeSubmitted - TimeStarted
        
        ProcessDeliveryDocumentLineXML is a DerivedField
			type is XMLDocument
			restricted
			if(PickForParTicketLineRel exists)
				for each PickForParTicketLineRel
					ProcessDeliveryDocumentLineXML += template.PFPDeliveryDocumentLine_ST document for each
			else
				return ""
        
        DerivedIssuedBy is a DerivedField
            type is Text
            restricted
            return MobileSupplyChainParCountRel.CountedBy
        
        DerivedPostalAddress is a DerivedField
            type is Text
            restricted
            return RequestingLocationRel.PostalAddress.FormattedAddress1
        
        DerivedCompanyName is a DerivedField
            type is Text
            restricted
            return Company.DerivedCompanyDisplay
        
        DerivedInventoryLocationName is a DerivedField
            type is Text
            restricted
            return InventoryLocation.DerivedInventoryLocationIDName
        
        DerivedStatus is a DerivedField
            type is MessageField
            restricted
            if(Status.Open)
                return "Open"
            else
            if(Status.InProgress)
                return "InProgress"
            else
            if(Status.Downloaded)
                return "Downloaded"
            else
            if(Status.Cancelled)
                return "Cancelled"
            else
            if(Status.ProcessedWithErrors)
                return "ProcessedWithErrors"
            else
            if(Status.Processed)
                return "Processed"
                
        DerivedIsoLocale is a DerivedField
            type is Text
            restricted
            return actor.IsoLocale
        
        DerivedRequestingLocationName is a DerivedField
            type is like Name
            restricted
            return RequestingLocationRel.Name
    Sets
        ByMobileSupplyChainParCount
            Sort Order
                RequestingCompany
                RequestingLocation
                MobileSupplyChainParCount
                PickForParTicket
        
        ByPickForParTicket
            Sort Order
                PickForParTicket descending
                RequestingCompany
                RequestingLocation
                MobileSupplyChainParCount


    Relations
        InventoryTransactionRel
            one-to-one relation to InventoryTransaction
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.InventoryLocation       = InventoryLocation
                related.InventoryTransaction    = InventoryTransaction

        AdditionalInventoryTransactionRel
            one-to-one relation to InventoryTransaction
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.InventoryLocation       = InventoryLocation
                related.InventoryTransaction    = AdditionalInventoryTransaction    
        
        RequestingLocationRel
            one-to-one relation to InventoryLocation
            Field Mapping uses symbolic key
                related.Company                 = RequestingCompany
                related.InventoryLocation       = RequestingLocation
         
        MobileSupplyChainParCountRel
            one-to-one relation to MobileSupplyChainParCount
            Field Mapping uses symbolic key
                related.Company                     = RequestingCompany
                related.InventoryLocation           = RequestingLocation
                related.MobileSupplyChainParCount   = MobileSupplyChainParCount
        
        PickForParTicketLineRel
            one-to-many relation to PickForParTicketLine
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.InventoryLocation       = InventoryLocation
                related.PickForParTicket        = PickForParTicket

        PickForParTicketLineWithInsufficientQuantitiesRel
            one-to-many relation to PickForParTicketLine
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.InventoryLocation       = InventoryLocation
                related.PickForParTicket        = PickForParTicket
            Instance Selection
                where (related.HasInsufficientStockOnHandQuantity
                and related.Status.Error)
        
        PickForParTicketLineWithErrorsRel
            one-to-many relation to PickForParTicketLine
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.InventoryLocation       = InventoryLocation
                related.PickForParTicket        = PickForParTicket
            Instance Selection
                where (related.Status.Error)

		MobileSupplyChainResourceAccessCompanyAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization		            = actor.agent(Employee).HROrganization
    			related.MobileSupplyChainResource		= actor.agent(Employee).Employee
    			related.Company				            = Company
    			related.MobileSupplyChainLocation	    = blank

        MobileSupplyChainResourceAccessRel
    		one-to-many relation to MobileSupplyChainResourceAccess
    		Field Mapping uses symbolic key
    			related.HROrganization		            = actor.agent(Employee).HROrganization
    			related.MobileSupplyChainResource		= actor.agent(Employee).Employee
    			related.Company				            = Company
    			related.MobileSupplyChainLocation	    = InventoryLocation        

		MobileSupplyChainResourceRel
			one-to-one relation to MobileSupplyChainResource
			Field Mapping uses symbolic key
				related.HROrganization              = actor.agent(Employee).HROrganization
				related.MobileSupplyChainResource   = actor.agent(Employee).Employee

        MobileSupplyChainCompanyRel
            one-to-one relation to MobileSupplyChainCompany
            Field Mapping uses symbolic key
                related.Company                 =   Company


    Conditions
        IsValidForDownload
            when (Status.Open or Status.Downloaded)

        IsAccessibleByMobileSupplyChainResource
        	restricted
        	when (MobileSupplyChainResourceRel.GlobalLocationAccess or MobileSupplyChainCompanyRel.MobileSupplyChainResourceCompanyAccessRelExists or MobileSupplyChainResourceAccessRel exists)

        HasSufficientQuantitiesOnHand
            restricted
            when (PickForParTicketLineWithInsufficientQuantitiesRel not exists)

        InventoryTransactionExists
            restricted
            when (InventoryTransactionRel.InventoryTransaction exists)

    Actions
        Create is a Create Action
            restricted

        Update is an Update Action
            restricted

        Delete is a Delete Action
            restricted
        
        TransitionToCancelled is an Instance Action
            restricted
            Action Rules
                Status          = Status.Cancelled
        
        TransitionToDownloaded is an Instance Action
            restricted
            valid when (IsValidForDownload)
            Action Rules
                Status             = Status.Downloaded
                LastDownloadedTime = current timestamp
            
        TransitionToInProgress is an Instance Action
            restricted
            Parameters
                PrmMobileSupplyChainUpload       is like MobileSupplyChainUpload
                    default label is "MobileSupplyChainUpload"
            Action Rules
                TimeSubmitted   = current timestamp
                Status          = Status.InProgress

        CreateIssueTransaction is an Instance Action
            restricted
            Parameters
                PrmIsAdditionalQuantity     is Boolean
                    default label is "IsAdditionalQuantity"
            Local Fields
                NewInventoryTransaction     is an InventoryTransaction view

            Action Rules
                invoke Unreleased.CreateIssue InventoryTransaction
                    assign result to NewInventoryTransaction
                    invoked.Company                                     = Company
                    invoked.InventoryLocation                           = InventoryLocation
                    invoked.InventoryDocumentType                       = InventoryDocumentType.InventoryIssue
                    invoked.FromToCompanyLocation.FromToCompany         = RequestingCompany
                    invoked.FromToCompanyLocation.RequestingLocation    = RequestingLocation
                
                if (PrmIsAdditionalQuantity)
                    AdditionalInventoryTransaction  = NewInventoryTransaction.InventoryTransaction
                else
                    InventoryTransaction            = NewInventoryTransaction.InventoryTransaction

        Reprocess is an Instance Action
            valid when (Status.ProcessedWithErrors)
            Action Rules
                if (InventoryTransactionRel exists 
                and InventoryTransactionRel.IsUnreleased
                and HasSufficientQuantitiesOnHand)
                    invoke Release InventoryTransactionRel

                    Status = Status.Processed

                    for each PickForParTicketLineWithErrorsRel          
                        invoke Update each
                            invoked.Status = 7
                
                constraint (HasSufficientQuantitiesOnHand)
					"LineErrorsMustBeResolvedFirst"
        
        PrintDeliveryDocument is an Instance Action
			restricted
			Action Rules			
				invoke Create MobileSupplyChainPrinterJob
		        	invoked.FinanceEnterpriseGroup          = Company.FinanceEnterpriseGroup
		            invoked.Company 						= Company
		            invoked.MobileSupplyChainPrinter 		= MobileSupplyChainPrinter
                    invoked.TransactionType                 = MobileSupplyChainPrintingTransactionType.IssuePFP
                    invoked.NumberOfCopies 					= 1
					invoked.PrintDataFile 					= template.PFPDeliveryDocument_ST document for this instance
