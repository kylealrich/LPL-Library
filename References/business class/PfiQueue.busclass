PfiQueue is a BusinessClass
    owned by pfi
    prefix is PfiQU
    default label is "Queue"
    disable data area copy

    Ontology
        symbolic key is PfiQueue

    Patterns
        implements CRUD
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated
        implements IncrementalReplication
            indicator field is IsReplicated
                replicate when false
                    then set to true

    Persistent Fields

        WorkDescription     is a PfiWorkTitle
        ActionTaken         is a PfiActionTaken
        DisplayType         is a PfiDisplayType
        DisplayName         is Alpha size 30
        HideActions         is Boolean
        DisplayExec         is Text
            default label is "Content"
        PhoneDisplayType         is a PfiDisplayType
        PhoneDisplayName         is Alpha size 30
        PhoneDisplayExec         is Text
            default label is "PhoneContent"
        TabletDisplayType         is a PfiDisplayType
        TabletDisplayName         is Alpha size 30
        TabletDisplayExec         is Text
            default label is "TabletContent"
        IONDisplayName         is Text
        IONDisplayExec         is Text
            default label is "IONContent"
        AppsData            is Alpha size 512
            default label is "ApplicationData"
        AppsFilter          is Alpha size 100
            default label is "ApplicationFilter"
        DispStatus          is a PfiQueueStatus
            default label is "DisplayStatus"
        WaitHours1          is Decimal size 6.2
            default label is "ReminderInHours"
        SendNotification    is Boolean
        Reminded1           is Boolean
            default label is "FirstReminderHasBeenSent"
        WaitHours2          is Decimal size 6.2
            default label is "ReminderInHours"
        Reminded2           is Boolean
            default label is "SecondReminderHasBeenSent"
        CurrentAssignment   is Numeric size 6
        ReassignTask        is AlphaUpper size 20 
        ReassignHours       is Numeric size 4 
        PfiFilterKey
        FilterValue         is a PfiFilterValue
        RoutingRule         is Alpha size 250
        NotifyAll1          is Boolean
            default label is "NotifyUsers"
        Addnotifytype1      is Numeric size 2
            States
                ToTask value is 1
                ToUser value is 2
                ToNone value is 3
            default label is "ReminderType"
        Addnotify1          is Alpha size 20
            default label is "NotifyAdditionallyTo"
        NotifyAll2          is Boolean
            default label is "NotifyUsers"
        Addnotifytype2      is Numeric size 2
            States
                ToTask value is 1
                ToUser value is 2
                ToNone value is 3
            default label is "ReminderType"
        Addnotify2          is Alpha size 20
            default label is "NotifyAdditionallyTo"
        ProLogicType        is a PfiProLogicType
            default label is "ProceedCondition"
        ProLogicVal1        is Alpha size 30
            default label is "Action"
        ProLogicVal2        is Alpha size 30
            default label is "Percentage"
        TimeOutType         is Numeric size 1
            States
                NotSet               value is 0
                Action               value is 1
                Reassign             value is 2
                ActionDone           value is 3
                ReassignDone         value is 4
                ReassignToSupervisor value is 5
        TimeOutHour         is Decimal size 6.2
            default label is "TimeoutInHours"
        TimeoutAction       is Alpha size 30
        TimedOut            is Boolean
        FailAction          is Alpha size 100
            default label is "FailedAction"
        LastQueueAction     is Numeric size 5
        ConfigName          is Alpha size 200
        IsReplicated is Boolean
            default label is untranslatable

    Derived Fields

        OriginatorUser is a DerivedField
            type is Actor
            return PfiWorkunit.Actor

        DisplayExecAsString is a DerivedField
            type is Alpha size 100
            default label is "DisplayInformationAsString"
            return DisplayExec

        PhoneDisplayExecAsString is a DerivedField
            type is Alpha size 100
            default label is "PhoneDisplayInformationAsString"
            return PhoneDisplayExec

        TabletDisplayExecAsString is a DerivedField
            type is Alpha size 100
            default label is "TabletDisplayInformationAsString"
            return TabletDisplayExec

        ActionTakenOrPendingCount is a DerivedField
            type is Numeric 3
            ActionCount = 0
            for each PfiQueueTaskCurrentAssignmentRel
                if (each.Status.ActionTaken or each.Status.ActionPending)
                    ActionCount = ActionCount + 1
            return ActionCount

        ActionTakenBy is a DerivedField
            type is Actor
            default label is "ActorID"
            if (PfiMetricsRel exists)
                if (instance count of PfiMetricsRel = 1)
                    return first PfiMetricsRel.PfiMetrics.PfiUserProfile 
                else
                    return "(multiple)" 
            return "" 

        UserName is a DerivedField
            type is Alpha size 101 
            if (PfiMetricsRel exists)
                if (instance count of PfiMetricsRel = 1)
                    return PfiActorRel.FirstNameAndLastNameOrActor
                else
                    return "(multiple)" 
            return "" 

        MingleInboxId is a DerivedField
            type is Numeric 12
            default label is "InboxId"
            if (PfiCurrentQueueAssignmentRel exists)
                return PfiCurrentQueueAssignmentRel.MingleInboxId
            return 0
            
        NotificationCenterStatus is a DerivedField
            type is Numeric 1
            if (PfiCurrentQueueAssignmentRel exists)
                return PfiCurrentQueueAssignmentRel.NotificationCenterStatus
            return 0
            		
        VariationId is a DerivedField
            type is Numeric 6
            if (PfiCurrentQueueAssignmentRel exists)
                return PfiCurrentQueueAssignmentRel.VariationId
            return 0

        IsMingleInboxCompletionMessageSent is a DerivedField
            type is Boolean
            default label is "InboxCompletionMessageSent"
            if (PfiCurrentQueueAssignmentRel exists)
                return PfiCurrentQueueAssignmentRel.IsMingleInboxCompletionMessageSent
            return false

        UserTimeZone is a NativeField
            type is Alpha size 50

    Local Fields

        ActionCount is Numeric 3

    Relations

        PfiMetricsRel
            one-to-many relation to PfiMetrics
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = PfiActivity

        PfiQueueTaskRel
            one-to-many relation to PfiQueueTask
            Field Mapping uses symbolic key
                related.PfiWorkunit = PfiWorkunit
                related.PfiActivity = PfiActivity

        PfiCurrentQueueAssignmentRel
            one-to-one relation to PfiQueueAssignment
            Field Mapping uses symbolic key
                related.PfiWorkunit        = PfiWorkunit
                related.PfiActivity        = PfiActivity
                related.PfiQueue           = blank
                related.PfiQueueAssignment = CurrentAssignment

        PfiQueueTaskCurrentAssignmentRel
            one-to-many relation to PfiQueueTask
            Field Mapping uses symbolic key
                related.PfiWorkunit        = PfiWorkunit
                related.PfiActivity        = PfiActivity
                related.PfiQueueAssignment = CurrentAssignment

        PfiActorRel
            one-to-many relation to Actor
            Field Mapping uses symbolic key
                related.Actor = ActionTakenBy

    Conditions

        PhoneDisplayExecEntered
            default label is"PhoneDisplayInformationEntered"
            when (PhoneDisplayExec entered)

        TabletDisplayExecEntered
            default label is"TabletDisplayInformationEntered"
            when (TabletDisplayExec entered)

    Sets

        ByCategoryValueWorkunitActivity
            sql name is PfiQU01
            indexed
            Sort Order
                FilterValue
                PfiWorkunit
                PfiActivity

        ByDispStatusWorkunitActivity
            sql name is PfiQU02
            indexed
            Sort Order
                DispStatus
                PfiWorkunit
                PfiActivity

        ByCategoryValueDispStatusWorkunitActivity
            sql name is PfiQU03
            indexed
            Sort Order
                FilterValue
                DispStatus
                PfiWorkunit
                PfiActivity

        ByAppsFilterDispStatusWorkunitActivity
            sql name is PfiQU04
            indexed
            Sort Order
                AppsFilter
                DispStatus
                PfiWorkunit
                PfiActivity

        ByActivityName
            sql name is PfiQU05
            Sort Order
                PfiActivity.ActivityName
                PfiWorkunit
                PfiActivity

        ByActionTaken
            sql name is PfiQU06
            indexed
            Sort Order
                ActionTaken
                PfiWorkunit
                PfiActivity

        ByStartDate
            sql name is PfiQU07
            Sort Order
                PfiActivity.StartDate
                PfiWorkunit
                PfiActivity

        ByEndDate
            sql name is PfiQU08
            Sort Order
                PfiActivity.EndDate
                PfiWorkunit
                PfiActivity

        ByIsReplicatedUniqueID
            sql name is PfiQU09
            indexed
            Sort Order
                IsReplicated
                UniqueID

    Field Rules

        DispStatus
            if (DispStatus changed)
                invoke UpdateQueueTaskIsCurrentActionWaitingSnapshot

        CurrentAssignment
            if (CurrentAssignment changed)
                invoke UpdateQueueTaskIsCurrentActionWaitingSnapshot

        DisplayExec
            if (DisplayType.URL)
                constraint (DisplayExecAsString[1:7] = "http://" or DisplayExecAsString[1:8] = "https://")
                    "InvalidProtocolSpecifiedInContentURL"

        PhoneDisplayExec
            if (PhoneDisplayType.URL)
                constraint (PhoneDisplayExecAsString[1:7] = "http://" or PhoneDisplayExecAsString[1:8] = "https://")
                    "InvalidProtocolSpecifiedInContentURL"

        TabletDisplayExec
            if (TabletDisplayType.URL)
                constraint (TabletDisplayExecAsString[1:7] = "http://" or TabletDisplayExecAsString[1:8] = "https://")
                    "InvalidProtocolSpecifiedInContentURL"

    Actions

        Create is a Create Action
            Entrance Rules
                if (WorkDescription = blank)
                    WorkDescription = PfiWorkunit.WorkTitle

        Update is an Update Action

            Exit Rules
                IsReplicated = false

        Delete is a Delete Action

        UserActionRouting is an Update Action

        UpdateQueueTaskIsCurrentActionWaitingSnapshot is an Instance Action
            default label is untranslatable
            restricted
            Action Rules
                for each PfiQueueTaskRel
                    invoke BuildIsCurrentActionWaitingSnapshot each

        Cancel is an Instance Action

            Action Rules

                DispStatus = DispStatus.ActionCancelled
                for each PfiQueueTaskRel
                    invoke Cancel each

            Exit Rules
                IsReplicated = false
