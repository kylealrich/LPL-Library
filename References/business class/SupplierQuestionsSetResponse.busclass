SupplierQuestionsSetResponse is a BusinessClass
    owned by procurement
    prefix is SPQS

    Ontology
    	part of Supplier
    		relative key is SupplierQuestions
    
    Patterns
        implements TemplateDriven by SupplierQuestions
        	create instance
        		when (SupplierQuestionsSetAnswer entered)
        		
    Persistent Fields
		SupplierQuestionsSetAnswer
    	Attachment
   
	Local Fields
		LocalRepositoryQuestion		is a Question
		LocalSupplierGroupQuestion	is a Question
		LocalAnswerChanged			is Boolean
		LocalLastQuestion			is Numeric 2
	Transient Fields 
		FromConditionalQuestionAllowance is Boolean 
    Derived Fields
    	SupplierResponseAnswers is a ConditionalField
    		type is Alpha size 250
    		if (SupplierQuestions.ResponseType.Text)
    			SupplierQuestionsSetAnswer.TextValue
    		else
    		if (SupplierQuestions.ResponseType.Number)
    			SupplierQuestionsSetAnswer.NumericValue
    		else 
    		if (SupplierQuestions.ResponseType.Date)
    			DerivedDate
    		else
    		if (SupplierQuestions.ResponseType.List)
    			SupplierQuestionsSetAnswer.SupplierQuestionsListValue
    		else
    		if (SupplierQuestions.ResponseType.YesNo)
    			if (SupplierQuestionsSetAnswer.YesNoValue.Yes)
    				YesText
    			else
    				NoText
    		else
    			YesNoText
    			
    	YesNoText is a ConditionalField
			type is Alpha size 255
			restricted
			if (SupplierQuestionsSetAnswer.YesNoText.YesNo.Yes)
				YesText + ". " + SupplierQuestionsSetAnswer.YesNoText.Text
			else
				NoText + ". " + SupplierQuestionsSetAnswer.YesNoText.Text

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"

		DerivedDate is a StringField
			type is Alpha 10
			restricted
			SupplierQuestionsSetAnswer.DateValue month
			"/"
			SupplierQuestionsSetAnswer.DateValue day
			"/"
			SupplierQuestionsSetAnswer.DateValue year
			
		ResponseRequiredMessage is a MessageField
			restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
        	restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
        	restricted
            "YesOr_NoRequired;AttachmentRequiredIfAnswerIs_Yes"
            
        NoAttachmentMessage is a MessageField
        	restricted
            "YesOr_NoRequired;AttachmentRequiredIfAnswerIs_No"
            
        YesOrNoAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIs_Yes"
            
        NoAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIs_No"
            
        AttachmentAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;BothTextAndAttachmentAreRequiredIfAnswerIs_Yes"
            
        NoAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOr_NoRequired;BothTextAndAttachmentAreRequiredIfAnswerIs_No"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
            restricted
            if (SupplierQuestions.OnlyRequiredForFuture < Supplier.DateRegistered)
	            if (SupplierQuestions.ResponseRules.ResponseRequired
	            or  SupplierQuestions.YesNoResponseRules.ResponseRequired)
	                ResponseRequiredMessage        
	            else
	            if (SupplierQuestions.ResponseRules.ResponseRequiredAndAttachmentRequired
	            or  SupplierQuestions.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
	                ResponseAndAttachmentRequiredMessage
	            else
	            if (SupplierQuestions.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
	                YesAttachmentMessage
	            else
	            if (SupplierQuestions.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
	                NoAttachmentMessage
	            else
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
	                YesOrNoAndTextMessage
	            else
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
	                AttachmentOrTextMessage
	            else
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
	                YesAttachmentOrTextMessage
	            else
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
	                NoAttachmentOrTextMessage
	            else 
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
	                AttachmentAndTextMessage
	            else
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
	                YesAttachmentAndTextMessage
	            else
	            if (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
	                NoAttachmentAndTextMessage
	            else 
	            	blank
            else
                blank

    Conditions
    
        CanRespondToQuestion
        	restricted
        	when (Supplier.IsUpdatable
        	and   ActiveSupplierOrNotSupplier)
        
        ActiveSupplierOrNotSupplier
			restricted
			when ((!actor.agent(SupplierSourceId).SupplierSourceId.InactiveAtSomeLevel
			and   actor.agent(SupplierSourceId).SupplierSourceId exists)
			or    actor.agent(SupplierSourceId).SupplierSourceId !exists)
        
        HasResponseAttachment
        	restricted
            when (Attachment entered)
        
        AttachmentAlwaysRequired   
        	restricted
            when (SupplierQuestions.AlwaysRequireResponseAttachment
            and   SupplierQuestions.OnlyRequiredForFuture < Supplier.DateRegistered
            and  (SupplierQuestions.NonYesNoResponseType
            or    SupplierQuestions.ResponseType.YesNo))
            
       	QuestionAnswerEntered
       		restricted
       		when (SupplierQuestionsSetAnswer entered)
       	
       	QuestionAnswerChangedAndEntered
       		restricted
       		when (SupplierQuestionsSetAnswer entered
			and   SupplierQuestionsSetAnswer changed)

       	FromRequiredQuestion
       		restricted
       		when (SupplierQuestions.ResponseRequired
       		and   SupplierQuestions.OnlyRequiredForFuture < Supplier.DateRegistered)
       		
       	UnansweredRequired
       		restricted
       		when ((FromRequiredQuestion
       		and   !QuestionAnswerEntered)
       		or    (AttachmentAlwaysRequired
       		and   !HasResponseAttachment))
       		
       	Required
       		restricted
       		when (FromRequiredQuestion
       		or    AttachmentAlwaysRequired)

		ConditionalQuestionsExist 
			restricted
			when (((SupplierQuestions.ResponseType.YesNo
			or    SupplierQuestions.ResponseType.YesNoText)
			and   ConditionalYesNoQuestionRel exists)
			or   (SupplierQuestions.ResponseType.List
			and   ConditionalListQuestionRel exists))			


		SupplierQuestionsSetAnswerEntered 
			restricted
			when (SupplierQuestionsSetAnswer.TextValue					not = blank
			or    SupplierQuestionsSetAnswer.NumericValue				> 0
			or    SupplierQuestionsSetAnswer.DateValue					> 0
			or    SupplierQuestionsSetAnswer.SupplierQuestionsListValue	not = blank
			or    SupplierQuestionsSetAnswer.YesNoValue					> 0
			or    SupplierQuestionsSetAnswer.YesNoText.YesNo			> 0
			or    SupplierQuestionsSetAnswer.YesNoText.Text				not = blank)

		SupplierQuestionsSetAnswerOnlyATextChange
			restricted
			when (SupplierQuestions.ResponseType.YesNoText
			and   SupplierQuestionsSetAnswer.YesNoText.YesNo not changed
			and   SupplierQuestionsSetAnswer.YesNoText.Text changed)

    Relations
    	
    	SupplierGroupExtensionRel
			one-to-one relation to SupplierGroupExtension
			Field Mapping uses symbolic key
				related.SupplierGroup   = SupplierGroup

		LocalRepositoryQuestionRel 
			one-to-one relation to Question 
			Field Mapping uses symbolic key 
				related.ProcurementGroup	= SupplierGroup
				related.Question			= LocalRepositoryQuestion

		ActiveSupplierQuestionsRel 
			one-to-many relation to SupplierQuestions 
			Field Mapping uses symbolic key 
				related.SupplierGroup		= SupplierGroup
				related.Supplier			= Supplier 

		ConditionalListQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup			= SupplierGroup 
				related.Question					= SupplierQuestions.Question 
				related.QuestionAnswer				= SupplierQuestionsSetAnswer.SupplierQuestionsListValue 

		ConditionalYesNoQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses symbolic key 
				related.ProcurementGroup									= SupplierGroup 
				related.Question											= SupplierQuestions.Question 
			Instance Selection
				where ((SupplierQuestions.ResponseType.YesNo
				and	    related.ConditionalQuestion.QuestionAnswer.QuestionAnswerGroup.YesNoAnswer	= SupplierQuestionsSetAnswer.YesNoValue)		
				or     (SupplierQuestions.ResponseType.YesNoText
				and     related.ConditionalQuestion.QuestionAnswer.QuestionAnswerGroup.YesNoText.YesNo	= SupplierQuestionsSetAnswer.YesNoText.YesNo))

		CreatedFromThisQuestionRel 
			one-to-many relation to SupplierQuestions 
			Field Mapping uses symbolic key  
				related.SupplierGroup		= SupplierGroup
				related.Supplier			= Supplier
			Instance Selection 
				where (related.CreatedFromQuestion	= SupplierQuestions)			

		CreatedFromThisQuestionAnsweredRel
			one-to-many relation to SupplierQuestions 
			Field Mapping uses symbolic key  
				related.SupplierGroup		= SupplierGroup
				related.Supplier			= Supplier
			Instance Selection
				where (related.CreatedFromQuestion = SupplierQuestions
				and    related.AnyQuestionResponses)	

	Field Rules
		Attachment
         	if (!SupplierQuestionsSetAnswer entered)
                cannot be entered
					"MustAnswerQuestionToAttachADocument"			

    Rule Blocks
		UpdateValidationEmail
			send email
				to SupplierGroup.NotificationEmailAddress
				from SupplierGroup.AdminEmailAddress
				cc Supplier.PrimaryContact.EmailAddress
				subject "<SupplierGroupExtensionRel.FinalUpdatedValidationEmailSubject>"
				Contents
					"<SupplierGroupExtensionRel.FinalUpdatedValidationEmailContent>"
					"QuestionInformationWasModifiedOn<update stamp>"

		CreateConditionalSupplierQuestions

			if ((SupplierQuestions.ResponseType.YesNo
			or   SupplierQuestions.ResponseType.YesNoText)
			and  ConditionalYesNoQuestionRel exists)
				for each ConditionalYesNoQuestionRel
					LocalRepositoryQuestion = each.ConditionalQuestion
					invoke AttachToSupplierQuestionsIndirect LocalRepositoryQuestionRel
						invoked.SupplierGroup				= SupplierGroup
						invoked.Supplier					= Supplier
						invoked.ParmFromQuestion			= LocalRepositoryQuestion
						invoked.ParmCreatedFromQuestion		= SupplierQuestions
						invoked.ParmSupplierGroupQuestion	= SupplierQuestions.SupplierGroupQuestion

			if (SupplierQuestions.ResponseType.List
			and ConditionalListQuestionRel exists)
				for each ConditionalListQuestionRel
					LocalRepositoryQuestion = each.ConditionalQuestion
					invoke AttachToSupplierQuestionsIndirect LocalRepositoryQuestionRel
						invoked.SupplierGroup		      	= SupplierGroup
						invoked.Supplier					= Supplier
						invoked.ParmFromQuestion			= LocalRepositoryQuestion
						invoked.ParmCreatedFromQuestion		= SupplierQuestions
						invoked.ParmSupplierGroupQuestion	= SupplierQuestions.SupplierGroupQuestion

		EditsForSupplierQuestionsSetAnswerField

            if (SupplierQuestions.OnlyRequiredForFuture < Supplier.DateRegistered)
	            if (SupplierQuestions.ResponseRequired)
					constraint (SupplierQuestionsSetAnswer entered)
                    	"MustAnswerAllRequiredQuestions"
	            if (SupplierQuestions.ResponseType.YesNoText
	            and !HasResponseAttachment
	            and (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
	            or  (SupplierQuestionsSetAnswer.YesNoText.YesNo.Yes
	            and  SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
	            or  (SupplierQuestionsSetAnswer.YesNoText.YesNo.No
	            and  SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
	                constraint (SupplierQuestionsSetAnswer.YesNoText.Text entered)
	                    "MustEnterEitherTextOrAnAttachmentForQuestion<SupplierQuestions.QuestionText>"
	                    
	            if (SupplierQuestions.ResponseType.YesNo
	            and (SupplierQuestionsSetAnswer.YesNoValue.Yes
	            and  SupplierQuestions.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
	            or  (SupplierQuestionsSetAnswer.YesNoValue.No
	            and  SupplierQuestions.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SupplierQuestions.QuestionText>"    
	            
	            if (AttachmentAlwaysRequired
	            and !SupplierQuestions.ResponseType.YesNoText)
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SupplierQuestions.QuestionText>"
	                       
	            if  (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
	            or   SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SupplierQuestionsSetAnswer.YesNoText.YesNo.Yes
	            and  SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SupplierQuestionsSetAnswer.YesNoText.YesNo.No
	            and  SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (SupplierQuestionsSetAnswer.YesNoText.Text entered)
	                    "MustEnterTextForQuestion<SupplierQuestions.QuestionText>"
	                    
	            if  (SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
	            or  (SupplierQuestionsSetAnswer.YesNoText.YesNo.Yes
	            and  SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
	            or  (SupplierQuestionsSetAnswer.YesNoText.YesNo.No
	            and  SupplierQuestions.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
	                constraint (HasResponseAttachment)
	                    "AttachmentIsRequiredForQuestion<SupplierQuestions.QuestionText>"

            	if (SupplierQuestions.AlwaysRequireResponseAttachment)
            		constraint (Attachment entered)
                    	"MustAttachADocumentForAllQuestionsRequiringAnAttachment"


    Actions
		Create is a Create Action
     		valid when (CanRespondToQuestion)

			Action Rules
				if (ConditionalQuestionsExist)
					confirmation required
						"AdditionalQuestionsWillBeCreatedAsAResultOfYourAnswers;PleaseRefreshScreenToViewNewQuestionsIfTheyDoNotAppear"

            	if (!FromConditionalQuestionAllowance)
					include EditsForSupplierQuestionsSetAnswerField

				   								
   			Exit Rules
				if (ConditionalQuestionsExist)
					include CreateConditionalSupplierQuestions

   				if (Supplier.SupplierStatus.Validated
				and SupplierGroup.RequireUpdatedSupplierValidation)
					invoke StatusUpdate Supplier
 					
 					include UpdateValidationEmail
 											
     	Update is an Update Action
			valid when (CanRespondToQuestion)

			Action Rules
				if (SupplierQuestions.YesNoOrList
				and	SupplierQuestionsSetAnswer changed
				and !SupplierQuestionsSetAnswerOnlyATextChange)
					LocalAnswerChanged = true
					constraint (CreatedFromThisQuestionAnsweredRel !exists)
						"CannotChangeAnswer;ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichHaveBeenAnswered"
					if (CreatedFromThisQuestionRel exists)
						confirmation required 
							"ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichWillBeDeleted;RefreshScreenToViewRemainingQuestions"	
						LocalLastQuestion = last CreatedFromThisQuestionRel.SupplierQuestions

					if (ConditionalQuestionsExist)
						confirmation required
							"AdditionalQuestionsWillBeCreatedAsAResultOfYourChangeToAnswers;RefreshScreenToViewNewQuestions"				   								

            	include EditsForSupplierQuestionsSetAnswerField

			Exit Rules
				if (SupplierQuestions.YesNoOrList
				and	LocalAnswerChanged
				and QuestionAnswerChangedAndEntered
				and ConditionalQuestionsExist)
					include CreateConditionalSupplierQuestions

				if (CreatedFromThisQuestionRel exists)
					invoke DeleteConditionalSupplierQuestions SupplierQuestions
						invoked.ParmSupplierGroup		= SupplierGroup
						invoked.ParmSupplier			= Supplier
						invoked.ParmOriginalQuestion    = SupplierQuestions 
						invoked.ParmLastOldQuestion     = LocalLastQuestion

				if (Supplier.SupplierStatus.Validated
				and SupplierGroup.RequireUpdatedSupplierValidation
				and SupplierQuestionsSetAnswer changed)
					invoke StatusUpdate Supplier
 					
 					include UpdateValidationEmail
     						
     	Delete is a Delete Action
			valid when (CanRespondToQuestion)

			Entrance Rules 
				if (SupplierGroup.UseConditionalQuestions)
					constraint (SupplierQuestionsSetAnswerEntered)
 	                    "CannotRemoveResponse"
				if (SupplierQuestions.YesNoOrList)
					constraint (CreatedFromThisQuestionAnsweredRel !exists)
						"CannotDeleteAnswer;ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichHaveBeenAnswered"
					if (CreatedFromThisQuestionRel exists)
						confirmation required 
							"ConditionalQuestionsHaveBeenCreatedFromThisAnswerWhichWillBeDeleted;RefreshScreenToViewRemainingQuestions"	
						for each CreatedFromThisQuestionRel
							invoke Delete each

			Exit Rules
     			if (Supplier.SupplierStatus.Validated
				and SupplierGroup.RequireUpdatedSupplierValidation)
					invoke StatusUpdate Supplier
 					
 					include UpdateValidationEmail
