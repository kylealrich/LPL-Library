IONConnection is a BusinessClass
    owned by la

    store using com.lawson.rdtech.framework.ion.IONConnectionStorage
    
    Ontology
        symbolic key is IONConnection

    Patterns
    	implements ReadOnly
    	disable UniqueID
    	 
    Persistent Fields
    	Description is Alpha size 100
    	CanonicalName is Alpha size 20
    	SupportedVersions is Alpha size 100
    	ConnectionString is Alpha size 200
    	SchemaName is Alpha size 100
    	EndPoint is Alpha size 500
			default label is "Endpoint"
    	Version is Alpha size 100
    	LogicalId is Alpha size 250
    	SecondaryLogicalId is Alpha size 250
    	BaseEnvironment is Boolean
    		restricted
    	Tenant is Alpha size 100
    	Status is Numeric size 1 
            States
            	Unknown                    value is 0            	
            	Healthy                    value is 1
            	Unhealthy				   value is 2
            	TimedOut				   value is 3
				Failed	            	   value is 4
				NotConfigured			   value is 5    
				Configured 				   value is 6     	

        ErrorCode is Numeric size 3
		StatusMessage is Text
		DBSchema	   is Alpha size 100
			default label is "DatabaseSchema"
		DBType			is Numeric size 2
			default label is "DatabaseType"
			States
				SQLServer2000		  value is 4 
				SQLServer2008	 	  value is 14
				SQLServer2016         value is 19
				Oracle                value is 2
				PostgreSQL            value is 17
					default label is "Postgre\SQL"
					
				
		DBName 			is Alpha size 100
			default label is "DatabaseName"
		DBServer		is Alpha size 200
			default label is "DatabaseServer"
		DBPort			is Numeric size 5
			default label is "DatabasePort"
		MultiSubnet		is Boolean
		MSJDBCDir		is Alpha size 200
			default label is "MSJDBC_Directory"
		User			is Alpha size 100
			default label is "Username"
		Password 		is Password
		AlertMessage    is Alpha size 400
			restricted
		Alert			is Numeric size 1
			restricted
			States
				None	value is 0
				Red		value is 1
				Yellow  value is 2
				Green   value is 3
				Blue    value is 4
    	OAuth is like OAuth
			default label is "OpenAuthorization"
    	Default is Boolean
    	IsMultiTenant is Boolean
    		restricted
    	HasActions is Boolean
    		restricted
    	Usage is Numeric size 1
    		restricted
    		States
    			None value is 0
    			Outbox value is 1
    	DefaultAction is an IONConnectionAction
    	InSweepClass is a BusinessView
    		default label is "InboxSweepClass"
    		restricted
    	InSweepAction is a BusinessAction
    		restricted
    		context of InSweepClass
    	OutSweepClass is a BusinessView
    		default label is "OutboxSweepClass"
    		restricted
    	OutSweepAction is a BusinessAction
    		restricted 
    		context of OutSweepClass
    	MaxRetries is Numeric size 6	
    	
    	HasTestTenant is Boolean
    		restricted
							
	Derived Fields
	    ConnectionTypeMapping is a DerivedField
	        type is Numeric 1
	        if (IONConnection = 8)
	            return 1
	        else
	            return 0

		IsIMS is a DerivedField
			type is Boolean
			default label is "IMS"
			if (IONConnection = 2)
				return true
			else
				return false				
			
		IsIOBox is a DerivedField
	        type is Boolean
			default label is "IOB\ox"
			if (IONConnection = 1)
				return true
			else
				return false
				
		IsPulse is a DerivedField
			type is Boolean
			default label is "Pulse"
			if (IONConnection = 5)
				return true
			else
				return false
				
		IsIonDataCatalog is a DerivedField
			type is Boolean
			default label is "ION_DataCatalog"
			if (IONConnection = 3)
				return true
			else
				return false
				
		ConfigurableTenant is a NativeField
			type is Boolean
			restricted
			
		UpperCaseDataArea is a NativeField
			type is DataAreaName
			restricted

			
		ProductLine is a NativeField
			type is ProductLineName
				
		FailedTriggersExist is a DerivedField
			type is Boolean
			for each AsyncRequestsRel
				if (each.FailedTriggersExist)
					return true

			return false		
			
		IsOutboundRequestScheduled is a DerivedField
			type is Boolean
			default label is "RequestScheduled"
			if (HasOutbound)
                for each AsyncRequestOutRel
                    if (not each.PendingScheduling)
                        return false
					
			return true

		IsInboundRequestScheduled is a DerivedField
			type is Boolean
			default label is "RequestScheduled"
			if (HasInbound)
                for each AsyncRequestInRel
                    if (not each.PendingScheduling)
                        return false

			return true



	Local Fields
		LocalAsyncActionRequest is an AsyncActionRequest
	
	Conditions
	    UsesOutboxQueue
	        when (IONConnection = 1
	        or    IONConnection = 2
	        or    IONConnection = 4)

	    UsesDataLakeQueue
	        when (IONConnection = 7
	        or    IONConnection = 8)

		HasVersionInformation
			default label is "VersionInformationExists"
			when (Version != blank)

		HasSupportedVersions
			default label is "SupportedVersionsExist"
			when (SupportedVersions != blank)
			
		ValidTestTenant
			restricted
			when (HasTestTenant and not Status.NotConfigured)
		HasAsyncRequests
			default label is "AsyncRequestsRelationExists"
			when (AsyncRequestsRel exists)
			
		IsIMSConfigurationValid
			default label is "IMSConfigurationValid"
			when (IsIMS and not IsMultiTenant)			
			
		IsIOBoxConfigurationValid
			default label is "IOB\oxConfigurationValid"
			when (IsIOBox and not IsMultiTenant)			
			
		IsDataCatalogConfigurationValid
			default label is "DataCatalogConfigurationValid"
			when (IsIonDataCatalog and not IsMultiTenant)	
	
		DocumentNameRequired 
			when (IsIMS or IsIOBox)
			
		ActionRequired
			when (IsPulse)	
			
		FailedIMS
			when (IsIMS and FailedTriggersExist)
			
		FailedIOBox 
			default label is "FailedIO\Box"
			when (IsIOBox and FailedTriggersExist)

		HasInbound
		    restricted
		    when (InSweepClass != blank
		    and   InSweepAction != blank)
			
		HasOutbound
		    restricted
		    when (OutSweepClass != blank
		    and   OutSweepAction != blank)




	Relations
		AsyncRequestOutRel
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
				related.DataArea = UpperCaseDataArea
				related.ImplementingClass = OutSweepClass
				related.AsyncAction = OutSweepAction

		AsyncRequestInRel
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
				related.DataArea = UpperCaseDataArea
				related.ImplementingClass = InSweepClass
				related.AsyncAction = InSweepAction
			
		AsyncRequestsRel
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
			Instance Selection
				where ((related.ImplementingClass = InSweepClass
				and     related.AsyncAction       = InSweepAction)
				or     (related.ImplementingClass = OutSweepClass
				and     related.AsyncAction       = OutSweepAction))

		SweepRequestsRel
			one-to-many relation to AsyncActionRequest
			Field Mapping uses ByDataAreaAction
			Instance Selection
				where (related.ImplementingClass = OutSweepClass
				and    related.AsyncAction       = OutSweepAction)

		IONOutboxQueueRel
		    one-to-many relation to IONOutboxQueue
		    Field Mapping uses ByLogicalId
		    Instance Selection
		        where (related.IONConnection = IONConnection)

		DataLakeQueueRel
		    one-to-many relation to DataLakeQueue
		    Field Mapping uses ByProcessingOrder
		    Instance Selection
		        where ((related.Status = related.Status.New
		        or      related.Status = related.Status.Failed)
		        and     related.Type = ConnectionTypeMapping)
				
	Actions
		UpdateAsyncRequest is an Instance Action
			restricted
			manual update
			
			Action Rules
				for each AsyncRequestOutRel
					if (each.SystemRequest = 1)
						invoke SetPendingScheduling each
							invoked.ParamPendingScheduling = true
	
		RequeueAsyncRequests is an Instance Action
			valid when (FailedTriggersExist)
			manual update
			Action Rules
				for each AsyncRequestsRel
					if (each.FailedTriggersExist)
						invoke RequeueFailedTriggers each
		
		ScheduleOutBoundAsyncRequest is an Instance Action
			default label is "ScheduleOutboundAsyncRequest"
			valid when (not IsOutboundRequestScheduled)
			manual update
			Action Rules

				if (OutSweepClass exists
				and OutSweepAction exists
				and AsyncRequestOutRel exists)
					for each AsyncRequestOutRel
						invoke SetPendingScheduling each
							invoked.ParamPendingScheduling = true
					
		ScheduleInbBoundAsyncRequest is an Instance Action
			default label is "ScheduleInboundAsyncRequest"
			valid when (not IsInboundRequestScheduled)
			manual update
			Action Rules

				if (InSweepClass exists
				and InSweepAction exists
				and AsyncRequestInRel exists)
					LocalAsyncActionRequest = first AsyncRequestInRel.AsyncActionRequest
	
					if (LocalAsyncActionRequest exists)
						invoke SetPendingScheduling LocalAsyncActionRequest
							invoked.ParamPendingScheduling = true
					
		SendNow is an Instance Action
			restricted
			manual update
			Parameters
				RecordID is an IONOutboxQueue
			
		TestTenant is an Instance Action
			valid when (ValidTestTenant)
			manual update
				
		ConfigureIONDataCatalogConnection is an Instance Action
			default label is "ConfigureConnection"
			valid when (IsDataCatalogConfigurationValid)
			manual update
			Parameters
				PEndpoint				is Alpha size 200
					default label is "Endpoint"
				PTenant					is Alpha size 100
					default label is "Tenant"
				IONAPICredentials			is BinaryDocument
					default label is "IONAPI_Credentials"
				
			Parameter Rules
				PEndpoint
					initial value is EndPoint
					
				PTenant
					initial value is Tenant
				
		ConfigureIMSConnection is an Instance Action
			default label is "ConfigureConnection"
			valid when (IsIMSConfigurationValid)
			manual update
			Parameters
				PEndpoint				is Alpha size 200
					default label is "Endpoint"
				PTenant					is Alpha size 100
					default label is "Tenant"
				IONAPICredentials			is BinaryDocument
					default label is "IONAPI_Credentials"
				
			Parameter Rules
				PEndpoint
					initial value is EndPoint
					
				PTenant
					initial value is Tenant
										
		ConfigureIOBoxConnection is an Instance Action
			default label is "ConfigureConnection"
			valid when (IsIOBoxConfigurationValid)
			manual update
			
			Parameters
				ParamDBType is Numeric size 2
					States
						SQlServer2000		  value is 4 
							default label is "SQLServer2000"
						SQLServer2008	 	  value is 14
						SQLServer2016         value is 19
						Oracle                value is 2
						PostgreSQL            value is 17
							default label is "Postgre\SQL"
					default label is "DatabaseType"
						
				ParamDBName 		is Alpha size 100
					default label is "DatabaseName"
				ParamDBServer		is Alpha size 200
					default label is "DatabaseServer"
				ParamDBSchemaName	is Alpha size 100
					default label is "DatabaseSchemaName"
				ParamDBPort			is Numeric size 5
					default label is "DatabasePort"
				ParamMultiSubnet	is Boolean
					default label is "MultiSubnet" 
				ParamMSJDBCDir		is Alpha size 200
					default label is "MSJDBC_Directory" 
				ParamUser			is Alpha size 100
					default label is "Username" 
				ParamPassword 		is Password
					default label is "Password" 
				ParamTenant			is Alpha size 20
					default label is "Tenant" 
			
			Parameter Rules
				ParamTenant
					initial value is Tenant
					
				ParamDBType
					initial value is DBType
					
				ParamDBName
					initial value is DBName
					
				ParamDBServer
					initial value is DBServer
					
				ParamDBSchemaName
					initial value is DBSchema
					
				ParamDBPort
					initial value is DBPort
					
				ParamMultiSubnet
					initial value is MultiSubnet
					
				ParamMSJDBCDir
					initial value is MSJDBCDir
					
				ParamUser
					initial value is User
					
				ParamPassword
					initial value is Password
					
