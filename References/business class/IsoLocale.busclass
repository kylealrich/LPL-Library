IsoLocale is a BusinessClass
    owned by i18n
    store using com.lawson.rdtech.framework.i18n.IsoLocaleStorage
    
    Ontology
        symbolic key is IsoLocale

    Patterns
    	implements ReadOnly
 
    Derived Fields
        LocaleName is a NativeField
            type is Alpha size up to 240
            
		ActiveForAnalytics is a NativeField
			type is Boolean        
            
	Actions
		ActivateForAnalytics is an Instance Action
			manual update
			valid when (not ActiveForAnalytics)
			
			Local Fields
				ConfigurationParameter
				
			Action Rules
				ConfigurationParameter.ConfigurationID = com.lawson.apps.i18n.IsoLocale_ActiveForAnalytics.getConfigName()
				ConfigurationParameter.Name = IsoLocale
			
				if (ConfigurationParameter exists)
					invoke Update ConfigurationParameter
						invoked.Type = 8233
						invoked.Value = "true"
				else
					invoke Create ConfigurationParameter
						invoked.Type = 8233
						invoked.Value = "true"
						
			Exit Rules
				invoke FireConfigReload
			
		DeactivateForAnalytics is an Instance Action
			manual update
			valid when (ActiveForAnalytics)
			
			Local Fields 
				ConfigurationParameter
				
			Action Rules
				ConfigurationParameter.ConfigurationID = com.lawson.apps.i18n.IsoLocale_ActiveForAnalytics.getConfigName()
				ConfigurationParameter.Name = IsoLocale

				if (ConfigurationParameter exists)
					invoke Delete ConfigurationParameter
									
			Exit Rules
				invoke FireConfigReload
				
		FireConfigReload is an Instance Action
			restricted
			manual update
			

							       
