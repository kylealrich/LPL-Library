ItemMasterSyncReport is a BusinessClass
	owned by ic
	
	prefix is IMSR
	
	Ontology
		symbolic key is ItemMasterSyncReport
	
	Patterns
	
	Persistent Fields
		ItemGroup
		Item
		OldDescription1				 is a Description
		OldDescription2				 is a Description
		OldDescription3				 is a Description3
		ReplacedDescription1		 is a Description
		ReplacedDescription2		 is a Description
		ReplacedDescription3		 is a Description3
		OldManufacturer				 is a Manufacturer
		OldManufacturerNumber		 is a ManufacturerNumber
		Manufacturer
		ManufacturerNumber
	
	Conditions
	
		ContractsFound
			when (ContractLinesRel exists)

		InterfacesFound
			restricted
			when (ContractInterfaceLinesRel exists)
			
		GTINsFound
			restricted
			when (ItemGTINRel exists)
	
	Relations
	
		ItemLocationRel
			one-to-many relation to ItemLocation				
			Field Mapping uses ByItem
				related.Item = Item
			Instance Selection
				where (related.Item.ItemGroup = ItemGroup)
				
		VendorItemRel
            one-to-many relation to VendorItem
            Field Mapping uses symbolic key
                related.ProcurementGroup	= ItemGroup
                related.Item				= Item
                
    	ContractLinesRel
    		one-to-many relation to ContractLine
    		Field Mapping uses ByVendorItemOpen
    			related.ContractGroup = ItemGroup	
				related.ItemNumber	  = Item
    		Instance Selection
    			where (!related.ContractLineState.Closed)

		ContractInterfaceLinesRel
   			one-to-many relation to ContractLineImport
    		Field Mapping uses ByItemVendorItem
    			related.ContractGroup = ItemGroup	
				related.ItemNumber	  = Item
				
		ItemGTINRel
			one-to-many relation to ItemGTIN
			Field Mapping uses symbolic key
				related.ItemGroup     = ItemGroup
				related.Item          = Item			
	
	Sets
	
		ByItem
			Sort Order
				Item
				Actor
				BatchNumber
				ItemMasterSyncReport
				
		ByActorBatchItem
			Sort Order
				Actor
				BatchNumber
				Item
				ItemMasterSyncReport
	
	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
				
