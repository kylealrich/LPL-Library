EDIDataSubstitutionTable is a BusinessClass
    owned by edi
    prefix is EDS
    classic name is EDSUBTBL

    Ontology
        symbolic key is EDIDataSubstitutionTable
            classic set name is EDSSET1

    Patterns
        implements StaticJava
        disable AuditIndex

    Relations
        ExternalToListNameLawsonValueRel
            one-to-many relation to EDIDataSubstitutionTable
            Field Mapping uses ByLawsonValue
                related.EDIDataSubstitutionTable.EdilistName = EDIDataSubstitutionTable.EdilistName
                related.EDIDataSubstitutionTable.LawsonValue = EDIDataSubstitutionTable.LawsonValue
            Instance Selection
                where (related.EDIDataSubstitutionTable.ExternalValue != EDIDataSubstitutionTable.ExternalValue)
                
        LawsonToListNameExternalValueRel
            one-to-many relation to EDIDataSubstitutionTable
            Field Mapping uses ByExternalValue
                related.EDIDataSubstitutionTable.EdilistName = EDIDataSubstitutionTable.EdilistName
                related.EDIDataSubstitutionTable.ExternalValue = EDIDataSubstitutionTable.ExternalValue
            Instance Selection
                where (related.EDIDataSubstitutionTable.LawsonValue != EDIDataSubstitutionTable.LawsonValue)


    Derived Fields
        DerivedTpid is a DerivedField
            type is AlphaUpper size 10
            restricted
            Index = 0
            while (Index <10)
                if (EDIDataSubstitutionTable.EdilistName[Index] = "_")
                    return EDIDataSubstitutionTable.EdilistName[0:Index-1]
                    end while
                else
                    Index = Index +1
            return ""
    
    
    Local Fields
        Index               is Numeric size 2
        LastIndex           is Numeric size 2
        FieldCount          is Numeric size 1
        LocalEdilistName    is Alpha size 80
        LocalLawsonValue    is Alpha size 80
        LocalExternalValue  is Alpha size 80
        LocalCompany        is like Company
        LocalVendor         is like Vendor
        LocalShipto         is like Location
        LocalPurchFrom      is like VendorLocation
            
    Rule Blocks
 
        GetShipToValue
            Index = 0
            LastIndex = 0
            while (Index < 80)
                if (this instance.EDIDataSubstitutionTable.LawsonValue[Index] = "+")
                    LocalShipto = this instance.EDIDataSubstitutionTable.LawsonValue[0:Index-1]
                    end while
                else
                    Index = Index +1

        GetVendorValue
            Index = 0
            LastIndex = 0
            FieldCount = 1
            while (Index < 80)
                if (this instance.EDIDataSubstitutionTable.LawsonValue[Index] = "+")
                    if (FieldCount = 2)
                        LocalVendor = this instance.EDIDataSubstitutionTable.LawsonValue[LastIndex:Index-1]
                        end while
                    else
                        FieldCount = FieldCount+1
                        Index = Index+1
                        LastIndex = Index 
                else 
                    Index = Index+1
                    
        GetPurchFromValue
            Index = 0
            LastIndex = 0
            FieldCount = 1
            while (Index < 80)
                if (this instance.EDIDataSubstitutionTable.LawsonValue[Index] = "+")
                    if (FieldCount = 3)
                        if (Index - LastIndex > 0) 
                            LocalPurchFrom = this instance.EDIDataSubstitutionTable.LawsonValue[LastIndex:Index-1]
                            end while
                        else
                            end while
                    else
                        FieldCount = FieldCount+1
                        Index = Index+1
                        LastIndex = Index 
                else 
                    Index = Index+1

        GetCompanyValue
            Index = 80
            LastIndex = 80
            while (Index > 0)
                if (this instance.EDIDataSubstitutionTable.LawsonValue[Index] = "+")
                    LocalCompany = this instance.EDIDataSubstitutionTable.LawsonValue[Index+1:80]
                    end while
                else
                    Index = Index -1 

        GetInternalValues
            include GetCompanyValue
            include GetVendorValue
            include GetShipToValue
            include GetPurchFromValue
           
    Sets
        ByExternalValue
            Sort Order
                EDIDataSubstitutionTable.EdilistName
                EDIDataSubstitutionTable.ExternalValue
                EDIDataSubstitutionTable.LawsonValue

        ByLawsonValue
            Sort Order
                EDIDataSubstitutionTable.EdilistName
                EDIDataSubstitutionTable.LawsonValue
                EDIDataSubstitutionTable.ExternalValue

    Conditions
        IsAShiptoRecord
            when (EDIDataSubstitutionTable.EdilistName matches "\w+_O_SHIPTO")
            
        HasManyExternalToOneLawsonValue
            when ( EDIDataSubstitutionTable.ExternalToListNameLawsonValueRel exists)

        HasManyLawsonToOneExternalValue
            when ( EDIDataSubstitutionTable.LawsonToListNameExternalValueRel exists)

    Actions
        Create is a Create Action
        Delete is a Delete Action

        CreateInboundCompanyAndVendor is an Instance Action
            default label is "CreateI\_\CompanyAndI\_\Vendor"
            valid when (IsAShiptoRecord)
            
            Entrance Rules
                constraint (DerivedTpid !="")
                    "UnableToDeriveTpid"            
            
            Action Rules
                invoke CreateICompany
                invoke CreateIVendor

        CreateICompany is an Instance Action
            default label is "Create_I\_Company"
            valid when (IsAShiptoRecord)
            Entrance Rules
                constraint (DerivedTpid !="")
                    "UnableToDeriveTpid"            
        
            Action Rules
                include GetCompanyValue
                invoke Create EDIDataSubstitutionTable
                    invoked.EDIDataSubstitutionTable.EdilistName     = DerivedTpid + "_I_COMPANY"
                    invoked.EDIDataSubstitutionTable.LawsonValue     = LocalCompany
                    invoked.EDIDataSubstitutionTable.ExternalValue   = EDIDataSubstitutionTable.ExternalValue
                        
        CreateIVendor is an Instance Action
            default label is "Create_\_I\_Vendor"
            valid when (IsAShiptoRecord)
            Entrance Rules
                constraint (DerivedTpid !="")
                    "UnableToDeriveTpid"            
        
            Action Rules
                include GetVendorValue
                invoke Create EDIDataSubstitutionTable
                    invoked.EDIDataSubstitutionTable.EdilistName     = DerivedTpid + "_I_VENDOR"
                    invoked.EDIDataSubstitutionTable.LawsonValue     = LocalVendor
                    invoked.EDIDataSubstitutionTable.ExternalValue   = EDIDataSubstitutionTable.ExternalValue
            
        CreateIShipTo is an Instance Action
            default label is "Create_\_I\_ShipTo"
            valid when (IsAShiptoRecord)
            Entrance Rules
                constraint (DerivedTpid !="")
                    "UnableToDeriveTpid"            
        
            Action Rules
                include GetShipToValue
                invoke Create EDIDataSubstitutionTable
                    invoked.EDIDataSubstitutionTable.EdilistName     = DerivedTpid + "_I_SHIPTO"
                    invoked.EDIDataSubstitutionTable.LawsonValue     = LocalShipto
                    invoked.EDIDataSubstitutionTable.ExternalValue   = EDIDataSubstitutionTable.ExternalValue
                        
        CreateIPurchFrom is an Instance Action
            default label is "Create_\_I\_PurchFrom"
            valid when (IsAShiptoRecord)
            Entrance Rules
                constraint (DerivedTpid !="")
                    "UnableToDeriveTpid"            
        
            Action Rules
                include GetPurchFromValue
                invoke Create EDIDataSubstitutionTable
                    invoked.EDIDataSubstitutionTable.EdilistName     = DerivedTpid + "_I_PURCHFROM"
                    invoked.EDIDataSubstitutionTable.LawsonValue     = LocalPurchFrom
                    invoked.EDIDataSubstitutionTable.ExternalValue   = EDIDataSubstitutionTable.ExternalValue


        Edit is an Instance Action
            disable multiple instance selection
            Parameters
                PrmEdilistName is Alpha size 80
                    default label is "ListName"
                PrmLawsonValue  is Alpha size 80
                    default label is "YourValue"
                PrmExternalValue is Alpha size 80
                    default label is "PartnerValue"

            Parameter Rules
                PrmEdilistName
                    required
                    initial value is EDIDataSubstitutionTable.EdilistName
                    default to EDIDataSubstitutionTable.EdilistName
                    
                PrmLawsonValue
                    required 
                    initial value is EDIDataSubstitutionTable.LawsonValue
                    default to EDIDataSubstitutionTable.LawsonValue
                PrmExternalValue
                    required
                    initial value is EDIDataSubstitutionTable.ExternalValue
                    default to EDIDataSubstitutionTable.ExternalValue

            Action Rules
                invoke Delete EDIDataSubstitutionTable
                invoke Create EDIDataSubstitutionTable
                    invoked.EDIDataSubstitutionTable.EdilistName     = PrmEdilistName
                    invoked.EDIDataSubstitutionTable.LawsonValue     = PrmLawsonValue
                    invoked.EDIDataSubstitutionTable.ExternalValue   = PrmExternalValue

            
        NewFromSelected is an Instance Action
            disable multiple instance selection
            Parameters
                PrmEdilistName is Alpha size 80
                    default label is "NewListName"
                PrmLawsonValue  is Alpha size 80
                    default label is "(New)YourValue"
                PrmExternalValue is Alpha size 80
                    default label is "(New)PartnerValue"

            Parameter Rules
                PrmEdilistName
                    required
                    initial value is EDIDataSubstitutionTable.EdilistName
                    default to EDIDataSubstitutionTable.EdilistName
                    
                PrmLawsonValue
                    required 
                    initial value is EDIDataSubstitutionTable.LawsonValue
                    default to EDIDataSubstitutionTable.LawsonValue
                PrmExternalValue
                    required
                    initial value is EDIDataSubstitutionTable.ExternalValue
                    default to EDIDataSubstitutionTable.ExternalValue
                
            Action Rules
                invoke Create EDIDataSubstitutionTable
                    invoked.EDIDataSubstitutionTable.EdilistName     = PrmEdilistName
                    invoked.EDIDataSubstitutionTable.LawsonValue     = PrmLawsonValue
                    invoked.EDIDataSubstitutionTable.ExternalValue   = PrmExternalValue
                    
