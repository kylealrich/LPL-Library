CashLedgerAutoTranNumber is a BusinessClass
	owned by cb
	
	prefix is CLATN
	
	Ontology
		symbolic key is CashLedgerAutoTranNumber
		
	
	Patterns
		implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       	
	Persistent Fields
		OriginatingTransaction					is BusinessObjectReference
		CreateOperator							is an Operator 
			holds pii
		CreateDate								is TimeStamp
		DeleteOperator							is an Operator 
			holds pii
		DeleteDate								is TimeStamp
		CreationType							is Numeric size 1
			States
				AutoCreated						value is 1
				ManuallyCreated					value is 2
		AutoNumberStatus						is Numeric size 2
			States
				RelatedDocumentDeleted			value is 1
				CannotBeAssignedAlreadyInUse	value is 2
				NumberAssignedToDocument		value is 3
				LastNumberUsedApplied			value is 4
		AutoNumberBy							is Numeric size 2
			States
				Company							value is 1
				CashCode						value is 2
				BankTransactionCode				value is 3
				CashCodeTranCode				value is 4
		DocumentCashCode						is like CashCode
		DocumentBankTransactionCode				is like BankTransactionCode 
		
	Local Fields
		DocumentUniqueId						is like UniqueID
							
	Derived Fields
		DerivedDocumentNumberType				is a DerivedField
			type is Alpha size up to 20
        	restricted
			if (CashLedgerDocumentType.CashLedgerPayment 
			or  CashLedgerDocumentType.ElectronicFundTransfer)
				return ReferenceNumberMessage
			if (CashLedgerDocumentType.ReturnedPayment
			or  CashLedgerDocumentType.CashLedgerTransaction 
			or  CashLedgerDocumentType.BankFundTransfer)
				return TransactionNumberMessage
			if (CashLedgerDocumentType.CashReceipt)
				return DepositNumberMessage 
			
		TransactionNumberMessage 			is a MessageField
			restricted
			"TransactonNumber"
		ReferenceNumberMessage 				is a MessageField
			restricted
			"ReferenceNumber"
		DepositNumberMessage 				is a MessageField
			restricted
			"DepositNumber"					
		
	Transient Fields
		LastNumberUsed						is like CashLedgerAutoTranNumber		
		AutoTransactionNumber				is like CashLedgerAutoTranNumber		
			derive value from CashLedgerAutoTranNumber
						
	Conditions
		IsAutoNumberByCompany
			restricted
			when (AutoNumberBy = AutoNumberBy.Company)
			
		IsAutoNumberByCashCode
			restricted
			when (AutoNumberBy = AutoNumberBy.CashCode)
		
		IsAutoNumberByBankTransactionCode
			restricted
			when (AutoNumberBy = AutoNumberBy.BankTransactionCode)		
	
		IsAutoNumberByCashCodeTranCode
			restricted
			when (AutoNumberBy = AutoNumberBy.CashCodeTranCode)

		DuplicateTranNumberExists
			restricted	
			when (CashLedgerTransactionsTransNbrRel exists
			or    CashLedgerCashReceiptHeadersRel exists
			or    CashLedgerReturnedPaymentsRel exists 	
			or    CashLedgerFundTransferDetailsRel exists
			or   (CashLedgerDocumentType.BankFundTransfer
            and   CashLedgerFundTransferHeaderRel exists)
            or   (!CashLedgerDocumentType.BankFundTransfer
            and   CashLedgerFundTransferHeaderRel exists
            and   CashLedgerFundTransferHeaderRel.BankTransactionCode = DocumentBankTransactionCode))

		DuplicateTransactionRecordExists
			restricted	
			when (CashLedgerTransactionsTransNbrRel exists)

		DuplicatePayablesPaymentRecordExists
			restricted	
			when (CashLedgerPayablesPaymentsByTranNbrRel exists)

		DuplicateReceiptRecordExists
			restricted	
			when (CashLedgerCashReceiptHeadersRel exists)
			
		DuplicateReturnedPaymentRecordExists
			restricted	
			when (CashLedgerReturnedPaymentsRel exists)
			
		DuplicateFundTransferDetailRecordExists
			restricted	
			when (CashLedgerFundTransferDetailsRel exists)
												
		DuplicateFundTransferHeaderRecordExists
			restricted	
			when ((CashLedgerDocumentType.BankFundTransfer
            and   CashLedgerFundTransferHeaderRel exists)
            or   (!CashLedgerDocumentType.BankFundTransfer
            and   CashLedgerFundTransferHeaderRel exists
            and   CashLedgerFundTransferHeaderRel.BankTransactionCode = DocumentBankTransactionCode))

		DuplicatePaymentRecordExists
			restricted	
			when (CashLedgerPaymentsRel exists)
			
		DuplicateEFTRecordExists
			restricted	
			when (EFTTransactionsRel exists)
			
		IsDeletable
			restricted
			when (AutoCreatedRel not exists)

		RecordExists	
			restricted					
			when (CashLedgerAutoTranNumber exists)																																				

		DocumentDataExists	
			restricted					
			when (DocumentCashCode entered)	

		DocumentDeleted	
			restricted					
			when (DeleteOperator entered)	

		IsAutoReferenceNumber
			restricted
			when (CashLedgerDocumentType.CashLedgerPayment 
			or    CashLedgerDocumentType.ElectronicFundTransfer) 
		
		IsAutoTransactionNumber
			restricted
			when (CashLedgerDocumentType.CashReceipt 
			or    CashLedgerDocumentType.ReturnedPayment
			or    CashLedgerDocumentType.CashLedgerTransaction
			or    CashLedgerDocumentType.BankFundTransfer) 	
			
	Relations
		CashLedgerTransactionsTransNbrRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses Set6
				related.CashManagementGroup		= CashManagementGroup	
				related.CashCode   				= DocumentCashCode
				related.BankTransactionCode 	= DocumentBankTransactionCode
				related.TransactionNumber   	= CashLedgerAutoTranNumber	

		CashLedgerPayablesPaymentsByTranNbrRel
			one-to-many relation to CashLedgerPayablesPayment
			Field Mapping uses Set4
			Instance Selection
				where (related.CashManagementGroup							 = CashManagementGroup
				and    related.CashCode 						 			 = DocumentCashCode
				and    related.CashLedgerPayablesPayment.BankTransactionCode = DocumentBankTransactionCode
				and    related.TransactionNumber	  						 = CashLedgerAutoTranNumber)
						
		CashLedgerFundTransferHeaderRel
			one-to-one relation to CashLedgerFundTransferHeader
			Field Mapping uses symbolic key
				related.CashManagementGroup 			= CashManagementGroup	
				related.CashCode 						= DocumentCashCode		
				related.CashLedgerFundTransferHeader	= CashLedgerAutoTranNumber

		CashLedgerFundTransferDetailsRel
			one-to-many relation to CashLedgerFundTransferDetail
			Field Mapping uses Set3
				related.CashLedgerFundTransferDetail.DetailCashCode = DocumentCashCode
				related.CashLedgerFundTransferHeader				= CashLedgerAutoTranNumber
			Instance Selection
				where (related.BankTransactionCode 					= DocumentBankTransactionCode)	

		CashLedgerReturnedPaymentsRel
			one-to-many relation to CashLedgerReturnedPayment
			Field Mapping uses Set2
				related.CashCode										= DocumentCashCode
				related.CashLedgerReturnedPayment.BankTransactionCode	= DocumentBankTransactionCode
				related.TransactionNumber 								= CashLedgerAutoTranNumber		

        CashLedgerCashReceiptHeadersRel
            one-to-one relation to CashLedgerCashReceiptHeader
            Field Mapping uses symbolic key
            	related.CashManagementGroup 							= CashManagementGroup	
                related.CashCode                           				= DocumentCashCode
                related.CashLedgerCashReceiptHeader.BankTransactionCode = DocumentBankTransactionCode
                related.CashLedgerCashReceiptHeader.TransactionNumber   = CashLedgerAutoTranNumber

		CashLedgerPaymentsRel
			one-to-many relation to CashLedgerPayment
			Field Mapping uses Set2
				related.CashManagementGroup 					= CashManagementGroup
                related.CashCode								= DocumentCashCode
                related.CashLedgerPayment.BankTransactionCode	= DocumentBankTransactionCode
                related.Reference								= CashLedgerAutoTranNumber
			Instance Selection
				where (related.UniqueID	!= DocumentUniqueId
				and    !related.Status.Canceled
				and	   !related.Status.Voided)

		EFTTransactionsRel
			one-to-many relation to CashLedgerElectronicFundsTransferTransaction
			Field Mapping uses Set2
                related.CashManagementGroup						= CashManagementGroup
                related.CashCode								= DocumentCashCode
                related.CashLedgerElectronicFundsTransferTransaction.BankTransactionCode = DocumentBankTransactionCode
                related.Reference								= CashLedgerAutoTranNumber
			Instance Selection
				where (related.UniqueID	!= DocumentUniqueId
				and    !related.Status.Voided
				and	   !related.Status.StopPayment
				and    !related.Status.ElectronicFundsRejectedReinstated)
			
		AutoCreatedRel
            one-to-many relation to CashLedgerAutoTranNumber
            Field Mapping uses symbolic key
            	related.CashManagementGroup 		= CashManagementGroup
                related.CashLedgerDocumentType		= CashLedgerDocumentType
            	related.Company						= Company
            	related.CashCode					= CashCode
                related.BankTransactionCode 		= BankTransactionCode
			Instance Selection
                where (related.CreationType 		= CreationType.AutoCreated)               
                
		AutoNumberByCompanyRel
            one-to-many relation to CashLedgerAutoTranNumber
            Field Mapping uses ByCompany	
            	related.CashManagementGroup 		= CashManagementGroup
                related.CashLedgerDocumentType		= CashLedgerDocumentType
            	related.Company						= Company

		AutoNumberByCashCodeRel
            one-to-many relation to CashLedgerAutoTranNumber
            Field Mapping uses ByCashCode	
            	related.CashManagementGroup 		= CashManagementGroup
                related.CashLedgerDocumentType		= CashLedgerDocumentType
            	related.CashCode					= CashCode

		AutoNumberByBankTransactionCodeRel
            one-to-many relation to CashLedgerAutoTranNumber
            Field Mapping uses ByBankTransactionCode	
            	related.CashManagementGroup 		= CashManagementGroup
                related.CashLedgerDocumentType		= CashLedgerDocumentType
            	related.BankTransactionCode			= BankTransactionCode

		AutoNumberByCashCodeTranCodeRel
            one-to-many relation to CashLedgerAutoTranNumber
            Field Mapping uses ByCashCodeTranCode	
            	related.CashManagementGroup 		= CashManagementGroup
                related.CashLedgerDocumentType		= CashLedgerDocumentType
            	related.CashCode					= CashCode
            	related.BankTransactionCode			= BankTransactionCode

            	
	Sets
		ByCompany
			indexed
			Instance Selection
                where (IsAutoNumberByCompany)
			Sort Order
				CashManagementGroup
				CashLedgerDocumentType		
        		Company
            	CashLedgerAutoTranNumber
            	CashCode
            	BankTransactionCode
            	
		ByCashCode
			indexed
			Instance Selection
                where (IsAutoNumberByCashCode)
			Sort Order
				CashManagementGroup
				CashLedgerDocumentType		
            	CashCode
        		CashLedgerAutoTranNumber
        		BankTransactionCode
        		Company
        		
        ByBankTransactionCode
			indexed
			Instance Selection
                where (IsAutoNumberByBankTransactionCode)
			Sort Order
				CashManagementGroup
				CashLedgerDocumentType		
            	BankTransactionCode
            	CashLedgerAutoTranNumber
            	CashCode
            	Company
            	
        ByCashCodeTranCode
			indexed
			Instance Selection
                where (IsAutoNumberByCashCodeTranCode)
			Sort Order
				CashManagementGroup
				CashLedgerDocumentType		
            	CashCode
            	BankTransactionCode            	
            	CashLedgerAutoTranNumber
            	Company

	Field Rules
		CashLedgerDocumentType
			required
			if (CashLedgerDocumentType.CashReceipt) 
				constraint (CashManagementGroup.AutoTranNumberForReceiptForm)
					"RequiresApplicableFormSelectionInCashManagementGroup"
			if (CashLedgerDocumentType.CashLedgerTransaction) 	
				constraint (CashManagementGroup.AutoTranNumberForTransactionForm)
					"RequiresApplicableFormSelectionInCashManagementGroup"
			if (CashLedgerDocumentType.ElectronicFundTransfer) 	
				constraint (CashManagementGroup.AutoReferenceNumberForEFTForm)
					"RequiresApplicableFormSelectionInCashManagementGroup"
			if (CashLedgerDocumentType.ReturnedPayment) 	
				constraint (CashManagementGroup.AutoTranNumberForReturnedPaymentForm)
					"RequiresApplicableFormSelectionInCashManagementGroup"
			if (CashLedgerDocumentType.CashLedgerPayment) 	
				constraint (CashManagementGroup.AutoReferenceNumberForPaymentForm)
					"RequiresApplicableFormSelectionInCashManagementGroup"
			if (CashLedgerDocumentType.BankFundTransfer) 		
				constraint (CashManagementGroup.AutoTranNumberForTransferForm)
					"RequiresApplicableFormSelectionInCashManagementGroup"
				
		Company
			if (CashCode not entered
			and BankTransactionCode not entered)
				required
			if (Company entered)
				if (IsAutoTransactionNumber)
					if (CreationType.AutoCreated)
						constraint (CashManagementGroup.AutoTranNumberByCompany)
							"<DerivedDocumentNumberType>Required;AutoTransactionNumberByCompanySelectionInCashManagementGroupIsNotSetup"		
					else
						constraint (CashManagementGroup.AutoTranNumberByCompany)
							"RequiresAutoTransactionNumberByCompanySelectionInCashManagementGroup"
				else
					if (CreationType.AutoCreated)
						constraint (CashManagementGroup.AutoReferenceNumberByCompany)
							"<DerivedDocumentNumberType>Required;AutoReferenceNumberByCompanySelectionInCashManagementGroupIsNotSetup"
					else
						constraint (CashManagementGroup.AutoReferenceNumberByCompany)
							"RequiresAutoReferenceNumberByCompanySelectionInCashManagementGroup"		
				if (CreationType.AutoCreated)				
					constraint (Company.AutoTransactionNumbering)
						"<DerivedDocumentNumberType>Required;CompanyIsNotSetupForAutoTransactionNumbering"
				else
					constraint (Company.AutoTransactionNumbering)
						"CompanyIsNotSetupForAutoTransactionNumbering"					
				if (CreationType.ManuallyCreated)
					constraint (CashCode not entered) 					  
						"CashCodeInvalidWithCompany"		
					constraint (BankTransactionCode not entered) 
						"BankTransactionCodeInvalidWithCompany"					

		CashCode
			if (Company not entered
			and BankTransactionCode not entered)
				required
			if (CashCode entered)
				if (IsAutoTransactionNumber)
					if (CreationType.AutoCreated)
						constraint (CashManagementGroup.AutoTranNumberByCashCode
						or			CashManagementGroup.AutoTranNumberByCashCodeBankTranCode)
							"<DerivedDocumentNumberType>Required;AutoTransactionNumberByCashCodeSelectionInCashManagementGroupIsNotSetup"
					else
						constraint (CashManagementGroup.AutoTranNumberByCashCode
						or			CashManagementGroup.AutoTranNumberByCashCodeBankTranCode)
							"RequiresAutoTransactionNumberByCashCodeSelectionInCashManagementGroup"			
				else
					if (CreationType.AutoCreated)
						constraint (CashManagementGroup.AutoReferenceNumberByCashCode
						or			CashManagementGroup.AutoReferenceNumberByCashCodeBankTranCode)
							"<DerivedDocumentNumberType>Required;AutoReferenceNumberByCashCodeSelectionInCashManagementGroupIsNotSetup"
					else
						constraint (CashManagementGroup.AutoReferenceNumberByCashCode
						or			CashManagementGroup.AutoReferenceNumberByCashCodeBankTranCode)
							"RequiresAutoReferenceNumberByCashCodeSelectionInCashManagementGroup"		
				if (CreationType.AutoCreated)							
					constraint (CashCode.AutoTransactionNumbering)
						"<DerivedDocumentNumberType>Required;CashCodeIsNotSetupForAutoTransactionNumbering"
				else
					constraint (CashCode.AutoTransactionNumbering)
						"CashCodeIsNotSetupForAutoTransactionNumbering"			
				if (CreationType.ManuallyCreated)
					constraint (Company not entered) 					  
						"CompanyInvalidWithCashCode"		
						
		BankTransactionCode
			if (CashCode not entered
			and Company not entered)
				required
			if (BankTransactionCode entered)
				if (IsAutoTransactionNumber)
					if (CreationType.AutoCreated)	
						constraint (CashManagementGroup.AutoTranNumberByBankTransactionCode
						or			CashManagementGroup.AutoTranNumberByCashCodeBankTranCode)
							"<DerivedDocumentNumberType>Required;AutoTransactionNumberByBankTransactionCodeSelectionInCashManagementGroupIsNotSetup"
					else
						constraint (CashManagementGroup.AutoTranNumberByBankTransactionCode
						or			CashManagementGroup.AutoTranNumberByCashCodeBankTranCode)
							"RequiresAutoTransactionNumberByBankTransactionCodeSelectionInCashManagementGroup"		
				else
					if (CreationType.AutoCreated)	
						constraint (CashManagementGroup.AutoReferenceNumberByBankTransactionCode
						or			CashManagementGroup.AutoReferenceNumberByCashCodeBankTranCode)
							"<DerivedDocumentNumberType>Required;AutoReferenceNumberByBankTransactionCodeSelectionInCashManagementGroupIsNotSetup"
					else
						constraint (CashManagementGroup.AutoReferenceNumberByBankTransactionCode
						or			CashManagementGroup.AutoReferenceNumberByCashCodeBankTranCode)
							"RequiresAutoReferenceNumberByBankTransactionCodeSelectionInCashManagementGroup"		
				if (CreationType.AutoCreated)						
					constraint (BankTransactionCode.AutoTransactionNumbering)
						"<DerivedDocumentNumberType>Required;BankTransactionCodeIsNotSetupForAutoTransactionNumbering"
				else
					constraint (BankTransactionCode.AutoTransactionNumbering)
						"BankTransactionCodeIsNotSetupForAutoTransactionNumbering"		
				if (CreationType.ManuallyCreated)
					constraint (Company not entered) 					  
						"CompanyInvalidWithBankTransactionCode"		

	Rule Blocks
		SetAutoNumberByValue
			if (Company entered)
				AutoNumberBy			= AutoNumberBy.Company
			if (CashCode entered)
				AutoNumberBy			= AutoNumberBy.CashCode
			if (BankTransactionCode entered)
				AutoNumberBy			= AutoNumberBy.BankTransactionCode
			if (CashCode entered
			and BankTransactionCode entered)
				AutoNumberBy			= AutoNumberBy.CashCodeTranCode
					
	Actions
		Create is a Create Action
			Entrance Rules
				constraint (AutoCreatedRel not exists)
					"AutoCreatedRecordsExist;CanNoLongerUseTheCreateAction"
			Action Rules
				include SetAutoNumberByValue
				CreationType				= CreationType.ManuallyCreated
				CashLedgerAutoTranNumber	= LastNumberUsed
				CreateOperator				= actor
				CreateDate					= current timestamp
				AutoNumberStatus			= AutoNumberStatus.LastNumberUsedApplied			
			Field Rules
				LastNumberUsed	
					required
					if (CashLedgerDocumentType.CashLedgerPayment)
					 	constraint (!DuplicatePaymentRecordExists)
							"CannotCreate;NumberAlreadyInUse"
					else
					if (CashLedgerDocumentType.ElectronicFundTransfer)
						constraint (!DuplicateEFTRecordExists)
							"CannotCreate;NumberAlreadyInUse"
					else
						constraint (!DuplicateTranNumberExists)
							"CannotCreate;NumberAlreadyInUse"
						if (CashLedgerDocumentType.CashLedgerTransaction)
							constraint (!DuplicatePayablesPaymentRecordExists)
								"CannotCreate;NumberAlreadyInUse"
													
		AutoCreate is a Create Action
			default label is untranslatable
			restricted
			Action Rules
				include SetAutoNumberByValue
				CreationType			= CreationType.AutoCreated	
			Field Rules
				CashLedgerAutoTranNumber
					autosequence
						minimize contention and gaps
			Exit Rules
				if (CashLedgerDocumentType.CashLedgerPayment)
					if (DuplicatePaymentRecordExists)
						AutoNumberStatus	= AutoNumberStatus.CannotBeAssignedAlreadyInUse
					else
						AutoNumberStatus	= AutoNumberStatus.NumberAssignedToDocument
				else			
				if (CashLedgerDocumentType.ElectronicFundTransfer)
					if (DuplicateEFTRecordExists)
						AutoNumberStatus	= AutoNumberStatus.CannotBeAssignedAlreadyInUse
					else
						AutoNumberStatus	= AutoNumberStatus.NumberAssignedToDocument			
				else			
					if (DuplicateTranNumberExists
					or (CashLedgerDocumentType.CashLedgerTransaction			
					and DuplicatePayablesPaymentRecordExists))
						AutoNumberStatus	= AutoNumberStatus.CannotBeAssignedAlreadyInUse
					else
						AutoNumberStatus	= AutoNumberStatus.NumberAssignedToDocument	
					
		Delete is a Delete Action
			valid when (IsDeletable)	
		


		DocumentDeleteUpdate 		is an Instance Action
			default label is untranslatable
			restricted
			Action Rules			
				AutoNumberStatus	= AutoNumberStatus.RelatedDocumentDeleted
				DeleteOperator		= actor	
				DeleteDate			= current timestamp
		
