DirectForecastSummary is a BusinessClass
	owned by cashmgmt
	prefix is CMDFS	

	Ontology
		symbolic key is DirectForecastSummary

	Patterns
 		implements DynamicCreation
 		
	Persistent Fields
		CompanyAmount				is an InternationalAmount
		AccountAmount				is an InternationalAmount
		CompanyDiscountAmount		is an InternationalAmount
		AccountDiscountAmount		is an InternationalAmount

	Local Fields
		CurrencyTable
		FromCurrency
		CurrencyAmount
		ExchangeDate
		LocalCurrencyExchange	is a CurrencyExchange
		LocalToCurrency			is a ToCurrency		

	Context Fields
		DirectForecast
		
	Derived Fields
		DerivedAccountAmount is a DerivedField
			type is like InternationalAmount 
			default label is "AccountAmount"
			LocalToCurrency = CashManagementAccount.Currency		
			if (CashManagementAccount.CashManagementGroup.ShowDiscountedAmount)
				return AccountAmount - AccountDiscountAmount
			else
				return AccountAmount 

		DerivedCompanyAmount is a DerivedField
			type is like InternationalAmount 
			default label is "CompanyAmount"
			LocalToCurrency = DirectForecast.GeneralLedgerCompany.Currency		
			if (CashManagementAccount.CashManagementGroup.ShowDiscountedAmount)
				return CompanyAmount - CompanyDiscountAmount
			else
				return CompanyAmount

		ReportAmount is a DerivedField
			type is like InternationalAmount
			if (DirectForecast.ReportCurrency entered)
				CurrencyTable	= CashManagementAccount.CashManagementGroup.FinanceEnterpriseGroup.CurrencyTable
				FromCurrency	= CashManagementAccount.Currency
				CurrencyAmount	= DerivedAccountAmount
				ExchangeDate	= first DirectForecastDetailRel.LoadDate
				LocalCurrencyExchange.ToCurrency = DirectForecast.ReportCurrency
				LocalToCurrency = DirectForecast.ReportCurrency		
				return LocalCurrencyExchange.OutputCurrencyAmount

	Transient Fields
		TransientFromCurrency	is a FromCurrency
			derive value from CashManagementAccount.Currency

	Sets
		ByDueDate
			Sort Order
				CashManagementAccount
				GeneralLedgerSystemCode
				DirectForecastSummary
				Company

		ByCompany
			Sort Order
				Company
				CashManagementAccount
				DirectForecastSummary
				GeneralLedgerSystemCode

		ByCashManagementAccount
			Sort Order
				CashManagementAccount
				Company
				DirectForecastSummary
				GeneralLedgerSystemCode

		BySystemCodeAccount
			Sort Order
				GeneralLedgerSystemCode
				CashManagementAccount
				DirectForecastSummary
				Company

	Relations
		DirectForecastDetailRel
			one-to-many relation to DirectForecastDetail
			Field Mapping uses CompanySystemAccountByDueDate
				related.Company				  = Company
				related.SystemCode			  = GeneralLedgerSystemCode
				related.CashManagementAccount = CashManagementAccount
				related.DueDate				  = DirectForecastSummary

	Conditions
		ReportCurrencyEntered
			restricted
			when (DirectForecast.ReportCurrency entered)


  	Actions
  		Create is a Create Action
			restricted
			
  		Update is an Update Action
  			restricted
  			
  		Delete is a Delete Action
  			restricted
  			
  		Purge is a Purge Action
  			restricted
