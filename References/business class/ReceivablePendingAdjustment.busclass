ReceivablePendingAdjustment is a BusinessClass
	owned by ar
	prefix is PND
	classic name is ARPENDADJ

	Ontology
		symbolic key is ReceivablePendingAdjustment
			classic set name is PNDSET1
			classic name for ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType is TRANS-TYPE
			classic name for ReceivablePendingAdjustment.BatchNumber is BATCH-NBR
			classic name for ReceivablePendingAdjustment.ApplicationSequence is APP-SEQ
			classic name for ReceivablePendingAdjustment.AdjustmentSequence is ADJ-SEQ

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		User
		Customer
		Source				is a ReceivableSource
		ActionCode			is AlphaUpper 1
			classic name is ACTION
			States
				Review   			value is "R"
				DeleteAndDispute 	value is "B"
				Delete   			value is "D"
				Change				value is "C"
				Expense				value is "E"
		ReasonCode
		DisputeCode		   is a ReceivableInvoiceDisputeCode
		GeneralLedgerDate   is Date
			classic name is GL-DATE
			default label is "GlobalLedgerDate"
		Description
		DistributionAccount is a FinanceCodeBlock
			classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
			classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
			classic name for DistributionAccount.Project is ACTIVITY

	Local Fields
		NewReceivableApplication 						is a ReceivableApplication view

	Field Rules
		User
			cannot be changed

		Customer
			cannot be changed

		Source
			cannot be changed

		GeneralLedgerDate



			if (DisputeCode not entered)
				default to current corporate date
			if (CompanySystemClosingControlRel.Control and Company.VerifyGLDateWithinGLDateRange)		
				constraint (GeneralLedgerDate within CompanySystemClosingControlRel.ValidEntryDate)
					"PostDate<GeneralLedgerDate>IsNotWithinValidEntryDatesForCompany<Company>;ValidDateRangeIs<CompanySystemClosingControlRel.ValidEntryDate.Begin>-<CompanySystemClosingControlRel.ValidEntryDate.End>"


	Sets

		Set2
			indexed
			Sort Order
				User
				Source
				Company
				Customer
				ReceivablePendingAdjustment

		ByReceivablePendingAdjustmentRel
			indexed
			Sort Order
				Company
				ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
				ReceivablePendingAdjustment.BatchNumber
				ReceivablePendingAdjustment.ApplicationSequence
				ReceivablePendingAdjustment.AdjustmentSequence

	Relations


		CompanySystemClosingControlRel	
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode	 = "AR"
				related.Company					 = Company

		ReceivableApplicationAdjustmentRel
			one-to-one relation to ReceivableApplicationAdjustment
			required
			Field Mapping uses symbolic key
					related.Company										= Company
					related.ReceivableApplication.TransType 			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType 
					related.ReceivableApplication.Invoice 				= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice	
					related.ReceivableApplication.PaymentSeq 			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
					related.ReceivableApplication.BatchNumber 			= ReceivablePendingAdjustment.BatchNumber	 
					related.ReceivableApplication.ApplicationSequence 	= ReceivablePendingAdjustment.ApplicationSequence	 
					related.ReceivableApplicationAdjustment 			= ReceivablePendingAdjustment.AdjustmentSequence 

		ReceivableApplicationRel
			one-to-one relation to ReceivableApplication
			Field Mapping uses SequenceDescending
					related.Company										= Company
					related.ReceivableApplication.TransType 			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType 
					related.ReceivableApplication.Invoice 				= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice	
					related.ReceivableApplication.PaymentSeq 			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
					related.ReceivableApplication.BatchNumber 			= ReceivablePendingAdjustment.BatchNumber	 
					related.ReceivableApplication.ApplicationSequence 	= ReceivablePendingAdjustment.ApplicationSequence	 

		LastApplicationRel
			one-to-many relation to ReceivableApplication
			Field Mapping uses SequenceDescending
					related.Company										= Company
					related.ReceivableApplication.TransType 			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType 
					related.ReceivableApplication.Invoice 				= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice	
					related.ReceivableApplication.PaymentSeq 			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
					related.ReceivableApplication.BatchNumber 			= ReceivablePendingAdjustment.BatchNumber	 

		ReceivableInvoiceDetailRel
			one-to-one relation to ReceivableInvoiceDetail
			required
			Field Mapping uses symbolic key
				related.Company												 = Company
				related.ReceivableInvoiceDetail.ReceivableInvoiceDetailType	 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				related.ReceivableInvoiceDetail.Invoice						 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				related.ReceivableInvoiceDetail.PaymentSeq					 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq

		ReceivableApplicationAdjustmentSelectRel
			one-to-many relation to ReceivableApplicationAdjustmentSelect
			Field Mapping uses symbolic key
				related.Company														= Company
			Instance Selection
				where (related.ReceivableApplicationAdjustmentSelect.TransType		= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				and	related.ReceivableApplicationAdjustmentSelect.Invoice			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				and	related.ReceivableApplicationAdjustmentSelect.PaymentSeq		= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
				and	related.ReceivableApplicationAdjustmentSelect.BatchNumber		= ReceivablePendingAdjustment.BatchNumber)
	
		ReceivableApplicationSelectRel
			classic name is ARSAPPLIED
			one-to-one relation to ReceivableApplicationSelect
			required
			Field Mapping uses Set2
				related.ReceivableApplicationSelect.TransactionCompany									 = Company
				related.ReceivableApplicationSelect.ReceivableInvoiceDetail.ReceivableInvoiceDetailType	 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				related.ReceivableApplicationSelect.ReceivableInvoiceDetail.Invoice						 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				related.ReceivableApplicationSelect.ReceivableInvoiceDetail.PaymentSeq					 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq

		ReceivableInvoiceApplyReverseHeaderRel
			classic name is ARTRNRVHDR
			one-to-one relation to ReceivableInvoiceApplyReverseHeader
			Field Mapping uses symbolic key
				related.Company																					= Company
				related.ReceivableInvoiceApplyReverseHeader.ReceivableInvoiceDetail.ReceivableInvoiceDetailType	= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				related.ReceivableInvoiceApplyReverseHeader.ReceivableInvoiceDetail.Invoice						= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				related.ReceivableInvoiceApplyReverseHeader.ReceivableInvoiceDetail.PaymentSeq					= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq

		ReceivableInvoiceApplyReverseDetailRel
			classic name is ARTRNRVDTL
			one-to-many relation to ReceivableInvoiceApplyReverseDetail
			delete restricted
			Field Mapping uses Set2
				related.CreditCompany				= Company
				related.CreditBatchNumber			= ReceivablePendingAdjustment.BatchNumber
				related.CreditTransactionType		= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				related.CreditTransactionNumber		= blank
				related.CreditInvoice				= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				related.CreditPaymentSequence		= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
							
		ReceivablePaymentApplyReverseDetailRel
			one-to-many relation to ReceivablePaymentApplyReverseDetail
			Field Mapping uses Set2
				related.Company								= Company
				related.TransactionType						= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				related.Invoice								= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
				related.PaymentSequence						= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq

		CompanyCustomerTransactionCommentRel
			one-to-many relation to CompanyCustomerComment
			Field Mapping uses Set2
				related.Company							 = Company
				related.Customer  						 = ReceivableInvoiceDetailRel.Customer
				related.CommentType 					 = "T"
				related.TransactionType 				 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
				related.Transaction 					 = ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice



	Rule Blocks
	
		CreateUpdateEdits

			constraint (!ReceivableApplicationAdjustmentSelectRel exists)
				"CannotSelect;PendingAdjustmentExists"

			constraint (!ReceivableApplicationSelectRel exists)
				"CannotSelect;PendingApplicationExists"

			constraint (!ReceivableInvoiceDetailRel.ARToAPApplicationSelectRel exists)
				"CannotSelect;AR-To-APSelectRecordExists"

			constraint (!ReceivableInvoiceDetailRel.ReceivableInvoiceObligationGroupRel exists)
				"CannotSelect;PendingObligationGroupRecordExists"

			constraint (!ReceivableApplicationRel.ReceivableInvoiceApplyReverseHeaderRel exists)
				"CannotSelect;ReversalPendingForThisInvoice"

			constraint (!ReceivableInvoiceApplyReverseDetailRel exists)
				"CannotSelect;ReversalPendingForThisInvoice"

			constraint (!ReceivablePaymentApplyReverseHeaderRel exists)
				"CannotSelect;ReversalPendingForThisInvoice"

			if (!(ReceivableApplicationAdjustmentRel.ReviewStatus.ReviewNeedsApproval
			or    ReceivableApplicationAdjustmentRel.ReviewStatus.PendingApproval))
				constraint (!ReceivableApplicationAdjustmentRel.AdjustFlag.Accrued)
					"CannotChange;MustExpenseUsingAR38.3" 

				constraint ((ReasonCode entered or DisputeCode entered) and (ReasonCode != ReceivableApplicationAdjustmentRel.AdjustmentReason))
					"MustEitherChangeReasonCodeOrEnterDisputeCode"

				if (ReasonCode entered)
					if (ReceivableApplicationRel.Status.ApplicationPosted)
						constraint (ReasonCode.ReceivableAdjustmentReasonType.OverShort
						or		  ReasonCode.ReceivableAdjustmentReasonType.Discount)
							"ReasonMustBeDiscountOrOverShortIfApplicationPosted"
					else
						if (ReasonCode.ReceivableAdjustmentReasonType.Chargeback)
							constraint (!ReasonCode.ChargebackType.Memo)
								"CannotChooseChargebackMemo"
				else
				if  (ReceivableApplicationAdjustmentRel.AdjustmentReason entered)
					if (ReceivableApplicationRel.Status.ApplicationPosted)
						constraint (ReceivableApplicationAdjustmentRel.AdjustmentReason.ReceivableAdjustmentReasonType.OverShort
						or		  ReceivableApplicationAdjustmentRel.AdjustmentReason.ReceivableAdjustmentReasonType.Discount)
							"ReasonMustBeDiscountOrOverShortIfApplicationPosted"
					else
						if (ReasonCode.ReceivableAdjustmentReasonType.Chargeback)
							constraint (!ReceivableApplicationAdjustmentRel.AdjustmentReason.ChargebackType.Memo)
								"CannotChooseChargebackMemo"

 		
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action

			Action Rules
				if (ReasonCode not entered)
					if (ReceivableApplicationAdjustmentRel.ReviewStatus.ReviewNeedsApproval
					or  ReceivableApplicationAdjustmentRel.ReviewStatus.PendingApproval)
						ActionCode				= "R"
					else
					if (DisputeCode entered)
						ActionCode				= "B"
					else
						ActionCode				= "D"
				else
					ActionCode					= "C"

		Delete is a Delete Action
			restricted
			Action Rules


		Purge is a Purge Action
			default label is "Delete"
			confirmation required
				"AreYouSureYouWantToRemoveThisTransaction?"

		ProcessRecords is a Set Action		
			restricted				
			run in foreground
			Parameters
				PrmUser		is a User
				PrmSource	is a ReceivableSource
				PrmCompany	is a ReceivableCompany
				PrmCustomer	is a Customer
				PrmOperator is like ReceivableOperator

			Instance Selection
				where (User 		= PrmUser
				and	Source			= PrmSource
				and	Company 		= PrmCompany
				and  (PrmCustomer not entered
				or	 Customer		= PrmCustomer))
				


			Action Rules
				Instance Rules

					if (ActionCode.Review)
						invoke Reviewed ReceivableApplicationAdjustmentRel
					else
						if (ActionCode.DeleteAndDispute)

							invoke CreateFromAdjustmentMaintenance ReceivableInvoiceDispute
								invoked.Company										= Company
								invoked.Customer									= Customer
								invoked.ReceivableInvoiceDetail.ReceivableInvoiceDetailType	= ReceivablePendingAdjustment.ReceivableInvoiceDetail.ReceivableInvoiceDetailType
								invoked.ReceivableInvoiceDetail.Invoice				= ReceivablePendingAdjustment.ReceivableInvoiceDetail.Invoice
								invoked.ReceivableInvoiceDetail.PaymentSeq			= ReceivablePendingAdjustment.ReceivableInvoiceDetail.PaymentSeq
								invoked.DisputeCode									= DisputeCode
								invoked.DisputedAmount								= ReceivableApplicationAdjustmentRel.AdjustmentAmount.CurrencyAmount
								invoked.OriginalDisputeAmount						= ReceivableApplicationAdjustmentRel.AdjustmentAmount.CurrencyAmount
								invoked.BaseAmount									= ReceivableInvoiceDetailRel.InvoiceAmount.FunctionalAmount.EnteredCurrencyAmount
								invoked.TransactionAmount							= ReceivableInvoiceDetailRel.InvoiceAmount.CurrencyAmount
								invoked.OriginalCurrency							= ReceivableInvoiceDetailRel.OriginalCurrency
								invoked.DisputeDate									= GeneralLedgerDate
								invoked.Description									= Description
	
						if  (ReceivableApplicationRel.Status.ApplicationPosted)
							if  (ActionCode.DeleteAndDispute
							or   ActionCode.Delete
							or   ActionCode.Change
							or   ActionCode.Expense)

								invoke ReversePosted ReceivableApplicationAdjustmentRel
									invoked.PrmAdjusted	= true

								invoke AvailableForPosting.Create ReceivableApplication
									assign result to NewReceivableApplication
									fill in fields from ReceivableApplicationRel
										except invoked.ReceivableApplication.ApplicationSequence
										except invoked.CreditAppliedSequence
										except invoked.BaseDebitCreditAdjustAmount			
										except invoked.CreditAdjustAmount

										except invoked.CreditTransaction.CreditApplicationAmount
										except invoked.AdjustmentSequence
									initialize invoked.ReceivableApplication.ApplicationSequence
									initialize invoked.AdjustmentSequence
									initialize invoked.ApplicationAmount
									initialize invoked.CreditTransaction.CreditApplicationAmount
									invoked.TransientZeroAmountApplication								= true
									invoked.ApplicationAmount.KeepRateAndAmount 						= ReceivableApplicationRel.ApplicationAmount * -1
									invoked.CreditTransaction.CreditApplicationAmount.KeepRateAndAmount = ReceivableApplicationRel.CreditTransaction.CreditApplicationAmount * -1
									invoked.ApplicationAmount.KeepRateAndAmount 						= true
									invoked.CreditTransaction.CreditApplicationAmount.KeepRateAndAmount = true
									invoked.ApplicationSource											= "J"
									invoked.Status														= 4
									invoked.GeneralLedgerDate											= GeneralLedgerDate
									invoked.ReceivableOperator											= PrmOperator
									invoked.BaseDebitCreditAdjustAmount									= ReceivableApplicationRel.BaseDebitCreditAdjustAmount * -1
									invoked.ApplicationAmount.KeepRateAndAmount							= true
									invoked.BaseDebitCreditAdjustAmount									= ReceivableApplicationRel.BaseDebitCreditAdjustAmount * -1
									invoked.ApplicationSource											= "J"
									invoked.CreditAdjustAmount											= ReceivableApplicationRel.CreditAdjustAmount * -1




								invoke CreateOnly ReceivableApplicationAdjustment
									fill in fields from ReceivableApplicationAdjustmentRel
									invoked.ReceivableApplication.ApplicationSequence 					= NewReceivableApplication.ReceivableApplication.ApplicationSequence 
									invoked.ReceivableApplicationAdjustment								= 1
									invoked.AdjustmentReason											= ReasonCode
									invoked.Description													= "Reversal"
									invoked.Adjusted													= true

									invoked.TransientReversal											= true
									invoked.CreditAdjustAmount											= ReceivableApplicationAdjustmentRel.CreditAdjustAmount * -1
									invoked.AdjustmentAmount.CurrencyAmount								= ReceivableApplicationAdjustmentRel.AdjustmentAmount.CurrencyAmount * -1
									invoked.AdjustmentAmount.FunctionalAmount.EnteredCurrencyAmount 	= ReceivableApplicationAdjustmentRel.AdjustmentAmount.FunctionalAmount.EnteredCurrencyAmount * -1
									invoked.BaseEarnedDiscountAmount									= ReceivableApplicationAdjustmentRel.BaseEarnedDiscountAmount * -1

								
								invoke AvailableForPosting.Create ReceivableApplication
									assign result to NewReceivableApplication
									fill in fields from ReceivableApplicationRel
										except invoked.ReceivableApplication.ApplicationSequence
										except invoked.CreditAppliedSequence
										except invoked.BaseDebitCreditAdjustAmount			
										except invoked.CreditAdjustAmount

									initialize invoked.ReceivableApplication.ApplicationSequence
									initialize invoked.ApplicationAmount
									initialize invoked.CreditTransaction.CreditApplicationAmount
									invoked.TransientZeroAmountApplication								= true

									invoked.ApplicationAmount.KeepRateAndAmount 						= ReceivableApplicationRel.ApplicationAmount
									invoked.CreditTransaction.CreditApplicationAmount.KeepRateAndAmount = ReceivableApplicationRel.CreditTransaction.CreditApplicationAmount
									invoked.ApplicationAmount.KeepRateAndAmount 						= true
									invoked.CreditTransaction.CreditApplicationAmount.KeepRateAndAmount = true
									invoked.ApplicationSource											= "J"
									invoked.Status														= 4
									invoked.GeneralLedgerDate											= GeneralLedgerDate
									invoked.ReceivableOperator											= PrmOperator
									invoked.BaseDebitCreditAdjustAmount									= ReceivableApplicationRel.BaseDebitCreditAdjustAmount



								invoke CreateOnly ReceivableApplicationAdjustment
									fill in fields from ReceivableApplicationAdjustmentRel
										
									invoked.ReceivableApplication.ApplicationSequence 	= NewReceivableApplication.ReceivableApplication.ApplicationSequence 
									invoked.ReceivableApplicationAdjustment				= 1
									invoked.AdjustmentReason							= ReasonCode
									invoked.Description									= Description
									invoked.Adjusted									= false
									invoked.DistributionAccount				 		= DistributionAccount




						else
							if  (ActionCode.DeleteAndDispute
							or   ActionCode.Delete)

								invoke DeleteFromAdjustmentMaintenance ReceivableApplicationAdjustmentRel
		
							if  (ActionCode.Change
							or   ActionCode.Expense)

								invoke UpdateFromAdjustmentMaintenance ReceivableApplicationAdjustmentRel
									if (DistributionAccount entered)
										invoked.DistributionAccount				= DistributionAccount
									if (Description entered)
										invoked.Description						= Description
									if  (ActionCode.Change
									and  ReasonCode entered)
										invoked.AdjustmentReason				= ReasonCode
									if (ActionCode.Expense)
										invoked.AdjustFlag						= 2


					invoke Purge  
