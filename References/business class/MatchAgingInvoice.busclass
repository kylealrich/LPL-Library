MatchAgingInvoice is a BusinessClass
    owned by ma
    prefix is MAgIn

    Ontology
		symbolic key is MatchAgingInvoice

    Patterns
    	disable AuditIndex
    	disable Auditing
    	disable EffectiveDated
		implements CreateStamp	
		implements UpdateStamp	
		implements Archivable

    Persistent Fields
		FinanceEnterpriseGroup
		VendorGroup
		Invoice
		Suffix
    	Vendor						is like Vendor
    	InvoiceDate					is Date
    	DueDate 					is Date
		InvoiceAmount				is an InternationalAmount
		FunctionalAmount    		is an InternationalAmount
		MatchDate					is Date  
		CreateDate                  is Date
		CreateYear					is Numeric 4
		CreateMonth					is Numeric 2
		DaysToMatch					is Numeric 7
		MatchAgingPeriod			is like MatchAgingPeriod

	Transient Fields
		TransientDaysUnmatched	is Numeric 9
			derive value from DaysUnmatched

	Local Fields
		NextSentence				is Boolean		
		LocalPayablesInvoice		is like PayablesInvoice
		LocalMatchAging				is like MatchAging
		LocalFinanceEnterpriseGroup is like FinanceEnterpriseGroup
		LocalVendorGroup			is like VendorGroup
		
	Context Fields
				
	Derived Fields

		DaysUnmatched is a DerivedField
			type is Numeric 9
			default label is "DaysNotMatched"
			if (CreateDate = MatchAgingRel.MigrationDate)  
				return current date - InvoiceDate
			else 
				return current date - CreateDate 

		LastMatchAttempt is a DerivedField
			type is Alpha 200
			if (PayablesInvoice.MatchReconQueueRel.MatchWork.VerifyMatchMessage entered)
				return PayablesInvoice.MatchReconQueueRel.MatchWork.VerifyMatchMessage
			return last AutoMatchResultInvoiceRel.ErrorMessage

	Sets

 		ByVendor
 			Sort Order
			 	FinanceEnterpriseGroup
				VendorGroup  
 				Vendor
				CreateYear descending
				CreateMonth descending
 				Invoice
 				Company
 				PayablesInvoice

 		ByVendorAgingPeriod
 			Sort Order
			 	FinanceEnterpriseGroup 
 				VendorGroup
				Vendor 
				MatchAgingPeriod 
 				Invoice
 				Company
 				PayablesInvoice

		ByYearMonthAgingPeriod
			Sort Order
			 	FinanceEnterpriseGroup 
				CreateYear descending
				CreateMonth descending
				MatchAgingPeriod
				Company
				PayablesInvoice 

		ByAgingPeriod
			Sort Order
			 	FinanceEnterpriseGroup 
				MatchAgingPeriod
				Company
				PayablesInvoice

 		ByInvoiceAmount
 			Sort Order
			 	FinanceEnterpriseGroup 
				InvoiceAmount
 				Company
 				PayablesInvoice

	Relations

		MatchCompanyRel
			one-to-one relation to MatchCompany
			Field Mapping uses symbolic key
				related.Company = Company

        VendorRel
            one-to-one relation to Vendor
            Field Mapping uses symbolic key
                related.VendorGroup	 			= VendorGroup
                related.Vendor            		= Vendor
		MatchAgingRel
			one-to-many relation to MatchAging
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = Company.FinanceEnterpriseGroup
		MatchAgingPeriodRel
			one-to-many relation to MatchAgingPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = Company.FinanceEnterpriseGroup

		AllMatchAgingSummaryRel
			one-to-many relation to MatchAgingSummary 
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = Company.FinanceEnterpriseGroup

		YearMonthSummaryRel
			one-to-many relation to MatchAgingSummary
			Field Mapping uses BySummaryTypeYearMonth
				related.FinanceEnterpriseGroup 	= LocalFinanceEnterpriseGroup
				related.MatchAging				= LocalMatchAging
				related.SummaryType				= 2 
				related.Year					= CreateYear
				related.Month					= CreateMonth
				related.VendorGroup				= blank
				related.Vendor					= blank

		VendorSummaryRel
			one-to-many relation to MatchAgingSummary
			Field Mapping uses BySummaryTypeVendor
				related.FinanceEnterpriseGroup 	= LocalFinanceEnterpriseGroup
				related.MatchAging				= LocalMatchAging
				related.SummaryType				= 1 
				related.VendorGroup				= LocalVendorGroup
				related.Vendor					= Vendor

		VendorUnmatchedSummaryRel
			one-to-many relation to MatchAgingSummary
			Field Mapping uses BySummaryTypeVendor
				related.FinanceEnterpriseGroup 	= LocalFinanceEnterpriseGroup
				related.MatchAging				= LocalMatchAging
				related.SummaryType				= 3 
				related.VendorGroup				= LocalVendorGroup
				related.Vendor					= Vendor

		AutoMatchResultInvoiceRel
			one-to-many relation to AutoMatchResultInvoice
			Field Mapping uses ByInvoice
				related.Invoice					= Invoice
				related.Company 				= Company
				related.PayablesInvoice			= PayablesInvoice
	Conditions

	Field Rules

	Rule Blocks
		UpdateValues
			FinanceEnterpriseGroup = PayablesInvoice.Company.FinanceEnterpriseGroup
			VendorGroup = PayablesInvoice.Company.VendorGroup
			Vendor 				= PayablesInvoice.Vendor
			Invoice				= PayablesInvoice.Invoice
			Suffix 				= PayablesInvoice.Suffix
			InvoiceDate			= PayablesInvoice.InvoiceDate
			DueDate				= PayablesInvoice.DueDate
			InvoiceAmount 		= PayablesInvoice.InvoiceAmount.CurrencyAmount
			FunctionalAmount 	= PayablesInvoice.InvoiceAmount.FunctionalAmount.EnteredCurrencyAmount
			CreateDate			= PayablesInvoice.create date 
			CreateYear			= PayablesInvoice.create date year 
			CreateMonth			= PayablesInvoice.create date month 
			MatchDate			= PayablesInvoice.MatchDate

	Actions
		Create is a Create Action
			restricted
			Action Rules
				invoke AddToAging

		Update is an Update Action
			restricted

		UpdateSnapshotFields is an Instance Action
			restricted
			Action Rules

				LocalMatchAging = first MatchAgingRel.MatchAging
				LocalFinanceEnterpriseGroup = Company.FinanceEnterpriseGroup
				LocalVendorGroup = PayablesInvoice.VendorGroup				
				if (VendorUnmatchedSummaryRel exists
				and InvoiceAmount != PayablesInvoice.InvoiceAmount.CurrencyAmount)
					invoke SubtractFromAging VendorUnmatchedSummaryRel
						invoked.PrmNewAmount = ((InvoiceAmount - PayablesInvoice.InvoiceAmount.CurrencyAmount) * -1) 
				include UpdateValues

		Delete is a Delete Action
			restricted
			Entrance Rules

				LocalMatchAging = first MatchAgingRel.MatchAging
				LocalFinanceEnterpriseGroup = Company.FinanceEnterpriseGroup
				LocalVendorGroup = PayablesInvoice.VendorGroup		
				if  (PayablesInvoice.MatchProcessType.ServiceContract
				or  (PayablesInvoice.MatchProcessType.AOCOnly
				and !PayablesInvoice.CostComponentMatch)		
				and !PayablesInvoice.Status.Unreleased)
					NextSentence = true   
				else 
				if (VendorUnmatchedSummaryRel exists)
					invoke SubtractFromAging VendorUnmatchedSummaryRel
						invoked.PrmTotalCount				= 1
						invoked.PrmNewAmount				= InvoiceAmount * -1

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules

		DeleteSet is a Set Action
			restricted
			default label is "SummarizeMatchAging"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmMatchAging					is a MatchAging

			Sort Order
				Company
				PayablesInvoice
			Instance Selection
				where (PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup)
			Action Rules
				Empty Set Rules
					invoke DeleteSet MatchAgingSummary
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmMatchAging				= PrmMatchAging

				Set Rules
					Exit Rules
						invoke DeleteSet MatchAgingSummary
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmMatchAging				= PrmMatchAging
				Instance Rules
					invoke FastDelete


		Summarize is a Set Action
			restricted
			default label is "SummarizeMatchAging"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmMatchAging					is a MatchAging

			Sort Order
				Company
				PayablesInvoice
			Instance Selection
				where (PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup)

			Local Fields
			Action Rules
				Set Rules
					Exit Rules
						invoke Update PrmMatchAging
							invoked.Status = 1

				Instance Rules
					invoke AddToAging

		AddToAging is an Instance Action
			restricted
			default label is "AddToAging"
			Parameters
				FromMatch is Boolean
			Action Rules
				include UpdateValues

				if (MatchDate entered)
					if (CreateDate = MatchAgingRel.MigrationDate)  
						DaysToMatch			= MatchDate - InvoiceDate
					else
						DaysToMatch			= MatchDate - CreateDate
					for each MatchAgingPeriodRel
						if (DaysToMatch <= each.ThroughDays)
							MatchAgingPeriod = each.MatchAgingPeriod
							end for each

					if (MatchAgingPeriod not entered)
						MatchAgingPeriod = 4  

				LocalMatchAging = first MatchAgingRel.MatchAging
				LocalFinanceEnterpriseGroup = Company.FinanceEnterpriseGroup
				LocalVendorGroup = PayablesInvoice.VendorGroup				
				
				if  (MatchDate entered)
					if (VendorSummaryRel exists)

						invoke AddToAging VendorSummaryRel
							invoked.PrmTotalCount				= 1
							invoked.PrmNewAmount				= InvoiceAmount
							invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod
					else

						invoke Create MatchAgingSummary
							invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
							invoked.MatchAging				= LocalMatchAging
							invoked.SummaryType				= 1 
							invoked.Vendor					= Vendor
							invoked.VendorGroup				= PayablesInvoice.VendorGroup
							invoked.TotalCount				+= 1
							invoked.NewAmount				= InvoiceAmount
							invoked.MatchAgingPeriod 		= MatchAgingPeriod

					if (YearMonthSummaryRel exists)

						invoke AddToAging YearMonthSummaryRel
							invoked.PrmTotalCount				= 1
							invoked.PrmNewAmount				= InvoiceAmount
							invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod
					else

						invoke Create MatchAgingSummary
							invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
							invoked.MatchAging				= LocalMatchAging
							invoked.SummaryType				= 2 
							invoked.Year					= CreateYear
							invoked.Month					= CreateMonth
							invoked.TotalCount				+= 1
							invoked.NewAmount				= InvoiceAmount
							invoked.MatchAgingPeriod 		= MatchAgingPeriod

					if (FromMatch)
						if (VendorUnmatchedSummaryRel exists)

							invoke SubtractFromAging VendorUnmatchedSummaryRel
								invoked.PrmTotalCount				= 1
								invoked.PrmNewAmount				= InvoiceAmount * -1

				else
					if (VendorUnmatchedSummaryRel exists)

						invoke AddToAging VendorUnmatchedSummaryRel
							invoked.PrmTotalCount				= 1
							invoked.PrmNewAmount				= InvoiceAmount
							invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod
					else

						invoke Create MatchAgingSummary
							invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
							invoked.MatchAging				= LocalMatchAging
							invoked.SummaryType				= 3 
							invoked.Vendor					= Vendor
							invoked.VendorGroup				= PayablesInvoice.VendorGroup
							invoked.TotalCount				+= 1
							invoked.NewAmount				= InvoiceAmount
							invoked.MatchAgingPeriod 		= MatchAgingPeriod

		Unmatch is an Instance Action
			restricted
			default label is "AddToAging"
			Action Rules

				LocalMatchAging = first MatchAgingRel.MatchAging
				LocalFinanceEnterpriseGroup = Company.FinanceEnterpriseGroup
				LocalVendorGroup = PayablesInvoice.VendorGroup				
				
				if (VendorSummaryRel exists)

					invoke SubtractFromAging VendorSummaryRel
						invoked.PrmTotalCount				= 1
						invoked.PrmNewAmount				= InvoiceAmount * -1
						invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod

				if (YearMonthSummaryRel exists)

					invoke SubtractFromAging YearMonthSummaryRel
						invoked.PrmTotalCount				= 1
						invoked.PrmNewAmount				= InvoiceAmount * -1
						invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod

				initialize MatchDate
				initialize DaysToMatch
				initialize MatchAgingPeriod

				if  (PayablesInvoice.MatchProcessType.ServiceContract
				or  (PayablesInvoice.MatchProcessType.AOCOnly
				and !PayablesInvoice.CostComponentMatch))
					NextSentence = true   
				else 
				if (VendorUnmatchedSummaryRel exists)

					invoke AddToAging VendorUnmatchedSummaryRel
						invoked.PrmTotalCount				= 1
						invoked.PrmNewAmount				= InvoiceAmount
				else

					invoke Create MatchAgingSummary
						invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
						invoked.MatchAging				= LocalMatchAging
						invoked.SummaryType				= 3 
						invoked.Vendor					= Vendor
						invoked.VendorGroup				= PayablesInvoice.VendorGroup
						invoked.TotalCount				+= 1
						invoked.NewAmount				= InvoiceAmount


		UpdateMigrationDate is a Set Action
			restricted
			default label is "UpdateMigrationDate"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
				PrmMatchAging					is a MatchAging
				OldMigrationDate 				is Date 
				NewMigrationDate 				is Date 

			Sort Order
				Company
				PayablesInvoice
			Instance Selection
				where (PrmFinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup 
				and    MatchAgingPeriod entered)

			Local Fields
				NewMatchAgingPeriod is like MatchAgingPeriod 
			Action Rules
				Empty Set Rules 
					invoke Update PrmMatchAging 
						invoked.MigrationDate = NewMigrationDate 

				Set Rules 
					Entrance Rules 

					Exit Rules 
						invoke Update PrmMatchAging 
							invoked.MigrationDate = NewMigrationDate 

				Instance Rules


					if  (MatchAgingPeriod not entered)

						NextSentence = true 
					else 
					if  (CreateDate = OldMigrationDate 
					or   CreateDate = NewMigrationDate)

						LocalMatchAging 			= PrmMatchAging
						LocalFinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
						LocalVendorGroup 			= PayablesInvoice.VendorGroup				

						if (CreateDate = NewMigrationDate)  
							DaysToMatch			= MatchDate - InvoiceDate
						else
							DaysToMatch			= MatchDate - CreateDate

						for each MatchAgingPeriodRel
							if (DaysToMatch <= each.ThroughDays)
								NewMatchAgingPeriod = each.MatchAgingPeriod
								end for each

						if (NewMatchAgingPeriod not entered)
							NewMatchAgingPeriod = 4  


						if (NewMatchAgingPeriod != MatchAgingPeriod)



							
							if (VendorSummaryRel exists)

								invoke SubtractFromAging VendorSummaryRel
									invoked.PrmTotalCount				= 1
									invoked.PrmNewAmount				= InvoiceAmount * -1
									invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod

							if (YearMonthSummaryRel exists)

								invoke SubtractFromAging YearMonthSummaryRel
									invoked.PrmTotalCount				= 1
									invoked.PrmNewAmount				= InvoiceAmount * -1
									invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod


						


							MatchAgingPeriod = NewMatchAgingPeriod 

							if (VendorSummaryRel exists)

								invoke AddToAging VendorSummaryRel
									invoked.PrmTotalCount				= 1
									invoked.PrmNewAmount				= InvoiceAmount
									invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod
							else

								invoke Create MatchAgingSummary
									invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
									invoked.MatchAging				= LocalMatchAging
									invoked.SummaryType				= 1 
									invoked.Vendor					= Vendor
									invoked.VendorGroup				= PayablesInvoice.VendorGroup
									invoked.TotalCount				+= 1
									invoked.NewAmount				= InvoiceAmount
									invoked.MatchAgingPeriod 		= MatchAgingPeriod

							if (YearMonthSummaryRel exists)

								invoke AddToAging YearMonthSummaryRel
									invoked.PrmTotalCount				= 1
									invoked.PrmNewAmount				= InvoiceAmount
									invoked.PrmMatchAgingPeriod 		= MatchAgingPeriod
							else

								invoke Create MatchAgingSummary
									invoked.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
									invoked.MatchAging				= LocalMatchAging
									invoked.SummaryType				= 2 
									invoked.Year					= CreateYear
									invoked.Month					= CreateMonth
									invoked.TotalCount				+= 1
									invoked.NewAmount				= InvoiceAmount
									invoked.MatchAgingPeriod 		= MatchAgingPeriod

