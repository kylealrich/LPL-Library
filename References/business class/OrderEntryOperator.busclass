OrderEntryOperator is a BusinessClass
    owned by oe
    prefix is OPE
    classic name is OPERATOR

    Ontology
        symbolic key is OrderEntryOperator
            classic set name is OPESET2
            classic name is OPR-CODE
            classic name for Company is DEF-COMPANY

    Patterns
        implements StaticJava

        disable AuditIndex

    Persistent Fields
        Name
        BillingProcessLevel
            classic name is DEF-PROC-LEV
		DefaultCustomer     	is a Customer
      	DefaultLocation     	is an InventoryLocation
      	DefaultOrderType    	is an OrderEntryOrderType
      	DefaultOrderSource  	is an OrderEntryOrderSource
      	DefaultOrderDate    	is Date
      	DefaultShipDate     	is Date
		RestrictToDefaults      is Boolean
      	AllowLocationTransfers 	is Boolean
      	CurrencyOverride        is Boolean
      	CurrencyRateOverride    is Boolean
      	
	Derived Fields			
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforUser"
			
		ContextMessageText is a MessageField
			restricted
			"OrderEntryOperator<OrderEntryOperator><Name>"
	
		OrderEntryOperatorFormTitle is a DerivedField
			type is Alpha size up to 80
			if (RecordExists)
				return OrderEntryOperatorTypeTitle
			else
				return OrderEntryOperatorTitleNoRecord
		
		OrderEntryOperatorTitleNoRecord is a LabelField
			restricted
			"OrderEntryOperator"
		
		OrderEntryOperatorTypeTitle is a LabelField
			restricted
			"OrderEntryOperator_<OrderEntryOperator>"

	Conditions
		AnonymizeLogAnonymizeRelExists	
			restricted
			when (first AnonymizeLogAnonymizeRel exists)
		
		AnonymizeLogPurgeRelExists	
			restricted
			when (first AnonymizeLogPurgeRel exists)

		RecordExists
			restricted
			when (OrderEntryOperator exists)
			
    Relations
        CustomerOrderRel
            one-to-many relation to CustomerOrder
            Field Mapping uses Set5
                related.Company            = Company
                related.OrderEntryOperator = OrderEntryOperator

		AnonymizeLogAnonymizeRel	
			one-to-many relation to AnonymizeLog
			Field Mapping uses ByOrderEntryOperator
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.BillingCompany			= Company
				related.OrderEntryOperator		= OrderEntryOperator
			Instance Selection
				where (related.Status.Anonymized)
				
		AnonymizeLogPurgeRel	
			one-to-many relation to AnonymizeLog
			Field Mapping uses ByOrderEntryOperator
				related.FinanceEnterpriseGroup	= Company.FinanceEnterpriseGroup
				related.BillingCompany			= Company
				related.OrderEntryOperator		= OrderEntryOperator
			Instance Selection
				where (related.Status.AuditLogPurged) 
	
		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company		= Company
				related.Customer	= DefaultCustomer
			
    Sets

        Set1
            indexed
            Sort Order
                OrderEntryOperator
                Company

	Field Rules
		OrderEntryOperator
			required
				"FieldUserIdIsRequired"
		
		Name

			required
		
		DefaultCustomer
			constraint (CompanyCustomerRel exists)
				"CompanyCustomerDoesNotExist"
		
		CurrencyOverride
			default to Company.MultipleCurrency
			
			constraint (CurrencyOverride = Company.MultipleCurrency)
				"CannotOverrideCurrency;CompanyDoesNotHaveMultipleCurrency" //OE53.123"
				
		CurrencyRateOverride
			default to Company.RateOverride
			
			constraint (CurrencyOverride = Company.RateOverride)
				"CannotOverrideRate;CompanyOptionNotSelected" //OE53.124"	
		
		DefaultOrderDate
			constraint (DefaultOrderDate <= current corporate date)
				"OrderDateCannotBeGreaterThanCurrentDate" //OE53.126"
		
		DefaultShipDate
			constraint (DefaultShipDate >= current corporate date)
				"ShipDateCannotBeLessThanCurrentDate" //Oe53.128"
			
			if (DefaultOrderDate entered)
				constraint (DefaultShipDate >= DefaultOrderDate)
					"ShipDateCannotBeLessThanOrderDate" //OE53.127"
					
		RestrictToDefaults
			if(RestrictToDefaults)
				constraint(Company  entered)
					"CompanyRequiredWhenRestrictedToDefaults"
						
				if(AllowLocationTransfers)
					cannot be entered
						"LocationTransferMust=NoIfUserRestrictToDefault"
						
				constraint(DefaultOrderType  entered)
					"OrderTypeRequiredWhenUserRestrictedToDefaults"
						
				constraint(BillingProcessLevel  entered)
					"ProcessLevelRequiredWhenUserRestrictedToDefaults"
						
				constraint(DefaultCustomer  entered)
					"CustomerRequiredWhenUserRestrictedToDefaults"
						
				constraint(DefaultLocation  entered)
					"LocationRequiredWhenUserRestrictedToDefaults"
						
				constraint(DefaultOrderSource  entered)	
					"MustEnterOrderSourceWhenUserRestrictedToDefaults"
							
	Actions
		Create is a Create Action 
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		Anonymize is an Instance Action	
			restricted
			Action Rules
				if (AnonymizeLogAnonymizeRelExists)
					confirmation required
						"Warning,TheAnonymizeActionIsNonReversableAndAnonymizeRecordAlreadyExists;AreYouSureYouWantToContinue?"
					confirmation required
						"ProceedToAnonymizeAgain?"		
				else
					confirmation required
						"Warning,TheAnonymizeActionIsNonReversable;AreYouSureYouWantToContinue?"
					confirmation required
						"ProceedToAnonymize?"
										
				Name = 999999999
				 	
			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= Company.FinanceEnterpriseGroup
					invoked.Status						= 1
					invoked.AffectedBusinessClass		= "OE1"
					invoked.BillingCompany				= Company
					invoked.OrderEntryOperator			= OrderEntryOperator
	
	    PurgeAuditLog is an Instance Action	
	    	restricted
	        Action Rules
	        	if (AnonymizeLogPurgeRelExists)
					confirmation required
						"Warning,ThePurgeAuditLogActionIsNonReversableAndAnonymizeRecordAlreadyExists;AreYouSureYouWantToContinue?"
					confirmation required
						"ProceedToPurgeAuditLogAgain?"		
				else
					confirmation required
						"Warning,ThePurgeAuditLogActionIsNonReversable;AreYouSureYouWantToContinue?"
					confirmation required
						"ProceedToPurgeAuditLog?"			

				invoke purge audit log entries
				 
          	Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup		= Company.FinanceEnterpriseGroup
					invoked.Status						= 2
					invoked.AffectedBusinessClass		= "OE1"
					invoked.BillingCompany				= Company
					invoked.OrderEntryOperator			= OrderEntryOperator
          			
			
