TransferPricingRun is a BusinessClass
	owned by transpricing
	
	prefix is TrPRn
	
	Ontology
		symbolic key is TransferPricingRun
		
	
	Patterns
		
	Persistent Fields
		AllocationSourceSystem
		AllocationControl
		AllocationRun
        Status				is Numeric 1
            States
                Started				value is 0
                Completed			value is 1
                Failed				value is 2
                Processed			value is 3
                
        PostingDate			is Date
        TransactionDate		is Date
		CurrencyTable
		AllEntities			is Boolean		
		BatchRun			is a TransferPricingBatchRun		
		ErrorMessage 		is Alpha 150	
		AllocateAmountType
			default label is "SourceAmountType"
	
	Local Fields
		
		LocalAllocation						is an Allocation
		LocalAllocationLine					is an AllocationLine
		LocalGeneralLedgerCompany			is a GeneralLedgerCompany
		LocalAccountingEntity				is an AccountingEntity
		LocalParentAccountingEntity			is an AccountingEntity
		LocalTransferPricingSource			is a TransferPricingSource
		LocalTransferPricingTransactionView is a TransferPricingTransaction view
		LocalTransferPricingTransaction		is a TransferPricingTransaction
		LocalParentTransferPricingTransaction is a TransferPricingTransaction
		
	Derived Fields
		NoTransactionErrorMessage is a MessageField
			"<AllocationRun.NoTransactionsErrorMessage>;<CountTransactionWarningMessage>"
	
		CountTransactionErrorMessage is a MessageField
			"ErrorCount:<instance count of TransferPricingTransactionWithErrorRel>;<CountTransactionWarningMessage>"
		
		CountTransactionWarningMessage is a MessageField
			"WarningCount:<instance count of TransferPricingTransactionWithWarningRel>"
			
		DerivedErrorMessage is a DerivedField
			type is Alpha 150	
			default label is "Error"
			if (Status.Failed)
				if (AllocationRun.NoTransactions)
					return NoTransactionErrorMessage
				else 
				if (ErrorMessage entered)
					return ErrorMessage
				else	
					return CountTransactionErrorMessage

		TransferPricingRunTitle is a MessageField
			"Recharge_Calculation<TransferPricingRun>_-_<TransferPricing>"
	Sets
    	ByRechargeCalculationDescending
    		indexed
    		Sort Order
    			FinanceEnterpriseGroup
    			TransferPricing
				TransferPricingRun descending
				
		ByStatusAscending
    		Sort Order
    			FinanceEnterpriseGroup
    			TransferPricing
				Status
				TransferPricingRun descending
				
		ByTransactionDateDescending
    		Sort Order
    			FinanceEnterpriseGroup
    			TransferPricing
				TransactionDate descending
				TransferPricingRun descending
				
		ByAllocationRun
			indexed
			Sort Order
				FinanceEnterpriseGroup
				AllocationRun				
	
	Relations
		AllocationRel
			one-to-many relation to Allocation
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AllocationSourceSystem	= AllocationSourceSystem
				related.AllocationControl 		= AllocationControl

		AllAllocationTransactionDetailRel
			one-to-many relation to AllocationTransactionDetail
			Field Mapping uses ByAccountingEntity
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AllocationControl		= AllocationControl
				related.AllocationRun			= AllocationRun
				
		AllocationTransactionDetailRel
			one-to-many relation to AllocationTransactionDetail
			Field Mapping uses ByAllocationAndStep
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AllocationRun			= AllocationRun
			Instance Selection
				where (related.FinanceCodeBlock.ToAccountingEntity		= LocalAccountingEntity
				and    related.AllocationLine							= LocalAllocationLine)
									
		CurrentTransferPricingTransactionDetailRel
			one-to-one relation to TransferPricingTransactionDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.TransferPricing				= TransferPricing
				related.TransferPricingRun			= TransferPricingRun
				related.TransferPricingTransaction  = LocalTransferPricingTransaction
				related.TransferPricingSource		= LocalTransferPricingSource	
			
		TransferPricingEntitiesRel
			one-to-many relation to TransferPricingEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing

		CurrentTransferPricingGlobalEntityRel
			one-to-one relation to TransferPricingGlobalEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.AccountingEntity		= LocalAccountingEntity
				
		TransferPricingGlobalAdditionalEntityRel
			one-to-many relation to TransferPricingGlobalAdditionalEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
			Instance Selection
				where (related.AdditionalEntity	= LocalAccountingEntity)
				
		TransferPricingSourcesRel
			one-to-many relation to TransferPricingSource
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing 
		
		TransferPricingUnprocessedWithValidTransactionRel
			one-to-many relation to TransferPricingTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing 
				related.TransferPricingRun		= TransferPricingRun
			Instance Selection
				where (related.IsValidForICB)

		CurrentTransferPricingTransactionRel
			one-to-many relation to TransferPricingTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing 
				related.TransferPricingRun		= TransferPricingRun
			Instance Selection
				where (related.ToEntity			= LocalAccountingEntity)
				
		TransferPricingTransactionParentRel
			one-to-many relation to TransferPricingTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing 
				related.TransferPricingRun		= TransferPricingRun
			Instance Selection
				where (related.ToEntity			= LocalParentAccountingEntity
				and    related.Parent			= blank
				and    not related.TransferPricingTransaction = LocalTransferPricingTransaction)
		
		CurrentTransferPricingGlobalAdditionalEntitiesRel
			one-to-many relation to TransferPricingGlobalAdditionalEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.AccountingEntity		= LocalAccountingEntity
			Instance Selection
				where (related.AdditionalEntity = LocalAccountingEntity)
				
		TransferPricingTransactionResequenceRel
			one-to-many relation to TransferPricingTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing 
				related.TransferPricingRun		= TransferPricingRun
			Instance Selection
				where (related.Parent			= blank)
				
		AllocationLineRel
			one-to-many relation to AllocationLine
			Field Mapping uses symbolic key
    			related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
    			related.AllocationSourceSystem	= AllocationSourceSystem
    			related.AllocationControl		= AllocationControl
			Instance Selection
				where (related.AllocateYearToDate)
				
		TransferPricingProcessedTransactionRel is a TransferPricingTransaction set
			Instance Selection
				where (related.Status.Processed)
				
		TransferPricingTransactionWithErrorRel is a TransferPricingTransaction set
			Instance Selection
				where (related.Status.Failed)
				
		TransferPricingTransactionWithWarningRel is a TransferPricingTransaction set
			Instance Selection
				where (not related.WarningFlag)
				
		TransferPricingUnprocessedTransactionRel
			one-to-many relation to TransferPricingTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TransferPricing			= TransferPricing 
				related.TransferPricingRun		= TransferPricingRun
			Instance Selection
				where (related.Status.Unprocessed)

	Conditions
		IsValidForICB
			restricted
			when (TransferPricingUnprocessedWithValidTransactionRel exists)
			
		WarningExists
			restricted
			when (Status.Completed and TransferPricingTransactionWithWarningRel exists)
			
	Rule Blocks
		CreateTransactionDetails
			for each TransferPricingSourcesRel
				LocalAllocationLine	= each.TransferPricingSource

				initialize LocalSourceAmount

				for each AllocationTransactionDetailRel
					LocalTransactionAmount 				+= each.TransactionAmount
					LocalSourceAmount					+= each.TransactionAmount
				
				invoke Create TransferPricingTransactionDetail
					fill in fields from this instance
					invoked.TransferPricingTransaction 	= LocalTransferPricingTransaction
					invoked.TransferPricingSource		= each.TransferPricingSource
					invoked.TransactionAmount			= LocalSourceAmount

			LocalParentTransactionAmount		+= LocalTransactionAmount
			
			invoke SetTransactionAmount LocalTransferPricingTransaction
				invoked.PrmTransactionAmount			= LocalTransactionAmount
				
			if (LocalTransferPricingTransaction.Status.Failed)
				increment LocalTransactionErrorCount
		
		CreateTransactionHeader
	
			invoke Create TransferPricingTransaction
				assign result to LocalTransferPricingTransactionView
				
				fill in fields from this instance
					except invoked.CurrencyTable
				invoked.AllocationControl				= AllocationControl
				invoked.AllocationRun					= AllocationRun
										
				invoked.FromEntity						= TransferPricing.Company.AccountingEntity
				invoked.FromCompany						= TransferPricing.Company
				invoked.ToCompany						= LocalGeneralLedgerCompany
				invoked.ToEntity						= LocalAccountingEntity 
				
				invoked.Currency						= TransferPricing.Currency
				invoked.Parent							= LocalParentTransferPricingTransaction
				
			LocalTransferPricingTransaction	= LocalTransferPricingTransactionView.TransferPricingTransaction
			
		CreateTransactionParent
			invoke Create TransferPricingTransaction
				assign result to LocalTransferPricingTransactionView
				fill in fields from this instance
					except invoked.CurrencyTable
				invoked.AllocationControl				= AllocationControl
				invoked.AllocationRun					= AllocationRun
				invoked.FromCompany						= TransferPricing.Company
				invoked.FromEntity						= TransferPricing.Company.AccountingEntity
				if (LocalParentAccountingEntity entered)
					invoked.ToEntity					= LocalParentAccountingEntity
				else
					invoked.ToEntity					= LocalAccountingEntity 
				invoked.Currency						= TransferPricing.Currency
			
			invoke ResolveAccounts LocalTransferPricingTransactionView.TransferPricingTransaction
			
			invoke UpdateFields each
				invoked.PrmParentTransaction 	= LocalTransferPricingTransactionView.TransferPricingTransaction
				
			invoke AddTransactionAmount LocalTransferPricingTransactionView.TransferPricingTransaction
				invoked.PrmTransactionAmount = each.TransactionAmount
			
	
	Actions
		Delete is a Delete Action
			Local Fields
				LocalAllocationRun					is an AllocationRun
			Entrance Rules
				constraint (TransferPricingProcessedTransactionRel not exists)
					"CannotDelete:InterfacedTransferPricingTransactionExists."	
				LocalAllocationRun = AllocationRun
				initialize AllocationRun
				for each TransferPricingTransaction set
					invoke Delete each
			Action Rules 
				if (LocalAllocationRun.Status.Started)
					invoke Started.DeleteRun LocalAllocationRun
				else
				if (LocalAllocationRun.Status.Completed)
					invoke Completed.DeleteRun LocalAllocationRun
				else
				if (LocalAllocationRun.Status.Failed)
					invoke Failed.DeleteRun LocalAllocationRun
			Exit Rules
				invoke ChangeStatus BatchRun
					invoked.PrmDeleteFlag 	= true
					invoked.PrmRun 			= TransferPricingRun
				
				invoke Delete AllocationControl	
					resume on error
			
	StateCycles
		RechargeProcessing is a StateCycle
			state field is Status

			Started is a State
				Create is a Create Action
					restricted
					
				Run is an Instance Action
					restricted
					Parameters
						PrmDate					is Date
							default label is "Date"
						PrmSkipRefresh			is Boolean
					Action Rules
		
						invoke Run AllocationControl
							invoked.PrmDate						= PrmDate
							invoked.PrmStartDate				= TransferPricing.StartDate
							invoked.PrmRetainSourceValues	 	= true
							invoked.PrmSkipRefresh				= PrmSkipRefresh
							invoked.PrmAllocationCallback.AllocationTransferPricingCallBack.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.PrmAllocationCallback.AllocationTransferPricingCallBack.TransferPricing		    = TransferPricing
							invoked.PrmAllocationCallback.AllocationTransferPricingCallBack.TransferPricingRun	    = TransferPricingRun
							
						AllocationRun = AllocationControl.AllocationRun	

				CreateTransaction is an Instance Action
					restricted
					Local Fields
						LocalTransactionCounter			is Numeric size 9
						LocalTransactionChildCounter	is Numeric size 9
						LocalTransactionErrorCount		is Numeric size 4
						LocalTransactionAmount			is an InternationalAmount
						LocalParentTransactionAmount	is an InternationalAmount
						LocalSourceAmount				is an InternationalAmount
					Action Rules
						if (TransferPricing.AllEntities)
							initialize LocalAccountingEntity
							initialize LocalAllocation
							initialize LocalAllocationLine
							for each AllAllocationTransactionDetailRel
								if (LocalAllocationLine != each.AllocationLine or LocalAccountingEntity != each.FinanceCodeBlock.ToAccountingEntity)
									if (LocalAllocationLine entered)
										invoke Create TransferPricingTransactionDetail
											fill in fields from this instance
											invoked.TransferPricingTransaction 	= LocalTransferPricingTransaction
											invoked.TransferPricingSource		= LocalAllocationLine 
											invoked.TransactionAmount			= LocalSourceAmount
									
									initialize LocalSourceAmount
									LocalAllocationLine = each.AllocationLine
									LocalAllocation		= each.Allocation
								
								if (LocalAccountingEntity != each.FinanceCodeBlock.ToAccountingEntity)
									if (LocalAccountingEntity entered)
										invoke SetTransactionAmount LocalTransferPricingTransaction
											invoked.PrmTransactionAmount		= LocalTransactionAmount
									
									initialize LocalTransactionAmount						
									LocalAccountingEntity 						= each.FinanceCodeBlock.ToAccountingEntity
									
									invoke Create TransferPricingTransaction
										assign result to LocalTransferPricingTransactionView
										fill in fields from this instance
											except invoked.CurrencyTable
										invoked.AllocationControl				= AllocationControl
										invoked.AllocationRun					= AllocationRun
																
										invoked.FromCompany						= TransferPricing.Company
										invoked.FromEntity						= TransferPricing.Company.AccountingEntity
										
										invoked.ToEntity						= LocalAccountingEntity 
										invoked.Currency						= TransferPricing.Currency					
										
									LocalTransferPricingTransaction	= LocalTransferPricingTransactionView.TransferPricingTransaction
		
								LocalTransactionAmount 				+= each.TransactionAmount
								LocalSourceAmount					+= each.TransactionAmount
		
							if (LocalAllocationLine entered)
								invoke Create TransferPricingTransactionDetail
									fill in fields from this instance
									invoked.TransferPricingTransaction 	= LocalTransferPricingTransaction
									invoked.TransferPricingSource		= LocalAllocationLine 
									invoked.TransactionAmount			= LocalSourceAmount
								invoke SetTransactionAmount LocalTransferPricingTransaction
									invoked.PrmTransactionAmount		= LocalTransactionAmount
									
							for each TransferPricingTransaction set
								LocalTransferPricingTransaction = each.TransferPricingTransaction
									for each TransferPricingSourcesRel
										LocalTransferPricingSource = each.TransferPricingSource
											
										if (CurrentTransferPricingTransactionDetailRel not exists)
											invoke Create TransferPricingTransactionDetail
												fill in fields from this instance
												invoked.TransferPricingTransaction 	= LocalTransferPricingTransaction
												invoked.TransferPricingSource		= LocalTransferPricingSource
								
								LocalAccountingEntity = each.ToEntity
								
								if (each.Parent not entered
								and CurrentTransferPricingGlobalEntityRel.HasAdditionalEntities)
									include CreateTransactionParent
									
							for each TransferPricingTransaction set		
								LocalAccountingEntity = each.ToEntity
								
								if (each.Parent not entered
								and TransferPricingGlobalAdditionalEntityRel exists)
									LocalParentAccountingEntity		= first TransferPricingGlobalAdditionalEntityRel.AccountingEntity
									LocalTransferPricingTransaction = each.TransferPricingTransaction
									
									if (TransferPricingTransactionParentRel not exists)
										include CreateTransactionParent
									else
										invoke UpdateFields each
											invoked.PrmParentTransaction 	= first TransferPricingTransactionParentRel.TransferPricingTransaction
											
										invoke AddTransactionAmount first TransferPricingTransactionParentRel.TransferPricingTransaction
											invoked.PrmTransactionAmount = each.TransactionAmount
						else
							for each TransferPricingEntitiesRel
								LocalGeneralLedgerCompany 	= each.Company
								LocalAccountingEntity 		= each.Company.AccountingEntity
						
								initialize LocalTransactionAmount

								initialize LocalParentTransferPricingTransaction
								include CreateTransactionHeader
								invoke ResolveAccounts LocalTransferPricingTransaction
																								
								if (each.AdditionalEntityExist)
									initialize LocalParentTransactionAmount
									LocalParentTransferPricingTransaction = LocalTransferPricingTransaction 

									include CreateTransactionHeader
									include CreateTransactionDetails
									
									initialize LocalGeneralLedgerCompany
									for each each.TransferPricingAdditionalEntitiesRel
										LocalAccountingEntity 		= each.AdditionalEntity
										initialize LocalTransactionAmount
										
										include CreateTransactionHeader
										include CreateTransactionDetails
										
									invoke SetTransactionAmount LocalParentTransferPricingTransaction
										invoked.PrmTransactionAmount			= LocalParentTransactionAmount
										
								else
									include CreateTransactionDetails
															
						if (AllocationRun.NoTransactions
						or ErrorMessage entered
						or LocalTransactionErrorCount entered)
							make transition to Failed
						else
							make transition to Completed		
					
						initialize LocalTransactionCounter
						for each TransferPricingTransactionResequenceRel
							increment LocalTransactionCounter
							invoke UpdateFields each
								invoked.PrmTransactionNumber = LocalTransactionCounter
								
							if (each.HasChildren)
								initialize LocalTransactionChildCounter
								for each each.ChildrenRel
									increment LocalTransactionChildCounter
									invoke UpdateFields each
										invoked.PrmTransactionNumber 	= LocalTransactionChildCounter
										invoked.PrmSetChildStatus 		= true
					
				Exit Rules
					invoke ChangeStatus BatchRun
						invoked.PrmStatus	= BatchRun.Status.Completed
						invoked.PrmRun 		= TransferPricingRun
					
            Completed is a State
            	CreateBillingDocument is an Instance Action
					valid when (IsValidForICB)
					default label is "InterfaceRechargeTransaction"
					Action Rules	
						invoke InterfaceAllTransactions TransferPricingTransaction
							invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
							invoked.PrmTransferPricing			= TransferPricing
							invoked.PrmTransferPricingRun		= TransferPricingRun
            
            	SetRunToProcessed is an Instance Action
					restricted
					Action Rules
						if (TransferPricingUnprocessedTransactionRel not exists)
							make transition to Processed
					Exit Rules
						invoke ChangeStatus BatchRun
							invoked.PrmStatus 	= BatchRun.Status.Processed
							invoked.PrmRun 		= TransferPricingRun
            
            Failed is a State
            
            Processed is a State
            	SetRunToUnprocessed is an Instance Action
            		restricted
            		Action Rules
            			if (TransferPricingUnprocessedTransactionRel exists)
            				make transition to Completed
        			Exit Rules
        				invoke ChangeStatus BatchRun
        					invoked.PrmStatus 	= BatchRun.Status.Completed
							invoked.PrmRun 		= TransferPricingRun
