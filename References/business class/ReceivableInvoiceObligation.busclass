ReceivableInvoiceObligation is a BusinessClass
	owned by ar
	prefix is TDD

	Ontology
		symbolic key is ReceivableInvoiceObligation

	Patterns
		disable Auditing
		disable EffectiveDated


	Persistent Fields
		AppliedStatus								  is Alpha size 20
		Customer
		Status										is Numeric size 1
			States
				Unreleased				 value is 0
				OpenTransaction			value is 1
					default label is "Open transaction"
				SelectForDraftApplication  value is 2
					default label is "Select for Draft Application"
				ApplicationByDraftAcactNo  value is 5
					default label is "Application by Draft AcAct = No"
				ApplicationByDraftAcactYes value is 6
					default label is "Application By Draft AcAct = Yes"
				FullyAppliedTransaction	value is 7
					default label is "Fully applied transaction"
				ClosedTransaction		  value is 8
					default label is "Closed transaction"
				HistoricalTransaction	  value is 9
					default label is "Historical transaction"

		LastStatementDate							 is Date
		TransactionDate							   is Date
		BaseAmount									is an InternationalAmount
			precision is Company.Currency.NumberOfDecimals			
		BaseAppliedAmount							 is an InternationalAmount
			precision is Company.Currency.NumberOfDecimals
		LastApplicationSequence
		BaseAdjustAmount							  is an InternationalAmount
			precision is Company.Currency.NumberOfDecimals
		DisputeSequence							   is a DisputeSeq
		DueDate
		LastFinanceChargeDate						 is Date
		DiscountDate								  is a DiscDateX3InAroitems
		DiscountPercent							   is a DiscPctX3InAroitems
		TransactionAmount							 is an InternationalAmount
		AppliedAmount								 is an InternationalAmount
		AdjustAmount								  is an InternationalAmount
		Description
		ItemUser1
		Project
		LineNumber
		LineType
		LineSequence								  is a Sequence3
		ItemReference								 is AlphaUpper size 32
		EFTNotification							   is an EftNote
		EftExtract
		EftAction
		GainLossAmount								is an InternationalAmount
		AlternateReceivableInvoiceType
		HistoryPeriods								is a HistPer
		InvoiceLineObjID
		CustomerPurchaseOrder
		TransactionUserField1						 is a TransUser1
		ReceivableProcessLevel
		SortAmount
		BankTransactionCode
		BankTransactionType						   is a BankInstType	
		Sort1
		Sort2
		Sort3
		Sort4
		OriginalCurrency							  is a Currency
			default label is "TransactionCurrency"	
		CurrencyDecimalsNumber						is an OrigNd
		CurrencyRate								  is an OrigRate
		BatchNumber
		CurrencyMultiplicationOrDivision
			sql name is CMultiplicationOrDivision
		SummarizeDetail							   is Boolean
		GeneralLedgerDate							 is Date
			default label is "GlobalLedgerDate"
		ReceivableGeneralLedgerCode
			default label is "ReceivableGlobalLedgerCode"
		RevalueCurrencyTransactions				   is Boolean
		CancelFlag
		CurrencyRateToCustomerGroupCurrency
			sql name is CRateToCustomerGroupCurrency
		CustomerGroupCurrencyMultiplicationOrDivision
			sql name is CGCMultiplicationOrDivision
		SalesRepresentative
		ReceivableCrossReference





		Origin										is AlphaUpper size 1
			States
				ManualInvoice			value is "B"
					default label is "Manual invoice"
				Chargeback			   value is "C"
				FinanceLatePaymentCharge value is "F"
					default label is "Finance late payment charge"
				GroupedMemo			  value is "G"
					default label is "Grouped memo"
				Converted				value is "L"
				ManualMemo			   value is "M"
					default label is "Manual memo"
				OrderProcessing		  value is "O"
					default label is "Order processing"
				ReturnedPayment		  value is "R"
					default label is "Returned payment"
				ClosingCreatedChargeback value is "E"
					default label is "Closing created chargeback"
				PaymentCreatedCreditMemo value is "P"
					default label is "Payment created credit memo"
				Obligation			   value is "X" 
 
		TermsCode
		ProjectContractCustomer					   is an AcCustomerId
		ExemptProject								 is Boolean
		LateCharge									is AlphaUpper size 1
		LastDunningLetter
		LastDunningLetterDate						 is Date
		LastDunningLetterTextCode					 is like DunningTextCode
		SentForCollection							 is a SentForColl
		AssessDunningFee							  is an AssessDunFee
		DunningLetterIndicator						is Boolean
		DiscountAmount								is an OrigDiscAmtX3InAroitems

		NationalAccountLastStatementDate			  is Date
		CreditDebitMemoReason

		DestinationIntrastatCountry					  is AlphaUpper size 2
		OriginIntrastatCountry		 				  is AlphaUpper size 2
		CustomerBillTo								is a BillTo
		ForeignEconomicRegulatoryCode				 is a Code

	Field Rules
		ReceivableInvoiceObligation
			if (ReceivableInvoiceRel.SummarizeDetail = "Y")			
				constraint (IsSummarizedLinesAndSameDueDate)		
					"IfSummarizedLineItemsIndicated,AllObligationDueDatesMustBeTheSame"  			
			constraint (ReceivableInvoiceRel.Status.Released)
				"TransactionIsNotReleased,CannotAdd"
  			constraint (IsReceivableInvoiceDetailOpen)
				"NoInvoiceDetailTransactionsAreOpenStatus"

		TransactionAmount


			required


			if (ReceivableInvoice.ReceivableInvoiceType.Invoice or ReceivableInvoice.ReceivableInvoiceType.DebitMemo)
				constraint (TransactionAmount > 0)
						"TransactionTypeNotMixedSignedType,CannotEnterNegativeAmount"
		DueDate
			required
		Customer
			default to ReceivableInvoice.Customer
		Status  
			default to 1
		TransactionUserField1
			default to ReceivableInvoice.TransactionUserField1		
		TransactionDate
			default to ReceivableInvoice.TransactionDate
		AlternateReceivableInvoiceType
			default to ReceivableInvoice.AlternateReceivableInvoiceType			   
		CustomerPurchaseOrder
			default to ReceivableInvoice.CustomerPurchaseOrder 
		ReceivableProcessLevel
			default to ReceivableInvoice.ReceivableProcessLevel

		BankTransactionCode
			default to ReceivableInvoice.BankTransactionCode
		BankTransactionType
			default to ReceivableInvoice.BankTransactionType
		OriginalCurrency
			default to ReceivableInvoice.OriginalCurrency		
		CurrencyDecimalsNumber
			default to ReceivableInvoice.CurrencyDecimalsNumber			
		CurrencyRate
			default to ReceivableInvoice.InvoiceAmount.FunctionalAmount.EnteredCurrencyRate
		BatchNumber
			default to ReceivableInvoice.BatchNumber
		CurrencyMultiplicationOrDivision
			default to ReceivableInvoice.CurrencyMultiplicationOrDivision		
		SummarizeDetail
			default to ReceivableInvoice.SummarizeDetail		
		GeneralLedgerDate 
			force default to ReceivableInvoice.GeneralLedgerDate
				  
		ReceivableGeneralLedgerCode
			default to ReceivableInvoice.ReceivableGeneralLedgerCode		  
		RevalueCurrencyTransactions
			default to ReceivableInvoice.ReceivableInvoice		 
		CancelFlag
			default to ReceivableInvoice.CancelFlag   
		Description
			default to ReceivableInvoice.Description		   
		CurrencyRateToCustomerGroupCurrency
			default to ReceivableInvoice.CurrencyRateToCustomerGroupCurrency	 
		CustomerGroupCurrencyMultiplicationOrDivision
			default to ReceivableInvoice.CustomerGroupCurrencyMultiplicationOrDivision	 
		SalesRepresentative
			default to ReceivableInvoice.SalesRepresentative		  
		ReceivableCrossReference.Company
			default to ReceivableInvoice.ReceivableCrossReference.Company	
		ReceivableCrossReference.ReceivableInvoiceType
			default to ReceivableInvoice.ReceivableInvoiceType	
		ReceivableCrossReference.ReceivableInvoice
			default to ReceivableInvoice.ReceivableInvoice			
		Origin
			default to ReceivableInvoice.Origin
		TermsCode
			default to ReceivableInvoice.TermsCode
		ProjectContractCustomer
			default to ReceivableInvoice.ProjectContractCustomer


		LastDunningLetter
			default to ReceivableInvoice.LastDunningLetter
		LastDunningLetterDate
			default to ReceivableInvoice.LastDunningLetterDate		
		LastDunningLetterTextCode
			default to ReceivableInvoice.LastDunningLetterTextCode 
		SentForCollection
			default to ReceivableInvoice.SentForDunningCollection		 
		AssessDunningFee
			default to ReceivableInvoice.AssessDunningFee	
		DunningLetterIndicator
			default to ReceivableInvoice.DunningLetterIndicator		 
		CreditDebitMemoReason
			default to ReceivableInvoice.CreditDebitMemoReason		  


		DestinationIntrastatCountry
		 	default to ReceivableInvoice.DestinationIntrastatCountry	   
		OriginIntrastatCountry
			default to ReceivableInvoice.OriginIntrastatCountry		 
		CustomerBillTo
			default to ReceivableInvoice.CustomerBillTo		 
		ForeignEconomicRegulatoryCode
		 	default to ReceivableInvoice.ForeignEconomicRegulatoryCode	   


	Sets
		ByReceivableInvoice
			duplicates
			Sort Order
				Company
				ReceivableInvoiceType
				ReceivableInvoice
	


	Relations
		ReceivableInvoiceObligationRel		
			one-to-many relation to ReceivableInvoiceObligation
			Field Mapping uses ByReceivableInvoice
				related.Company 						= Company
				related.ReceivableInvoiceType			= ReceivableInvoiceType		
				related.ReceivableInvoice 				= ReceivableInvoice				
			Instance Selection
				where  (related.UniqueID not = UniqueID)

		ReceivableInvoiceRel		
			one-to-one relation to ReceivableInvoice
			Field Mapping uses symbolic key
				related.Company 						= Company
				related.ReceivableInvoiceType			= ReceivableInvoiceType		
				related.ReceivableInvoice 				= ReceivableInvoice				


	Conditions
		IsSummarizedLinesAndSameDueDate
			restricted
			when (ReceivableInvoiceRel.first ReceivableInvoiceDetailRel.DueDate = all ReceivableInvoiceRel.ReceivableInvoiceDetailRel.DueDate
			and ReceivableInvoiceRel.SummarizeDetail = "Y")

		IsReceivableInvoiceDetailOpen
			restricted
			when (any ReceivableInvoiceRel.ReceivableInvoiceDetailRel.Status.OpenTransaction)

  							
	Actions
		Create is a Create Action 

		Update is an Update Action				
			Action Rules
				constraint (Status.OpenTransaction)
					"CannotChange,TransactionIsNotOpen"				
				if (Project entered)
					constraint (!TransactionAmount changed)
						"CannotChangeTransactionAmount,ProjectExits"	
				if (SummarizeDetail)
					constraint (DueDate = all ReceivableInvoiceObligationRel.DueDate)
						"SummarizeDetailForDisplayRequiresAllDueDatesToBeTheSame"	
				else
					if(DueDate changed)
						invoke Update ReceivableInvoiceRel.ReceivableInvoiceDetailRel
							invoked.DueDate = DueDate						

		Delete is a Delete Action
			Action Rules
				constraint (Project not entered)
					"CannotDelete,ProjectExists"		






			
			
			



			









							
