ContractQuestionListValue is a BusinessClass
    owned by po
    prefix is QCLV

    Ontology
    	symbolic key is ContractQuestionListValue

    Patterns
        implements DynamicCreation
		implements Resequence on DisplayOrder
			new sequence field is NewDisplayOrder
			set is ByDisplayOrder
     
    Dynamic Creation Rules 
    	constraint (parentcontext.name = "ContractQuestionAnswer")
    		"TheAnswer-<ContractQuestionListValue>-ForTheQuestion-<ContractQuestion.QuestionText>-DoesNotExist"

    Persistent Fields
    	DisplayOrder
		QuestionToCreate	is like Question

	Transient Fields
		NewDisplayOrder				is a DisplayOrder
		FromTemplateOrCopy          is Boolean
    		    

	Local Fields 
		LocalConditionalQuestion is like Question 

	Conditions 
		RepositoryHasConditionalQuestions 
			restricted
			when (RepositoryConditionalQuestionRel exists)

		HasContractConditionalQuestions
			restricted
			when (ContractConditionalListQuestionRel exists)
	
	Relations 

		ContractConditionalListQuestionRel is a ContractConditionalListQuestion set 
		LocalConditionalQuestionRel 
			one-to-one relation to ContractConditionalListQuestion
			Field Mapping uses symbolic key 
				related.ContractGroup   				= ContractGroup
				related.Contract                        = Contract
				related.ContractQuestion    			= ContractQuestion
				related.ContractQuestionListValue       = ContractQuestionListValue
				related.ContractConditionalListQuestion	= LocalConditionalQuestion	

		SameRepositoryQuestionRel 
			one-to-many relation to ContractQuestion 
			Field Mapping uses ByQuestion
				related.ContractGroup       = ContractGroup 
				related.Question            = LocalConditionalQuestion 
				related.Contract            = Contract 

		RepositoryConditionalQuestionRel 
			one-to-many relation to ConditionalQuestion 
			Field Mapping uses ByListAnswer
				related.ProcurementGroup										= ContractGroup 
				related.Question                        						= ContractQuestion.Question 
				related.QuestionAnswer.QuestionAnswerGroup.QuestionListValue	= ContractQuestionListValue 		

    Sets
    	ByDisplayOrder
    		duplicates
    		Sort Order
    			ContractGroup
    			Contract
    			ContractQuestion
    			DisplayOrder

 
	Actions
	
    	Create is a Create Action
    		valid when (!ContractQuestion.QuestionSource.Interview)

			Field Rules
				DisplayOrder
					if (!Contract.CreateByCopy)
						autosequence using ContractQuestion.LastQuestionValueDisplayOrder 

    		Action Rules 
    			if (ContractQuestion.Question entered) 
    				invoke Update ContractQuestion
    					initialize invoked.Question	

			Exit Rules 


		CreateDisplay is a Create Action 
			restricted
			Field Rules
				DisplayOrder
					if (!Contract.CreateByCopy)
						autosequence using ContractQuestion.LastQuestionValueDisplayOrder

			Exit Rules 









    	Update is an Update Action
    		valid when (!ContractQuestion.QuestionSource.Interview)
    		
		CreateConditionalQuestion is an Instance Action
			valid when (ContractQuestion.CanCreateConditionalListQuestion)
			Parameters 
				ParmConditionalQuestion is a Question
					default label is "ConditionalQuestion"

			Parameter Rules 
				ParmConditionalQuestion 
					required
			Action Rules

				constraint (ParmConditionalQuestion.Active)
					"CannotUseAnInactiveQuestion"
				LocalConditionalQuestion	= ParmConditionalQuestion
				constraint (SameRepositoryQuestionRel !exists)
					"ConditionalQuestionAlreadyExistsAsAContractQuestion"
				constraint (LocalConditionalQuestionRel !exists)
					"ConditionalQuestionAlreadyExists"
				if (ContractQuestion.Question entered)
					constraint (ParmConditionalQuestion != ContractQuestion.Question)
						"ConditionalQuestionCannotBeTheSameAsQuestion"

				invoke Create ContractConditionalListQuestion
					invoked.ContractGroup					= ContractGroup 
					invoked.Contract                        = Contract 
					invoked.ContractQuestion				= ContractQuestion
					invoked.ContractQuestionListValue		= ContractQuestionListValue
					invoked.ContractConditionalListQuestion	= ParmConditionalQuestion

    	CopyContractConditionalQuestions is an Instance Action 
			restricted 
			Parameters 
				NewContract		is a Contract 
				NewQuestion     is a ContractQuestion 

			Action Rules 
				for each ContractConditionalListQuestionRel
					invoke Create ContractConditionalListQuestion 
						invoked.ContractGroup					= ContractGroup 
						invoked.Contract                    	= NewContract 
						invoked.ContractQuestion            	= NewQuestion 
						invoked.ContractQuestionListValue   	= ContractQuestionListValue 
						invoked.ContractConditionalListQuestion	= each.ContractConditionalListQuestion
		
		Delete is a Delete Action
    		valid when (!ContractQuestion.QuestionSource.Interview)
    		
    		Action Rules
    			if (ContractQuestion.Question entered) 
    				invoke Update ContractQuestion
    					initialize invoked.Question
