ProjectContractReallocationSelection is a BusinessClass
	sql name is "ProjConRealSel"
    owned by Projects
    prefix is PCRSN

    Ontology
    	part of ProjectContract
    		relative key is SequenceNumber
    		
	Patterns
		disable Auditing

    Persistent Fields
    	GLTransactionDetail

	Derived Fields
		DerivedAmount is a DerivedField
			type is like InternationalAmount
				precision is ProjectContract.Currency.NumberOfDecimals
			return (GLTransactionDetail.ReportCurrencyAmount.ProjectAmount.EnteredCurrencyAmount)

    Conditions
			
    Relations
				
    Sets
    	ByGLTransactionDetail
    		no duplicates
    		Sort Order
    			FinanceEnterpriseGroup
    			GLTransactionDetail

    Field Rules
    	SequenceNumber
    		autosequence
    			minimize contention

	Actions
		Create is a Create Action
			restricted
		Delete is a Delete Action

		Update is an Update Action
			restricted
		Purge is a Purge Action
			restricted
		PurgeAll is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
				PrmProjectContract			is a ProjectContract
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and    ProjectContract			= PrmProjectContract)
			Sort Order is primary
			Action Rules
				Instance Rules
					invoke Purge

		UnselectTransactionsForReallocation is a Set Action
			restricted
			synchronized on "ProjectContract:<PrmFinanceEnterpriseGroup><ContractGroup>"
			Parameters
				PrmFinanceEnterpriseGroup	is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				ContractGroup				is a ProjectContract group
				PostingDateRange			is a DateRange
				TransactionDateRange		is a DateRange
				FundingSource1				is a FinanceDimension2
				FundingSource2				is a FinanceDimension2
				Billed						is Numeric 1
        		    States
        		    	Both				value is 0
                		Unprocessed			value is 1
		                Processed			value is 2
			   	System						is a GeneralLedgerSystemCode
				GeneralLedgerEvent
			Parameter Rules
				PrmFinanceEnterpriseGroup
					required
				FundingSource1
					constraint (FundingSource1.DimensionType.Posting)
						"<PrmFinanceEnterpriseGroup.FinanceDimension2Label>MustBePostingType"
				FundingSource2
					constraint (FundingSource2.DimensionType.Posting)
						"<PrmFinanceEnterpriseGroup.FinanceDimension2Label>MustBePostingType"
        	Instance Selection
        		where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup
        		and   (ProjectContract within ContractGroup
        		or     ContractGroup not entered)
        		and    GLTransactionDetail.PostingDate within PostingDateRange
        		and    GLTransactionDetail.TransactionDate within TransactionDateRange
				and   (GLTransactionDetail.FinanceCodeBlock.FinanceDimension2 = FundingSource1
				or     GLTransactionDetail.FinanceCodeBlock.FinanceDimension2 = FundingSource2
				or    (FundingSource1 not entered
				and    FundingSource2 not entered))
				and   (GLTransactionDetail.System = System
				or     System not entered)
				and   (GLTransactionDetail.GeneralLedgerEvent = GeneralLedgerEvent
				or     GeneralLedgerEvent not entered)
				and  ((GLTransactionDetail.Billed.Unprocessed
				and   (Billed.Both
				or     Billed.Unprocessed))
				or    (GLTransactionDetail.Billed.Processed
				and   (Billed.Both
				or     Billed.Processed))))
        	Action Rules
        		Instance Rules
        			invoke Purge
				
