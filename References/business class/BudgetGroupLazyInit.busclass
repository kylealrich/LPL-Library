BudgetGroupLazyInit is a BusinessClass
	owned by GeneralLedger
	prefix is BUDLZ  
   
    Ontology
    	symbolic key is BudgetGroupLazyInit

    Patterns
   		implements DynamicCreation
 		disable Auditing 
 		disable EffectiveDated

    Persistent Fields
    	BudgetLazyDocument
    		delete ignored
    	BudgetEditGroup				is UniqueID
    	IsForUpdate					is Boolean
		Counter						is Numeric 4	
		Amount						is a TotalAmount
		
    Transient Fields
		
	Local Fields
		BudgetGroupTotal
		PreviousAmount				is a TotalAmount
		
	Derived Fields
	
	Conditions

	Relations
		
		NotReadyLazyInitsRel
			one-to-many relation to BudgetGroupLazyInit
			Field Mapping uses ByBudgetEditGroup
				related.BudgetEditGroup					= BudgetEditGroup
			Instance Selection
				where (!related.BudgetGroup.LazyInitialized
				and related.BudgetGroupLazyInit			!= BudgetGroupLazyInit) 
        
		LazyDocumentRel
			one-to-one relation to BudgetLazyDocument
			Field Mapping uses symbolic key
				related.BudgetLazyDocument.DocumentID	= BudgetEditGroup

	Sets
		ByBudgetEditGroup
			Sort Order
				BudgetEditGroup
				BudgetGroup.LazyInitialized
							





	Field Rules
					
	Attach Rules
		
	Rule Blocks
		DoObligation
			PreviousAmount					= BudgetGroupTotal.Amount 
			invoke Update BudgetGroupTotal
				invoked.BusinessClassName	= "BudgetGroupLazyInit"
				invoked.EditContext			= "BudgetGroup=<BudgetGroup.>,FEG=<FinanceEnterpriseGroup>,Scenario=<Scenario>,BudgetTemplate=<BudgetTemplate>"
				invoked.IncrementAmount		= Amount
				invoked.PreviousAmount		= PreviousAmount

    Actions
	
    	Create is a Create Action
    		restricted
 			Exit Rules

 				if (BudgetGroup.LazyInitialized)
					if (IsForUpdate)
						include DoObligation
						invoke Delete
					else

					if (BudgetLazyDocument.ReadyToRelease
					and NotReadyLazyInitsRel not exists)
						invoke Delete
						invoke ReleaseRoute LazyDocumentRel.BudgetLazyDocument
					else
  						invoke Delete
    		
		Update is an Action
			restricted
				
		Delete is a Delete Action
			restricted
			bypass relational integrity rules
			
		UpdateBudgetGroupLazyInit is an Instance Action
			restricted
			refresh and lock this instance
			Parameters
				PrmBudgetEditGroup		is UniqueID
				PrmBudgetLazyDocument	is a BudgetLazyDocument
			Action Rules
				Counter				+= 1
				BudgetEditGroup		= PrmBudgetEditGroup
				BudgetLazyDocument	= PrmBudgetLazyDocument



		NotifyGroupIsReady is an Instance Action
			restricted
			refresh and lock this instance
			Action Rules
				display "NotifyLazy<BudgetGroup.DisplayMessage>"
				if (IsForUpdate)
					include DoObligation
					invoke Delete
				else
				if (BudgetLazyDocument.ReadyToRelease
				and NotReadyLazyInitsRel not exists)
					
					display "ReleasingDocument"
					invoke Delete
					invoke ReleaseRoute LazyDocumentRel.BudgetLazyDocument
				else
					display "DeleteNoReleaseDocument"
					invoke Delete
		
		NotifyGroupIsDeleting is an Instance Action
			restricted
			Action Rules
				if (IsForUpdate)
					invoke Delete
				else
				if (BudgetLazyDocument exists
				and BudgetLazyDocument.ReadyToRelease
				and NotReadyLazyInitsRel not exists)
					
					display "ReleasingDocument"
					invoke Delete
					invoke ReleaseRoute BudgetLazyDocument
				else
					display "DeleteNoReleaseDocument"
					invoke Delete
			
		DeleteAllLazyGroupsForTemplate is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmScenario						is a Scenario
					default label is "Scenario"
				PrmTemplate						is a BudgetTemplate
					default label is "Template"
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and Scenario					= PrmScenario
				and BudgetTemplate				= PrmTemplate) 
			Action Rules
				Instance Rules
					display "DeleteLazyDoc"
					invoke Delete BudgetLazyDocument
						resume on error
					display "DeleteLazyInit"
					invoke Delete
				
		DeleteAllLazyGroupsForScenario is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmScenario						is a Scenario
					default label is "Scenario"
			Instance Selection
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
				and Scenario					= PrmScenario)
			Action Rules
				Instance Rules
					invoke Delete BudgetLazyDocument
						resume on error
					invoke Delete
				
























					
