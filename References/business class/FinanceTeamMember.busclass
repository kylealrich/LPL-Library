FinanceTeamMember is a BusinessClass
	owned by sharedfinance
	prefix is FTMem
	representative text is "<FinanceTeamMember.TeamMember.Employee.FirstLastNameID>"
	representative image is FinanceTeamMember.TeamMember.Employee.ResourcePicture.File
		display as portrait image
		missing image is random background
			foreground text is "<FinanceTeamMember.TeamMember.Employee.Name.Initials>"

	Ontology
		symbolic key is FinanceTeamMember
	
	Patterns
		implements ContextualParent
			
	Persistent Fields

		SearchName			is a snapshot of FinanceTeamMember.TeamMember.SearchName
		MaxApprovalAmount	is a CurrencyAmount		
			default label is "PayablesMaximumApprovalAmount"

	Field Rules

	Context Fields
		FinanceResource

	Local Fields
		LocalYear					is Year
		LocalPeriod					is Numeric 2
		LocalTeam					is AlphaUpper 20
		LocalGroupingForTeam		is Numeric 9
			
	Derived Fields
		PreferredPresentationName is a DerivedField
            type is Alpha size 153
            return FinanceTeamMember.TeamMember.PreferredPresentationName
            

		OtherTeamMember is a DerivedField
			type is Boolean
			restricted
			if (!FinanceTeamMember.TeamMember = FinanceResource)
				return true
	
		EmailAddress is a DerivedField
			type is Alpha 100
			return FinanceTeamMember.TeamMember.EmailAddress
				

	Conditions
		ActiveEmployee 
			restricted
			when (FinanceTeamMember.TeamMember.ActiveEmployee)



		ActiveResource
			restricted
			when (FinanceTeamMember.TeamMember.ActiveResource)



		DisplayMessage
			restricted
			when (FinanceTeamMember.TeamMember.DisplayMessage entered)
		AskQuestionValid
			restricted
			when (FinanceTeamMember.TeamMember.Active
			and   FinanceTeamMember != actor.agent(Employee).Employee)
		HasPeriodTasksOpen
			when (OpenTaskRel exists)
		HasCloseTasks
			when (CloseTaskRel exists)
		HasOpenPeriodReconciliations
			when (OpenPeriodEndReconciliationRel exists)
		HasReconciliationAccounts
			when (ReconciliationAccountRel exists)
		TeamMemberEntered
			when (FinanceTeamMember.TeamMember entered)
		
	Relations
		SendToTeamMembersRel
			one-to-many relation to FinanceTeamMember 
			Field Mapping uses ByFinanceTeam
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.FinanceTeam 			= LocalTeam		
		OpenTaskRel
			one-to-many relation to ClosePeriodTask
			Field Mapping uses ByTeamMemberAndStatus
				related.CloseManagementGroup = FinanceEnterpriseGroup
				related.FinanceTeam			 = FinanceTeam
				related.TaskOwner.TeamMember = FinanceTeamMember.TeamMember
				related.TaskLevel			 = 1 
			Instance Selection
				where (related.TaskStatus < 7)

		CloseTaskRel
			one-to-many relation to CloseTask
			Field Mapping uses ByTeamAndOwner
				related.CloseManagementGroup	= FinanceEnterpriseGroup
				related.FinanceTeam				= FinanceTeam
				related.TaskOwner.TeamMember	= FinanceTeamMember.TeamMember

		OpenPeriodEndReconciliationRel
			one-to-many relation to PeriodEndReconciliation
			Field Mapping uses ByAssignedResource
				related.ReconciliationManagementGroup	= FinanceEnterpriseGroup
				related.AssignedResource.TeamMember		= FinanceTeamMember.TeamMember 
			Instance Selection
				where (related.FinanceTeam = FinanceTeam
				and    related.Status < 3)
				
		ReconciliationAccountRel
			one-to-many relation to ReconciliationAccount
			Field Mapping uses ByAssignedResource
				related.ReconciliationManagementGroup	= FinanceEnterpriseGroup
				related.AssignedResource.TeamMember		= FinanceTeamMember.TeamMember 
			Instance Selection
				where (related.FinanceTeam = FinanceTeam)















	Sets
		ByFinanceTeam
			Sort Order
				FinanceEnterpriseGroup
				FinanceTeam
				FinanceTeamMember.TeamMember
					
		ByResource
			Sort Order
				FinanceEnterpriseGroup
				FinanceTeamMember.TeamMember
				FinanceTeam

		BySearchName
            Sort Order
                FinanceEnterpriseGroup
                FinanceTeam
                SearchName
                FinanceTeamMember.TeamMember
    Actions
		AddMember is a Create Action









		Update is an Update Action		

		RemoveMember is a Purge Action
			Entrance Rules
				constraint (!HasCloseTasks)
					"TeamMemberIsAssignedToTasksInTheCloseProcessDefinition.TheTasksNeedToBeReassignedBeforeThisMemberCanBeRemovedFromTheTeam.ViewCurrentAssignmentsUnderTheResource."
				constraint (!HasPeriodTasksOpen)
					"TeamMemberHasOpenPeriodTasksThatNeedToBeCompletedOrReassignedBeforeRemovingThemFromTheTeam.ViewCurrentAssignmentsUnderTheResource."
				constraint (!HasOpenPeriodReconciliations)
					"TeamMemberHasOpenPeriodReconciliationsThatNeedToBeCompletedOrReassignedBeforeRemovingThemFromTheTeam.ViewCurrentAssignmentsUnderTheResource"
				constraint (!HasReconciliationAccounts)
					"TeamMemberIsAssignedToReconciliationAccounts.TheReconciliationAccountsNeedToBeReassignedBeforeThisMemberCanBeRemovedFromTheTeam.ViewCurrentAssignmentsUnderTheResource."




