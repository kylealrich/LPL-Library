ProcurementCardStatementInterfaceResult is a BusinessClass
	owned by po
	
	prefix is PCSIR
	sql name is ProcurementCardInterfaceResult
	
	Ontology
        symbolic key is ProcurementCardStatementInterfaceResult
	
	Patterns
		implements StaticJava
        disable AuditIndex
       	disable EffectiveDated
	
	Persistent Fields
		RunTime					is TimeStamp
		Status					is Numeric 1
			States
				InProcess       value is 0
				Complete		value is 1
				Incomplete 		value is 2
		ProcurementCardProgram
		ProcurementCardStatement is like ProcurementCardStatementImport
	
	Derived Fields
	
		NoRecordsToProcessMsg is a MessageField
			restricted
			"NoRecordsToProcess"
			 
        CompleteMessage is a MessageField
        	restricted
        	"Complete"
        	
        IncompleteMessage is a MessageField
        	restricted
        	"Incomplete"
        	
        NoRecordsMessage is a MessageField
        	restricted
        	"AllErrorsProcessed"
        
        InProcessMessage is a MessageField
        	restricted
        	"InProcess"
        
        DerivedStatus is a DerivedField
        	type is Alpha 50
        	if (Status.InProcess)
        		return InProcessMessage
        	else
        	if (Status.Complete)
        		return CompleteMessage
        	else
        	if (Status.Incomplete
        	and NoErrorRecordsToProcess)
        		return NoRecordsMessage
        	else
        		return IncompleteMessage
				
		DerivedResultStatus is a DerivedField
			type is Alpha 50
			if (Status.Incomplete
			and NoErrorRecordsToProcess)
				return NoRecordsToProcessMsg
			else
				return DerivedStatus
        
		InterfacedStatementsCount is a DerivedField
			type is Numeric 7	
			return (instance count of ProcurementCardStatementsRel)

		NonInterfacedStatementsCount is a DerivedField
			type is Numeric 7
			return (instance count of ProcessedProcurementCardStatementImportRel)
		
		DerivedRecordsProcessed is a DerivedField
			type is Numeric 7
			return (InterfacedStatementsCount)
			
		DerivedTotalRecordsProcessed is a DerivedField
			type is Numeric 7
			return (DerivedRecordsProcessed + NonInterfacedStatementsCount)   

	Field Rules
	
	
	Conditions

		IncompleteWithErrors
			restricted
			when (Status.Incomplete
			and   ProcessedProcurementCardStatementImportRel exists)

		NoErrorRecordsToProcess
			restricted
			when (ProcessedProcurementCardStatementImportRel !exists)
		
		HasStatementsCreated
			restricted
			when (ProcurementCardStatementsRel exists)

		EligibleToDelete
			restricted
			when (Status.Complete
			or   (Status.Incomplete
			and   NoErrorRecordsToProcess)
			or   (Status.InProcess
			and   DerivedTotalRecordsProcessed = 0))
		
		NoRecordsToProcess
			restricted
			when (Status entered
			and   DerivedRecordsProcessed = 0)
			
		NoRecordsEitherKindToProcess
			restricted
			when (NoRecordsToProcess
			and   NoErrorRecordsToProcess)
		
		ShowPendingPanel
			restricted
			when (ProcessedProcurementCardStatementImportRel exists)
			
		InputRecordsRemain
			restricted
			when (ProcurementCardStatementInputRel exists)

	Relations
				


		ProcessedProcurementCardStatementImportRel
			one-to-many relation to ProcurementCardStatementImport
			Field Mapping uses ByResult
				related.ResultProcessedBy 		= ProcurementCardStatementInterfaceResult
				related.ProcurementGroup        = ProcurementGroup
		
		ProcurementCardStatementsRel
			one-to-many relation to ProcurementCardStatement
			Field Mapping uses symbolic key
				related.ProcurementGroup        = ProcurementGroup
			Instance Selection
				where (related.OriginatingInterfaceRun	= ProcurementCardStatementInterfaceResult
				and    related.InterfaceInProcess = false)
				
		ProcurementCardStatementInputRel
			one-to-many relation to ProcurementCardStatementInput
			Field Mapping uses ByStatement
				related.ProcurementGroup         = ProcurementGroup
			Instance Selection
				where (((related.ProcurementCardProgram   = ProcurementCardProgram
				and    ProcurementCardProgram entered)
				or     ProcurementCardProgram !entered)
				and  ((related.ProcurementCardStatementImport   = ProcurementCardStatement
				and    ProcurementCardStatement entered)
				or     ProcurementCardStatement !entered))
				
	Sets
		ByRunTime
			Sort Order
				RunTime descending
				ProcurementCardStatementInterfaceResult
				ProcurementGroup
				UniqueID
		
	Actions
			
		Create is an Action
			restricted
			
		Update is an Action
			restricted
			
		Delete is an Action
			valid when (EligibleToDelete)
		
		Purge is a Purge Action
			restricted
		
		RerunForErrors is an Instance Action
			valid when (IncompleteWithErrors)
					
			Action Rules
			
				invoke InterfaceProcurementCardStatements ProcurementCardStatementImport
					invoked.PrmProcurementGroup				= ProcurementGroup
					invoked.PrmResult                       = ProcurementCardStatementInterfaceResult
		
		UpdateErrorInformation is an Instance Action
			restricted	
			Action Rules
			
				Status = 2 
						
		Finish is an Instance Action
			restricted
			Parameters
				PrmCompleteStatus    is Boolean
			
			Action Rules
				if (Status != Status.Incomplete)
					Status = Status.Complete
					
	
	

	
