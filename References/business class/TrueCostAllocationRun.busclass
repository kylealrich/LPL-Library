TrueCostAllocationRun is a BusinessClass
	owned by truecost
	prefix is TCALR
	default label is "AllocationRun"
	
	
	Ontology
		symbolic key is TrueCostAllocationRun

	Patterns
	
	Persistent Fields
		TotalExpenseWithoutCostObject	is a TotalAmount
			default label is "AllocableExpense"
		TotalExpenseWithCostObject		is a TotalAmount
			default label is "CostObjectDirectExpense"
		TotalCosted						is a TotalAmount
			default label is "TotalCostedToCostObject"
		TotalAllocatedExpense			is a TotalAmount
			default label is "AllocatedExpense"
		TotalUnutilizedCapacity			is a TotalAmount
			default label is "UnutilizedCapacityCost"
		PublishForBirstReport			is Boolean  
		TotalsCalculated				is Boolean
		CostBehaviorSummaryStatusGenerated	is Boolean
		ReverseOriginalJournal			is Boolean
		
		ReconciliationStatus			is Numeric 1
			restricted
			States
            	NotStarted				value is 0
                Processing				value is 1
                Completed				value is 2
                
        CostBehaviorStatus				is Numeric 1
        	restricted
        	States
    			NotStarted				value is 0
				Processing				value is 1
				Completed				value is 2
        
	Sets
		ByRunDescending
			Sort Order
				FinanceEnterpriseGroup
				TrueCostAllocationRun descending

    Transient Fields
        TrueCostStatus				is Numeric 2
			default label is "Status"
			derive value from DerivedStatus
            States
                Started				value is 0
                Completed			value is 1
                Failed				value is 2
                JournalizeInProcess	value is 3
                Journalized			value is 4
                PreparingForEdit	value is 5
                EditInProgress		value is 6
                DeleteInProgress	value is 7
                Finalized			value is 8
                Canceled			value is 9
                ReconciliationInProgress value is 10
                
		TransientPeriodCostRunStatus	is Numeric 1
			default label is "Status"
			derive value from TrueCostAllocationRun.Status
			States
				Journalized			value is 4
				Finalized			value is 8
				
		TransientAllocationPeriod			is a GeneralLedgerCalendarPeriod
			default label is "AllocationPeriod"
    		derive value from TrueCostAllocationRun.AllocationRun.AllocationPeriod
	
	Local Fields
		LocalWithCostObject					is Boolean
		LocalCostDriver						is like TrueCostResourceUtilizationDriver
		LocalAsyncId							is a AsyncActionRequest

	Conditions
		NoJournalizedTransactions
			when (!TrueCostAllocationRun.Status.Journalized 
			and  !(TrueCostAllocationRun.Status.Canceled and TrueCostAllocationRun.WithGLJournal))
		WithGLJournal
			restricted
			when (GeneralLedgerJournalControlRel exists)
		ShowReconciliation
			restricted
			when (TotalsCalculated)
		WithAllocationSnapshot
			restricted
			when (TrueCostAllocationRun.AllocationSnapshot entered)
		ErrorMessageEntered
			restricted
			when (TrueCostAllocationRun.ErrorMessage entered)
		RunDescriptionEntered
			restricted
			when (TrueCostAllocationRun.RunDescription entered)
		ForMultiMonthAllocation
			restricted
			when (TrueCostAllocationRun.MultiMonthAllocation)
		AllocationRunJournalized
			restricted
			when (TrueCostAllocationRun.Status.Journalized)
		AllocationRunCanceledWithJournal
			restricted
			when (TrueCostAllocationRun.Status.Canceled
			and   WithGLJournal)
		GenerateListingDataEntered
			restricted
			when (TrueCostAllocationRun.AllocationRun.GenerateListingData)
		ShowNonJournalizedCalculationReportDrill
			restricted
			when (TrueCostAllocationRun.AllocationRun.NoJournalizedTransactions
			and   TrueCostAllocationRun.AllocationRun.GenerateListingData)
		ShowJournalizedCalculationReportDrill
			restricted
			when (not TrueCostAllocationRun.AllocationRun.NoJournalizedTransactions
			and   TrueCostAllocationRun.AllocationRun.GenerateListingData)
		CanceledOrJournalizedWithGLJournal
			restricted
			when (TrueCostAllocationRun.AllocationRun.WithGLJournal
			and  (TrueCostAllocationRun.AllocationRun.Status.Canceled
			or    TrueCostAllocationRun.AllocationRun.Status.Journalized))
		StatusJournalized
			restricted
			when (TrueCostAllocationRun.AllocationRun.Status.Journalized)
		StatusStarted
			restricted
			when (TrueCostStatus.Started)
		StatusCompleted
			restricted
			when (TrueCostStatus.Completed)
		StatusFailed
			restricted
			when (TrueCostStatus.Failed)
		IsCapacity
			restricted
			when (TrueCostAllocationRun.AllocationRun.UseCapacityLedger)
		IsStarted
			restricted
			when (TrueCostStatus.Started)
		IsCompleted
			restricted
			when (TrueCostStatus.Completed)
		IsFinalized
			restricted
			when (TrueCostStatus.Finalized)
		IsJournalized
			restricted
			when (TrueCostStatus.Journalized)
		IsFailed
			restricted
			when (TrueCostStatus.Failed)
		IsCanceled
			restricted
			when (TrueCostStatus.Canceled)
		CanGetReconciliationData
			restricted
 			when (ReconciliationStatus.NotStarted
 			and  (IsCompleted
 			or    IsFinalized))
		CanGenerateCostBehaviorSummary
 			when  ((IsCompleted
 			or      IsJournalized)
			and not CostBehaviorSummaryStatusGenerated)
		CanJournalize
			restricted
			when (IsFinalized and TrueCostAllocationRun.AllocationRun.ErrorCount not entered)
		IsForBirstPublishing
			restricted
			when  ((IsJournalized 
			or      IsFinalized)
			and not PublishForBirstReport)
		IsForBirstUnpublishing
			restricted
			when ((IsJournalized 
			or     IsFinalized)
			and    PublishForBirstReport)
		CanFinalize
			when (IsCompleted
			and   GetUnitCostRel exists  
			and   NotCompletedResourceUnitCostRel not exists)
		CanGenerateCosts
			restricted
			when (TrueCostStatus.Completed)
		WithUnitCost
			restricted
			when (GetUnitCostRel exists)
		NoGLJournal
			restricted
			when (IsCanceled
			and GeneralLedgerJournalControlRel not exists)

		CanDelete
			restricted
			when (IsStarted
			or   IsFailed
			or   CanDeleteCompletedRun
			or   NoGLJournal)


		CanDeleteCompletedRun
			restricted
			when (TrueCostStatus.Completed
			and not ReconciliationStatus.Processing
			and not CostBehaviorStatus.Processing
			and ProcessingUnitCostRel not exist)
						
		CanCancelCompleted
			restricted
			when (IsCompleted
			and   not CostBehaviorStatus.Processing 
			and   not ReconciliationStatus.Processing
			and   ProcessingUnitCostRel not exists)
			
		CanCancelJournalized
			restricted
			when (IsJournalized
			and   not CostBehaviorStatus.Processing 
			and   not ReconciliationStatus.Processing
			and   ProcessingUnitCostRel not exists)
		
		NotYetReconciled
			restricted	
			when(ReconciliationStatus.NotStarted
			and TrueCostAllocationRun.Status.Completed)

	Derived Fields
		DerivedStatus is a DerivedField
			type is Numeric 2
			if (TrueCostAllocationRun.Status.Completed
			or (TrueCostAllocationRun.Status.Failed and TrueCostAllocationRun.ErrorMessage not entered))
				if(ReconciliationStatus.Processing)
					return TrueCostStatus.ReconciliationInProgress
				else
					return TrueCostStatus.Completed
			else
				return TrueCostAllocationRun.Status
					
		TotalExpenseBalanceForAllocation is a DerivedField
			type is like TotalAmount
				precision is OutputNumberOfDecimals
			return TotalExpenseWithCostObject + TotalExpenseWithoutCostObject
		TotalCostObjectUtilization is a DerivedField
			type is like TotalAmount
				precision is OutputNumberOfDecimals
			return TotalAllocatedExpense + TotalUnutilizedCapacity
		TotalExpenseUnallocatedToCostObject is a DerivedField
			type is like TotalAmount
				precision is OutputNumberOfDecimals
			return TotalCosted - TotalAllocatedExpense - TotalUnutilizedCapacity
		TotalNotCosted is a DerivedField
			type is like TotalAmount
				precision is OutputNumberOfDecimals
			return TotalExpenseWithoutCostObject - TotalCosted
		OutputNumberOfDecimals is a DerivedField
			type is Numeric 1
			if (actor.context.FinanceEnterpriseGroup.DefaultDecimalsOption entered)
				return actor.context.FinanceEnterpriseGroup.DefaultNumberOfDecimals
			else
				return 2

		AllocationCubeMap is a DerivedField
			type is LPLText
			restricted
			
			AllocationCubeMap += "related.FinanceCodeBlock.ToAccountingEntity = AccountingEntitiesRel.AccountingEntity\n"


			if (FinanceEnterpriseGroup.AccountingUnitSelected)
				AllocationCubeMap += "related.FinanceCodeBlock.AccountingUnit = TrueCostAllocationControlRel.AccountingUnitStructure.AccountingUnitTopNodeGroup.DimensionNode\n"
							
			AllocationCubeMap += "related.FinanceCodeBlock.GeneralLedgerChartAccount = TrueCostAllocationControlRel.ExpenseAccounts\n"

			if (FinanceEnterpriseGroup.ProjectEntry)
				AllocationCubeMap += "related.FinanceCodeBlock.Project = TrueCostAllocationControlRel.ProjectStructure.ProjectNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension1Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension1)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension1 = FinanceDimension1HierarchyRel.FinanceDimension1\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension1 = TrueCostAllocationControlRel.FinanceDimension1Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension2Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension2)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension2 = FinanceDimension2HierarchyRel.FinanceDimension2\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension2 = TrueCostAllocationControlRel.FinanceDimension2Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension3Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension3)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension3 = FinanceDimension3HierarchyRel.FinanceDimension3\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension3 = TrueCostAllocationControlRel.FinanceDimension3Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension4Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension4)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension4 = FinanceDimension4HierarchyRel.FinanceDimension4\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension4 = TrueCostAllocationControlRel.FinanceDimension4Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension5Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension5)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension5 = FinanceDimension5HierarchyRel.FinanceDimension5\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension5 = TrueCostAllocationControlRel.FinanceDimension5Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension6Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension6)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension6 = FinanceDimension6HierarchyRel.FinanceDimension6\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension6 = TrueCostAllocationControlRel.FinanceDimension6Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension7Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension7)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension7 = FinanceDimension7HierarchyRel.FinanceDimension7\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension7 = TrueCostAllocationControlRel.FinanceDimension7Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension8Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension8)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension8 = FinanceDimension8HierarchyRel.FinanceDimension8\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension8 = TrueCostAllocationControlRel.FinanceDimension8Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension9Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension9)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension9 = FinanceDimension9HierarchyRel.FinanceDimension9\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension9 = TrueCostAllocationControlRel.FinanceDimension9Structure.DimensionNode\n"

			if (FinanceEnterpriseGroup.FinanceDimension10Entry)
				if (TrueCostConfigurationRel.CostObjectDimension.Type.Dimension10)
					if (LocalWithCostObject)
						AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension10 = FinanceDimension10HierarchyRel.FinanceDimension10\n"
				else
					AllocationCubeMap += "related.FinanceCodeBlock.FinanceDimension10 = TrueCostAllocationControlRel.FinanceDimension10Structure.DimensionNode\n"
			AllocationCubeMap += "related.EntityYearPeriod = TrueCostAllocationRun.AllocationRun.AllocationPeriod\n"

			AllocationCubeMap += "related.CurrencyCode.Currency where (TransactionAmount not empty)\n"
			return AllocationCubeMap
	Rule Blocks
		GenerateReconciliationReport
			ReconciliationStatus = ReconciliationStatus.Processing
			LocalAsyncId = current async action request id
			invoke Refresh AllocationCubeRel in background
				run after LocalAsyncId
				assign async action request id to LocalAsyncId
			
			invoke ComputeExpenseSnapshotBalance in background
				run after LocalAsyncId
				assign async action request id to LocalAsyncId
				invoked.PrmProcessCostObjectOnly	= false
				
			invoke ReconcileTrueCost AllocationTransactionDetail in background
				run after LocalAsyncId
				assign async action request id to LocalAsyncId
				invoked.PrmTrueCostConfiguration	= FinanceEnterpriseGroup
				invoked.PrmTrueCostAllocationRun	= TrueCostAllocationRun

			invoke ComputeOriginalSourceValue AllocationLineSourceValue in background
				run after LocalAsyncId
				assign async action request id to LocalAsyncId
				invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
				invoked.PrmTrueCostAllocationRun	= TrueCostAllocationRun

			invoke UpdateAllocationRun in background
				run after LocalAsyncId
				assign async action request id to LocalAsyncId
	Relations
		GeneralLedgerCalendarPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup   = FinanceEnterpriseGroup
			Instance Selection
				where(related.CalendarRel.GeneralLedgerCalendar entered)
	
		AllocationCubeRel
			one-to-one relation to AnalyticCube
			Field Mapping uses AnalyticCubeSet
				related.BusinessClass = "AllocationTransactionDetail"

		AccountingEntitiesRel
			one-to-many relation to AccountingEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
			Instance Selection
				where (related.PostingEntity)

		TrueCostConfigurationRel
			one-to-one relation to TrueCostConfiguration
			Field Mapping uses symbolic key
				related.TrueCostConfiguration			= FinanceEnterpriseGroup
				
		TrueCostAllocationControlRel
			one-to-one relation to TrueCostAllocationControl
			Field Mapping uses symbolic key
				related.TrueCostConfiguration			= FinanceEnterpriseGroup
				related.TrueCostAllocationControl		= TrueCostAllocationRun.AllocationControl.TrueCostAllocationControl
		
		FinanceDimension1HierarchyRel
			one-to-many relation to FinanceDimension1Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension1Structure		= TrueCostAllocationControlRel.FinanceDimension1Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension1Structure.DimensionNode)
	
		FinanceDimension2HierarchyRel
			one-to-many relation to FinanceDimension2Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension2Structure		= TrueCostAllocationControlRel.FinanceDimension2Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension2Structure.DimensionNode)
	
		FinanceDimension3HierarchyRel
			one-to-many relation to FinanceDimension3Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension3Structure		= TrueCostAllocationControlRel.FinanceDimension3Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension3Structure.DimensionNode)
	
		FinanceDimension4HierarchyRel
			one-to-many relation to FinanceDimension4Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension4Structure		= TrueCostAllocationControlRel.FinanceDimension4Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension4Structure.DimensionNode)
	
		FinanceDimension5HierarchyRel
			one-to-many relation to FinanceDimension5Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension5Structure		= TrueCostAllocationControlRel.FinanceDimension5Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension5Structure.DimensionNode)
	
		FinanceDimension6HierarchyRel
			one-to-many relation to FinanceDimension6Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension6Structure		= TrueCostAllocationControlRel.FinanceDimension6Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension6Structure.DimensionNode)
	
		FinanceDimension7HierarchyRel
			one-to-many relation to FinanceDimension7Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension7Structure		= TrueCostAllocationControlRel.FinanceDimension7Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension7Structure.DimensionNode)
	
		FinanceDimension8HierarchyRel
			one-to-many relation to FinanceDimension8Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension8Structure		= TrueCostAllocationControlRel.FinanceDimension8Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension8Structure.DimensionNode)
	
		FinanceDimension9HierarchyRel
			one-to-many relation to FinanceDimension9Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension9Structure		= TrueCostAllocationControlRel.FinanceDimension9Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension9Structure.DimensionNode)
	
		FinanceDimension10HierarchyRel
			one-to-many relation to FinanceDimension10Hierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 			= FinanceEnterpriseGroup
				related.FinanceDimension10Structure		= TrueCostAllocationControlRel.FinanceDimension10Structure
			Instance Selection
				where (related.ParentDimension = TrueCostAllocationControlRel.FinanceDimension10Structure.DimensionNode)
	
		GeneralLedgerJournalControlRel
			one-to-many relation to GeneralLedgerJournalControl
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
			Instance Selection
   				where  (related.JournalizeGroupResult	= TrueCostAllocationRun
				and 	related.System					= TrueCostAllocationRun.AllocationRun.AllocationSourceSystem)

		AllocationRunPublishedForBirstRel
			one-to-many relation to TrueCostAllocationRun
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
			Instance Selection
				where(related.TrueCostAllocationRun.AllocationRun.AllocationControl			= TrueCostAllocationRun.AllocationRun.AllocationControl
				and   related.TrueCostAllocationRun.AllocationRun.AllocationSourceSystem	= TrueCostAllocationRun.AllocationRun.AllocationSourceSystem
				and   related.TrueCostAllocationRun.AllocationRun.UseCapacityLedger			= TrueCostAllocationRun.AllocationRun.UseCapacityLedger
				and   related.TrueCostAllocationRun.AllocationRun.AllocationPeriod			= TrueCostAllocationRun.AllocationRun.AllocationPeriod
				and	  (related.TrueCostAllocationRun.AllocationRun.Status.Finalized
				or	   related.TrueCostAllocationRun.AllocationRun.Status.Journalized)
				and   related.PublishForBirstReport		= true)				

		TrueCostCapacityVolumesUpdateRel
			one-to-many relation to TrueCostCapacityVolumes
			Field Mapping uses symbolic key
				related.TrueCostConfiguration		= FinanceEnterpriseGroup
				related.TrueCostAllocationControl	= TrueCostAllocationRun.AllocationRun.AllocationControl.TrueCostAllocationControl
				related.AllocationRun				= TrueCostAllocationRun.AllocationRun
			Instance Selection
				where (related.Update				= true)
				
		TrueCostResourceUnitCostRel
			one-to-one relation to TrueCostResourceUnitCost
			Field Mapping uses symbolic key
				related.TrueCostConfiguration				= FinanceEnterpriseGroup
				related.AllocationRun						= TrueCostAllocationRun.AllocationRun
				related.TrueCostResourceUtilizationDriver 	= LocalCostDriver
		
		GetUnitCostRel
			one-to-many relation to TrueCostResourceUnitCost
			Field Mapping uses symbolic key
				related.TrueCostConfiguration				= FinanceEnterpriseGroup
				related.AllocationRun						= TrueCostAllocationRun.AllocationRun
				
		ProcessingUnitCostRel
			one-to-many relation to TrueCostResourceUnitCost
			Field Mapping uses symbolic key
				related.TrueCostConfiguration				= FinanceEnterpriseGroup
				related.AllocationRun						= TrueCostAllocationRun.AllocationRun
			Instance Selection
				where(related.Status.Processing)		
			
		NotCompletedResourceUnitCostRel
			one-to-many relation to TrueCostResourceUnitCost
			Field Mapping uses symbolic key
				related.TrueCostConfiguration					= FinanceEnterpriseGroup
				related.AllocationRun							= TrueCostAllocationRun.AllocationRun
			Instance Selection
				where(not related.Status.Completed)
				
		TrueCostCapacityVolumeRel
			one-to-many relation to TrueCostCapacityVolume
			Field Mapping uses symbolic key
				related.TrueCostConfiguration		= FinanceEnterpriseGroup
				related.TrueCostAllocationControl	= TrueCostAllocationRun.AllocationRun.AllocationControl.TrueCostAllocationControl
				related.TrueCostAllocationRun		= TrueCostAllocationRun

		TrueCostCapacityVolumeUpdateRel
			one-to-many relation to TrueCostCapacityVolume
			Field Mapping uses symbolic key
				related.TrueCostConfiguration		= FinanceEnterpriseGroup
				related.TrueCostAllocationControl	= TrueCostAllocationRun.AllocationRun.AllocationControl.TrueCostAllocationControl
				related.TrueCostAllocationRun		= TrueCostAllocationRun
			Instance Selection
				where (related.Update				= true)
				
		AllocationSnapshotRel
			one-to-many relation to AllocationSnapshot
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AllocationSourceSystem		= TrueCostAllocationRun.AllocationRun.AllocationSourceSystem
			Instance Selection
				where (related.AllocationSnapshot	= TrueCostAllocationRun.AllocationRun.AllocationSnapshot)

		AllocationTransactionDetailsForReportRel
			one-to-many relation to AllocationTransactionDetail
			Field Mapping uses ByAllocationAndStep
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AllocationRun 				= TrueCostAllocationRun
			Instance Selection
				where (related.IsGroupByAllocationAndStep)

		AllocationTransactionDetailsForReportCurrencyRel
			one-to-many relation to AllocationTransactionDetail
			Field Mapping uses ByAllocationAndStep
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.AllocationRun 				= TrueCostAllocationRun
			Instance Selection
				where (related.IsGroupByCurrencyReportTotal)

		AllocationTransactionJournalizedDetailsForReportRel
			one-to-many relation to AllocationJournalizedDetail
			Field Mapping uses ByAllocationAndStep
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.AllocationRun 				= TrueCostAllocationRun
			Instance Selection
				where (related.IsGroupByAllocationAndStep)

		AllocationTransactionJournalizedDetailsForReportCurrencyRel
			one-to-many relation to AllocationJournalizedDetail
			Field Mapping uses ByAllocationAndStep
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.AllocationRun 				= TrueCostAllocationRun
			Instance Selection
				where (related.IsGroupByCurrencyReportTotal)

		TrueCostRunParameterRel
			one-to-many relation to TrueCostRunParameter
			Field Mapping uses symbolic key
				related.TrueCostConfiguration		= FinanceEnterpriseGroup
				related.TrueCostAllocationControl	= TrueCostAllocationRun.AllocationRun.AllocationControl.TrueCostAllocationControl
			Instance Selection
				where (related.RunName				= TrueCostAllocationRun.AllocationRun.RunName)









	Field Rules
		
	Actions
		Create is an Action
			restricted

		Update is an Action
			restricted

		Delete is an Action
			restricted
		
		ReconcileForCompletedRun is an Instance Action
			restricted
			default label is untranslatable
			Action Rules
				if (TrueCostAllocationRun.Status.Completed 
				or (TrueCostAllocationRun.Status.Failed and TrueCostAllocationRun.ErrorMessage not entered))
					include GenerateReconciliationReport
				else
					ReconciliationStatus =	ReconciliationStatus.NotStarted
				
		Reconcile is an Instance Action
			default label is "GetReconciliationData"
			confirmation required
			completion message is "CollectingReconciliationData"
 			valid when (CanGetReconciliationData)
			Action Rules
				include GenerateReconciliationReport
		
		ComputeExpenseSnapshotBalance is an Instance Action
			restricted
			Parameters
				PrmProcessCostObjectOnly 	is Boolean
			Local Fields
				LocalTotal is an InternationalAmount
			Action Rules

				if (not PrmProcessCostObjectOnly)
					initialize LocalWithCostObject
					for each AllocationSnapshotMatrixRel
						LocalTotal += each.TransactionAmount
					TotalExpenseWithoutCostObject	= LocalTotal
				
				initialize LocalTotal
				if (not CostBehaviorSummaryStatusGenerated)
					LocalWithCostObject = true

					for each AllocationSnapshotMatrixRel
						LocalTotal += each.TransactionAmount
					TotalExpenseWithCostObject	= LocalTotal

		UpdateAllocationRun is an Instance Action
			restricted
			Action Rules
				ReconciliationStatus 						= ReconciliationStatus.Completed
				TotalsCalculated							= true
		GenerateCostBehaviorSummary is an Instance Action
			valid when (CanGenerateCostBehaviorSummary)
			default label is untranslatable
			confirmation required
			completion message is "GeneratingCostBehaviorSummary"
			Local Fields
				AsyncId							is a AsyncActionRequest
			Action Rules
				CostBehaviorStatus = CostBehaviorStatus.Processing				
				AsyncId 								= current async action request id
				if (not TotalsCalculated)
					invoke ComputeExpenseSnapshotBalance in background
						run after AsyncId
						assign async action request id to AsyncId
						invoked.PrmProcessCostObjectOnly = true
				
				invoke GenerateCostBehaviorTotals TrueCostAllocationLine
					run after AsyncId
					assign async action request id to AsyncId
					invoked.PrmFinanceEnterpriseGroup			= FinanceEnterpriseGroup
					invoked.PrmTrueCostAllocationControl		= TrueCostAllocationControlRel.TrueCostAllocationControl 
					invoked.PrmTrueCostAllocationRun			= TrueCostAllocationRun.TrueCostAllocationRun









				invoke CostBehaviorSummaryCompleted in background
					run after AsyncId
					assign async action request id to AsyncId
					
		CostBehaviorSummaryCompleted is an Instance Action
			restricted
			default label is untranslatable
			Action Rules
				CostBehaviorStatus = CostBehaviorStatus.Completed
				CostBehaviorSummaryStatusGenerated 				= true

		PublishForBirstReport is an Instance Action
			default label is "Publish_for_BirstReport"
			valid when (IsForBirstPublishing)
			confirmation required
				"DoYouWantToProceed?"
			Entrance Rules
				constraint(AllocationRunPublishedForBirstRel not exists)
					"CannotPublishMultipleAllocationRunsFor_BirstReport."
			Action Rules
				PublishForBirstReport 							= true
				invoke Update TrueCostAllocationRun.AllocationRun
					invoked.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
					invoked.AllocationRun						= TrueCostAllocationRun
					invoked.AllocationRun.PublishForBirstReport = true

		UnpublishForBirstReport is an Instance Action
			default label is "Unpublish_for_BirstReport"
			valid when (IsForBirstUnpublishing)
			confirmation required
				"DoYouWantToProceed?"
			Action Rules
				PublishForBirstReport 							= false
				invoke Update TrueCostAllocationRun.AllocationRun
					invoked.FinanceEnterpriseGroup				= FinanceEnterpriseGroup
					invoked.AllocationRun						= TrueCostAllocationRun
					invoked.AllocationRun.PublishForBirstReport = false

		UnfinalizeFinalized is an Instance Action
			default label is "Unfinalize"
			valid when (IsFinalized)
			Action Rules
 				constraint(not PublishForBirstReport)
 					"CannotUnfinalizeAllocationRun.<TrueCostAllocationRun>IsPublishedFor_BirstReport."
				invoke Finalized.Unfinalize TrueCostAllocationRun.AllocationRun

		JournalizeFinalized is an Instance Action 
			default label is "Journalize"
			valid when (CanJournalize)
			restricted
			Local Fields
				AsyncId							is a AsyncActionRequest
			Action Rules
				for each distinct AllocationFieldGroup in TrueCostAllocationRun.AllocationRun.AllocationTransactionDetail set

					invoke Journalizing each.Allocation
						invoked.PrmAllocationPeriod = TrueCostAllocationRun.AllocationRun.AllocationPeriod
						invoked.PrmJournalizeGroup	= TrueCostAllocationRun.AllocationRun
						if (TrueCostAllocationRun.AllocationRun.UseCapacityLedger)
							invoked.PrmIsCapacity	= true	

				invoke Update TrueCostAllocationRun.AllocationRun
					invoked.Status = TrueCostAllocationRun.AllocationRun.Status.JournalizeInProcess

			Exit Rules
				invoke CreateJournalizedDetails AllocationTransactionDetail in background
					assign async action request id to AsyncId
					invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmAllocationRun	= TrueCostAllocationRun.AllocationRun
				invoke CreateGLTransactionDetails AllocationJournalizedDetail in background
					run after AsyncId
					assign async action request id to AsyncId
					invoked.PrmEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmAllocationRun	= TrueCostAllocationRun.AllocationRun
				invoke InitiateJournalizeForRunGroup FinanceEnterpriseGroup in background
					run after AsyncId
					assign async action request id to AsyncId
					invoked.PrmJournalizeGroup	= TrueCostAllocationRun.AllocationRun
					invoked.PrmAutoReverseDate	= TrueCostAllocationRun.AllocationRun.AutoReverseDate
				invoke JournalizeInProcess.SetJournalized TrueCostAllocationRun.AllocationRun in background
					run after AsyncId

		FinalizeCompleted is an Instance Action
			default label is "Finalize"
			valid when (CanFinalize)
			Action Rules
 				constraint (TotalsCalculated)
 					"MustGetReconciliationDataBeforeFinalizing"		







				invoke UpdatePracticalCapacityRecords TrueCostCapacityVolumeUpdateRel

				invoke Update TrueCostAllocationRun.AllocationRun
					invoked.Status = TrueCostAllocationRun.AllocationRun.Status.Finalized

		DeleteRunStarted is an Instance Action 
			restricted
			default label is "DeleteRun"
			valid when (IsStarted)
			Action Rules


		DeleteRunCompleted is an Instance Action 
			restricted
			default label is "DeleteRun"
			valid when (IsCompleted)
			Action Rules


		DeleteRunFailed is an Instance Action 
			restricted
			default label is "DeleteRun"
			valid when (IsFailed)
			Action Rules

		
		DeleteRunCanceled is an Instance Action 
			restricted
			default label is "DeleteRun"
			valid when (NoGLJournal)
			Action Rules


		DeleteRun is an Instance Action
			valid when(CanDelete)
			Action Rules
				if (IsStarted)
					invoke Started.DeleteRun TrueCostAllocationRun.AllocationRun
				else
				if (CanDeleteCompletedRun)
					invoke Completed.DeleteRun TrueCostAllocationRun.AllocationRun
				else
				if (IsFailed)
					invoke Failed.DeleteRun TrueCostAllocationRun.AllocationRun
				else
				if(NoGLJournal)
					invoke Canceled.DeleteRun TrueCostAllocationRun.AllocationRun
		
		Cancel is an Instance Action
			valid when(CanCancelCompleted)
			Action Rules
				invoke Update TrueCostAllocationRun.AllocationRun
					invoked.Status 	= TrueCostAllocationRun.AllocationRun.Status.Canceled

		CancelJournalized is an Instance Action
			default label is "Cancel"
			valid when (CanCancelJournalized)
 			Parameters
 				PrmReverseOriginalJournal	is Boolean
 					default label is "ReverseOriginalJournal"
 			Parameter Rules
 				PrmReverseOriginalJournal
 					initial value is true

			Action Rules
 				constraint(not PublishForBirstReport)
 					"CannotCancelJournalizedAllocationRun.<TrueCostAllocationRun>IsPublishedFor_BirstReport."

				if (PrmReverseOriginalJournal = true)
					invoke CopyJournal GeneralLedgerJournalControlRel
						invoked.CopyOption							= 1
						invoked.AutoReverseOption					= 0
						invoked.ReverseAmounts						= true
						invoked.RetainCurrencyAmounts				= false

				ReverseOriginalJournal = PrmReverseOriginalJournal

				invoke Update TrueCostAllocationRun.AllocationRun
					invoked.ReverseOriginalJournal 	= PrmReverseOriginalJournal
					invoked.Status 					= TrueCostAllocationRun.AllocationRun.Status.Canceled

		GenerateCost is an Instance Action
			valid when (CanGenerateCosts)
			Parameters
				PrmTrueCostConfiguration				is a TrueCostConfiguration
					default label is "TrueCostConfiguration"
				PrmTrueCostResourceUtilizationDriver 	is a TrueCostResourceUtilizationDriver
					
			Local Fields
				LocalError is Alpha 50
				
			Parameter Rules
				PrmTrueCostConfiguration
					force default to FinanceEnterpriseGroup
					
				PrmTrueCostResourceUtilizationDriver
					LocalCostDriver = PrmTrueCostResourceUtilizationDriver
					if (TrueCostResourceUnitCostRel exists)
						constraint(not TrueCostResourceUnitCostRel.Status.Processing)
							"UnitCostFor<LocalCostDriver>IsProcessing"
					constraint((TrueCostConfigurationRel.CostObjectIsDimension1 and PrmTrueCostResourceUtilizationDriver.HasDimension1)
					or  (TrueCostConfigurationRel.CostObjectIsDimension2 and PrmTrueCostResourceUtilizationDriver.HasDimension2)
					or  (TrueCostConfigurationRel.CostObjectIsDimension3 and PrmTrueCostResourceUtilizationDriver.HasDimension3)
					or  (TrueCostConfigurationRel.CostObjectIsDimension4 and PrmTrueCostResourceUtilizationDriver.HasDimension4)
					or  (TrueCostConfigurationRel.CostObjectIsDimension5 and PrmTrueCostResourceUtilizationDriver.HasDimension5)
					or  (TrueCostConfigurationRel.CostObjectIsDimension6 and PrmTrueCostResourceUtilizationDriver.HasDimension6)
					or  (TrueCostConfigurationRel.CostObjectIsDimension7 and PrmTrueCostResourceUtilizationDriver.HasDimension7)
					or  (TrueCostConfigurationRel.CostObjectIsDimension9 and PrmTrueCostResourceUtilizationDriver.HasDimension9)
					or  (TrueCostConfigurationRel.CostObjectIsDimension10 and PrmTrueCostResourceUtilizationDriver.HasDimension10))
							"CostDriverMustHave<TrueCostConfigurationRel.CostObjectDimension>."
			Action Rules
				if(TrueCostResourceUnitCostRel not exists)
					invoke Create TrueCostResourceUnitCost
						invoked.TrueCostConfiguration					= PrmTrueCostConfiguration
						invoked.AllocationRun							= TrueCostAllocationRun
						invoked.TrueCostResourceUtilizationDriver		= PrmTrueCostResourceUtilizationDriver
					
				invoke GenerateCost TrueCostResourceUnitCostRel
						
										
					
	Cube Relations
				
		AllocationSnapshotMatrixRel
			matrix relation to AllocationSnapshotDetail
			allow dimension reordering
			dynamic mapping is AllocationCubeMap
        	Dimension Mapping
        		related.AllocationSnapshot						= TrueCostAllocationRun.AllocationRun.AllocationSnapshot
				related.FinanceCodeBlock.Ledger					= TrueCostAllocationControlRel.SourceLedger
			Preload Measures
				TransactionAmount
	    		UnitsAmount
