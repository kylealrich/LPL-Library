ContractImportTierQualifier is a BusinessClass
    owned by po
    prefix is CITQ

    Ontology
    	symbolic key is ContractImportTierQualifier

	Patterns
		disable Auditing 
       	disable EffectiveDated
	
	Persistent Fields
		BaseTier                            is Boolean
			default label is "NoQualifierRequired"
		PurchaseType	 					is Numeric size 1
			States
				TotalPurchases					value is 1
				TargetedPurchases				value is 2		
		PurchaseBy 							is Numeric size 1
    		States
    			PurchasePrice  					value is 1
    			InvoicePrice     				value is 2
    	PurchaseOperator					is AlphaUpper size 2
    		States
    			LessThanOrEqualTo   			value is "LE"
    			GreaterThanOrEqualTo			value is "GE"
    			EqualTo							value is "EQ"
    			LessThan						value is "LT"
    			GreaterThan						value is "GT"
    	PurchaseAmtPct    			
    	PurchaseQuantity				    is an UnsignedItemQuantity	
 		PurchaseBeginRange  				is Date
 		PurchaseEndRange	    			is Date    	
    	OrGroup								is AlphaUpper size 1
    	TargetedPurchasesFromDate			is Date
    	TargetedPurchasesToDate				is Date
		ModifiedLinesExist					is Boolean
    	
	Local Fields
	
	Context Fields
	 
	Derived Fields
    	ConvertedPercent is a ComputeField
    		type is Decimal size 4.2
    		restricted
   			(PurchaseAmtPct.PurchasePercent * 100)
   			
    	ConvertedAmount is a DerivedField
    		type is like InternationalCost
    		restricted
   			return (PurchaseAmtPct.PurchaseAmount * 1)
		
		DerivedPercent is a StringField
			type is Alpha 8
			restricted			
			ConvertedPercent
			"%"
			
    	FormattedPercentAmount is a ConditionalField
			type is Alpha 30
			if (PurchaseAmtPct.PurchaseAmount > 0)
				ConvertedAmount				
			else
			if (PurchaseAmtPct.PurchasePercent > 0)
				DerivedPercent
			else
				blank
			
	Conditions
		EmptyQualifier
			restricted
			when (PurchaseType = 0)
		MissingAmtPct
			restricted
			when (PurchaseAmtPct.PurchaseAmount !entered
			and   PurchaseAmtPct.PurchasePercent !entered)
		CodesExist
			restricted
			when (ContractTierQualifierCodeRel exists)
		PurchaseAmountQualifier
			restricted
			when (PurchaseAmtPct.PurchaseAmount entered)
		PurchasePercentQualifier
			restricted
			when (PurchaseAmtPct.PurchasePercent entered)

    Relations
    	ContractTierQualifierCodeRel is a ContractImportTierQualCode set
    	
    	ManufacturersRel
    		one-to-many relation to Manufacturer
            Field Mapping uses  symbolic key
          		related.ItemGroup = ContractGroup
          		
  		CommodityCodeRel
  			one-to-many relation to CommodityCode
  			Field Mapping uses symbolic key
  				related.ItemGroup	= ContractGroup
  				
  		UNSPSCCodesRel
  			one-to-many relation to UNSPSCCode
  			Field Mapping uses symbolic key
  				related.ItemGroup	= ContractGroup
  		
        MajorClassRel
  			one-to-many relation to MajorClass
  			Field Mapping uses symbolic key
  				related.ItemGroup	= ContractGroup
  				related.ClassType	= "P"
  				
  		MinorClassRel
  			one-to-many relation to MinorClass
  			Field Mapping uses symbolic key
  				related.ItemGroup	= ContractGroup
  				related.ClassType	= "P"
        
        ContractTierQualifierCodesUNSPSCRel is a ContractTierQualifierCode set
           	Instance Selection 
           		where (related.ItemCategory.UNSPSCCode)
           		
        ContractTierQualifierCodesCommodityCodeRel is a ContractTierQualifierCode set
           	Instance Selection 
           		where (related.ItemCategory.CommodityCode)

        ContractTierQualifierCodesManufacturerInformationRel is a  ContractTierQualifierCode set
           	Instance Selection 
           		where (related.ItemCategory.ManufacturerInformation)

        ContractTierQualifierCodesPurchasingClassRel is a ContractTierQualifierCode set
           	Instance Selection 
           		where (related.ItemCategory.PurchasingClass)
           		
	Field Rules
		
	    PurchaseEndRange
           	constraint (PurchaseEndRange > PurchaseBeginRange)
                "TotalPurchaseEndRangeCannotBeLessThanBeginRange"
	    
	    TargetedPurchasesFromDate
	    	if (PurchaseType.TargetedPurchases)
	    		required
	    			"TargetedPurchasesFromDateIsRequiredWhenTargetedPurchasesIsSelected"
			else
				cannot be entered
	    			"TargetedPurchasesFromDateCannotBeEnteredUnlessTargetedPurchasesIsSelected"
			if (ContractImport.EffectiveDate > 0)	    		
	           	constraint (TargetedPurchasesFromDate >= ContractImport.EffectiveDate)
	                "TargetedPurchasesFromDateCannotBeLessThanContractImportEffectiveDate"
	        if (ContractImport.ExpirationDate > 0)
	            constraint (TargetedPurchasesFromDate <= ContractImport.ExpirationDate)
	                "TargetedPurchasesFromDateCannotBeGreaterThanContractImportExpirationDate"                
		
		TargetedPurchasesToDate
	    	if (PurchaseType.TargetedPurchases)
	    		required
	    			"TargetedPurchasesToDateIsRequiredWhenTargetedPurchasesIsSelected"
			else
				cannot be entered	    		
	    			"TargetedPurchasesToDateCannotBeEnteredUnlessTargetedPurchasesIsSelected"
           	constraint (TargetedPurchasesToDate > TargetedPurchasesFromDate)
                "TargetedPurchasesToDateCannotBeLessThanFromDate"
           	if (ContractImport.ExpirationDate > 0)
	            constraint (TargetedPurchasesToDate <= ContractImport.ExpirationDate)
	                "TargetedPurchasesToDateCannotBeGreaterThanContractImportExpirationDate"
			
		ModifiedLinesExist
			default to false
		
	Actions
    	Create is a Create Action
    		Action Rules
				if (!BaseTier)
					constraint (PurchaseType entered)
						"PurchaseTypeIsRequired"
					constraint (PurchaseBy entered)
						"PurchaseByIsRequired"
					constraint (PurchaseOperator entered)
						"PurchaseOperatorIsRequired"
				
    			constraint (ContractImportTier exists)
    				"TierDoesNotExist"
				if (!PurchaseQuantity entered
				and !BaseTier)
					constraint (!MissingAmtPct)
						"PurchasePercentAnd/OrAmountIsRequired"
				if (BaseTier)
					initialize PurchaseType	 					
					initialize PurchaseBy 							
			    	initialize PurchaseOperator					
			    	initialize PurchaseAmtPct    			
			    	initialize OrGroup								
			    	initialize TargetedPurchasesFromDate			
			    	initialize TargetedPurchasesToDate				
			    	initialize PurchaseQuantity				    	
			 		initialize PurchaseBeginRange  				
			 		initialize PurchaseEndRange	 
			Exit Rules
		
    	Update is an Update Action
    		Action Rules
				if (!BaseTier)
					constraint (PurchaseType entered)
						"PurchaseTypeIsRequired"
					constraint (PurchaseBy entered)
						"PurchaseByIsRequired"
					constraint (PurchaseOperator entered)
						"PurchaseOperatorIsRequired"
				constraint (!EmptyQualifier
				and         !BaseTier)
					"PurchaseTypeIsRequired"
				if (!PurchaseQuantity entered
				and !BaseTier)
					constraint (!MissingAmtPct)
						"PurchasePercentOrAmountIsRequired"
				if (BaseTier)
					initialize PurchaseType	 					
					initialize PurchaseBy 							
			    	initialize PurchaseOperator					
			    	initialize PurchaseAmtPct    			
			    	initialize OrGroup								
			    	initialize TargetedPurchasesFromDate			
			    	initialize TargetedPurchasesToDate				
			    	initialize PurchaseQuantity				    	
			 		initialize PurchaseBeginRange  				
			 		initialize PurchaseEndRange	 
			Exit Rules
		
    	Delete is a Delete Action
    		Action Rules
				    	
					
