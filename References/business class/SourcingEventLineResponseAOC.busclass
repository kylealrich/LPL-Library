SourcingEventLineResponseAOC is a BusinessClass
	owned by ss
	prefix is EVLRA

	Ontology
		symbolic key is SourcingEventLineResponseAOC

	Patterns
		implements ContextualParent

	Persistent Fields
		ItemNumber							is a snapshot of SourcingEventLine.ItemNumber
		ItemType							is a snapshot of SourcingEventLine.ItemType
		AOC									is an AddOnCharge
		AddOnChargeType						is a snapshot of AOC.AddOnChargeType
		AOCRate         					is an AddOnChargePercent
		AOCOption							is Numeric 1
			States
				PerUnit		value is 1
				Total		value is 2
		CurrencyAOCCost 					is a UnitCost
			default label is "AOCCost"
			precision is DerivedNumberOfDecimalsCost
		AOCCost								is a UnitCost
			precision is DerivedNumberOfDecimalsCost
		ZeroCost							is Boolean
		AddOnChargeOrigin
		SpreadMethod
		Account         					is a TransactionCodeBlock
		CurrencyTotalAOCCost 				is an InternationalCost
			default label is "TotalAOCCost"
			precision is DerivedNumberOfDecimalsCost
		TotalAOCCost    					is an InternationalCost
			precision is DerivedNumberOfDecimalsCost
		
	Local Fields
		LocalAccountingEntity 	is an AccountingEntity
		LocalCurrencyExchange   is a CurrencyExchange
		
	Transient Fields
		SavedFromCurrency   		    is a FromCurrency
		SavedCurrencyTable     		    is a CurrencyTable
		SavedTransactionAmount  		is a CurrencyAmount
		SavedExchangeDate				is a ExchangeDate
		SavedEnterpriseGroup    		is a EnterpriseGroup
	
	Derived Fields

		UnitAOCCost				is a DerivedField
			type is like InternationalCost
			restricted
			return (TotalAOCCost / SourcingEventLineResponse.Quantity)

		RequiredAOCNotEnteredText is a MessageField
			restricted
			"ValueHasNotBeenEnteredForRequiredAOC;MustEnterRate,Cost,OrZeroCostYes"
			
		DerivedNumberOfDecimalsCost is a ConditionalField
			type is Numeric size 1
			restricted
			if (ItemNumber exists)
				ItemNumber.NumberOfDecimalsCost
			else
			if (SourcingEventLine.OutputType.PO)
				Company.InventoryCompanyRel.NumberOfDecimalsCost
			else
				Company.SourcingGroup.ItemGroupRel.DefaultNumberOfDecimalsCost
						
	Sets
		ByAddOnChargeType
			Sort Order
				NotifiedSupplier.SupplierGroup
				NotifiedSupplier.Supplier
				NotifiedSupplier.SupplierSourceId
				Company
				SourcingEvent
				SourcingEventLine
				AddOnChargeType
				AOC
				SourcingEventLineResponseAOC
				
		ByEvent
			Sort Order
				Company
				SourcingEvent
				SourcingEventLine
				NotifiedSupplier.SupplierGroup
				NotifiedSupplier.Supplier
				NotifiedSupplier.SupplierSourceId
				AOC
				SourcingEventLineResponseAOC

	Conditions
		IsHeaderAOC
			restricted
			when (SourcingEventLine = 0)
		FromSourcingEventLineAOC
			restricted
			when (SourcingEventLineAOCRel exists)
		RequiredAOC
			restricted
			when (first SourcingEventLineAOCRel.Required)
		CostNotEntered
			restricted
			when (!ZeroCost
			and   AOCRate not entered
			and   AOCCost not entered)
		SomethingEntered
			restricted
			when (ZeroCost
			or    AOCRate entered
			or    AOCCost entered)
		RequiredAOCNotEntered
			restricted
			when (RequiredAOC
			and   CostNotEntered)
		NotFromSourcingEventLineAOC
			restricted
			when (SourcingEventLineAOCRel not exists)
		SourcingEventLineResponseAOCExists	
			restricted		
			when (SourcingEventLineResponseAOC != 0)
		AvailableToUpdateAOCHeader
			restricted
			when (SourcingEventResponse.Status.Draft
			and   SourcingEvent.HeaderAOCExists)
		AvailableToUpdateAOC
			restricted
			when (SourcingEventLine.AllowAddOnCharge
			and   SourcingEventLineResponse.Status.Draft)
		HeaderAOCExists
			restricted
			when (SourcingEventHeaderAOCRel exists)
		CanBeDeleted
			restricted
			when (AvailableToUpdateAOC
			and   NotFromSourcingEventLineAOC)
		NotContract
			restricted
			when (SourcingEventLine.OutputType.PO
			or    SourcingEventLine.OutputType.NoOutput)
		DefaultRateExists
			restricted
			when (AOC.AddOnChargePercent > 0)
		IsSpread
			restricted
			when (!SpreadMethod.NoSpread)
			
    Relations
		AddOnChargeRel
			one-to-one relation to AddOnCharge
			Field Mapping uses symbolic key
				related.Company		= Company
				related.AddOnCharge	= AOC

		SourcingEventLineAOCRel
			one-to-many relation to SourcingEventLineAOC
			Field Mapping uses symbolic key
				related.Company				= Company
				related.SourcingEvent		= SourcingEvent
				related.SourcingEventLine	= SourcingEventLine
			Instance Selection
				where (related.AOC	= AOC)

		SourcingEventHeaderAOCRel
			one-to-many relation to SourcingEventLineAOC
			Field Mapping uses symbolic key
				related.Company				= Company
				related.SourcingEvent		= SourcingEvent
				related.SourcingEventLine	= 0
		
		SourcingEventLineResponseAOCRel
			one-to-many relation to SourcingEventLineResponseAOC
			Field Mapping uses symbolic key
				related.NotifiedSupplier.SupplierGroup		= NotifiedSupplier.SupplierGroup
		 		related.NotifiedSupplier.Supplier			= NotifiedSupplier.Supplier
		 		related.NotifiedSupplier.SupplierSourceId	= NotifiedSupplier.SupplierSourceId
				related.Company								= Company
				related.SourcingEvent						= SourcingEvent
				related.SourcingEventLine					= SourcingEventLine
			Instance Selection
				where (related.SourcingEventLineResponseAOC	!= SourcingEventLineResponseAOC
				and    related.AOC				 			= AOC)

	Field Rules
		AOC
			required
			cannot be changed
			constraint (AddOnChargeRel exists)
				"AddOnCostCode<AOC>DoesNotExist"			
			constraint (SourcingEventLineResponseAOCRel not exists)
				"AddOnCostCode<AOC>AlreadyExistsForThisSourcingEventLineResponse"

		AOCOption
			if (HeaderAOCExists)
				force default to AOCOption.Total
			else
			if (CurrencyAOCCost entered)
				default to AOCOption.PerUnit
				required
			else
				initialize AOCOption
				cannot be entered
					"AOCOptionCanBeEnteredOnlyWhenAddOnChargeCostIsEntered"

		ZeroCost
			default to false	

		AOCRate
			initial value is AOC.AddOnChargePercent
			if (AOC.AddOnChargePercent = 0)
				constraint (CurrencyAOCCost = 0)
					"AddOnCostRateCannotBeEnteredWhenAddOnChargeCostIsEntered"
				constraint (!ZeroCost)
					"AddOnCostRateCannotBeEnteredWhenTheZeroCostFlagIsYes"
			if (AOC.AddOnChargePercent > 0)
				constraint (CurrencyAOCCost = 0)
					"AddOnCostRateCannotBeEnteredWhenAddOnChargeCostIsEntered;RateHasBeenDefaultedFromTheAddOnChargeCode"
				constraint (!ZeroCost)
					"AddOnCostRateCannotBeEnteredWhenTheZeroCostFlagIsYes;RateHasBeenDefaultedFromTheAddOnChargeCode"
			if (IsSpread
			and !SpreadMethod.Rate)
				constraint (AOCRate !entered)
					"CannotEnterARate;AOCIsCalculatedByAmount"
			if (IsSpread
			and SpreadMethod.Rate)
				constraint (AOCRate entered)
					"MustEnterARate;AOCIsCalculatedByRate"
			

		CurrencyAOCCost
			constraint (!ZeroCost)
				"AddOnChargeCostCannotBeEnteredWhenTheZeroCostFlagIsYes"
			if (AOC.AddOnChargeType.Allowance)
				if (CurrencyAOCCost > 0)
					CurrencyAOCCost = CurrencyAOCCost * -1
				constraint (CurrencyAOCCost < 0)
					"AddOnChargeCostCannotBeGreaterThanZeroForAllowanceTypeAOC"
			else
			if (AOC.AddOnChargeType.Cost)
				constraint (CurrencyAOCCost >= 0)
					"AddOnChargeCostCannotBeLessThanZeroForCostTypeAOC"
					
			if (AOC.AddOnChargeType.Allowance
			and NotContract)
				constraint (SourcingEventLineResponse.NetUnitPrice >= 0)
					"CannotEnterAnAddOnChargeAllowanceAmountThatWillCauseTheNetUnitPriceToBeLessThanZero"

		AOCCost
			if (SourcingEventResponse.EnteredCurrencyCode   = SourcingEvent.CurrencyCode)
				AOCCost   = CurrencyAOCCost
			else
				initialize LocalCurrencyExchange
		
				if (Company.CurrencyTable entered
				and CurrencyAOCCost entered)

					SavedFromCurrency                       = SourcingEventResponse.EnteredCurrencyCode
					SavedEnterpriseGroup					= Company.ProcurementGroup
					SavedCurrencyTable      				= Company.CurrencyTable
					SavedTransactionAmount      			= CurrencyAOCCost
					SavedExchangeDate		 	 			= current corporate date
					LocalCurrencyExchange.ToCurrency 		= SourcingEvent.CurrencyCode

					AOCCost            	    	    		= LocalCurrencyExchange.OutputCurrencyAmount

	Actions
		Create is a Create Action
			restricted
			Field Rules
				AOCRate
					default to AOC.AddOnChargePercent
					
				AddOnChargeOrigin
					if (HeaderAOCExists)
						default to SourcingEventHeaderAOCRel.AddOnChargeOrigin
					else
						default to SourcingEventLineAOCRel.AddOnChargeOrigin

			Action Rules
				LocalAccountingEntity = Company.AccountingEntity
				
			Exit Rules
				
				invoke UpdateTotalAOCCost

		Update is an Update Action
			valid when (AvailableToUpdateAOC)
			
			Field Rules
			
				AOCRate
				
					if (SourcingEventLineResponse.LowestBidRel exists)
	 					if (!SourcingEventLineResponse.LineHasAllowanceAOC)
		 					if (SourcingEventLine.LowestBidOption.LessThanLowestBid)
		 						constraint (SourcingEventLineResponse.TotalResponse < SourcingEventLineResponse.LowestBidExtended)
		 							"BidTotalAmount<SourcingEventLineResponse.TotalResponse>MustBeLessThanTheLowestBidOf<SourcingEventLineResponse.LowestBidExtended>"
		 					if (SourcingEventLine.LowestBidOption.LessThanOrEqualToLowestBid)
		 						constraint (SourcingEventLineResponse.TotalResponse <= SourcingEventLineResponse.LowestBidExtended)
		 							"BidTotalAmount<SourcingEventLineResponse.TotalResponse>MustBeLessThanOrEqualToTheLowestBidOf<SourcingEventLineResponse.LowestBidExtended>"	
				
				CurrencyAOCCost
				
					if (SourcingEventLineResponse.LowestBidRel exists)
	 					if (!SourcingEventLineResponse.LineHasAllowanceAOC)
		 					if (SourcingEventLine.LowestBidOption.LessThanLowestBid)
		 						constraint (SourcingEventLineResponse.TotalResponse < SourcingEventLineResponse.LowestBidExtended)
		 							"BidTotalAmount<SourcingEventLineResponse.TotalResponse>MustBeLessThanTheLowestBidOf<SourcingEventLineResponse.LowestBidExtended>"
		 					if (SourcingEventLine.LowestBidOption.LessThanOrEqualToLowestBid)
		 						constraint (SourcingEventLineResponse.TotalResponse <= SourcingEventLineResponse.LowestBidExtended)
		 							"BidTotalAmount<SourcingEventLineResponse.TotalResponse>MustBeLessThanOrEqualToTheLowestBidOf<SourcingEventLineResponse.LowestBidExtended>"	
			
			Action Rules
				constraint (SourcingEventResponse.CanUpdateResponse)
					"BuyerCannotUpdateAResponseEnteredByASupplier"
					
				LocalAccountingEntity = Company.AccountingEntity
				
			Exit Rules
			
				invoke UpdateTotalAOCCost

		UpdateHeader is an Update Action
			valid when (AvailableToUpdateAOCHeader)
			
			Action Rules
				constraint (SourcingEventResponse.CanUpdateResponse)
					"BuyerCannotUpdateAResponseEnteredByASupplier"
					
				LocalAccountingEntity = Company.AccountingEntity
				
			Exit Rules	
				invoke UpdateTotalAOCCost
		
		UpdateTotalAOCCost is an Instance Action
			restricted
			
			Action Rules
				display "AOCOption<AOCOption>"
				if (ZeroCost)
					TotalAOCCost = 0
				else
				if (AOCRate entered)
					if (IsHeaderAOC)
						if (AOC.AddOnChargeType.Allowance)
							CurrencyTotalAOCCost =(SourcingEventResponse.SupplierTotalBidAmountInResponseCurrency * AOCRate * -1)
						else
							CurrencyTotalAOCCost = (SourcingEventResponse.SupplierTotalBidAmountInResponseCurrency * AOCRate)
					else
					if (!IsHeaderAOC)
						if (AOC.AddOnChargeType.Allowance)
							CurrencyTotalAOCCost = (SourcingEventLineResponse.CurrencyExtendedPrice * AOCRate * -1)
						else
							CurrencyTotalAOCCost = (SourcingEventLineResponse.CurrencyExtendedPrice * AOCRate)
				else
				if (AOCOption.PerUnit)
					CurrencyTotalAOCCost = (SourcingEventLineResponse.Quantity * CurrencyAOCCost)
					display "Quantity<SourcingEventLineResponse.Quantity>"
					display "Cost<CurrencyAOCCost>"
				else
				if (AOCOption.Total)
					CurrencyTotalAOCCost = CurrencyAOCCost		
					
				if (SourcingEventResponse.EnteredCurrencyCode   = SourcingEvent.CurrencyCode)
					TotalAOCCost   = CurrencyTotalAOCCost
				else
					initialize LocalCurrencyExchange
			
					if (Company.CurrencyTable entered
					and CurrencyTotalAOCCost entered)
	
						SavedFromCurrency                       = SourcingEventResponse.EnteredCurrencyCode
						SavedEnterpriseGroup					= Company.ProcurementGroup
						SavedCurrencyTable      				= Company.CurrencyTable
						SavedTransactionAmount      			= CurrencyTotalAOCCost
						SavedExchangeDate		 	 			= current corporate date
						LocalCurrencyExchange.ToCurrency 		= SourcingEvent.CurrencyCode
	
						TotalAOCCost            	    	    = LocalCurrencyExchange.OutputCurrencyAmount
		
		FastUpdate is an Update Action
			restricted
				
		FastUpdateWithoutEdits is an Instance Action
			restricted

		FastDelete is a Delete Action
			restricted

