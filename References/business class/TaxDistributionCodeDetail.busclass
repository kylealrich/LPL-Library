TaxDistributionCodeDetail is a BusinessClass	 
    owned by tx	 
    prefix is TxDis
  
    Ontology
        symbolic key is TaxDistributionCodeDetail

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		TaxDistributionLevel				 						 
			default label is "DistributionLevel"
		TaxDistributionLevelValue		is a TaxDistributionLevel	 
		Reference						 
		Description						 
		Country
		SystemGenerated					is Boolean					 	
			restricted
		Jurisdiction					is AlphaUpper 60			 	
		DistributionType				is a VertexImpositionType	 
		TaxInputAccount           		is a FinanceCodeBlockFull   
        TaxInputDueAccount        		is a FinanceCodeBlockFull   
        TaxOutputAccount          		is a FinanceCodeBlockFull   
        TaxOutputDueAccount       		is a FinanceCodeBlockFull   
        RecoverableAccount        		is a FinanceCodeBlockFull  	  	 
        NonRecoverableAccount        	is a FinanceCodeBlockFull  	 	 
        ForeignRecoverableAccount  		is a FinanceCodeBlockFull  	 	 
        ForeignNonRecoverableAccount    is a FinanceCodeBlockFull	 
		VATSuspenseAccount				is a FinanceCodeBlockFull	 
			restricted
		LastValidated 					is TimeStamp
							
	Relations
 		DefaultRecordRel
			one-to-many relation to TaxDistributionCodeDetail
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.TaxEntity				= TaxEntity
				related.TaxDistributionCode	   	= TaxDistributionCode
			Instance Selection
				where (related.TaxDistributionLevel = "DEFAULT")
				
        EntityTaxCodesRel	 
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup   
                related.TaxEntity 					= TaxEntity
                related.TaxCode						= DerivedTaxCode 

    	CheckEntityTaxCodesRel	 
            one-to-one relation to EntityTaxCode
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup   
                related.TaxEntity 					= TaxEntity
                related.TaxCode						= OverrideTaxCode 
                
	Transient Fields	
		BypassAccountEdit			is Boolean                					
		OverrideTaxCode				is a TaxCode
		UpdateBlankAccounts			is Boolean 	
		RequireAllInputOutputAccounts is Boolean			
										
	Local Fields 

		EditTaxAccount4PostOption	is a TaxAccountPostOptionEdit
		LocalContinue				is Boolean
		LocalSkipAccountEdit		is Boolean

	Rule Blocks
		EditAccountsForPostOption
			if (BypassAccountEdit)
				LocalSkipAccountEdit = true
			else	
				EditTaxAccount4PostOption.TaxEntity	= TaxEntity
				EditTaxAccount4PostOption.TaxCode	= DerivedTaxCode
				if (EditTaxAccount4PostOption.IsValid)
					LocalContinue = true
				else
					constraint (EditTaxAccount4PostOption.ErrorMessage not entered)
						"<EditTaxAccount4PostOption.ErrorMessage>"																					


		UpdateBlankAccountsFromSource
			if (TaxEntity.UseTaxCodeAccounts)	
				if (EntityTaxCodesRel exist)				
					if (TaxInputAccount not entered)
						TaxInputAccount           	= EntityTaxCodesRel.TaxInputAccount  
					if (TaxInputDueAccount not entered)
				        TaxInputDueAccount        	= EntityTaxCodesRel.TaxInputDueAccount  
					if (TaxOutputAccount not entered)				              	
				        TaxOutputAccount          	= EntityTaxCodesRel.TaxOutputAccount   
					if (TaxOutputDueAccount not entered)				              					               	
				        TaxOutputDueAccount       	= EntityTaxCodesRel.TaxOutputDueAccount 				

					if (RecoverableAccount not entered)				              					              	
                        RecoverableAccount        	= TaxEntity.RecoverableAccount  
                    if (RecoverableAccount not entered)	          	
                        NonRecoverableAccount       = TaxEntity.NonRecoverableAccount 
                    if (RecoverableAccount not entered)	          
                        ForeignRecoverableAccount  	= TaxEntity.ForeignRecoverableAccount  
                    if (RecoverableAccount not entered)	    	
                        ForeignNonRecoverableAccount = TaxEntity.ForeignNonRecoverableAccount
			else
				if (TaxInputAccount not entered)
					TaxInputAccount           		 = TaxEntity.TaxInputAccount   
				if (TaxInputDueAccount not entered)	        	
			        TaxInputDueAccount        		 = TaxEntity.TaxInputDueAccount   
			    if (TaxOutputAccount not entered)         	
			        TaxOutputAccount          		 = TaxEntity.TaxOutputAccount    
			    if (TaxOutputDueAccount not entered)          	
			        TaxOutputDueAccount       		 = TaxEntity.TaxOutputDueAccount 
			    if (RecoverableAccount not entered)          	
			        RecoverableAccount        		 = TaxEntity.RecoverableAccount 
			    if (NonRecoverableAccount not entered)           	
			        NonRecoverableAccount        	 = TaxEntity.NonRecoverableAccount   
			    if (ForeignRecoverableAccount not entered)        
			        ForeignRecoverableAccount  		 = TaxEntity.ForeignRecoverableAccount 
			    if (ForeignNonRecoverableAccount not entered)     	
			        ForeignNonRecoverableAccount     = TaxEntity.ForeignNonRecoverableAccount
 			
																								
	Field Rules
		Country
			initial value is TaxDistributionCode.Country
			default to TaxDistributionCode.Country
		TaxDistributionLevelValue
			default to TaxDistributionLevel.TaxDistributionLevelValue
		Reference
			default to TaxDistributionLevel.Reference
			
		TaxDistributionLevel
			if (TaxDistributionLevel changed
			and old TaxDistributionLevel = "DEFAULT")
				cannot be changed

		DistributionType
			if (TaxDistributionLevel = "DEFAULT")
				cannot be entered
					"DistributionTypeNotAllowedOnADefaultRecord"
					
		SystemGenerated
			if (TaxDistributionLevel = "DEFAULT")
				cannot be changed	

		TaxInputAccount
			if (!TaxDistributionLevel = "DEFAULT")
				if (TaxEntity.UseTaxCodeAccounts							
				and OverrideTaxCode entered									
				and CheckEntityTaxCodesRel exist)							
					default to CheckEntityTaxCodesRel.TaxInputAccount		
				else			
					default to DefaultRecordRel.TaxInputAccount

			if (EditAccount)	
				required
					"TaxInputAccountRequired"  		
								
			if (!action type.Delete)
				EditTaxAccount4PostOption.TaxAccount 		= TaxInputAccount
				EditTaxAccount4PostOption.TaxAccountText 	= InputAccountText
				include EditAccountsForPostOption		

       	TaxInputDueAccount  
			if (!TaxDistributionLevel = "DEFAULT")
				if (TaxEntity.UseTaxCodeAccounts							
				and OverrideTaxCode entered									
				and CheckEntityTaxCodesRel exist)							
					default to CheckEntityTaxCodesRel.TaxInputDueAccount		
				else						
					default to DefaultRecordRel.TaxInputDueAccount
				
			if (EditAccount)	
				required
					"InputDueAccountRequired"  
			if (!action type.Delete)		     	     
				EditTaxAccount4PostOption.TaxAccount 		= TaxInputDueAccount
				EditTaxAccount4PostOption.TaxAccountText 	= InputDueAccountText
				include EditAccountsForPostOption		
       				
		TaxOutputAccount
			if (!TaxDistributionLevel = "DEFAULT")
				if (TaxEntity.UseTaxCodeAccounts							
				and OverrideTaxCode entered									
				and CheckEntityTaxCodesRel exist)							
					default to CheckEntityTaxCodesRel.TaxOutputAccount		
				else											
					default to DefaultRecordRel.TaxOutputAccount

			if (EditAccount)	
				required
					"OutputAccountRequired"  		
						
			if (!action type.Delete)					
				EditTaxAccount4PostOption.TaxAccount 		= TaxOutputAccount
				EditTaxAccount4PostOption.TaxAccountText 	= OutputAccountText
				include EditAccountsForPostOption		
   		 
        TaxOutputDueAccount  			
        	if (!TaxDistributionLevel = "DEFAULT")	
				if (TaxEntity.UseTaxCodeAccounts							
				and OverrideTaxCode entered									
				and CheckEntityTaxCodesRel exist)							
					default to CheckEntityTaxCodesRel.TaxOutputDueAccount		
				else											        		
					default to DefaultRecordRel.TaxOutputDueAccount
				
			if (EditAccount)	
				required
					"OutputDueAccountRequired"  				

			if (!action type.Delete)					
				EditTaxAccount4PostOption.TaxAccount 		= TaxOutputDueAccount
				EditTaxAccount4PostOption.TaxAccountText 	= OutputDueAccountText
				include EditAccountsForPostOption		

        RecoverableAccount 
        	if (!TaxDistributionLevel = "DEFAULT")		
				default to DefaultRecordRel.RecoverableAccount

			if (!action type.Delete)					
				EditTaxAccount4PostOption.TaxAccount 		= RecoverableAccount
				EditTaxAccount4PostOption.TaxAccountText 	= RecoverableAccountText
				include EditAccountsForPostOption	
					               		 
        NonRecoverableAccount 
        	if (!TaxDistributionLevel = "DEFAULT")		
				default to DefaultRecordRel.NonRecoverableAccount

			if (!action type.Delete)					
				EditTaxAccount4PostOption.TaxAccount 		= NonRecoverableAccount
				EditTaxAccount4PostOption.TaxAccountText 	= NonRecoverableAccountText
				include EditAccountsForPostOption		

			if (action type.Create)
		        if (TaxEntity.LandNonRecoverable											
		        and !TaxEntity.UseTaxCodeAccounts)
		        	if (NonRecoverableAccount entered)
		        		constraint (NonRecoverableAccount = TaxInputAccount
		        		        or  NonRecoverableAccount = TaxOutputAccount)
		        			"Tax\Distribution\Code\Detail:<TaxDistributionLevel>:_\NonRecoverableAccountsMustBeLanded;ThereforeMustEqualInputOrOutputAccount"
				            	 
        ForeignRecoverableAccount  		
        	if (!TaxDistributionLevel = "DEFAULT")		
				default to DefaultRecordRel.ForeignRecoverableAccount

			if (!action type.Delete)					
				EditTaxAccount4PostOption.TaxAccount 		= ForeignRecoverableAccount
				EditTaxAccount4PostOption.TaxAccountText 	= ForeignRecoverableAccountText
				include EditAccountsForPostOption		
				         
        ForeignNonRecoverableAccount     
        	if (!TaxDistributionLevel = "DEFAULT")		
				default to DefaultRecordRel.ForeignNonRecoverableAccount

			if (!action type.Delete)					
				EditTaxAccount4PostOption.TaxAccount 		= ForeignNonRecoverableAccount
				EditTaxAccount4PostOption.TaxAccountText 	= ForeignNonRecoverableAccountText
				include EditAccountsForPostOption		

			if (action type.Create)
		        if (TaxEntity.LandNonRecoverable											
		        and !TaxEntity.UseTaxCodeAccounts)
	        		if (ForeignNonRecoverableAccount entered)
		        		constraint (ForeignNonRecoverableAccount = TaxInputAccount
		        		        or  ForeignNonRecoverableAccount = TaxOutputAccount)
							"Tax\Distribution\Code\Detail:<TaxDistributionLevel>:_\NonRecoverableAccountsMustBeLanded;ThereforeMustEqualInputOrOutputAccount"	     											
										        
		VATSuspenseAccount				 
		OverrideTaxCode
			constraint (!OverrideTaxCode.TaxType.TaxTableCode)
				"CannotUseATaxTableFor_\Override\Tax\Code"
																
    Derived Fields
		DerivedTaxCode is a DerivedField	 
			type is like TaxCode
			restricted
			if (OverrideTaxCode entered)
				constraint (TaxEntity.UseTaxCodeAccounts)
					"TaxEntityMustBeSetToUseTaxCodeAccounts"
				constraint (CheckEntityTaxCodesRel exist)
					"EntityTaxCodeDoesNotExistForTaxCode<OverrideTaxCode>"
			if (OverrideTaxCode entered)	 
				return OverrideTaxCode						
			if (TaxDistributionCode.TransientTaxCode entered)	 
				return TaxDistributionCode.TransientTaxCode

			return TaxDistributionCode.DefaultTaxCode	    

		DerivedPostOption is a DerivedField	 
			type is like PostingOption
			if (TaxEntity.UseTaxCodeAccounts)
				if (DerivedTaxCode entered
				and EntityTaxCodesRel exist) 
					return EntityTaxCodesRel.PostOption
				else
					return blank
			else
			if (!TaxEntity.UseTaxCodeAccounts)	
				return TaxEntity.PostOption		

			return blank    
						
		DerivedMessage is a DerivedField	 
			type is MessageField
			if (TaxInputAccount not entered
			or  TaxInputDueAccount not entered
			or  TaxOutputAccount not entered
			or  TaxOutputDueAccount not entered)
				return AccountRequiredText
				
	        if (TaxEntity.LandNonRecoverable											
	        and !TaxEntity.UseTaxCodeAccounts)
	        	if (NonRecoverableAccount entered)
	        		if (NonRecoverableAccount = TaxInputAccount
	        		or  NonRecoverableAccount = TaxOutputAccount)
	        			LocalContinue = true
	        		else	
	        			return LandNonRecoverableError

        		if (ForeignNonRecoverableAccount entered)
	        		if (ForeignNonRecoverableAccount = TaxInputAccount
	        		or  ForeignNonRecoverableAccount = TaxOutputAccount)
	        			LocalContinue = true
	        		else
						return LandNonRecoverableError
				            	 	        			
	        			
			return blank

		AccountRequiredText is a MessageField
			"Error...Input,InputDue,OutputAndOutputDueAccountsRequired"
			
		LandNonRecoverableError is a MessageField
			"NonRecoverableAccountsMustBeLanded;ThereforeMustEqualInputOrOutputAccount"
			
		DerivedTaxLabel is a DerivedField
			type is MessageField
			restricted
			if (TaxEntity.UseTaxCodeAccounts)
				return DisplayEntityTaxCodeText
			
			return DisplayTaxEntityText

		DisplayTaxEntityText is a MessageField
			restricted
			"TaxEntityAccounts"
			
		DisplayEntityTaxCodeText is a MessageField
			restricted
			"EntityTaxCodeAccounts"

		InputAccountText is a MessageField 
			"TaxInputAccount"	
		InputDueAccountText is a MessageField 
			"TaxInputDueAccount"	
		OutputAccountText is a MessageField 
			"TaxOutputAccount"	
		OutputDueAccountText is a MessageField 
			"TaxOutputDueAccount"	
        RecoverableAccountText is a MessageField 
			"RecoverableAccount" 
        NonRecoverableAccountText is a MessageField 
			"NonRecoverableAccount" 
        ForeignRecoverableAccountText is a MessageField 
			"ForeignRecoverableAccount"  		
        ForeignNonRecoverableAccountText is a MessageField 
			"ForeignNonRecoverableAccount"    
			        									
    Conditions
    	EditAccount
			when (!BypassAccountEdit						 
			and   !TaxDistributionLevel = "DEFAULT"			 
			and   !action type.Delete)    					 
		InputOrOutputAccountMissing
			when (TaxInputAccount not entered
			or	  TaxOutputAccount not entered)
		EntityTaxCodeLevel
			when (TaxEntity.UseTaxCodeAccounts
			and   DerivedTaxCode entered
			and   EntityTaxCodesRel exist)	
    Sets
        ByTaxDistributionLevel
            indexed
            Sort Order
				FinanceEnterpriseGroup
				TaxEntity
				TaxDistributionCode				
                TaxDistributionLevel		    
				DistributionType				
				
	Actions
		Create is a Create Action
			Entrance Rules
									
		Update is an Update Action
			Action Rules
				if (UpdateBlankAccounts)
					include UpdateBlankAccountsFromSource
					UpdateBlankAccounts = false
				if (RequireAllInputOutputAccounts)	
					constraint (TaxInputAccount entered
					and  	    TaxInputDueAccount entered
					and         TaxOutputAccount entered
					and         TaxOutputDueAccount entered)
						"AllInput,InputDue,OutputAndOutputDueAccountsRequiredToUse_\Tax\Distribution\Code"
						
		Delete is a Delete Action
			Entrance Rules
				constraint (!TaxDistributionLevel = "DEFAULT")
					"CannotDeleteDefaultRecord"
				constraint (!SystemGenerated)
					"CannotDeleteSystemGeneratedDefaultRecord"
								
