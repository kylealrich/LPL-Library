CashCodeGroup is a BusinessClass
    owned by cb
    prefix is CCG
    classic name is CBCCGRP
	
    Ontology
        symbolic key is CashCodeGroup
            classic set name is CCGSET1
            classic name is CASH-CODE-GRP

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        CashCodeList    is Text   

    Relations

        CashCodeGroupDetailsRel
            classic name is CBGRPCC
            one-to-many relation to CashCodeGroupDetail
            delete cascades
            Field Mapping uses symbolic key
            	related.CashManagementGroup = CashManagementGroup	
                related.CashCodeGroup		= CashCodeGroup

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = actor.context.FinanceEnterpriseGroup
				related.DimensionGroupType			  = 15
				related.FinanceDimensionStructure	  = CashCodeGroup

    Field Rules

        Description
            required

	Derived Fields
		CashCodeGroupAndDescription is a MessageField
			default label is "CashCodeGroup"
			"<CashCodeGroup>_-_<Description>"
			
	Conditions
		RecordExists
			restricted
			when (CashCodeGroup exists)
			
		HasCashCodeGroupDetails
			when (CashCodeGroupDetailsRel exists)
			
		UsedInASecurityGroup
			when (SecurityGroupRel exists)

		SecurityGroupAllowsAccess
			when (actor.context.CashCodeSecurityGroup = blank
			or    actor.context.CashCodeSecurityGroup.FinanceDimensionStructure = CashCodeGroup)		

	Rule Blocks
	
	Actions
		Create is a Create Action
			Action Rules

		Update is an Update Action
			Action Rules

		Delete is a Delete Action
		
		CopyMembers is an Instance Action
			Parameters
				CopyToGroup is an CashCodeGroup
				
			Parameter Rules
			
				CopyToGroup
					constraint (CopyToGroup != CashCodeGroup)
						"CannotCopyAGroupToItself"
						
			Action Rules
				for each CashCodeGroupDetail set
					invoke Create CashCodeGroupDetail
						resume on error
						fill in fields from each
							except invoked.CashCodeGroup
						invoked.CashCodeGroup = CopyToGroup
					commit transaction				


		BuildCashCodeList is an Instance Action 
		    default label is untranslatable
		    restricted
		    Local Fields
		        FirstCashCode is Boolean
		    Action Rules
		        initialize CashCodeList
		        FirstCashCode = true
		        for each CashCodeGroupDetailsRel
		            if (not FirstCashCode)
		                CashCodeList += ","
		            else
		                FirstCashCode = false
		            CashCodeList += each.CashCode

		RebuildCashCodeLists is a Set Action   
		    default label is untranslatable
		    restricted
		    Parameters
		    Action Rules
		        Instance Rules
		            invoke BuildCashCodeList
