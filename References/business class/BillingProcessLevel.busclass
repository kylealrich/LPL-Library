BillingProcessLevel is a BusinessClass
    owned by bl
    prefix is OEP
    classic name is OEPROCLEV

    Ontology
        symbolic key is BillingProcessLevel
            classic set name is OEPSET1
            classic name is PROCESS-LEVEL

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Name
        CompanyName                     is a Name	 
        	holds pii
            classic name is POA-NAME
        CompanyAddress                  is a PostalAddressV2	
        	holds pii
            classic name for CompanyAddress.DeliveryAddress.AddressLine1 is POA-ADDR1
            classic name for CompanyAddress.DeliveryAddress.AddressLine2 is POA-ADDR2
            classic name for CompanyAddress.DeliveryAddress.AddressLine3 is POA-ADDR3
            classic name for CompanyAddress.DeliveryAddress.AddressLine4 is POA-ADDR4
            classic name for CompanyAddress.Municipality is POA-CITY
            classic name for CompanyAddress.StateProvince is POA-STATE
            classic name for CompanyAddress.PostalCode is POA-ZIP
            classic name for CompanyAddress.County is POA-COUNTY
            classic name for CompanyAddress.Country is POA-CTRY-CD
            classic name for CompanyAddress.Region is POA-REGION




        RemitToName                     is a Name	 
        	holds pii
            classic name is RMT-NAME
        RemitToAddress                  is a PostalAddressV2	
        	holds pii
            classic name for RemitToAddress.DeliveryAddress.AddressLine1 is RMT-ADDR1
            classic name for RemitToAddress.DeliveryAddress.AddressLine2 is RMT-ADDR2
            classic name for RemitToAddress.DeliveryAddress.AddressLine3 is RMT-ADDR3
            classic name for RemitToAddress.DeliveryAddress.AddressLine4 is RMT-ADDR4
            classic name for RemitToAddress.Municipality is RMT-CITY
            classic name for RemitToAddress.StateProvince is RMT-STATE
            classic name for RemitToAddress.PostalCode is RMT-ZIP
            classic name for RemitToAddress.County is RMT-COUNTY
            classic name for RemitToAddress.Country is RMT-CTRY-CD
            classic name for RemitToAddress.Region is RMT-REGION




        DefaultLocation                 is a InventoryLocation
            classic name is DEF-LOCATION
        DefaultOrderType                is an OrderEntryOrderType
            classic name is DEF-ORDER-TYP
        DefaultReason                   is a CbReason
            classic name is DEF-REASON
        DefaultCashOnDeliveryHoldCode   is a ReceivableHoldCode
            classic name is DEF-COD-HOLD
        DefaultCashInAdvanceHoldCode    is a ReceivableHoldCode
            classic name is DEF-CIA-HOLD





        InvoiceEditOption               is Boolean
            classic name is INVC-EDIT-OPT
        InterfaceCost                   is Boolean
            classic name is INTF-COST-FL
        InterfaceInvoice	            is Boolean
            classic name is INTF-INV-FL
        TaxCalulationAndPrinting
            classic name is TAX-PRINT-CD
        TaxPrice	                    is Boolean
            classic name is TAX-PRICE-FL
        DiscountAddOnCharge	            is Boolean
            classic name is DISC-AOC-FL
        TermsAddOnCharge                is Boolean
            classic name is TERMS-AOC-FL
        InvoiceCreation		            is Boolean
            classic name is INVC-CR-FL
        InvoiceEdit		                is Boolean
            classic name is INVC-EDIT-FL
        InvoicePrint	                is Boolean
            classic name is INVC-PRT-FL
        InvoiceUpdate					is Boolean
            classic name is INVC-UPD-FL
        InvoiceDetail	                is Boolean
            classic name is INVC-DTL-FL
        CashOnDeliveryOption            is Boolean
            classic name is COD-INVC-OPT
        ShipDays                        is Numeric size 3
        EDINumber
        InUse	                        is Boolean
            classic name is USED-FL
        TaxExemptCode
            classic name is TAX-EXEMPT-CD
        TaxCode
        CompanyEuropeanCommunityCountry is Numeric size 3
            sql name is CEuropeanCommunityCountry
            classic name is POA-EC-CTRY
        CompanyISOCountry               is Numeric size 3
            classic name is POA-ISO-CTRY
        RemitToEuropeanCommunityCountry is a RmtEcCtry
            sql name is RToEuropeanCommunityCountry
            classic name is RMT-EC-CTRY
        RemitToISOCountry               is a RmtIsoCtry
            classic name is RMT-ISO-CTRY
        TermsFreight	                is Boolean
            classic name is TERMS-FRT-FL

        SalesAccount                    is a FinanceCodeBlock
            classic name for SalesAccount.AccountingUnit is SLS-ACCT-UNIT
            classic name for SalesAccount.GeneralLedgerChartAccount is SLS-ACCOUNT
        ExpenseAccount                  is a FinanceCodeBlock
            classic name for ExpenseAccount.AccountingUnit is EXP-ACCT-UNIT
            classic name for ExpenseAccount.GeneralLedgerChartAccount is EXP-ACCOUNT
        SpecialOrderSalesAccount        is a FinanceCodeBlock
            classic name for SpecialOrderSalesAccount.AccountingUnit is MI-ACCT-UNIT
            classic name for SpecialOrderSalesAccount.GeneralLedgerChartAccount is MI-ACCOUNT
        SpecialOrderCOGSAccount         is a FinanceCodeBlock
            classic name for SpecialOrderCOGSAccount.AccountingUnit is MCG-ACCT-UNIT
            classic name for SpecialOrderCOGSAccount.GeneralLedgerChartAccount is MCG-ACCOUNT
        DiscountAccount                 is a FinanceCodeBlock
            classic name for DiscountAccount.AccountingUnit is DSC-ACCT-UNIT
            classic name for DiscountAccount.GeneralLedgerChartAccount is DSC-ACCOUNT
        RoundingAccount                 is a FinanceCodeBlock
            classic name for RoundingAccount.AccountingUnit is RND-ACCT-UNIT
            classic name for RoundingAccount.GeneralLedgerChartAccount is RND-ACCOUNT
		BillingInvoiceProcessLevelIDMTemplate  is an IDMTemplate
			default label is "BillingInvoice"	
		BillingInvoiceDocument
			default label is "BillingInvoice"
 		BillingInvoiceEmailTemplate			is an IDMTemplate
			default label is "BillingInvoice"
		BillingInvoiceEmailSubject			is Alpha size 255
			default label is "BillingInvoice"
			Text Variables
				CompanyName
				InvoiceNumber
				InvoiceDate	    
		PrintFullInvoiceNumber				is Boolean       
    
    Context Fields

    	InventoryLocation


	Local Fields
		ValidateReceivablePrefix 
		CalculateTax	
		LocalPrefix 						is like CmPrefix
		LocalInvoiceType					is like ReceivableInvoiceType
		LocalCountry						is a Country
		LocalState							is a StateProvince
		LocalZip 							is a PostalCode

   		VertexErrorNumber					is Numeric 3
   		MultipleTaxCodes     				is Boolean	
    	WebServiceError     				is Boolean
    	WebServiceErrorMessage 				is Text	
		LocalLookupKey 						is Alpha 100
        LocalResponseBusclassKey 			is like LastSequence	 
        LocalSequence 						is like Sequence
        OldTaxCode							is like TaxCode
 		NewTaxCode							is like TaxCode
		CalculateTaxOutputMessage 			is Alpha 200	

		LocalProcessLevelTotalsComputed		is Boolean
		LocalProcessLevelTotalFreightAmount is an InternationalAmount
		LocalProcessLevelTotalTaxAmount		is an InternationalAmount
		LocalProcessLevelTotalInvoiceNetAmt	is an InternationalAmount
		LocalBillingInvoiceIDMEmailSubject	is Alpha size 255
			Text Variables
				CompanyName					value is Company.Name
				InvoiceNumber				value is "IL1234"
				InvoiceDate					value is current corporate date		

	
	Transient Fields
		TransientInvoicePrefix 				is like CmPrefix
			derive value from InvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix 
    	TransientCreditMemoPrefix 			is like CmPrefix
			derive value from CreditMemoBillingPrefixRel.BillingInvoicePrefix.Prefix
		TransientCreditMemoPrintOption 		is a CreditMemoPrintOption
    		derive value from CreditMemoBillingPrefixRel.BillingInvoicePrefix.CreditMemoPrintOption    	
    	TransientRecurringInvoicePrefix 	is like CmPrefix
			derive value from RecurringInvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix    	
    	TransientLastInvoice 				is Numeric size 10
    		derive value from InvoiceBillingPrefixRel.LastDocNbr
    	TransientLastCreditMemo 			is Numeric size 10
    		derive value from CreditMemoBillingPrefixRel.LastDocNbr
    	TransientLastRecurringInvoice 		is Numeric size 10
    		derive value from RecurringInvoiceBillingPrefixRel.LastDocNbr
		TransientDefaultReasonDescription 	is like Description
			derive value from OrderCancelCreditReasonRel.Description
		TransientFEG 						is like FinanceEnterpriseGroup
			derive value from Company.CustomerBusinessGroup.FinanceEnterpriseGroup
		ObjectReference 					is BusinessObjectReference
			derive value from reference to this instance	
		       		
	Rule Blocks
				
		ValidatePrefix		
			initialize ValidateReceivablePrefix
		   	ValidateReceivablePrefix.EditReceivableCompany		= Company
			ValidateReceivablePrefix.EditSystem					= "BL"	 
			ValidateReceivablePrefix.EditReceivablePrefix		= LocalPrefix	
			ValidateReceivablePrefix.EditReceivableInvoiceType	= LocalInvoiceType
		    constraint (ValidateReceivablePrefix.ErrorNumber not entered)
		      	"<ValidateReceivablePrefix.ErrorMessage>"	
		
		ValidateOrCreateTaxCode
			if (TaxExemptCode.Taxable
			and IsThirdPartySetToVertexStqGeocode
			and TaxCode not entered)

				initialize CalculateTax
				CalculateTax.TaxEntity						= Company.AccountingEntity	
				CalculateTax.PostalAddress					= CompanyAddress
                CalculateTax.VertexQuantumOption            = "G"
              	
              	constraint (CalculateTax.OutputErrorNumber not entered)  
                    "<CalculateTax.OutputErrorMessage>"
                    
                if (CalculateTax.ReturnedGeoCode entered)
            		TaxCode = CalculateTax.ReturnedGeoCode 
                if (CompanyAddress.County not entered)
                	CompanyAddress.County	  = CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County 
                if (CompanyAddress.PostalCode not entered)
                	CompanyAddress.PostalCode = CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode 
				
				LocalState	= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].StateProvince 
				LocalZip	= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode	
				

				if (TaxCode not exists
				and TaxEntityRel.UseTaxCodeAccounts)
					invoke Create TaxCode
						invoked.TaxCode = TaxCode
						invoked.TaxCode.TaxType = "S" 
						invoked.TaxCode.Description = LocalTaxCodeDescription 
		
		DeleteVertexResultsFiles
			invoke Delete VertexTaxAreaIdResultsRel
			invoke Delete UpdatePostalAddressRecordsRel
			invoke Delete VertexLookupResultsRel
							      	
	
	Derived Fields

		DerivedProcessLevelTotalFreight is a DerivedField
    		type is like InternationalAmount
	    		precision is Company.BaseNumberOfDecimals
			if (Company.LocalIDMDocumentType.InvoiceRegister)
				return sum BillingInvoicesForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
			else
			if (Company.LocalIDMDocumentType.InvoiceAndRegisterPrint) 
				if (Company.LocalEnteredInvoicePrefix entered)
					return sum PrintedBillingInvoiceRegisterForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
				else
					return sum EditedBillingInvoiceRegisterForIDMRel.FreightCharge.BaseAmount.EnteredCurrencyAmount
    		
    	DerivedProcessLevelTotalTax is a DerivedField
    		type is like InternationalAmount
    			precision is Company.BaseNumberOfDecimals
			if (Company.LocalIDMDocumentType.InvoiceRegister)
				return sum BillingInvoicesForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount
 			else
			if (Company.LocalIDMDocumentType.InvoiceAndRegisterPrint) 
				if (Company.LocalEnteredInvoicePrefix entered)
					return sum PrintedBillingInvoiceRegisterForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount
				else
					return sum EditedBillingInvoiceRegisterForIDMRel.TaxTotal.BaseAmount.EnteredCurrencyAmount

    	DerivedProcessLevelTotalInvoiceNet is a DerivedField
    		type is like InternationalAmount
    			precision is Company.BaseNumberOfDecimals
			if (Company.LocalIDMDocumentType.InvoiceRegister)
				return sum BillingInvoicesForIDMRel.DerivedInvoiceNetAmountForIDM     			
 			else
			if (Company.LocalIDMDocumentType.InvoiceAndRegisterPrint) 
				if (Company.LocalEnteredInvoicePrefix entered)
					return sum PrintedBillingInvoiceRegisterForIDMRel.DerivedInvoiceNetAmountForIDM
				else
					return sum EditedBillingInvoiceRegisterForIDMRel.DerivedInvoiceNetAmountForIDM  

    		
		DefaultReasonCode					is a MessageField
			restricted
			"DefaultCancelReasonCode"
		Message502 							is a MessageField
			restricted
		 	"AddComplete-StateRequiredForQuantumGeocodeLookup"
		LocalTaxCodeDescription 			is a StringField 
			type is AlphaUpper size 30
			restricted
			LocalState
			LocalZip	
		CompletionMessage	is a DerivedField
   			type is MessageField
   			restricted
   			if (VertexErrorNumber entered)
   				return TaxErrorMessage
   			else
   			if (WebServiceError)
				return VertexWebserviceErrorMessage
   			else	
   			if (MultipleTaxCodes)
   				return VertexMultTaxCodesMessage
   			else
   			if (TaxCode entered)
   				return VertexCompletionMessage	
   			else	
  			if (TaxCode entered)
   				return VertexCompletionMessage	    				 
		
		TaxErrorMessage is a MessageField
			restricted
			"TaxErrorOccurred;TaxCodeNotUpdated"
			
		VertexMultTaxCodesMessage is a MessageField
			restricted
			"TaxCodeNotUpdated.MultipleTaxCodesExistsForVendorsAddress"
			
		VertexCompletionMessage	is a MessageField
			restricted
			"UpdateTaxCodeComplete"	
			
		VertexWebserviceErrorMessage	is a MessageField
			restricted
			"NoTaxCodesReturnedDueToA\VertexError"		

		OldTaxCodeMsg				is a MessageField
			restricted
			"OldTaxCode:<OldTaxCode>"

		VertexNoChangeMessage				is a LabelField
			"NoChangeNecessary,Correct\Tax\Area\IdExists"
		
		BillingProcessLevelDescriptionID is a MessageField
			"<BillingProcessLevel>_-_<Name>"

		DerivedLookupMessage				is a DerivedField
			type is MessageField
			restricted
			if (NewTaxCode = OldTaxCode)
				return VertexNoChangeMessage
			else
				return CalculateTaxOutputMessage 

		TaxCodeMessage				is a DerivedField
			type is MessageField
			restricted
			if (NewTaxCode = OldTaxCode)
				return blank
			else
				return OldTaxCodeMsg
			
		DerivedOEDailyShipmentJournalProcessLevelTotal is a DerivedField
			type is like InternationalAmount
			for each BillingInvoiceForDailyShipmentJournalIDMRel
				DerivedOEDailyShipmentJournalProcessLevelTotal += each.GrossBaseAmount
			return DerivedOEDailyShipmentJournalProcessLevelTotal

		DerivedBillingInvoiceIDMEmailSubject	is a DerivedField
			type is AlphaUpper size 255
			restricted
			LocalBillingInvoiceIDMEmailSubject		=	BillingInvoiceEmailSubject
			return LocalBillingInvoiceIDMEmailSubject text		
										   								
    Conditions

        IsProcessLevelInvoicing
        	restricted
            when (Company.InvoiceOption.ProcessLevelInvoicing)
            
        IsCompanyLevelInvoicing
        	restricted
        	when (Company.InvoiceOption.CompanyLevelInvoicing)
		
        WasValueInCombinationOne
        	restricted
        	when (old TaxCalulationAndPrinting.CalcAndPrintInSummary
        	or    old TaxCalulationAndPrinting.CalcSummPrtDetailSumm)
        
        IsValueInCombinationOne
        	restricted
        	when (TaxCalulationAndPrinting.CalcAndPrintInSummary
        	or    TaxCalulationAndPrinting.CalcSummPrtDetailSumm)
        
        WasValueInCombinationTwo
        	restricted
        	when (old TaxCalulationAndPrinting.CalcByLinePrintDetail
		    or 	  old TaxCalulationAndPrinting.CalcByLinePrintSummary
		    or 	  old TaxCalulationAndPrinting.CalcLinePrtDetailSumm
		    or 	  old TaxCalulationAndPrinting.PriceInclTaxPrintSummary)
		    
        IsValueInCombinationTwo
        	restricted
        	when (TaxCalulationAndPrinting.CalcByLinePrintDetail
		    or 	  TaxCalulationAndPrinting.CalcByLinePrintSummary
		    or 	  TaxCalulationAndPrinting.CalcLinePrtDetailSumm
		    or 	  TaxCalulationAndPrinting.PriceInclTaxPrintSummary)
        
        IsUniqueInvoicePrefix
        	restricted
    		when (TransientInvoicePrefix			!= TransientCreditMemoPrefix
			and   TransientInvoicePrefix			!= TransientRecurringInvoicePrefix)
						
		IsUniqueCreditMemoPrefix
			restricted
    		when (TransientCreditMemoPrefix			!= TransientInvoicePrefix
			and   TransientCreditMemoPrefix			!= TransientRecurringInvoicePrefix)
			
		IsUniqueRecurringInvoicePrefix
			restricted
    		when (TransientRecurringInvoicePrefix	!= TransientCreditMemoPrefix
			and   TransientRecurringInvoicePrefix	!= TransientInvoicePrefix)
			
		IsThirdPartySetToVertexStqGeocode
			restricted

           	when (TaxEntityRel.ThirdParty.VertexOSeries)	
 
 		VertexResultsExist	 
 			restricted
			when (VertexTaxAreaIdResultsRel exist
			and  !VertexTaxAreaIdResultsRel.WebServiceError)

		VertexErrorExist	 
			restricted
			when (VertexWebserviceErrorRel exist)
			
		HasBillingInvoiceForIDMOrderEntryDailyShipmentJournal
			restricted
			when (BillingInvoiceForDailyShipmentJournalIDMRel exists)
			          	
    Relations


    	BillingInvoicesForIDMRel
    		one-to-many relation to BillingInvoice
			Field Mapping uses Set9
				related.Company				= Company
				related.BillingProcessLevel	= BillingProcessLevel
			Instance Selection
				where	(related.Status.ARGLUpdated
				and		 related.InvoiceDate 			within Company.IDMDateRange
				and		 related.GeneralLedgerDate 		within Company.IDMGLDateRange
				and		(Company.LocalCustomerForIDM not entered
				or		 related.Customer				= Company.LocalCustomerForIDM))
		

		EditedBillingInvoiceRegisterForIDMRel
			one-to-many relation to BillingInvoice
			Field Mapping uses ByBillingProcessLevel
				related.Company	= Company	
				related.BillingProcessLevel = BillingProcessLevel
			Instance Selection
				where (related.Status.Edited
				and	   related.HoldCode not entered
				and   (Company.LocalCustomerForIDM not entered or related.Customer = Company.LocalCustomerForIDM)
				and   (Company.LocalCutOffDate not entered or related.InvoiceDate <= Company.LocalCutOffDate)
				and	  (not Company.LocalInvoiceIsCOD or related.CustomerOrder.CashOnDelivery = Company.LocalInvoiceIsCOD)
				and   (not Company.LocalPrintInvoicesOnly or not related.BillingInvoice.BillingInvoiceType.Credit)
				and   (not Company.LocalPrintCreditMemosOnly or related.BillingInvoice.BillingInvoiceType.Credit))

		PrintedBillingInvoiceRegisterForIDMRel
			one-to-many relation to BillingInvoice
			Field Mapping uses ByBillingProcessLevel
				related.Company	= Company	
				related.BillingProcessLevel = BillingProcessLevel
			Instance Selection
				where ((related.Status.Printed or related.Status.ARGLUpdated)
				and   (Company.LocalCustomerForIDM not entered or related.Customer = Company.LocalCustomerForIDM)
				and   (Company.LocalEnteredInvoicePrefix not entered or related.BillingInvoice.InvoicePrefix = Company.LocalEnteredInvoicePrefix)
				and	  (Company.LocalFromInvNumber not entered or related.BillingInvoice.InvoiceNumber >= Company.LocalFromInvNumber)
				and	  (Company.LocalToInvNumber not entered or related.BillingInvoice.InvoiceNumber   <= Company.LocalToInvNumber))				


				
		ReceivableProcessLevelRel
            one-to-one relation to ReceivableProcessLevel
            Field Mapping uses symbolic key
                related.Company                							= Company
                related.ReceivableProcessLevel 							= BillingProcessLevel

        BillingInvoicePrefixesRel is a BillingInvoicePrefix set

		ElectronicPaymentBillingPrefixRel	
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= BillingProcessLevel
				related.BillingInvoicePrefix	= "TI"

		RecurringInvoiceBillingPrefixRel	
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= BillingProcessLevel  
				related.BillingInvoicePrefix	= "RI"

		InvoiceBillingPrefixRel				
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= BillingProcessLevel  
				related.BillingInvoicePrefix	= "IN"

       	AuthBatchBillingPrefixRel		
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= BillingProcessLevel  
				related.BillingInvoicePrefix	=  "CB"

   		CreditMemoBillingPrefixRel				
            one-to-one relation to BillingInvoicePrefix
            Field Mapping uses symbolic key
                related.Company 				= Company		
				related.BillingProcessLevel 	= BillingProcessLevel  
				related.BillingInvoicePrefix	= "CM"	

       	OrderCancelCreditReasonRel
            one-to-one relation to OrderCancelCreditReason
            Field Mapping uses symbolic key
                related.Company                 = Company
                related.OrderCancelCreditReason = DefaultReason

		UnreleasedBillingInvoicesByProcessLevelsRel
			one-to-many relation to BillingInvoice
            Field Mapping uses Set2
                related.Company 				= Company
                related.Status					= 0 
                related.BillingProcessLevel 	= BillingProcessLevel

        OtherBillingInvoicePrefixRel
        	one-to-many relation to BillingInvoicePrefix
            Field Mapping uses ByBillingProcessLevel
                related.Company 				= Company
                related.Prefix					= LocalPrefix
            Instance Selection
        		where (related.BillingProcessLevel  != BillingProcessLevel)

       	TaxEntityRel				 
            one-to-one relation to TaxEntity
            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup	
                related.TaxEntity 				= Company.AccountingEntity

 		VertexConfigRel
        	one-to-one relation to VertexConfig
        	Field Mapping uses symbolic key
				related.VertexConfig 									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup 		

  		VertexWebserviceErrorRel
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance
				and    related.WebServiceError							= true)
								
    	VertexTaxAreaIdResultsRel
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance)

  		UpdatePostalAddressRecordsRel
			one-to-many relation to VertexTaxAreaIdPostalAddress
			Field Mapping uses ByTaxAreaId	 
				related.VertexConfig										= Company.CustomerBusinessGroup.FinanceEnterpriseGroup	
				related.VertexTaxAreaIdPostalAddress.LookupKey  			= LocalLookupKey
            Instance Selection
				where  (related.VertexTaxAreaIdPostalAddress.ResponseBusclassKey 	= LocalResponseBusclassKey)
				
   		SelectedTaxAreaIdRecordsRel
			one-to-many relation to VertexTaxAreaIdResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
				related.RelatedObjectReference							= reference to this instance
			Instance Selection	
				where  (related.Selected 								= true)	 

   		SelectedPostalAddressRecordsRel
			one-to-many relation to VertexTaxAreaIdPostalAddress
			Field Mapping uses ByTaxAreaId	 
				related.VertexConfig										= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
				related.VertexTaxAreaIdPostalAddress.LookupKey  			= LocalLookupKey
				related.VertexTaxAreaIdPostalAddress.ResponseBusclassKey 	= LocalResponseBusclassKey
			Instance Selection	
				where (related.Selected 									= true)
				
		VertexLookupResultsRel
			one-to-many relation to VertexLookupResults
			Field Mapping uses ByRelatedObjectReference	 
				related.VertexConfig									= Company.CustomerBusinessGroup.FinanceEnterpriseGroup 
			Instance Selection	
				where (related.RelatedObjectReference					= reference to this instance)
				
		BillingInvoiceForDailyShipmentJournalIDMRel
			one-to-many relation to BillingInvoice
			Field Mapping uses ByBillingProcessLevel
				related.Company				= Company
				related.BillingProcessLevel = BillingProcessLevel
				related.BatchNumber			= InventoryLocation.LocalBatchNumber
			Instance Selection
				where (related.WarehouseShipment.InventoryLocation 	= InventoryLocation
				and	   related.CustomerOrder entered
				and    related.ShipDate 						   <= InventoryLocation.TransientShipDateCutoff
				and    (InventoryLocation.LocalShipmentType = 1
					or related.CustomerOrder.CashOnDelivery))








							
    Field Rules
    	BillingProcessLevel
			constraint (ReceivableProcessLevelRel exists)
				"ReceivableProcessLevel<BillingProcessLevel>DoesNotExist"
				
        DefaultCashOnDeliveryHoldCode
            required

        DefaultCashInAdvanceHoldCode
            required

        DefaultReason
        	default to Company.DefaultReason
            required
			constraint (OrderCancelCreditReasonRel.Canceled)
				"ReasonCodeInvalidForCancelations"				

        Name
        	default to ReceivableProcessLevelRel.Name
            required		
         
        CompanyAddress
        	default to ReceivableProcessLevelRel.PostalAddress

        TaxExemptCode
			initial value is TaxExemptCode.Exempt
        	default to TaxExemptCode.Exempt
            



        


        
        RemitToName
        	default to Company.RemitToName 			
        	
        RemitToAddress
        	default to Company.RemitToAddress













       	RemitToEuropeanCommunityCountry
        	default to Company.RemitToEuropeanCommunityCountry

			
       	RemitToISOCountry
       		default to Company.RemitToISOCountry

       		
		InterfaceCost



			
			if (IsCompanyLevelInvoicing) 
				cannot be entered
					"CannotSetInterfaceCostDueToCompanyLevelInvoicing"  
					
		InvoiceEditOption



			
			if (IsCompanyLevelInvoicing)
				cannot be entered

					"CannotSetInvoiceEditOptionAtProcessLevelForCompanyLevelInvoicing"

		CashOnDeliveryOption
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotSetCashOnDeliveryOptionForCompanyLevelInvoicing" 

		DiscountAddOnCharge
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotSetDiscountAddOnChargeForCompanyLevelInvoicing" 
					
		TermsAddOnCharge
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotSetTermsAddOnChargeForCompanyLevelInvoicing" 

		TermsFreight
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotSetTermsFreightDueToCompanyLevelInvoicing" 

		TaxCalulationAndPrinting 
			if (IsProcessLevelInvoicing)
				required
					"MustEnterTaxCalulationAndPrintingOption" 
				
				if (TaxCalulationAndPrinting.CalcAndPrintInSummary
        		or TaxCalulationAndPrinting.CalcLinePrtDetailSumm
        		or TaxCalulationAndPrinting.CalcSummPrtDetailSumm)
        			constraint (TaxEntityRel.ThirdParty.InforTax)
        				"OptionsCalcAndPrintInSummary,CalcLinePrtDetailSumm,AndCalcSummPrtDetailSummNotValidWithThirdPartyTaxSystems" 
				
				if (TaxCalulationAndPrinting changed) 
					if (WasValueInCombinationOne  
					and IsValueInCombinationTwo)
						constraint (UnreleasedBillingInvoicesByProcessLevelsRel not exists)
							"Invoice(s)Exist;CanOnlyChangeToOptionCalcAndPrintInSummaryOrCalcSummPrtDetailSumm" 
	
					if	(WasValueInCombinationTwo 
					and IsValueInCombinationOne)
						constraint (UnreleasedBillingInvoicesByProcessLevelsRel not exists)
							"Invoice(s)Exist;CanOnlyChangeToOptionCalcByLinePrintDetail,CalcByLinePrintSummary,PriceInclTaxPrintSummary,OrCalcLinePrtDetailSumm" 		
			
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotEnterTaxPrintOption;ForCompanyLevelInvoicing"	

		InvoiceDetail
			if (IsProcessLevelInvoicing)
				default to true	
				
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotSetPrintDetail;InvoiceOptionIsNotByCompanyLevelInvoicing"	
					
		TaxCode
			if (TaxExemptCode.Taxable
			and !IsThirdPartySetToVertexStqGeocode)//(TXTXC-THIRD-PARTY NOT = "Q")
				required
					"TaxCodeMustBeEnteredWhenTaxExemptCodeIsTaxable"		
			
			if (TaxExemptCode.Exempt)
				cannot be entered
					"TaxCodeCannotBeEnteredWhenTaxExemptCodeIsExempt"	
			
			if (TaxExemptCode.Taxable
			and !IsThirdPartySetToVertexStqGeocode) //(TXTXC-THIRD-PARTY NOT = "Q")
				initialize CalculateTax
				CalculateTax.TaxEntity	= Company.AccountingEntity	
				CalculateTax.TaxCode	= TaxCode
				CalculateTax.Function	= CalculateTax.Function.Inquire
				constraint (CalculateTax.OutputErrorNumber not entered)
					"<CalculateTax.OutputErrorMessage>"
			
		TransientInvoicePrefix
			if (IsProcessLevelInvoicing)
				required
					"MustEnterInvoicePrefix"						

				LocalPrefix			= TransientInvoicePrefix
				constraint (OtherBillingInvoicePrefixRel not exists)
					"InvoicePrefixIsAlreadyInUseByAnotherProcessLevel"		

			   	if (action type.Create
			   	or (action type.Update 
			   	and TransientInvoicePrefix not = InvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix))
					LocalInvoiceType	= "I"
					include ValidatePrefix
								      	
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotEnterInvoicePrefix;InvoiceOptionIsNotByProcessLevelInvoicing" 
			
			constraint (IsUniqueInvoicePrefix)
				"Invoice,CreditMemo,RecurringPrefixMustBeUnique"				
					
        			
		TransientCreditMemoPrefix
			if (IsProcessLevelInvoicing)
				required
					"MustEnterCreditMemoPrefix"			

				LocalPrefix			= TransientCreditMemoPrefix
				constraint (OtherBillingInvoicePrefixRel not exists)
					"CreditMemoPrefixIsAlreadyInUseByAnotherProcessLevel"	

			   	if (action type.Create
			   	or (action type.Update 
			   	and TransientCreditMemoPrefix not = CreditMemoBillingPrefixRel.BillingInvoicePrefix.Prefix))
					LocalInvoiceType	= "C"
					include ValidatePrefix
				
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotEnterCreditMemoPrefix;InvoiceOptionIsNotByProcessLevelInvoicing"		

			constraint (IsUniqueCreditMemoPrefix)
				"Invoice,CreditMemo,RecurringPrefixMustBeUnique"				
			
			
		TransientRecurringInvoicePrefix
			if (IsProcessLevelInvoicing)
				required
					"MustEnterRecurringInvoicePrefix"			

			    LocalPrefix			= TransientRecurringInvoicePrefix
				constraint (OtherBillingInvoicePrefixRel not exists)
					"RecurringInvoicePrefixIsAlreadyInUseByAnotherProcessLevel"		

			   	if (action type.Create
			   	or (action type.Update 
			   	and TransientRecurringInvoicePrefix not = RecurringInvoiceBillingPrefixRel.BillingInvoicePrefix.Prefix))
				    LocalInvoiceType	= "I"
					include ValidatePrefix
				
			if (IsCompanyLevelInvoicing)
				cannot be entered
					"CannotEnterRecurringPrefix;InvoiceOptionIsNotByProcessLevelInvoicing"		

			constraint (IsUniqueRecurringInvoicePrefix)
				"Invoice,CreditMemo,RecurringPrefixMustBeUnique"				
		
		TransientDefaultReasonDescription
			default to DefaultReasonCode
		
		SalesAccount
			default to Company.SalesAccount
			required
			
		ExpenseAccount
			default to Company.ExpenseAccount
			required
			
		SpecialOrderSalesAccount
			default to Company.SpecialOrderSalesAccount
			required
		
		SpecialOrderCOGSAccount
			default to Company.SpecialOrderCOGSAccount
			required
		
		DiscountAccount
			default to Company.DiscountAccount
			required
		
		RoundingAccount
			if (IsProcessLevelInvoicing
			and Company.Rounding)
				required
			 			
		BillingInvoiceProcessLevelIDMTemplate
			constraint (BillingInvoiceProcessLevelIDMTemplate.IDMDocumentType.BillingInvoice)
				"InvalidTemplate"
						
		BillingInvoiceEmailTemplate
			constraint (BillingInvoiceEmailTemplate.IDMDocumentType.BillingInvoiceEmail)
				"InvalidTemplate"
			 	
			 			
	Actions
		Create is a Create Action
			Action Rules
				if (DefaultReason entered
				and OrderCancelCreditReasonRel not exists)
					invoke Create OrderCancelCreditReason
						invoked.Company								= Company
        				invoked.OrderCancelCreditReason				= DefaultReason
						invoked.Description							= TransientDefaultReasonDescription //"DefaultCancelReasonCode"
						invoked.Canceled							= true
						invoked.Billing								= false
						invoked.RemoveBooking						= false
						invoked.Active								= true
						
				include ValidateOrCreateTaxCode
				
			Exit Rules
				invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
        			invoked.BillingProcessLevel 	= BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "CM"
        			invoked.Prefix					= TransientCreditMemoPrefix

        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
        			invoked.BillingProcessLevel 	= BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "IN"
        			invoked.Prefix					= TransientInvoicePrefix
        			
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
        			invoked.BillingProcessLevel 	= BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "RI"
        			invoked.Prefix					= TransientRecurringInvoicePrefix
				
				invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
        			invoked.BillingProcessLevel		= BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "TI"
        		
        		invoke Create BillingInvoicePrefix
        			invoked.Company					= Company
        			invoked.BillingProcessLevel		= BillingProcessLevel
        			invoked.BillingInvoicePrefix	= "CB"
        			
        		if (TaxExemptCode.Taxable
				and IsThirdPartySetToVertexStqGeocode
				and TaxCode not entered)
					if (CompanyAddress.StateProvince not entered)
						display "<Message502>" 
			
		T2VCreate is a Create Action				
			restricted 
			default label is untranslatable 
			bypass field rules 
			
		Update is an Update Action
			Action Rules

				if (DefaultReason changed
				and OrderCancelCreditReasonRel not exists)
					invoke Create OrderCancelCreditReason
						invoked.Company								= Company
        				invoked.OrderCancelCreditReason				= DefaultReason
						invoked.Description							= TransientDefaultReasonDescription //"DefaultCancelReasonCode"
						invoked.Canceled							= true
						invoked.Billing								= false
						invoked.RemoveBooking						= false
						invoked.Active								= true

				if (TransientInvoicePrefix changed)
					invoke Update InvoiceBillingPrefixRel
						if (IsProcessLevelInvoicing)	
							invoked.Prefix								= TransientInvoicePrefix
						else
							invoked.Prefix								= blank		
					
				if (TransientCreditMemoPrefix changed)
	    			invoke Update CreditMemoBillingPrefixRel 
		    			if (IsProcessLevelInvoicing)
		    				invoked.Prefix								= TransientCreditMemoPrefix
		    			else
							invoked.Prefix								= blank
				
				invoke Update CreditMemoBillingPrefixRel 
	    			if (IsProcessLevelInvoicing
	    			and TransientCreditMemoPrintOption entered)
	    				invoked.CreditMemoPrintOption					= TransientCreditMemoPrintOption
	    			else
						initialize invoked.CreditMemoPrintOption
				
				if (TransientRecurringInvoicePrefix changed)							
	        		invoke Update RecurringInvoiceBillingPrefixRel
		        		if (IsProcessLevelInvoicing) 
		        			invoked.Prefix								= TransientRecurringInvoicePrefix
		        		else
							invoked.Prefix								= blank	
				
				include ValidateOrCreateTaxCode
			Exit Rules
				TransientCreditMemoPrintOption = CreditMemoBillingPrefixRel.BillingInvoicePrefix.CreditMemoPrintOption
										
		Delete is a Delete Action


		VertexTaxCodeLookup is an Instance Action		 

			default label is "UpdateVertexTaxCode"
			valid when (TaxExemptCode.Taxable)
			run in foreground	

			completion message is "<CompletionMessage>" 

			Parameters

			Parameter Rules

			Local Fields
				LocalTaxCode			  is a TaxCode                
			Action Rules								
		    	constraint (CompanyAddress entered)
		      		"AddressRequiredForVertexLookup"
  				MultipleTaxCodes = false    				 
    			WebServiceError  = false   	
		      	initialize VertexErrorNumber					 
    			initialize WebServiceErrorMessage 				 
				initialize LocalLookupKey 						 
        		initialize LocalResponseBusclassKey 			 	 
        		initialize LocalSequence 						 	
				initialize CalculateTax
				CalculateTax.RelatedObjectReference         	= reference to this instance
				CalculateTax.System								= "BR"
				CalculateTax.CallingModule						= "BillingProcessLevel"			
				CalculateTax.VertexQuantumOption				= "G"
				CalculateTax.TaxEntity							= Company.AccountingEntity
				CalculateTax.InFinanceEnterpriseGroup       	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup	 
				CalculateTax.VendorGrp							= blank
				CalculateTax.TaxEntity							= blank	
				CalculateTax.PostalAddress						= CompanyAddress

				LocalTaxCode									= CalculateTax.ReturnedGeoCode
				VertexErrorNumber								= CalculateTax.OutputErrorNumber 


				MultipleTaxCodes								= CalculateTax.MultipleTaxAreaIds
				WebServiceError									= CalculateTax.WebServiceError 
				WebServiceErrorMessage							= CalculateTax.WebServiceErrorMessage
				







				if (LocalTaxCode entered)
					TaxCode = LocalTaxCode
				if (LocalTaxCode entered
				and !MultipleTaxCodes
				and !WebServiceError				
				and CalculateTax.OutputErrorNumber not entered)					
					if (CompanyAddress.PostalCode not entered)
						CompanyAddress.PostalCode 	= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
						CompanyAddress.County 		= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County

					if (VertexLookupResultsRel exist)		
						for each VertexLookupResultsRel
							invoke Update each
								invoked.Status = 1		
































		UpdateTaxCodeWithSelectedRecords is an Instance Action			 

			restricted
			valid when (!WebServiceError)
			Parameters
				DeleteFilesAfterUpdate	  is Boolean				 
					default label is "DeleteResultsFilesAfterUpdate"
			Local Fields
				LocalTaxCode			  is a TaxCode 
			Action Rules
				if (SelectedTaxAreaIdRecordsRel exist)						 
					TaxCode 					= first SelectedTaxAreaIdRecordsRel.TaxAreaId
					LocalTaxCode 				= first SelectedTaxAreaIdRecordsRel.TaxAreaId
					for each VertexTaxAreaIdResultsRel
						invoke UpdateAndBypassRules each	 
							invoked.Selected = false			

				if (VertexTaxAreaIdResultsRel exist)	
					LocalLookupKey 				= VertexTaxAreaIdResultsRel.VertexTaxAreaIdResults.LookupKey
	        		LocalResponseBusclassKey 	= VertexTaxAreaIdResultsRel.VertexTaxAreaIdResults.ResponseBusclassKey	 
	        		LocalSequence 				= VertexTaxAreaIdResultsRel.VertexTaxAreaIdResults.Sequence

				if (SelectedPostalAddressRecordsRel exist)	
					CompanyAddress.County 		= first SelectedPostalAddressRecordsRel.SubDivision
					CompanyAddress.PostalCode	= first SelectedPostalAddressRecordsRel.PostalCode																							
					for each UpdatePostalAddressRecordsRel
						invoke UpdateAndBypassRules each
							each.Selected = false
				else
					CompanyAddress.County 		= first UpdatePostalAddressRecordsRel.SubDivision
					CompanyAddress.PostalCode	= first UpdatePostalAddressRecordsRel.PostalCode									
			
				if (DeleteFilesAfterUpdate)
					include DeleteVertexResultsFiles

				if (VertexLookupResultsRel exist)
					for each VertexLookupResultsRel
						invoke Update each
							invoked.Status = 1			


		BatchUpdateWithVertexTaxAreaId is a Set Action		

			completion message is "<CompletionMessage>" 


			Parameters
				PrmEnterpriseGroup	is a VertexConfig				 
					default label is "FinanceEnterpriseGroup"
				PrmProcessGroup		is Alpha 50
					default label is "ProcessGroup"

				PrmBillingCompany			is a BillingCompany	 
					default label is "BillingCompany"
				PrmBeginBillingProcLevel	is like BillingProcessLevel
					default label is "BeginBillingProcessLevel"
				PrmEndBillingProcLevel		is like BillingProcessLevel
					default label is "EndBillingProcessLevel"
				PrmReadOnly					is Boolean
					default label is "ReadOnly"	
				EntityTaxCodeDefault 		is like TaxCode	
				PrmTaxEntity 		 		is like TaxEntity	
					default label is "TaxEntity"						
			Parameter Rules
				PrmBillingCompany
					if (PrmEndBillingProcLevel entered
					or  PrmBeginBillingProcLevel entered)
						required
				PrmBeginBillingProcLevel
					if (PrmEndBillingProcLevel not entered)
						required
				PrmEndBillingProcLevel
					if (PrmBeginBillingProcLevel not entered)
						required
			Local Fields
				LocalTaxCode			is like TaxCode 
				CompletionMessage		is Alpha 150
				RecordCount				is Numeric 10
				
			Instance Selection
				where (TaxExemptCode.Taxable 
				and    PrmEnterpriseGroup   = Company.CustomerBusinessGroup.FinanceEnterpriseGroup
				and    Company   			= PrmBillingCompany
				and    BillingProcessLevel	>= PrmBeginBillingProcLevel 
				and    BillingProcessLevel	<= PrmEndBillingProcLevel)
							
			Sort Order
				BillingProcessLevel

			Accumulators

				
				        								
			Action Rules								
				Empty Set Rules
					CompletionMessage = "<CompletionMessage>"
				
				Set Rules
					Entrance Rules				 
	                	constraint (VertexConfigRel exist)
							"VertexConfigurationNotSetUp;CheckIfTaxEntitySetUpToUseVertex" 
						initialize RecordCount
					Exit Rules
						
				BillingProcessLevel Set Rules
					Entrance Rules
			        
					Exit Rules				
						
				Instance Rules
					increment RecordCount
					initialize CalculateTax
	  				MultipleTaxCodes = false    				 
	    			WebServiceError  = false   	
			      	initialize VertexErrorNumber					 
	    			initialize WebServiceErrorMessage 				 
					initialize LocalLookupKey 						 
	        		initialize LocalResponseBusclassKey 			 	 
	        		initialize LocalSequence 						 	
					initialize CalculateTax
					CalculateTax.RelatedObjectReference         	= reference to this instance
					CalculateTax.System								= "BR"
					CalculateTax.CallingModule						= "BillingProcessLevel"			
					CalculateTax.VertexQuantumOption				= "G"
					CalculateTax.TaxEntity							= Company.AccountingEntity
					CalculateTax.InFinanceEnterpriseGroup       	= Company.CustomerBusinessGroup.FinanceEnterpriseGroup	 
					CalculateTax.VendorGrp							= blank
					CalculateTax.TaxEntity							= PrmTaxEntity	 		
					CalculateTax.EntityTaxCodeDefault				= EntityTaxCodeDefault	
					CalculateTax.PostalAddress						= CompanyAddress
					LocalTaxCode									= CalculateTax.ReturnedGeoCode
					VertexErrorNumber								= CalculateTax.OutputErrorNumber 
					MultipleTaxCodes								= CalculateTax.MultipleTaxAreaIds
					WebServiceError									= CalculateTax.WebServiceError 
					WebServiceErrorMessage							= CalculateTax.WebServiceErrorMessage
					CalculateTaxOutputMessage						= CalculateTax.OutputErrorMessage
					NewTaxCode = LocalTaxCode	
									
					OldTaxCode = TaxCode
					if (LocalTaxCode entered
					and !PrmReadOnly)
						TaxCode = LocalTaxCode
					if (LocalTaxCode entered
					and !MultipleTaxCodes
					and !WebServiceError				
					and CalculateTax.OutputErrorNumber not entered
					and !PrmReadOnly)					
						if (CompanyAddress.PostalCode not entered)
							CompanyAddress.PostalCode 	= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].PostalCode
							CompanyAddress.County 		= CalculateTax.OutputMultipleGeoCodes.GeoCodeTable[1].County
	
					LocalLookupKey 	= CalculateTax.ResultsLookupKey
					LocalSequence	= CalculateTax.ResultsLookupKeySequence	



					if (VertexLookupResultsRel exist)
						invoke Delete VertexLookupResultsRel
						
					invoke Create VertexLookupResults
						invoked.VertexConfig			   			= Company.CustomerBusinessGroup.FinanceEnterpriseGroup
							invoked.VertexLookupResults.LookupKey 	= LocalLookupKey
							invoked.VertexLookupResults.Sequence	= LocalSequence 
							invoked.RelatedObjectReference			= reference to this instance
							invoked.LookupBillingCompany			= Company 
							invoked.LookupBillingProcessLevel		= BillingProcessLevel
							invoked.LookupProcessGroup				= PrmProcessGroup
							invoked.ReturnedHighesTaxAreaId			= LocalTaxCode
							invoked.NumberOfTaxAreaIdsReturned		= CalculateTax.NumberOfTaxCodes	 
							invoked.LookupPostalAddress 			= CompanyAddress
							invoked.LookupTaxEntity					= PrmTaxEntity	
							invoked.ErrorNumber						= CalculateTax.OutputErrorNumber  
							invoked.ErrorMessage					= DerivedLookupMessage	
							invoked.LookupDescription				= TaxCodeMessage	
							invoked.WebServiceError 				= CalculateTax.WebServiceError 
							invoked.WebServiceErrorMessage 			= CalculateTax.WebServiceErrorMessage 
							if (MultipleTaxCodes
							or  WebServiceError
							or  CalculateTax.OutputErrorNumber entered)								
								invoked.Status = 0						
							else
							if (LocalTaxCode not = OldTaxCode)
								invoked.Status = 1						
							else
								invoked.Status = 2						
									

		UpdateTaxCodeAndAddress is an Instance Action

			restricted
			Parameters
				PrmTaxCode		is like TaxCode
				PrmCountry		is a Country
				PrmState		is a StateProvince
				PrmZip			is a PostalCode
				PrmCounty		is a County
			Parameter Rules
			Action Rules

				if (PrmTaxCode entered)
					TaxCode = PrmTaxCode
				if (CompanyAddress.PostalCode not entered)
					CompanyAddress.PostalCode	= PrmZip
				if (CompanyAddress.County not entered)
					CompanyAddress.County 		= PrmCounty																		
