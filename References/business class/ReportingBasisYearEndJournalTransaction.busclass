ReportingBasisYearEndJournalTransaction is a BusinessClass
	owned by GeneralLedger
	sql name is "RBYearEndJournalTransaction"
	prefix is YEJT
	
	Ontology
    	symbolic key is ReportingBasisYearEndJournalTransaction	

	Patterns
		disable Auditing			
	Persistent Fields
		ReportType						is Numeric 1
			States
				BalanceSheetOpening			value is 1
				ProfitAndLoss    			value is 2
				BalanceSheetClosing			value is 3		
		FinanceDimension1
		FinanceDimension2
		FinanceDimension3
		FinanceDimension4
		FinanceDimension5
		FinanceDimension6
		FinanceDimension7
		FinanceDimension8
		FinanceDimension9
		FinanceDimension10	
        NetTransactionAmount			is a TotalAmount
			precision is ReportingBasisYearEndJournalTransaction.Currency.NumberOfDecimals    
		NetFunctionalAmount				is a TotalAmount
			precision is ReportingBasisYearEndJournal.AccountingEntity.FunctionalCurrency.NumberOfDecimals
		NetAlternateAmount				is a TotalAmount
			precision is ReportingBasisYearEndJournal.AccountingEntity.AlternateCurrency.NumberOfDecimals
		NetAlternateAmount2				is a TotalAmount
			precision is ReportingBasisYearEndJournal.AccountingEntity.AlternateCurrency2.NumberOfDecimals
		NetAlternateAmount3				is a TotalAmount
			precision is ReportingBasisYearEndJournal.AccountingEntity.AlternateCurrency3.NumberOfDecimals
        NetProjectAmount				is a TotalAmount
			precision is ReportingBasisYearEndJournalTransaction.Project.Currency.NumberOfDecimals	
		NetReportAmount1				is a TotalAmount
			precision is FinanceEnterpriseGroup.ReportCurrencyOne.NumberOfDecimals
		NetReportAmount2				is a TotalAmount
			precision is FinanceEnterpriseGroup.ReportCurrencyTwo.NumberOfDecimals			
		NetReportAmount3				is a TotalAmount
			precision is FinanceEnterpriseGroup.ReportCurrencyThree.NumberOfDecimals			
		NetReportAmount4				is a TotalAmount
			precision is FinanceEnterpriseGroup.ReportCurrencyFour.NumberOfDecimals			
		NetReportAmount5				is a TotalAmount
			precision is FinanceEnterpriseGroup.ReportCurrencyFive.NumberOfDecimals		    

	Local Fields
		LocalAccountingEntityGroup		is like AccountingEntityGroup
		LocalCalendarPeriod				is a GeneralLedgerCalendarPeriod	
		LocalBasisYear					is like ReportingBasisYear
		LocalAccountingEntity	is an AccountingEntity

		LocalJournalEntryLineSeq 			is Numeric size 9
		LocalGLJCCalendarYear				is Alpha size 5
		LocalConfigurationParameter			is Alpha size up to 200

 	Derived Fields
 		DerivedYearKey   is a DerivedField
			type is Year
			restricted
			if (BalanceSheetOpening)
				return ReportingBasisYearEndJournalTransaction.EntityYearPeriod.Year - 1
			else
				return ReportingBasisYearEndJournalTransaction.EntityYearPeriod.Year
				
		DerivedBeginingYearEnderiodString is a StringField
			type is AlphaUpper 20
			restricted
			DerivedYearKey
			"000"		
		
		BalanceSheetClosingLabel is a LabelField
			restricted
			"BalanceSheetClosingTransaction"
		
		ProfitAndLossLabel is a LabelField
			restricted
			"IncomeStatementClosingTransaction"

		BalanceSheetOpeningLabel is a LabelField
			restricted
			"BalanceSheetOpeningTransaction"
			
		DerivedFormName	is a DerivedField
			type is Alpha 40
			
			if (BalanceSheetOpening)
				return BalanceSheetOpeningLabel
				
			if (ProfitAndLossReport)
				return ProfitAndLossLabel
			
			if (BalanceSheetClosing)
				return BalanceSheetClosingLabel		

		DerivedListName is a DerivedField
			type is Alpha 40
			if (ReportingBasisYearEndJournal.BalanceSheetOpening)
				return BalanceSheetOpeningLabel
			
			if (ReportingBasisYearEndJournal.ProfitAndLoss)
				return ProfitAndLossLabel
				
			if (ReportingBasisYearEndJournal.BalanceSheetClosing)
				return BalanceSheetClosingLabel

		DerivedNewYear		is a DerivedField
			type is Year
			restricted
			return LocalBasisYear + 1

		DerivedCalendarPeriodString is a StringField
			type is AlphaUpper 20
			restricted
			DerivedNewYear
			"000"
			

#ifdef module integration	
		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter = "Generic_Delimiter"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedIntegrationType is a DerivedField
			type is Alpha size 10
			restricted
			LocalConfigurationParameter = "IntegrationApplication"
			if (FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedReportingChartID is a DerivedField
			type is Alpha size 25
			restricted
			return FinanceEnterpriseGroup+ DerivedDelimiter + DerivedDescription
					
		DerivedSrcDocRefType is a DerivedField
			type is Alpha size 30
			restricted
			if(DerivedSystem = "AP" and DerivedIntegrationType = "Local.ly")
				return "SupplierInvoice"
			else
			if(DerivedSystem = "AR" and DerivedIntegrationType = "Local.ly")
				return "Invoice"
			else
				return DerivedSystem
			if(DerivedIntegrationType = "Local.ly" and (AccountingEntity.AddressCode = "PL" or AccountingEntity.AddressCode = "SG") and DerivedSrcDocRefType = "GL")
				return  "SourceSystemJournalEntry"
				
		DerivedDocumentID is a DerivedField
			type is Alpha size 60
			restricted
			if(DerivedIntegrationType = "Local.ly")
				return FinanceEnterpriseGroup + DerivedDelimiter + ReportingBasisYearEndJournalTransaction.EntityYearPeriod + DerivedDelimiter + ReportingBasis
#endif			
		DerivedDescription is a DerivedField
			type is Alpha size 60
			return ReportingBasis.Description
			
		DerivedSystem is a DerivedField
			type is Alpha 60
			restricted
			return ReportingBasisYearEndJournalTransaction.System
			
		DerivedCurrency is a DerivedField
			type is Alpha size 5
			restricted 
			return ReportingBasisYearEndJournalTransaction.Currency
			
		DerivedFunctionalCurrency is a DerivedField
			type is Alpha size 5
			restricted
			return AccountingEntity.FunctionalCurrency
			
		DerivedReportCurrencyOne is a DerivedField
			type is AlphaUpper size 5
			restricted
			return FinanceEnterpriseGroup.ReportCurrencyOne		

	Conditions
		BalanceSheetOpening
			restricted
			when (ReportType.BalanceSheetOpening)
		ProfitAndLossReport
			restricted
			when (ReportType.ProfitAndLoss)		
		BalanceSheetClosing
			restricted
			when (ReportType.BalanceSheetClosing)	
		AlternateCurrencyUsed
			restricted
			when (ReportingBasisYearEndJournal.AccountingEntity.AlternateCurrencyEntered)
		AlternateCurrency2Used
			restricted
			when (ReportingBasisYearEndJournal.AccountingEntity.AlternateCurrency2Entered)
		AlternateCurrency3Used
			restricted
			when (ReportingBasisYearEndJournal.AccountingEntity.AlternateCurrency3Entered)							
        ProjectUsed
        	restricted
        	when (FinanceEnterpriseGroup.ProjectLabelExists)
        ProjectExists
        	restricted
        	when (ReportingBasisYearEndJournalTransaction.Project entered)
		ReportCurrencyOneUsed
			restricted
			when (FinanceEnterpriseGroup.ReportCurrencyOneEntered)
		ReportCurrencyTwoUsed
			restricted
			when (FinanceEnterpriseGroup.ReportCurrencyTwoEntered)
		ReportCurrencyThreeUsed
			restricted
			when (FinanceEnterpriseGroup.ReportCurrencyThreeEntered)
		ReportCurrencyFourUsed
			restricted
			when (FinanceEnterpriseGroup.ReportCurrencyFourEntered)
		ReportCurrencyFiveUsed
			restricted
			when (FinanceEnterpriseGroup.ReportCurrencyFiveEntered)        	
		AccountingUnitUsed
			restricted
			when (FinanceEnterpriseGroup.AccountingUnitLabelExists)
		Dimension1Used
			restricted
			when (FinanceEnterpriseGroup.Dimension1LabelExists)
		Dimension2Used
			restricted
			when (FinanceEnterpriseGroup.Dimension2LabelExists)
		Dimension3Used
			restricted
			when (FinanceEnterpriseGroup.Dimension3LabelExists)
		Dimension4Used
			restricted
			when (FinanceEnterpriseGroup.Dimension4LabelExists)
		Dimension5Used
			restricted
			when (FinanceEnterpriseGroup.Dimension5LabelExists)
		Dimension6Used
			restricted
			when (FinanceEnterpriseGroup.Dimension6LabelExists)
		Dimension7Used
			restricted
			when (FinanceEnterpriseGroup.Dimension7LabelExists)
		Dimension8Used
			restricted
			when (FinanceEnterpriseGroup.Dimension8LabelExists)
		Dimension9Used
			restricted
			when (FinanceEnterpriseGroup.Dimension9LabelExists)
		Dimension10Used
			restricted
			when (FinanceEnterpriseGroup.Dimension10LabelExists)				
		HasJournalReferenceNumber									
			when (GeneralLedgerJournalControlRefRel exists)
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))
			
	Relations
		GeneralLedgerTotalRel
			one-to-many relation to GeneralLedgerTotal
			Field Mapping uses ByGeneralLedgerChartAccountPeriod
				related.FinanceEnterpriseGroup							= FinanceEnterpriseGroup
				related.Scenario										= FinanceEnterpriseGroup.ActualsScenario
				related.GeneralLedgerTotal.GeneralLedgerChartAccount	= ReportingBasisYearEndJournalTransaction.GeneralLedgerChartAccount
				related.AccountingEntity								= ReportingBasisYearEndJournal.AccountingEntity
			Instance Selection
				where ((related.GeneralLedgerTotal.EntityYearPeriod		>= DerivedBeginingYearEnderiodString
				and  related.GeneralLedgerTotal.EntityYearPeriod		<= ReportingBasisYearEndJournalTransaction.EntityYearPeriod)
				and (related.GeneralLedgerTotal.Ledger					= ReportingBasisYearEndJournalTransaction.PrimaryLedger)
				and (related.GeneralLedgerTotal.System					= ReportingBasisYearEndJournalTransaction.System)
				and (related.GeneralLedgerTotal.Currency				= ReportingBasisYearEndJournalTransaction.Currency)
        		and (related.GeneralLedgerTotal.AccountingUnit			= ReportingBasisYearEndJournalTransaction.AccountingUnit)
            	and (related.GeneralLedgerTotal.Project					= ReportingBasisYearEndJournalTransaction.Project)
				and (related.GeneralLedgerTotal.DimensionCode			= ReportingBasisYearEndJournalTransaction.DimensionCode))
				
		GeneralLedgerJournalControlRefRel							
			one-to-many relation to GeneralLedgerJournalControlRef
			Field Mapping uses ByLedgerYearEnd
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ReportingBasis				= ReportingBasisYearEndJournal.ReportingBasisYear.ReportingBasis
				related.AccountingEntity			= ReportingBasisYearEndJournal.AccountingEntity
				related.BasisYear					= ReportingBasisYearEndJournal.ReportingBasisYear
				related.Ledger						= ReportingBasisYearEndJournalTransaction.Ledger
				related.ReportingBasisYear			= ReportingBasisYearEndJournal.ReportingBasisYear
				related.ReportingBasisYearEndJournal = ReportType
						
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup	= actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity		= ReportingBasisYearEndJournal.AccountingEntity

		AccountingEntityGroupMemberRel
			one-to-many relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup 		= FinanceEnterpriseGroup
				related.AccountingEntityGroup 		= LocalAccountingEntityGroup
				related.AccountingEntity	  		= ReportingBasisYearEndJournal.ReportingBasis.AccountingEntityHierarchyDetailRel.SubordinateAccountingEntity						

		GeneralLedgerCalendarPeriodRel
			one-to-many relation to GeneralLedgerCalendarPeriod
			Field Mapping uses ByTopNode
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.TopNode							= ReportingBasisYearEndJournal.ReportingBasis.GeneralLedgerCalendar.TopNode
				related.Year							= LocalBasisYear
				related.PeriodType						= 1

		GeneralLedgerJournalControlRefByYearEndJournalRel							
			one-to-many relation to GeneralLedgerJournalControlRef
			Field Mapping uses ByYearEndJournal
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ReportingBasis				= ReportingBasisYearEndJournal.ReportingBasisYear.ReportingBasis
				related.AccountingEntity			= LocalAccountingEntity
				related.GeneralLedgerJournalControl	= 0
				related.ReportingBasisYear			= LocalCalendarPeriod.Year

		ReportingBasisYearEndJournalRel
			one-to-many relation to ReportingBasisYearEndJournal
			Field Mapping uses symbolic key
		        related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
		        related.ReportingBasis         = ReportingBasisYearEndJournal.ReportingBasisYear.ReportingBasis
		        related.ReportingBasisYear     = LocalCalendarPeriod.Year
		        related.AccountingEntity       = LocalAccountingEntity
		        

#ifdef module integration
		FSMBODConfigurationParameterRel
        	one-to-one relation to FSMBODConfigurationParameter
			Field Mapping uses symbolic key
            	related.FSMBODConfigurationParameter 		= LocalConfigurationParameter
#endif


	Sets
		ByLedger
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				ReportingBasisYear
				AccountingEntity
				ReportingBasisYearEndJournal
				ReportingBasisYearEndJournalTransaction.Ledger
				
		BySystem
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				ReportingBasisYear
				AccountingEntity
				ReportingBasisYearEndJournal
				ReportingBasisYearEndJournalTransaction.System
	Actions
		Create is a Create Action
			restricted
			bypass field rules		
		
		Update is an Update Action
			restricted
			bypass field rules
			
		Purge is a Purge Action
			restricted
		
		PurgeYearEndJournalPerEntity is a Set Action
			default label is untranslatable										
			restricted
			run in background
			Parameters															
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup		
				PrmReportingBasis				is a ReportingBasis				
				PrmReportingBasisYear			is a ReportingBasisYear			
				PrmAccountingEntity				is an AccountingEntity			
				PrmAccountingEntityGroup	    is an AccountingEntityGroup		
				PrmBalanceSheetOpeningBasisYear is like ReportingBasisYear
			Parameter Rules
				PrmAccountingEntityGroup
					LocalAccountingEntityGroup	= PrmAccountingEntityGroup
			Instance Selection													
				where (FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup		
				and	   ReportingBasis			= PrmReportingBasis	



				and    ((ReportingBasisYearEndJournalTransaction.ReportingBasisYearEndJournal != 1 and ReportingBasisYear	= PrmReportingBasisYear)  
				or      (ReportingBasisYearEndJournalTransaction.ReportingBasisYearEndJournal  = 1 and ReportingBasisYear	= PrmBalanceSheetOpeningBasisYear))	
				and    ((PrmAccountingEntity  not entered and PrmAccountingEntityGroup not entered)
				or      (PrmAccountingEntity      entered and AccountingEntity 	= PrmAccountingEntity)
				or      (PrmAccountingEntityGroup entered and AccountingEntity	= AccountingEntityGroupMemberRel.AccountingEntity)))
				
			Sort Order is ByLedger
			Action Rules
				AccountingEntity Set Rules
					Exit Rules
						LocalBasisYear 				 = PrmReportingBasisYear
						LocalAccountingEntity = AccountingEntity

						LocalCalendarPeriod	= GeneralLedgerCalendarPeriodRel.EndDateJulian	
						for each GeneralLedgerJournalControlRefByYearEndJournalRel
							invoke Delete 	   each
						
						for each ReportingBasisYearEndJournalRel 
							invoke PurgeHeader each

						LocalCalendarPeriod	= DerivedCalendarPeriodString
						for each GeneralLedgerJournalControlRefByYearEndJournalRel
							invoke Delete 	   each
						
						for each ReportingBasisYearEndJournalRel
							invoke PurgeHeader each 
				Instance Rules
					invoke Purge
					
			
