CashLedgerFundTransferDetail is a BusinessClass
    owned by cb
    prefix is CBL
    classic name is CBTRFDTL

    Ontology
        symbolic key is CashLedgerFundTransferDetail
            classic set name is CBLSET1

            classic name for CashLedgerFundTransferHeader is TRANSFER-NBR

    Patterns

        implements StaticJava
        disable AuditIndex
		enable explicit context override			

    Persistent Fields
    	DetailCompany				is a CompanyIsolated 		
    		classic name is DTL-COMPANY
    	CurrencyTable											
        BankExpenseAccount          is a TransactionCodeBlock 	
            classic name for BankExpenseAccount.ToAccountingEntity is DTL-COMPANY
            classic name for BankExpenseAccount.AccountingUnit is BEXP-ACCT-UNIT
            classic name for BankExpenseAccount.GeneralLedgerChartAccount is BEXP-ACCOUNT

        DetailBankAmount            is a CurrencyAmount 
            classic name is DTL-TRAN-AMT
        DetailBaseAmount            is a FinanceCurrencyAmount  
            classic name is DTL-BASE-AMT
            
        HeaderAmountData			is a FinanceCurrencyAmountGroup

        	sql prefix is  HBANK 
        	classic name for HeaderAmountData.CurrencyAmount is HDR-TRAN-AMT

        	sql prefix is  HBASE	
        	classic name for HeaderAmountData.FunctionalAmount.EnteredCurrencyAmount is HDR-BASE-AMT





        BankExpenseData				is a FinanceCurrencyAmountGroup  

			classic name for BankExpenseData.CurrencyAmount is BEXP-TRAN-AMT

        	classic name for BankExpenseData.FunctionalAmount.EnteredCurrencyAmount is BEXP-BASE-AMT
        	
        BankTransactionCode
            classic name is BANK-INST-CODE
            


        JournalBook
        	context of DetailCompany.OrigCompany
            classic name is JRNL-BOOK-NBR
            restricted
            
        DetailCompanyCashCode     is a CompanyCashCode
        	context of DetailCompany.OrigCompany
        	context of CashLedgerFundTransferDetail.DetailCashCode

        DetailAccountingUnit        is like AccountingUnit
            default label is "<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>"
    	DetailFund					is a FinanceDimension1				
			
	Derived Fields
		DerivedTransactionIdentifier is a DerivedField
			type is like CashLedgerTransactionIdentifier
			return DtlCashLedgerTransactionRel.CashLedgerTransactionIdentifier

        DerivedTransientAccountingUnitLabel is a LabelField              
            restricted
            "_<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>"

        DerivedDetailFund is a LabelField                                
            restricted
            "_<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>"
			
		FromMessage is a LabelField
			restricted
			"From"

		ToMessage is a LabelField
			restricted
			"To"                  

		DerivedDetailTransactionTypeLabel   is a DerivedField
			type is Alpha 13
            if (CashLedgerFundTransferHeader.TransferType.MultipleDeposits)
                return ToMessage
            if (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)
                return FromMessage
                			
		DerivedHeaderTransactionTypeLabel   is a DerivedField
			type is Alpha 13
            if (CashLedgerFundTransferHeader.TransferType.MultipleDeposits)
                return FromMessage
            if (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)
                return ToMessage

		DerivedTotalTransferAmount is a DerivedField
			type is like CurrencyAmount
			return (DetailBankAmount + BankExpenseData.CurrencyAmount)

	Transient Fields
		TransientAccountingEntity			is an AccountingEntity
			derive value from DetailCompany.OrigCompany.AccountingEntity
		TransientAccountingUnit			    is an AccountingUnit
			default label is "<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>"
			derive value from DetailAccountingUnit
		TransientFromCurrency				is a FromCurrency
			derive value from CashLedgerFundTransferDetail.DetailCashCode.Currency
		IsCurrencyExchangeOverride			is Boolean
		DetailBankAmountUnsigned			is an UnsignedInternationalAmount
			derive value from CashLedgerFundTransferDetail.DetailBankAmount		
		BankExpenseAmountUnsigned			is an UnsignedInternationalAmount
			derive value from CashLedgerFundTransferDetail.BankExpenseData.CurrencyAmount		
		HeaderTranAmount					is an UnsignedInternationalAmount
			derive value from HeaderAmountData.CurrencyAmount
		HeaderTranAmountDisplayOnly			is an UnsignedInternationalAmount
			derive value from HeaderAmountData.CurrencyAmount			
										
    Local Fields
		CashLedgerSourceRecord
        NewCashLedgerTransaction    		is a  CashLedgerTransaction view
        SavedCashLedgerTransaction  		is a  CashLedgerTransaction
        LocalExchangeDate					is an ExchangeDate
    	LocalDocumentCurrencyTotals 		is a  DocumentCurrencyTotals
    	LocalFinanceCurrencyAmount			is a  FinanceCurrencyAmount
    	LocalFinanceCurrencyAmountGroup		is a  FinanceCurrencyAmountGroup		
		LocalCurrencyExchangeGroup			is a  CurrencyExchangeGroup
		LocalHeaderBaseAmount				is like UnsignedInternationalAmount
		LocalToAccountingEntity				is like AccountingEntity
		LocalAccountingUnit					is like AccountingUnit
		LocalFinanceDimension1				is a FinanceDimension1
		
    Conditions
    	RecordExists
    		restricted
    		when (CashLedgerFundTransferDetail exists)
    	
    	CashLedgerTransactionIdentifierExists
			restricted
			when (DerivedTransactionIdentifier entered)
					
        IsManyDeposits
        	restricted
            classic name is CBTRFDTL-DEP
            when (CashLedgerFundTransferHeader.TransferType.MultipleDeposits)

        IsManyWithdrawals
        	restricted
            classic name is CBTRFDTL-WTH
            when (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)

        IsNonBaseDeposit
        	restricted
            classic name is DEP-CPY-CURR
            when (CashLedgerFundTransferHeader.TransferType.MultipleDeposits
            and   (DetailBankAmount != DetailBaseAmount.FunctionalAmount.EnteredCurrencyAmount
            or     HeaderAmountData.CurrencyAmount != HeaderAmountData.FunctionalAmount.EnteredCurrencyAmount))

        IsMultipleDepositDetail
        	restricted
            classic name is DTL-DEP-CURR
            when (CashLedgerFundTransferHeader.TransferType.MultipleDeposits
            and   DetailBankAmount != HeaderAmountData.CurrencyAmount)

        IsMultipleWithdrawlDetail
        	restricted
            classic name is DTL-WTH-CURR
            when (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals
            and   DetailBankAmount != HeaderAmountData.CurrencyAmount)

        IsNonBaseWithdrawl
        	restricted
            classic name is WTH-CPY-CURR
            when (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals
            and   (DetailBankAmount != DetailBaseAmount.FunctionalAmount
			or     HeaderAmountData.CurrencyAmount != HeaderAmountData.FunctionalAmount.EnteredCurrencyAmount))



			



		IsDtlCashLedgerTransactionExist
			restricted
			when (DtlCashLedgerTransactionRel exists)

        IsDtlCashLedgerCashReceiptHeaderExist
        	restricted
            when (DtlCashLedgerCashReceiptHeaderRel exists)
            
        IsCurrencyExchangeOverrideAllowed
        	restricted
        	when (CashCode.Currency != CashLedgerFundTransferDetail.DetailCashCode.Currency)

        IsValidDepositCreate
        	restricted
            when (CashLedgerFundTransferHeader.IsUnreleased
            and   !CashLedgerFundTransferHeader.BudgetEditInProgress            
            and   CashLedgerFundTransferHeader.TransferType.MultipleDeposits)

        IsValidWithdrawalCreate
        	restricted
        	when (CashLedgerFundTransferHeader.IsUnreleased
            and   !CashLedgerFundTransferHeader.BudgetEditInProgress            
            and   CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)

		GLCommitRelExists
			restricted
			when (GLCommitRel exists)

		ShowDetailAccountingUnit
            when (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting
			and   CashManagementGroup.FinanceEnterpriseGroup.FundDimension.AccountingUnit)
		ShowDetailFund
			when (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting
			and   CashManagementGroup.FinanceEnterpriseGroup.FundDimension.Dimension1)

    Relations
        HeaderCashCodeBankTransferRel
            classic name is CBTRFTABLE
            one-to-one relation to CashCodeBankTransfer
            required
            Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode				= CashCode

        FinancialInstitutionBranchRel
            classic name is CBBANKENT-DTL
            one-to-one relation to FinancialInstitutionBranch
            required
            Field Mapping uses symbolic key
            	related.CashManagementGroup			= CashManagementGroup	
                related.FinancialInstitution		= CashLedgerFundTransferDetail.DetailCashCode.FinancialInstitution
                related.FinancialInstitutionBranch 	= CashLedgerFundTransferDetail.DetailCashCode.FinancialInstitutionBranch

        HeaderFinancialInstitutionBranchRel
            classic name is CBBANKENT-HDR
            one-to-one relation to FinancialInstitution
            required
            Field Mapping uses symbolic key
            	related.CashManagementGroup			= CashManagementGroup	
            	related.FinancialInstitution		= CashCode.FinancialInstitution

        GeneralLedgerCompanyRel
            classic name is GLSYSTEM-HDR
            one-to-one relation to GeneralLedgerCompany
            required
            Field Mapping uses symbolic key
                related.Company = CashLedgerFundTransferHeader.Company

        DtlCashLedgerTransactionRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set6
            	related.CashManagementGroup				= CashManagementGroup	
        		related.CashCode						= CashLedgerFundTransferDetail.DetailCashCode
				related.BankTransactionCode 			= BankTransactionCode
				related.TransactionNumber				= CashLedgerFundTransferHeader

        DtlCashCodeBankTransferRel
            one-to-one relation to CashCodeBankTransfer
            required
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode				= CashLedgerFundTransferDetail.DetailCashCode

        DtlCashLedgerCashReceiptHeaderRel
            one-to-one relation to CashLedgerCashReceiptHeader
            Field Mapping uses symbolic key
            	related.CashManagementGroup 								= CashManagementGroup	
                related.CashCode                           				    = CashLedgerFundTransferDetail.DetailCashCode
                related.CashLedgerCashReceiptHeader.BankTransactionCode     = BankTransactionCode
                related.CashLedgerCashReceiptHeader.TransactionNumber       = CashLedgerFundTransferHeader

        DtlBankTransactionCodeRel
            one-to-one relation to BankTransactionCode
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.BankTransactionCode     = BankTransactionCode

        CashLedgerFundTransferHeaderRel
            one-to-one relation to CashLedgerFundTransferHeader
            Field Mapping uses symbolic key
            	related.CashManagementGroup 		 = CashManagementGroup	
                related.CashCode       				 = CashCode
                related.CashLedgerFundTransferHeader = CashLedgerFundTransferHeader

		GeneralLedgerSystemCodeRel		
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= CashManagementGroup
				related.GeneralLedgerSystemCode	= "CB"	

		GLCommitRel		
			one-to-many relation to GLCommit
			Field Mapping uses BySystemAndHeaderUniqueID	
				related.FinanceEnterpriseGroup	= CashManagementGroup
				related.HeaderUniqueID	= UniqueID



		CashCodeFundRel
			one-to-one relation to CashCodeFund
			Field Mapping uses ByAccountingEntityFund
				related.CashManagementGroup		= CashManagementGroup
				related.CashCode				= CashLedgerFundTransferDetail.DetailCashCode
				related.AccountingEntity		= LocalToAccountingEntity
		 		related.AccountingUnit			= LocalAccountingUnit
				related.FinanceDimension1		= LocalFinanceDimension1

		CompanySystemClosingControlRel
			one-to-one relation to CompanySystemClosingControl
			Field Mapping uses BySystemCode
				related.GeneralLedgerSystemCode		= "CB"
				related.Company						= DetailCompany

    Sets
        Set3
            indexed
            Sort Order
                CashLedgerFundTransferDetail
                CashLedgerFundTransferHeader
                CashCode

                
    Rule Blocks





		ReverseBaseAmounts
			LocalFinanceCurrencyAmountGroup.FunctionalAmount.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.AlternateAmount.EnteredCurrencyAmount		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.AlternateAmount2.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.AlternateAmount3.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ToFunctionalAmount.EnteredCurrencyAmount 	*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount.EnteredCurrencyAmount		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount2.EnteredCurrencyAmount 	*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount3.EnteredCurrencyAmount 	*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ProjectAmount.EnteredCurrencyAmount 	 	*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount1.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount2.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount3.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount4.EnteredCurrencyAmount 		*= DistMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount5.EnteredCurrencyAmount 		*= DistMultiplier

		ReverseOffsetBaseAmounts
			LocalFinanceCurrencyAmountGroup.FunctionalAmount.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.AlternateAmount.EnteredCurrencyAmount		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.AlternateAmount2.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.AlternateAmount3.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ToFunctionalAmount.EnteredCurrencyAmount	*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount.EnteredCurrencyAmount		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount2.EnteredCurrencyAmount 	*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount3.EnteredCurrencyAmount 	*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ProjectAmount.EnteredCurrencyAmount 	 	*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount1.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount2.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount3.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount4.EnteredCurrencyAmount 		*= OffsetMultiplier
			LocalFinanceCurrencyAmountGroup.ReportAmount5.EnteredCurrencyAmount 		*= OffsetMultiplier

		ReverseBankExpenseBaseDistributionAmounts
			LocalFinanceCurrencyAmountGroup.FunctionalAmount.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.AlternateAmount.EnteredCurrencyAmount	 *= -1
			LocalFinanceCurrencyAmountGroup.AlternateAmount2.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.AlternateAmount3.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.ToFunctionalAmount.EnteredCurrencyAmount *= -1
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount.EnteredCurrencyAmount	 *= -1
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount2.EnteredCurrencyAmount *= -1
			LocalFinanceCurrencyAmountGroup.ToAlternateAmount3.EnteredCurrencyAmount *= -1
			LocalFinanceCurrencyAmountGroup.ProjectAmount.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.ReportAmount1.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.ReportAmount2.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.ReportAmount3.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.ReportAmount4.EnteredCurrencyAmount 	 *= -1
			LocalFinanceCurrencyAmountGroup.ReportAmount5.EnteredCurrencyAmount 	 *= -1

		SetExchangeDate
			LocalExchangeDate 				= CashLedgerFundTransferHeader.TransferDate

        GetBankTransactionCode
        	if (CashLedgerFundTransferDetail.DetailCashCode entered)
            	constraint (DtlCashCodeBankTransferRel exists)
            		"BankFundTransferInformationNotDefinedOnCashCode"     
	            if (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)
	                BankTransactionCode = DtlCashCodeBankTransferRel.FromBankTransactionCode
	            else
	                BankTransactionCode = DtlCashCodeBankTransferRel.ToBankTransactionCode












		PerformFundAmountEdit

			LocalToAccountingEntity			= TransientAccountingEntity
			if (DetailAccountingUnit entered)
				LocalAccountingUnit			= DetailAccountingUnit
				if (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals			
				and CashCode.EditAvailableFunds)
					constraint (DerivedTotalTransferAmount <= CashCodeFundRel.EditAvailableAmount)
						"TotalTransferAmount<DerivedTotalTransferAmount>ExceedCashFundAmountForCashCode<CashLedgerFundTransferDetail.DetailCashCode>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<LocalToAccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>_<LocalAccountingUnit>"
				else
					constraint (CashCodeFundRel exists)
						"CashCode<CashLedgerFundTransferDetail.DetailCashCode>IsRestricted:<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<LocalToAccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>_<LocalAccountingUnit>IsNotOnRestrictedList"
			if (DetailFund entered)
				LocalFinanceDimension1		= DetailFund
				if (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals
				and CashCode.EditAvailableFunds)
					constraint (DerivedTotalTransferAmount <= CashCodeFundRel.EditAvailableAmount)
						"TotalTransferAmount<DerivedTotalTransferAmount>ExceedCashFundAmountForCashCode<CashLedgerFundTransferDetail.DetailCashCode>And<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<LocalToAccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>_<LocalFinanceDimension1>"
				else
					constraint (CashCodeFundRel exists)
						"CashCode<CashLedgerFundTransferDetail.DetailCashCode>IsRestricted:<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>_<LocalToAccountingEntity>And<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>_<LocalFinanceDimension1>IsNotOnRestrictedList"



					                    				
                    				                		
    Field Rules
		DetailCompany
			default to CashLedgerFundTransferHeader.Company
			constraint (DetailCompanyCashCode.TransferClearingAccount entered)
				"MustDefineATransferClearingAccountInCompanyCashCode"      //MustSelectClearingAccountIn_\C\B00.3"  
			if (DtlBankTransactionCodeRel.AutomaticReconciliation
			and DetailCompanyCashCode.ReconcileGeneralLedgerEntry)
				constraint (DetailCompanyCashCode.ReconciliationAccount entered)
					"MustDefineAReconciliationAccountInCompanyCashCode" 
			else
				constraint (DetailCompanyCashCode.CashAccount entered)
					"MustDefineACashAccountInCompanyCashCode" 
			
		CurrencyTable	
			default to CompanySystemClosingControlRel.CurrencyTable
			default to DetailCompany.OrigCompany.CurrencyTable

        	if (CurrencyTable changed)
			    cannot be changed	
			    	"CannotChangeCurrencyTable;DeleteAndRe-\Add"	
			    							    						
        BankExpenseAccount
            if (BankExpenseAmountUnsigned entered)
            	TransientAccountingEntity	= DetailCompany.OrigCompany.AccountingEntity
                default to DetailCompanyCashCode.BankExpenseAccount
                    default individual fields

				if (TransientAccountingEntity.ValidSystems entered)
					constraint (GeneralLedgerSystemCode within TransientAccountingEntity.ValidSystems)
						"InvalidSystemFor<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>"
				if (BankExpenseAccount.ToAccountingEntity.ValidSystems entered)
					constraint (GeneralLedgerSystemCode within BankExpenseAccount.ToAccountingEntity.ValidSystems)
						"InvalidSystemForPosting<CashManagementGroup.FinanceEnterpriseGroup.AccountingEntityLabel>"
				if (BankExpenseAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
					constraint (GeneralLedgerSystemCode within BankExpenseAccount.GeneralLedgerChartAccount.Account.SystemRestriction)
						"InvalidSystemForAccount"
                required 
			
        DetailBankAmount	
        	DetailBankAmount 			= DetailBankAmountUnsigned
        	TransientAccountingEntity	= DetailCompany.OrigCompany.AccountingEntity  
			TransientFromCurrency 		= CashLedgerFundTransferDetail.DetailCashCode.Currency	
        	if (DetailBankAmount changed)
	       		initialize DetailBaseAmount
	       		initialize HeaderAmountData
	        required
	        
		DetailBaseAmount	
			required

		HeaderAmountData	
			initialize LocalCurrencyExchangeGroup
			TransientAccountingEntity										= CashLedgerFundTransferHeader.Company.OrigCompany.AccountingEntity	


			if (CashLedgerFundTransferDetail.DetailCashCode.Currency 		= CashLedgerFundTransferHeader.CashCode.Currency)
				HeaderTranAmount											= DetailBankAmount  
			else	
				if (HeaderTranAmount not entered)

					LocalCurrencyExchangeGroup.TransactionAmount			= DetailBankAmount	
					TransientFromCurrency 									= CashLedgerFundTransferDetail.DetailCashCode.Currency
					LocalCurrencyExchangeGroup.BaseAmount.ToCurrency 		= CashLedgerFundTransferHeader.CashCode.Currency
					HeaderTranAmount				 						= LocalCurrencyExchangeGroup.BaseAmount.OutputCurrencyAmount


			initialize LocalCurrencyExchangeGroup
			initialize LocalHeaderBaseAmount
			if (CashLedgerFundTransferHeader.Company.OrigCompany.Currency 	= CashLedgerFundTransferDetail.DetailCashCode.Currency)

				LocalCurrencyExchangeGroup.TransactionAmount				= DetailBankAmount 
				TransientFromCurrency										= CashLedgerFundTransferDetail.DetailCashCode.Currency
				LocalCurrencyExchangeGroup.BaseAmount.ToCurrency			= CashLedgerFundTransferHeader.Company.OrigCompany.Currency
				LocalHeaderBaseAmount										= LocalCurrencyExchangeGroup.BaseAmount.OutputCurrencyAmount  
			else
				if (CashLedgerFundTransferHeader.Company.OrigCompany.Currency = CashLedgerFundTransferHeader.CashCode.Currency)

					LocalCurrencyExchangeGroup.TransactionAmount			= HeaderTranAmount 
					TransientFromCurrency									= CashLedgerFundTransferHeader.CashCode.Currency
					LocalCurrencyExchangeGroup.BaseAmount.ToCurrency		= CashLedgerFundTransferHeader.Company.OrigCompany.Currency
					LocalHeaderBaseAmount									= LocalCurrencyExchangeGroup.BaseAmount.OutputCurrencyAmount
				else

					LocalCurrencyExchangeGroup.TransactionAmount			= HeaderTranAmount  
					TransientFromCurrency									= CashLedgerFundTransferHeader.CashCode.Currency
					LocalCurrencyExchangeGroup.BaseAmount.ToCurrency		= CashLedgerFundTransferHeader.Company.OrigCompany.Currency
					LocalHeaderBaseAmount				 					= LocalCurrencyExchangeGroup.BaseAmount.OutputCurrencyAmount
		

			initialize LocalCurrencyExchangeGroup 
			initialize HeaderAmountData
			TransientFromCurrency									= CashLedgerFundTransferHeader.CashCode.Currency
			TransientAccountingEntity								= CashLedgerFundTransferHeader.Company.OrigCompany.AccountingEntity
			HeaderAmountData.ExchangeDate 							= LocalExchangeDate
			HeaderAmountData.CurrencyAmount							= HeaderTranAmount  
			HeaderAmountData.FunctionalAmount.EnteredCurrencyAmount	= LocalHeaderBaseAmount 
			required
								        	
 		BankExpenseData	
            if (BankExpenseAmountUnsigned entered)
                constraint (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)
                    "CannotSetBankExpenseAmount"
                constraint (DtlCashCodeBankTransferRel.ExpenseBankTransactionCode entered)
                    "ExpenseBankTransactionCodeNotDefinedOnCashCode"    
				TransientFromCurrency				= CashLedgerFundTransferDetail.DetailCashCode.Currency
				TransientAccountingEntity			= DetailCompany.OrigCompany.AccountingEntity	
				BankExpenseData.ExchangeDate		= LocalExchangeDate
				BankExpenseData.CurrencyAmount 		= BankExpenseAmountUnsigned
        		required
        		
        BankTransactionCode
            if (!CashLedgerFundTransferHeader.AutoNumberingAllowed)
                constraint (!IsDtlCashLedgerTransactionExist)
                    "BankTransactionAlreadyExists"                 
                constraint (!IsDtlCashLedgerCashReceiptHeaderExist)
                    "TransferNumberAlreadyInUseAs_\Cash_\Receipt_\Deposit" 
                required


        DetailCompanyCashCode
            required

        DetailAccountingUnit
			if (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.AccountingUnit)
                if (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting)
                    required
                if (CashLedgerFundTransferHeader.CashCode = CashLedgerFundTransferDetail.DetailCashCode)
                    constraint (CashLedgerFundTransferHeader.HeaderAccountingUnit != TransientAccountingUnit)
                        "<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>OnDetailLineCannotBeTheSameAs<CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel>OnHeaderWhenTransferingToSameCashCode"
			DetailAccountingUnit = TransientAccountingUnit

		DetailFund
			if (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.Dimension1)
                if (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting)
                    required
                if (CashLedgerFundTransferHeader.CashCode = CashLedgerFundTransferDetail.DetailCashCode)
                    constraint (CashLedgerFundTransferHeader.HeaderFund != DetailFund)
                        "<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>OnDetailLineCannotBeTheSameAs<CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label>OnHeaderWhenTransferingToSameCashCode"

	Actions

        CreateBankFundTransferDepositsDetail is a Create Action
        	valid when (IsValidDepositCreate)
            Entrance Rules
               	include SetExchangeDate
               	include GetBankTransactionCode
			Action Rules
			Exit Rules
				invoke UpdateHeaderAmounts CashLedgerFundTransferHeaderRel
				
        CreateBankFundTransferWithdrawalsDetail is a Create Action
        	valid when (IsValidWithdrawalCreate)
            Entrance Rules
                include SetExchangeDate
                include GetBankTransactionCode
			Action Rules
			Exit Rules
				invoke UpdateHeaderAmounts CashLedgerFundTransferHeaderRel
					
		Update is an Update Action
	        valid when (CashLedgerFundTransferHeader.IsUnreleased)
			Entrance Rules
				include SetExchangeDate
				include GetBankTransactionCode
                constraint (!CashLedgerFundTransferHeader.BudgetEditInProgress)		
                    "CannotUpdate;BudgetEditInProgress"
            Exit Rules
				invoke UpdateHeaderAmounts CashLedgerFundTransferHeaderRel
	
		Delete is a Delete Action		
            valid when (CashLedgerFundTransferHeader.IsUnreleased)
            Entrance Rules
                constraint (!CashLedgerFundTransferHeader.BudgetEditInProgress)		
                    "CannotDelete;BudgetEditInProgress"
			Action Rules
				invoke UpdateHeaderAmounts CashLedgerFundTransferHeaderRel	
				
		Purge is a Purge Action
			restricted

        Release is an Instance Action
            default label is untranslatable
            restricted
            Local Fields
                DistMultiplier is Numeric size 1
                OffsetMultiplier is Numeric size 1
            Entrance Rules
                if (CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)
                    DistMultiplier = 1
                    OffsetMultiplier = -1
                else
                    DistMultiplier = -1
                    OffsetMultiplier = 1

				if (CashLedgerFundTransferDetail.DetailCashCode.EditAvailableFunds
				or  CashLedgerFundTransferDetail.DetailCashCode.RestrictedFunds)
					include PerformFundAmountEdit

            Action Rules        

                invoke Unreleased.CreateFromCashLedgerWithoutEdits CashLedgerTransaction
                    assign result to NewCashLedgerTransaction

					invoked.CashManagementGroup 				= CashLedgerFundTransferDetail.DetailCashCode.CashManagementGroup
                    invoked.CashCode          				 	= CashLedgerFundTransferDetail.DetailCashCode
                    invoked.BankTransactionCode             	= BankTransactionCode
                    invoked.TransactionNumber               	= CashLedgerFundTransferHeader


                    invoked.TransactionNumberSuffix         	= 0
                    invoked.SourceCode                      	= "CB"
                    invoked.Company								= DetailCompany
                    invoked.IssueDate                       	= CashLedgerFundTransferHeader.TransferDate
                    invoked.IssuedBankAmount                	= DetailBankAmount
                    invoked.IssuedBaseAmount					= DetailBaseAmount
                    invoked.TransactionCurrencyNumberOfDecimals = CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                    invoked.BaseCurrencyNumberOfDecimals        = DetailCompany.OrigCompany.CurrencyNumberOfDecimals
                    invoked.Description                     	= CashLedgerFundTransferHeader.Description
                    invoked.Reference                      	 	= "BankFundTransfer"
                    invoked.GeneralLedgerPostDate          		= CashLedgerFundTransferHeader.PostDate
                    invoked.Type                            	= "D"
					invoked.BypassBudgetEditing					= true
					
                SavedCashLedgerTransaction = NewCashLedgerTransaction.CashLedgerTransaction

                LocalFinanceCurrencyAmountGroup = HeaderAmountData
				include ReverseBaseAmounts
                invoke Create CashLedgerGLDistribution
                    invoked.CashLedgerGLDistribution.TransactionIDNumber 	= SavedCashLedgerTransaction
                	invoked.CashManagementGroup 							= CashLedgerFundTransferDetail.DetailCashCode.CashManagementGroup
                    invoked.CashCode                          				= CashLedgerFundTransferDetail.DetailCashCode
                    invoked.CashLedgerSourceRecord							= "CBT"
                    invoked.CashLedgerGLDistribution.BankTransactionCode	= BankTransactionCode
                    invoked.TransactionNumber                               = CashLedgerFundTransferHeader
                    invoked.TransactionNumberSuffix                         = 0
                    invoked.GeneralLedgerSourceCode                         = "CL"
                    invoked.Company                                         = CashLedgerFundTransferHeader.Company
                    invoked.CashLedgerGLDistribution.DistributionType       = "E"
                    invoked.Status                                          = 1  
                    invoked.RecordType									 	= ""
                    invoked.PostDate                                        = CashLedgerFundTransferHeader.TransferDate
					invoked.GLFinanceCodeBlock		                        = CashLedgerFundTransferHeader.CompanyCashCode.TransferClearingAccount	
                    if (CashLedgerFundTransferHeader.HeaderAccountingUnit entered)
                        invoked.GLFinanceCodeBlock.AccountingUnit           = CashLedgerFundTransferHeader.HeaderAccountingUnit
					if (CashLedgerFundTransferHeader.HeaderFund entered)
                    	invoked.GLFinanceCodeBlock.FinanceDimension1		= CashLedgerFundTransferHeader.HeaderFund
                    invoked.GLTransactionAmount                      		= DetailBankAmount *DistMultiplier
                    invoked.DistributionToBankAmount                    	= DetailBankAmount *DistMultiplier
                    invoked.GLBaseAmount 									= LocalFinanceCurrencyAmountGroup
                    invoked.Reference                                   	= "BankFundTransfer"
                    invoked.Description                                 	= CashLedgerFundTransferHeader.Description
                    invoked.TransactionCurrencyNumberOfDecimals         	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                    invoked.ToTransactionCurrencyNumberOfDecimals       	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                    invoked.BaseCurrencyNumberOfDecimals                	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
                    invoked.ToBaseCurrencyNumberOfDecimals              	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
					invoked.TransferHeaderCashCode            				= CashLedgerFundTransferHeader.CashCode
					invoked.BypassBudgetAndCommitmentProcessing 			= true
					invoked.BypassStatusEdit								= true
					
                LocalFinanceCurrencyAmountGroup = HeaderAmountData
				include ReverseOffsetBaseAmounts                
                invoke Create CashLedgerGLDistribution
                    invoked.CashLedgerGLDistribution.TransactionIDNumber 	= SavedCashLedgerTransaction
                    invoked.CashManagementGroup 							= CashLedgerFundTransferDetail.DetailCashCode.CashManagementGroup
                    invoked.CashCode                           				= CashLedgerFundTransferDetail.DetailCashCode
                    invoked.CashLedgerSourceRecord							= "CBT"
                    invoked.CashLedgerGLDistribution.BankTransactionCode	= BankTransactionCode
                    invoked.TransactionNumber                               = CashLedgerFundTransferHeader
                    invoked.TransactionNumberSuffix                         = 0
                    invoked.GeneralLedgerSourceCode                         = "CL"
                    invoked.Company                                         = CashLedgerFundTransferHeader.Company
                    invoked.CashLedgerGLDistribution.DistributionType       = "E"
                    invoked.RecordType                                      = "C"
                    invoked.Status                                          = 1  
                    invoked.PostDate                                        = CashLedgerFundTransferHeader.TransferDate
                    if (DtlBankTransactionCodeRel.AutomaticReconciliation
                    and DetailCompanyCashCode.ReconcileGeneralLedgerEntry)
                        invoked.GLFinanceCodeBlock                          = CashLedgerFundTransferDetail.DetailCompanyCashCode.ReconciliationAccount
                    else
                        invoked.GLFinanceCodeBlock                          = CashLedgerFundTransferDetail.DetailCompanyCashCode.CashAccount
                    if (DetailAccountingUnit entered)
                        invoked.GLFinanceCodeBlock.AccountingUnit           = DetailAccountingUnit
                    if (DetailFund entered)
                        invoked.GLFinanceCodeBlock.FinanceDimension1        = DetailFund
                    invoked.GLTransactionAmount                      		= DetailBankAmount *OffsetMultiplier
                    invoked.DistributionToBankAmount                    	= DetailBankAmount *OffsetMultiplier
                    invoked.GLBaseAmount 									= LocalFinanceCurrencyAmountGroup 
                    invoked.Reference                                   	= "BankFundTransfer"
                    invoked.Description                                 	= CashLedgerFundTransferHeader.Description
                    invoked.TransactionCurrencyNumberOfDecimals         	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                    invoked.ToTransactionCurrencyNumberOfDecimals       	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                    invoked.BaseCurrencyNumberOfDecimals                	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
                    invoked.ToBaseCurrencyNumberOfDecimals              	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
					invoked.TransferHeaderCashCode            				= CashLedgerFundTransferHeader.CashCode
					invoked.BypassStatusEdit								= true
					
                if (BankExpenseData.CurrencyAmount entered
                and CashLedgerFundTransferHeader.TransferType.MultipleWithdrawals)
                    invoke Unreleased.CreateFromCashLedgerWithoutEdits CashLedgerTransaction
                        assign result to NewCashLedgerTransaction
						fill in fields from CashLedgerFundTransferHeader
						invoked.CashManagementGroup 				= CashLedgerFundTransferDetail.DetailCashCode.CashManagementGroup 
                        invoked.CashCode           					= CashLedgerFundTransferDetail.DetailCashCode
                        invoked.BankTransactionCode             	= HeaderCashCodeBankTransferRel.ExpenseBankTransactionCode
                        invoked.TransactionNumber               	= CashLedgerFundTransferHeader


                        invoked.TransactionNumberSuffix         	= 0
                        invoked.SourceCode                      	= "CB"
                        invoked.Company 					    	= DetailCompany
                        invoked.IssueDate                       	= CashLedgerFundTransferHeader.TransferDate
                        invoked.IssuedBankAmount                	= BankExpenseData.CurrencyAmount
                        invoked.IssuedBaseAmount					= BankExpenseData
                        invoked.TransactionCurrencyNumberOfDecimals = CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                        invoked.BaseCurrencyNumberOfDecimals        = CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
                        invoked.Description                     	= CashLedgerFundTransferHeader.Description
                        invoked.Reference                       	= "BankFundTransfer"
                        invoked.GeneralLedgerPostDate           	= CashLedgerFundTransferHeader.PostDate
						invoked.BypassBudgetEditing					= true
						
                    SavedCashLedgerTransaction = NewCashLedgerTransaction.CashLedgerTransaction

                    invoke Create CashLedgerGLDistribution
                        invoked.CashLedgerGLDistribution.TransactionIDNumber 	= SavedCashLedgerTransaction
                        invoked.CashManagementGroup 							= CashLedgerFundTransferDetail.DetailCashCode.CashManagementGroup
                        invoked.CashCode                          				= CashLedgerFundTransferDetail.DetailCashCode
                   		invoked.CashLedgerSourceRecord							= "CBT"
                        invoked.CashLedgerGLDistribution.BankTransactionCode	= DtlCashCodeBankTransferRel.ExpenseBankTransactionCode
                        invoked.TransactionNumber                               = CashLedgerFundTransferHeader
                        invoked.TransactionNumberSuffix                         = 0
                        invoked.GeneralLedgerSourceCode                         = "CL"
                        invoked.Company                                         = CashLedgerFundTransferHeader.Company
                        invoked.CashLedgerGLDistribution.DistributionType       = "E"
                        invoked.RecordType									 	= ""
                        invoked.Status                                          = 1  
                        invoked.PostDate                                        = CashLedgerFundTransferHeader.PostDate
                        invoked.GLFinanceCodeBlock                              = BankExpenseAccount
                        if (DetailAccountingUnit entered)
                            invoked.GLFinanceCodeBlock.AccountingUnit           = DetailAccountingUnit
						if (DetailFund entered)
	                    	invoked.GLFinanceCodeBlock.FinanceDimension1		= DetailFund
                        invoked.GLTransactionAmount                      		= BankExpenseData.CurrencyAmount
                        invoked.DistributionToBankAmount                    	= BankExpenseData.CurrencyAmount
                        invoked.GLBaseAmount 									= BankExpenseData
                        invoked.Reference                                   	= "BankFundTransfer"
                        invoked.Description                                 	= CashLedgerFundTransferHeader.Description
                        invoked.TransactionCurrencyNumberOfDecimals         	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                        invoked.ToTransactionCurrencyNumberOfDecimals       	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                        invoked.BaseCurrencyNumberOfDecimals                	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
                        invoked.ToBaseCurrencyNumberOfDecimals              	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
						invoked.TransferHeaderCashCode            				= CashLedgerFundTransferHeader.CashCode
						invoked.BypassBudgetEditing								= true
						invoked.BypassStatusEdit								= true
						
					LocalFinanceCurrencyAmountGroup	= BankExpenseData
					include ReverseBankExpenseBaseDistributionAmounts	
                    invoke Create CashLedgerGLDistribution	
                        invoked.CashLedgerGLDistribution.TransactionIDNumber 	= SavedCashLedgerTransaction
                        invoked.CashManagementGroup 							= CashLedgerFundTransferDetail.DetailCashCode.CashManagementGroup
                        invoked.CashCode                           				= CashLedgerFundTransferDetail.DetailCashCode
                    	invoked.CashLedgerSourceRecord							= "CBT"
                        invoked.CashLedgerGLDistribution.BankTransactionCode    = DtlCashCodeBankTransferRel.ExpenseBankTransactionCode
                        invoked.TransactionNumber                               = CashLedgerFundTransferHeader
                        invoked.TransactionNumberSuffix                         = 0
                        invoked.GeneralLedgerSourceCode                         = "CL"
                        invoked.Company                                         = CashLedgerFundTransferHeader.Company
                        invoked.CashLedgerGLDistribution.DistributionType       = "E"
                        invoked.RecordType                                      = "C"
                        invoked.Status                                          = 1  
                        invoked.PostDate                                        = CashLedgerFundTransferHeader.PostDate
                        if (DtlBankTransactionCodeRel.AutomaticReconciliation
                        and DetailCompanyCashCode.ReconcileGeneralLedgerEntry)
                            invoked.GLFinanceCodeBlock                          = CashLedgerFundTransferDetail.DetailCompanyCashCode.ReconciliationAccount
                        else
                            invoked.GLFinanceCodeBlock                          = CashLedgerFundTransferDetail.DetailCompanyCashCode.CashAccount
                        if (DetailAccountingUnit entered)
                            invoked.GLFinanceCodeBlock.AccountingUnit           = DetailAccountingUnit
                        if (DetailFund entered)
                            invoked.GLFinanceCodeBlock.FinanceDimension1	    = DetailFund
                        invoked.GLTransactionAmount                      		= BankExpenseData.CurrencyAmount *-1
                        invoked.DistributionToBankAmount                    	= BankExpenseData.CurrencyAmount *-1
                        invoked.GLBaseAmount 									= LocalFinanceCurrencyAmountGroup
                        invoked.Reference                                   	= "BankFundTransfer"
                        invoked.Description                                 	= CashLedgerFundTransferHeader.Description
                        invoked.TransactionCurrencyNumberOfDecimals         	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                        invoked.ToTransactionCurrencyNumberOfDecimals       	= CashLedgerFundTransferDetail.DetailCashCode.BankAccountNumber.Currency.NumberOfDecimals
                        invoked.BaseCurrencyNumberOfDecimals                	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
                        invoked.ToBaseCurrencyNumberOfDecimals              	= CashLedgerFundTransferHeader.Company.OrigCompany.CurrencyNumberOfDecimals
						invoked.TransferHeaderCashCode             				= CashLedgerFundTransferHeader.CashCode
						invoked.BypassStatusEdit								= true



        UpdateHeaderNumber is an Instance Action
            default label is untranslatable
            restricted
            Parameters
                PrmHeaderNumber is AlphaUpper 22
            Parameter Rules
                PrmHeaderNumber
                    required
            Action Rules
                CashLedgerFundTransferHeader = PrmHeaderNumber
