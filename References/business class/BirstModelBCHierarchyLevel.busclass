BirstModelBCHierarchyLevel is a BusinessClass
    owned by la
    prefix is BMBHL

    Ontology
        symbolic key is BirstModelBCHierarchyLevel

    Patterns
    	disable AsOfDateProcessing
        implements Resequence on OrderNumber
            new sequence field is NewOrderNumber
            set is ByOrderNumber

    Persistent Fields
        BusinessClass           is a snapshot of RepSetBC.BusinessClass
        OrderNumber             is Numeric size 4
            default label is "SortOrder"        
        PostingField            is Alpha size 300
        ParentField             is Alpha size 300
        NextParentField         is Alpha size 300
        BirstFieldNamePrefix    is Alpha size 30
        BirstFieldNameSuffix    is Alpha size 30
        PostingFieldDataType    is Alpha size 30
        PostingFieldWidth       is Numeric size 4
        HierarchyFieldType      is Numeric size 1
            States
                None                    value is 0
                LevelCode               value is 1
                LevelDescription        value is 2
                LevelCodeAndDescription value is 3
                SequenceNumber          value is 4

    Transient Fields
        NewOrderNumber is Numeric size 4
            default label is "NewSortOrder"

    Local Fields
        LocalLevelNumber is Numeric size 2

    Field Rules
        PostingFieldDataType
            if (PostingField entered)        
                default to SQLPostingFieldDataType

        PostingFieldWidth
            if (PostingField entered)        
                default to SQLPostingFieldWidth
        
        HierarchyFieldType
            initial value is 1

    Derived Fields
        SQLPostingFieldDataType is a NativeField
            type is Alpha size 30
            restricted

        SQLPostingFieldWidth is a NativeField
            type is Alpha size 4
            restricted    

    Actions
        Create is an Action
        Update is an Action            
        Delete is an Action

        AddLevelFields is an Instance Action
            completion message is "Added<TotalCount>Fields"
            Parameters
                AddLevelDetailFields    is Boolean

            Local Fields
                TotalCount          is Numeric size 6
                LocalCreateField    is Boolean
                I1                  is Numeric size 2

            Action Rules
                I1 = 1
                while (I1 <= 10)        
                    LocalLevelNumber = I1
                    LocalCreateField = false

                    if (BirstModelBCHierarchyLevelDetailRel not exists)
                        LocalCreateField = true

                    if (LocalCreateField)
                        invoke Create BirstModelBCHierarchyLevelDetail
                        	invoked.BirstModel                      = BirstModel
                            invoked.ReplicationSet                  = ReplicationSet
                            invoked.RepSetBC                        = RepSetBC
                            invoked.BirstModelBCHierarchyLevel      = BirstModelBCHierarchyLevel
                            invoked.LevelNumber                     = I1
                            
                        TotalCount += 1
                    I1 += 1

    Sets
        ByOrderNumber
            indexed
            Sort Order
                BirstModel
                ReplicationSet
                RepSetBC
                OrderNumber
                BirstModelBCHierarchyLevel

    Relations
        BirstModelBCHierarchyLevelDetailRel
            one-to-many relation to BirstModelBCHierarchyLevelDetail
            Field Mapping uses ByLevelNumber
            	related.BirstModel                        = BirstModel
                related.ReplicationSet                    = ReplicationSet
                related.RepSetBC                          = RepSetBC
                related.BirstModelBCHierarchyLevel        = BirstModelBCHierarchyLevel
                related.LevelNumber                       = LocalLevelNumber
