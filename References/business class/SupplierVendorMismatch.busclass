SupplierVendorMismatch is a BusinessClass
	owned by procurement
	prefix is Supm

	Ontology
		symbolic key is SupplierVendorMismatch
	
	Patterns
	
	Persistent Fields
		Vendor
		
	Conditions
		
		NoDetailExists
			restricted
			when (!SupplierVendorMismatchDetail set exists)
			
		NotSupplierDiversityOnly
			restricted
			when (NotSupplierDiversityOnlyRel exists)
			
		NotVendorDiversityOnly
			restricted
			when (NotVendorDiversityOnlyRel exists)
			
		HasDiversityDate
			restricted
			when (DiversityDateRel exists)
			
		HasDiversityCode
			restricted
			when (DiversityCodeRel exists)
		
		HasCertificationDate
			restricted
			when (CertificationDateRel exists)
			
		HasCertificationCode
			restricted
			when (CertificationCodeRel exists)
			
		HasContacts
			restricted
			when (ContactsRel exists)
	
	Relations
	
		SupplierVendorMismatchRel
			one-to-many relation to SupplierVendorMismatchDetail
			Field Mapping uses ByType
				related.SupplierGroup        = SupplierGroup
				related.Supplier             = Supplier

		NotSupplierDiversityOnlyRel is a SupplierVendorMismatchDetail set	
			Instance Selection
				where (related.VendorValuesToUse)
				
		NotVendorDiversityOnlyRel
			one-to-many relation to SupplierVendorMismatchDetail
			Field Mapping uses ByType
				related.SupplierGroup   	 = SupplierGroup
				related.Supplier			 = Supplier
			Instance Selection
				where (!related.Type.DiversityCodeVendorOnly
				and    !related.Type.DiversityCodeExpirationDateVendor)	
				
		DiversityDateRel is a SupplierVendorMismatchDetail set  	
			Instance Selection
				where (related.Type.DiversityCodeExpirationDateSupplier
				or     related.Type.DiversityCodeExpirationDateVendor
				or     related.Type.DiversityCodeEffectiveDate
				or     related.Type.DiversityCodeReviewDate)
				
		DiversityCodeRel is a SupplierVendorMismatchDetail set  	
			Instance Selection
				where (related.Type.DiversityCodeSupplierOnly
				or     related.Type.DiversityCodeVendorOnly)
		
		CertificationDateRel is a SupplierVendorMismatchDetail set
			Instance Selection
				where (related.Type.CertificationCodeExpDate)
				
		CertificationCodeRel is a SupplierVendorMismatchDetail set
			Instance Selection
				where (related.Type.CertificationCodeSupplierOnly
				or     related.Type.CertificationCodeVendorOnly)
				
		ContactsRel is a SupplierVendorMismatchDetail set
		 	Instance Selection
				where (related.ContactExists)
				
		
					
									   					   
	Actions
	
		Create is a Create Action
			restricted
		
		Delete is a Delete Action
		

		Purge is a Purge Action
			restricted

		DeleteNoDetails is an Instance Action
			restricted
			Action Rules
				if (NoDetailExists)
					invoke Delete
		
		UpdateSupplierFromVendor is an Instance Action
			default label is "UseVendorValues"
			
			Action Rules
			
				invoke UpdateSupplierFromVendorDetail SupplierVendorMismatchRel
		
		UpdateVendorFromSupplier is an Instance Action
			default label is "UseSupplierValues"
			
			Action Rules
				
				invoke UpdateVendorFromSupplierDetail SupplierVendorMismatchRel

		UpdateAllSuppliersWithVendorInformation is a Set Action
			Parameters
				ParmSupplierGroup is a SupplierGroup

			Parameter Rules
				ParmSupplierGroup
					required

			Instance Selection
				where (ParmSupplierGroup = SupplierGroup)

			Action Rules
				Instance Rules
					invoke UpdateSupplierFromVendor

		CompareSupplierAndVendor is an Instance Action
			default label is "CompareSupplier_andVendor"
			Parameters
				ParmSupplierGroup		is a SupplierGroup
				ParmSupplier			is a Supplier
				CompareSupplier			is Boolean
				CompareContacts			is Boolean
				CompareDiversity		is Boolean
				CompareCertification	is Boolean
				SupplierList			is a Supplier group

			Parameter Rules
				ParmSupplierGroup
					required
				ParmSupplier
					if (ParmSupplier entered)
						constraint (SupplierList !entered)
							"CannotEnterBothASupplierAndSupplierList"
						constraint (ParmSupplier.Vendor entered)
							"SupplierMustHaveAVendorToCompareTo"
				CompareSupplier
					initial value is true
				CompareContacts
					initial value is true
				CompareDiversity
					initial value is true
				CompareCertification
					initial value is true
				SupplierList
					if (SupplierList entered)
						constraint (ParmSupplier !entered)
							"CannotEnterBothASupplierAndSupplierList"
											
			Action Rules
				invoke CompareSupplierAndVendor Supplier
					invoked.ParmSupplierGroup 	 = ParmSupplierGroup
					invoked.ParmSupplier		 = ParmSupplier
					invoked.CompareSupplier  	 = CompareSupplier
					invoked.CompareContacts   	 = CompareContacts
					invoked.CompareDiversity  	 = CompareDiversity
					invoked.CompareCertification = CompareCertification	
					invoked.SupplierList         = SupplierList
		
 
			

