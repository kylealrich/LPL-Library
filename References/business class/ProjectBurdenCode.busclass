ProjectBurdenCode is a BusinessClass
    owned by Projects
    prefix is PRJBC

    Ontology
        symbolic key is ProjectBurdenCode

    Persistent Fields
        Description  
		Rate					is Percent size 6.3
		OffsetCodeBlockOption	is a RevenueCodeBlockOption
			default label is "OffsetFinanceStructureOption"
		OffsetCodeBlock			is a FinanceCodeBlockFullBurdenOffset
			default label is "FullOffsetAccountFinanceStructure"
		OffsetPartialCodeBlock	is a FinanceCodeBlockBurdenOffset
			default label is "PartialOffsetAccountFinanceStructure"

	Relations
					        
    Field Rules
        Description
            required

		Rate
			required
			constraint (Rate > 0)
				"RateMustBePositive"

		OffsetCodeBlock
			if (OffsetCodeBlockOption.FullStructure)
				required
					"FullOffsetAccountFinanceStructureRequired"
			else
				cannot be entered
					"CannotEnterFullOffsetFinanceStructureUnlessOffsetFinanceStructureOptionIs_Full"

		OffsetPartialCodeBlock
			if (OffsetCodeBlockOption.PartialStructure)
				required
					"PartialOffsetAccountFinanceStructureRequired"
			else
				cannot be entered
					"CannotEnterPartialOffsetFinanceStructureUnlessOffsetFinanceStructureOptionIs_Partial"

    Actions
        Create is a Create Action
        Update is an Update Action
        Delete is a Delete Action
        
        CreateNewVersion is an Instance Action
        	Parameters
        		NewRate 			is Percent size 6.3
        			default label is "Rate"
				NewEffectiveDate	is Date
					default label is "EffectiveDate"
			Parameter Rules
				NewRate
					initial value is Rate
					required
				NewEffectiveDate
					initial value is current corporate date
					required
			Action Rules
				invoke Create
					fill in fields from this instance
					invoked.EffectiveDate	= NewEffectiveDate
					invoked.Rate			= NewRate



