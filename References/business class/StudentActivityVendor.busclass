StudentActivityVendor is a BusinessClass
    owned by studentactivities
    prefix is SAVN

    Ontology
    	symbolic key is StudentActivityVendor

    Persistent Fields
		Name			is Alpha 60
		Address			is a PostalAddressV2	
			holds pii
		TaxId 			is a StudentActivityTaxId
		VendorGroup
		IncomeCode		is a PayablesIncomeCode
		Active

	Derived Fields
			
	Sets
		ByName
			Sort Order	
				StudentActivityDistrict
				Name
				StudentActivityVendor
				
		ByTaxId
			Sort Order
				StudentActivityDistrict				
				TaxId.TaxNumber
				Name
				StudentActivityVendor
			
	Relations
		DuplicateTaxIdRel
			one-to-many relation to StudentActivityVendor
			Field Mapping uses ByTaxId
				related.StudentActivityDistrict			= StudentActivityDistrict
				related.TaxId.TaxNumber					= TaxId.TaxNumber
			Instance Selection
				where (related.StudentActivityVendor 	!= StudentActivityVendor)
								
		ReportableIncomeBankTransactionsRel is a StudentActivityBankTransaction set
			Instance Selection
				where (related.ReportableIncomeObjectID entered)	


	Conditions
		VendorExists
			when (StudentActivityVendor exists)

	Field Rules
		Address
			if (Address.DeliveryAddress.AddressLine1 entered
			and !Address.Country entered)
				Address.Country = StudentActivityDistrict.EnterpriseGroup.DefaultCountry

		IncomeCode	
			constraint (TaxId entered)
				"TaxIDRequiredIfIncomeCodeEntered"

		Name
			required

		TaxId
			constraint (!DuplicateTaxIdRel exists)
				"TaxIdExistsForVendor<first DuplicateTaxIdRel.StudentActivityVendor>"
				
	Attach Rules
		constraint (Active)
			"VendorIsInactive"
			
	Actions
		Create is a Create Action
		
		Update is an Update Action
			Exit Rules
				if (Name	changed
				or  Address changed
				or  TaxId	changed)
					invoke UpdateReportableIncomeVendor ReportableIncomeBankTransactionsRel
				
		Delete is a Delete Action
		
