IONDocumentProcessor is a BusinessClass
    owned by la
    prefix is iondp
    
    Ontology
    	symbolic key is IONDocumentProcessor
    	
    Patterns
		implements ExtendsExternalData
			using com.lawson.rdtech.framework.ion.IONDocumentStorage
		implements ConfigurationMetadata			
    	
    Derived Fields
    	Implicit is a NativeField
    		type is Boolean
    		
    	DerivedConnection is a NativeField
    		type is like IONConnection
    		restricted

    	DerivedRollbackOnError is a NativeField
    		type is Boolean
    		
	Persistent Fields
    	Status is Numeric size 1
    		States
    			Inactive value is 0
    			Active value is 1

    	RollbackOnError is Numeric size 1
            States
    		    Default value is 0
    			Yes value is 1
    			No value is 2

    Conditions
        DefaultRollbackOnErrorValid
            when (not Implicit and (RollbackOnError != RollbackOnError.Default))


        RollbackOnErrorValid
            when (not Implicit and (RollbackOnError != RollbackOnError.Yes))


        NoRollbackOnErrorValid
            when (not Implicit and (RollbackOnError != RollbackOnError.No))


    Transient Fields
    	IONConnection
    		derive value from DerivedConnection
    		
    Field Rules
    	IONDocumentProcessor.BusinessAction
    		if (IONDocumentProcessor.BusinessView.Type.BusinessClass)
    			constraint (IONDocumentProcessor.BusinessAction.IsImport)
    				"BusinessActionMustBeAnImportAction"

    	RollbackOnError
    		constraint (not Implicit and (RollbackOnError = RollbackOnError.Default))
    			"RollbackOnErrorCannotBeSetOnImplicitProcessors"
    			
    Actions
    	Create is a Create Action
    	
    	Delete is a Delete Action
    		valid when (not Implicit)
    		
    	Activate is an Instance Action
    		valid when (Status.Inactive)
    		Action Rules
    			Status = Status.Active
    			
    	Inactivate is an Instance Action
    		valid when (Status.Active)
    		Action Rules
    			Status = Status.Inactive

    	DefaultRollbackOnError is an Instance Action
    		valid when (DefaultRollbackOnErrorValid)
    		Action Rules
    			RollbackOnError = RollbackOnError.Default

    	RollbackOnError is an Instance Action
    		valid when (RollbackOnErrorValid)
    		Action Rules
    			RollbackOnError = RollbackOnError.Yes

    	NoRollbackOnError is an Instance Action
    	    valid when (NoRollbackOnErrorValid)
        	Action Rules
        		RollbackOnError = RollbackOnError.No
    	
