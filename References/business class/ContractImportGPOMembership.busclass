ContractImportGPOMembership is a BusinessClass
	owned by po
	
	prefix is CGPO
	
	Ontology
		symbolic key is ContractImportGPOMembership
	
	Patterns
	
	Persistent Fields
		ContractGroup
		ParticipantLocation 
		Name                            is Alpha 100
		ClassOfTrade                    is Alpha 200
		Address1                        is an AddressLine       
		Address2                        is an AddressLine
		Municipality
			default label is "City"
		StateProvince                   is like StateProvince
			default label is "State"
		PostalCode
		NewRecord                       is Boolean	    
			restricted
	
	Derived Fields
    	CompanyName is a StringField 
     		type is like Name
    		holds pii
			ParticipantLocation.Company.Name
    	LocationName is a StringField 
     		type is like Name
    		holds pii
			ParticipantLocation.Location.Name
    	RequestingLocationName is a StringField 
     		type is like Name
    		holds pii
			ParticipantLocation.RequestingLocation.Name
		DerivedCreateDate is a DerivedField
			type is Date
			for each audit log records
				if (each.action = "create")
					return each.effective date

	Local Fields
		LocalGPOContractNumber           is like GPOContractNumber
		LocalContractGroup               is like ContractGroup
		LocalContractImportGPOMembership is like ContractImportGPOMembership
		LocalContractImport              is like ContractImport
		LocalCompany                     is like InventoryCompany
		LocalLocation                    is like InventoryLocation
		LocalRequestingLocation          is like RequestingLocation
		LocalPricingGroup                is like PricingGroup
		LocalParticipantLocation         is like ParticipantLocation
			
	Conditions
	
		ParticipantEntered
			restricted
			when (ParticipantLocation.Company entered
			or    ParticipantLocation.PricingGroup entered)
	
		LocationNotActive
			restricted
			when (ParticipantLocation.Location entered
			and   ParticipantLocation.Location.Status != 0)
			
		RequestingLocationInactive
			restricted
			when (ParticipantLocation.RequestingLocation entered
			and   ParticipantLocation.RequestingLocation.Active = false)

		TierMembersExist
			restricted
			when (ContractUpdateImportTierMemberRel exists)
			
		TierMembersForALocationExist
			restricted
			when (ContractImportTierMemberByLocationFirst exists)

		OtherHasSameMembers
			restricted
			when (MemberMapWithSameMembersRel exists)
		
		CanChangeMembershipID
			restricted
			when (OtherHasSameMembers
			and   TierMembersExist)
		
		EligibleForNewTierMembers
			restricted
			when (ParticipantEntered
			and   !TierMembersExist
			and  ((ContractGroup.GPOIsHPGOrVizient
			and   ContractGPOParticipantRel exists)
			or   (ContractGroup.GPOIsGHX
			and   ContractGPOGHXInterfaceRel exists)))	     
	
	Relations
	
		ContractGPOGHXInterfaceRel
			one-to-many relation to ContractGPOGHXInterface
			Field Mapping uses ByOrganizationEID
				related.OrganizationEID      = ContractImportGPOMembership
			Instance Selection
				where (related.InterfaceContractExists)
		
		ContractGPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByMember
				related.GPOMembership         = ContractImportGPOMembership
			Instance Selection
				where (related.InterfaceContractExists)	  
				
		ContractGPODistributorRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByMember
				related.ContractImportGPOMembership = ContractImportGPOMembership			

		ContractOldUpdateImportTierMemberRel
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByMembershipFirst
				related.ContractGroup               		= ContractGroup
				related.ContractImportGPOMembership         = ContractImportGPOMembership
			Instance Selection
				where (related.ContractImportTierMember.Company 			= ParticipantLocation.Company
				and    related.ContractImportTierMember.Location			= ParticipantLocation.Location 	
				and    related.ContractImportTierMember.RequestingLocation	= ParticipantLocation.RequestingLocation
				and    related.ContractImportTierMember.PricingGroup        = ParticipantLocation.PricingGroup)

		ContractUpdateImportTierMemberRel
			one-to-many relation to ContractImportTierMember					
			Field Mapping uses ByMembershipFirst
				related.ContractGroup               		= ContractGroup
				related.ContractImportGPOMembership         = ContractImportGPOMembership
							
		ContractImportTierMemberRel
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByMembership
				related.ContractGroup               		= LocalContractGroup
				related.GPOContractNumber                   = LocalGPOContractNumber
				related.ContractImportGPOMembership         = LocalContractImportGPOMembership
				
		ContractImportTierMemberByLocationFirst
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByLocationFirst
				related.ContractGroup                     			= ContractGroup
				related.ContractImportTierMember.Company    		= ParticipantLocation.Company
				related.ContractImportTierMember.Location       	= ParticipantLocation.Location
				related.ContractImportTierMember.RequestingLocation = ParticipantLocation.RequestingLocation
				related.ContractImportTierMember.PricingGroup   	= ParticipantLocation.PricingGroup

		ContractImportTierMemberByLocationRel
			one-to-one relation to ContractImportTierMember
			Field Mapping uses symbolic key
				related.ContractGroup                     		= LocalContractGroup
				related.ContractImport                    		= LocalContractImport
				related.ContractImportTierMember.Company    	= LocalCompany
				related.ContractImportTierMember.Location       = LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   = LocalPricingGroup

		ContractGPOMembershipRel
			one-to-many relation to ContractGPOMembership
			Field Mapping uses ByGPOMembership
				related.GPOMembership                       = ContractImportGPOMembership

		MemberMapWithSameMembersRel
			one-to-many relation to ContractImportGPOMembership
			Field Mapping uses ByLocation
				related.ContractGroup                       	= ContractGroup
				related.ParticipantLocation.Company             = ParticipantLocation.Company
				related.ParticipantLocation.Location            = ParticipantLocation.Location
				related.ParticipantLocation.RequestingLocation  = ParticipantLocation.RequestingLocation
				related.ParticipantLocation.PricingGroup        = ParticipantLocation.PricingGroup
			Instance Selection
				where (related.UniqueID                    != UniqueID
				and    ParticipantLocation entered)
		
		MemberMapWithSameMembersLocalRel
			one-to-many relation to ContractImportGPOMembership
			Field Mapping uses ByLocation
				related.ContractGroup                       = LocalContractGroup
				related.ParticipantLocation                 = LocalParticipantLocation

		ContractImportRel
			one-to-one relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber                   = LocalGPOContractNumber
				related.ContractGroup                       = ContractGroup

	Sets
	
		ByLocation
			Sort Order
				ContractGroup
				ParticipantLocation
				ContractImportGPOMembership			
	
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action

			Action Rules
				constraint (!TierMembersExist)
					"CannotChangeMemberDefinition;TierMembersAlreadyExist;MustUse_Change_Member_MappingAction"
				
				if (ParticipantLocation.PricingGroup entered)
					constraint (!ParticipantLocation.PricingGroup.NoDetails)
    					"CannotEnter;PricingGroupHasNoCompaniesOrLocationsAttached"						
				
				if (MemberMapWithSameMembersRel exists)
					confirmation required
						"AnotherMemberRecordIsMappedWithTheSameLocationValues;DoYouWantToContinue?"
						
		Delete is a Delete Action
			restricted	
		
		ChangeMembershipIDOnContracts is an Instance Action
			valid when (CanChangeMembershipID) 
			Parameters
				NewMembershipID 			is a ContractImportGPOMembership
				
			Parameter Rules
				NewMembershipID
					required
			
			Action Rules
			
				constraint (ParticipantLocation = NewMembershipID.ParticipantLocation)
					"NewMembershipIDMustHaveSameMappingAsThisMember"
				constraint (NewMembershipID != ContractImportGPOMembership)
					"NewMembershipIDCannotBeTheSameAsThisMember"
				for each ContractUpdateImportTierMemberRel
					invoke UpdateMemberID each
						invoked.ParmContractImportGPOMembership = NewMembershipID 
			
		ChangeMemberMapping is an Instance Action
			valid when (TierMembersExist)
			Parameters
				ParmContractGroup       is a ContractGroup
				ParmNewMapping 			is a ParticipantLocation
					default label is "NewLocationsOrPricingGroup"
				UpdateExistingContracts is Boolean
				
			Parameter Rules
				ParmContractGroup
					initial value is ContractGroup
					default to ContractGroup
				
				ParmNewMapping
					required
						"NewLocationsOrPricingGroupAreRequired"
					
					if (ContractGroup entered)
						if (ParticipantLocation.PricingGroup !entered)
							constraint (ParticipantLocation.Company entered)
								"MustEnterAtLeastACompanyIfContractGroupIsEntered"
					
					if (ParticipantLocation.PricingGroup entered)
						constraint (!ParticipantLocation.PricingGroup.NoDetails)
	    					"CannotEnter;PricingGroupHasNoCompaniesOrLocationsAttached"						
					
					LocalContractGroup 			= ParmContractGroup
					LocalParticipantLocation 	= ParmNewMapping 
					if (MemberMapWithSameMembersLocalRel exists)
						confirmation required
							"AnotherMemberRecordIsMappedWithTheSameLocationValues;DoYouWantToContinue?"
							
					if (ParmNewMapping.Company      		= ParticipantLocation.Company
					and ParmNewMapping.Location     		= ParticipantLocation.Location
					and ParmNewMapping.RequestingLocation 	= ParticipantLocation.RequestingLocation)
						constraint (ParmNewMapping.PricingGroup != ParticipantLocation.PricingGroup)
							"NewMappingMustBeDifferentThanOldMapping" 

				UpdateExistingContracts
					initial value is true
			
			Action Rules
			
				if (UpdateExistingContracts)
					for each ContractOldUpdateImportTierMemberRel
						invoke DeleteOldTierMember each
				
				ParticipantLocation = ParmNewMapping					
				
				if (UpdateExistingContracts)
					invoke ProcessNewTierMembersForAMember 
						invoked.AutoUpdateContracts = UpdateExistingContracts
		
		ProcessNewTierMembersForAMember is an Instance Action
			valid when (EligibleForNewTierMembers)
			Parameters
				AutoUpdateContracts is Boolean
			
			Action Rules
			
				if (ContractGroup.GPOIsHealthTrust
				or  ContractGroup.GPOIsHealthTrust2021
				or  ContractGroup.GPOIsVizient)
					invoke ProcessNewMembers ContractGPOParticipant
						invoked.ParmContractGroup   = ContractGroup
						invoked.ParmGPOMembership   = ContractImportGPOMembership
						invoked.AutoUpdateContracts = AutoUpdateContracts
				if (ContractGroup.GPOIsGHX)
					invoke ProcessNewlyMappedMembers ContractGPOGHXInterface
						invoked.ParmContractGroup   = ContractGroup
						invoked.ParmGPOMembership   = ContractImportGPOMembership
						invoked.AutoUpdateContracts = AutoUpdateContracts						 

		ProcessNewMembers is a Set Action   
			restricted
			Parameters
				ParmContractGroup   is a ContractGroup
				ParmGPOMembership   is a ContractImportGPOMembership
				
															

					
					
					
			
