CustomerInvoiceTypeCode is a BusinessClass
    owned by ar
    prefix is ARCTC
	
    Ontology
        symbolic key is CustomerInvoiceTypeCode

    Patterns
        implements StaticJava
        disable AuditIndex		

	Persistent Fields
		TermsCode
		Default					is Boolean
		
	Relations
		OtherCustomerInvoiceTypeCodesRel
			one-to-many relation to CustomerInvoiceTypeCode
			Field Mapping uses symbolic key
				related.Company					= Company
				related.Customer				= Customer
			Instance Selection
				where (related.InvoiceTypeCode != InvoiceTypeCode
				and    related.Default)

		OtherCustomerInvoiceTypeCodesNotDefaultRel
			one-to-many relation to CustomerInvoiceTypeCode
			Field Mapping uses symbolic key
				related.Company					= Company
				related.Customer				= Customer
			Instance Selection
				where (related.InvoiceTypeCode != InvoiceTypeCode
				and    !related.Default)

		OtherCustomerInvoiceTypeCodesExistRel
			one-to-many relation to CustomerInvoiceTypeCode
			Field Mapping uses symbolic key
				related.Company					= Company
				related.Customer				= Customer
			Instance Selection
				where (related.InvoiceTypeCode != InvoiceTypeCode)

	Sets
		ByDefault
			indexed
			Sort Order		
				Company
				Customer
				Default
				InvoiceTypeCode

	Conditions
		NoDefaultExits
			restricted		
			when (OtherCustomerInvoiceTypeCodesExist
			and   NoCustomerInvoiceTypeCodeDefault)	

		NoCustomerInvoiceTypeCodeDefault
			restricted		
			when (all OtherCustomerInvoiceTypeCodesExistRel.Default = false)	

		OtherCustomerInvoiceTypeCodesExist
			restricted	
			when (OtherCustomerInvoiceTypeCodesExistRel exists)

	Field Rules
		Default
			if (OtherCustomerInvoiceTypeCodesRel exists)
				cannot be entered
					"AnotherCustomerInvoiceTypeCodeSetToDefault"

	Rule Blocks
		NoDefaultNoOther	
			if (!Default
			and !OtherCustomerInvoiceTypeCodesExistRel exists)
				confirmation required
					"NoInvoiceTypeCodeSetToDefault-Continue?"
		NoDefaultOther	
			if (!Default
			and NoCustomerInvoiceTypeCodeDefault)
				confirmation required
					"NoInvoiceTypeCodeSetToDefault-Continue?"


    Actions
        Create is a Create Action
        	Field Rules
        		TermsCode
        			default to InvoiceTypeCode.TermsCode
			Action Rules
				include NoDefaultNoOther
				include NoDefaultOther
							       			
        Update is an Update Action
			Action Rules
				include NoDefaultNoOther
				include NoDefaultOther





        Delete is a Delete Action
			Entrance Rules
				if (NoDefaultExits)
					confirmation required
						"DefaultWillNotExist;ConsiderUpdatingAnExistingInvoiceTypeCodeAsTheDefault-Continue"
        
