MobileSupplyChainInventoryTransactionLineDetail is a BusinessClass

    owned by mscm
    prefix is MSITD
    sql name is "MSCMITransactionLineDetail"

    Ontology
        symbolic key is MobileSupplyChainInventoryTransactionLineDetail

    Persistent Fields
        UnitOfMeasure
        Bin
        Lot                     is an ItemLot
        Item
        Serial                  is an ItemSerialNumber
        Quantity
        ErrorMessage            is a Text
        GTIN                    is like ItemGTIN
        UDIExpirationDate       is Date               
        UDIManufacturingDate    is Date        
        LotExpirationDate       is Date
        ManufacturingDate          
        ShelfLife
        
    Local Fields
        LocalError                      is Boolean
        LocalErrorMessage               is Text
        LocalStockOnHandDetailSOH       is like Quantity
        StockUomStockOnHandDetailSOH    is like Quantity
        UOMCalculation       
        LocalBin                        is like Bin
        LocalLot                        is like ItemLot
        LocalUOM                        is like UnitOfMeasure
        LocalSublot                     is like Sublot

    Derived Fields
        DerivedStockOnHand  is a DerivedField
            type is like Quantity
            restricted
            initialize LocalBin
            initialize LocalLot
            initialize LocalUOM
            initialize LocalStockOnHandDetailSOH
            if (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt)
                if ((Serial.Status.OnHand)
                and (!IsBinTracked or Serial.Bin = Bin))    
                    LocalStockOnHandDetailSOH = 1 
            else
                if (IsStockOnHandDetailTracked)
                    LocalBin            = Bin

                    if (IsLotAtReceipt)
                        LocalLot        = Lot

                    if (IsMultiTrackedUOM)
                        LocalUOM        = UnitOfMeasure
                    else
                        LocalUOM        = Item.StockUOM

                    LocalStockOnHandDetailSOH   = StockOnHandDetailByLocalUOMRel.AvailableQuantity

                else
                    LocalUOM                    = Item.StockUOM
                    LocalStockOnHandDetailSOH   = ItemLocationRel.AvailableQuantity

                if  (LocalUOM != UnitOfMeasure)
                    initialize UOMCalculation
                    UOMCalculation.InputUOM         = LocalUOM
                    UOMCalculation.InputToUOM       = UnitOfMeasure
                    UOMCalculation.InputQuantity    = LocalStockOnHandDetailSOH
                    UOMCalculation.Method           = UOMCalculation.Method.ConvertToAlternate  

                    LocalStockOnHandDetailSOH   = UOMCalculation.OutputQuantity

            return LocalStockOnHandDetailSOH

        DerivedDetailErrorMessageDisplay is a DerivedField
            type is MessageField
            if(MobileSupplyChainInventoryTransaction.InventoryTransaction.Status.Unreleased)
                return ErrorMessage
        
        DerivedAllowLotDetailsInput is a DerivedField
            type is Boolean
            restricted
            if (IsLotAtReceipt)
                if (IsPositiveAdjustment)
                    if (Lot exists)
                        if (Lot.LotExpirationDate not entered)
                            return true
                    else
                        return true
                else
                if (IsNegativeIssue)
                    if (Lot not exists)
                        return true
            else
            if (IsLotAtIssue)
                if (IsPositiveIssue)
                    return true

    Relations
        ItemLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = InventoryLocation
                related.Item              = Item  

        MobileSupplyChainInventoryTransactionLineRel
            one-to-many relation to  MobileSupplyChainInventoryTransactionLineDetail
            Field Mapping uses symbolic key
                related.Company                                                     = Company
                related.InventoryLocation                                           = InventoryLocation
                related.MobileSupplyChainInventoryTransaction                       = MobileSupplyChainInventoryTransaction
                related.MobileSupplyChainInventoryTransactionLine                   = MobileSupplyChainInventoryTransactionLine

        StockOnHandDetailByLocalUOMRel
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = LocalBin
                related.StockOnHandDetail.UnitOfMeasure     = LocalUOM
                related.StockOnHandDetail.Lot               = LocalLot
                related.StockOnHandDetail.Sublot            = LocalSublot


        StockOnHandDetailByLotStockUomRel 
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = blank
                related.StockOnHandDetail.UnitOfMeasure     = Item.StockUOM
                related.StockOnHandDetail.Lot               = Lot
                related.StockOnHandDetail.Sublot            = blank

        StockOnHandDetailByLotTransactionUomRel  
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = blank
                related.StockOnHandDetail.UnitOfMeasure     = UnitOfMeasure
                related.StockOnHandDetail.Lot               = Lot
                related.StockOnHandDetail.Sublot            = blank

        StockOnHandDetailByBinLotStockUomRel  
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = Bin
                related.StockOnHandDetail.UnitOfMeasure     = Item.StockUOM
                related.StockOnHandDetail.Lot               = Lot
                related.StockOnHandDetail.Sublot            = blank

        StockOnHandDetailByBinLotTransactionUomRel  
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = Bin
                related.StockOnHandDetail.UnitOfMeasure     = UnitOfMeasure
                related.StockOnHandDetail.Lot               = Lot
                related.StockOnHandDetail.Sublot            = blank

        StockOnHandDetailByBinStockUomRel  
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = Bin
                related.StockOnHandDetail.UnitOfMeasure     = Item.StockUOM
                related.StockOnHandDetail.Lot               = blank
                related.StockOnHandDetail.Sublot            = blank

        StockOnHandDetailByBinTransactionUomRel  
            one-to-one relation to StockOnHandDetail
            Field Mapping uses Set3
                related.Company                             = Company
                related.InventoryLocation                   = InventoryLocation
                related.Item                                = Item
                related.StockOnHandDetail.Bin               = Bin
                related.StockOnHandDetail.UnitOfMeasure     = UnitOfMeasure
                related.StockOnHandDetail.Lot               = Lot
                related.StockOnHandDetail.Sublot            = blank

        StockOnHandDetailByLocationUOMRel  
            one-to-many relation to StockOnHandDetail
            Field Mapping uses symbolic key
				related.Company							= Company
				related.InventoryLocation				= InventoryLocation
				related.Item							= Item
				related.StockOnHandDetail.UnitOfMeasure	= Item.StockUOM
				related.StockOnHandDetail.Lot			= Lot
				related.StockOnHandDetail.Sublot		= blank
				related.StockOnHandDetail.Bin			= Bin


        ItemTransactionUomRel
            one-to-one relation to ItemUOM
            Field Mapping uses symbolic key
                related.ItemGroup               = Company.ItemGroup
                related.Item                    = Item
                related.UnitOfMeasure           = UnitOfMeasure

        InventoryTransactionIntransitRecLineDetailRel
            one-to-one relation to InventoryTransactionLineDetail
            Field Mapping uses symbolic key
                related.Company                                     = Company
                related.InventoryLocation                           = InventoryLocation
                related.InventoryTransaction                        = MobileSupplyChainInventoryTransaction.OriginatingIntransitRec
                related.TransactionSystemCode                       = TransactionSystemCode.InventoryControl
                related.InventoryTransactionLine.WarehouseShipment  = blank
                related.InventoryTransactionLine.LineNumber         = MobileSupplyChainInventoryTransactionLine
                related.InventoryTransactionLine.ComponentSequence  = blank
                related.InventoryTransactionLineDetail              = MobileSupplyChainInventoryTransactionLineDetail

        InventoryIntransitRecUDIDetailRel
            one-to-one relation to InventoryUDIDetail
            Field Mapping uses symbolic key
                related.Company                                     = Company
                related.InventoryLocation                           = InventoryLocation
                related.InventoryUDIDetail.InventoryTransactionType = InventoryDocumentType.IntransitReceiving
                related.InventoryUDIDetail.TransactionSystemCode    = TransactionSystemCode.InventoryControl
                related.InventoryUDIDetail.DocumentNumberNumeric    = MobileSupplyChainInventoryTransaction.OriginatingIntransitRec
                related.InventoryUDIDetail.WarehouseShipment        = blank
                related.InventoryUDIDetail.LineNumber               = MobileSupplyChainInventoryTransactionLine
                related.InventoryUDIDetail.TransactionSequence      = MobileSupplyChainInventoryTransactionLineDetail

        InventoryIntransitRecUDIDetailsRel
            one-to-many relation to InventoryUDIDetail
            Field Mapping uses symbolic key
                related.Company                                     = Company
                related.InventoryLocation                           = InventoryLocation
            Instance Selection
                where (related.InventoryUDIDetail.InventoryTransactionType = InventoryDocumentType.IntransitReceiving
                and    related.InventoryUDIDetail.TransactionSystemCode    = TransactionSystemCode.InventoryControl
                and    related.InventoryUDIDetail.DocumentNumberNumeric    = MobileSupplyChainInventoryTransaction.OriginatingIntransitRec
                and    related.InventoryUDIDetail.LineNumber               = MobileSupplyChainInventoryTransactionLine)
                
    Conditions
        IsBinTracked
            when (ItemLocationRel.BinTracked)

        IsLotAtReceipt
            when (ItemLocationRel.LotTracked.LotRequiredAtReceipt)
        
        IsLotAtIssue
            when(ItemLocationRel.LotTracked.LotRequiredAtIssue)

        IsLotTracked
            when (ItemLocationRel.LotTracked.LotRequiredAtReceipt or ItemLocationRel.LotTracked.LotRequiredAtIssue)

        IsSerialTracked
            when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt or ItemLocationRel.SerialTracked.SerialRequiredAtIssue)

        IsUDITracked
            when (ItemLocationRel.UDITracked.UDIRequiredAtReceipt or ItemLocationRel.UDITracked.UDIRequiredAtIssue)
            
        TransactionUOMIsTracked
            when(ItemTransactionUomRel.TrackedIn)

        IsUDIDetail
            when ((MobileSupplyChainInventoryTransaction.IsInventoryIssues and ItemLocationRel.IsUDITracked)
            or    (MobileSupplyChainInventoryTransaction.IsInventoryAdjustments and ItemLocationRel.UDITracked.UDIRequiredAtReceipt)
            or    (MobileSupplyChainInventoryTransaction.IsInventoryTransfer and ItemLocationRel.UDITracked.UDIRequiredAtReceipt)
            or    (MobileSupplyChainInventoryTransaction.IsReceivingTransfer and ItemLocationRel.UDITracked.UDIRequiredAtReceipt))

        IsBinTrackedOnly
            when (IsBinTracked
            and   not IsLotTracked
            and   not IsSerialTracked)

        IsMultiTrackedUOM
            restricted
            when (not ItemLocationRel.HasItemLocationStockUOM
            and   ItemLocationRel.DerivedMultipleUOM > 1)

        IsStockOnHandDetailTracked
            restricted
            when (IsBinTracked
            or    IsLotAtReceipt
            or    IsMultiTrackedUOM)
        
        IsPositiveAdjustment
            restricted
            when (MobileSupplyChainInventoryTransaction.IsInventoryAdjustments 
            and  Quantity > 0)

        IsPositiveIssue
            restricted
            when (MobileSupplyChainInventoryTransaction.IsInventoryIssues 
            and  Quantity > 0)
        
        IsNegativeIssue
            restricted
            when (MobileSupplyChainInventoryTransaction.IsInventoryIssues 
            and  Quantity < 0)
    Actions

        Create is a Create Action  
            Local Fields
                LocalInventoryTransaction          is like InventoryTransaction
                LocalDocumentType                  is like InventoryDocumentType
                LocalSourceDocumentAlpha           is like InventoryTransaction

            Action Rules
                if (MobileSupplyChainInventoryTransactionLine.ErrorMessage not entered)
                    if (IsUDIDetail)
                        if (MobileSupplyChainInventoryTransaction.IsReceivingTransfer)
                            invoke Delete InventoryIntransitRecUDIDetailsRel

                            LocalDocumentType               = InventoryDocumentType.IntransitReceiving
                            LocalInventoryTransaction       = MobileSupplyChainInventoryTransaction.OriginatingIntransitRec
                            LocalSourceDocumentAlpha        = MobileSupplyChainInventoryTransaction.OriginatingIntransitRec.OriginatingDocument
                        else
                            LocalDocumentType               = MobileSupplyChainInventoryTransaction.InventoryDocumentType
                            LocalInventoryTransaction       = MobileSupplyChainInventoryTransaction.InventoryTransaction
                            LocalSourceDocumentAlpha        = MobileSupplyChainInventoryTransaction.InventoryTransaction

                        invoke Create InventoryUDIDetail
                            resume on error
                                ErrorMessage    = error message 
                            invoked.Company                                         = Company
                            invoked.InventoryLocation                               = InventoryLocation
                            invoked.InventoryUDIDetail.InventoryTransactionType     = LocalDocumentType
                            invoked.InventoryUDIDetail.TransactionSystemCode        = TransactionSystemCode.InventoryControl
                            invoked.InventoryUDIDetail.DocumentNumberNumeric        = LocalInventoryTransaction
                            invoked.InventoryUDIDetail.LineNumber                   = MobileSupplyChainInventoryTransactionLine   
                            invoked.Item                                            = Item
                            invoked.Quantity                                        = Quantity
                            invoked.SourceDocumentAlpha                             = LocalSourceDocumentAlpha
                            invoked.GTIN                                            = GTIN
                            invoked.ManufacturingDate                               = UDIManufacturingDate
                            invoked.ExpirationDate                                  = UDIExpirationDate
                            invoked.Bin                                             = Bin
                            invoked.Lot                                             = Lot
                            invoked.SerialNumber                                    = Serial
                            invoked.UOM                                             = UnitOfMeasure
                            invoked.StockUOM                                        = Item.StockUOM 

                    else
                    if ((MobileSupplyChainInventoryTransaction.IsReceivingTransfer and IsBinTrackedOnly)
                    or  not MobileSupplyChainInventoryTransaction.IsReceivingTransfer)

                        if (MobileSupplyChainInventoryTransaction.IsReceivingTransfer)
                            LocalInventoryTransaction               = MobileSupplyChainInventoryTransaction.OriginatingIntransitRec
                        else
                            LocalInventoryTransaction               = MobileSupplyChainInventoryTransaction.InventoryTransaction
                        
                        invoke Unreleased.Create InventoryTransactionLineDetail   
                            resume on error
                                ErrorMessage    = error message
                            invoked.InventoryTransaction                   =      LocalInventoryTransaction
                            invoked.InventoryTransactionLine.LineNumber    =      MobileSupplyChainInventoryTransactionLine
                            invoked.Company                                =      Company
                            invoked.InventoryLocation                      =      InventoryLocation
                            invoked.TransactionSystemCode                  =      TransactionSystemCode.InventoryControl
                            invoked.Quantity                               =      Quantity
                            invoked.Bin                                    =      Bin
                            invoked.Serial                                 =      Serial
                            invoked.Lot                                    =      Lot  
                            invoked.UnitOfMeasure                          =      UnitOfMeasure
                            if (DerivedAllowLotDetailsInput)
                                invoked.LotExpirationDate                  =      LotExpirationDate
                                if (ItemLocationRel.IsAWarehouseLocationItem)
                                    invoked.ManufacturingDate              =      ManufacturingDate
                                    invoked.ShelfLife                      =      ShelfLife
                    else
                    if (MobileSupplyChainInventoryTransaction.IsReceivingTransfer 
                    and (IsSerialTracked or IsLotTracked))
                        invoke Update InventoryTransactionIntransitRecLineDetailRel 
                            resume on error     
                                ErrorMessage                = error message  
                            invoked.Quantity                = Quantity
                            invoked.Bin                     = Bin

        Update is an Update Action
        Delete is a Delete Action
