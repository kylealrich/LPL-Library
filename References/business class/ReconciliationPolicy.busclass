ReconciliationPolicy is a BusinessClass
    owned by reconciliation
    prefix is RMARP

    Ontology
    	symbolic key is ReconciliationPolicy

    Persistent Fields
		Description
		Summary							is a Description4
		ZeroLedgerAmount				is Boolean
		ZeroLedgerAmountPeriods			is Numeric 2
		ZeroOutOfBalanceAmount			is Boolean
		ZeroOutOfBalanceAmountPeriods	is Numeric 2
		NoLedgerTransactions			is Boolean
		NoLedgerTransactionsPeriods		is Numeric 2
		Active

	Field Rules
		Description
			required
			
		Summary
			default to Description

		ZeroLedgerAmount
			if (!ZeroOutOfBalanceAmount)
				required
					"MustSelectBalanceCriteriaForTheAutoReconciliationPolicy"
		
		ZeroLedgerAmountPeriods
			if (ZeroLedgerAmount)
				default to 1
			else
				initialize
				
			constraint (ZeroLedgerAmountPeriods > 0)
				"NumberOfZeroLedgerAmountPeriodsMustBeGreaterThanZero"
				
			constraint (ZeroLedgerAmountPeriods < 7)
				"CanOnlyCheckForAZeroLedgerAmountForTheLastSixPeriods"
							
		ZeroOutOfBalanceAmount
			if (!ZeroLedgerAmount)
				required
					"MustSelectBalanceCriteriaForTheAutoReconciliationPolicy"
			
			if (ZeroLedgerAmount
			and !ZeroOutOfBalanceAmount)
				confirmation required
					"AllBalancesMustBeZeroWhenZeroLedgerAmountIsSelected;WouldYouLikeToContinue?"
				ZeroOutOfBalanceAmount = true
				required
					"AllBalancesMustBeZeroWhenZeroLedgerAmountIsSelected;MustSelectZeroOutOfBalanceAmount"
							
		ZeroOutOfBalanceAmountPeriods
			if (ZeroOutOfBalanceAmount)
				default to 1
			else
				initialize
				
			constraint (ZeroOutOfBalanceAmountPeriods > 0)
				"NumberOfZeroOutOfBalanceAmountPeriodsMustBeGreaterThanZero"
				
			constraint (ZeroOutOfBalanceAmountPeriods < 7)
				"CanOnlyCheckForAZeroOutOfBalanceAmountForTheLastSixPeriods"
			
			if (ZeroLedgerAmount)
				ZeroOutOfBalanceAmountPeriods = ZeroLedgerAmountPeriods
				constraint (ZeroOutOfBalanceAmountPeriods = ZeroLedgerAmountPeriods)
					"NumberOfZeroOutOfBalancePeriodsMustMatchTheNumberOfZeroLedgerBalancePeriods"
										
		NoLedgerTransactions
			initial value is true
			
			constraint (ZeroLedgerAmount
			or			ZeroOutOfBalanceAmount)
				"MustSelectBalanceCriteriaWhenNoLedgerTransactionsIsSelected"
	
		NoLedgerTransactionsPeriods
			if (NoLedgerTransactions)
				default to 1
			else
				initialize
				
			constraint (NoLedgerTransactionsPeriods > 0)
				"NumberOfNoLedgerTransactionsPeriodsMustBeGreaterThanZero"
				
			constraint (NoLedgerTransactionsPeriods < 7)
				"CanOnlyCheckForNoLedgerTransactionsForTheLastSixPeriods"
				
	Conditions
		ZeroLedgerAmountPeriodsForCurrentPeriod
			when (ZeroLedgerAmountPeriods = 1)
			
		ZeroOutOfBalanceAmountPeriodsForCurrentPeriod
			when (ZeroOutOfBalanceAmountPeriods = 1)
		
		NoLedgerTransactionsPeriodsForCurrentPeriod
			when (NoLedgerTransactionsPeriods = 1)
							
	Attach Rules
		constraint (Active)
			"AutoReconciliationPolicyIsInactive"
			
	Actions
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
