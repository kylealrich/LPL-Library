ProjectEffortSchedule is a BusinessClass
    owned by Projects
    prefix is PJESC

	Ontology
		symbolic key is ProjectEffortSchedule
        		
	Persistent Fields
		Description				
		Active
        FirstPeriodStartDate 	is Date
        Frequency				is Numeric size 1
			States
				Weekly      	value is 1
				BiWeekly    	value is 2
				SemiMonthly 	value is 3
				Monthly     	value is 4
				FourWeekly  	value is 5        
				Quarterly    	value is 6
				SemiAnnual		value is 7
				Annual     		value is 8
		
    Local Fields    
        CalculatedStartDate			is Date	
        LocalActualStartDate		is Date	
        LocalActualEndDate			is Date	
        PeriodThruDate				is Date
 		LocalPeriodThruDate			is Date

	Derived Fields

	Conditions

	Relations
		OpenProjectEffortPeriodRel is a ProjectEffortPeriod set
			Instance Selection
				where (related.Status.Open)

		ProjectEffortPeriodRel
			one-to-many relation using OpenProjectEffortPeriodRel			
			Instance Selection
				where (related.ProjectAssignmentEffortExists)

		NewProjectEffortPeriodRel
			one-to-many relation to ProjectEffortPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectEffortSchedule	= ProjectEffortSchedule
			Instance Selection
				where (related.DateRange.Begin	= LocalActualStartDate
				and    related.DateRange.End	= LocalActualEndDate)
			
	Sets
		
	Attach Rules
		constraint (Active)
			"ProjectEffortSchedule<ProjectEffortSchedule>IsInactive"

	Field Rules
		Description 
			required
			default to ProjectEffortSchedule
			
		FirstPeriodStartDate
			required
			if (Frequency.Monthly
			or  Frequency.Quarterly
			or  Frequency.SemiAnnual
			or  Frequency.Annual)
				constraint (FirstPeriodStartDate day = 1)
					"FirstPeriodStartDayMustBe1"
			if (Frequency.SemiMonthly)
				constraint (FirstPeriodStartDate day = 1
				or          FirstPeriodStartDate day = 16)
					"FirstPeriodStartDayMustBe1Or16ForSemiMonthlyFrequency"
			if (ProjectEffortPeriod set exist)
				cannot be changed
					"FirstPeriodStartDateCannotBeChanged;ProjectEffortPeriodsExist"
							
		Frequency
			required
			if (ProjectEffortPeriod set exist)
				cannot be changed
					"FrequencyCannotBeChanged;ProjectEffortPeriodsExist"

	Actions 
		Create is a Create Action
			Exit Rules
				LocalPeriodThruDate	= FirstPeriodStartDate + 1 year - 1 day
   				CalculatedStartDate	= FirstPeriodStartDate    
				invoke CreatePeriods

		CreateNoRules is a Create Action
			restricted
			bypass field rules
											
		Update is an Update Action 
			Exit Rules
  				if (FirstPeriodStartDate changed)
					LocalPeriodThruDate = FirstPeriodStartDate + 1 year - 1 day
    				CalculatedStartDate = FirstPeriodStartDate    
  					invoke CreatePeriods
								
		Delete is a Delete Action	
		
		CreatePeriods is an Action
			restricted
			Action Rules
				if (LocalPeriodThruDate > 0)					
					while	(CalculatedStartDate <= LocalPeriodThruDate)					 			
						LocalActualStartDate				= CalculatedStartDate
						if	(Frequency.Weekly)
							LocalActualEndDate				= CalculatedStartDate + 6 days	
						if	(Frequency.BiWeekly)
							LocalActualEndDate				= CalculatedStartDate + 13 days
						if	(Frequency.SemiMonthly)
							if (CalculatedStartDate day = 16)
					  			LocalActualEndDate			= CalculatedStartDate + 1 month - 16 days 
							else
								LocalActualEndDate			= CalculatedStartDate + 14 days		
						if	(Frequency.Monthly)	
							LocalActualEndDate				= CalculatedStartDate + 1 month - 1 day			
						if	(Frequency.FourWeekly)		
							LocalActualEndDate				= CalculatedStartDate + 27 days
						if	(Frequency.Quarterly)	
							LocalActualEndDate				= CalculatedStartDate + 3 months - 1 day			
						if	(Frequency.SemiAnnual)	
							LocalActualEndDate				= CalculatedStartDate + 6 months - 1 day			
						if	(Frequency.Annual)	
							LocalActualEndDate				= CalculatedStartDate + 12 months - 1 day			
						if (NewProjectEffortPeriodRel not exists)
							invoke Create ProjectEffortPeriod  
								invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
								invoked.ProjectEffortSchedule	= ProjectEffortSchedule
								invoked.DateRange.Begin			= LocalActualStartDate
								invoked.DateRange.End			= LocalActualEndDate
						CalculatedStartDate					= LocalActualEndDate + 1 day
	
		CreatePeriodDates is an Instance Action
			Parameters
  				PeriodThruDate	is Date
			Parameter Rules
				PeriodThruDate 
					required	

			Action Rules
				LocalPeriodThruDate	= PeriodThruDate
				if (FirstPeriodStartDate entered)
					if (ProjectEffortPeriod set exists)
						CalculatedStartDate = last ProjectEffortPeriod set.DateRange.End + 1 day
						invoke CreatePeriods					
					else
						CalculatedStartDate = FirstPeriodStartDate
						invoke CreatePeriods



					
