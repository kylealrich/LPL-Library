SourcingEventResponseAttachment is a BusinessClass
    owned by ss
    prefix is SERA
    sql name is SERESPONSEATTACHMENT

    Ontology
    	part of SourcingEventResponse
    		relative key is SeqNumber is a Sequence
    		
    Persistent Fields
		Attachment              is an AttachmentWithTitleAudited
		FromTechnicalProposal   is Boolean
		CMAttachment            is Boolean
	
	Derived Fields
		
		SetSendToCM is a ConditionalField
			type is Text
			restricted
			if (CMAttachment)
				SetToNoMessage
			else
				SetToYesMessage
		
		SetToNoMessage is a MessageField
			restricted
   			"Set_Send_To_CM_To_No"
   			
   		SetToYesMessage is a MessageField
			restricted
   			"Set_Send_To_CM_To_Yes"
   		
	Field Rules
		SeqNumber
			autosequence 
		Attachment
			if (true)
				required
	Create Rules
		if (!SourcingEvent.CreateByCopy)
			CMAttachment = true     		

	Conditions
		
		AttachmentExists
			restricted
			when (SeqNumber entered)
		
		HasAttachment
			restricted
			when (Attachment entered)
			
		CanSetCMToNo
			restricted
			when (SourcingEvent.ContractOutputExists 
			and   CMAttachment)
			
		CanSetCMToYes
			restricted
			when (SourcingEvent.ContractOutputExists
			and   CMAttachment = false)
			
		DisplayCMAttachmentButton
			restricted
			when (SourcingEvent.ContractOutputExists
			and   SourcingEvent.Status.Open)
		
	Actions
		Create is a Create Action
			default label is "Upload"
			valid when (SourcingEventResponse.Status.Draft)
     		
     		Action Rules
 				constraint (SourcingEventResponse.CanUpdateResponse)
 					"BuyerCannotCreateAnAttachmentForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEventResponse.HasCurrentModificationRequests)
	 					if (!SourcingEvent.BestAndFinalStarted
	 					and !SourcingEvent.StepTwoStarted)
	 						constraint (SourcingEvent.CanRespond)
	 							"CanOnlyRespondBetweenEventOpenDateAndTimeAndCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotCreateIfResponseHasBeenSubmitted"
   								
     	Update is an Update Action
     		valid when (SourcingEventResponse.Status.Draft)
     		Action Rules
 				constraint (SourcingEventResponse.CanUpdateResponse)
 					"BuyerCannotUpdateAnAttachmentForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEventResponse.HasCurrentModificationRequests)	 				
	 					constraint (SourcingEvent.CanRespond)  
	 						"CannotUpdateResponseAfterEventCloseDateAndTime"
	 			constraint (!FromTechnicalProposal)
	 				"CannotChangeAttachmentWhichWasPartOfAnApprovedTechnicalProposal"
	 	
	 	SupplierCreate is a Create Action
			default label is "Upload"
	 		valid when (SourcingEventResponse.Status.Draft)
	 	
	 	
	 	SupplierUpdate is an Update Action
	 		valid when (SourcingEventResponse.Status.Draft)	
     	
     	Delete is a Delete Action
     		valid when (SourcingEventResponse.Status.Draft)
    		Action Rules
 				constraint (SourcingEventResponse.CanUpdateResponse)
 					"BuyerCannotDeleteAnAttachmentForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotDeleteIfResponseHasBeenSubmitted"
 				constraint (!FromTechnicalProposal)
	 				"CannotDeleteAttachmentWhichWasPartOfAnApprovedTechnicalProposal"
	 				
	 	UpdateFromAcceptTechnicalProposal is an Update Action
	 		restricted
	 		
	 	UpdateCMAttachment is an Instance Action
	 		
	 		Action Rules
	 			if (CanSetCMToNo)
	 				CMAttachment = false
	 			else
	 			if (CanSetCMToYes)
	 				CMAttachment = true
	 	
