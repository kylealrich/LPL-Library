StaleDateOutputFileHeader is a BusinessClass
    owned by cb
    prefix is SDOFH

    Ontology
        symbolic key is StaleDateOutputFileHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
		CreateDate					is TimeStamp
    	CashCode
    	CashCodeGroup
    	BankTransactionCode
    	ProcessLevelProcessing		is Boolean
    	ProcessLevel
    	IncludeAllTransactions		is Boolean
    	PrintAddress				is Boolean	
    	StaleDate					is Date
    	Days						is Numeric 4
    	PostDate					is Date
    	FileName					is AlphaUpper 50
    	DetailFile					is an Attachment
    	Status						is Numeric 1
			States
				Created				value is 1
				HasErrors 			value is 2
				ReadyToUpdate 		value is 3
				Completed 			value is 4
				NoRecordsToProcess	value is 5
				Processing 			value is 6
		TotalNumberOfRecords		is Numeric 5 					
		TotalRecordAmount			is like InternationalAmount 	

	Transient Fields
		TransientPerformStaleDate			is Boolean		
    
    Local Fields
		StaleDateBackgroundGroup			is AlphaUpper up to 200
				    
    Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 5
			(instance count of StaleDateOutputFileDetail set)

		TotalAmount is a DerivedField
			type is like InternationalAmount
			return (sum StaleDateOutputFileDetail set.IssuedBankAmount)

        DetailOutputFileName is a StringField
            type is Alpha 100
			FileName
			"-"
			CashCode
			"ExtractDetail"

		ConcatenatedKey is a StringField
			type is Alpha 15
			CashManagementGroup
			"-"
			StaleDateOutputFileHeader
				
		BackgroundGroupKey is a StringField
			type is Alpha 150
			"StaleDateProcessing-PerformStaleDate"
			"-"
			ConcatenatedKey
			"-"
			
    Conditions
		HasDetailAttachment
			restricted
			when (DetailFile entered)

		ErrorsExist
			restricted
			when (any CashLedgerTransactionErrorForStaleDateRel exists)
		
		DetailsExist
			restricted
			when (any StaleDateOutputFileDetailsRel exists)
			
		IsCreated
			restricted
			when (Status.Created)
			
		HasErrors
			restricted
			when (Status.HasErrors)
		
		IsReadyToUpdate
			restricted
			when (Status.ReadyToUpdate)
		
		IsCompleted
			restricted
			when (Status.Completed)
		
		SelectedWithProcessLevelProcessing
			restricted
			when (ProcessLevelProcessing
			and  (HasErrors	
			or    IsReadyToUpdate))
						
		SelectedWithoutProcessLevelProcessing
			restricted
			when (!ProcessLevelProcessing
			and  (HasErrors	
			or    IsReadyToUpdate))
						
		ProcessedWithProcessLevelProcessing
			restricted
			when (ProcessLevelProcessing
			and   IsCompleted)	
						
		ProcessedWithoutProcessLevelProcessing
			restricted
			when (!ProcessLevelProcessing
			and   IsCompleted)
			
		SecurityGroupAllowsAccess
    		when((CashCode not entered and CashCodeGroup not entered)
    		or	(CashCode entered and CashCode.SecurityGroupAllowsAccess)
    		or	(CashCodeGroup entered 
    		and (actor.context.CashCodeSecurityGroup.FinanceDimensionStructure = CashCodeGroup
    		or   actor.context.CashCodeSecurityGroup = "")))	
																						
    Relations
		CashLedgerTransactionErrorForStaleDateRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByStaleDateErrorSource
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 1	
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader
		
		StaleDateOutputFileDetailsRel
			one-to-many relation to StaleDateOutputFileDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader
				
		StaleDateOutputFileDetailsSelectedRel
			one-to-many relation to StaleDateOutputFileDetail
			Field Mapping uses ByStatus
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader
				related.Status						= 1	
				
		StaleDateOutputFileDetailsByCashCodeSelectedRel
			one-to-many relation to StaleDateOutputFileDetail
			Field Mapping uses ByCashCode
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader
			Instance Selection
				where (related.Status.Selected)	
				
		StaleDateOutputFileDetailsByProcessLevelSelectedRel
			one-to-many relation to StaleDateOutputFileDetail
			Field Mapping uses ByProcessLevel
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader
			Instance Selection
				where (related.Status.Selected)
									
		StaleDateOutputFileDetailsByCashCodeProcessedRel
			one-to-many relation to StaleDateOutputFileDetail
			Field Mapping uses ByCashCode
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader
			Instance Selection
				where (related.Status.Processed)
					
		StaleDateOutputFileDetailsByProcessLevelProcessedRel
			one-to-many relation to StaleDateOutputFileDetail
			Field Mapping uses ByProcessLevel
				related.CashManagementGroup			= CashManagementGroup
				related.StaleDateOutputFileHeader	= StaleDateOutputFileHeader	
			Instance Selection
				where (related.Status.Processed)
	
		StaleDateOutputFileHeaderHasErrorsRel
			one-to-many relation to StaleDateOutputFileHeader
			Field Mapping uses ByStatus
				related.CashManagementGroup			= CashManagementGroup
				related.Status						= 2	
													
    Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			CashManagementGroup
    			CashCode
    			BankTransactionCode
    			ProcessLevel
    			FileName
    			StaleDateOutputFileHeader

		ByCreateStatus
    		indexed
    		Instance Selection
                where (IsCreated)
			Sort Order
				CashManagementGroup
				Status
				StaleDateOutputFileHeader		
				
		ByStatus
			indexed
			Sort Order
				CashManagementGroup
				Status
				StaleDateOutputFileHeader
				
    Field Rules
        CreateDate
            default to current timestamp 

		FileName
			default to "StaledDate"    

		Status
			initial value is 1	
			default to 1
			
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action	
			restricted

		StatusUpdate is an Update Action
			default label is untranslatable
			restricted
			Action Rules
				if (ErrorsExist)
					Status = Status.HasErrors
				else
				if (DetailsExist)
					Status = Status.ReadyToUpdate
				else
					Status = Status.NoRecordsToProcess
			Exit Rules
				if (Status.ReadyToUpdate
				and TransientPerformStaleDate)		
					invoke PerformStaleDate
						
		DeleteStaleDateOutputFiles is an Instance Action	
			Action Rules
				if (ErrorsExist)
					invoke RemoveStaleDateErrorFromList CashLedgerTransactionErrorForStaleDateRel
				if (DetailsExist)
					invoke DeleteStaleDateOutputFileDetails StaleDateOutputFileDetail	
						invoked.PrmCashManagementGroup	 		= CashManagementGroup
						invoked.PrmStaleDateOutputFileHeader	= StaleDateOutputFileHeader 

		Delete is a Delete Action
			restricted	

		PerformStaleDate is an Instance Action
			valid when (IsReadyToUpdate)
			Action Rules
				trigger "CBStaleDateOutputFiles" PA service
					resume on error
					title is "CreateStaleDateOutputFiles"
					Variables
						CashManagementGroup
						StaleDateOutputFileHeader
				if (StaleDateOutputFileDetailsSelectedRel exists)
					StaleDateBackgroundGroup	= BackgroundGroupKey
					Status 						= Status.Processing
					invoke ProcessStaleDateDetails StaleDateOutputFileDetail in background group(StaleDateBackgroundGroup)
						invoked.PrmCashManagementGroup	 		= CashManagementGroup
						invoked.PrmStaleDateOutputFileHeader	= StaleDateOutputFileHeader
					invoke MakeTransitionToCompleted in background
						run after background group (StaleDateBackgroundGroup)

		MakeTransitionToCompleted is an Instance Action
			restricted
			run in background
			Action Rules
				Status = Status.Completed 
				
		ResendDetailFile is an Instance Action				
			valid when (HasDetailAttachment)
			Action Rules
				trigger "CBStaleDateOutputFiles" PA service
					resume on error
					title is "CreateStaleDateOutputFiles"
					Variables
						CashManagementGroup
						StaleDateOutputFileHeader
				
					
