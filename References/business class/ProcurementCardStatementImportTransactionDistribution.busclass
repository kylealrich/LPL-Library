ProcurementCardStatementImportTransactionDistribution is a BusinessClass
    owned by po
    prefix is PX3
    sql name is PCSITransactionDistribution
    classic name is PDCCHRGDST

    Ontology
        symbolic key is ProcurementCardStatementImportTransactionDistribution
            classic set name is PX3SET1
            sql name is PCSITransactionDistribution
            classic name is SEQ-NBR
            classic name for ProcurementCardProgram is PCARD-PROGRAM
            classic name for ProcurementCardStatementImport is STATEMENT
            classic name for ProcurementCardStatementImportTransaction.TransactionNumber is TXN-NBR

    Patterns
        implements ContextualParent
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
        ProcurementChargeDistributionType
            sql name is PChargeDistributionType
            classic name is DIST-TYPE
        DistributionAmount                is an InternationalAmount
            classic name is DIST-AMOUNT
        DistributionAccount               is a TransactionCodeBlock
            classic name for DistributionAccount.ToAccountingEntity is DIST-COMPANY
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
            classic name for DistributionAccount.Project is ACTIVITY

	Local Fields
		LocalGeneralLedgerSystemCode 				is a GeneralLedgerSystemCode
		LocalPostingDate							is a PostingDate
		LocalTransactionDate						is an ExchangeDate


    Transient Fields
    	TransientAccountingEntity           is an AccountingEntity  
			derive value from ProcurementCardStatementImportTransaction.Company.AccountingEntity
			
		TransientTransactionDate			is an ExchangeDate
			derive value from ProcurementCardStatementImportTransaction.TransactionDate
			
		TransientPostingDate				is a PostingDate
			derive value from ProcurementCardStatementImportTransaction.PostDate
		
    Conditions
    
    	NoPurchaseOrder
    		restricted
    		when (ProcurementCardStatementImportTransaction.PurchaseOrder !entered)

	Relations
		GeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= ProcurementGroup.FinanceEnterpriseGroup
				related.GeneralLedgerSystemCode	= "PO"

	Rule Blocks
		ValidateProjectDates
			if (GeneralLedgerSystemCodeRel exists)
				LocalGeneralLedgerSystemCode = GeneralLedgerSystemCodeRel.GeneralLedgerSystemCode
			if (DistributionAccount.Project entered
			and ProcurementGroup.FinanceEnterpriseGroup.ProjectDateEdit entered)
				if (ProcurementGroup.FinanceEnterpriseGroup.ProjectDateEdit.TransactionDate)
					LocalTransactionDate		= ProcurementCardStatementImportTransaction.TransactionDate
				else
					if (ProcurementCardStatementImportTransaction.PostDate entered)
						LocalPostingDate  		= ProcurementCardStatementImportTransaction.PostDate
					else
						LocalPostingDate  		= current corporate date
	
	Field Rules
        ProcurementChargeDistributionType
            initial value is ProcurementChargeDistributionType.Goods
            default to ProcurementChargeDistributionType.Goods

		DistributionAmount
			required
	
	Actions
		Create is a Create Action
			valid when (NoPurchaseOrder)
			Action Rules
				include ValidateProjectDates

		CreateNoRules is a Create Action
			restricted
			bypass field rules
			Action Rules
				ProcurementCardStatementImportTransactionDistribution = ProcurementCardStatementImportTransaction.DerivedLastDistribution + 1
				include ValidateProjectDates
		
		Update is an Update Action
			Action Rules
				include ValidateProjectDates
					
		Delete is a Delete Action
		
		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
