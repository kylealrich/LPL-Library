RecurringInvoiceParameter is a BusinessClass
    owned by bl
    prefix is BRM
    classic name is BLRIMAST

    Ontology
        symbolic key is RecurringInvoiceParameter
            classic set name is BRMSET1
            classic name is RECUR-INVOICE

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        BillingInvoiceType
            classic name is INVC-TYPE
		ListName 				 is a PriceListContractPromotion  
        Currency
            classic name is CURRENCY-CODE
        TermCode				 is a TermsCode	
            classic name is TERMS-CD
        Reference
        BillingProcessLevel      
            classic name is PROCESS-LEVEL
        Location				 is a InventoryLocation	
        Territory
        SalesRepresentative
            classic name is SALESMAN
		SalesRepresentative2 	 is a SalesRepresentative
		CommissionRate1          is a ChrgRate
            classic name is COMM-RATE-1
		CommissionRate2          is a ChrgRate
            classic name is COMM-RATE-2

		CommissionSplitPercentage
            classic name is COMM-SPLIT
        Frequency				 is AlphaUpper size 1
	        States
	            Weekly       value is "W"   
	            SemiMonthly  value is "S"    
	            Monthly      value is "M"
	            Quarterly    value is "Q"
	            Yearly       value is "Y"
	            UserDefined  value is "U"        										
        CycleCode                is AlphaUpper size 3         
        BillMonth				 is Numeric size 2			
        BillDay					 is Numeric size 2			  
        BillDayOfWeek            is a DayOfWeek
            classic name is BILL-DAY-OF-WK
        NumberOfInvoices         is Numeric size 3
            classic name is NBR-OF-INVCS
        StartDate
        StopDate                 is Date
        CurrentBillDate          is Date
            classic name is CURR-BILL-DATE
        Project
            classic name is ACTIVITY
        StandardCommentCode1     is a BillingStandardCommentHeader 
            classic name is STD-COMM1
        StandardCommentCode2     is a BillingStandardCommentHeader 
            classic name is STD-COMM2
        StandardCommentCode3     is a BillingStandardCommentHeader  
            classic name is STD-COMM3
        UserDefinedField1        is a TransUser1
            classic name is TRANS-USER1
        UserDefinedField2        is a S3Agent
            classic name is TRANS-USER2
        UserDefinedField3        is Date
            classic name is TRANS-USER3
        UserDefinedField4        is a TransUser4
            classic name is TRANS-USER4
        UserDefinedField5        is an InternationalAmount
            classic name is TRANS-USER5
        LastLineNumber           is a LineNumber
            classic name is LAST-LINE-NBR
        LastAddOnChargeSequence  is a Sequence3
            classic name is LAST-MISC-SEQ
        LastCommentSequence      is a Sequence3
            classic name is LAST-COMM-SEQ
		InUse			 		 is Boolean
            classic name is USED-FL
        TaxCode
        TaxExemptCode
            classic name is TAX-EXEMPT-CD
	
	Local Fields
		CurrencyRelationship
		
	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforInvoice"
    
    	ContextMessageText is a MessageField
			restricted
			"RecurringInvoiceParameter<RecurringInvoiceParameter><Description>ForCompany<Company>"
    Relations

		CustomerRecurringInvoicesRel is a CustomerRecurringInvoice set
			Instance Selection
				where (related.InvoiceCount > 0)
		
		CustomerRecurringInvoicesForStopDateRel is a CustomerRecurringInvoice set
			Instance Selection
				where (related.StopDate > StopDate)
					
		RecurringInvoiceCommentsRel is a RecurringInvoiceComment set
            Instance Selection
                where (related.RecurringInvoiceLine not entered)

		PriceListContractPromotionRel	
            one-to-one relation to PriceListContractPromotion
            valid when (IsListName)
            Field Mapping uses symbolic key
                related.Company                              = Company
                related.PriceListContractPromotion.Customer  = blank
                related.PriceListContractPromotion.ListName  = ListName
                related.PriceListContractPromotion.Promotion = blank

		InventoryCompanyRel
			one-to-one relation to InventoryCompany
			Field Mapping uses symbolic key
				related.Company 											= Company

		BillingItemGeneralLedgerAccountRel
			one-to-many relation to BillingItemGeneralLedgerAccount
			Field Mapping uses symbolic key
				related.Company												= Company
			Instance Selection
				where (related.BillingItemGeneralLedgerAccount.BillingProcessLevel = BillingProcessLevel)			
    
		RecurringInvoiceLinesRel is a RecurringInvoiceLine set								
		
		RecurringInvoiceLinesForStartDateRel is a RecurringInvoiceLine set
			Instance Selection
				where (related.StartDate < StartDate)
		
		RecurringInvoiceLinesForStopDateRel is a RecurringInvoiceLine set
			Instance Selection
				where (related.StopDate > StopDate)						
		
		RecurringInvoiceAddOnChargesRel is a RecurringInvoiceAddOnCharge set
			Instance Selection
				where (related.RecurringInvoiceLine not entered)
			
		
		RecurringInvoiceAddOnChargeForStartDateRel is a RecurringInvoiceAddOnCharge set
			Instance Selection
				where (related.StartDate < StartDate)
		
		RecurringInvoiceAddOnChargeForStopDateRel is a RecurringInvoiceAddOnCharge set
			Instance Selection
				where (related.StopDate > StopDate)						

		RecurringInvoiceAttachmentsRel is a RecurringInvoiceAttachment set
				
    Conditions

        IsDayGreaterThan15
        	restricted
            when (BillDay >  15)

        IsDayGreaterThan31
        	restricted
            when (BillDay >  31)

        IsBillDayRequired
        	restricted
            when (Frequency.SemiMonthly
            or    Frequency.Monthly
            or    Frequency.Quarterly
            or    Frequency.Yearly)
		
		IsBillMonthRequired
			restricted
			when (Frequency.Quarterly
			or    Frequency.Yearly)
			
        IsDebitInvoiceType
        	restricted
            when (!BillingInvoiceType.Credit)

        IsFrequencyNotChanged
        	restricted
            when (old Frequency = Frequency)

        IsInUse
        	restricted
            when (CurrentBillDate entered)

        IsInvoiceInBase
        	restricted
            when (Currency entered
            and   Currency = Company.Currency)

        IsListName
        	restricted
            when (ListName entered)

        IsLocationBasedPricing
        	restricted
            when (Company.LocationBasedPricing)

        IsLocationEntered
        	restricted
            when (Location entered)

        IsMonthGreaterThan3
        	restricted
            when (BillMonth >  3)

        IsNotInUse
        	restricted
            when (CurrentBillDate not entered)

        IsQuarterlyFrequency
        	restricted
            when (Frequency.Quarterly)

        IsSemiMonthlyFrequency
        	restricted
            when (Frequency.SemiMonthly)

        IsSalesmanEntered
        	restricted
            when (SalesRepresentative entered)

        IsSalesRepresentative2Entered
            classic name is SALESMAN2-ENTR
            restricted
            when (SalesRepresentative2 entered)

        IsStopDateLessThanStartDate
        	restricted
            when (StopDate entered
            and   StopDate <  StartDate)

		HasBillingInvoices								
			restricted
			when (CustomerRecurringInvoicesRel exists) 
		
		HasRecurringInvoiceParameter
			restricted
			when (RecurringInvoiceParameter exists)

    
    Sets

        Set2
            indexed
            Sort Order
                Company
                Frequency
                RecurringInvoiceParameter

    Field Rules
    	Company
    		constraint (InventoryCompanyRel exist)
    			"InventoryCompanyDoesNotExist"						
		
		Description
			required
				"RecurringInvoiceDescriptionIsRequired"				
       
        BillingInvoiceType
            required
			constraint (BillingInvoiceType.IsDebit)
				"DebitOnlyInvoiceTypeIsRequired"					
        
        Currency
        	if (!Company.MultipleCurrency)
        		default to Company.Currency
        		constraint (Company.Currency = Currency)
        			"CurrencyCodeMustBeBaseCurrency"				
       	
        	if (Currency != Company.Currency)
        		CurrencyRelationship.FromCurrency	= Currency
        		CurrencyRelationship.ToCurrency		= Company.Currency
        		constraint (CurrencyRelationship exist)
        			"CurrencyRelationDoesNotExist"				
        		
        Location
            required
			cannot be changed
		
		SalesRepresentative
			if (SalesRepresentative2 entered)
				required												
		
		SalesRepresentative2				
			constraint (SalesRepresentative != SalesRepresentative2)
				"SalesRepresentative1CannotBeSameAsSalesRepresentative2"				
        
        CommissionRate1
        	default to SalesRepresentative.CommisionPercent
        	if (SalesRepresentative not entered)
        		cannot be entered
        			"CannotEnterCommisionRateIfSalesRepresentiveIsNotEntered"		
        	
        	constraint (CommissionRate1 <= 100%)
        		"CommisionRateForSalesRepresentativeShouldNotBeGreaterThan100Percent"  
        
        CommissionRate2	
        	default to SalesRepresentative2.CommisionPercent
        	if (SalesRepresentative2 not entered)
        		cannot be entered
        			"CannotEnterCommisionRateIfSalesRepresentiveIsNotEntered"		
        	
        	constraint ((CommissionRate1 + CommissionRate2) <= 100%) 
        		"CommisionRateForSalesRepresentativesShouldNotBeGreaterThan100Percent"  
        
        CommissionSplitPercentage
        	if (SalesRepresentative2 entered)
        		required															
        	else
        		cannot be entered													
					"CannotEnterSplitPercentageWithoutSalesRepresentative2"			
        
        BillingProcessLevel
            required
			constraint (BillingItemGeneralLedgerAccountRel exist)
				"ItemGlobalLedgerAccountSetupIsIncompleteForThisProcessLevel"					
        	
			if (HasBillingInvoices)			
       			cannot be changed											
       				"CannotBeChanged;ProcessingHasBegun"				   
		
		Frequency
			required
			cannot be changed			
		
		BillMonth
			if (IsBillMonthRequired)
				required
			else
				cannot be entered		
			
			constraint (BillMonth <= 12)
				"BillMonthCannotBeGreaterThan12"						
				
			if (Frequency.Quarterly)									
				constraint (BillMonth <= 3)
					"EnterGivenMonthOfTheQuarterForBilling"				
			
			if (HasBillingInvoices)
				cannot be changed
		
		BillDay
			if (IsBillDayRequired)
				required
			else
				cannot be entered
			
			constraint (BillDay <= 31)
				"BillDayCannotBeGreaterThan31"						
			
			if (Frequency.SemiMonthly)
				constraint (BillDay <= 15)
					"BillDayCannotBeGreaterThan15ForSemiMonthly"				

			if (HasBillingInvoices)
				cannot be changed			
		
		BillDayOfWeek
			if (Frequency.Weekly)
				required		
			else
				cannot be entered

			constraint (BillDayOfWeek <= 7)
				"BillDayOfWeekCannotBeGreaterThan7"						
			
			if (HasBillingInvoices)
				cannot be changed		
		
		NumberOfInvoices
			if (HasBillingInvoices)
				cannot be changed											
			
		CycleCode
			cannot be changed
        
        StartDate
            required	
				"StartDateIsRequired"								
			
			constraint (RecurringInvoiceLinesForStartDateRel not exist)
				"InvoiceStartDateCannotBeGreaterThanLineStartDate"
			
			constraint (RecurringInvoiceAddOnChargeForStartDateRel not exist)
				"InvoiceStartDateCannotBeGreaterThanAddOnChargeStartDate"			
		
		StopDate
			if (NumberOfInvoices not entered)
				required
					"StopDateIsRequired"						
			else
				cannot be entered										
					"CannotEnterBothStopDateAndNumberOfInvoices"		
			
			constraint (StopDate >= StartDate)
				"StopDateCannotBeLessThanStartDate"					
			
			constraint (RecurringInvoiceLinesForStopDateRel not exist)
				"InvoiceStopDateCannotBeLessThanLineStopDate"		
			
			constraint (RecurringInvoiceAddOnChargeForStopDateRel not exist)
				"InvoiceStopDateCannotBeLessThanAddOnChargeStopDate" 
			
			constraint (CustomerRecurringInvoicesForStopDateRel not exist)
				"InvoiceStopDateCannotBeLessThanCustomerStopDate"		
		
		TaxCode
			if (TaxExemptCode.Exempt)
				cannot be entered											
		
	Actions	
		
		Create is a Create Action
			Field Rules
				CurrentBillDate
					cannot be entered
		
		Update is an Update Action
			Field Rules
				CurrentBillDate
					cannot be changed	

		Delete is a Delete Action
			Entrance Rules
				constraint (CurrentBillDate not entered)
					"CannotDelete;ProcessingHasBegun"								
