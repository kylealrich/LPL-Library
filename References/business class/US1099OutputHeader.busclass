US1099OutputHeader is a BusinessClass
    owned by ap
    prefix is UOH

    Ontology
        symbolic key is US1099OutputHeader

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields
    	FileName					is AlphaUpper 50
		CreateDate					is TimeStamp

        TaxGroup                	is a PayablesReportableIncomeGroup
        InvCurrency             	is a Currency
        ReportType              	is AlphaUpper size 1
            States
                Miscellaneous          value is "M"
                InterestIncome         value is "I"
                Dividend               value is "D"
                GovernmentPayments     value is "G"
                MerchCardAndThirdParty value is "K"
                    default label is "Merch Card and Third Party"
                NonEmployeeCompensation value is "N"
        TapeSelection        is AlphaUpper size 1
        	default label is "TapeType"
            States
                Original    				value is "O"
                Test   					    value is "T"
                Replacement 				value is "R"
                OneTransactionCorrectedG	value is "G"
                TwoTransactionCorrectedG	value is "Z"
                TwoTransactionCorrectedC	value is "C" 
    	TapeFile					is a Attachment
		IDMJobA				is an IDMJob
			protected
			delete ignored    	
		CopyA				is a DocumentPID
			protected
		IDMJobB				is an IDMJob
			protected
			delete ignored 	
		CopyB				is a DocumentPID
			protected
		ReportYear is Year
		ConsolidatedCopyBSingleVendorPerPage	is Boolean
			protected
			

	Transient Fields
    
    Local Fields

		IDMAttributes
		IDMGenerateDocument
		IDMItem	
		IDMBatchOperation
		LocalReportYear	is Alpha size 6
		    
    Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 10	
			(instance count of US1099OutputDetail set)

		DerivedReportType	is a DerivedField 
			type is Text
			restricted
			if (ReportType.Miscellaneous)
				return "1099-MISC" 
			else
			if (ReportType.InterestIncome)
				return "1099-INT"
			else
			if (ReportType.Dividend)
				return "1099-DIV"
    		else
    		if (ReportType.GovernmentPayments)
    			return "1099-G"
			else
			if (ReportType.MerchCardAndThirdParty)
				return "1099-K"
			else
			if (ReportType.NonEmployeeCompensation)
				return "1099-NEC"
		
		DerivedCopyBConsolidatedFormat is a DerivedField
			type is Text
			restricted
			if (ConsolidatedCopyBSingleVendorPerPage)
				return " - Consolidated Copy B - Single Vendor Per Page"
			else
				return " - Consolidated Copy B - Multiple Vendors Per Page"

		DerivedReportTypeDescription	is a DerivedField 
			type is Text
			restricted
			if (ReportType.Miscellaneous)
				return "Miscellaneous Income"
			else
			if (ReportType.InterestIncome)
				return "Interest Income"
			else
			if (ReportType.Dividend)
    			return "Dividends And Distributions"
    		else
    		if (ReportType.GovernmentPayments)
				return "Certain Government Payments"
			else
			if (ReportType.MerchCardAndThirdParty)
				return "Payment Card And Third Party Network Transactions"
			else
			if (ReportType.NonEmployeeCompensation)
				return "Non Employee Compensation"

		DerivedCopyAFileName is a DerivedField
			type is like IDMFileName	
			restricted	
			return DerivedReportType + "_Copy-A_" + DerivedReportYear + "_" +  TaxGroup.Name + ".pdf"

		
		DerivedCopyAIDMTemplateDocumentType	    is a DerivedField
			type is Numeric 3
			restricted
			if (ReportType.Dividend)
				return 28 
			else
    		if (ReportType.GovernmentPayments)
    			return 31 
			else
			if (ReportType.InterestIncome)
				return 34 
    		else
			if (ReportType.MerchCardAndThirdParty)
				return 37 
			else
			if (ReportType.Miscellaneous)
				return 40 
			else 
			if (ReportType.NonEmployeeCompensation)
				return 43 

		DerivedCopyBIDMTemplateDocumentType	is a DerivedField
			type is Numeric 3
			restricted
			if (ReportType.Dividend)
				return 51 
			else
    		if (ReportType.GovernmentPayments)
    			return 52 
			else
			if (ReportType.InterestIncome)
				return 53 
    		else
			if (ReportType.MerchCardAndThirdParty)
				return 54 
			else
			if (ReportType.Miscellaneous)
				return 55 
			else
			if (ReportType.NonEmployeeCompensation)
				return 56 

		DerivedReportYear is a DerivedField
			type is Alpha 4
			if (ReportYear entered)
				return ReportYear
			LocalReportYear = first US1099OutputDetailBRecordRel.Data
			return LocalReportYear[2:5]

		DerivedCopyAYear is a DerivedField 
			type is AlphaUpper 4
			restricted
			return ""

		DerivedCopyALink is a DerivedField
			type is Alpha 2083
			restricted
			if (CopyA entered)
				IDMItem.DocumentType	= "FSM_US1099Form"
				IDMItem.IDMUniqueId		= CopyA 	
				return IDMItem.GetLink
			return blank	

		DerivedCopyBLink is a DerivedField
			type is Alpha 2083
			restricted
			if (CopyB entered)
				IDMItem.DocumentType	= "FSM_US1099Form"
				IDMItem.IDMUniqueId		= CopyB	
				return IDMItem.GetLink
			return blank			

		DerivedCopyBYear is a DerivedField 
			type is AlphaUpper 4
			restricted
			return ""

		DerivedCopyBFileName is a DerivedField
			type is like IDMFileName	
			restricted		
			return DerivedReportType + "_Copy-B_" + DerivedReportYear + "_LaserCopy.pdf"

		VendorGroupText is a StringField
			type is Text
			default label is "VendorGroup"
			VendorGroup " - " VendorGroup.Description

		DerivedCopyBMessageField is a DerivedField
			type is Text
			if (ConsolidatedCopyBSingleVendorPerPage)
				return "Single Vendor Per Page Layout"
			else
				return "Multiple Vendors Per Page Layout"
			
    Conditions
		HasDetailAttachment
			restricted
			when (TapeFile entered)

		ValidForMassPrintOrEmailForm1099
			restricted
			when (US1099OutputDetailWithDocumentRel exists
			or CopyB entered)
			
		NoVendorEmailAddress
			restricted
			when (US1099OutputDetailNoVendorEmailAddressRel exists)
		
		NoPayerEmailAddress
			restricted
			when (US1099OutputDetailNoPayorEmailAddressRel exists)

		HasCopyA
    		restricted
    		when (CopyA entered)

		HasCopyB
    		restricted
    		when (CopyB entered)

		IsSupportedYear
			restricted
			when (DerivedReportYear = 2024
			or	  DerivedReportYear = 2023
			or	  DerivedReportYear = 2022)

		ValidForConsolidatedCopySingleVendor
			restricted
			when (DerivedReportYear >= 2024)
			
    Relations
    	US1099OutputDetailBRecordRel 
			one-to-many relation to US1099OutputDetail
    		Field Mapping uses BRecordType
    			related.VendorGroup 	   = VendorGroup
    			related.US1099OutputHeader = US1099OutputHeader

		US1099OutputDetailWithDocumentRel
			one-to-many relation to US1099OutputDetail
    		Field Mapping uses BRecordType
    			related.VendorGroup 	   = VendorGroup
    			related.US1099OutputHeader = US1099OutputHeader
 			Instance Selection
				where (related.HasGeneratedCopy)   

		US1099OutputDetailNoVendorEmailAddressRel
			one-to-many relation to US1099OutputDetail
    		Field Mapping uses BRecordType
    			related.VendorGroup 	   = VendorGroup
    			related.US1099OutputHeader = US1099OutputHeader
 			Instance Selection
				where (not related.HasVendorEmailAddress) 

		US1099OutputDetailNoPayorEmailAddressRel
			one-to-many relation to US1099OutputDetail
    		Field Mapping uses BRecordType
    			related.VendorGroup 	   = VendorGroup
    			related.US1099OutputHeader = US1099OutputHeader
 			Instance Selection
				where (not related.HasPayorEmailAddress) 

		UserDefaultPrinterRel
			one-to-one relation to UserDefaultPrinter
			Field Mapping uses symbolic key
				related.UserDefaultPrinter.Actor = actor

		IDMTemplateRel
			one-to-many relation to IDMTemplate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = VendorGroup.FinanceEnterpriseGroup
			Instance Selection
				where (related.IDMDocumentType 	= DerivedCopyAIDMTemplateDocumentType
				and    related.TemplateYear		= DerivedReportYear)

		IDMTemplateCopyBRel
			one-to-many relation to IDMTemplate
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = VendorGroup.FinanceEnterpriseGroup
			Instance Selection
				where (related.IDMDocumentType 	= DerivedCopyBIDMTemplateDocumentType
				and    related.TemplateYear		= DerivedReportYear)

		IDMConfigurationRel
			one-to-one relation to IDMConfiguration
			Field Mapping uses symbolic key
				related.IDMConfiguration = actor.context.FinanceEnterpriseGroup

    Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			VendorGroup
    			FileName
    			US1099OutputHeader

    	ByFileName
    		Sort Order
    			FileName
    			CreateDate				descending
    			VendorGroup
    			US1099OutputHeader

    Field Rules
        CreateDate
            default to current timestamp 

		FileName
			default to "US1099"
			
	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
			restricted

		CreateUS1099TapeFile is an Instance Action
			Action Rules
				trigger "US1099TapeFile" PA service
					resume on error
					title is "CreateUS1099TapeFile"
					Variables
						VendorGroup
						US1099OutputHeader
						FileName

		PurgeUS1099OutputRecords is a Set Action

			completion message is "PurgeOfUS1099OutputRecordsHasCompleted"
			Parameters
				PrmVendorGroup								is a VendorGroup
				PrmCreateDateRange							is a DateRange 
				
			Parameter Rules
				PrmCreateDateRange
					required
				PrmVendorGroup
					required
					
			Local Fields
				AsyncId			is an AsyncActionRequest	
				
			Instance Selection
				where (VendorGroup = PrmVendorGroup
				and    CreateDate date within PrmCreateDateRange)
	
			Action Rules
				Instance Rules
					if (VendorGroup.EnableUS1099IDMForms)
						if (CopyA entered)
							invoke ArchiveCopyAIDM1099Forms in background
								assign async action request id to AsyncId
								
						if (US1099OutputDetailWithDocumentRel exists)
							invoke ArchiveCopyBAndCIDM1099Forms US1099OutputDetail in background
								run after AsyncId
								assign async action request id to AsyncId
								invoked.PrmVendorGroup  	  = VendorGroup
								invoked.PrmUS1099OutputHeader = US1099OutputHeader
						
						invoke PurgeIntance in background
							run after AsyncId
					else
						invoke Purge


		PurgeIntance is an Instance Action
			default label is untranslatable	
			restricted
			Action Rules
				invoke Purge
					

		MassGenerateForm1099 is an Instance Action
			valid when (IsSupportedYear)
			Parameters
				GenerateCopyA		is Boolean
					default label is "GenerateCopyA_-_ForInternalRevenueServiceCenter"
				GenerateCopyB		is Boolean
					default label is "GenerateCopyB_-_ForRecipient"
				CopyBIndividualCopies is Boolean
					default label is "IndividualCopies"
				CopyBConsolidatedCopy is Boolean	
					default label is "ConsolidatedCopy"
				GenerateCopyC		is Boolean
					default label is "GenerateCopyC_-_ForPayer"
				PrintCopyA	 		is Boolean
				PrintCopyB   		is Boolean
				PrintCopyBIndividualCopies is Boolean
				PrintCopyBConsolidatedCopy is Boolean
				PrintCopyC   		is Boolean
				CopyAPrinter		is an IDMPrinter
					default label is "Printer"
				CopyBPrinter		is an IDMPrinter
					default label is "Printer"
				CopyCPrinter		is an IDMPrinter
					default label is "Printer"
				EmailCopyBToVendors	is Boolean
					default label is "EmailIndividualCopyBToVendors"
				EmailCopyCToPayer	is Boolean
				ConsolidatedCopyBOption is Numeric size 1
					States
						MultipleVendorsPerPage 	value is 0
						SingleVendorPerPage		value is 1

        	Parameter Rules
        		GenerateCopyA
        			if (not GenerateCopyA
        			and not GenerateCopyB
        			and not GenerateCopyC)
        				required
        					"AtLeastOneGenerateCopyIsNeededToProceed"	
					
				CopyBIndividualCopies 
					if (GenerateCopyB)
						if (not CopyBIndividualCopies
						and not CopyBConsolidatedCopy)
							required
								"EitherIndividualCopiesOrConsolidatedCopyIsNeededToProceed"							
					else
						initialize
						
				CopyBConsolidatedCopy 
					if (not GenerateCopyB)
						initialize
					
        		PrintCopyA
        			if (not GenerateCopyA)
        				initialize
        		
        		PrintCopyB
        			if (not GenerateCopyB)
        				initialize
 
				PrintCopyBIndividualCopies
					if (PrintCopyB)
						if not (CopyBIndividualCopies)
							cannot be entered
								"MustGenerateCopyBIndividualCopies"
								
						if (not PrintCopyBIndividualCopies
						and not PrintCopyBConsolidatedCopy)
							required
								"EitherIndividualCopiesOrConsolidatedCopyIsNeededToProceed"		
					else
						initialize
						
				PrintCopyBConsolidatedCopy
					if (PrintCopyB)
						if (not CopyBConsolidatedCopy)
							cannot be entered
								"MustGenerateCopyBConsolidatedCopy"
					else
						initialize
						       		
        		PrintCopyC
        			if (not GenerateCopyC)
        				initialize
        	
				CopyAPrinter
					initial value is UserDefaultPrinterRel.IDMPrinter
					if (PrintCopyA)
						required
					else
						initialize
				
				CopyBPrinter
					initial value is UserDefaultPrinterRel.IDMPrinter
					if (PrintCopyB)
						required
					else
						initialize
				
				CopyCPrinter
					initial value is UserDefaultPrinterRel.IDMPrinter
					if (PrintCopyC)
						required
					else
						initialize
				
				EmailCopyBToVendors
        			if (not GenerateCopyB)
        				initialize
        				
				EmailCopyCToPayer					
        			if (not GenerateCopyC)
        				initialize

			Rule Blocks
				UpdateConsolidatedCopyBFormat
					initialize invoked.CopyB
					if (ConsolidatedCopyBOption.SingleVendorPerPage)
						invoked.ConsolidatedCopyBSingleVendorPerPage = true
					else 
						initialize invoked.ConsolidatedCopyBSingleVendorPerPage

			Action Rules
				if (ReportYear not entered)
					confirmation required
						"GeneratingUS1099IDMDocumentForReportYear<DerivedCopyBYear>.Continue?"
					invoke Update
						invoked.ReportYear = DerivedReportYear
						if (CopyBConsolidatedCopy)
							include UpdateConsolidatedCopyBFormat
				else 
					if (CopyBConsolidatedCopy)
						invoke Update
							include UpdateConsolidatedCopyBFormat

				if (GenerateCopyA)
					invoke GenerateCopyA in background
						invoked.PrmPrintCopyA      = PrintCopyA	 
						invoked.PrmCopyAPrinter	   = CopyAPrinter

				if (GenerateCopyB
				and CopyBConsolidatedCopy)
					invoke GenerateCopyB in background
						if (PrintCopyBConsolidatedCopy)
							invoked.PrmPrintCopyB      = PrintCopyB	 
							invoked.PrmCopyBPrinter	   = CopyBPrinter					
				
				if ((GenerateCopyB and CopyBIndividualCopies)
				or  GenerateCopyC)						
					invoke GenerateIDM1099FormSet US1099OutputDetail
		        		invoked.PrmVendorGroup         = VendorGroup
		        		invoked.PrmUS1099OutputHeader  = US1099OutputHeader
						invoked.PrmGenerateCopyB	   = GenerateCopyB		
						invoked.PrmGenerateCopyC	   = GenerateCopyC	
						if (PrintCopyB and PrintCopyBIndividualCopies)	
							invoked.PrmPrintCopyB	   = PrintCopyB   		
							invoked.PrmCopyBPrinter	   = CopyBPrinter		
						invoked.PrmPrintCopyC          = PrintCopyC   		
						invoked.PrmCopyCPrinter        = CopyCPrinter		
						invoked.PrmEmailCopyBToVendors = EmailCopyBToVendors
						invoked.PrmEmailCopyCToPayer   = EmailCopyCToPayer



		GenerateCopyA is an Instance Action
			default label is untranslatable	
			restricted
			Parameters
				PrmPrintCopyA     is Boolean
				PrmCopyAPrinter   is an IDMPrinter
				
			Local Fields
				IDMJobAView			is an IDMJob view
				
			Action Rules
    			initialize IDMGenerateDocument
    			
    			IDMGenerateDocument.IDMXMLDefinition.Busclass	     		= reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName     			= "US1099IDMOutputList"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName 			= "US1099Form"
 
	 			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName	 = "US1099OutputDetailBRecordRel"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName		 = "US1099OutputDetailIDMList"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1 = 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag		 = "Recipients"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag		 = "Recipient"
    			
    			initialize IDMAttributes
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "VendorGroup"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= VendorGroup
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "ReportType"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= DerivedReportType
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "Description"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue	= DerivedReportTypeDescription
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "Copy"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= "Copy A - For Internal Revenue Service Center"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "PayerTaxID" 
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= TaxGroup.TaxID 
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "RecipientTaxID" 
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= blank				
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeName	= "PrintType" 
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeValue	= "Original Form"
				IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeName  = "Date"
				IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeDate	= CreateDate date
				IDMAttributes.SingleValue.IDMAttribute[9].IDMAttributeName  = "RecipientName"
				IDMAttributes.SingleValue.IDMAttribute[9].IDMAttributeValue	= blank
				IDMAttributes.SingleValue.IDMAttribute[10].IDMAttributeName  = "TapeType"
				IDMAttributes.SingleValue.IDMAttribute[10].IDMAttributeValue = TapeSelection					
					
     			IDMGenerateDocument.DocumentType 							= "FSM_US1099Form"
  			    IDMGenerateDocument.IDMAccessControlList 				    = "CSFDefined"	 			
    			IDMGenerateDocument.FileName							    = DerivedCopyAFileName				
				IDMGenerateDocument.TemplateUniqueId 						= IDMTemplateRel.IDMUniqueId
    			IDMGenerateDocument.IDMAttributes	 						= IDMAttributes
			
				if (PrmPrintCopyA)
					IDMGenerateDocument.IDMPrinter = PrmCopyAPrinter
					
    			invoke CreateFromGenerateDocument IDMJob
    				assign result to IDMJobAView
    				invoked.Actor = actor
	    			invoked.Description			= DerivedReportType
    				invoked.IDMGenerateDocument	= IDMGenerateDocument
 
 				IDMJobA = IDMJobAView.IDMJob
 				
				invoke UpdateCopyA in background
					run after IDMJobA.AsyncId
					

		UpdateCopyA is an Instance Action
			default label is untranslatable	
			restricted
			Action Rules
				if (IDMJobA.GenerationStatus.Finished
				and IDMJobA.MDSID entered)
           			CopyA = IDMJobA.MDSID 


		GenerateCopyB is an Instance Action
			default label is untranslatable	
			restricted
			Parameters
				PrmPrintCopyB     is Boolean
				PrmCopyBPrinter   is an IDMPrinter
				
			Local Fields
				IDMJobBView			is an IDMJob view
				
			Action Rules
    			initialize IDMGenerateDocument
    			
    			IDMGenerateDocument.IDMXMLDefinition.Busclass	     		= reference to this instance
    			IDMGenerateDocument.IDMXMLDefinition.ListName     			= "US1099IDMGenerateList"
    			IDMGenerateDocument.IDMXMLDefinition.DocumentName 			= "US1099Form"
 
	 			IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName	 = "US1099OutputDetailBRecordRel"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName		 = "US1099OutputDetailIDMList"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1 = 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag		 = "Recipients"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag		 = "Recipient"
    			
    			initialize IDMAttributes
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "VendorGroup"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= VendorGroup
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "ReportType"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= DerivedReportType
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "Description"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue	= DerivedReportTypeDescription
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "Copy"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeValue	= "Copy B - For Recipient"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "PayerTaxID" 
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= TaxGroup.TaxID 
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "PrintType" 
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= "Laser Copy"
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeName  = "Date"
				IDMAttributes.SingleValue.IDMAttribute[7].IDMAttributeDate	= CreateDate date
				IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeName  = "TapeType"
				IDMAttributes.SingleValue.IDMAttribute[8].IDMAttributeValue = TapeSelection	
					
     			IDMGenerateDocument.DocumentType 							= "FSM_US1099Form"
  			    IDMGenerateDocument.IDMAccessControlList 				    = "CSFDefined"	 			
    			IDMGenerateDocument.FileName							    = DerivedCopyBFileName				
				IDMGenerateDocument.TemplateUniqueId 						= IDMTemplateCopyBRel.IDMUniqueId
    			IDMGenerateDocument.IDMAttributes	 						= IDMAttributes
			
				if (PrmPrintCopyB)
					IDMGenerateDocument.IDMPrinter = PrmCopyBPrinter
					
    			invoke CreateFromGenerateDocument IDMJob
    				assign result to IDMJobBView
    				invoked.Actor = actor
	    			invoked.Description			= DerivedReportType + DerivedCopyBConsolidatedFormat
    				invoked.IDMGenerateDocument	= IDMGenerateDocument
 
 				IDMJobB = IDMJobBView.IDMJob
 				
				invoke UpdateCopyB in background
					run after IDMJobB.AsyncId
					

		UpdateCopyB is an Instance Action
			default label is untranslatable	
			restricted
			Action Rules
				if (IDMJobB.GenerationStatus.Finished
				and IDMJobB.MDSID entered)
           			CopyB = IDMJobB.MDSID 


		MassPrintForm1099    is an Instance Action
			valid when (ValidForMassPrintOrEmailForm1099)
			Parameters
				PrintCopyA	 		is Boolean
				PrintCopyB   		is Boolean
				CopyBPrintOption	is Numeric 1
					States
						IndividualCopies value is 0
						ConsolidatedCopy value is 1
				PrintCopyC   		is Boolean
				CopyAPrinter		is an IDMPrinter
					default label is "Printer"
				CopyBPrinter		is an IDMPrinter
					default label is "Printer"
				CopyCPrinter		is an IDMPrinter
					default label is "Printer"
									
			Parameter Rules
        		PrintCopyA
        			if (not PrintCopyA
        			and not PrintCopyB
        			and not PrintCopyC)
        				required
        					"AtLeastOnePrintCopyIsNeededToProceed"      
        			initial value is true  
        					  		
        		PrintCopyB
        			initial value is true
        		
        		CopyBPrintOption
	        		if (CopyBPrintOption.IndividualCopies)
	        			constraint (US1099OutputDetailWithDocumentRel exists)
	        				"IndividualCopyBNotYetGenerated"
        			if (CopyBPrintOption.ConsolidatedCopy)
	        			constraint (CopyB entered)
	        				"Consolidated_\\Copy_\\BNotYetGenerated"
        			
        		PrintCopyC
        			initial value is true

				CopyAPrinter
					initial value is UserDefaultPrinterRel.IDMPrinter
					if (PrintCopyA)
						required
					else
						initialize
				
				CopyBPrinter
					initial value is UserDefaultPrinterRel.IDMPrinter
					if (PrintCopyB)
						required
					else
						initialize
				
				CopyCPrinter
					initial value is UserDefaultPrinterRel.IDMPrinter
					if (PrintCopyC)
						required 
        			else
        				initialize	
        			
			Action Rules
				if (PrintCopyA)
					invoke PrintCopyA
		        		invoked.PrmCopyAPrinter		  = CopyAPrinter

				if (PrintCopyB 
				and CopyBPrintOption.ConsolidatedCopy)
					invoke PrintCopyB
						invoked.PrmCopyBPrinter       = CopyBPrinter

	        	if (PrintCopyC
	        	or (PrintCopyB and CopyBPrintOption.IndividualCopies))
					invoke PrintIDM1099FormSet US1099OutputDetail
		        		invoked.PrmVendorGroup        = VendorGroup
		        		invoked.PrmUS1099OutputHeader = US1099OutputHeader
		        		if (CopyBPrintOption.IndividualCopies)
		        			invoked.PrmPrintCopyB	  = PrintCopyB
		        		invoked.PrmPrintCopyC		  = PrintCopyC	
						invoked.PrmCopyBPrinter		  = CopyBPrinter		
						invoked.PrmCopyCPrinter       = CopyCPrinter		        		


		PrintCopyA is an Instance Action
			default label is untranslatable	
			restricted
			Parameters
				PrmCopyAPrinter is an IDMPrinter
				
			Action Rules
				initialize IDMItem
				IDMItem.DocumentType = "FSM_US1099Form"
							
				IDMItem.IDMUniqueId	 = CopyA
				IDMItem.IDMPrinter   = PrmCopyAPrinter				
				
				constraint (IDMItem.GetItemDetails)
					"DocumentDoesNotExistInIDM"

				IDMItem.IDMPID     = IDMItem.IDMItemDetails.PID
				
				invoke SendToPrinter IDMJob
					invoked.Description = DerivedReportType
					invoked.FileName	= IDMItem.IDMItemDetails.FileName
					invoked.IDMItem		= IDMItem


		PrintCopyB is an Instance Action
			default label is untranslatable	
			restricted
			Parameters
				PrmCopyBPrinter is an IDMPrinter
				
			Action Rules
				initialize IDMItem
				IDMItem.DocumentType = "FSM_US1099Form"
							
				IDMItem.IDMUniqueId	 = CopyB
				IDMItem.IDMPrinter   = PrmCopyBPrinter				
				
				constraint (IDMItem.GetItemDetails)
					"DocumentDoesNotExistInIDM"

				IDMItem.IDMPID     = IDMItem.IDMItemDetails.PID
				
				invoke SendToPrinter IDMJob
					invoked.Description = DerivedReportType + DerivedCopyBConsolidatedFormat
					invoked.FileName	= IDMItem.IDMItemDetails.FileName
					invoked.IDMItem		= IDMItem


		MassEmailForm1099	is an Instance Action
			valid when (ValidForMassPrintOrEmailForm1099)
			Parameters
				EmailCopyBToVendors	   is Boolean
					default label is "EmailIndividualCopyBToVendors"
				EmailCopyCToPayer 	   is Boolean
			
			Parameter Rules
				EmailCopyBToVendors
					initial value is true
					
					if (not EmailCopyBToVendors
					and not EmailCopyCToPayer)
						required
						 	"AtLeastOneEmailCopyIsNeededToProceed"
						
				EmailCopyCToPayer	   				
					initial value is true
			
			Action Rules
				invoke EmailIDM1099FormSet US1099OutputDetail
	        		invoked.PrmVendorGroup         = VendorGroup
	        		invoked.PrmUS1099OutputHeader  = US1099OutputHeader				
					invoked.PrmEmailCopyBToVendors = EmailCopyBToVendors
					invoked.PrmEmailCopyCToPayer   = EmailCopyCToPayer			


		ArchiveCopyAIDM1099Forms is an Instance Action
			default label is untranslatable	
			restricted
			Local Fields
				LocalExecute			is Boolean
				AttrSearch				is an IDMAttributeOccurs
				
			Action Rules
				initialize AttrSearch
				AttrSearch.IDMAttribute[1].IDMAttributeName		= "VendorGroup"
				AttrSearch.IDMAttribute[1].IDMAttributeValue	= VendorGroup
				AttrSearch.IDMAttribute[2].IDMAttributeName		= "ReportType"
				AttrSearch.IDMAttribute[2].IDMAttributeValue	= DerivedReportType
				AttrSearch.IDMAttribute[3].IDMAttributeName		= "Copy"
				AttrSearch.IDMAttribute[3].IDMAttributeValue	= "Copy A - For Internal Revenue Service Center"
				AttrSearch.IDMAttribute[4].IDMAttributeName		= "PayerTaxID" 
				AttrSearch.IDMAttribute[4].IDMAttributeValue	= TaxGroup.TaxID
				AttrSearch.IDMAttribute[5].IDMAttributeName		= "PrintType" 
				AttrSearch.IDMAttribute[5].IDMAttributeValue	= "Original Form"
				AttrSearch.IDMAttribute[6].IDMAttributeName     = "Date"
				AttrSearch.IDMAttribute[6].IDMAttributeDate		= CreateDate date
								
				IDMBatchOperation.AttributeSearch				= AttrSearch
				IDMBatchOperation.IDMDocumentType				= "FSM_US1099Form"
				LocalExecute									= IDMBatchOperation.ArchiveByAttributes

	        							
