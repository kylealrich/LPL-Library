ProjectVendorContract is a BusinessClass
	owned by Projects
	prefix is pvc
	
	Ontology
		part of Project
			relative key is SequenceNumber is Numeric size 4
	
	Patterns
	
	Persistent Fields
#ifdef module po	
		ContractGroup
		Contract
		Vendor
#endif		
		VendorType				is Numeric 1
			States	
				Contractor		value is 1			
				Supplier		value is 2
				Other			value is 3

	Transient Fields
	
		TransientStructure		is a ProjectStructure	
			derive value from FinanceEnterpriseGroup.EnterpriseProjectStructure				
	
	Context Fields
		ProjectContract
		
    Field Rules
    
    	SequenceNumber
    		autosequence
    		
    	Project
    		if (Project.ProjectType.Summary)
    			constraint (ProjectContract.Project = Project)
    				"<FinanceEnterpriseGroup.ProjectLabel>MustBeFor<FinanceEnterpriseGroup.ProjectLabel>Contract<ProjectContract.DisplayContract>"
			if (Project.ProjectType.Posting)
	    		constraint (Project.ProjectType.Posting)
	    			"MustBePosting<FinanceEnterpriseGroup.ProjectLabel>"
				constraint (BelongsToProjectContract)  
					"Posting<FinanceEnterpriseGroup.ProjectLabel>MustBeWithin<FinanceEnterpriseGroup.ProjectLabel>Contract<ProjectContract.DisplayContract>"
				constraint (Project.Active)
					"Posting<FinanceEnterpriseGroup.ProjectLabel>MustBeActive"
    			
#ifdef module po    		
		ContractGroup
			required
			 
		Contract
			if (Vendor not entered)
				required
					"ContractOrVendorRequired"							 
		Vendor
			default to Contract.Vendor
			if (Contract not entered)
				required
					"VendororContractRequired"							 
#endif		  	
	
		VendorType
			required
				default to VendorType.Contractor
				
    Derived Fields
    				
		BlankField is a DerivedField
			type is Alpha 1
			return blank
				
				 		
    Conditions
#ifdef module po   
    	HasContract
			restricted    	
    		when (ContractRel exists)

    	HasVendor
			restricted    	
    		when (VendorRel exists)
#endif    		
    	BelongsToProjectContract
    		when (ShadowPostingDescendantRel exists)	
		
	Relations
	
		ShadowPostingDescendantRel
        	one-to-one relation to ProjectShadow
            Field Mapping uses BySummaryProject
            	related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
                related.SummaryProject         = ProjectContract
                related.Project                = Project
                           
#ifdef module po    

		ContractRel
			one-to-one relation to Contract
			Field Mapping uses ByVendor
				related.ContractGroup = ContractGroup
				related.Vendor		  = Vendor
				related.Contract	  = Contract
				
		VendorRel
			one-to-one relation to Vendor
			Field Mapping uses symbolic key
				related.VendorGroup	= ContractGroup
				related.Vendor		= Vendor
				
	Actions
		Create is a Create Action
			Action Rules
				if (Project not entered)
					Project = ProjectContract.Project
		
		Update is an Update Action
		
		Delete is a Delete Action	
				
    Sets
    	
    	ByContractDescending
    		Sort Order
    			FinanceEnterpriseGroup
    			Project
				ContractGroup    			
    			Contract descending
				Vendor 
    
    	ByVendor
    		Sort Order
    			FinanceEnterpriseGroup
				Project    			
				ContractGroup    			
    			Vendor
    			Contract
    			
    	ByContractVendor
    		Sort Order
    			FinanceEnterpriseGroup
    			ContractGroup
    			Contract 
    			Vendor
				Project
				
    	ByVendorContract
    		Sort Order
    			FinanceEnterpriseGroup
    			ContractGroup
    			Vendor 
				Contract    			
				Project
#endif    
