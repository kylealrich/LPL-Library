AnalyticsSpaceManagement is a BusinessClass
	owned by filecreation
	
	prefix is AnSp
	
	Ontology
		symbolic key is AnalyticsSpaceManagement
		
	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		SpaceID 			is Alpha size 100
		SpaceOwner			is Alpha size 200
		BackupExpiration 	is Date
		LockBackup  		is Boolean
			
	Local Fields 
		LocalBackUpSpaceName		is a AnalyticsSpaceManagement  
						
	Derived Fields
		
		DerivedBirstApiUserName is a DerivedField
			type is Alpha size 100
			restricted
			return BirstApiUserNameRel.Value
		
		DerivedBirstApiUserPassword is a DerivedField		
			type is Alpha size 100
			restricted
			return BirstApiUserPasswordRel.Value
			
		DerivedBirstApiURL is a DerivedField				
			type is Alpha size 100
			restricted
			return BirstApiURLRel.Value

		DerivedBirstApiTenantID is a DerivedField			
			type is Alpha size 100
			restricted
			return BirstApiTenantIDRel.Value

		CancelMessage is a MessageField
            restricted
            "DoYouReallyWantToDeleteTheSelectedSpaces?"
			
	Relations
		
		SpaceManagementRel									
			one-to-many relation to AnalyticsSpaceManagement
			Field Mapping uses symbolic key

		BirstApiUserNameRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "BirstApiUserName"
				
		BirstApiUserPasswordRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "BirstApiUserPassword"
					
		BirstApiURLRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "BirstApiURL"
				
		BirstApiTenantIDRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "TenantID"
			
		AnalyticsConfigurationsRel
			one-to-many relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key	
		
		BackUpSpaceRel
			one-to-one relation to AnalyticsSpaceManagement
			Field Mapping uses symbolic key
				related.AnalyticsSpaceManagement = LocalBackUpSpaceName

	Actions
		Create is an Action

		Update is an Action
		
		Delete is an Action
	
		SyncBirstSpace is a Set Action
			no records message is "ListOfSpacesUpdatedSuccessfully"
			run in foreground
			Instance Selection
				where (false)
			Action Rules
				Empty Set Rules
					constraint(first AnalyticsConfigurationsRel.BirstLoginStatus)
						"FailedToSyncSpaces._BirstUsernameAndPassword,BirstApiUrlAndTenantIDMustBeCorrect"
					invoke GetSpaceList 
		
		GetSpaceList is an Instance Action
			restricted
			Action Rules

							
		BackupSpace is an Instance Action
			completion message is "BackupSpaceCompletedSuccessfully."
			Parameters
				BackupExpiration 	is Date
				LockBackup  		is Boolean

			Parameter Rules
				BackupExpiration
					required

			Action Rules
				constraint(first AnalyticsConfigurationsRel.BirstLoginStatus)
					"FailedCreatingBackUpSpace._BirstUsernameAndPassword,BirstApiUrlAndTenantIDMustBeCorrect"
				constraint (BackupExpiration > current timestamp)
					"BackupExpirationShouldBeGreaterThanCurrentDate"


			Exit Rules
				invoke Update BackUpSpaceRel
					invoked.BackupExpiration=BackupExpiration
					invoked.LockBackup=LockBackup
				invoke SyncBirstSpace
		
		DeleteSpace is an Instance Action
			confirmation required
				"<CancelMessage>"
			completion message is "BirstSpace/sDeletedSuccessfully."
			Action Rules
				constraint(first AnalyticsConfigurationsRel.BirstLoginStatus)
					"FailedDeletingSpace._BirstUsernameAndPassword,BirstApiUrlAndTenantIDMustBeCorrect"
				constraint(SpaceOwner = DerivedBirstApiUserName)
					"CannotDeleteSpaceAsOwnerIsNotSameForTheParticularSpace"
			Exit Rules
				invoke Delete

			
		DeleteExpiredBackupSpaces is a Set Action
			confirmation required
				"<CancelMessage>"
			completion message is "ExpiredBackupSpacesAreDeletedSuccessfully."
			no records message is "NoExpiredBackupsToDelete"
			run in foreground
			Instance Selection
				where (BackupExpiration entered and BackupExpiration <= system current corporate date and !LockBackup)
			Action Rules
				Instance Rules
					if(BackupExpiration entered and BackupExpiration <= system current corporate date and !LockBackup)
						invoke DeleteSpace				
				
				
			
			
				
