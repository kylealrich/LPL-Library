CustomerRepContactPreMigrationIssues is a BusinessClass
	owned by ar
	prefix is CRCPI
	sql name is CRCPMI

	Ontology
		symbolic key is CustomerRepContactPreMigrationIssues

	Patterns
		disable Auditing
		disable EffectiveDated

	Persistent Fields
		CustomerGroup
		CustomerRepresentative
			delete ignored
		CustomerRepresentativeContact
			delete ignored
		CustomerRepresentativeContactEmailAddress	is like EmailAddress
		CustomerRepresentativeContactName			is like PersonName
		ActorEmailAddress							is like EmailAddress
		ActorName									is like PersonName
		ProcessMessage								is Alpha 150
		DuplicateCustomerGroup						is like CustomerGroup
		DuplicateCustomerRepresentative				is like CustomerRepresentative
		DuplicateCustomerRepresentativeContact		is like CustomerRepresentativeContact
		DuplicateEmailAddress						is like EmailAddress
		DuplicateName								is like PersonName
		PreventsMigration							is Boolean


		RunMode										is Numeric 1
			States
				ReportOnly					value is 0
				ReportAndUpdate				value is 1
		RunDateStart								is TimeStamp
		RunDateEnd									is TimeStamp
		CustomerRepresentativeContactCount			is Numeric 9  
		CustomerRepresentativeContactErrorCount		is Numeric 9  
		MissingActorCount							is Numeric 9
		ContactAndActorMissingEmailCount			is Numeric 9
		ContactMissingEmailCount					is Numeric 9
		ActorMissingEmailCount						is Numeric 9
		ContactAndActorEmailMismatchCount			is Numeric 9
		ContactAndActorMissingFirstNameCount		is Numeric 9
		ContactMissingFirstNameCount				is Numeric 9
		ActorMissingFirstNameCount					is Numeric 9
		ContactAndActorFirstNameMismatchCount		is Numeric 9
		ContactAndActorMissingLastNameCount			is Numeric 9
		ContactMissingLastNameCount					is Numeric 9
		ActorMissingLastNameCount					is Numeric 9
		ContactAndActorLastNameMismatchCount		is Numeric 9
		DuplicateEmailAddressCount					is Numeric 9

		
	Derived Fields
		RunModeMessage is a DerivedField
			type is Alpha 50
			if (RunMode.ReportOnly)
				return ReportModeUpdateMessage
			else
				return UpdateModeUpdateMessage

		ReportModeUpdateMessage is a MessageField
			"Program_Will_Fix_In_Update_Mode"
			
		UpdateModeUpdateMessage is a MessageField
			"Program_Fixed"

		FixManualMessage is a MessageField
			"Fix_Manually"

		TotalRecordsWithIssuesMessage is a MessageField
			"(SomeRecordsMayHaveMoreThan1Issue)"

		HeaderProgramFixMessage is a DerivedField
			type is Alpha 50
			if (RunMode.ReportOnly)
				return HeaderReportModeFixMessage
			else
				return HeaderUpdateModeFixMessage

		HeaderReportModeFixMessage is a MessageField
			"SystemWillFixInUpdateMode"

		HeaderUpdateModeFixMessage is a MessageField
			"SystemFixedInUpdateMode"

		PremigrationCompleteMF is a MessageField
			"Congratulations!_Customer_Representative_Contact_PremigrationCompleted._No_PremigrationIssuesFound."

	Conditions
		IsSummary
			when (CustomerRepContactPreMigrationIssues.RecordType.Summary)

		IsDetail
			when (CustomerRepContactPreMigrationIssues.RecordType.Detail)

		HasIssueRecords
			when (CustomerRepresentativeContactErrorCount > 0)

		IsForDuplicateEmail
			when (DuplicateCustomerGroup entered)

		SummaryExists
			when (CustomerRepContactPreMigrationIssuesSummaryRel exists)

	Relations
		IssuesDetailsForNonDuplicatesRel
			one-to-many relation to CustomerRepContactPreMigrationIssues
			Field Mapping uses ByRecordType 
				related.CustomerRepContactPreMigrationIssues.RecordType	= 2 
			Instance Selection
				where (!related.IsForDuplicateEmail)

		IssuesDetailsForDuplicatesRel
			one-to-many relation to CustomerRepContactPreMigrationIssues
			Field Mapping uses ByRecordType 
				related.CustomerRepContactPreMigrationIssues.RecordType	= 2 
			Instance Selection
				where (related.IsForDuplicateEmail)

		CustomerRepresentativeContactRel
			one-to-one relation to CustomerRepresentativeContact
			Field Mapping uses symbolic key
				related.CustomerGroup					= CustomerGroup
				related.CustomerRepresentative			= CustomerRepresentative
				related.CustomerRepresentativeContact	= CustomerRepresentativeContact

		DuplicateCustomerRepresentativeContactRel
			one-to-one relation to CustomerRepresentativeContact
			Field Mapping uses symbolic key
				related.CustomerGroup					= DuplicateCustomerGroup
				related.CustomerRepresentative			= DuplicateCustomerRepresentative
				related.CustomerRepresentativeContact	= DuplicateCustomerRepresentativeContact

		CustomerRepresentativeContactActorRel
			one-to-one relation to Actor
			Field Mapping uses symbolic key
				related.Actor		= CustomerRepresentativeContact.LoginName

		CustomerRepContactPreMigrationIssuesSummaryRel
			one-to-one relation to CustomerRepContactPreMigrationIssues
			Field Mapping uses symbolic key
				related.CustomerRepContactPreMigrationIssues.RecordType	= 1 
				related.CustomerRepContactPreMigrationIssues.Sequence	= 1

	Sets
		ByRecordType
			Sort Order
				CustomerRepContactPreMigrationIssues.RecordType
				CustomerRepContactPreMigrationIssues.Sequence


	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted

		ExternalEntityPreMigrationCheck is a Set Action
			Parameters
				PrmRunMode			is Numeric 1
					States
						ReportOnly			value is 0
						ReportAndUpdate		value is 1

			Action Rules
				Empty Set Rules
					invoke ExternalEntityPreMigrationCheck CustomerRepresentativeContact
						invoked.PrmRunMode = PrmRunMode
				Set Rules
					Entrance Rules
						invoke ExternalEntityPreMigrationCheck CustomerRepresentativeContact
							invoked.PrmRunMode = PrmRunMode
