PricingWorksheetItem is a BusinessClass
    owned by bl
    prefix is OWP
    classic name is OEWKPRICE

    Ontology
        symbolic key is PricingWorksheetItem
            classic set name is OWPSET1
            classic name is ITEM
            classic name for PricingWorksheet is WORK-NAME

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        NextEffectiveDate                   is Date
            classic name is NXT-EFF-DATE
        NextMultipleSellPriceUnitsOfMeasure is a SingleOrMultiplePricingUOM
            sql name is NMSellPriceUnitsOfMeasure
            classic name is NXT-MULT-SPR
		NextSellPriceByUOM					is a SellPriceInWorksheetItemArray   
			classic name for NextSellPriceByUOM.SellPriceInWorksheetItem.UnitOfMeasure is NXT-SPR-UOM
			classic name for NextSellPriceByUOM.SellPriceInWorksheetItem.UOMMultiplier is NXT-SPR-MULT
			classic name for NextSellPriceByUOM.SellPriceInWorksheetItem.SellPriceCode is NXT-SPR-CD
			classic name for NextSellPriceByUOM.SellPriceInWorksheetItem.SellPrice is NXT-PRICE
		
    Local Fields
	
		LoopCounter							is Numeric 2
		LocalUOM							is a UnitOfMeasure

    Relations

        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            Field Mapping uses symbolic key
                related.Company 							= Company

        ItemUnitOfMeasureRel
			one-to-one relation to ItemUOM
			Field Mapping uses symbolic key
				related.ItemGroup							= InventoryCompanyRel.ItemGroup
				related.Item								= Item
				related.UnitOfMeasure						= LocalUOM	 
				 
				
				
    Field Rules
    	
			
		NextEffectiveDate
			required
			constraint (NextEffectiveDate > current corporate date)
				"EffectiveDateShouldBeGreaterThanToday'sDate"   
		
		NextSellPriceByUOM
			required
			initialize LoopCounter
			LoopCounter = 1
			while (LoopCounter <= 10)		
				
				if (NextSellPriceByUOM.SellPriceInWorksheetItem[LoopCounter].UnitOfMeasure entered)

					LocalUOM = NextSellPriceByUOM.SellPriceInWorksheetItem[LoopCounter].UnitOfMeasure  
					
					constraint (ItemUnitOfMeasureRel.OKForSellPrice)
						"SellPriceUnitOfMeasureIsNotDefinedFor<Item>Item" 			
					
					NextSellPriceByUOM.SellPriceInWorksheetItem[LoopCounter].UOMMultiplier = ItemUnitOfMeasureRel.UOMConversion
	
					NextSellPriceByUOM.SellPriceInWorksheetItem[LoopCounter].SellPriceCode = ItemUnitOfMeasureRel.ValidForSellPrice
					
					LoopCounter +=1
				else
					LoopCounter +=10
				
	Actions
		Create 						is a Create Action
			valid when (PricingWorksheet.IsCreated)
			Action Rules
				constraint (PricingWorksheet.IsCreated)
					"CreateActionIsNotValidForWorksheetInReleaseOrProcessedState"
			
		Update						is an Update Action
			valid when (PricingWorksheet.IsCreated)
			
			Action Rules
				constraint (PricingWorksheet.IsCreated)
					"WorksheetIsNotInEditOrCreatedState;CannotBeUpdated"
					
				constraint (PricingWorksheet.IsNoWorksheetUpdatable)
					"SpreadSheetFormatHasBeenCreated;CannotBeUpdated" 	
				
		Delete						is a Delete Action
			valid when (PricingWorksheet.IsCreated)
		
			Action Rules
				constraint (PricingWorksheet.IsCreated)
						"DeletedActionIsNotValidForWorksheetInReleaseOrProcessedState"
						
		Purge						is a Purge Action
			restricted	
			
