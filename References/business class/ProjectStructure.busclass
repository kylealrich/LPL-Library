ProjectStructure is a BusinessClass
	owned by Projects
	prefix is PRJSt
	
	Ontology
		symbolic key is ProjectStructure
		
	Patterns
	
    Persistent Fields
    	Description				 
		ProjectNode				 is a Project
			delete ignored
		StructureSequence		 is Numeric 4
		IsEnterpriseStructure	 is Boolean
		UnusedDimensionStructure is Boolean
			restricted		
		Active
		Currency
			default label is "DefaultCurrency"
		ReportingBasis
			restricted
		LaborDistributionApproval	is Boolean
		ProjectContractChangeApproval is Boolean		
		ProjectContractInvoiceAndRevenueApproval is Boolean		
		ProjectContractInvoiceApproval is Boolean
		ProjectContractRevenueApproval is Boolean
		DistributionApprovalGroup	is an Employee group
		GeneralLedgerCalendar
			default label is "GlobalLedgerCalendar"
		ProjectInvoiceFromEmailAddress	is an EmailAddress	
		InvoicePrinterEmailAddress		is an EmailAddress 
			holds pii
		CertifyEffortConfirmation		is Text
		LaborDistributionConfirmation   is Text		
		Approve100PercentLaborActualOnly is Boolean	
			default label is "RequireLaborDistributionEqual100%" 
		SalaryEncumbranceCodeBlock		is a FinanceCodeBlockFullNoProject
			default label is "SalaryEncumbranceFinanceStructure"
		FringeEncumbranceCodeBlock		is a FinanceCodeBlockFullNoProject
			default label is "FringeEncumbranceFinanceStructure"
		OverageAccountDefaultOption		is Numeric size 1
			default label is "SalaryCapOverageFinanceStructureOption"
			States
				OriginalTransaction		value is 0
				ProjectEmployee			value is 1
#ifdef module idm				
		IDMPrinter
#endif
#ifndef module idm				
		IDMPrinter						is AlphaUpper size 20
#endif
			
   	Context Fields
		Project
		 		
    Derived Fields
    	ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforCodeDefinition"
		
		ContextMessageText is a MessageField
			restricted
			"Project<Project>"
    
		TopNodeString	    is a StringField
			type is AlphaUpper 15
			restricted			
			StructureSequence
			"_TOP_NODE"

		DerivedOldPrefix	is a StringField
			type is AlphaUpper 15
			restricted			
			StructureSequence
			"_"
			
		DerivedNewPrefix    is a StringField
			type is AlphaUpper 15
			restricted			
			LocalNewStructureSequence
			"_"
			
		DerivedNewProject   is a DerivedField
			type is like Project
			restricted
			I1 = DerivedOldPrefix size
			I1 += 1
			DerivedNewProject = DerivedNewPrefix + LocalCopyProject[I1:25]

		DerivedSystemProject is a DerivedField
			type is like Project
			if (SystemProjectRel exists)
				return SystemProjectRel.Project
			else
				return UndefinedMsg

		UndefinedMsg is a LabelField
			restricted		
			"NotDefined"
			
		InactiveMF is a MessageField
			restricted
			"INACTIVE"

		ActiveMF is a MessageField
			restricted
			"ACTIVE"
						
		StatusMessage is a ConditionalField
			type is Alpha size 20
			if (Active)
				ActiveMF
			else
				InactiveMF

		IsEnterpriseStructureMF is a MessageField
			restricted
			"Enterprise_Structure"
						
		IsEnterpriseStructureMessage is a ConditionalField
			type is Alpha size 20
			if (IsEnterpriseStructure)
				IsEnterpriseStructureMF
			else
				blank
			
	Local Fields
		LocalNewStructure	   		is a ProjectStructure
		LocalCopyProject        	is a Project
		LocalNewStructureSequence   is Numeric 4
		I1					    	is Numeric 2		
 		InvokedByUnusedStructure
		LocalRunGroup				is AlphaUpper 30
		LocalCounter				is Numeric 3
				 		
	Relations
	
		StructureProjectRel
			one-to-many relation to Project
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.Project	!= StructureProjectHierarchyRel.Project)
		StructureProjectHierarchyRel 
			one-to-many relation to ProjectHierarchy
			Field Mapping uses ByParentProject
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.ParentProject		   = ProjectNode
		ProjectsInStructureRel
			one-to-many relation to Project
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.DimensionStructure	   = ProjectStructure
		ProjectHierarchyInEnterpriseStructureRel
			one-to-many relation to ProjectHierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.ProjectStructure		= FinanceEnterpriseGroup.EnterpriseProjectStructure
			Instance Selection
				where (related.ProjectType.Posting)
    	ProjectPersonnelRel
			one-to-many relation to ProjectPersonnel
			Field Mapping uses ByEmployee
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup			
				related.Employee               = actor.agent(Employee).Employee
			Instance Selection
				where (related.Active
				and    related.IsPartofEnterpriseStructure
				and    related.IsFirstRole)
		PostingProjectsResourceInEnterpriseRel				
			one-to-many relation to Project
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.Project                = ProjectPersonnelRel.Project
    	GLTransactionDetailPersonnelRel
			one-to-many relation to GLTransactionDetail
			Field Mapping uses TransactionsByProject
				related.FinanceEnterpriseGroup   = FinanceEnterpriseGroup			
				related.FinanceCodeBlock.Project = ProjectPersonnelRel.Project
		GLCommitsPersonnelRel
			one-to-many relation to GLCommit
			Field Mapping uses ByProjectAndSystemCurrent
				related.FinanceEnterpriseGroup   = FinanceEnterpriseGroup
				related.FinanceCodeBlock.Project = ProjectPersonnelRel.Project
		PostingProjectsInEnterpriseStructureRel				
			one-to-one relation to Project
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
				related.Project                = ProjectHierarchyInEnterpriseStructureRel.Project
		EnterpriseProjectStructureRel
			one-to-many relation to ProjectStructure
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.IsEnterpriseStructure
			    and    related.UniqueID != UniqueID)				
		NotInProjectStructureRel
			one-to-many relation to Project
			Field Mapping uses LeafRecords
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.NotUsedInHierarchy)
		SystemProjectRel
			one-to-one relation to ProjectHierarchy
			Field Mapping uses BySystemProjectInStructure
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ProjectStructure			= ProjectStructure
		UploadErrorsRel is a ProjectUpload set
			Instance Selection
				where (related.ErrorMessage entered)
		ContractProjectRel
			one-to-many relation to ProjectShadow
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectStructure		= ProjectStructure
			Instance Selection
				where (related.IsContract)
		MySummaryProjectsRel
			one-to-many relation to Project
			Field Mapping uses ByStructureAndPersonResponsible
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= ProjectStructure
				related.PersonResponsible		= actor.agent(Employee).Employee
			Instance Selection
				where (related.ProjectType.Summary)
		ProjectTeamMemberRel
			one-to-many relation to ProjectTeamMember
			Field Mapping uses ByTeamMember
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.TeamMember				= actor.agent(Employee).Employee
		MySummaryProjectsTeamRel
			one-to-many relation to Project
			Field Mapping uses ByStructureAndProjectTeam
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= ProjectStructure
				related.ProjectTeam				= ProjectTeamMemberRel.ProjectTeam
			Instance Selection
				where (related.ProjectType.Summary)
		SummaryProjectsRel
			one-to-many relation to Project
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= ProjectStructure
			Instance Selection
				where (related.ProjectType.Summary)
		MyContractProjectsRel
			one-to-many relation to Project
			Field Mapping uses ByStructureAndPersonResponsible
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= ProjectStructure
				related.PersonResponsible		= actor.agent(Employee).Employee
			Instance Selection
				where (related.Contract)
		ContractProjectsRel
			one-to-many relation to Project
			Field Mapping uses ByDimensionStructure
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.DimensionStructure		= ProjectStructure
			Instance Selection
				where (related.Contract)
		ProjectFundingSourceRel
			one-to-many relation to ProjectFundingSource
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectContract			= ContractProjectsRel.Project
			Instance Selection
				where (related.FinanceDimension2.IsProjectInvoicingExpenditures)
		ProjectFundingSourceManagerRel
			one-to-many relation to ProjectFundingSource
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.ProjectContract			= MyContractProjectsRel.Project
			Instance Selection
				where (related.FinanceDimension2.IsProjectInvoicingExpenditures)
		RunGroupRel
			one-to-many relation to ProjectUpload
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.ProjectStructure			  = ProjectStructure
			Instance Selection
				where (related.ProjectUpload.RunGroup = LocalRunGroup)
		ActiveEmployeeRel
			one-to-many relation to Employee
			Field Mapping uses symbolic key
				related.HROrganization			= FinanceEnterpriseGroup.HROrganization
			Instance Selection
				where (related.IsActive
				and    related.ProjectEmployeeNotExists)
		ProjectEmployeeRel
			one-to-many relation to ProjectEmployee
			Field Mapping uses symbolic key
				related.HROrganization	= FinanceEnterpriseGroup.HROrganization
		ContextDimensionHierarchyRel
			one-to-many relation to ProjectHierarchy
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ProjectStructure			= ProjectStructure
				related.Project						= Project
		ReportingBasisRel
			one-to-many relation to ReportingBasis
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.ProjectStructure		= ProjectStructure)

		SecurityGroupRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 13
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
		SecurityGroupMemberRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 13
				related.FinanceDimensionStructure     = ProjectStructure	
		SecurityGroupsRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroupMember
			Field Mapping uses ByStructure
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 13
				related.FinanceDimensionStructure     = ProjectStructure
			Instance Selection
				where (related.RequiresRebuild)
		SecurityGroupsNotRequiringRebuildRel
			one-to-many relation to FinanceDimensionSecurityGroup
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		  = FinanceEnterpriseGroup
				related.DimensionGroupType			  = 13
				related.FinanceDimensionSecurityGroup = any SecurityGroupMemberRel.FinanceDimensionSecurityGroup
			Instance Selection
				where (!related.RequiresRebuild)
				
		ProjectContractChangeRequestRel
			one-to-many relation to ProjectContractChangeRequest
			Field Mapping uses ByStatus
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (!related.Status.Approved)

		ProjectFundingSourceChangeRequestRel
			one-to-many relation to ProjectFundingSourceChangeRequest
			Field Mapping uses ByStatus
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (!related.Status.Approved)
						
		ProjectContractInvoiceRel
			one-to-many relation to ProjectContractInvoice
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.Status.Created)

		ProjectContractBillInvoiceRel
			one-to-many relation to ProjectContractInvoice
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.Status.Created
				and    related.InvoiceType.Bill)

		ProjectContractRevenueInvoiceRel
			one-to-many relation to ProjectContractInvoice
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.Status.Created
				and    related.InvoiceType.RevenueRecognition)
				
		PfiServiceDefinitionContractChangeReqRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses ByServiceDefinitionEnabled
				related.PfiServiceDefinition	= "ProjectContractChangeRequest"

		PfiServiceDefinitionContractFundingChangeReqRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses ByServiceDefinitionEnabled
				related.PfiServiceDefinition	= "ProjectFundingSourceChangeRequest"

		PfiServiceDefinitionJournalizeInvoiceRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses ByServiceDefinitionEnabled
				related.PfiServiceDefinition	= "JournalizeInvoiceApproval"

		PfiServiceDefinitionJournalizeRevenueRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses ByServiceDefinitionEnabled
				related.PfiServiceDefinition	= "JournalizeRevenueApproval"

		PfiServiceDefinitionReverseJournalizeRevenueRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses ByServiceDefinitionEnabled
				related.PfiServiceDefinition	= "ReverseProjectContractRevenueApproval"

		PfiServiceDefinitionReverseJournalizedInvoiceRel
			one-to-one relation to PfiServiceDefinition
			Field Mapping uses ByServiceDefinitionEnabled
				related.PfiServiceDefinition	= "ReverseProjectContractInvoiceApproval"
						
		ContractChangeReqPfiProcessFlowRel
			one-to-many relation to PfiServiceFlowDefinition
			Field Mapping uses ByFlowDefinition
				related.FlowDefinition			= "ProjectContractChangeRequest"
				related.PfiServiceDefinition	= PfiServiceDefinitionContractChangeReqRel.PfiServiceDefinition
			Instance Selection
				where (related.IsServiceFlowEnabled	= true)
				
		ContractChangeFundingReqPfiProcessFlowRel
			one-to-many relation to PfiServiceFlowDefinition
			Field Mapping uses ByFlowDefinition
				related.FlowDefinition			= "ProjectFundingSourceChangeRequest"
				related.PfiServiceDefinition	= PfiServiceDefinitionContractFundingChangeReqRel.PfiServiceDefinition
			Instance Selection
				where (related.IsServiceFlowEnabled	= true)

		JournalizeInvoicePfiProcessFlowRel
			one-to-many relation to PfiServiceFlowDefinition
			Field Mapping uses ByFlowDefinition
				related.FlowDefinition			= "JournalizeInvoiceApproval"
				related.PfiServiceDefinition	= PfiServiceDefinitionJournalizeInvoiceRel.PfiServiceDefinition
			Instance Selection
				where (related.IsServiceFlowEnabled	= true)

		JournalizeRevenuePfiProcessFlowRel
			one-to-many relation to PfiServiceFlowDefinition
			Field Mapping uses ByFlowDefinition
				related.FlowDefinition			= "JournalizeRevenueApproval"
				related.PfiServiceDefinition	= PfiServiceDefinitionJournalizeRevenueRel.PfiServiceDefinition
			Instance Selection
				where (related.IsServiceFlowEnabled	= true)
					
		ReverseJournalizeRevenuePfiProcessFlowRel
			one-to-many relation to PfiServiceFlowDefinition
			Field Mapping uses ByFlowDefinition
				related.FlowDefinition			= "ReverseProjectContractRevenueApproval"
				related.PfiServiceDefinition	= PfiServiceDefinitionReverseJournalizeRevenueRel.PfiServiceDefinition
			Instance Selection
				where (related.IsServiceFlowEnabled	= true)

		ReverseProjectContractInvoicePfiProcessFlowRel
			one-to-many relation to PfiServiceFlowDefinition
			Field Mapping uses ByFlowDefinition
				related.FlowDefinition			= "ReverseProjectContractInvoiceApproval"
				related.PfiServiceDefinition	= PfiServiceDefinitionReverseJournalizedInvoiceRel.PfiServiceDefinition
			Instance Selection
				where (related.IsServiceFlowEnabled	= true)
						
    Conditions
		UploadRecordsExist
			restricted		
			when (ProjectUpload set exists)
		UploadErrorsExist
			restricted		
			when (UploadErrorsRel exists)
		IsActive
			restricted		
			when (Active)			
		EligibleForExport
			restricted
			when ((!UploadRecordsExist)
			and   (!UnusedDimensionStructure))
		AvailableStructure
			restricted
			when ((!UnusedDimensionStructure)
			and   (!ContextDimensionHierarchyRel exists))
		IsProjectInvoicing
			restricted
			when (IsEnterpriseStructure
			and   FinanceEnterpriseGroup.ProjectInvoicing)
		UsedInASecurityGroup
			when (SecurityGroupMemberRel exists)
		SecurityGroupRequiresRebuild
			when (SecurityGroupsRequiringRebuildRel exists)
				
		UnprocessedProjectContractChangeRequestExists	
			restricted					
			when (ProjectContractChangeRequestRel exists)

		UnprocessedProjectFundingSourceChangeRequestExists
			restricted						
			when (ProjectFundingSourceChangeRequestRel exists)

		UnprocessedProjectContractInvoicesExists	
			restricted					
			when (ProjectContractInvoiceRel exists)

		UnprocessedProjectContractBillInvoicesExists	
			restricted					
			when (ProjectContractBillInvoiceRel exists)

		UnprocessedProjectContractRevenueInvoicesExists	
			restricted					
			when (ProjectContractRevenueInvoiceRel exists)
						
		ContractChangeRequestServiceExists
			restricted
			when (ContractChangeReqPfiProcessFlowRel exists)

		FundingSourceChangeRequestServiceExists
			restricted
			when (ContractChangeFundingReqPfiProcessFlowRel exists)

		JournalizeInvoiceApprovalServiceExists
			restricted
			when (JournalizeInvoicePfiProcessFlowRel exists)

		JournalizeRevenueApprovalServiceExists
			restricted
			when (JournalizeRevenuePfiProcessFlowRel exists)

		ReverseJournalizeRevenueApprovalServiceExists
			restricted
			when (ReverseJournalizeRevenuePfiProcessFlowRel exists)

		ReverseProjectContractInvoiceApprovalServiceExists
			restricted
			when (ReverseProjectContractInvoicePfiProcessFlowRel exists)

		EligibleToDelete
			restricted
			when (!IsEnterpriseStructure
			and   !UnusedDimensionStructure
			and   !Active)

	Sets
		ByStructureSequence
			Sort Order
				FinanceEnterpriseGroup
				StructureSequence
		
		ByEnterpriseStructure
			Sort Order
				FinanceEnterpriseGroup
				IsEnterpriseStructure descending
				ProjectStructure

	Attach Rules
		if (!parentcontext.name = "Project"
		and !parentcontext.name = "ProjectUpload"		
		and !parentcontext.name = "ProjectShadow"
		and !parentcontext.name = "ProjectHierarchy"
		and !parentcontext.name = "FinanceEnterpriseGroup"
		and !parentcontext.name = "AccountAnalysisSettings"
		and !parentcontext.name = "ReportingBasis")		
			constraint (Active)
				"StructureIsNotActive"

    Field Rules
    
		Description
			required
		StructureSequence
			autosequence using ByStructureSequence
			if (!StructureSequence = old StructureSequence)
				cannot be changed
					"ProjectStructureSequenceCannotBeChanged"
		IsEnterpriseStructure
			constraint (Active)
				"EnterpriseProjectMustBeActive"
			constraint (!UnusedDimensionStructure)
				"TheUnusedDimensionStructureCannotBeTheEnterpriseStructure"				
			constraint (!EnterpriseProjectStructureRel exists)
				"ThisIsAlreadyDesignatedAsEnterpriseStructure<first EnterpriseProjectStructureRel.ProjectStructure>"
			if (IsEnterpriseStructure changed
			and old IsEnterpriseStructure)
				constraint (ContractProjectRel not exists)
					"CannotChangeEnterpriseStructure;ContractsExistInStructure"
				







															
		GeneralLedgerCalendar
			if (IsEnterpriseStructure)
				required
					"GlobalLedgerCalendarRequiredForEnterpriseStructure"
			else
				GeneralLedgerCalendar         = blank
				cannot be entered	
					"GlobalLedgerCalendarOnlyValidForEnterpriseStructure"

		Currency
			if (!IsEnterpriseStructure)		
				Currency = blank
			if (FinanceEnterpriseGroup.ProjectLabel entered)
				constraint (IsEnterpriseStructure)
					"CurrencyValidOnlyForPosting<FinanceEnterpriseGroup.ProjectLabel>"
				constraint (Currency = FinanceEnterpriseGroup.ReportCurrencyOne
				or  		Currency = FinanceEnterpriseGroup.ReportCurrencyTwo		
				or  		Currency = FinanceEnterpriseGroup.ReportCurrencyThree		
				or			Currency = FinanceEnterpriseGroup.ReportCurrencyFour			
				or  		Currency = FinanceEnterpriseGroup.ReportCurrencyFive)		
					"MustBeOneOfTheEnterpriseCurrencies"				

		Active
			if (UnusedDimensionStructure)
				Active = false
				
		ProjectContractChangeApproval
			if (ProjectContractChangeApproval)
				constraint (ContractChangeRequestServiceExists)
					"CannotBeSelected;ProjectContractChangeRequestServiceIsNotDefined"							
				constraint (FundingSourceChangeRequestServiceExists)
					"CannotBeSelected;ProjectFundingSourceChangeRequestServiceIsNotDefined"				
			if (ProjectContractChangeApproval changed)

				constraint (!UnprocessedProjectContractChangeRequestExists)
					"CannotChange;UnprocessedProjectContractChangeRequestExists"		
				constraint (!UnprocessedProjectFundingSourceChangeRequestExists)
					"CannotChange;UnprocessedProjectFundingSourceChangeRequestExists"		

		ProjectContractInvoiceApproval
			if (ProjectContractInvoiceApproval)
				constraint (JournalizeInvoiceApprovalServiceExists)
					"CannotBeSelected;JournalizeInvoiceApprovalServiceIsNotDefined"				
				constraint (ReverseProjectContractInvoiceApprovalServiceExists)	
					"CannotBeSelected;ReverseProjectContractInvoiceApprovalServiceIsNotDefined"	
			if (ProjectContractInvoiceApproval changed)

				constraint (!UnprocessedProjectContractBillInvoicesExists)
					"CannotChange;UnprocessedProjectContractInvoicesExists"		
		
		ProjectContractRevenueApproval
			if (ProjectContractRevenueApproval)
				constraint (JournalizeRevenueApprovalServiceExists)
					"CannotBeSelected;JournalizeRevenueApprovalServiceIsNotDefined"	
				constraint (ReverseJournalizeRevenueApprovalServiceExists)	
					"CannotBeSelected;ReverseProjectContractRevenueApprovalServiceIsNotDefined"	
			if (ProjectContractRevenueApproval changed)

				constraint (!UnprocessedProjectContractRevenueInvoicesExists)
					"CannotChange;UnprocessedProjectContractRevenueExists"		
			














			
		CertifyEffortConfirmation
			if (!IsEnterpriseStructure)
				CertifyEffortConfirmation = blank
				cannot be entered	
					"CertifyEffortConfirmationOnlyValidForEnterpriseStructure"

		LaborDistributionConfirmation
			if (!IsEnterpriseStructure)
				LaborDistributionConfirmation = blank
				cannot be entered	
					"LaborDistributionConfirmationOnlyValidForEnterpriseStructure"

		SalaryEncumbranceCodeBlock
			if (!IsEnterpriseStructure)
				SalaryEncumbranceCodeBlock = blank
				cannot be entered	
					"SalaryEncumbranceFinanceStructureOnlyValidForEnterpriseStructure"

		FringeEncumbranceCodeBlock
			if (!IsEnterpriseStructure)
				FringeEncumbranceCodeBlock = blank
				cannot be entered	
					"FringeEncumbranceFinanceStructureOnlyValidForEnterpriseStructure"

		OverageAccountDefaultOption				
			if (!IsEnterpriseStructure)
				OverageAccountDefaultOption = blank
				cannot be entered	
					"SalaryCapOverageFinanceStructureDefaultOptionOnlyValidForEnterpriseStructure"

	Rule Blocks
		UpdateFinanceEnterpriseGroupProjectStructure
			if (action type.Create)
				if (IsEnterpriseStructure)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						invoked.PrmEnterpriseProjectStructure = ProjectStructure
			else								
				if (IsEnterpriseStructure changed)
					invoke SetEnterpriseChart FinanceEnterpriseGroup
						if (!IsEnterpriseStructure)
							invoked.PrmClearProjectStructure = true
						else
							invoked.PrmEnterpriseProjectStructure = ProjectStructure

	Actions
		T2VCreate is a Create Action
			restricted
			default label is untranslatable

		Create is a Create Action
			Entrance Rules

			Exit Rules
				InvokedByUnusedStructure = true
				
				invoke Create Project
					invoked.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
					invoked.Project					= TopNodeString
					invoked.DisplayProject			= "TOP_NODE"
					invoked.DimensionStructure		= ProjectStructure
					invoked.Description				= Description + " Top Level"

					invoked.ProjectType				= DimensionType.Node
					invoked.SkipHierarchy			= true
					invoked.IsTopNode				= true
					invoked.LocalTopNode			= true
					invoked.LocalTriggerFrom        = "ProjectStructure"
				ProjectNode = TopNodeString
				include UpdateFinanceEnterpriseGroupProjectStructure
							
		Update is an Update Action
			Action Rules
				include UpdateFinanceEnterpriseGroupProjectStructure
				invoke Update ProjectNode
					invoked.Description				= Description + " Top Level"
					invoked.LocalTriggerFrom        = "ProjectStructure"

		DeleteStructure is an Instance Action
			valid when (EligibleToDelete)
			run in background
			confirmation required
			Action Rules
				constraint (!IsEnterpriseStructure)
					"CannotDeleteEnterpriseStructure"
				constraint (!UnusedDimensionStructure)
					"CannotDeleteUnusedDimensionStructure"
				constraint (!Active)
					"CannotDeleteActiveProjectStructure"
					
				invoke DeleteProjectStructure Project
					invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
					invoked.PrmProjectStructure			= ProjectStructure
				invoke Delete
				
		Delete is a Delete Action
			restricted
		
			Entrance Rules
				constraint (!Active)
					"CannotDeleteActiveProjectStructure"

		RestrictedDelete is a Delete Action
			restricted
			
			Entrance Rules
				constraint (!Active)
					"CannotDeleteActiveProjectStructure"
												
			Exit Rules
				invoke DeleteAll ProjectsInStructureRel	
										
		UploadHierarchy is an Instance Action
			valid when (UploadRecordsExist)
			completion message is "UploadInitiated"	
			run in background
			
			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"	
			
			Action Rules
				invoke UploadHierarchy ProjectUpload in background
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmProjectStructure		  = ProjectStructure
					invoked.PrmRunGroup				  = PrmRunGroup

		ResetUploadErrors is an Instance Action

			completion message is "ResetInitiated"

			Parameters
				PrmRunGroup				is AlphaUpper 30
					default label is "RunGroup"
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (RunGroupRel exists)
						"NoUploadRecordsExistForThisRunGroup"
									
			Action Rules
				invoke ResetAllErrors ProjectUpload in background
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmProjectStructure		  = ProjectStructure
					invoked.PrmRunGroup				  = PrmRunGroup
							
							
		DeleteUploadRecordsForRunGroup is an Instance Action
			valid when (UploadRecordsExist)
			confirmation required
			completion message is "DeleteSubmitted"
			
			Parameters
				PrmRunGroup			is AlphaUpper 30
					default label is "RunGroup"
					
			Parameter Rules
				PrmRunGroup
					required
					
			Action Rules
				invoke DeleteUploadRunGroup ProjectUpload in background
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmProjectStructure       = ProjectStructure
					invoked.PrmRunGroup				  = PrmRunGroup
											
		CopyStructure is an Instance Action
			valid when (!UnusedDimensionStructure)
			run in background
			completion message is "CopyInitiated"		
			
			Parameters
				NewStructure		   is like ProjectStructure
				NewDescription		   is a Description

				MakeNewStructureActive is Boolean
							
			Parameter Rules
				NewStructure
					required
					LocalNewStructure = NewStructure
					constraint (!LocalNewStructure exists)
						"DimensionStructureAlreadyExists"
				NewDescription
					required








			Local Fields
				AsyncId			is an AsyncActionRequest
															
			Action Rules
				invoke Create ProjectStructure
					invoked.FinanceEnterpriseGroup	   = FinanceEnterpriseGroup
					invoked.ProjectStructure 		   = NewStructure

					invoked.Description			   	   = NewDescription
					invoked.IsEnterpriseStructure      = false
					invoked.Active					   = MakeNewStructureActive
			Exit Rules
				
				invoke CopyProjects ProjectHierarchy in background
					assign async action request id to AsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmStructure	 		  = ProjectStructure
					invoked.PrmNewStructure	 		  = NewStructure
					
				invoke BuildShadowFile LocalNewStructure in background
					run after AsyncId
									
		BuildShadowFile is an Instance Action
			restricted
					
			Local Fields
				LocalAsyncId				is an AsyncActionRequest
						
			Action Rules
				invoke DeleteShadowForStructure ProjectShadow in background
					assign async action request id to LocalAsyncId				
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmDimensionStructure	  = ProjectStructure				
					
				invoke BuildShadowFile ProjectHierarchy in background
					run after LocalAsyncId
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmProjectStructure	 	  = ProjectStructure
					
		BuildShadowFileForProject is an Instance Action
			restricted
			
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmProject						is a Project
					default label is "Project"
		
			Action Rules
				invoke BuildShadowFile ProjectHierarchy in foreground
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmProjectStructure	  	  = ProjectStructure
					invoked.PrmProject			 	  = PrmProject
		
		ExportToUpload is an Instance Action


			completion message is "ExportInitiated"
						
			Parameters
				PrmRunGroup		  		is AlphaUpper 30
					default label is "RunGroup"
				PrmProjectGroup   		is a Project group
					default label is "CustomGroup"
				PrmSummaryProject		is a Project
					default label is "Summary<FinanceEnterpriseGroup.ProjectLabel>"	
				PrmPostingProject		is a Project
					default label is "Posting<FinanceEnterpriseGroup.ProjectLabel>"					
				PrmBypassEdits	  is Boolean
					default label is "BypassEdits"					
				
			Parameter Rules
				PrmRunGroup
					required
					LocalRunGroup = PrmRunGroup
					constraint (!RunGroupRel exists)
						"UploadRecordsAlreadyExistForThisRunGroup"
							
				PrmProjectGroup
					constraint (!PrmSummaryProject entered
					and			!PrmPostingProject entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
						
				PrmSummaryProject
					constraint (!PrmProjectGroup entered
					and         !PrmPostingProject entered)
						"CannotBeUsedWithOtherSpecifiedParameters"
										
				PrmPostingProject
					constraint (!PrmProjectGroup entered
					and         !PrmSummaryProject entered)
						"CannotBeUsedWithOtherSpecifiedParameters"					
			
			Action Rules
				if ((!PrmProjectGroup entered)
				and (!PrmSummaryProject entered)
				and (!PrmPostingProject entered))
					invoke ExportToUpload ProjectNode in background
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.PrmProjectStructure			= ProjectStructure
						invoked.PrmRunGroup					= PrmRunGroup	
						invoked.PrmBypassEdits              = PrmBypassEdits								
				else
				if (PrmSummaryProject entered)
					invoke ExportToUpload PrmSummaryProject in background
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.PrmProjectStructure			= ProjectStructure
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmBypassEdits              = PrmBypassEdits						
				else
				if (PrmPostingProject entered)														
					invoke ExportToUpload PrmPostingProject in background
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.PrmProjectStructure			= ProjectStructure
						invoked.PrmRunGroup					= PrmRunGroup
						invoked.PrmBypassEdits              = PrmBypassEdits						
				else																		
					invoke ExportToUpload ProjectHierarchy in background
						invoked.PrmFinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.PrmProjectStructure			= ProjectStructure
						invoked.PrmRunGroup				   	= PrmRunGroup	
						invoked.PrmProjectGroup	   			= PrmProjectGroup
						invoked.PrmBypassEdits              = PrmBypassEdits
						
		CreateProjectStructureWizard is a Create Action
			default label is "CreateProjectStructureWizard"
			completion message is "<ProjectStructure>SucessfullyCreated"
			Entrance Rules
			Exit Rules
				invoke Create Project
					invoked.FinanceEnterpriseGroup  = FinanceEnterpriseGroup
					invoked.Project					= TopNodeString
					invoked.DisplayProject			= "TOP_NODE"
					invoked.DimensionStructure		= ProjectStructure
					invoked.Description				= ("Top Node For " + ProjectStructure)
					invoked.ProjectType				= DimensionType.Node
					invoked.SkipHierarchy			= true
					invoked.IsTopNode				= true	
					invoked.LocalTriggerFrom        = "ProjectStructure"
				ProjectNode = TopNodeString
				include UpdateFinanceEnterpriseGroupProjectStructure

		CleanupUnusedDimensions is an Instance Action
			restricted
			
			Action Rules
				InvokedByUnusedStructure = true
				invoke CleanupUnusedProjects Project in background
					invoked.PrmFinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.PrmProjectStructure		  = ProjectStructure
					
		AddProjectToUnusedStructure is an Instance Action
			restricted
			
			Parameters
				Project
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<ProjectStructure>"
					
				InvokedByUnusedStructure = true
				invoke AddPostingProjectToStructure Project
					invoked.PrmStructure	 = ProjectStructure
					invoked.PrmParentProject = ProjectNode
					
		RemoveProjectFromUnusedStructure is an Instance Action
			restricted
			
			Parameters
				Project
				
			Action Rules
				constraint (UnusedDimensionStructure)
					"ActionNotValidForStructure<ProjectStructure>"
					
				InvokedByUnusedStructure = true
				invoke RemoveFromStructure Project
	
								
