MimeType is a BusinessClass
	default label is "MIME_Type"
    owned by la
    prefix is smt
    stored in environment
	contains environment data
	disable data area copy
		preserve target data
  
    Ontology
    	symbolic key is MimeType

	Patterns
		implements ExtendsExternalData
			using com.lawson.rdtech.framework.MimeTypeStorage
		implements ConfigurationMetadata			
			 
	Persistent Fields
		DefaultExtension is Alpha size 10
		Extensions is Alpha up to 2000
		Parent is a MimeType
			default label is "SuperType"
		Status 	is a MimeTypeStatus
			restricted
		
	Transient Fields
		InputStatus is a MimeTypeStatus
			default label is "Status"
			derive value from EffectiveStatus
			
	Derived Fields
		BaseEnvironment is a NativeField
			type is Boolean	
			
		AllowStatusUpdate is a NativeField
			type is Boolean
			
		NativeStatus is a NativeField
			type is Numeric size 1
			restricted
			
        EffectiveStatus is a DerivedField
        	type is like MimeTypeStatus
        	default label is "Status"
        	return NativeStatus
		
		ReducingBlockingMessage is a MessageField
			restricted
			"AllowingMIME_TypeMayIntroduceMaliciousCode" 
			
		HasError is a NativeField
			type is Boolean
			default label is "ErrorExists"
			
		Error is a NativeField
			type is MessageField
			
	Conditions
		InError
			when (HasError)
			
	Field Rules
		Status
			constraint (AllowStatusUpdate)
				"MIME_TypeIsBlockedByThePlatform"
				
			if  (old Status = Status.Blocked or old Status = Status.SystemBlocked)
		    	if (Status.Active)
		    		confirmation required
		    			"<ReducingBlockingMessage>"
		    	else
		    	if (Status.Inactive)
		    		confirmation required
		    			"<ReducingBlockingMessage>"
		   	else
		   	if (old Status = Status.Inactive
			and Status.Active)
				confirmation required
					"<ReducingBlockingMessage>"	
					
			constraint (com.lawson.rdtech.type.MimeTypeMap.isValid(MimeType))
				"InvalidMIME_Type"				   	
			
	Relations
		ParentRel
			one-to-one relation to MimeType
			Field Mapping uses symbolic key
				related.MimeType = Parent

	Attach Rules
		if (parentcontext.name != "ASStoredConfiguration")
			constraint (Status.Active)
				"MIME_TypeIsInactive"
				
	Actions
		Create is a Create Action
			Entrance Rules
				constraint (MimeType != "application/octet-stream" and MimeType != "text/plain")
					"CannotChangeDefaultMIME_Type"
													
				if (InputStatus entered)
					Status = InputStatus
					
				
		Update is an Update Action
			Entrance Rules
				constraint (MimeType != "application/octet-stream" and MimeType != "text/plain")
					"CannotChangeDefaultMIME_Type"

				if (InputStatus entered)
					Status = InputStatus
				else
					Status = Status.Active	
					
		Delete is a Delete Action
