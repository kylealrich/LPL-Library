BillingInvoiceCommentAttachment is a BusinessClass
	owned by bl
	prefix is BUCA1
    sql name is BRInvoiceCommentAttach

	Ontology
		symbolic key is BillingInvoiceCommentAttachment
	
	Patterns
        implements StaticJava
		implements Archivable
        
	Persistent Fields
		Name				is a CommentName
		Comment				is a NewCommentText
		Type				is AlphaUpper 1
		Description
		Attachment			is a AlternateAttachment

	Field Rules
		Attachment
			required

	Local Fields
		LocalAttributeCtr   is Numeric 2		
			
	Conditions
		HasAttachment
			restricted
			when (Attachment entered)

	Derived Fields

		DerivedBillingCompany is a DerivedField
			type is like BillingCompany
			restricted
			return BillingInvoice.Company
		
		DerivedBillingCompanyName is a DerivedField
			type is like Name
			restricted
			return BillingInvoice.Company.Name

		DerivedInvoiceNumber is a DerivedField
			type is like InvoiceNumber
			restricted
			return BillingInvoice.InvoiceNumber

		DerivedInvoicePrefix is a DerivedField
			type is like InvoicePrefix
			restricted
			return BillingInvoice.InvoicePrefix

		DerivedBillingInvoiceLine is a DerivedField
			type is like BillingInvoiceLine
			restricted
			return BillingInvoiceLine

		DerivedCustomer is a DerivedField
			type is like Customer
			restricted
			return BillingInvoice.Customer

		DerivedCustomerName is a DerivedField
			type is like VendorName
			restricted
			return BillingInvoice.Customer.Name

	Sets
		ByDescendingAttachment
			indexed
			Sort Order
				Company
				BillingInvoice
				BillingInvoiceLine
				BillingInvoiceComments
				BillingInvoiceCommentAttachment descending

	Create Rules   
		include IDM.CreateRules 
			replace AttachmentField with Attachment	
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment
	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Actions
		Create is a Create Action

		Update is an Update Action

		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted


		UploadToIDM is an Instance Action
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment
						
						
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment 

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
