MatchInvoiceMessageComment is a BusinessClass
	owned by ma
	prefix is Mimcm
	
	Ontology
		part of MatchInvoiceMessage
			delete cascades
			relative key is SequenceNumber

	Patterns
		implements Archivable
					
	Persistent Fields
    	CommentTitle					is a CommentName
    	CommentType						is AlphaUpper size 1
    		States
                MessageComments				value is "M"
    	CommentText
		Attachment						is an AlternateAttachment

	Derived Fields
		Yes is a MessageField
			restricted
			"Yes"
			
		No is a MessageField
			restricted
			"No"
				
		HasAttachmentDerived is a DerivedField
			type is Alpha 25
			if (HasAttachment)
			    return Yes
			else
				return No

	Local Fields
		LocalAttributeCtr   is Numeric 2

	Field Rules
		SequenceNumber
			autosequence
				minimize contention
			
		CommentType
			force default to "M"

		CommentTitle
			required
		
	Relations

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

    Conditions
    	HasAttachment
    		when (Attachment entered)

	Sets
  		ByCommentType
  			Sort Order
  				Company	
  				PayablesInvoice
  				PayablesInvoiceDetail
				PurchaseOrder
				PurchaseOrderLine
				Contract
				ContractLine
  				MatchInvoiceMessage
				CommentType
				SequenceNumber


	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Delete Action

		Purge is a Purge Action
			restricted
			bypass relational integrity rules
			Action Rules
				Attachment.LocalPurgeTriggered = true

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	

