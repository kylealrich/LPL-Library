CashForecastPublishDate is a BusinessClass
	owned by cashmgmt
	prefix is CMCPD	

	Ontology
		symbolic key is CashForecastPublishDate

	Persistent Fields
		Description			is Alpha 250
		PublishType 		is Numeric 2
			default label is "Type"
			States 
				Weekly			value is 1
				Monthly 		value is 2
		PublishDateRange	is a DateRange
	
	Derived Fields
		CurrentPeriod is a DerivedField
			type is Numeric 3
			return PublishDateRange.End year day
		
		CurrentYear is a DerivedField
			type is Numeric 4
			return PublishDateRange.End year
			
	Field Rules
		Description
			required
			
		PublishType
			required
			
		PublishDateRange
			required
	
	Sets
		ByPublishDateRange
			Sort Order
				CashManagementGroup
				CashForecast
				PublishDateRange
				CashForecastPublishDate
	
	Relations
		CashForecastPeriodAmountRel
			one-to-many relation to CashForecastPeriodAmount
			Field Mapping uses symbolic key
				related.CashManagementGroup	= CashManagementGroup
				related.CashForecast		= CashForecast
			Instance Selection
  				where (related.CashForecastPeriod within PublishDateRange)
		
		CashForecastAccountRel
			one-to-many relation to CashForecastAccount
			Field Mapping uses symbolic key
				related.CashManagementGroup	= CashManagementGroup
				related.CashForecast		= CashForecast
			
  	Actions
  		Create is a Create Action
			restricted
			
  		Update is an Update Action
  			Field Rules
  				PublishType
					cannot be changed
						"TypeCannotBeChanged"
					
				PublishDateRange
					cannot be changed
						"PublishedDatesCannotBeChanged"
		  			
  		Delete is a Delete Action
  			restricted
  			
  		Purge is a Purge Action
  			restricted
