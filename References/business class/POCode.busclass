POCode is a BusinessClass
    owned by po
    prefix is POO
    classic name is POCODE

    Ontology
        symbolic key is POCode
            classic set name is POOSET1
            classic name for ProcurementGroup is PROCURE-GROUP

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description
        Active      is Boolean
            classic name is ACTIVE-STATUS
        InUse       is AlphaUpper size 1
            classic name is USED-FLAG
            States
                Used                   value is "Y"
                Unused                 value is "N"
                UsedOnContracts value is "P"
        ConsignCode
			default label is "BillingCode"
			
	Derived Fields
		RepresentativeText is a StringField
			type is Text
			default label is "POCode"
			POCode " - " Description

    Conditions

        IsActive
        	restricted
            when (Active)

        IsInactive
        	restricted
            when (not Active)
            
        IsInUse
        	restricted
        	when (PurchaseOrderRel exists)
        
        BypassRuleForTheseFiles
			restricted
	   		when (parentcontext.name = "PurchaseOrderEDIOutput")
    
    Relations
    	PurchaseOrderRel
    		one-to-many relation to PurchaseOrder
    		Field Mapping uses Set8
    		Instance Selection
    			where (related.POCode = POCode
    			and    related.Company.ProcurementGroup = ProcurementGroup)

    Field Rules

		Description
			required

        Active
            initial value is true

	Attach Rules
		if (not BypassRuleForTheseFiles)
            constraint (Active)
			    "POCodeIsNotActive"

    Actions
        Create is a Create Action

        Update is an Update Action

        Delete is a Delete Action
        	Action Rules
        	
        		constraint(not IsInUse)
        			"CannotDelete;POCodeIsInUse"
