BuyerPurchasingClass is a BusinessClass
    owned by po
    prefix is BCL
    classic name is BUYCLASS

    Ontology
        symbolic key is BuyerPurchasingClass
            classic set name is BCLSET1
            classic name for BuyerPurchasingClass.MajorPurchasingClass is PURCH-MAJCL
            classic name for BuyerPurchasingClass.MinorPurchasingClass is PURCH-MINCL
            classic name for HROrganization is PROCURE-GROUP
            classic name for Buyer is BUYER-CODE

    Patterns
        implements StaticJava
        disable AuditIndex

    Conditions

        HasMinorPurchasingClass
        	restricted
            classic name is MINORCL
            when (BuyerPurchasingClass.MinorPurchasingClass entered)

    Relations

		SetupAtMajorClassRel
			one-to-one relation to BuyerPurchasingClass
			Field Mapping uses symbolic key
				related.HROrganization								= HROrganization
				related.Buyer										= Buyer
				related.ItemGroup									= ItemGroup
				related.BuyerPurchasingClass.MajorPurchasingClass	= BuyerPurchasingClass.MajorPurchasingClass
				related.BuyerPurchasingClass.MinorPurchasingClass	= blank

		SetupAtMinorClassRel
			one-to-many relation to BuyerPurchasingClass
			Field Mapping uses symbolic key
				related.HROrganization	= HROrganization
				related.Buyer			= Buyer
				related.ItemGroup		= ItemGroup
			Instance Selection
				where (related.BuyerPurchasingClass.MajorPurchasingClass = this instance.BuyerPurchasingClass.MajorPurchasingClass
				and    related.BuyerPurchasingClass.MinorPurchasingClass entered)

		AnotherRecordRel
			one-to-many relation to BuyerPurchasingClass
			Field Mapping uses symbolic key
				related.HROrganization	= HROrganization
				related.Buyer			= Buyer
				related.ItemGroup		= ItemGroup
			Instance Selection
				where (related.BuyerPurchasingClass.MajorPurchasingClass != this instance.BuyerPurchasingClass.MajorPurchasingClass
				or     (related.BuyerPurchasingClass.MajorPurchasingClass = this instance.BuyerPurchasingClass.MajorPurchasingClass
				and     related.BuyerPurchasingClass.MinorPurchasingClass != this instance.BuyerPurchasingClass.MinorPurchasingClass))

	Field Rules
		ItemGroup
			initial value is Buyer.ProcurementGroup
			constraint (ItemGroup.FinanceEnterpriseGroup.HROrganization = HROrganization)
				"FinanceEnterpriseGroupAssociatedWithItemGroupDoesNotUseEnteredHROrganization"
		
		BuyerPurchasingClass
			if (BuyerPurchasingClass.MinorPurchasingClass entered)
				constraint (SetupAtMajorClassRel not exists)
					"AlreadySetupAtMajorClass;CannotAddMinorClass"
			else
				constraint (SetupAtMinorClassRel not exists)
					"AlreadySetupAtMinorClass;CannotAddMajorClassOnly"

	Actions
		Create is a Create Action
		
			Action Rules
				
				if (Buyer.ProcurementGroup entered)
					ItemGroup = Buyer.ProcurementGroup

		Update is an Update Action

		Delete is a Delete Action
			Entrance Rules
				if (Buyer.RestrictPurchasingClasses)
					constraint (AnotherRecordRel exists)
						"BuyerHasRestrictedPurchasingClasses;CannotDeleteLastRecord"
