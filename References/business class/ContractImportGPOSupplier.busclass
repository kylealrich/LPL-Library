ContractImportGPOSupplier is a BusinessClass
	owned by po
	
	prefix is CIGS
	
	Ontology
		symbolic key is ContractImportGPOSupplier
	
	Patterns
	
	Persistent Fields
		ContractSupplier                is a Supplier
		Name                            is Alpha 250
		Address1                        is an AddressLine       
		Address2                        is an AddressLine
		Municipality
			default label is "City"
		StateProvince                   is like StateProvince
			default label is "State"
		PostalCode		
		Active
		ManufacturerCodeDivision        is a Manufacturer
		TaxID                           is AlphaUpper 25
		DefaultDistributorContract      is a Contract 
		DoNotCreateDistributorContract  is Boolean
	
	Derived Fields
	
		VendorName is a DerivedField
			type is Alpha 120
			restricted
			if (VendorEntered)
				return ContractSupplier.Vendor.VendorName
			else
				return blank 
	
	Conditions
	
		SupplierMapped
			restricted
			when (ContractSupplier entered)
			
		SupplierNotMapped
			restricted
			when (ContractSupplier !entered)
			
		VendorEntered
			when (SupplierMapped
			and   ContractSupplier.Vendor entered)
	
		ContractsExist
			when (ContractGPOHeaderRel exists
			and   ContractGroupRel.GPOIsHealthTrust)
	
		InterfaceContractsExist
			restricted
			when (ContractImportRel exists
			or    ContractImportDistributorRel exists)
	
		ManufacturerSupplierChanged
			restricted
			when (ContractImportSupplierRel exists)
			
		ManufacturerContractsExist
			restricted
			when (ContractImportRel exists)
			
		DistributorContractsExist
			restricted
			when (ContractImportDistributorRel exists)
		
		MatchedByTaxID
			restricted
			when (first SupplierByTaxIDRel.Supplier = ContractSupplier
			and   ContractSupplier entered
			and   TaxID entered)

		DistributorSupplierChanged
			restricted
			when (ContractImportDistributorSupplierRel exists)
	










		SupplierChangedManually
			restricted
			when (SupplierChangedFromMappingRel exists
			or    DistributorSupplierChangedFromMappingRel exists)

		GPOSupplier2021Exists
			restricted
			when (ContractGPOSupplierRel exists)
			
		GPOHeaderSupplier2021Exists
			restricted
			when (ContractGPOHeaderRel exists)	

		CanRunUOMMappingActions 
			restricted 
			when (SupplierUOMMappingRel !exists
			and   SupplierMapped)

		HasUOMMapping
			restricted 
			when (SupplierUOMMappingRel exists)
		
		HasMissingMappings 
			restricted 
			when (SupplierUOMMappingNotFullyMapped exists)
	
	Relations
	
		ContractGroupRel
			one-to-one relation to ContractGroup
			Field Mapping uses symbolic key
				related.ContractGroup = SupplierGroup

		ContractGPOSupplierRel 
			one-to-many relation to ContractGPOSupplier
			Field Mapping uses ByGPOSupplier
				related.GPOSupplier   = ContractImportGPOSupplier
				
		ContractGPOHeaderRel
			one-to-many relation to ContractGPOHeader
			Field Mapping uses ByGPOSupplier
				related.ManufacturerSupplier   = ContractImportGPOSupplier
	
		ContractImportRel
			one-to-many relation to ContractImport
			Field Mapping uses ByManufacturerSupplier
				related.ContractGroup          = SupplierGroup
				related.ManufacturerSupplier   = ContractImportGPOSupplier
			Instance Selection
				where (!related.SupplierChangedFromMappingOrGPO)
				
		ContractImportSupplierRel
			one-to-many relation to ContractImport
			Field Mapping uses ByManufacturerSupplier
				related.ContractGroup          = SupplierGroup
				related.ManufacturerSupplier   = ContractImportGPOSupplier
			Instance Selection
				where (related.SupplierChangedFromMappingOrGPO)

		SupplierChangedFromMappingRel
			one-to-many relation to ContractImport
			Field Mapping uses ByManufacturerSupplier
				related.ContractGroup          = SupplierGroup
				related.ManufacturerSupplier   = ContractImportGPOSupplier
			Instance Selection
				where (related.SupplierChangedFromMapping)			

		SupplierRel
			one-to-one relation to Supplier
			Field Mapping uses symbolic key
				related.SupplierGroup      	= SupplierGroup
				related.Supplier            = ContractSupplier

		SupplierByTaxIDRel
			one-to-many relation to Supplier
			Field Mapping uses TaxIDOnly
				related.TaxIdGroup.TaxId       = TaxID 
				related.SupplierGroup          = SupplierGroup
			Instance Selection
				where (related.TaxIdGroup.TaxId entered)

		ContractDistributorInforSupplierRel
			one-to-many relation to ContractImportDistributor
			Field Mapping uses symbolic key
				related.ContractGroup          		= SupplierGroup
			Instance Selection	
				where (related.ContractImportDistributor 	= ContractSupplier)			
		
		ContractImportDistributorRel
			one-to-many relation to ContractImportDistributor
			Field Mapping uses ByDistributorSupplier
				related.ContractGroup          = SupplierGroup
				related.GPODistributorSupplier = ContractImportGPOSupplier
			Instance Selection
				where (!related.SupplierChangedFromGPOOrMapping)			 
			
		ContractImportDistributorSupplierRel
			one-to-many relation to ContractImportDistributor
			Field Mapping uses ByDistributorSupplier
				related.ContractGroup          = SupplierGroup
				related.GPODistributorSupplier = ContractImportGPOSupplier	
			Instance Selection
				where (related.SupplierChangedFromGPOOrMapping)	
				
		DistributorSupplierChangedFromMappingRel
			one-to-many relation to ContractImportDistributor
			Field Mapping uses ByDistributorSupplier
				related.ContractGroup          = SupplierGroup
				related.GPODistributorSupplier = ContractImportGPOSupplier	
			Instance Selection
				where (related.SupplierChangedFromMapping)	

		SupplierUOMMappingRel is a ContractImportGPOSupplierUOM set  

		SupplierUOMMappingNotFullyMapped is a ContractImportGPOSupplierUOM set
			Instance Selection 
				where (related.UnitOfMeasure !entered)

				
	Sets
	
		ByContractSupplier
			Sort Order
				SupplierGroup
				ContractSupplier
				ContractImportGPOSupplier
				
		BySupplierFirst
			Sort Order
				ContractImportGPOSupplier
				SupplierGroup	

	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
			Field Rules
				Active 
					default to true
			
		Update is an Update Action
			
		
			Field Rules
			
				ContractSupplier
				
					constraint (ContractSupplier.Active)
						"ContractSupplierMustBeActive"
			
					constraint (!ContractSupplier.GroupPurchasingOrganization)
						"MappedContractSupplierCannotBeAGroupPurchasingOrganization"
					
					if (ContractSupplier changed)
						constraint (!SupplierChangedManually)
							"SupplierNumberInTheProcess_ofBeingChanged;MustRun_Change_Supplier_And_VendorAction_or_Revert_Supplier_UpdateAction"
						
						constraint (!InterfaceContractsExist)
							"ContractInterfaceRecordsExist;MustRun_Update_SupplierAction"
							
						initialize DefaultDistributorContract

				DefaultDistributorContract
				
					if (DefaultDistributorContract changed
					and DefaultDistributorContract entered)
					
						constraint (ContractSupplier entered)
							"MustEnterAContractSupplierToEnterADefaultDistributorContract"
						
						constraint (DefaultDistributorContract.IsDistributorContract)
							"MustEnterADistributorContractInTheDefaultDistributorContractField" 
					
						constraint (ContractSupplier = DefaultDistributorContract.Supplier) 
							"DefaultDistributorContractSupplierMustBeTheSameAsMappedSupplier"
							
						constraint (!DefaultDistributorContract.ContractStatus.Closed)	
							"DefaultDistributorContractCannotBeClosed"
			
		Delete is a Delete Action
			restricted
			
		UpdateFast is an Update Action
			restricted
			bypass field rules
		
		MapSuppliersByTaxID is a Set Action
			Parameters
				ParmSupplierGroup    is a SupplierGroup
					default label is "SupplierGroup"
					
			Parameter Rules
			
				ParmSupplierGroup
					required
						"MustEnterSupplierGroup"
			
			Instance Selection
				where (ParmSupplierGroup = SupplierGroup
				and    ContractSupplier !entered
				and    TaxID entered)
				
			Action Rules
			
				Instance Rules
				
					if (SupplierByTaxIDRel exists)
						ContractSupplier = first SupplierByTaxIDRel.Supplier			
		
		RevertSupplierUpdate is an Instance Action
			valid when (SupplierChangedManually)
			
			Action Rules
			
				if (SupplierChangedFromMappingRel exists)
					ContractSupplier = first SupplierChangedFromMappingRel.ContractsFromImportNavRel.Supplier
					for each SupplierChangedFromMappingRel
						invoke FastUpdate each
							each.Supplier = ContractSupplier
							each.Vendor   = ContractSupplier.Vendor							
				else
				if (SupplierChangedFromMappingRel !exists
				and DistributorSupplierChangedFromMappingRel exists)
					ContractSupplier = first DistributorSupplierChangedFromMappingRel.DistributorContractNumber.Supplier
				if (DistributorSupplierChangedFromMappingRel exists)
					invoke UpdateContractSupplier ContractImportDistributor
						invoked.ContractGroup         = SupplierGroup
						invoked.ParmGPOSupplier       = ContractImportGPOSupplier
						invoked.NewSupplier           = ContractSupplier			

		UpdateSupplier is an Instance Action
			valid when (InterfaceContractsExist)
			Parameters
				UpdateSupplierOnExistingInterfaceContracts is Boolean
				NewContractSupplier                        is a Supplier
					
			Parameter Rules
				
				UpdateSupplierOnExistingInterfaceContracts

					initial value is true

				NewContractSupplier
					constraint (NewContractSupplier != ContractSupplier)
						"MustEnterADifferentSupplier"
					required
					
			Action Rules
			
				ContractSupplier    = NewContractSupplier
				if (UpdateSupplierOnExistingInterfaceContracts)
					for each ContractImportRel
						invoke FastUpdate each
							each.Supplier = NewContractSupplier
							each.Vendor   = NewContractSupplier.Vendor
					if (ContractImportDistributorRel exists)
						invoke UpdateContractSupplier ContractImportDistributor
							invoked.ContractGroup         = SupplierGroup
							invoked.ParmGPOSupplier       = ContractImportGPOSupplier
							invoked.NewSupplier           = NewContractSupplier

		CopyUOMMapping is an Instance Action 
			valid when (HasUOMMapping)
			Parameters 
				CopyToSupplier is a ContractImportGPOSupplier 

			Parameter Rules 
				CopyToSupplier 
					required
					constraint (CopyToSupplier != ContractImportGPOSupplier)
						"CannotCopyToSameSupplier"

			Action Rules 

				invoke CopyToAnotherSupplier ContractImportGPOSupplierUOM 
					invoked.ParmSupplierGroup  	= SupplierGroup
					invoked.CopyToSupplier 		= CopyToSupplier
					invoked.CopyFromSupplier	= ContractImportGPOSupplier 

		LoadForUOMMapping is an Instance Action 
			valid when (CanRunUOMMappingActions)
			Action Rules 

				if (ContractGroupRel.GPOUsed = 2
				or  ContractGroupRel.GPOUsed = 4)
					invoke LoadGPOSupplierUOMs ContractGPOItem 
						invoked.ParmSupplierGroup       = SupplierGroup
						invoked.ParmSupplier            = ContractImportGPOSupplier
				else 
				 	invoke LoadGPOSupplierUOMs ContractGPOGHXInterface 
				 		invoked.ParmSupplierGroup       = SupplierGroup 
				 		invoked.ParmSupplier            = ContractImportGPOSupplier

				
					
			
		
	
