LeaseAllocationCode is a BusinessClass
    owned by lm
    prefix is LEA
    classic name is LMEXPALLOC


    Ontology
        symbolic key is LeaseAllocationCode
            classic set name is LEASET1
            classic name is ALLOC-CD

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        Description		
            classic name is ALLOC-CD-DESC
        Status                    is Numeric size 1
            States
                Unreleased value is 0
                Released   value is 1
        TotalAllocationPercentage is an AllocationPercent
            classic name is TOT-ALLOC-PCT
        CreationDate              is TimeStamp
        CreatorID                 is an Operator 
        	holds pii
    
    Derived Fields
    
   		LeaseAllocationRepresentativeText is a StringField
			type is Text
			default label is "AllocationCode"
			LeaseAllocationCode " - " Description

	Attach Rules				
		if (parentcontext.name != "LeaseAllocationCodeDetail"   
		and parentcontext.name != "LM305")    					
			constraint (Status.Released)
				"LeaseAllocationCodeMustBeReleased"

    Conditions
        IsReleased
        	restricted
            when (Status.Released)
            
        IsCompletelyAllocated
        	restricted
        	when (TotalAllocationPercentage = 100%)

	Field Rules
		Description
			required
		
		CreationDate
			default to current timestamp
		
		CreatorID
			default to actor 

		TotalAllocationPercentage
			constraint (TotalAllocationPercentage > 0%)
				"AllocationPercentCannotBeNegative" 

	Actions
	
		Purge is a Purge Action
			restricted	
			
		T2VUpdate is an Update Action
			restricted
			default label is untranslatable
			bypass field rules
			Action Rules
				Status = Status.Released
		
	StateCycles
		
		LeaseAllocationCodeState is a StateCycle
			state field is Status
		
			Unreleased is a State
		
				Create is a Create Action
		
				Update is an Update Action
					
				Delete is a Delete Action
					
				Release is an Instance Action
					Action Rules
						constraint (IsCompletelyAllocated)
							"CannotReleaseIfTotalPercentageIsNot100%"		

						make transition to Released
				
			Released is a State		
		
				Update is an Update Action			
					Action Rules
						make transition to Unreleased
