DunningLetterHeader is a BusinessClass
	owned by ar
	prefix is DHD
	classic name is ARDUNSLHDR

	Ontology
		symbolic key is DunningLetterHeader
			classic set name is DHDSET1
			classic name for DunningLevel is DUN-LEVEL

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields

		DunningCycleID					is a DunCycleId
			classic name is DUN-CYCLE-ID
		CreditAnalyst
			classic name is CREDIT-ANLYST
		DunningProcessCode
			classic name is DUN-PROCESS-CD
		DunTextCode						is like DunningTextCode
		DunningLetterTextCode
		LanguageCode					is an IsoLocale
		CurrentBalance					is an InternationalAmount
			classic name is CURR-BAL
		AssessDunningFee				is an AssessDunFee
			classic name is ASSESS-DUN-FEE
		CreditAgencyDate				is Date
			classic name is DUNN-DATE
		IDMDocumentPID					is an DocumentPID
			protected
			restricted
		FeeAmount						is an InternationalAmount
		IDMDocumentSequenceForBasic		is Numeric size 3
			restricted
			protected
		IDMDocumentSequenceForAdvanced	is Numeric size 3
			restricted
			protected


	Local Fields
		OldDunningLevel												is a DunLevel
		LocalAdvancedDunning										is Boolean
		LocalCurrency												is like Currency
		LocalByCurrCode												is AlphaUpper size 1
			States
				DoNotPrintByCurrencyCode	value is "N"
				PrintByCurrencyCode			value is "Y"
		IDMAttributes
		IDMGenerateDocument
		LocalIDMEmailSubject										is Alpha 255
			Text Variables
				CompanyName		value is Company.Name
				CustomerName	value is Customer.Name
				PrintDate		value is TodaysDate

		SumOfDunningLetterDetailByDueDateRelDone					is Boolean	
		SumOfDunningLetterDetailByCurrencyRelDone					is Boolean	
		SumOfDunningLetterDetailByDueDateRelDerivedBaseOpenAmount	is an InternationalAmount	
		SumOfDunningLetterDetailByDueDateRelPastDueAmount			is an InternationalAmount	
		SumOfDunningLetterDetailByCurrencyRelPastDueAmount			is an InternationalAmount	


	Field Rules

		AssessDunningFee
			required


	Rule Blocks
		SumDunningLetterDetailByDueDateRelAmounts	
			if (!SumOfDunningLetterDetailByDueDateRelDone)	
				initialize SumOfDunningLetterDetailByDueDateRelDerivedBaseOpenAmount	
				initialize SumOfDunningLetterDetailByDueDateRelPastDueAmount	
					for each DunningLetterDetailByDueDateRel
						SumOfDunningLetterDetailByDueDateRelDerivedBaseOpenAmount	 += each.DerivedBaseOpenAmount	
						SumOfDunningLetterDetailByDueDateRelPastDueAmount			 += each.PastDueAmount	
				SumOfDunningLetterDetailByDueDateRelDone = true	

		SumDunningLetterDetailByCurrencyRelAmounts	
			if (!SumOfDunningLetterDetailByCurrencyRelDone)	
				initialize SumOfDunningLetterDetailByCurrencyRelPastDueAmount	
					for each DunningLetterDetailByCurrencyRel
						SumOfDunningLetterDetailByCurrencyRelPastDueAmount		 += each.PastDueAmount	
				SumOfDunningLetterDetailByCurrencyRelDone = true	


	Derived Fields

		TodaysDate  is a DerivedField
			type is Date
			return current corporate date

		TotalDunned is a DerivedField	
			type is like InternationalAmount
			include SumDunningLetterDetailByDueDateRelAmounts	
			include SumDunningLetterDetailByCurrencyRelAmounts	
			if (LocalAdvancedDunning)

				return (SumOfDunningLetterDetailByDueDateRelDerivedBaseOpenAmount)	
			else
				if (LocalByCurrCode.PrintByCurrencyCode)

					return (SumOfDunningLetterDetailByCurrencyRelPastDueAmount)	
				else

					return (SumOfDunningLetterDetailByDueDateRelPastDueAmount)	


		DunningText is a DerivedField
			type is Alpha 700
			if (LocalAdvancedDunning)
				return DerivedAdvancedDunningText
			else
				return DerivedBasicDunningText

		DerivedBasicDunningText	is a StringField
			type is Alpha 400
			DunningLetterTextCodeRel.DunningTexts.DunningText[1]
			" "
			DunningLetterTextCodeRel.DunningTexts.DunningText[2]
			" "
			DunningLetterTextCodeRel.DunningTexts.DunningText[3]
			" "
			DunningLetterTextCodeRel.DunningTexts.DunningText[4]
			" "
			DunningLetterTextCodeRel.DunningTexts.DunningText[5]
			" "
			DunningLetterTextCodeRel.DunningTexts.DunningText[6]

		DerivedAdvancedDunningText is a StringField
			type is Alpha 700
			DunningTextCodeRel.DunningText.DunningText[1]
			" "
			DunningTextCodeRel.DunningText.DunningText[2]
			" "
			DunningTextCodeRel.DunningText.DunningText[3]
			" "
			DunningTextCodeRel.DunningText.DunningText[4]
			" "
			DunningTextCodeRel.DunningText.DunningText[5]
			" "
			DunningTextCodeRel.DunningText.DunningText[6]
			" "
			DunningTextCodeRel.DunningText.DunningText[7]
			" "
			DunningTextCodeRel.DunningText.DunningText[8]
			" "
			DunningTextCodeRel.DunningText.DunningText[9]
			" "
			DunningTextCodeRel.DunningText.DunningText[10]
			" "
			DunningTextCodeRel.DunningText.DunningText[11]
			" "
			DunningTextCodeRel.DunningText.DunningText[12]

		DerivedFileName is a DerivedField
			type is Alpha 100
			restricted
			if (LocalAdvancedDunning)
				return "Dunning Letter " + Company + "-" + Customer + "-" + DerivedDate
			else
				return "Dunning Letter " + Company + "-" + Customer + "-" + DerivedCurrency + "-" + DerivedDate

		DerivedDate is a DerivedField
			type is Alpha 8
			restricted
			return current corporate date

		DerivedCreditAnalystPhoneNumber is a StringField
			type is Alpha up to 100
			CreditAnalyst.PhoneNumber.SubscriberNumber
			"-"
			CreditAnalyst.PhoneNumber.Extension

		DerivedCurrency is a DerivedField
			type is like Currency
			if (LocalAdvancedDunning)
				return Company.Currency
			else
				if (LocalByCurrCode.PrintByCurrencyCode)
					return LocalCurrency
				else
					return Company.Currency

		DerivedBasicDunningLetterEmailSubject is a MessageField
			restricted
			"DunningLetter<Company.Name>_<Customer>"

		DerivedAdvancedDunningLetterEmailSubject is a MessageField
			restricted
			"DunningLetter<Company.Name>_<Customer>"

		DerivedCompanyBasicDunningEmailSubject is a DerivedField
			type is Alpha size 255
			restricted
			LocalIDMEmailSubject = Company.BasicDunningLetterIDMEmailSubject
			return LocalIDMEmailSubject text

		DerivedCompanyAdvancedDunningEmailSubject is a DerivedField
			type is Alpha size 255
			restricted
			LocalIDMEmailSubject = Company.AdvancedDunningLetterIDMEmailSubject
			return LocalIDMEmailSubject text


	Conditions

		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	


	Relations

		DunningTextCodeRel
			one-to-one relation to DunningTextCode
			Field Mapping uses symbolic key
				related.IsoLocale		 = LanguageCode
				related.DunningTextCode	 = DunTextCode

		DunningLetterTextCodeRel
			one-to-one relation to DunningLetterTextCode
			Field Mapping uses symbolic key
				related.Company					 = Company
				related.IsoLocale				 = LanguageCode
				related.DunningLetterTextCode	 = DunningLetterTextCode

		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company				 = Company
				related.Customer			 = Customer

		DunningLetterDetailRel
			one-to-many relation to DunningLetterDetail
			required
			Field Mapping uses symbolic key
				related.Company				 = Company
				related.Customer			 = Customer
				related.DunningLevel		 = DunningLevel

		OldDunningLetterDetailRel
			one-to-many relation to DunningLetterDetail
			required
			Field Mapping uses symbolic key
				related.Company				 = Company
				related.Customer			 = Customer
				related.DunningLevel		 = OldDunningLevel

		DunningLetterDetailByDueDateRel
			one-to-many relation to DunningLetterDetail
			Field Mapping uses Set2
				related.Company				 = Company
				related.Customer			 = Customer
				related.DunningLevel		 = DunningLevel

		DunningLetterDetailByCurrencyRel
			one-to-many relation to DunningLetterDetail
			Field Mapping uses Set2
				related.Company				 = Company
				related.Customer			 = Customer
				related.DunningLevel		 = DunningLevel
				related.Currency			 = LocalCurrency

		DunningLetterDetailDistinctCurrencyRel
			one-to-many relation to DunningLetterDetail
			Field Mapping uses Set2
				related.Company				 = Company
				related.Customer			 = Customer
				related.DunningLevel		 = DunningLevel
			Instance Selection
				where (related.Currency entered)

		DunningLetterRel
			one-to-one relation to DunningLetter
			required
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				 = GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.DunningLetter.DunningProcessCode	 = DunningProcessCode
				related.DunningLetter.DunningLevel			 = DunningLevel

		DunningLetterTotalsRel
			one-to-many relation to DunningLetterTotals
			Field Mapping uses symbolic key
				related.Company			 = Company
				related.Customer		 = Customer
				related.DunningLevel	 = DunningLevel
			Instance Selection
				where (related.Currency entered)

		CompanyCustomerContactForAdvancedDunningIDMAutoEmailRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses symbolic key
				related.Company		 = Company
				related.Customer	 = Customer
			Instance Selection
				where (related.EmailAdvancedDunningLetter
				and	related.EmailAddress entered)

		CompanyCustomerContactForBasicDunningIDMAutoEmailRel
			one-to-many relation to CompanyCustomerContact
			Field Mapping uses symbolic key
				related.Company = Company
				related.Customer = Customer
			Instance Selection
				where (related.EmailBasicDunningLetter
				and	related.EmailAddress entered)

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company




	Create Rules
		constraint (DunningLetterRel exists)
			"DunningLevelNotDefinedForProcessCode"		


	Actions

		Create is a Create Action
			restricted


		Update is an Update Action
			Action Rules
				if (DunningLevel changed)
					OldDunningLevel				= old DunningLevel
					invoke Update OldDunningLetterDetailRel
						invoked.DunningLevel	= DunningLevel


		Delete is a Delete Action
			Entrance Rules
				invoke Delete DunningLetterDetailRel
				invoke Delete DunningLetterTotalsRel


		UploadAdvancedDunningLetterInIDM is an Instance Action
			restricted
			Parameters
				PrmIDMPrinter				is an IDMPrinter

			Local Fields


				LocalRecipientEmailAddress	is Alpha 4000
					holds pii

			Action Rules
				initialize IDMGenerateDocument
				LocalAdvancedDunning = true

				IDMGenerateDocument.IDMXMLDefinition.Busclass		= reference to this instance
				IDMGenerateDocument.IDMXMLDefinition.ListName		= "DunningLetterHeaderListforIDMDoc"
				IDMGenerateDocument.IDMXMLDefinition.DocumentName	= "AdvancedDunningLetter"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName		= "DunningLetterDetailByDueDateRel"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName			= "DunningLetterDetailListforIDMDoc"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1	= 1
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag			= "Lines"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag			= "Line"			

				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].RelationName		= "DunningLetterTotalsRel"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListName			= "DunningLetterTotalsListForIDMDoc"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].LevelSection1	= 2
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ListTag			= "Totals"
				IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[2].ItemTag			= "Total"
				initialize IDMAttributes

				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "Company"
				IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= Company
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "Customer"
				IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= Customer
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "Currency"
				IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue	= DerivedCurrency
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "PrintDate"
				IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeDate	= TodaysDate
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "CompanyName"
				IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= Company.Name
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "CustomerName"
				IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= Customer.Name

				IDMGenerateDocument.IDMAttributes		 = IDMAttributes
				IDMGenerateDocument.TemplateUniqueId	 = Company.AdvancedDunningLetterTemplate.IDMUniqueId
				IDMGenerateDocument.DocumentType		 = "FSM_AdvancedDunningLetter"
				IDMGenerateDocument.FileName			 = DerivedFileName + Company.AdvancedDunningLetterTemplate.DerivedOutputFormat

				if (CompanyCustomerRel.EmailAdvancedDunningLetter)
					if (not CompanyCustomerRel.AdvancedDunningLetterEmailContactsOnly)
						LocalRecipientEmailAddress += CompanyCustomerRel.EmailAddress

					for each CompanyCustomerContactForAdvancedDunningIDMAutoEmailRel
						if (LocalRecipientEmailAddress entered)
							LocalRecipientEmailAddress += ","
						LocalRecipientEmailAddress += each.EmailAddress

					if (Company.AdvancedDunningLetterIDMEmailSubject entered)
						IDMGenerateDocument.IDMEmail.Subject		= DerivedCompanyAdvancedDunningEmailSubject
					else
						IDMGenerateDocument.IDMEmail.Subject		= DerivedAdvancedDunningLetterEmailSubject

					if (Company.AdvancedDunningLetterIDMEmailTemplate entered)
						IDMGenerateDocument.EmailTemplateUniqueID	= Company.AdvancedDunningLetterIDMEmailTemplate.IDMUniqueId
					else
						IDMGenerateDocument.IDMEmail.Body			= ""

					IDMGenerateDocument.IDMEmail.From				= CompanyCustomerRel.AdvancedDunningLetterFromAndReplyToEmail
					IDMGenerateDocument.IDMEmail.To					= LocalRecipientEmailAddress


				if (CompanyCustomerRel.PrintAdvancedDunningLetter)
					if (PrmIDMPrinter entered)
						IDMGenerateDocument.IDMPrinter	 = PrmIDMPrinter 

				IDMGenerateDocument.IDMAccessControlList = "CSFDefined"

				invoke CreateFromGenerateDocument IDMJob
					invoked.Actor				 = actor
					invoked.Description			 = "Advanced Dunning Letter"
					invoked.IDMGenerateDocument	 = IDMGenerateDocument


		UploadBasicDunningLetterInIDM is an Instance Action
			restricted
			Parameters
				PrmByCurrCd					is AlphaUpper size 1
					States
						DoNotPrintByCurrencyCode	value is "N"
						PrintByCurrencyCode			value is "Y"
				PrmIDMPrinter				is an IDMPrinter

			Local Fields


				LocalRecipientEmailAddress	is Alpha 4000
					holds pii

			Rule Blocks
				GenerateIDMDocument
					initialize IDMGenerateDocument
					initialize LocalAdvancedDunning

					IDMGenerateDocument.IDMXMLDefinition.Busclass		= reference to this instance
					IDMGenerateDocument.IDMXMLDefinition.ListName		= "DunningLetterHeaderListforIDMDoc"
					IDMGenerateDocument.IDMXMLDefinition.DocumentName	= "BasicDunningLetter"

					if (PrmByCurrCd.PrintByCurrencyCode)
						IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName	= "DunningLetterDetailByCurrencyRel"
					else
						IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].RelationName	= "DunningLetterDetailByDueDateRel"

					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListName			= "DunningLetterDetailListforIDMDoc"
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].LevelSection1	= 1
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ListTag			= "Lines"
					IDMGenerateDocument.IDMXMLDefinition.IDMXMLChildren.IDMXMLChild[1].ItemTag			= "Line"

					initialize IDMAttributes

					IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeName	= "Company"
					IDMAttributes.SingleValue.IDMAttribute[1].IDMAttributeValue	= Company
					IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeName	= "Customer"
					IDMAttributes.SingleValue.IDMAttribute[2].IDMAttributeValue	= Customer
					IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeName	= "Currency"
					IDMAttributes.SingleValue.IDMAttribute[3].IDMAttributeValue	= DerivedCurrency
					IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeName	= "PrintDate"
					IDMAttributes.SingleValue.IDMAttribute[4].IDMAttributeDate	= TodaysDate
					IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeName	= "CompanyName"
					IDMAttributes.SingleValue.IDMAttribute[5].IDMAttributeValue	= Company.Name
					IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeName	= "CustomerName"
					IDMAttributes.SingleValue.IDMAttribute[6].IDMAttributeValue	= Customer.Name

					IDMGenerateDocument.IDMAttributes	 = IDMAttributes
					IDMGenerateDocument.TemplateUniqueId = Company.BasicDunningLetterTemplate.IDMUniqueId
					IDMGenerateDocument.DocumentType	 = "FSM_BasicDunningLetter"
					IDMGenerateDocument.FileName		 = DerivedFileName + Company.BasicDunningLetterTemplate.DerivedOutputFormat

					if (CompanyCustomerRel.EmailBasicDunningLetter)
						if (not CompanyCustomerRel.BasicDunningLetterEmailContactsOnly)
							LocalRecipientEmailAddress += CompanyCustomerRel.EmailAddress

						for each CompanyCustomerContactForBasicDunningIDMAutoEmailRel
							if (LocalRecipientEmailAddress entered)
								LocalRecipientEmailAddress += ","
							LocalRecipientEmailAddress += each.EmailAddress

						if (Company.BasicDunningLetterIDMEmailSubject entered)
							IDMGenerateDocument.IDMEmail.Subject		= DerivedCompanyBasicDunningEmailSubject
						else
							IDMGenerateDocument.IDMEmail.Subject		= DerivedBasicDunningLetterEmailSubject

						if (Company.BasicDunningLetterIDMEmailTemplate entered)
							IDMGenerateDocument.EmailTemplateUniqueID	= Company.BasicDunningLetterIDMEmailTemplate.IDMUniqueId
						else
							IDMGenerateDocument.IDMEmail.Body			= ""

						IDMGenerateDocument.IDMEmail.From				= CompanyCustomerRel.BasicDunningLetterFromAndReplyToEmail
						IDMGenerateDocument.IDMEmail.To					= LocalRecipientEmailAddress


					if (CompanyCustomerRel.PrintBasicDunningLetter)
						if (PrmIDMPrinter entered)
							IDMGenerateDocument.IDMPrinter = PrmIDMPrinter 

					IDMGenerateDocument.IDMAccessControlList = "CSFDefined"

					invoke CreateFromGenerateDocument IDMJob
						invoked.Actor				= actor
						invoked.Description			= "Basic Dunning Letter"
						invoked.IDMGenerateDocument = IDMGenerateDocument

			Action Rules
				LocalByCurrCode = PrmByCurrCd
				if (PrmByCurrCd.PrintByCurrencyCode)
					for each distinct Currency in DunningLetterDetailDistinctCurrencyRel
						LocalCurrency = each.Currency
						if (DunningLetterDetailByCurrencyRel exists)
							include GenerateIDMDocument
				else
					include GenerateIDMDocument



