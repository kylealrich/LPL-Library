SalesRepresentative is a BusinessClass
	owned by ar
	prefix is SAW
	classic name is SALESREP

	Ontology
		symbolic key is SalesRepresentative
			classic set name is SAWSET1
			classic name is SALESMAN

	Patterns
		implements StaticJava
		disable AuditIndex

	Persistent Fields
		Name
		Territory
		CommisionPercent	is Percent size 7.5
			classic name is COMM-RATE
		UsedFlag			is Boolean
			classic name is USED-FL
		Active				is Boolean
			classic name is ACTIVE-STATUS


	Derived Fields
		ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforSalesRepresentative"

		ContextMessageText is a MessageField
			restricted
			"SalesRepresentative<SalesRepresentative>"

		RepresentativeText is a StringField	
			type is Text
			default label is "SalesRepresentative"
			SalesRepresentative " - " Name

		DerivedSalesRepresentativeText	is a DerivedField	
			type is Alpha size up to 132
			default label is "SalesRepresentative"
			return RepresentativeText

	Field Rules
		Name
			required
		Territory
			required


	Relations
		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company


	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)



	Attach Rules
		constraint (Active)
			"SalesRepresentativeIsNotActive"


	Actions
		Create is a Create Action


		Update is an Update Action


		Delete is a Delete Action


		Anonymize is an Instance Action	
			restricted
			Action Rules
				Name	= 999999999

			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup			= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.Status							= 1
					invoked.AffectedBusinessClass			= "AR10"
					invoked.ReceivableCompany				= Company
					invoked.SalesRepresentative				= SalesRepresentative


		PurgeAuditLog is an Instance Action	
			restricted
			Action Rules
				invoke purge audit log entries

			Exit Rules
				invoke Create AnonymizeLog
					invoked.FinanceEnterpriseGroup			= GeneralLedgerCompanyRel.FinanceEnterpriseGroup
					invoked.Status							= 2
					invoked.AffectedBusinessClass			= "AR10"
					invoked.ReceivableCompany				= Company
					invoked.SalesRepresentative				= SalesRepresentative

