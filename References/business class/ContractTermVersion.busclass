ContractTermVersion is a BusinessClass
    owned by po
    prefix is CTVR

    Ontology
    	symbolic key is ContractTermVersion

    Persistent Fields
    	VersionOpenStamp			is TimeStamp
    	VersionCloseStamp			is TimeStamp
		ActionTaken					is Numeric 2
			States
				Approved	value is 1
				Disapproved	value is 2
				Rejected	value is 3
				Pending		value is 4
		ApprovalStage				is Numeric 2
			States
				BeforeSupplierNegotiation			value is 1
				SupplierNegotiation					value is 2
				AfterSupplierNegotiation			value is 3
				BeforeSupplierAddendumNegotiation	value is 4
				SupplierAddendumNegotiation			value is 5
				AfterSupplierAddendumNegotiation	value is 6
		LastAction					is Numeric 2
			States
				ModifiedBySupplier			value is 2
				ModifiedByPrimaryContact 	value is 3
				ApprovedBySupplier			value is 4
				ApprovedByPrimaryContact	value is 5
				ModifiedByFinalApprover		value is 6
				FinalApproval				value is 7
		ReasonCode						is AlphaUpper 50
		ActivatedVersion				is Boolean
		ModificationActor				is an Actor
		ModificationComment				is Text
		SupplierApprovalActor			is an Actor
		SupplierApprovalComment			is Text
		PrimaryContactApprovalActor		is an Actor
		PrimaryContactApprovalComment	is Text
		FinalApproverActor				is an Actor

	Field Rules
		VersionOpenStamp
    		default to current timestamp
    	
    	ActionTaken
    		default to 4	

	Derived Fields
		DerivedOriginalVersionNumber is a DerivedField
			type is Numeric 4
			restricted
			return first ContractTermVersionRel.ContractTermVersion

		InitialApprovalMessage is a MessageField
			restricted
			"InitialApproval"
			
		ModifiedMessage is a MessageField
			restricted
			"Modified"
			
		ApprovedMessage is a MessageField
			restricted
			"Approved"
			
		FinalApprovalMessage is a MessageField
			restricted
			"FinalApproval"
			
		DisapprovedMessage is a MessageField
			restricted
			"Disapproved"
			
		RejectedMessage is a MessageField
			restricted
			"Rejected"
			
		YesMessage is a MessageField
			restricted
			"Yes"
		
		DerivedAction is a DerivedField
			type is Alpha 20
			if ((ApprovalStage.BeforeSupplierNegotiation
			or	ApprovalStage.BeforeSupplierAddendumNegotiation)
			and	LastAction.FinalApproval)
				return InitialApprovalMessage
			if (LastAction.ModifiedBySupplier
			or  LastAction.ModifiedByPrimaryContact
			or  LastAction.ModifiedByFinalApprover)
				return ModifiedMessage
			else
			if (LastAction.ApprovedBySupplier
			or	LastAction.ApprovedByPrimaryContact)
				return ApprovedMessage
			else
			if (LastAction.FinalApproval)
				return FinalApprovalMessage
			else
				return ""

		DerivedPreviousActivatedVersionNumber is a DerivedField
			type is Numeric 4
			restricted
			return last PreviousActivatedContractTermVersionRel.ContractTermVersion

		DerivedFirstActivatedVersionNumber is a DerivedField
			type is Numeric 4
			restricted
			return first ActivatedContractTermVersionRel.ContractTermVersion

		SupplierAndName is a StringField
			type is Alpha 45
			Contract.Supplier
			" - "
			Contract.Supplier.SupplierName

		DerivedActor is a DerivedField
			type is Alpha 101
			if ((ApprovalStage.BeforeSupplierNegotiation
			or	ApprovalStage.BeforeSupplierAddendumNegotiation)
			and	LastAction.FinalApproval)
				return FinalApproverActor.FirstNameAndLastNameOrActor
			else
			if (LastAction.ModifiedBySupplier
			or  LastAction.ModifiedByPrimaryContact
			or  LastAction.ModifiedByFinalApprover)
				return ModificationActor.FirstNameAndLastNameOrActor
			else
			if (LastAction.FinalApproval)
				return FinalApproverActor.FirstNameAndLastNameOrActor
			else
			if (LastAction.ApprovedByPrimaryContact)
				return PrimaryContactApprovalActor.FirstNameAndLastNameOrActor
			else
			if (LastAction.ApprovedBySupplier)
				return SupplierApprovalActor.FirstNameAndLastNameOrActor
			else
				return ""

		DerivedComment is a DerivedField
			type is Text
			if (LastAction.ModifiedBySupplier
			or  LastAction.ModifiedByPrimaryContact
			or  LastAction.ModifiedByFinalApprover)
				return ModificationComment
			else
			if (LastAction.ApprovedByPrimaryContact)
				return PrimaryContactApprovalComment
			else
			if (LastAction.ApprovedBySupplier)
				return SupplierApprovalComment
			else
				return ""

		DerivedApproved is a DerivedField
			type is Alpha 5
			restricted
			if (LastAction.FinalApproval)
				return YesMessage
			else
				return blank

    Conditions
		OriginalVersion
			restricted
			when (DerivedOriginalVersionNumber = ContractTermVersion)
		ModifiedVersion
			restricted
			when (ModificationActor entered)
		PendingContractTermAndConditionUpdatesExist
			restricted
			when (PendingContractTermAndConditionUpdateRel exists)
		FirstActivatedVersion
			restricted
			when (DerivedFirstActivatedVersionNumber = ContractTermVersion)
		HasApproval
			restricted
			when (SupplierApprovalActor entered
			or	  PrimaryContactApprovalActor entered
			or	  FinalApproverActor entered)
		FinalApproverActorEntered
			restricted
			when (FinalApproverActor entered)

    Relations
		ContractTermVersionArticleRel is a ContractTermVersionArticle set

		ContractArticlesByDisplayRel
			one-to-many relation to ContractTermVersionArticle
			Field Mapping uses ByDisplayOrder
				related.ContractGroup 		= ContractGroup
				related.Contract      		= Contract
				related.ContractTermVersion	= ContractTermVersion

		ContractTermVersionRel
			one-to-many relation to ContractTermVersion
			Field Mapping uses symbolic key
				related.ContractGroup		= ContractGroup
				related.Contract			= Contract

		PendingContractTermAndConditionUpdateRel
			one-to-many relation to ContractTermAndConditionUpdate
			Field Mapping uses ByTermNegotiationVersion
				related.ContractGroup 			= ContractGroup
				related.Contract      			= Contract
				related.TermNegotiationVersion	= ContractTermVersion
			Instance Selection
				where (related.Status.Pending)

		PreviousActivatedContractTermVersionRel
			one-to-many relation to ContractTermVersion
			Field Mapping uses symbolic key
				related.ContractGroup		= ContractGroup
				related.Contract			= Contract
			Instance Selection
				where (related.ContractTermVersion	< ContractTermVersion
				and	   related.ActivatedVersion)

		ActivatedContractTermVersionRel
			one-to-many relation to ContractTermVersion
			Field Mapping uses symbolic key
				related.ContractGroup		= ContractGroup
				related.Contract			= Contract
			Instance Selection
				where (related.ActivatedVersion)

		FirstActivatedContractTermVersionRel
			one-to-many relation to ContractTermVersion
			Field Mapping uses symbolic key
				related.ContractGroup		= ContractGroup
				related.Contract			= Contract
			Instance Selection
				where (related.ContractTermVersion	= DerivedFirstActivatedVersionNumber)

	Sets
 		ByVersionOpenStamp
 			indexed
 			Sort Order
 				VersionOpenStamp
 				ContractTermVersion
 				Contract
 				ContractGroup

    Actions
    	CreateDeletedTerms is an Instance Action
    		restricted
			Action Rules
				for each ContractTermVersionArticleRel
					invoke CreateDeletedTerms each
		
		ApproveTermChanges is an Instance Action
			restricted
			Action Rules
				invoke Negotiation.NeedsSupplierApproval.ApproveTermChanges Contract

   		Create is a Create Action
   			restricted

   		Update is an Update Action
   			restricted

   		Delete is a Delete Action
   			restricted
