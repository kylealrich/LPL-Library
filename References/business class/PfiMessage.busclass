PfiMessage is a BusinessClass
    owned by pfi
    prefix is PfiME
    default label is "Message"
    disable data area copy

    Ontology
        symbolic key is PfiMessage

    Patterns
        implements CRUD
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated
        implements IncrementalReplication
            indicator field is IsReplicated
                replicate when false
                    then set to true

    Persistent Fields

        AttachDate  is TimeStamp
        AttachBy    is like Actor // LMRK-97977 Was "is Actor". Likely meant to be "is an Actor". Changed to "like" so we have the same dict type but without changing existing functionality.
            default label is "AttachedBy"
        Message     is Text
        Subject     is a PfiSubject
        MessageType is Numeric size 1
            States
                Workunit   value is 1
                	default label is "WorkUnit"
                Validation value is 2
                Comment    value is 3
                Action     value is 4
        IsReplicated is Boolean
            default label is untranslatable

    Derived Fields

        AttachByName is a DerivedField
            type is Alpha size 101
            default label is "AttachedByName"
            return PfiActorRel.FirstNameAndLastNameOrActor

    Field Rules

        AttachDate
            default to current time

        AttachBy
            default to actor

        MessageType
            default to MessageType.Workunit
            initial value is MessageType.Workunit

    Relations

        PfiActorRel
            one-to-one relation to Actor
            Field Mapping uses symbolic key
                related.Actor = AttachBy

    Sets

        BySubject
            sql name is PfiME01
            indexed
            Sort Order
                Subject
                PfiWorkunit
                PfiMessage

        ByMessageType
            sql name is PfiME02
            indexed
            Sort Order
                MessageType
                PfiWorkunit
                PfiMessage

        ByIsReplicatedUniqueID
            sql name is PfiME03
            indexed
            Sort Order
                IsReplicated
                UniqueID

    Actions

        Create is a Create Action

        Update is an Update Action

            Exit Rules
                IsReplicated = false

        Delete is a Delete Action
