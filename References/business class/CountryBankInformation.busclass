CountryBankInformation is a BusinessClass
	default label is "Country/\JurisdictionBankInformation"
	owned by cb
	
	prefix is Cbin
	
	Ontology
		symbolic key is CountryBankInformation
	
	Patterns
	
	Persistent Fields
		Country
        BankAccountValidationType
		UseBankEntity										is Boolean
		BankEntityLabel                                     is Alpha 25
			translatable
		ConfirmBankEntity                                   is Boolean
			default label is "RequireConfirmationOfBankEntityValueIfItDoesNotExist"
		UseBankAccountNumber                                is Boolean
		BankAccountNumberLabel                              is Alpha 25
			translatable
		ConfirmBankAccountNumber                            is Boolean
			default label is "RequireConfirmationOfBankAccountNumber"
		UseBankAccountType                                  is Boolean 
		UseBankCurrency                                     is Boolean
		UseCrossBorderIdentifier                            is Boolean

		UseSwiftID                                          is Boolean


		UseBankingCode                                      is Boolean

		UseBankID                                           is Boolean

		UseAssignmentNumber                                 is Boolean
		UseGiroNumber                                       is Boolean
		UseBACSReference                                    is Boolean
		UseBACSName                                         is Boolean
		UseCreditCardNumber                                 is Boolean
	
	Derived Fields
		DerivedCountryName is a ConditionalField
  			type is Alpha size 40
  			if (Country entered)
  				Country.Name
  			else
  				DefaultMessage
  				
  		DefaultMessage is a MessageField
  			restricted
  			"DEFAULT"
	
	Conditions
	
		BankInformationUsed
			restricted
			when (SupplierRel exists
			or    SupplierSourceIdRel exists)				
	
	Relations
		CountryRel  
			one-to-many relation to CountryBankInformation
        	Field Mapping uses ByCountry
        		related.Country	  = Country
	
		SupplierRel
			one-to-many relation to Supplier
			Field Mapping uses symbolic key
			Instance Selection
				where (related.BankInformationGroup.Country = Country
				and    related.HasBankInformation)	  
		
		SupplierSourceIdRel
			one-to-many relation to SupplierSourceId
			Field Mapping uses symbolic key
			Instance Selection
				where (related.BankInformationGroup.Country = Country
				and    related.HasBankInformation)

		FactSheetRel
			one-to-one relation to SampleDocumentTemplate
			Field Mapping uses symbolic key
				related.SampleDocumentTemplate      = "BankInformationForSuppliers_ST"

	Sets
		ByCountry
			Sort Order
				Country
				CountryBankInformation
	
	Field Rules
	
		BankAccountValidationType
			if (BankAccountValidationType entered)
				constraint (UseBankAccountNumber = true)
					"MustCheckBankAccountNumberWhenABankAccountValidationTypeIsEntered"
		
		UseBankEntity
			if (BankAccountValidationType.NationalClearingSystem)
				constraint (UseBankEntity = true)		
					"MustCheckBankEntityWith_\National_\Clearing_\SystemBankAccountValidationType"
					
		UseBankAccountNumber
			if (BankAccountValidationType entered					
			and not BankAccountValidationType.Check)
				constraint (UseBankAccountNumber = true)
					"MustCheckBankAccountNumberWithBankAccountValidationTypeOf<BankAccountValidationType>"			 	
	
		BankAccountNumberLabel
			if (UseBankAccountNumber = true)
				default to "Bank Account Number"
			else
				initialize
				
		BankEntityLabel
			if (UseBankEntity = true)
				default to "Bank Entity"
			else
				initialize	

		UseSwiftID
			if (BankAccountValidationType.IbanAndBIC
			or  BankAccountValidationType.BicCode)
				required
					"MustCheckUseSwiftIDIfBankAccountValidationTypeIsIBANAndBICOrBICCode" 
				
	Actions
	
		Create is a Create Action
		
			Action Rules
				constraint (CountryRel !exists)
					"CanOnlyEnterOneSetOfCountry/\jurisdictionBankInformationPerCountry/\jurisdiction,OrOneDefaultSet"
		
				if (UseBankAccountType = true)
					constraint (UseBankAccountNumber = true)
						"MustCheckBankAccountNumberIfBankAccountTypeIsChecked"
				
				if  (UseBankEntity            = false										
				and UseBankAccountNumber      = false
	        	and UseBankAccountType        = false
				and UseBankCurrency           = false                          
				and UseCrossBorderIdentifier  = false                          
				and UseSwiftID                = false                          
				and UseBankingCode            = false                          
				and UseBankID                 = false                          
				and UseAssignmentNumber       = false                          
				and UseGiroNumber             = false                          
				and UseBACSReference          = false                          
				and UseBACSName               = false)                          
					constraint (UseCreditCardNumber = true)
						"MustSelectAtLeastOneBankInformationFieldForACountry/\jurisdiction" 
		
		Update is an Update Action
		
			Action Rules
			
				if (UseBankAccountType = true)
					constraint (UseBankAccountNumber = true)
						"MustCheckBankAccountNumberIfBankAccountTypeIsChecked"
				
				if ((UseBankEntity 			  changed
				and  UseBankEntity            = false)
				or  (UseBankAccountNumber     changed										
				and UseBankAccountNumber      = false)
				or  (UseBankAccountType       changed
	        	and UseBankAccountType        = false)
	        	or  (UseBankCurrency          changed
				and UseBankCurrency           = false)
				or  (UseCrossBorderIdentifier changed                          
				and UseCrossBorderIdentifier  = false)
				or  (UseSwiftID               changed                          
				and UseSwiftID                = false)
				or  (UseBankingCode           changed                          
				and UseBankingCode            = false)
				or  (UseBankID                changed                          
				and UseBankID                 = false)
				or  (UseAssignmentNumber      changed                          
				and UseAssignmentNumber       = false)
				or  (UseGiroNumber            changed                        
				and UseGiroNumber             = false)
				or  (UseBACSReference         changed                          
				and UseBACSReference          = false)
				or  (UseBACSName              changed                          
				and UseBACSName               = false))
					constraint (!BankInformationUsed)
						"CannotRemoveBankInformationSelection;BankInformationIsEnteredOnSuppliersForThisCountry/\jurisdiction"    

				
				if  (UseBankEntity            = false										
				and UseBankAccountNumber      = false
	        	and UseBankAccountType        = false
				and UseBankCurrency           = false                          
				and UseCrossBorderIdentifier  = false                          
				and UseSwiftID                = false                          
				and UseBankingCode            = false                          
				and UseBankID                 = false                          
				and UseAssignmentNumber       = false                          
				and UseGiroNumber             = false                          
				and UseBACSReference          = false                          
				and UseBACSName               = false)                          
					constraint (UseCreditCardNumber = true)
						"MustSelectAtLeastOneBankInformationFieldForACountry/\jurisdiction" 
		
		Delete is a Delete Action			
