AssetAccountingUnitGroup is a BusinessClass
    owned by am
    prefix is AAU
    classic name is AMAUGROUP
	default label is "AssetDimensionGroup"
	
    Ontology
        symbolic key is AssetAccountingUnitGroup
            classic set name is AAUSET1
            classic name is AU-GROUP

    Patterns
        implements StaticJava
        implements Proxy for ImpairmentDimensionsExtRel 
        disable AuditIndex

    Persistent Fields

        Description
        AssetAccountingUnit                   is a FinanceCodeBlockNoAccountFull 
            classic name is AST-ACCT-UNIT
            default label is "Asset"
        AccumulatedDepreciationAccountingUnit is a FinanceCodeBlockNoAccountFull 
            classic name is ACM-ACCT-UNIT
            default label is "AccumulatedDepreciation"
        DepreciationExpenseAccountingUnit     is a FinanceCodeBlockNoAccountFull 
            sql prefix is DEPEX
            classic name is EXP-ACCT-UNIT
            default label is "DepreciationExpense"            
        ClearingAccountingUnit                is a FinanceCodeBlockNoAccountFull 
            classic name is CLR-ACCT-UNIT
            default label is "AssetClearing"            
        ProceedsAccountingUnit                is a FinanceCodeBlockNoAccountFull 
            classic name is PRO-ACCT-UNIT
            default label is "DisposalProceeds"            
        DisposalExpenseAccountingUnit         is a FinanceCodeBlockNoAccountFull 
            sql prefix is DISEX
            classic name is COS-ACCT-UNIT
            default label is "DisposalExpense"
        GainAccountingUnit                    is a FinanceCodeBlockNoAccountFull 
            classic name is GAN-ACCT-UNIT
            default label is "DisposalGain"
        LossAccountingUnit                    is a FinanceCodeBlockNoAccountFull 
            classic name is LOS-ACCT-UNIT
            default label is "DisposalLoss"
        DeferredGainAccountingUnit            is a FinanceCodeBlockNoAccountFull 
            classic name is DGN-ACCT-UNIT
            default label is "DeferredGain" 
        DeferredLossAccountingUnit            is a FinanceCodeBlockNoAccountFull 
            classic name is DLS-ACCT-UNIT
            default label is "DeferredLoss"
        RevalueSurplusAccountingUnit          is a FinanceCodeBlockNoAccountFull 
            classic name is RVS-ACCT-UNIT
            default label is "RevalueGain"
        RevalueLossAccountingUnit             is a FinanceCodeBlockNoAccountFull 
            classic name is RVL-ACCT-UNIT
            default label is "RevalueLoss"
		RevalueAmortization			  		  is a FinanceCodeBlockNoAccountFull 	
		RevalueReserve						  is a FinanceCodeBlockNoAccountFull 
		ReviewApprovalCode					  is a ApprovalCode
  
    Derived Fields
    
    	DepreciationExpenseAccountingUnitDimensionCode		is a StringField
			type is Alpha 70
			restricted
    		DepreciationExpenseAccountingUnit.DimensionCode 

		AssetDimensionGroup is a MessageField
			"<AssetAccountingUnitGroup>-<Description>"        
            
    Conditions

        AssetAdjustmentExists
        	restricted
            classic name is ADJUST-EXIST
            when (first AssetAdjustmentsRel exists)

        AssetAccountingUnitGroupEntered
        	restricted
            classic name is AUGRP-EXIST
            when (AssetAccountingUnitGroup entered)

        CompanyEntered
        	restricted
            classic name is COMPANY-EXIST
            when (Company entered)

		HasRevalueAccountsEntered
        	when (RevalueSurplusAccountingUnit entered
	        and RevalueLossAccountingUnit entered)

        AssetTransferExists
        	restricted
            classic name is TRF-EXIST
            when (first AssetTransfersRel exists)

		HasImpairmentAccounts 
			when (ImpairmentDimensionsExtRel.ImpairmentLoss entered
			and ImpairmentDimensionsExtRel.AccumulatedImpairmentLoss entered
			and	ImpairmentDimensionsExtRel.ImpairmentAmortization entered
			and	ImpairmentDimensionsExtRel.ImpairmentWriteOff entered)		
				
				
		HasRevaluationReserveAccounts
			when (RevalueAmortization entered
			and RevalueReserve entered)
			
		HasAllRequiredDimensions
			restricted
			when (AssetAccountingUnit entered
			and AccumulatedDepreciationAccountingUnit entered
			and DepreciationExpenseAccountingUnit entered  
			and ClearingAccountingUnit entered
			and ProceedsAccountingUnit entered
			and DisposalExpenseAccountingUnit entered
			and GainAccountingUnit entered
			and LossAccountingUnit entered
	        and DeferredGainAccountingUnit entered
			and DeferredLossAccountingUnit entered
			and ((Company.Revalue = true 
			and RevalueSurplusAccountingUnit entered
	        and RevalueLossAccountingUnit entered)
	        or (Company.Revalue = false)))

		HasRequiredDimensions
			restricted
			when (AssetAccountingUnit entered
			and AccumulatedDepreciationAccountingUnit entered
			and DepreciationExpenseAccountingUnit entered  
			and ClearingAccountingUnit entered
			and ProceedsAccountingUnit entered
			and DisposalExpenseAccountingUnit entered
			and GainAccountingUnit entered
			and LossAccountingUnit entered
			and ((Company.Revalue = true 
			and RevalueSurplusAccountingUnit entered
	        and RevalueLossAccountingUnit entered)
	        or (Company.Revalue = false)))

		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

		IsAssetReviewEnabled
			restricted
			when (Company.AssetReview 
			and Company.ApprovalLevel.AssetDimensionGroup)
	
    Relations

        AssetAdjustmentsRel
            classic name is AMASSETADJ
            one-to-many relation to AssetAdjustment
            Field Mapping uses Set2
                related.Company = Company

        AssetTransfersRel
            classic name is AMASSETTRF
            one-to-many relation to AssetTransfer
            Field Mapping uses Set2
                related.Company = Company
                
		AssetsWithoutAccountingUnitRel
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup 	= Company.AccountingEntity.FinanceEnterpriseGroup
				related.Company					= Company
			Instance Selection
				where (related.NotDisposed
				and    related.AssetAccountingUnitGroup = AssetAccountingUnitGroup
				and    related.AssetAccountingUnit not entered)                

		AssetCompanyRel 
			one-to-one relation to AssetCompany
			Field Mapping uses symbolic key
				related.Company		= Company	
 
		ImpairmentDimensionsExtRel		
			one-to-one relation to AssetImpairmentAccountingUnitGroup
			delete cascades	
			Field Mapping uses symbolic key
				related.Company						= Company
				related.AssetAccountingUnitGroup	= AssetAccountingUnitGroup  

		AssetsRequireImpairmentOrRevaluationAccount
			one-to-many relation to Asset
			Field Mapping uses Set2
				related.FinanceEnterpriseGroup 	= Company.AccountingEntity.FinanceEnterpriseGroup
				related.Company					= Company
			Instance Selection	
				where (!related.HasRequiredRevalueAndImpairmentAccounts) 

		GeneralLedgerCompanyRel
			one-to-one relation to GeneralLedgerCompany
            Field Mapping uses symbolic key
            	related.Company = Company

		AssetReviewDetailRel
			one-to-many relation to AssetReviewDetail
            Field Mapping uses symbolic key
            	related.Company					= Company

    Sets

        Set2
            indexed
            Sort Order
                AssetAccountingUnitGroup
                Company

	Field Rules
		Description
			required
					
		AssetAccountingUnit
			if (action type.Update)
				if (AssetsWithoutAccountingUnitRel exists)
					required
						"AssetDimensionsAreRequiredByExistingAssetsUsingThisGroup"

		AccumulatedDepreciationAccountingUnit
			if (action type.Update)
				if (AssetsWithoutAccountingUnitRel exists)
					required
						"AccumulatedDepreciationDimensionsAreRequiredByExistingAssetsUsingThisGroup"
						
						
		DepreciationExpenseAccountingUnit
			if (action type.Update)
				if (AssetsWithoutAccountingUnitRel exists)
					required
						"DepreciationExpenseDimensionsAreRequiredByExistingAssetsUsingThisGroup"
						
		ClearingAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"ClearingDimensionsAreRequiredByExistingAssetsUsingThisGroup"

		ProceedsAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"DisposalProceedsDimensionsAreRequiredByExistingAssetsUsingThisGroup"

		DisposalExpenseAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"DisposalExpenseDimensionsAreRequiredByExistingAssetsUsingThisGroup"
					
		GainAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"GainDimensionsAreRequiredByExistingAssetsUsingThisGroup"
						
		LossAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"LossDimensionsAreRequiredByExistingAssetsUsingThisGroup"
						
		DeferredGainAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"DeferredGainDimensionsAreRequiredByExistingAssetsUsingThisGroup"
						
		DeferredLossAccountingUnit
			if (AssetsWithoutAccountingUnitRel exists)
				required
					"DeferredLossDimensionsAreRequiredByExistingAssetsUsingThisGroup"
						
        RevalueSurplusAccountingUnit      
        	if (AssetCompanyRel.Revalue = true)
        		required
        			"AssetCompanyIsSetToRevalue;RevalueGainAndLossDimensionsAreRequired"

        RevalueLossAccountingUnit	
        	if (AssetCompanyRel.Revalue = true)
        		required
        			"AssetCompanyIsSetToRevalue;RevalueGainAndLossDimensionsAreRequired"

	Actions
		Create is a Create Action

		Update is an Update Action
			Entrance Rules
				if(AssetReviewDetailRel exists
				and ReviewApprovalCode changed)
					confirmation required
						"ChangingThisFieldWillNeedToDoRebuildAssetReviewDetailRecords"
			Exit Rules
				if ((RevalueSurplusAccountingUnit not entered
				and old RevalueSurplusAccountingUnit entered)      
       			or (RevalueLossAccountingUnit not entered 
       			and old RevalueLossAccountingUnit entered)           
				or (RevalueAmortization not entered
				and old RevalueAmortization entered)			  		
				or (RevalueReserve not entered	
				and old RevalueReserve entered))				
					constraint (!AssetsRequireImpairmentOrRevaluationAccount exists)
						"ImpairmentOrRevaluationDimensionCannotBeRemoved;AssetsRelatedToThisGroupAreAssignedAnAssetTypeWhichRequiresThem"
	

		Delete is a Delete Action

					
