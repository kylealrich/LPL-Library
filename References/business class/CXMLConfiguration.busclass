CXMLConfiguration is a BusinessClass
	owned by cxml
	prefix is CXMLS

	Ontology
		symbolic key is CXMLConfiguration

	Patterns
		implements CRUD

	Persistent Fields
		InvoiceAutoReject				is Boolean
		InvoiceAttachment				is Boolean
			default label is "StoreInvoiceAttachment"
		IgnoreFirstAttachment			is Boolean
		PaymentProposal					is Boolean
			default label is "SendPaymentProposal"
		PaymentRemittance				is Boolean
			default label is "SendPaymentRemittance"
		NotificationLevel				is Numeric 1
			States
				Error					value is 1
				Success					value is 2
				Both					value is 3
		NotificationType				is Numeric 1
			States
				None					value is 1
				Email					value is 2
				User					value is 3
				Both					value is 4
		ToNotificationEmail				is Alpha size 50
			default label is "ToEmail"
		FromNotificationEmail			is Alpha size 50
			default label is "FromEmail"
		Default							is Boolean
		FormatOrderID					is Boolean
			default label is "FormatOrder\ID"

	Conditions
		EmailNotification
			when (NotificationType.Email or NotificationType.Both)

	Relations
		DefaultCXMLConfigurationRel
			one-to-many relation to CXMLConfiguration
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.CXMLConfiguration != CXMLConfiguration)

        OtherCXMLConfigurationRel
            one-to-many relation to CXMLConfiguration
        	Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
        	Instance Selection
				where (related.Default
				and    related.CXMLConfiguration != CXMLConfiguration)

	Field Rules
		NotificationType
			initial value is 1
		NotificationLevel
			initial value is 1
		InvoiceAutoReject
			initial value is false
		IgnoreFirstAttachment
			initial value is false
		InvoiceAttachment
			initial value is true
		PaymentProposal
			initial value is false
		PaymentRemittance
			initial value is false
		Default
			if (DefaultCXMLConfigurationRel exists)
				if (Default)
					invoke ResetDefault DefaultCXMLConfigurationRel
			else
				if (OtherCXMLConfigurationRel not exists)
					force default to true
				else
					constraint(Default)
						"OneDefaultCXMLConfigurationMustExist"
		ToNotificationEmail
			if (EmailNotification)
				required
		FromNotificationEmail
			if (EmailNotification)
				required
		FormatOrderID
			initial value is false

 	Delete Rules
  	 	if (OtherCXMLConfigurationRel exists)
  	 		constraint (!Default)
	  	 		"CannotDeleteDefaultCXMLConfiguration"

	Actions
		Create is a Create Action
		Update is an Update Action
		Delete is a Delete Action

		ResetDefault is an Update Action
  	 		restricted
			bypass field rules
  	 		Action Rules
				Default = false
