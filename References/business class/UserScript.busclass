UserScript is a BusinessClass
    owned by la
    prefix is SRL
    stored in environment
    
    Ontology
    	symbolic key is UserScript
    
    Patterns
    	disable AsOfDateProcessing
		disable EffectiveDated
	
	Persistent Fields
		DisplayName is Alpha up to 100
		Description is Alpha up to 400
		
		ScriptState is Numeric size 1
			States
				New				value is 0
				Active			value is 6
				Inactive 		value is 9
			
		ScriptEngine
		ScriptDocument is BinaryDocument
		
	Derived Fields
		MimeType is a NativeField
			type is MimeType
			default label is "MIME_Type"	
			
		DisplayNameOrKey is a DerivedField
			type is Alpha 30
			restricted
			
			if (DisplayName entered)
				return DisplayName
				
			return UserScript

	Field Rules
		DisplayName
			default to UserScript
		ScriptEngine
			required
						
	Attach Rules
		constraint (ScriptState.Active)
			"ScriptRuleIsNotActive"
			
	Actions
		Create is a Create Action
			Action Rules
				ScriptState = ScriptState.New

		Update is an Action
			restricted
			
		Delete is an Action
			valid when (not ScriptState.Active)
			
	StateCycles
		Pending is a StateCycle
			state field is ScriptState	
		
			New is a State
				Update is an Action
				MakeActive is an Instance Action
					completion message is "<DisplayName>IsNowActive"
					Action Rules
						ScriptState = ScriptState.Active
				
			Active is a State
				MakeInactive is an Instance Action
					completion message is "<DisplayName>IsNowInactive"
					Action Rules
						ScriptState = ScriptState.Inactive
						
			Inactive is a State
				Update is an Action 
				MakeActive is an Instance Action
					completion message is "<DisplayName>IsNowActive"
					Action Rules
						ScriptState = ScriptState.Active
											
