Bin is a BusinessClass
    owned by ic
    prefix is ICB
    classic name is ICBIN

    Ontology
        symbolic key is Bin
            classic set name is ICBSET1
            classic name for InventoryLocation is LOCATION

    Patterns
        implements StaticJava
        disable AuditIndex
        
    Context Fields
    	FromCompanyLocationBin
		AuditDateRange 			is a DateRange
		Item
		ContextWarehouseStorageLocation is a WarehouseStorageLocation

    Persistent Fields

        BinType
        StorageCode
        GlobalLocationNumber
            classic name is GLN-NBR
        GeographicalType      is a GISType
            classic name is GEO-TYPE
        GeographicalLatitude  is a GISCoordinate
            classic name is GEO-LATITUDE
        GeographicalLongitude is a GISCoordinate
            classic name is GEO-LONGITUDE
        GeographicalAltitude  is a GISCoordinate
            classic name is GEO-ALTITUDE
        GeographicalDate      is TimeStamp
            classic name is GEO-DATE
		Active
		UseAsWarehouseStorageLocation	is Boolean
		WarehouseStorageLocation
			delete ignored

	Local Fields
		TempCounter1			is Numeric 2
		LocalBinTypeExists		is Boolean
		LocalWarehouseStorageLocationView	is a WarehouseStorageLocation view
		LocalCRUDWarehouseStorageLocation	is Numeric size 1
			States
				CreateStorageLocation 		value is 1
				UpdateStorageLocation		value is 2
				DeleteStorageLocation		value is 3
	
    Derived Fields

		DerivedDefaultLabelTemplate is a DerivedField
			type is like LabelTemplate
			restricted
			return first DefaultLabelTemplateRel.LabelTemplate

		BinAsAStorageLocationMessage	is a MessageField
			"BinIsSetAsA_Warehouse_Storage_Location"

		DerivedTotalStockOnhandDetailQuantityInStockUOM is a DerivedField
			type is like Quantity
			return sum StockOnHandDetailByBinItemRel.DerivedTransactionLineQuantityInStockUOM

		DerivedStockUOM is a DerivedField
			type is like UnitOfMeasure
			if (ItemLocationRel.HasItemLocationStockUOM)
				return ItemLocationRel.ItemLocationStockUOM
			else
				return Item.StockUOM

		BinUsedByMultipleBinsMessage is a MessageField
			"Bin<Bin>AlreadyExistsAsA_Warehouse_Storage_LocationUsedByMultipleBins"

    Conditions

        HasStorageCode
            classic name is BINSTORAGE
            restricted
            when (StorageCode entered)

        HasGlobalLocationNumber
            classic name is HAS-GLN-NBR
            restricted
            when (GlobalLocationNumber entered)
        
        RecordExists
        	restricted
        	when (Bin exists)
        	
        HasStockOnHand
        	restricted
        	when (StockOnHandDetailByBinItemRel exists)
            
        HasPhysicalInventoryBins
        	restricted
        	when (PhysicalInventoryBinRel exists)
        

        PrintLabelEnabled
			restricted
			when (MobileSupplyChainConfigurationRel.MobileSupplyChainEnabled
			and   Active)


		UseAsStorageLocation
			restricted
			when (RecordExists
			and UseAsWarehouseStorageLocation)

		HasItemStorageLocation
			restricted
			when (ItemStorageLocationRel exists)

        ValidForItemStorageLocation
            restricted
            when (ValidItemStorageLocationForItemRel exists)

        IsBulkStorageLocationType
            restricted
            when (ValidForItemStorageLocation
			and   WarehouseStorageLocation.StorageLocationType.BulkStorage
			and   Active)

        IsForwardPickingStorageLocationType
            restricted
            when (ValidForItemStorageLocation
			and   WarehouseStorageLocation.StorageLocationType.ForwardPickingStorage
			and   Active)

		IsBasicItemStorageLocationType
			restricted
			when (ValidForItemStorageLocation
			and   WarehouseStorageLocation.StorageLocationType.BasicItemStorage
			and   Active)

    Relations
    
        StockOnHandDetailsRel
            classic name is STOCKONHAND
            one-to-many relation to StockOnHandDetail
            delete restricted
            Field Mapping uses Set2
                related.Company               = Company
                related.InventoryLocation     = InventoryLocation
                related.StockOnHandDetail.Bin = Bin

        GlobalLocationNumberDetailRel
            classic name is GLNLOCDTL
            one-to-one relation to GlobalLocationNumberDetail
            valid when (HasGlobalLocationNumber)
            Field Mapping uses Set2
                related.ItemGroup                                           = GlobalLocationNumber.ItemGroup
                related.GlobalLocationNumberDetail.GlobalLocationNumberType = 11
                related.CompanyLocationBin.Company                          = Company
                related.CompanyLocationBin.InventoryLocation                = InventoryLocation
                related.CompanyLocationBin.Bin                              = Bin
                related.GlobalLocationNumber                                = GlobalLocationNumber
                

		StockOnHandDetailByBinRel											
			one-to-many relation to StockOnHandDetail
			Field Mapping uses Set2
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.StockOnHandDetail.Bin	= Bin
				
		StockOnHandDetailByBinItemRel
			one-to-many relation to StockOnHandDetail
			Field Mapping uses Set2
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.StockOnHandDetail.Bin	= Bin
				related.Item					= Item

		ItemLocationRel
			one-to-one relation to ItemLocation
			Field Mapping uses symbolic key
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
				related.Item					= Item
				
		PhysicalInventoryBinRel
			one-to-many relation to PhysicalInventoryBin
            delete cascades
			Field Mapping uses symbolic key
				related.Company					= Company
				related.InventoryLocation		= InventoryLocation
                related.Bin                     = Bin
			Instance Selection
				where (related.Active)


        MobileSupplyChainConfigurationRel
            one-to-one relation to MobileSupplyChainConfiguration
            Field Mapping uses symbolic key
                related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup

		DefaultLabelTemplateRel
			one-to-many relation to LabelTemplate
			Field Mapping uses symbolic key
				related.MobileSupplyChainConfiguration = Company.FinanceEnterpriseGroup
			Instance Selection	
				where (related.Default
				and    related.LabelType.StockBin)

		MobileSupplyChainLocationRel
			one-to-one relation to MobileSupplyChainLocation
			Field Mapping uses symbolic key
				related.Company							= Company
				related.MobileSupplyChainLocation		= InventoryLocation	



		BinIsWarehouseStorageLocationRel
			one-to-one relation to WarehouseStorageLocation
			Field Mapping uses symbolic key
				related.Company							= Company
				related.InventoryLocation				= InventoryLocation
				related.WarehouseStorageLocation		= Bin

		WarehouseStorageLocationRel
			one-to-one relation to WarehouseStorageLocation
			Field Mapping uses symbolic key
				related.Company							= Company
				related.InventoryLocation				= InventoryLocation
				related.WarehouseStorageLocation		= WarehouseStorageLocation

		WarehouseStorageLocationIsUsedByMultipleBinsRel
			one-to-many relation to Bin
			Field Mapping uses symbolic key
				related.Company							= Company
				related.InventoryLocation				= InventoryLocation
			Instance Selection
				where (related.WarehouseStorageLocation = Bin
				and	   related.Bin                     != Bin)

		ItemStorageLocationRel
			one-to-one relation to ItemStorageLocation
			Field Mapping uses symbolic key
				related.Company							= Company
				related.InventoryLocation				= InventoryLocation
				related.Item							= Item
				related.WarehouseStorageLocation        = WarehouseStorageLocation

        WarehouseItemStorageLocationRel
            one-to-many relation to ItemStorageLocation
            Field Mapping uses ByStorageLocation
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.WarehouseStorageLocation        = WarehouseStorageLocation

        ValidItemStorageLocationForItemRel
            one-to-one relation to ItemStorageLocation
            Field Mapping uses symbolic key
                related.Company                         = Company
                related.InventoryLocation               = InventoryLocation
                related.Item                            = Item
                related.WarehouseStorageLocation        = WarehouseStorageLocation

    Sets

        Set3
            indexed
            Sort Order
                Company
                BinType
                Bin
                InventoryLocation

        Set4
            indexed
            Sort Order
                Company
                InventoryLocation
                StorageCode
                Bin

	Attach Rules
		constraint (Active)
			"Bin<Bin>MustBeActive"

    Field Rules

		Bin
			constraint (Bin != "*      ")
				"AnAsteriskIsASpecialReservedValue"                        


		BinType
			if (BinType changed)
				constraint (StockOnHandDetailByBinRel not exists)
					"CannotChangeBinType;StockOnHandExistsForBinType"


			if (BinType entered)
				initialize LocalBinTypeExists
				initialize TempCounter1
				while (TempCounter1 < 9)
					TempCounter1 += 1
					if (BinType = Company.BinTypeArray.BinTypeGroup[TempCounter1].BinType)
						LocalBinTypeExists = true

				constraint (LocalBinTypeExists)
					"BinTypeDoesNotExist"


		UseAsWarehouseStorageLocation
			if (not InventoryLocation.UseAsWarehouseLocation)
				cannot be entered

			if (action type.Create
			and UseAsWarehouseStorageLocation)
				if (BinIsWarehouseStorageLocationRel not exists)
					LocalCRUDWarehouseStorageLocation = LocalCRUDWarehouseStorageLocation.CreateStorageLocation
				else
					constraint (WarehouseStorageLocationIsUsedByMultipleBinsRel not exists)
						"<BinUsedByMultipleBinsMessage>"
					WarehouseStorageLocation 		  = Bin
					LocalCRUDWarehouseStorageLocation = LocalCRUDWarehouseStorageLocation.UpdateStorageLocation
			else
			if (action type.Update
			and UseAsWarehouseStorageLocation changed)
				if (UseAsWarehouseStorageLocation)
					constraint (WarehouseStorageLocationIsUsedByMultipleBinsRel not exists)
						"<BinUsedByMultipleBinsMessage>"

					if (BinIsWarehouseStorageLocationRel exists)
						confirmation required
							"Bin<Bin>AlreadyExistsAsA_Warehouse_Storage_Location\._Proceed\?"
						WarehouseStorageLocation		  = Bin
						LocalCRUDWarehouseStorageLocation = LocalCRUDWarehouseStorageLocation.UpdateStorageLocation
					else
						LocalCRUDWarehouseStorageLocation = LocalCRUDWarehouseStorageLocation.CreateStorageLocation
				else
					LocalCRUDWarehouseStorageLocation = LocalCRUDWarehouseStorageLocation.DeleteStorageLocation

		WarehouseStorageLocation
			if (WarehouseStorageLocation entered)
				constraint (WarehouseStorageLocationRel.Active)
					"Warehouse_Storage_Location<WarehouseStorageLocation>MustBeActive"

			if (UseAsWarehouseStorageLocation not changed
			and UseAsWarehouseStorageLocation
			and WarehouseStorageLocation changed)
				constraint (WarehouseStorageLocation = Bin)
					"CannotChange._BinIsSetupAsA_Warehouse_Storage_Location."
			else
			if (not UseAsWarehouseStorageLocation
			and (WarehouseStorageLocation entered
			and WarehouseStorageLocation != Bin))
				constraint (not WarehouseStorageLocationRel.IsBin)
					"Warehouse_Storage_Location<WarehouseStorageLocation>IsNotValidForThis_Bin"

	Rule Blocks

		CRUDWarehouseStorageLocation
			if (LocalCRUDWarehouseStorageLocation.CreateStorageLocation)
				invoke Create WarehouseStorageLocation
					assign result to LocalWarehouseStorageLocationView
					invoked.Company						= Company
					invoked.InventoryLocation			= InventoryLocation
					invoked.WarehouseStorageLocation	= Bin
					invoked.Name						= Bin
					invoked.Active						= true
					invoked.IsBin						= true
				invoke FastUpdate Bin
					invoked.WarehouseStorageLocation	= LocalWarehouseStorageLocationView.WarehouseStorageLocation
			else
			if (LocalCRUDWarehouseStorageLocation.UpdateStorageLocation)
				invoke FastUpdate BinIsWarehouseStorageLocationRel
					invoked.IsBin = true
			else
			if (LocalCRUDWarehouseStorageLocation.DeleteStorageLocation)
				invoke FastUpdate WarehouseStorageLocationRel
					invoked.IsBin = false
				invoke DeleteWarehouseStorageLocation WarehouseStorageLocationRel
				initialize WarehouseStorageLocation

	Actions
		Create is a Create Action
			Exit Rules
				include CRUDWarehouseStorageLocation

		Update is an Update Action
			Exit Rules
				include CRUDWarehouseStorageLocation

		Delete is a Delete Action
			Entrance Rules
				if (InventoryLocation.UseAsWarehouseLocation
				and UseAsWarehouseStorageLocation
				and WarehouseStorageLocationRel.IsBin)
					constraint (StockOnHandDetailsRel not exists)
						"CannotDelete._StockOnHandExists"
					constraint (WarehouseItemStorageLocationRel not exists)
						"CannotDelete._Item_Storage_LocationExistsForWarehouseStorageLocation<WarehouseStorageLocationRel.WarehouseStorageLocation>"
					LocalCRUDWarehouseStorageLocation = LocalCRUDWarehouseStorageLocation.DeleteStorageLocation
					include CRUDWarehouseStorageLocation

		FastUpdate is an Update Action
			restricted
			bypass field rules

		UpdateGlobalLocationNumber is an Instance Action
			restricted
			Parameters
				PrmGlobalLocationNumber	is a GlobalLocationNumber
			Action Rules
				GlobalLocationNumber	=	PrmGlobalLocationNumber	

        PrintLabel is an Instance Action
			valid when (PrintLabelEnabled)			
			Parameters
				PrmMobileSupplyChainPrinter							is a MobileSupplyChainPrinter
					default label is "MobileSupplyChainPrinter"
				PrmNumberOfCopies						is Unsigned Decimal 2.0
					default label is "NumberOfCopies"
				PrmMobileSupplyChainConfiguration		is a MobileSupplyChainConfiguration
					default label is "MobileSupplyChainConfiguration"
				PrmLabelTemplate  					is a LabelTemplate
					default label is "LabelTemplate"
				PrmTransactionType						is a MobileSupplyChainPrintingTransactionType
					default label is "LabelTransactionType"
			Parameter Rules
				PrmMobileSupplyChainPrinter	
					initial value is MobileSupplyChainLocationRel.DerivedDefaultMobileSupplyChainLabelPrinter
					default to MobileSupplyChainLocationRel.DerivedDefaultMobileSupplyChainLabelPrinter		
					required
					constraint (PrmMobileSupplyChainPrinter.PrinterType.Label)
						"PrinterShouldOnlyBeLabelPrinter"
				PrmNumberOfCopies
					initial value is 1
					default to 1
					required
				PrmLabelTemplate
					initial value is DerivedDefaultLabelTemplate
					default to DerivedDefaultLabelTemplate
					required
					constraint (PrmLabelTemplate.LabelType.StockBin)
						"LabelTypeShouldBeStockBin"
				PrmTransactionType
					initial value is PrmTransactionType.LabelManagement
					default to PrmTransactionType.LabelManagement
			Action Rules					
				invoke Create MobileSupplyChainPrinterJob
		        	invoked.FinanceEnterpriseGroup          = Company.FinanceEnterpriseGroup
		            invoked.Company 						= Company
		            invoked.MobileSupplyChainPrinter 		= PrmMobileSupplyChainPrinter
                    invoked.NumberOfCopies 					= PrmNumberOfCopies
					invoked.TransactionType					= PrmTransactionType
					invoked.PrintDataFile 					= PrmLabelTemplate.TransientBinBinaryDocument document for this instance					

		AssignStorageLocationToBin is an Instance Action
			completion message is "BinHasBeenAssignedTo<WarehouseStorageLocation>"
			default label is "Assign_Bin"
			valid when (not WarehouseStorageLocation.IsBin)
			Action Rules
				constraint (Bin.Active)
					"Bin<Bin>MustBeActive"

				constraint (ContextWarehouseStorageLocation.Active)
					"Warehouse_Storage_Location_<ContextWarehouseStorageLocation>IsNotActive."

				constraint (not ContextWarehouseStorageLocation.IsBin)
					"Warehouse_Storage_LocationIsA_BinAndIsNotValidInThisContext."

				WarehouseStorageLocation = ContextWarehouseStorageLocation

		UnassignStorageLocationFromBin is an Instance Action
			completion message is "BinHasBeenUnassignedFromThis_Storage_Location"
			default label is "Unassign_Bin"
			valid when (not WarehouseStorageLocation.IsBin)
			confirmation required
				"Unassign_BinFromThis_Warehouse_Storage_Location?"
			Action Rules

				initialize WarehouseStorageLocation
