BirstModelBC is a BusinessClass
	owned by la 
	prefix is BMB

	Ontology
		symbolic key is BirstModelBC

	Patterns
		disable AsOfDateProcessing

	Persistent Fields
		BusinessClass	is a snapshot of RepSetBC.BusinessClass
		DisplayName		is Alpha size 60
		MeasurePrefix	is Alpha size 10
		BCType			is Numeric size 1
            States
                Dimension value is 0
                Fact value is 1
		IncludeInModel	is Boolean


	Field Rules
		DisplayName
			default to DefaultDisplayName

	Derived Fields
		DefaultDisplayName is a NativeField
			type is Alpha size 60
			restricted
			
		ModelType is a DerivedField
			type is Numeric size 1
			return BirstModel.ModelType
			
		UseColumnarVariationPattern is a DerivedField
			type is Boolean
			return RepSetBC.UseColumnarVariationPattern
		
		BirstModelResultDescription is a MessageField
			"Update_\BirstModelBusinessClass:<BusinessClass>"
		
		BirstSpace is a DerivedField
			type is Alpha 50
			return BirstModel.BirstSpace
			
	Local Fields
		LocalRelatedValue			is Alpha size 300	
		LocalUpdateMessage			is Text
		LocalActor					is an Actor
		LocalBirstModelResultView	is a BirstModelResult view
		LocalBirstModelResult		is UniqueID
		LocalBusinessClass			is like BusinessClass
		LocalStatus					is Numeric size 1
			States
				Success	value is 1
				Error	value is 2		
		
	Sets
		ByBusinessClass
			Sort Order
				BirstModel
				BusinessClass
				DisplayName
				
		ByModelType
			duplicates
			Sort Order
				BirstModel
				BCType
				DisplayName
				
		ByIncludeInModel
			Sort Order
				BirstModel
				IncludeInModel
				BCType
				BusinessClass
				DisplayName

	Actions
		Create is an Action
		Update is an Action			
		Delete is an Action
		Purge is a Purge Action

		UpdateModel is an Instance Action
			run in background
			Parameters
				UpdateTable		is Boolean
				UpdateJoins		is Boolean
				UpdatePackage	is Boolean

			Entrance Rules
				LocalActor	= actor
				invoke Create BirstModelResult
					assign result to LocalBirstModelResultView
					invoked.User			= LocalActor
					invoked.Description		= BirstModelResultDescription
					
				LocalBirstModelResult = LocalBirstModelResultView.BirstModelResult
			
			Action Rules


			Exit Rules
				invoke Create BirstModelResultDetail
					invoked.BirstModelResult	= LocalBirstModelResultView.BirstModelResult
					invoked.Status				= LocalStatus
					invoked.Message				= LocalUpdateMessage
					invoked.BirstModel			= BirstModel
					invoked.BusinessClass		= BusinessClass

		CreateBirstModelResult is an Instance Action
			restricted
			Action Rules
				invoke Create BirstModelResultDetail
					invoked.BirstModelResult	= LocalBirstModelResult
					invoked.Status				= LocalStatus
					invoked.Message				= LocalUpdateMessage
					invoked.BirstModel			= BirstModel
					invoked.BusinessClass		= LocalBusinessClass	

		AddFields is an Instance Action

			Parameters
				AddReplicationClassFields	is Boolean
				
			Local Fields				
				Count 						is Numeric size 3
				TotalCount					is Numeric size 6
				LocalCreateField			is Boolean

			Action Rules



				for each BirstModelBCHierarchyLevelDetailRel
					LocalCreateField = false

					LocalRelatedValue = each.BirstFieldName 

					if (not BirstModelBCFieldRel exists)
						LocalCreateField = true

					if (LocalCreateField)

						invoke Create BirstModelBCField
							invoked.BirstModel				= BirstModel
							invoked.ReplicationSet			= ReplicationSet
							invoked.RepSetBC				= RepSetBC
							invoked.RelatedValue			= each.BirstFieldName
							invoked.SQLName					= each.SQLCaseStatement
							invoked.DataType				= each.PostingFieldDataType
							invoked.Width					= each.PostingFieldWidth
							invoked.BirstFieldName			= each.BirstFieldName
							invoked.HierarchyLevelNumber	= each.LevelNumber
							invoked.HierarchyFieldType		= each.HierarchyFieldType
							invoked.IsHierarchyLevel		= true
							invoked.IsAttribute				= true
							invoked.FieldType				= 1
							invoked.IncludeInBirstModel		= true

			
	Relations		
		RepSetBCFieldRel
			one-to-many relation to RepSetBCField
			Field Mapping uses symbolic key
				related.ReplicationSet = ReplicationSet
				related.RepSetBC 	   = RepSetBC

		BirstModelBCFieldRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByRelatedValue
				related.BirstModel		= BirstModel
				related.ReplicationSet	= ReplicationSet
				related.RepSetBC		= RepSetBC
				related.RelatedValue 	= LocalRelatedValue

		BirstModelBCHierarchyLevelDetailRel
			one-to-many relation to BirstModelBCHierarchyLevelDetail
			Field Mapping uses symbolic key
				related.BirstModel		= BirstModel
				related.ReplicationSet	= ReplicationSet
				related.RepSetBC		= RepSetBC

		BirstModelForeignKeyFieldRel
			one-to-many relation to BirstModelBCField
			Field Mapping uses ByForeignKey
				related.BirstModel			= BirstModel
				related.ReplicationSet		= ReplicationSet
				related.RepSetBC			= RepSetBC
				related.IncludeInBirstModel	= true
				related.IsForeignKey		= true
