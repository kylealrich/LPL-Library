CustomerOrderReturnLineAddOnCharges is a BusinessClass
    owned by oe
    prefix is RMC
    sql name is COrderReturnLineAddOnCharges
    classic name is RETURNMISC

    Ontology
        symbolic key is CustomerOrderReturnLineAddOnCharges
            classic set name is RMCSET1
            sql name is COrderReturnLineAddOnCharges
            classic name is SEQ
            classic name for CustomerOrderReturn is AUTH-NO
            classic name for CustomerOrderReturnLine is LINE-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

      	Miscellaneous     		is an OrderEntryAddOnCharge
      		classic name is MISC
        ChargeType
      	EnteredUnitPrice  		is an InternationalCost
      		classic name is ENT-UNIT-PRC  
		PricePercent      		is a CostPercent
			classic name is PRICE-PCT
		EnteredPrice      		is an InternationalAmount
			classic name is ENT-PRICE
        UnitCost          		is an InternationalCost
        CostPercent
            classic name is COST-PCT
        Cost              		is an InternationalCost
      	AllocatedDiscount 		is an InternationalAmount  
      		classic name is ALLOC-DISC
        OrderDiscount	  		is Boolean
            classic name is ORD-DISC-FL



        AtnObjId          		is an ObjId
        AtnObjIdC         		is an ObjId
      	ChargeCurrencyAmount	is an InternationalAmount
      		classic name is CHARGE-CURR
        TrackType
        SalesAccount      		is a FinanceCodeBlock
            classic name for SalesAccount.AccountingUnit is SLS-ACCT-UNIT
            classic name for SalesAccount.GeneralLedgerChartAccount is SLS-ACCOUNT
            classic name for SalesAccount.Project is ACTIVITY
        OffsetAccount     		is a FinanceCodeBlock
            classic name for OffsetAccount.AccountingUnit is OFF-ACCT-UNIT
            classic name for OffsetAccount.GeneralLedgerChartAccount is OFF-ACCOUNT
        CostOfGoodsSoldAccount  is a FinanceCodeBlock
            classic name for CostOfGoodsSoldAccount.AccountingUnit is CGS-ACCT-UNIT
            classic name for CostOfGoodsSoldAccount.GeneralLedgerChartAccount is CGS-ACCOUNT
            classic name for CostOfGoodsSoldAccount.Project is ACTIVITY-C

	Derived Fields
		DerivedCustomerOrderReturnLineAddOnCharges is a DerivedField
			type is like CustomerOrderReturnLineAddOnCharges
			return CustomerOrderReturnLineAddOnCharges
	
	Local Fields
		LocalChargeCurrency is an InternationalAmount
		LocalChargeAmount	is like InternationalAmount			
		LocalGeneralLedgerSystemCode		    is a GeneralLedgerSystemCode

    Conditions

        IsCommitted
        	restricted
            when (AtnObjId entered)

        IsCommittedC
        	restricted
            when (AtnObjIdC entered)

        IsFlatAmount
        	restricted
            when (ChargeType.FlatAmount)

        IsLineCharge
        	restricted
            when (CustomerOrderReturnLine entered)

    Sets
        Set3
            indexed
            Instance Selection
                where (IsCommittedC)
            Sort Order
                AtnObjIdC
	
	Field Rules
		CustomerOrderReturnLine
			if (ChargeType.UnitPrice)
				required 				
				
		Miscellaneous
			required
			cannot be changed			
				"CannotChangeAOCCode;DeleteAndRe-add"
			if (Miscellaneous.ChargeType.UnitPrice)
				constraint (CustomerOrderReturnLine entered)
					"UnitPriceAOCIsNotAllowedOnTheReturnHeader"
			
       	ChargeType
       		default to Miscellaneous.ChargeType
       		
      	EnteredUnitPrice  
      		if (ChargeType.UnitPrice)
      			default to Miscellaneous.UnitPrice
      			required		
      		else
      			cannot be entered	

      	
		PricePercent
			if (ChargeType.Percentage)
				default to Miscellaneous.PricePercent 
				required 
			else
				cannot be entered  
	
		EnteredPrice     
      		if (ChargeType.FlatAmount)
      			default to Miscellaneous.Price	
      			required   
      		else
      			cannot be entered	 
    
        UnitCost    
        	if (ChargeType.UnitPrice)
        		default to Miscellaneous.UnitCost    
        	  
    
        CostPercent
        	if (ChargeType.Percentage)
        		default to Miscellaneous.CostPercent
        	else
        		cannot be entered   
    
        Cost  
        	default to Miscellaneous.Cost            

        OrderDiscount	
        	default to Miscellaneous.OrderDiscount  

		ChargeCurrencyAmount
			if (ChargeType.UnitPrice)
				LocalChargeCurrency = (CustomerOrderReturnLine.Quantity * CustomerOrderReturnLine.SellToStock * EnteredUnitPrice)
				default to LocalChargeCurrency
			else
				if (ChargeType.Percentage
				and CustomerOrderReturnLine entered)
					LocalChargeCurrency = (CustomerOrderReturnLine.Quantity * PricePercent * EnteredUnitPrice)
					default to LocalChargeCurrency
				
		SalesAccount
			default to Miscellaneous.SalesAccount
			LocalGeneralLedgerSystemCode = "OE"
			if (SalesAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
				constraint (LocalGeneralLedgerSystemCode within SalesAccount.GeneralLedgerChartAccount.Account.SystemRestriction)
					"InvalidSystemCodeForAccount<SalesAccount.GeneralLedgerChartAccount.Account>"

		
		OffsetAccount
			default to Miscellaneous.OffsetAccount

			LocalGeneralLedgerSystemCode = "OE"	
			if (OffsetAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
				constraint (LocalGeneralLedgerSystemCode within OffsetAccount.GeneralLedgerChartAccount.Account.SystemRestriction)					
					"InvalidSystemCodeForAccount<OffsetAccount.GeneralLedgerChartAccount.Account>"
		CostOfGoodsSoldAccount	
			default to Miscellaneous.CostOfGoodsSoldAccount
			LocalGeneralLedgerSystemCode = "OE"	
			if (CostOfGoodsSoldAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
				constraint (LocalGeneralLedgerSystemCode within CostOfGoodsSoldAccount.GeneralLedgerChartAccount.Account.SystemRestriction)					
					"InvalidSystemCodeForAccount<CostOfGoodsSoldAccount.GeneralLedgerChartAccount.Account>"
			
	Actions
		CreateFromBatch is a Create Action
			restricted			
		
	
		Create is a Create Action 
			valid when (CustomerOrderReturn.Status.Entered)
			Entrance Rules
				if (CustomerOrderReturn.TaxEntityRel.ThirdParty.TaxEngine)
					if (CustomerOrderReturn.TemporaryHold entered)
						constraint (CustomerOrderReturn.TemporaryHold.RejectedByEngine)
							"CreateNotAllowed;SentToApproval"

			Action Rules
				constraint (CustomerOrderReturn.Status.Entered)
					"CannotCreateCharges;ReturnIs<CustomerOrderReturn.Status>"					
				
				if (CustomerOrderReturnLine entered)
					constraint (CustomerOrderReturnLine.CreateCreditMemo)
						"AddOnChargesAreNotAllowedWhenLineDoesNotCreateCreditMemo"
						
					increment CustomerOrderReturnLine.LastAddOnChargeSequence
				else
					increment CustomerOrderReturn.LastAddOnChargeSequence					
				
		Update is an Update Action
			valid when (CustomerOrderReturn.Status.Entered)
			Entrance Rules
				if (CustomerOrderReturn.TaxEntityRel.ThirdParty.TaxEngine)
					constraint (!CustomerOrderReturn.TemporaryHold.SentForApproval)
						"UpdateNotAllowed;SentToApproval"

			Action Rules
				constraint (CustomerOrderReturn.Status.Entered)
					"CannotUpdate;ReturnIs<CustomerOrderReturn.Status>"					
		
		Delete is a Delete Action
			valid when (CustomerOrderReturn.Status.Entered)
			Entrance Rules
				if (CustomerOrderReturn.TaxEntityRel.ThirdParty.TaxEngine)
					if (CustomerOrderReturn.TemporaryHold entered)
						constraint (CustomerOrderReturn.TemporaryHold.RejectedByEngine)
							"DeleteNotAllowed;SentToApproval"

				constraint (CustomerOrderReturn.Status.Entered)
					"CannotDelete;ReturnIs<CustomerOrderReturn.Status>"					
				
				if (CustomerOrderReturnLine entered)
					decrement CustomerOrderReturnLine.LastAddOnChargeSequence
				else
					decrement CustomerOrderReturn.LastAddOnChargeSequence			
		
		Release is an Instance Action
			restricted
			Parameters
				PrmBillingInvoice is a BillingInvoice
			Action Rules
				if (CustomerOrderReturnLine not entered)
					constraint (Miscellaneous.Active)
						"AddOnCharge<Miscellaneous.Misc>ForReturnIsInactive"
				else
					constraint (Miscellaneous.Active)
						"AddOnCharge<Miscellaneous.Misc>OnLine<CustomerOrderReturnLine>IsInactive"
				
				LocalGeneralLedgerSystemCode = "OE"
				if (SalesAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
					constraint (LocalGeneralLedgerSystemCode within SalesAccount.GeneralLedgerChartAccount.Account.SystemRestriction)
						"InvalidSystemCodeForAccount<SalesAccount.GeneralLedgerChartAccount.Account>"
				if (OffsetAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
					constraint (LocalGeneralLedgerSystemCode within OffsetAccount.GeneralLedgerChartAccount.Account.SystemRestriction)
						"InvalidSystemCodeForAccount<OffsetAccount.GeneralLedgerChartAccount.Account>"
				if (CostOfGoodsSoldAccount.GeneralLedgerChartAccount.Account.SystemRestriction entered)
					constraint (LocalGeneralLedgerSystemCode within CostOfGoodsSoldAccount.GeneralLedgerChartAccount.Account.SystemRestriction)
						"InvalidSystemCodeForAccount<CostOfGoodsSoldAccount.GeneralLedgerChartAccount.Account>"
				invoke Create BillingInvoiceAddOnCharge
					invoked.Company 							= Company
					invoked.BillingInvoice.InvoicePrefix 		= PrmBillingInvoice.InvoicePrefix
					invoked.BillingInvoice.InvoiceNumber 		= PrmBillingInvoice.InvoiceNumber
					invoked.BillingInvoice 						= PrmBillingInvoice
					invoked.BillingInvoiceLine					= CustomerOrderReturnLine
					invoked.Miscellaneous						= Miscellaneous
					invoked.ChargeType 							= ChargeType
			      	invoked.AllocatedDiscount 					= AllocatedDiscount
			        invoked.OrderDiscount 						= OrderDiscount
			        invoked.TrackType 							= TrackType
			        invoked.SalesAccount 						= SalesAccount
			        invoked.OffsetAccount 						= OffsetAccount
			        invoked.COGSAccount 						= CostOfGoodsSoldAccount			
			        if (ChargeType.FlatAmount)
			        	invoked.EnteredPrice.TransactionAmount = EnteredPrice		
			        	invoked.Cost	= Cost	
			        if (ChargeType.UnitPrice)
			        	invoked.EnteredUnitPrice.TransactionAmount = EnteredUnitPrice			
			        	invoked.UnitCost		= UnitCost
			        if (ChargeType.Percentage)
			        	invoked.PricePercent 	= PricePercent
			        	invoked.CostPercent 	= CostPercent
			        invoked.LocalCreatedFromReturn				= true
		
		Purge is a Purge Action
			restricted

		CalculateDiscounts		is an Instance Action			
			restricted
			Parameters
				PrmLineTotal	is an InternationalAmount
			Action Rules
				initialize LocalChargeAmount
				if (OrderDiscount)
					if (ChargeType.FlatAmount)
						LocalChargeAmount = EnteredPrice
					else
					if (ChargeType.UnitPrice)
						LocalChargeAmount = (CustomerOrderReturnLine.Quantity * CustomerOrderReturnLine.SellToStock * EnteredUnitPrice)
					else
						LocalChargeAmount = PrmLineTotal * PricePercent
				if (LocalChargeAmount entered)
					invoke CalculateDiscountableAmount CustomerOrderReturn
						invoked.PrmDiscountableAmount = LocalChargeAmount

