CustodialContact is a BusinessClass
	owned by cam
	default label is "Contact"
	
	prefix is CamCo
	
	Ontology
		symbolic key is CustodialContact
		
	
	Patterns
	
	Persistent Fields
		Contact						is a CamContacts
			restricted
			protected
		Name						is Alpha size 120
			holds pii
		Address 					is a PostalAddressV2
			holds pii
		Email		 				is an EmailAddressMulti
			holds pii
		PhoneNumber					is a TelephoneNumber
		ContactType					is AlphaUpper size 1
			States
				External			value is "E"
				Internal			value is "I"
		IsRequestingSetup			is Boolean
		IsResponsibleIndividual		is Boolean
	
	Relations

		IsRequestingSetupRel
			one-to-many relation to CustodialContact
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.UniqueID != UniqueID 
				and 	related.IsRequestingSetup = IsRequestingSetup)
	
		IsResponsibleIndividualRel
			one-to-many relation to CustodialContact
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
			Instance Selection
				where (related.UniqueID != UniqueID 
				and 	related.IsResponsibleIndividual = IsResponsibleIndividual)
				
		CustodialContactDupCheckRel
        	one-to-many relation to CustodialContact
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup = FinanceEnterpriseGroup
            Instance Selection
            	where (related.UniqueID != UniqueID
            	and 	related.CustodialAccountManagement = CustodialAccountManagement
            	and		related.Name = Name)		

	Field Rules
	
		Name
			constraint (CustodialContactDupCheckRel not exists)
				"<Name>_AlreadyAdded"
			required
			
		ContactType
			required
			
		IsRequestingSetup
			constraint (ContactType.Internal)
				"IndividualRequestingSetupMustBeAnInternalContact"
				
			constraint (IsRequestingSetupRel not exists)
				"IndividualRequestingSetupHasAlreadyBeenAssignedToADifferentContact"
				
		IsResponsibleIndividual
			constraint (ContactType.Internal)
				"ResponsibleIndividualMustBeAnInternalContact"
				
			constraint (IsResponsibleIndividualRel not exists)
				"ResponsibleIndividualHasAlreadyBeenAssignedToADifferentContact"
				
