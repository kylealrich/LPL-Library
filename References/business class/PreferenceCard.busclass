PreferenceCard is a BusinessClass
    owned by ic
    prefix is PRFC

    Ontology
        symbolic key is PreferenceCard

    Patterns
        implements StaticJava
        implements DynamicCreation

    Persistent Fields
        PreferenceCardName 		    is Alpha size 135
            default label is "PreferenceCardName"
        PreferenceCardAlias 	    is Alpha size 100
            default label is "PreferenceCardAlias"
        Service					    is Alpha size 45
        ServiceDetail			    is Alpha size 45
        Active                      is Boolean
        LastComponentNumber         is Numeric size 6

    Derived Fields

        ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforPreferenceCard"

        ContextMessageText is a MessageField
            restricted
            "PreferenceCard<PreferenceCard><PreferenceCardName>"


        ActiveMessage is a LabelField
            "Active"

        InactiveMessage is a LabelField
            "Inactive"

        ActiveDisplay is a DerivedField
            type is MessageField
            default label is "Active"
            if (Active)
                return ActiveMessage
            else
                return InactiveMessage

		RepresentativeText is a StringField
			type is Text
			default label is "PreferenceCard"
			PreferenceCard " - " PreferenceCardName

    Relations
        PreferenceCardProviderRel is a PreferenceCardProvider set

        ComponentItemsRel is a ComponentItem set

        PreferenceCardProcedureRel is a PreferenceCardProcedure set

        PreferenceCardErrorRel is a PreferenceCardError set

        PreferenceCardHeaderErrorsRel is a PreferenceCardError set
            Instance Selection
                where (related.ErrorRecordType.PreferenceCard)

    Conditions
        RecordExists
            restricted
            when (PreferenceCard exists)

        ErrorExists
            restricted
            when (RecordExists
            and   PreferenceCardErrorRel exists)

    Field Rules
        Active
            initial value is true

    Rule Blocks
        CreateOrUpdateEdits
            invoke Purge PreferenceCardHeaderErrorsRel

            if (not ItemGroup.PreferenceCardsInUse)
                invoke Create PreferenceCardError
                    fill in fields from this instance
                    invoked.ErrorRecordType     = 9 
                    invoked.ErrorMessageNumber  = 99
                    invoked.ErrorPreferenceCard = PreferenceCard

    Actions
        Create is a Create Action
            Exit Rules
                include CreateOrUpdateEdits

        Update is an Update Action
            Entrance Rules
                include CreateOrUpdateEdits

        ReviewErrors is an Instance Action
            restricted
            Action Rules
                include CreateOrUpdateEdits

        FastUpdate is an Update Action
            restricted
            bypass field rules

        Delete is a Delete Action
            Entrance Rules
                invoke Delete ComponentItemsRel
                invoke Delete PreferenceCardProcedureRel
                invoke Delete PreferenceCardProviderRel
                invoke Delete PreferenceCardErrorRel
