MobileSupplyChainResourceAccessPrinter is a BusinessClass
	owned by mscm	
	prefix is MSRAP
	sql name is "MSCMResourceInvLocPrinter"
	
	Ontology
		symbolic key is MobileSupplyChainResourceAccessPrinter
			
	Patterns
	
	Persistent Fields
		DefaultReportPrinter is a MobileSupplyChainPrinter
		DefaultLabelPrinter  is a MobileSupplyChainPrinter
	
	Context Fields
		MobileSupplyChainPrinterContext   is a MobileSupplyChainPrinter

	Field Rules
		DefaultReportPrinter
			if (DefaultLabelPrinter not entered)
				required
					"EitherPrinterTypesMustBeEntered"
			constraint (DefaultReportPrinter.PrinterType.Report)
				"PrinterTypeShouldBeReport"
			constraint (HasPrinterAccess)
				"CompanyHasNoAssignedReportPrinter"

		DefaultLabelPrinter
			constraint (DefaultLabelPrinter.PrinterType.Label)
				"PrinterTypeShouldBeLabel"
			constraint (HasPrinterAccess)
				"CompanyHasNoAssignedLabelPrinter"		
		MobileSupplyChainLocation
			constraint (MobileSupplyChainLocation.IsAccessibleByMobileSupplyChainResource)
				"ResourceDoesNotHaveAccessToThisLocation"
	Conditions
		HasPrinterAccess
			when ((DefaultReportPrinter not entered or ReportPrinterAccessRel exists)
			and   (DefaultLabelPrinter  not entered or LabelPrinterAccessRel exists))
				
	Relations		
		ReportPrinterAccessRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company     = Company
			Instance Selection
				where (related.MobileSupplyChainPrinter = DefaultReportPrinter
				and    related.MobileSupplyChainPrinter.PrinterType.Report)
								
		LabelPrinterAccessRel
			one-to-many relation to MobileSupplyChainCompanyPrinter
			Field Mapping uses symbolic key
				related.Company     = Company
			Instance Selection
				where (related.MobileSupplyChainPrinter = DefaultLabelPrinter
				and    related.MobileSupplyChainPrinter.PrinterType.Label)
	
	Sets
		ByDefaultReportResourceAccessPrinter
			Sort Order
				DefaultReportPrinter
				MobileSupplyChainResource
				MobileSupplyChainResource.HROrganization
				Company
				MobileSupplyChainLocation

		ByDefaultLabelResourceAccessPrinter
			Sort Order
				DefaultLabelPrinter
				MobileSupplyChainResource
				MobileSupplyChainResource.HROrganization
				Company
				MobileSupplyChainLocation
				
	Actions
		Create is a Create Action
			Action Rules
				if (MobileSupplyChainPrinterContext exists)
					if (MobileSupplyChainPrinterContext.PrinterType.Report)
						DefaultReportPrinter = MobileSupplyChainPrinterContext
					else
						DefaultLabelPrinter  = MobileSupplyChainPrinterContext	
						
		Update is an Update Action
		
		Delete is a Delete Action

		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
			Instance Selection
				where (Company.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup)
			Set Is
				PrmFinanceEnterpriseGroup
			Action Rules
				Instance Rules
					invoke Delete
