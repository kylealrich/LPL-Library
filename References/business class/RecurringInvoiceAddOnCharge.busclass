RecurringInvoiceAddOnCharge is a BusinessClass
    owned by bl
    prefix is BMM
    classic name is BLRIMMISC

    Ontology
        symbolic key is RecurringInvoiceAddOnCharge
            classic set name is BMMSET1
            classic name is SEQ
            classic name for RecurringInvoiceParameter is RECUR-INVOICE
            classic name for RecurringInvoiceLine is LINE-NBR

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

		Miscellaneous is a OrderEntryAddOnCharge
            classic name is MISC
        ZeroAfterUse  is Boolean
        StartDate
        StopDate      is Date
        TaxExemptCode
            classic name is TAX-EXEMPT-CD
   		 		
    Conditions

        IsCurrencyEntered
        	restricted
            when (RecurringInvoiceParameter.Currency entered)

        IsLineCharge
        	restricted
            when (RecurringInvoiceLine entered)
		
			
    Relations
		CustomerRecurringInvoiceAddOnChargesRel
            one-to-many relation to CustomerRecurringInvoiceAddOnCharge
            Field Mapping uses Set2
                related.Company                   = Company
                related.RecurringInvoiceParameter = RecurringInvoiceParameter
                related.MasterLineNumber          = RecurringInvoiceLine
                related.MasterSequence            = RecurringInvoiceAddOnCharge

        	    		
    Field Rules

        Miscellaneous
            required
			
			if (Miscellaneous.ChargeType.UnitPrice)							
				constraint (RecurringInvoiceLine entered)
					"PerUnitAddOnChargeNotAllowedAtInvoiceLevel"			

			if (RecurringInvoiceParameter.HasBillingInvoices)
				cannot be changed
				        	
        ZeroAfterUse
			if (!Miscellaneous.ChargeType.FlatAmount)
				cannot be entered
					"ZeroAfterUseCannotBeSelectedForPercentOrPerUnitAddOnCharge"	

			if (RecurringInvoiceParameter.HasBillingInvoices)
				cannot be changed
				
		StartDate
			if (RecurringInvoiceLine not entered)							
				default to RecurringInvoiceParameter.StartDate
			else
				default to RecurringInvoiceLine.StartDate
			
			required

			if (RecurringInvoiceLine not entered)
				constraint (StartDate >= RecurringInvoiceParameter.StartDate)
					"StartDateCannotBeLessThanInvoiceStartDate"			
			else
				constraint (StartDate >= RecurringInvoiceLine.StartDate)
					"StartDateCannotBeLessThanInvoiceLineStartDate"				
		
		StopDate
			if (RecurringInvoiceLine not entered)
				default to RecurringInvoiceParameter.StopDate		
			else
				default to RecurringInvoiceLine.StopDate
			
			if (RecurringInvoiceParameter.StopDate entered)					
				required													
			
			
			constraint (StartDate <= StopDate)
				"StartDateCannotBeGreaterThanStopDate"						
			
			if (RecurringInvoiceLine not entered)							
				if (RecurringInvoiceParameter.StopDate entered)
					constraint (StopDate <= RecurringInvoiceParameter.StopDate)
						"StopDateCannotBeGreaterThanInvoiceStopDate"			
			else
			if (RecurringInvoiceLine.StopDate entered)
				constraint (StopDate <= RecurringInvoiceLine.StopDate)
					"StopDateCannotBeGreaterThanInvoiceLineStopDate"			
		
		TaxExemptCode
			default to Miscellaneous.TaxStatus
			
			if (RecurringInvoiceParameter.TaxExemptCode.Exempt
			and TaxExemptCode.Taxable)
				TaxExemptCode = RecurringInvoiceParameter.TaxExemptCode				

   	Derived Fields
   	
   		DerivedRecurringInvoiceAddOnCharge is a DerivedField
   			type is like RecurringInvoiceAddOnCharge
   			return RecurringInvoiceAddOnCharge
	
	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Delete Action
		
