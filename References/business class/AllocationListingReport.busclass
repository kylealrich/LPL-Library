AllocationListingReport is a BusinessClass
    owned by Allocations
    prefix is ALLR

    Ontology
    	symbolic key is AllocationListingReport

    Persistent Fields
        Allocation
        PostingDate
        AllocationGroup
        AllocationPeriod            is a GeneralLedgerCalendarPeriod
        Status                      is Numeric 1
            protected
            States
                InProgress         value is 0
                Completed          value is 2
        FromStep			        is a AllocationStepNumber
        ToStep				        is a AllocationStepNumber
        RunStartDate                is Date
        RunEndTimeStamp             is TimeStamp
            default label is "RunEndDate_/Time"
        ListingReport               is BinaryDocument
    
    Sets
        ByRunDescending
            Sort Order
                FinanceEnterpriseGroup
                AllocationControl
                AllocationListingReport descending
        
    Derived Fields
		CurrentDate is a DerivedField
			type is TimeStamp
			return current timestamp

        DerivedSequenceNumber   is a DerivedField
            type is AlphaRight size 9
            restricted
            return 100000000 + AllocationListingReport

        DerivedAllocationRunRepresentation    is a DerivedField
            type is like AllocationRun
            restricted
            return AllocationControl + "-" + DerivedSequenceNumber[2:9]

        RunEndDateTimeForList is a DerivedField
            type is TimeStamp
            default label is "RunEndDate_/Time"
            if (ListingReport entered)
                return RunEndTimeStamp
            else
                return blank
    Relations

        AllocationsForListingReportRel
			one-to-many relation to Allocation
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AllocationSourceSystem	= AllocationSourceSystem
				related.AllocationControl		= AllocationControl
			Instance Selection
				where ((related.Allocation		= Allocation
				or	    Allocation not entered)
				and    (related.AllocationGroup	= AllocationGroup
				or      AllocationGroup not entered))

        AllocationRunRel
			one-to-one relation to AllocationRun
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AllocationRun			= DerivedAllocationRunRepresentation

    Field Rules
        Status
            initial value is Status.InProgress
            default to Status.InProgress


    Actions 
        Create is an Action
            restricted
            Exit Rules

                invoke Create Started AllocationRun
                    invoked.FinanceEnterpriseGroup      = FinanceEnterpriseGroup
                    invoked.AllocationRun				= DerivedAllocationRunRepresentation
                    invoked.AllocationControl			= AllocationControl
                    invoked.AllocationSourceSystem		= AllocationSourceSystem
                    invoked.FromStep					= FromStep
                    invoked.ToStep						= ToStep
                    invoked.PostingDate					= PostingDate
                    invoked.AllocationPeriod			= AllocationPeriod
                    invoked.Allocation					= Allocation
                    invoked.AllocationGroup				= AllocationGroup
                    invoked.ListingReportOnly           = true

        Update is an Action
            restricted

        DeleteListingReport is a Delete Action
            Entrance Rules
                invoke Started.DeleteRun AllocationRunRel

        GenerateReportsInternal is an Instance Action
            default label is untranslatable
            restricted
            Action Rules

                RunEndTimeStamp = current timestamp
                
                generate document ListingReport as pdf in landscape font offset is -2
                    set using action SetListingReport

        SetListingReport    is an Instance Action
            default label is untranslatable
            restricted
            
            Parameters
                Report is BinaryDocument
            Action Rules
                ListingReport   = Report

                Status          = Status.Completed



