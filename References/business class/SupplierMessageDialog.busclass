SupplierMessageDialog is a BusinessClass
	owned by procurement
	prefix is SMD1
	
	Ontology
		symbolic key is SupplierMessageDialog
		
	Patterns
	
	Persistent Fields
		Origin              is Numeric size 1
			States
				Supplier    value is 0
				Other       value is 1
		Message 		  	is Alpha size 1000
		Response		  	is Alpha size 1000
		Status			  	is Numeric size 1
			States
				NoResponse	        value is 1
				Responded	        value is 2
				ResponseNotRequired value is 3
		DateCreated  	  	is TimeStamp
		ResponseDate	  	is TimeStamp
		ResponseRequested 	is Boolean
		MessageAttachment 	is an Attachment
		ResponseAttachment	is an Attachment
			
	Local Fields
		LocalContact        is like Employee

	Derived Fields
	
		PurchaseOrderMessage is a MessageField
			restricted
			"Purchase_Order"
			
		InvoiceMessage is a MessageField
			restricted
			"Payables_Invoice"
			
		ContractMessage is a MessageField
			restricted
			"Contract"
			
		ReceiptMessage is a MessageField
			restricted
			"Purchase_Order_Receipt"
			
		EventMessage is a MessageField
			restricted
			"Event"
			
		ReturnMessage is a MessageField
			restricted
			"Return"	
			
		SupplierSourceMessage is a MessageField
			restricted
			"Supplier"
			
		PurchasingMessage is a MessageField
			restricted
			"Purchasing"
			
		PayablesMessage is a MessageField
			restricted
			"Payables"
			
		SourcingMessage is a MessageField
			restricted
			"Sourcing"
		
		ContractsMessage is a MessageField
			restricted
			"Contracts"
		
		RequisitionMessage is a MessageField
			restricted
			"Requisitions"

		OtherResponseLinkback 													is a MessageField
			restricted
			"<linkback(webapp is Lpa navigation is DialogResponsesNav text is \"here\")>"

		SupplierResponseLinkback    											is a MessageField
			restricted
			"<linkback(webapp is SupplyManagementSupplier navigation is SupplierDialogResponsesNav text is \"here\")>"

		DerivedOrigin is a DerivedField
			type is Alpha 50
			default label is "Origin"
			if (Origin = 0)
				return SupplierSourceMessage
			else
			if (PurchaseOrderEntered
			or  ReceiptEntered
			or  ReturnEntered)
				return PurchasingMessage 
			else 
			if (InvoiceEntered)
				return PayablesMessage 
			else
			if (ContractEntered)
				return ContractsMessage 
			else	
			if (EventEntered)
				return SourcingMessage 
			else
			if (RequisitionEntered)
				return RequisitionMessage
		
		DerivedDocumentNumber is a DerivedField
			type is Alpha 50
			restricted
			if (PurchaseOrderEntered
			and !ReceiptEntered)
				return (PurchaseOrderMessage + ": " + SupplierContactMessage.OriginatingPO)
			else 
			if (InvoiceEntered)
				return (InvoiceMessage + ": " + SupplierContactMessage.InvoiceRel.Invoice)
			else
			if (ContractEntered)
				return (ContractMessage + ": " + SupplierContactMessage.OriginatingContract)
			else
			if (ReceiptEntered)
				return (ReceiptMessage + ": " + SupplierContactMessage.OriginatingReceipt)
			else	
			if (EventEntered)
				return (EventMessage + ": " + SupplierContactMessage.OriginatingEvent)
			else
			if (ReturnEntered)		
				return (ReturnMessage + ": " + SupplierContactMessage.OriginatingReturn)
			else
			if (RequisitionEntered)
				return (RequisitionMessage + ": " + SupplierContactMessage.OriginatingRequisition)				
	
	Conditions
	
		CanCreate
			restricted
			when (SupplierContactMessage.Active)
			
		CanRespond
			restricted
			when (!Status.Responded
			and ((ViewerIsSupplier
			and   Origin = 1)
			or   (ViewerIsMessageOwner
			and   Origin = 0)))	
	
		CanUpdate
			restricted
			when (!Status.Responded
			and ((ViewerIsSupplier
			and   Origin = 0)
			or   (ViewerIsMessageOwner
			and   Origin = 1)))	

		OpenSupplierReponse
			restricted
			when (!Status.Responded
			and   Origin = 1)

		SupplierOrigin
			restricted
			when (Origin = 0)
			
		OtherOrigin
			restricted
			when (Origin = 1)
		
		MustRespond
			restricted
			when (CanRespond
			and   ResponseRequested)

		ViewerIsSupplier
			restricted
			when (SupplierContactMessage.IsSupplierContact)
			
		ViewerIsMessageOwner
			restricted
			when  (SupplierContactMessage.MessageOwner = actor.agent(Employee).Employee
			or     IsApplicationAdministrator)

		MessageOwnerEntered 
			restricted 
			when (SupplierContactMessage.MessageOwner entered)
		
		ActorDialogNeedingResponsesExists
			restricted
			when (MustRespond
            and   !SupplierContactMessage.Deleted
			and   (SupplierContactMessage.MessageOwner = actor.agent(Employee).Employee
			or     IsApplicationAdministrator))

		CanDelete
			restricted
			when (!Status.Responded
			and ((ViewerIsSupplier
			and   Origin = 0)
			or   (ViewerIsMessageOwner
			and   Origin = 1)))	

		HasMessageAttachment
			restricted
			when (MessageAttachment entered)
			
		HasResponseAttachment
			restricted
			when (ResponseAttachment entered)
	
		HasResponse
			restricted
			when (Response entered)

		IsApplicationAdministrator
			restricted
			when (ApplicationAdministratorRel exists)

		PurchaseOrderEntered
			restricted
			when (SupplierContactMessage.OriginatingPO entered)

		InvoiceEntered
			restricted
			when (SupplierContactMessage.OriginatingInvoice entered)

		ContractEntered
			restricted
			when (SupplierContactMessage.OriginatingContract entered)
		
		EventEntered
			restricted
			when (SupplierContactMessage.OriginatingEvent entered)
		
		ReceiptEntered
			restricted
			when (SupplierContactMessage.OriginatingReceipt entered)

		ReturnEntered
			restricted
			when (SupplierContactMessage.OriginatingReturn entered)

		RequisitionEntered
			restricted
			when (SupplierContactMessage.OriginatingRequisition entered)

		PurchaseOrderLinkExists
			restricted
			when (SupplierContactMessage.PurchaseOrderRel exists)

		InvoiceLinkExists
			restricted
			when (SupplierContactMessage.InvoiceRel exists)

		ContractLinkExists
			restricted
			when (SupplierContactMessage.ContractRel exists)

		EventLinkExists
			restricted
			when (SupplierContactMessage.EventRel exists)
			
		ReceiptLinkExists
			restricted
			when (SupplierContactMessage.ReceiptRel exists)

		PaymentLinkExists
			restricted
			when (SupplierContactMessage.PaymentRel exists
			and   SupplierContactMessage.OriginatingPayment = true)
			
		ReturnLinkExists
			restricted
			when (SupplierContactMessage.VendorReturnRel exists)

		RequisitionLinkExists
			restricted
			when (SupplierContactMessage.RequisitionRel exists)
	
	Sets
		ByDateCreated
			Sort Order
				SupplierGroup
				DateCreated
				SupplierContactMessage
				SupplierMessageDialog
				












	
	Relations
	
		ApplicationAdministratorRel
			one-to-one relation to ActorRole
			Field Mapping uses symbolic key
				related.Actor			= actor
				related.ActorRole.Role	= "ApplicationAdministrator_ST"	

		SupplierDialogRel
			one-to-one relation to SampleDocumentTemplate
			Field Mapping uses symbolic key
				related.SampleDocumentTemplate 	= "SUPPLIERMESSAGESANDDIALOG_ST"

		SupplierGroupPortalContactsRel
			one-to-many relation to SupplierGroupPortalContacts
			Field Mapping uses ByEmployee
				related.Contact         = LocalContact
				related.SupplierGroup   = SupplierGroup
				
		OtherSupplierMessageDialogRel
			one-to-many relation to SupplierMessageDialog
			Field Mapping uses symbolic key
				related.SupplierGroup 			= SupplierGroup
				related.Supplier        		= Supplier
				related.SupplierSourceId		= SupplierSourceId
				related.SupplierMessage     	= SupplierMessage
				related.SupplierContactMessage	= SupplierContactMessage
			Instance Selection
				where (related.UniqueID != UniqueID)
	Field Rules
		
	Actions
	
		Create is a Create Action
			restricted
			Field Rules
				Message 
					required	
		
			Action Rules
				if (ResponseRequested)
					Status = Status.NoResponse
				else
					Status = Status.ResponseNotRequired
				DateCreated = current timestamp
		
			Exit Rules
			
				if (OtherOrigin)
					if (ResponseRequested)
						send email
							to SupplierSourceId.EmailAddress
							from SupplierContactMessage.MessageOwner.EmployeeWorkEmailAddress
							subject "ADialogHasBeenCreatedFor<DerivedDocumentNumber>"
							Contents
								"ADialogFor<DerivedDocumentNumber>HasBeenEntered"
								"TheDialogMessageIs:_<Message>"
								"TheDialogRequiresAResponse"
								"Click<SupplierResponseLinkback>ToRespondInSupplierPortal."
					else
					if (!ResponseRequested)
						send email
							to SupplierSourceId.EmailAddress
							from SupplierContactMessage.MessageOwner.EmployeeWorkEmailAddress
							subject "ADialogHasBeenCreatedFor<DerivedDocumentNumber>"
							Contents
								"ADialogFor<DerivedDocumentNumber>HasBeenEntered"
								"TheDialogMessageIs:_<Message>"
								"LogonToSupplierPortalAndViewTheDialogDetailsInMessagesAndDialog"
				
				if (SupplierOrigin)
					if (MessageOwnerEntered)
						if (ResponseRequested)
							send email
								to SupplierContactMessage.MessageOwner.EmployeeWorkEmailAddress
								from SupplierSourceId.EmailAddress
								subject "ASupplierHasCreatedADialogFor<DerivedDocumentNumber>"
								Contents
									"ADialogFor<DerivedDocumentNumber>HasBeenEntered"
									"TheDialogMessageIs:_<Message>"
									"TheDialogRequiresAResponse"
									"Click<OtherResponseLinkback>ToRespond."
						else
						if (!ResponseRequested)
							send email
								to SupplierContactMessage.MessageOwner.EmployeeWorkEmailAddress
								from SupplierSourceId.EmailAddress
								subject "ASupplierHasCreatedADialogFor<DerivedDocumentNumber>"
								Contents
									"ADialogFor<DerivedDocumentNumber>HasBeenEntered"
									"TheDialogMessageIs:_<Message>"
									"ViewTheDialogDetailsInMessagesAndDialog"							

		Update is an Update Action
			valid when (CanUpdate)

		Respond is an Update Action
			valid when (CanRespond)
			Field Rules
				Response
					required
					
			Action Rules
				ResponseDate = current timestamp
				Status       = Status.Responded
				if (SupplierOrigin)
					send email
						to SupplierSourceId.EmailAddress
						from SupplierContactMessage.MessageOwner.EmployeeWorkEmailAddress
						subject "TheDialogContactHasRespondedToYourDialogFromSupplierPortal"
						Contents
							"AResponseFor<DerivedDocumentNumber>HasBeenEntered"
							"TheOriginalMessageWas:_<Message>"
							"TheResponseIs:_<Response>"
							"LogonToSupplierPortalAndViewTheDialogResponseInMessagesAndDialog"
				
				if (OtherOrigin)
					send email
						to SupplierContactMessage.MessageOwner.EmployeeWorkEmailAddress
						from SupplierSourceId.EmailAddress
						subject "TheSupplierHasRespondedToYourDialog"
						Contents
							"AResponseFor<DerivedDocumentNumber>HasBeenEntered"
							"TheOriginalMessageWas:_<Message>"
							"TheResponseIs:_<Response>"
							"ViewTheDialogResponseInMessagesAndDialog"													
						
		Delete is a Delete Action
			valid when (CanDelete)
			
			Action Rules
			
				if (OtherSupplierMessageDialogRel !exists)
					invoke Delete SupplierContactMessage 

		ChangeDialogContact is an Instance Action
			valid when (CanRespond)
			Parameters
				NewDialogContact is an Employee
				
			Parameter Rules
				NewDialogContact
					required
					constraint (NewDialogContact != SupplierContactMessage.MessageOwner)
						"NewDialogContactMustBeDifferentThanCurrentContact"
					LocalContact = NewDialogContact
					constraint (SupplierGroupPortalContactsRel exists)
						"DialogContactMustBeAValidSupplierGroupPortalContact"
						
			Action Rules
				invoke UpdateContact SupplierContactMessage
					invoked.NewDialogContact = NewDialogContact
