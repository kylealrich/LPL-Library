AssetImport is a BusinessClass
    owned by am
    prefix is ACN
    classic name is AMASSETCNV

    Ontology
        symbolic key is AssetImport
            classic set name is ACNSET1
            classic name is CONVERSION-NBR

    Patterns
        implements StaticJava
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
		RunGroup
        RecordInError                 		is Boolean
		ErrorMessage						is Alpha 150
		Asset
			delete ignored
		AssetManagementInterfaceResult
				
        Description
        Classification              				is AlphaUpper size 1
            States
                Purchased value is "P"
                Finance   value is "C"
                Operating value is "O"
        AssetLease
        TagNumber
            classic name is TAG-NBR
        AssetGroup
        AssetCategory
		Reference2							is a Reference
        Simulated                  			is Boolean
        WorkInProcess              			is Boolean
            classic name is WORK-IN-PROC
        TaxExempt                  			is Boolean
        Used                       			is Boolean
        Company                    			is an AssetCompany
        	disable surrogates
        Currency
            classic name is CURRENCY-CODE
        AssetLocation
            classic name is LOCATION-NAME
        AssetDivision
            classic name is DIVISION
        AssetType
            classic name for AssetType.Type is ASSET-TYPE
        AssetAccountingUnit						 	is a FinanceCodeBlockNoAccountFull 
        	default label is "DefaultTransactionDimensions"
        DepreciationExpenseProject					is a FinanceCodeBlockProjectOnly
      		default label is "DepreciationExpenseProject"
        AssetAccountingUnitGroup
        	default label is "AssetDimensionGroup"
        AssetAccountGroup					
            classic name is ACCT-GRP		
        IncludeInPhysicalInventory 					is Boolean
            classic name is INV-FLAG
        AssetOwner									is a Employee
        AssetProject	 							is a FinanceCodeBlockProjectOnly
        AssetSummaryGroup
		AssetGuidelineClass
		
	Local Fields
    	InterfacedAsset					is an Asset view
    	LocalAsset						is like Asset
		LocalFund 						is an AssetFundField	
		LocalBookRegulation				is like BookRegulation
		LocalInServiceDate				is Date
		
	Derived Fields
		TransactionItemCostCurrency is a MessageField
			"<Currency>_<ItemTotalTransactionCost>"

		NumberOfDecimals is a DerivedField
			type is Numeric 1
			if (Currency entered)
				return AssetImport.Currency.NumberOfDecimals		
			else
				return AssetImport.Company.AccountingEntity.FunctionalCurrency.NumberOfDecimals
			
		ItemTotalTransactionCost is a DerivedField
			type is like InternationalAmount	
			return sum AssetItemImportRel.TransactionItemCost
					
    Conditions
        AssetBookImportExists
        	restricted
            classic name is BOOK-CNV-EXIST
            when (first AssetBookImportRel exists)

        ImportedAssetExists
        	restricted
            classic name is CNV-AST-EXIST
            when (ImportedAssetRel exists)

        AssetItemImportExists
        	restricted
            classic name is ITEM-CNV-EXIST
            when (first AssetItemImportRel exists)

        AssetTransactionImportExists
        	restricted
            classic name is TRAN-CNV-EXIST
            when (first AssetTransactionImportRel exists)

		IsNoRunningDuplicate
			when (RunningDuplicateRel not exists)
			
		FundAccounting
			when (FinanceEnterpriseGroup.FundAccounting
			and not AssetType.AllocateDepreciationExpense)


		FundAllocationComplete	
			when ((!FinanceEnterpriseGroup.FundAccounting)
			or  (FinanceEnterpriseGroup.FundAccounting
			and (!AssetImportFundsExists
			or   (AssetImportFundsExists
			and sum AssetImportFundsRel.PercentContribution = 1)))
			and (!AssetItemImportFundsExist
			or   (AssetItemImportFundsExist
			and all AssetItemImportRel.FundAllocationComplete)))

		AssetItemImportFundsExist
			when (AssetItemImportFundsRel exists)
			
		AssetImportFundsExists	
			when (AssetImportFundsRel exists)
						
		AseetGuidelineClassesExists
			restricted
			when (AssetGuidelineClassesRel exists)
		
		IsValidForActorContext
			when (FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)

    Relations

        AssetBookImportRel
            classic name is AMASTBKCNV
            one-to-many relation to AssetBookImport
            Field Mapping uses Set1
                related.AssetImport = AssetImport

        AssetBookImportErrorsRel
            one-to-many relation to AssetBookImport
            Field Mapping uses Set1
                related.AssetImport = AssetImport
           	Instance Selection
           		where (related.RecordInError = true)     
                
        AssetItemImportRel
            classic name is AMASTITCNV
            one-to-many relation to AssetItemImport
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
                related.AssetImport 			= AssetImport

        AssetCommentImportRel
            one-to-many relation to AssetCommentImport
            delete cascades
            Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
                related.AssetImport 			= AssetImport

        AssetTransactionImportRel
            classic name is AMASTTRCNV
            one-to-many relation to AssetTransactionImport
            Field Mapping uses Set1
                related.AssetTransactionImport.ConversionNumber = AssetImport
                
        NewAssetCommentImportRel
            one-to-many relation to AssetCommentImport
            Field Mapping uses symbolic key
            	related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
			Instance Selection
				where (related.ConversionNumber  = AssetImport)

        ImportedAssetRel
        	one-to-many relation to ImportedAsset
        	Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup	= AssetImport.FinanceEnterpriseGroup
         		related.AssetImport				= AssetImport        


		LocalAssetRel
        	one-to-many relation to Asset
        	Field Mapping uses symbolic key
                related.FinanceEnterpriseGroup	= AssetImport.FinanceEnterpriseGroup
         		related.Asset					= LocalAsset        
	
		RunningDuplicateRel
			one-to-many relation to AssetManagementInterfaceResult
			Field Mapping uses ByRunGroupStatus
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.RunGroup						= RunGroup
				related.Status							= 1
		
		AssetImportFundsRel
			one-to-many relation to AssetImportFund
			delete cascades
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup			
				related.AssetImport						= AssetImport	
			
				
		AssetItemImportFundsRel
			one-to-many relation to AssetItemImportFund
			delete cascades
			Field Mapping uses symbolic key				
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup			
				related.AssetImport						= AssetImport	

		GuidelineClassRel
			one-to-many relation to AssetGuidelineClass
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
			Instance Selection
				where (related.AssetGuidelineClass = AssetGuidelineClass)		

		AssetGuidelineClassesRel
			one-to-many relation to AssetGuidelineClass
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup	
		GuidelineClassRegulationsRel
			one-to-many relation to AssetGuidelineClassRegulation
			Field Mapping uses ByEffectiveDate
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup	
				related.AssetGuidelineClass		= AssetGuidelineClass	
                related.BookRegulation          = LocalBookRegulation      
			Instance Selection				
				where (related.EffectiveDate <= LocalInServiceDate
				and related.Active = true)		

    Sets

        Set2
            indexed
            Sort Order
				FinanceEnterpriseGroup
                Company
                AssetImport

        Set3
            indexed
            Sort Order
            	FinanceEnterpriseGroup
                AssetLease
                AssetImport

    Field Rules



				
		Description
			required
		Company
			required
			constraint (Company.AccountingEntity.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
				"CompanyDoesNotExist"				
		AssetType
        	if (AssetType.Property.Land)
        		constraint (AssetCategory.NonDepreciable)
        			"AssetTypeLandCannotBeDepreciable"
            required
		AssetAccountGroup
			required
			if (AssetAccountingUnitGroup not entered)
				constraint (AssetAccountingUnit entered)
					"DefaultTransactionDimensionsOrAssetDimensionGroupMustBeProvided"				
				
		AssetAccountingUnitGroup 
			if (AssetAccountingUnit not entered)
				required
				constraint (AssetAccountingUnitGroup entered)
					"DimensionGroupIsRequiredIfDefaultDimensionCodeIsNotProvided"
				constraint (AssetAccountingUnitGroup.AssetAccountingUnit entered)
					"AddDefaultDimensionCodeOrProvideAssetDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.ClearingAccountingUnit entered)
					"AddDefaultDimensionCodeOrProvideAssetClearingDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.AccumulatedDepreciationAccountingUnit entered)					
					"AddDefaultDimensionCodeOrProvideAccumulatedDepreciationDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.DepreciationExpenseAccountingUnit entered)
					"AddDefaultDimensionCodeOrProvideDepreciationExpenseDimensioncodeInDimensionGroup"		
				constraint (AssetAccountingUnitGroup.ProceedsAccountingUnit entered)	
					"AddDefaultDimensionCodeOrProvideProceedsDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.DisposalExpenseAccountingUnit entered)
					"AddDefaultDimensionCodeOrProvideExpenseDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.GainAccountingUnit entered)
					"AddDefaultDimensionCodeOrProvideGainDimensionCodInDimensionGroup"
				constraint (AssetAccountingUnitGroup.LossAccountingUnit	entered)	
					"AddDefaultDimensionCodeOrProvideLossDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.DeferredGainAccountingUnit	entered)
					"AddDefaultDimensionCodeOrProvideDeferredGainDimensionCodeInDimensionGroup"
				constraint (AssetAccountingUnitGroup.DeferredLossAccountingUnit entered)	
					"AddDefaultDimensionCodeOrProvideDeferredLossDimensionCodeInDimensionGroup"
				if (Company.Revalue)
					constraint (AssetAccountingUnitGroup.RevalueSurplusAccountingUnit entered)	
						"AddDefaultDimensionCodeOrProvideRevalueSurplusDimensionCodeInDimensionGroup"
					constraint (AssetAccountingUnitGroup.RevalueLossAccountingUnit entered)	
						"AddDefaultDimensionCodeOrProvideRevalueLossDimensionCodeInDimensionGroup"
        AssetCategory

		Classification

		Currency
			default to Company.Currency


	Rule Blocks

		InterfaceThisAsset

	
			initialize RecordInError
			initialize ErrorMessage
			if(Asset entered
			and Asset.InterfaceInProgress)
				InterfacedAsset = Asset
				invoke NoEditUpdate Asset
					fill in fields from this instance
						except invoked.AssetCategory
						except invoked.Classification
					invoked.AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
					if (AssetCategory entered)
						invoked.AssetCategory				= AssetCategory
					else
						invoked.AssetCategory				= Asset.AssetCategory
					if (Classification entered)
						invoked.Classification				= Classification
					else
						invoked.Classification				= Asset.Classification
			else
				invoke ImportCreate Asset
					assign result to InterfacedAsset
					resume on error
						RecordInError		= true
						ErrorMessage		= error message
					fill in fields from this instance
						except invoked.ItemTotalTransactionCost
					invoked.Reference1						= AssetImport
					invoked.AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
					if (AssetImportFundsExists)
						invoked.FundOverride 					= true

			if (!RecordInError)
				Asset							= InterfacedAsset.Asset
				AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
				RecordInError                   = false
				
				if (!Asset.InterfaceInProgress)
					invoke Create ImportedAsset
						invoked.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
						invoked.AssetImport				= AssetImport
						invoked.Asset					= Asset
				if (AssetImportFundsExists)	
					for each AssetImportFundsRel
						LocalFund						= each.Fund
						invoke InterfaceAssetFund each
							invoked.PrmAssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult





			else
				invoke IncrementErrorCount PrmAssetManagementInterfaceResult

	Actions		
        Create is a Create Action
			Entrance Rules
				if (AssetGuidelineClass entered)
					constraint (GuidelineClassRel exists)
						"AssetGuidelineClassDoesNotExist"
			Action Rules

        Update is an Update Action
			Entrance Rules
				if (AssetGuidelineClass changed
				and AssetGuidelineClass entered)
					constraint (GuidelineClassRel exists)
						"AssetGuidelineClassDoesNotExist"
					for each AssetBookImport set
						LocalBookRegulation = each.BookRegulation
						LocalInServiceDate	= each.InServiceDate
						if (LocalInServiceDate entered)
							constraint (GuidelineClassRegulationsRel exists)
								"GuidelineClassRegulationDoesNotExistForBookRegulation<LocalBookRegulation>_GuidelineClass<AssetGuidelineClass>_InServiceDate<LocalInServiceDate>"
							invoke Update each
								invoked.TransientUpdateFromImport = true
			Action Rules	
				if (Used changed)	 
					confirmation required
						"ThisChangeWillUpdateTheUsedFlagOnBookImportRecords;SelectOKToContinue"
					invoke Update AssetBookImport set	
						invoked.Used = Used

        Delete is a Delete Action
        	Entrance Rules
        		if (AssetItemImport set exists
        		or AssetBookImport set exists
        		or AssetCommentImport set exists)
        			confirmation required
        				"RelatedBook,ItemAndCommentImportRecordsWillAlsoBeDeleted;SelectYesToProceed"
        			invoke Delete AssetBookImport set	
        			invoke Delete AssetItemImport set	        
					invoke Delete AssetCommentImport set
				LocalAsset = Asset	
				invoke Delete LocalAssetRel
					
					
		InterfaceAssetsByLeaseCompany is a Set Action
			restricted
			completion message is "InterfaceAssetsSubmitted"
			Parameters
				PrmRunGroup							is a RunGroup
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
		        PrmLeaseCompany						is a GeneralLedgerCompany
		        PrmLease							is a Lease
		        PrmFromRange						is like AssetImport
		        PrmToRange							is like AssetImport
				PrmAssetManagementInterfaceResult	is a AssetManagementInterfaceResult
					context of PrmFinanceEnterpriseGroup

			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmLeaseCompany
					required

			Instance Selection			
				where (RunGroup							= PrmRunGroup
				and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
				and    AssetLease.LeaseCompany			= PrmLeaseCompany
				and   (AssetLease.Lease					= PrmLease
				or     PrmLease							not entered)
				and   (PrmFromRange						<= AssetImport
				and   (AssetImport						<= PrmToRange
				or     PrmToRange						not entered)))

			Local Fields
				CommitNow						is Boolean

			Sort Order
				FinanceEnterpriseGroup
				Company
				AssetLease.LeaseCompany
				AssetLease.Lease
				AssetImport
				
			Action Rules
				Company Set Rules
					Exit Rules
				   		invoke Update Company
							invoked.LastAsset = InterfacedAsset.Asset				
				
				Instance Rules

					if (PrmAssetManagementInterfaceResult.ErrorCount < PrmAssetManagementInterfaceResult.ErrorLimit)
						include InterfaceThisAsset


		InterfaceAssetsByCompany is a Set Action
			restricted
			completion message is "InterfaceAssetsSubmitted"
			Parameters
				PrmRunGroup							is a RunGroup
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
		        PrmCompany							is a GeneralLedgerCompany
		        PrmFromRange						is like AssetImport
		        PrmToRange							is like AssetImport
				PrmAssetManagementInterfaceResult	is a AssetManagementInterfaceResult
					context of PrmFinanceEnterpriseGroup

			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmCompany
					required

			Instance Selection			
				where (RunGroup							= PrmRunGroup
				and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup

				and    Company							= PrmCompany
				and   (PrmFromRange						<= AssetImport
				and   (AssetImport						<= PrmToRange
				or     PrmToRange						not entered)))

			Local Fields
				CommitNow						is Boolean

			Sort Order
				FinanceEnterpriseGroup
				Company
				AssetImport
				
			Action Rules
				Company Set Rules
					Exit Rules
				   		invoke Update Company
							invoked.LastAsset = InterfacedAsset.Asset				
				
				Instance Rules

					if (PrmAssetManagementInterfaceResult.ErrorCount < PrmAssetManagementInterfaceResult.ErrorLimit)
						include InterfaceThisAsset
				
		FastDelete is a Purge Action
			restricted
			bypass relational integrity rules

		DeleteImportedRecords is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmRunGroup				  			is a RunGroup
				PrmAssetManagementInterfaceResult	is an AssetManagementInterfaceResult


			Instance Selection
				where (RunGroup							= PrmRunGroup
                and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
                and    AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
				and    Asset entered
				and   !RecordInError)
				
			Sort Order 
				FinanceEnterpriseGroup
				AssetManagementInterfaceResult
				AssetImport
				
				
				
			Action Rules
			
				Instance Rules
					if (AssetBookImportErrorsRel exists)
				 		invoke NoEditUpdate Asset
							invoked.InterfaceInProgress = true
					else
					if (PrmAssetManagementInterfaceResult.ErrorCount = PrmAssetManagementInterfaceResult.ErrorLimit
					and AssetBookImportRel exists)
						invoke NoEditUpdate Asset
							invoked.InterfaceInProgress = true
					else
						invoke NoEditUpdate Asset
							invoked.InterfaceInProgress = false
						invoke BuildTextIndex Asset
						invoke Delete AssetImportFundsRel
					    invoke FastDelete
							resume on error
									
		
		DeleteRecordsInError is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup			is a FinanceEnterpriseGroup
				PrmRunGroup				  			is a RunGroup
				PrmAssetManagementInterfaceResult	is like AssetManagementInterfaceResult

			Instance Selection
				where (RunGroup							= PrmRunGroup
                and    FinanceEnterpriseGroup			= PrmFinanceEnterpriseGroup
                and    AssetManagementInterfaceResult	= PrmAssetManagementInterfaceResult
				and    Asset entered
				and    RecordInError)
				
			Action Rules
			
				Instance Rules





					LocalAsset = Asset										

					invoke NoEditUpdate LocalAssetRel
						invoked.InterfaceInProgress = true


		
		SetRecordInError is an Instance Action
			restricted
			Action Rules
				RecordInError = true
		
		PerformAssetInterface is an Instance Action
			completion message is "AssetInterfaceSubmitted"
			Entrance Rules
				constraint (IsNoRunningDuplicate)
					"AssetInterfaceAlreadyRunningForRunGroup"
			Parameters
				PrmRunGroup				  			is a RunGroup
                PrmCompany							is an AssetCompany
                PrmCompanyGroup						is a GeneralLedgerCompanyGroup
                PrmLeaseCompany						is a GeneralLedgerCompany 
                PrmLease							is a Lease
        			context of PrmLeaseCompany                
                PrmErrorLimit						is Numeric size 6
		        PrmFilesToConvert			    	is AlphaUpper size 1
		            States
		                All					value is "1"
		                AssetItemBook		value is "2"
		                    default label is "Asset/Item/Book"
		                TransactionsOnly	value is "4"
		                CommentsOnly		value is "5"
                PrmFromRange						is AlphaUpper size 22
                PrmToRange							is AlphaUpper size 22
		        PrmAttributeBy						is AlphaUpper size 1
		            States
		                ByAsset				value is "A"
		                    default label is "by Asset"
		                ByConversionNumber	value is "C"
		                    default label is "by Conversion Number"
				PrmComputeOption					is a Compute
				PrmAllowZeroLifeRemaining			is Boolean
			Parameter Rules
				PrmRunGroup
					required
					default to RunGroup
					initial value is RunGroup
					if (PrmCompany not entered
					and PrmCompanyGroup not entered
					and PrmLeaseCompany not entered)
						constraint (false)
							"MustEnterCompanyOrCompanyGroupOrLeaseCompany"
					if (((PrmCompany entered
					and  PrmCompanyGroup entered)
					or  (PrmCompany entered
					and  PrmLeaseCompany entered)
					or  (PrmCompanyGroup entered
					and  PrmLeaseCompany entered)))
						constraint (false)
							"CanOnlyEnterOneOfCompany,CompanyGroup,OrLease"
		        PrmCompany
					if (PrmCompany entered)
						constraint (PrmCompany.AccountingEntity.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)
							"CompanyDoesNotExist"			        
		        PrmErrorLimit
		        	initial value is 000100
		            default to 000100
		        PrmFilesToConvert
		        	initial value is PrmFilesToConvert.All
		            default to PrmFilesToConvert.All
				PrmFromRange
					if (PrmToRange entered)
						required
							"FromAndToImportGroupIsRequired"
					constraint (PrmFromRange <= PrmToRange)
						"FromRangeMustBeLessThanOrEqualToToRange"
				PrmToRange
					if (PrmFromRange entered)
						required
							"FromAndToImportGroupIsRequired"
				PrmAttributeBy
					if (PrmFilesToConvert.All)
						required 
							"ConversionParameterIsRequired"
						constraint (PrmAttributeBy.ByConversionNumber)
							"ConversionOptionMustBeByConversionNumberWhenFilesToConvertIsSetToAll"
					if (PrmFilesToConvert.TransactionsOnly
					or  PrmFilesToConvert.CommentsOnly)
						required
							"ConversionParameterIsRequired"	
					if (PrmFilesToConvert.AssetItemBook
					and PrmAttributeBy entered)
						cannot be entered
							"AttributeByCannotBeEnteredWhenFilesToConvertIsAssetItemBook"
				PrmComputeOption
					constraint(!PrmComputeOption = "P")
						"ComputeOptionCanNotBeProspectiveForImportedAssets"
				
			Action Rules
				invoke AssetInterface AssetManagementInterfaceResult
					invoked.FinanceEnterpriseGroup = FinanceEnterpriseGroup
					invoked.RunGroup			   = PrmRunGroup	
					invoked.Company				   = PrmCompany			
					invoked.CompanyGroup           = PrmCompanyGroup
					invoked.LeaseCompany           = PrmLeaseCompany
					invoked.Lease                  = PrmLease
					invoked.ErrorLimit             = PrmErrorLimit
					invoked.FilesToConvert         = PrmFilesToConvert
					invoked.FromRange              = PrmFromRange																														
					invoked.ToRange                = PrmToRange
					invoked.AttributeBy            = PrmAttributeBy
					invoked.ComputeOption          = PrmComputeOption
					invoked.TransientAllowZeroLifeRemaining		= PrmAllowZeroLifeRemaining
