ContractTermAndConditionUpdate is a BusinessClass
    owned by po
    prefix is CTTMU

    Ontology
    	symbolic key is ContractTermAndConditionUpdate

    Persistent Fields
		OriginalTitle						is a Description
		RevisedTitle						is a Description
		OriginalHeaderText					is Alpha 100
		RevisedHeaderText					is Alpha 100
		OriginalDescription					is Text
		RevisedDescription					is Text
		AttachmentToDelete					is Numeric 4
		DeletedAttachment					is a Attachment
		DeletedAttachmentRef				is a Description
		NewAttachment						is an Attachment
		NewAttachmentReference 				is a Description
		NewAttachmentDisplayOrder			is a DisplayOrder
		ActionTaken							is Numeric 2
			States
				Rejected	value is 1
				Approved	value is 2
				Withdrawn	value is 3
				Disapproved	value is 4
		Status								is Numeric 2
			States
				Draft		value is 1
				Pending		value is 2
				Complete	value is 3
		RequestType							is Numeric 2
			default label is "Action"
			States
				Create		value is 1
				Update		value is 2
				Delete		value is 3
				Restore		value is 4
		SpecificRequestType					is Numeric 2
			default label is "RequestType"
			States
				Create					value is 1
				UpdateTermAndCondition	value is 2
				AddAttachment			value is 3
				DeleteAttachment		value is 4
				DeleteTermAndCondition	value is 5
				Restore					value is 6
		RequestingActor						is an Actor
			default label is "RequestedBy"
		RejectionComment					is Text
		ApprovalComment						is Text
		RequestDateTime						is TimeStamp
			default label is "RequestDate"
		ApprovalStage						is Numeric 2
			States
				BeforeSupplierNegotiation			value is 1
				SupplierNegotiation					value is 2
				AfterSupplierNegotiation			value is 3
				BeforeSupplierAddendumNegotiation	value is 4
				SupplierAddendumNegotiation			value is 5
				AfterSupplierAddendumNegotiation	value is 6
		TermNegotiationVersion				is a SequenceNumber
			default label is "Version"
		AdhocTermAndCondition				is Boolean
		AddedDuringNegotiation				is Boolean
		NoChangesMade						is Boolean
		ContractTermAndConditionAttachment	is Numeric 4
		SupplierRequest						is Boolean

	Transient Fields

	Derived Fields
		DerivedComparedTitle is a NativeField
			type is RichText

		DerivedComparedHeaderText is a NativeField
			type is RichText

		DerivedComparedDescription is a NativeField
			type is RichText

		DerivedCompareVersionFirstLastHeaderText is a NativeField
			type is RichText
			restricted

		DerivedCompareVersionFirstLastDescription is a NativeField
			type is RichText
			restricted


		DerivedFirstVersionHeaderText is a DerivedField
			type is Alpha 100
			restricted
			return first UpdatesInThisVersionRel.OriginalHeaderText


		DerivedFirstVersionDescription is a DerivedField
			type is Text
			restricted
			return first UpdatesInThisVersionRel.OriginalDescription


		DerivedLastVersionHeaderText is a DerivedField
			type is Alpha 100
			restricted
			return last UpdatesInThisVersionRel.RevisedHeaderText


		DerivedLastVersionDescription is a DerivedField
			type is Text
			restricted
			return last UpdatesInThisVersionRel.RevisedDescription

		DerivedRequesterEmailAddress is a DerivedField
			type is Alpha 100
			return RequestingActor.ContactInfo.EmailAddress
		
		DerivedRequesterPhoneNumber is a DerivedField
			type is Alpha 100
			return RequestingActor.ContactInfo.TelephoneNumber.TelephoneDisplay

		DerivedRequesterName is a DerivedField
			type is Alpha 100
			return RequestingActor.PersonName.PreferredFirstAndLastName

	Conditions
		DisplayRejectionComment
			restricted
			when (RejectionComment entered)

		RequesterEmailAddressExists
			restricted
			when (DerivedRequesterEmailAddress entered)

		ChangesMade
			restricted
			when (!NoChangesMade
			and	 (((ContractTermAndCondition.AdhocTermAndCondition
			or	    AddedDuringNegotiation)
			and	  RequestType.Create)
			or	  RequestType.Update
			or	  RequestType.Delete))

		UpdateOrDeleteText
			restricted
			when (SpecificRequestType.UpdateTermAndCondition
			or    SpecificRequestType.DeleteTermAndCondition)
		
		ChangesApproved
			restricted
			when (ChangesMade
			and	  ActionTaken.Approved)

		MoreRecentUpdateInThisVersionExists
			restricted
			when (MoreRecentUpdateInThisVersionRel exists)

		AttachmentHasBeenDeleted
			restricted
			when (SpecificRequestType.DeleteAttachment
			and	  Status.Complete)

		AttachmentToBeDeleted
			restricted
			when (SpecificRequestType.DeleteAttachment
			and	  !Status.Complete)

		AttachmentToBeAdded
			restricted
			when (SpecificRequestType.AddAttachment
			and	  !Status.Complete)

	Relations
		TermAndConditionAttachmentRel
			one-to-one relation to ContractTermAndConditionAttachment
			Field Mapping uses symbolic key
				related.ContractGroup  						= ContractGroup
				related.Contract  							= Contract
				related.ContractArticle						= ContractArticle
				related.ContractTermAndCondition			= ContractTermAndCondition
				related.ContractTermAndConditionAttachment	= AttachmentToDelete

		ContractTermVersionTermAttachmentDeletionRecordRel
			one-to-many relation to ContractTermVersionTermAttachment
			Field Mapping uses symbolic key
				related.ContractGroup 						= ContractGroup
				related.Contract 							= Contract
				related.ContractTermVersion 				= TermNegotiationVersion
				related.ContractTermVersionArticle 			= ContractArticle
				related.ContractTermVersionTerm				= ContractTermAndCondition
			Instance Selection
				where (related.SpecificRequestType.DeleteAttachment)

		MoreRecentUpdateInThisVersionRel
			one-to-many relation to ContractTermAndConditionUpdate
			Field Mapping uses symbolic key
				related.ContractGroup				= ContractGroup
				related.Contract					= Contract
				related.ContractArticle				= ContractArticle
				related.ContractTermAndCondition	= ContractTermAndCondition
			Instance Selection
				where (related.TermNegotiationVersion = TermNegotiationVersion
				and	   related.RequestDateTime > RequestDateTime)

		UpdatesInThisVersionRel
			one-to-many relation to ContractTermAndConditionUpdate
			Field Mapping uses symbolic key
				related.ContractGroup				= ContractGroup
				related.Contract					= Contract
				related.ContractArticle				= ContractArticle
				related.ContractTermAndCondition	= ContractTermAndCondition
			Instance Selection
				where (related.TermNegotiationVersion = TermNegotiationVersion)

    Field Rules
		RevisedTitle
			if (SpecificRequestType.UpdateTermAndCondition)
				required
		RevisedDescription
			if (SpecificRequestType.UpdateTermAndCondition)
				required
		AttachmentToDelete
			if (SpecificRequestType.DeleteAttachment)
				required

 	Sets
 		ByRequestDateTime
 			indexed
 			Sort Order
 				RequestDateTime
 				ContractTermAndConditionUpdate
 				ContractTermAndCondition
 				ContractArticle
 				Contract
 				ContractGroup

		ByTermNegotiationVersion
			indexed
			Sort Order
				ContractGroup
				Contract
				TermNegotiationVersion
				ContractArticle
 				ContractTermAndCondition
 				ContractTermAndConditionUpdate

	Actions
		AutoCreate is a Create Action
			restricted
			Action Rules
				RevisedTitle				= ContractTermAndCondition.Title
				RevisedHeaderText			= ContractTermAndCondition.HeaderText
				RevisedDescription			= ContractTermAndCondition.Description
				RequestingActor				= actor
				RequestType					= 1		
				SpecificRequestType			= 1		
				RequestDateTime				= current timestamp
				OriginalTitle				= ContractTermAndCondition.Title
				OriginalHeaderText			= ContractTermAndCondition.HeaderText
				OriginalDescription			= ContractTermAndCondition.Description
				Status						= 3		
				ActionTaken					= 2		
				TermNegotiationVersion		= 1

		Create is a Create Action
			restricted

		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
