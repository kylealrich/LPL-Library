CashCode is a BusinessClass
    owned by cb
    prefix is CBC
    classic name is CBCASHCODE

    Ontology
        symbolic key is CashCode
            classic set name is CBCSET1

    Patterns
        implements StaticJava
        disable AuditIndex
		implements BODId
		
	Attach Rules
		if (parentcontext.name != "CompanyCashCode"
		and parentcontext.name != "CashCodeCashPosition"
		and parentcontext.name != "CashRequirementsResult"
		and parentcontext.name != "CashRequirementsError"
		and parentcontext.name != "PositivePayFileCreation"
		and parentcontext.name != "DirectForecastDetail"		
		and parentcontext.name != "DirectForecastDetailImport"	
		and parentcontext.name != "CashPaymentRegister"
		and parentcontext.name != "PayablesJobErrorResult")
			constraint (Active)
				"CashCodeIsNotActive"

    Persistent Fields
        FinancialInstitution
        FinancialInstitutionBranch
        	context of FinancialInstitution
        BankAccountNumber					is a CashManagementAccount
        	default label is "CashManagementAccount"
            classic name is BANK-ACCT-NBR
        BankAccountValidationType
            classic name is BANK-IDENT
        Currency
            classic name is CURRENCY-CODE
        SendingFinancialInstitution			is a FinancialInstitution
        SendingFinancialInstitutionBranch	is a FinancialInstitutionBranch
        	context of SendingFinancialInstitution
            classic name is SEND-BANK-ENT
        Active                              is Boolean
            classic name is ACTIVE-STATUS
        CurrentReconciledBalance            is an InternationalAmount
            classic name is REC-BALANCE
        	disable Auditing
        UnreconciledBalance                 is an InternationalAmount
            classic name is OPEN-BALANCE
        	disable Auditing
        PayableBillOfExhange                is an InternationalAmount
            classic name is AP-DRAFTS
        	disable Auditing
        ReceivableBillOfExhangeciledBalance is an InternationalAmount
            sql name is RBillOfExhangeciledBalance
            classic name is AR-DRAFTS
        	disable Auditing
        UsedFlag                            is Boolean
        CurrencyNumberOfDecimals            is an AcctNd
            classic name is CURR-ND
        ReturnOnInvestmentRate              is Decimal size 5.3
            classic name is ROI-RATE
        LastElectronicPaymentSerialNumber   is a SerialNum
            sql name is LElectronicPaymentSerialNumber
            classic name is SERIAL-NUM
        	disable Auditing
        CashLedgerUserClass
            classic name is USER-CLASS
        ReconciliationStatement             is Boolean
            classic name is RECON-STMT-REQ
        JournalBook                         is AlphaUpper size 12
            classic name is RECON-JRNL-BK
            restricted
		CashLedgerStatement
            classic name is RECON-STMT-NBR
        LastEndingBalance                   is an InternationalAmount
            classic name is LAST-END-BAL
        SingleOpenStatement                 is Boolean
            classic name is SGLSTMT-USR-FL
        Description
        SummarizeLedgerBalances             is Boolean
            classic name is CBO-BAL-FLAG
        CreatedUser                         is an Operator 
        	holds pii
            classic name is CREATED-BY
        LastUpdateDate                      is TimeStamp
            classic name is LAST-UPDT-DATE
        LastUpdateUser                      is an Operator 
        	holds pii
            classic name is LAST-UPDATE-BY
		ApprovalCode
		NewCashLedgerPaymentForVoid			is Boolean
			default label is "CreateNewCashLedgerPaymentRecordWhenVoidOrStopPaid"
		CancelRejectedPayments				is Boolean
			default label is "AutomaticallyCancelRejectedCashLedgerPayments"
		Sepa								is Boolean
			classic name is SEPA-FLAG
			default label is "SEPA"
		SwiftID
			default label is "SWIFT/BIC"
		AutoTransactionNumbering 			is Boolean
		DefaultPostCompany					is a GeneralLedgerCompany	
			default label is "DefaultPostCompany"
			disable surrogates
		NewEFTTransactionForVoid			is Boolean	
			default label is "CreateNewEFTTransactionWhenVoidOrRejected"
		StopPayBankConfirmRequired			is Boolean		
			default label is "StopPayBankConfirmationRequired"
		EditAvailableFunds					is Boolean
		RestrictedFunds						is Boolean	
		EnforceDateEditOnVoidStopPaid		is Boolean	
			default label is "EnforceDateEditOnVoidsAndStopPaids"
		UseBankCurrencyForPaymentPosting	is Boolean	
		AllowReconciliationForVoidStopPaid	is Boolean
			default label is "AllowReconciliationWhilePendingApprovalWhenVoidOrStopPaid"
		UseIDM								is Boolean
		EscheatmentNoticeIDMTemplate		is an IDMTemplate
			default label is "EscheatmentNotice"
		IDMEmailEscheatmentNotice			is Boolean
			default label is "AutomaticEmail"
		EscheatmentNoticeIDMEmailTemplate	is an IDMTemplate
			default label is "EmailTemplate"
		EscheatmentNoticeIDMEmailAddress	is an EmailAddress
			holds pii
			default label is "FromEmailAddress"
		EscheatmentNoticeIDMEmailSubject	is Alpha size 255
			default label is "EmailSubject"
			Text Variables
				CashCode
				CashCodeGroup
				Company
				CompanyName
				Vendor
				PayeeName
				FromIssueDate
				ToIssueDate
		UnreconciledRecordCount				is Numeric 10	
       		disable Auditing
       	ReceiverBank					is Numeric 2				
			States
				BankOfAmerica			value is 1
				WellsFargo				value is 2
				JPMC					value is 3
				USBank					value is 4
		ReceiverEmailAddress			is an EmailAddress
			holds pii		

	Transient Fields
		CompletionMessage				is Alpha size 150
    
    Local Fields
		LocalPaymentNumber				is Numeric 11
		LocalEndPaymentNumber			is Numeric 11
		LocalReserveCount				is Numeric 3
		LocalVoidCount					is Numeric 3
		LocalTransactionNumber			is a TransactionNumber
		LocalBankTransactionCode		is a BankTransactionCode		
		LocalCompany					is like GeneralLedgerCompany
		LocalCashPositionDate			is Date
		LocalPeriodDate					is Date
		LocalPostingDate				is Date
		AsyncId							is a AsyncActionRequest
		BODActionCode					is Alpha size 1
			States
				Create  value is "C"
				Update	value is "U"
				Delete	value is "D"
		LocalBODCurrentTimeStamp		is a BODCurrentTimeStamp  
		LocalNativeLPLBODTrigger  		is Boolean				
		LocalEscheatmentNoticeIDMEmailSubject is Alpha size 255
			holds pii
			Text Variables
				CashCode				value is CashCode
				CashCodeGroup			value is CashCodeGroup
				Company					value is "101"
				CompanyName				value is "CompanyName"
				Vendor					value is "1"
				PayeeName				value is "PayeeName"
				FromIssueDate			value is "01012023"
				ToIssueDate				value is "12312023"

		NewBODTracker  					is a FSMInboundBODTracker view
		LocalFSMInboundBODTracker		is Numeric 15
		Error            				is Boolean
	    ErrorMessage     				is Alpha 300
	    LocalBODTrigger					is Boolean
	    LocalConfigurationParameter		is Alpha size up to 200
	    
    Context Fields
    	ContextCompany						is a Company
    	ContextFinancialInstitution			is a FinancialInstitution
		ContextFinancialInstitutionBranch	is a FinancialInstitutionBranch
		CashManagementAccount
    	CashCodeGroup			
    	FromCompany
    	ToCompany
    	FSMInboundBODTracker
                
    Derived Fields
    	ContextMessageEntityType is a StringField
			type is Alpha 30
			restricted
			"InforBankDetails"

		ContextMessageText is a MessageField
			restricted
			"CashCode<CashCode>"

		CashCodeAndDescription is a MessageField
			default label is "CashCode"
			"<CashCode>_-_<Description>"

        NetBalance is a DerivedField		
            type is like InternationalAmount
            return (CurrentReconciledBalance + UnreconciledBalance)

		CurrentDate is a DerivedField
			type is TimeStamp
			restricted
			return current timestamp

		CurrentAccountBalanceDate is a DerivedField
			type is Date
			restricted
			return (first CbbalanceRel.BankAccountBalance)

		ManualFormsReservationMessage	is a MessageField
			restricted
			"<LocalReserveCount>CashLedgerPayablesPaymentsReserved"

		ManualFormsVoidMessage			is a MessageField
			restricted
			"CashLedgerPayablesPaymentsVoided"
		

		DerivedDelimiter is a DerivedField
			type is Alpha size 5
			restricted
			LocalConfigurationParameter	= "Generic_Delimiter"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
					
		DerivedTenantID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "tenantID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedReleaseID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "releaseID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedLogicalID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "logicalID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedVersionID is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "VersionID"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		DerivedappProdline is a DerivedField
			type is Alpha size 60
			restricted
			LocalConfigurationParameter	= "appProdline"
			if(FSMBODConfigurationParameterRel exists)
				return FSMBODConfigurationParameterRel.Value
		
		BODVariationID is a DerivedField
			type is Alpha 25
			restricted
			return bod id.VariationID
			
		DerivedBODCurrentTimeStamp is a DerivedField
			type is Alpha size 20
			restricted
			DerivedBODCurrentTimeStamp = system current timestamp
			return DerivedBODCurrentTimeStamp
			
		DerivedBODFormattedCurrentTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODCurrentTimeStamp[1:4] + "-" + DerivedBODCurrentTimeStamp[5:6] + "-" + DerivedBODCurrentTimeStamp[7:8] + "T" + DerivedBODCurrentTimeStamp[9:10] + ":" + DerivedBODCurrentTimeStamp[11:12] + ":" + DerivedBODCurrentTimeStamp[13:14] + "Z"
		
		DerivedBODActionCode is a DerivedField
			type is Alpha 10
			restricted
			if (action type.Create or BODActionCode.Create)
				return "Add"
			else
			if(action type.Update or action type.Delete)
				return "Replace"
			else
				return ""
				
		DerivedLastupdateBy is a DerivedField
			type is Alpha size 60
			restricted
			return update stamp.actor
			
		DerivedBODUpdateTimeStamp is a DerivedField
			type is Alpha size 25
			restricted
			DerivedBODUpdateTimeStamp = update stamp.timestamp
			return DerivedBODUpdateTimeStamp
		
		DerivedBODFormattedUpdateTimeStamp is a DerivedField
			type is Alpha size 30
			restricted
			return DerivedBODUpdateTimeStamp[1:4] + "-" + DerivedBODUpdateTimeStamp[5:6] + "-" + DerivedBODUpdateTimeStamp[7:8] + "T" + DerivedBODUpdateTimeStamp[9:10] + ":" + DerivedBODUpdateTimeStamp[11:12] + ":" + DerivedBODUpdateTimeStamp[13:14] + "Z"
			
		DerivedDocumentID is a DerivedField
			type is Alpha size 60
			restricted
			return CashManagementGroup + DerivedDelimiter + CashCode
		
		DerivedBODRevision is a DerivedField
			type is Alpha 25
			restricted
			return ""
				
		DerivedBODFormattedId is a DerivedField
			type is Alpha 100
			restricted
			return 	"infor-nid:" + DerivedTenantID + ":" + ":"  + ":" + DerivedDocumentID + ":" + DerivedBODRevision +"?CodeDefinition&verb=Sync&TrackerID="+LocalFSMInboundBODTracker
			
		DerivedBODStatusCode is a DerivedField
			type is Alpha 10
			restricted
			if (Active)
				return "Open"
			else
				return "Closed"
				
		DerivedBankAccountType is a DerivedField
			type is Alpha 60
			restricted
			if(BankAccountValidationType.AccountNorway)
				return "AccountNorway"
			else
			if(BankAccountValidationType.BicCode)
				return "BicCode"
			else
			if(BankAccountValidationType.Check)
				return "Check"
			else
			if(BankAccountValidationType.Fik71)
				return "Fik71"
			else
			if(BankAccountValidationType.Giro04)
				return "Giro04"
			else
			if(BankAccountValidationType.Inpayment73)
				return "Inpayment73"
			else
			if(BankAccountValidationType.Inpayment75)
				return "Inpayment75"
			else
			if(BankAccountValidationType.BankAccountSweden)
				return "BankAccountSweden"
			else
			if(BankAccountValidationType.NordeaFinland)
				return "NordeaFinland"
			else
			if(BankAccountValidationType.NationalClearingSystem)
				return "NationalClearingSystem"
			else
			if(BankAccountValidationType.Plusgiro)
				return "Plusgiro"
			else
			if(BankAccountValidationType.Bankgiro)
				return "Bankgiro"
			else
			if(BankAccountValidationType.RegNbrAccount)
				return "RegNbrAccount"
			else
			if(BankAccountValidationType.SocialSecurityNbr)
				return "SocialSecurityNbr"
			else
			if(BankAccountValidationType.Brazil)
				return "Brazil"
			else
			if(BankAccountValidationType.Iban)
				return "Iban"
			else
			if(BankAccountValidationType.IbanAndBIC)
				return "IbanAndBIC"
			else
				return "StandardAccountNumber"
		
		DerivedPostCompany is a DerivedField
			type is Alpha 4
			restricted
			if(DefaultPostCompany =0)
				return ""
			else
				return DefaultPostCompany
				
		DerivedReturnRate is a DerivedField
			type is Alpha 6
			restricted
			if(ReturnOnInvestmentRate = 0.00)
				return ""
			else
				return ReturnOnInvestmentRate
				
		UserAreaXML is a DerivedField
	    	type is XMLDocument
	        restricted
	        UserAreaXML = template.IONSyncCodeDefination_CashCode_UserArea_ST document for this instance
	        
	    CashCodeXML is a DerivedField
	    	type is XMLDocument
	        restricted
	        CashCodeXML = template.IONSyncCodeDefination_CashCode_CashCode_ST document for this instance

		FundLabel is a DerivedField
			type is Alpha 25
			restricted
			if (CashManagementGroup.FinanceEnterpriseGroup.FundDimension.AccountingUnit)
				return CashManagementGroup.FinanceEnterpriseGroup.AccountingUnitLabel
			else
				return CashManagementGroup.FinanceEnterpriseGroup.FinanceDimension1Label

		ActorSecurityGroup is a DerivedField
			type is like CashCodeSecurityGroup
			return actor.context.CashCodeSecurityGroup

		DerivedEscheatmentNoticeIDMEmailSubject is a DerivedField
			type is Alpha size 255
			restricted
			LocalEscheatmentNoticeIDMEmailSubject		=	EscheatmentNoticeIDMEmailSubject
			return LocalEscheatmentNoticeIDMEmailSubject text

    Conditions
        IsCashLedgerReconciliationAdjustmentExist
        	restricted
            classic name is CBADJUST-UNREL
            when (first CbadjustUnrelRel exists)

        CashLedgerPayablesPaymentsManualFormsReservedRelExists
        	restricted
        	when (CashLedgerPayablesPaymentsManualFormsReservedRel exists)
        
        IsBankAccountBalanceExist
        	restricted
            classic name is CBB-EXISTS
            when (first CbbalanceRel exists)

        IsCashCodeGroupExist
        	restricted
            classic name is CC-GRP-EXISTS
            when (first CbgrpccRel exists)

        IsCashLedgerCashReceiptHeaderExist
        	restricted
            classic name is RECPTHDR-UNREL
            when (first RecpthdrUnrelRel exists)

        IsCashLedgerFundTransferHeaderExist
        	restricted
            classic name is TRFHDR-UNREL
            when (first TrfhdrUnrelRel exists)




		CashCodeGroupDetailRelExists
			restricted
			when (CashCodeGroupDetailRel exists)

		CompanyCashCodeDoesNotExistsForDefaultCompany
			restricted
			when (DefaultPostCompany	entered
			and   !DefaultCompanyCashCodeRel exists)
		
		CompanyCashCodeInactiveForDefaultCompany
			restricted
			when (DefaultPostCompany    entered
			and   !DefaultCompanyCashCodeRel.Active)

 		SecurityGroupAllowsAccess
			when ((actor.context.CashCodeSecurityGroup = "")
			or   (actor.context.CashCodeSecurityGroup != ""
			and   CashCodeSecurityGroupDetailRel exists))

		HasEditGroups
			when (EditGroupRel exists)

		FundAccountingNoEdit
			when (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting
			and   !EditAvailableFunds)

		FundAccountingWithEdit
			when (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting
			and   EditAvailableFunds)
		
		RecordExists
			restricted
			when (CashCode exists)
		
		CurrencyEntered
			restricted
			when (Currency entered)
		
		ApprovalCodeEntered
			restricted
			when (ApprovalCode entered)
			 
    Relations

		CashLedgerManualReconciliationsRel
			one-to-many relation to CashLedgerManualReconciliation
			Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode				= CashCode
                			    
		CashCodeSecurityGroupDetailRel
			one-to-one relation to CashCodeGroupDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup         = actor.context.FinanceEnterpriseGroup
				related.CashCodeGroup				= actor.context.CashCodeSecurityGroup.FinanceDimensionStructure
				related.CashCode                    = CashCode

        CashLedgerTransactionsOpenRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set6
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode
            Instance Selection
                where (related.Status.Open)                
    
        CbadjustRel
            one-to-many relation to CashLedgerReconciliationAdjustment
            Field Mapping uses Set2
            	related.CashCode	= CashCode

        CbadjustUnrelRel
            one-to-many relation to CashLedgerReconciliationAdjustment
            Field Mapping uses Set2
                related.CashCode = CashCode
                related.Status   = blank

        CbbalanceRel
            one-to-many relation to BankAccountBalance
            delete restricted
            Field Mapping uses Set2
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode

        CbcashformRel
            one-to-many relation to CashPaymentFormat
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

        CbcheckRel
            one-to-many relation to CashLedgerPayablesPayment
            Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup 	           
                related.CashCode 				= CashCode

        CbcpycashRel
            one-to-many relation to CompanyCashCode
            Field Mapping uses Set2
                related.CashCode 	= CashCode

        CompanyCashCodeRel
            one-to-one relation to CompanyCashCode
            Field Mapping uses symbolic key
                related.Company		= ContextCompany
                related.CashCode 	= CashCode
                
        DefaultCompanyCashCodeRel
            one-to-one relation to CompanyCashCode
            Field Mapping uses symbolic key
                related.Company		= DefaultPostCompany
                related.CashCode 	= CashCode
        
        FromCompanyCashCodeRel
            one-to-one relation to CompanyCashCode
            Field Mapping uses symbolic key
                related.Company		= FromCompany
                related.CashCode 	= CashCode
        
        ToCompanyCashCodeRel
            one-to-one relation to CompanyCashCode
            Field Mapping uses symbolic key
                related.Company		= ToCompany
                related.CashCode 	= CashCode
                        
        CbgrpccRel
            one-to-many relation to CashCodeGroupDetail
            Field Mapping uses Set2
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

        CbtransRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set6
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode

        CbtransDateRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set7
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode

        CbtrfhdrRel
            one-to-many relation to CashLedgerFundTransferHeader
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

        RecpthdrUnrelRel
            one-to-many relation to CashLedgerCashReceiptHeader
            Field Mapping uses Set2
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

        TrfhdrUnrelRel
            one-to-many relation to CashLedgerFundTransferHeader
            Field Mapping uses Set2
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

        UnrelTrfhdrRel
            one-to-many relation to CashLedgerFundTransferHeader
            Field Mapping uses Set2
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

        BankFundTransferRel
            one-to-one relation to CashCodeBankTransfer
            Field Mapping uses symbolic key
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode 				= CashCode

		CashLedgerPayablesPaymentsManualFormsReservedRel
			one-to-many relation to CashLedgerPayablesPayment
			Field Mapping uses Set4
				related.CashManagementGroup		= CashManagementGroup
				related.CashCode				= CashCode
			Instance Selection
				where (related.Status.ReservedForManualPayment)

		CashLedgerPayablesPaymentsRel
			one-to-many relation to CashLedgerPayablesPayment
			Field Mapping uses Set4
				related.CashManagementGroup								= CashManagementGroup
		        related.CashCode										= CashCode
		        related.CashLedgerPayablesPayment.BankTransactionCode	= LocalBankTransactionCode
		        related.TransactionNumber								= LocalTransactionNumber
		        










		CashLedgerConsolidationCashCodeRel
			one-to-one relation to CashLedgerConsolidation
			Field Mapping uses symbolic key  
				related.CashManagementGroup 								= CashManagementGroup	
				related.CashCode 											= CashCode
				related.CashLedgerConsolidation.BankTransactionCode 		= LocalBankTransactionCode
				related.CashLedgerConsolidation.Company						= LocalCompany  	
				related.CashLedgerConsolidation.LastMaintenanceDate			= current corporate date

        CashLedgerConsolidationsPreviousDayRel
            one-to-many relation to CashLedgerConsolidation
            Field Mapping uses Set5
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode				= CashCode
			Instance Selection            				
                where (related.CashLedgerConsolidation.LastMaintenanceDate 	< current corporate date
                and    related.CashLedgerConsolidation.BankTransactionCode 	!entered
				and    related.CashLedgerConsolidation.Company 				!entered)

        CashCodeCashPositionRel
            one-to-many relation to CashCodeCashPosition
            Field Mapping uses ByCashCodeDescendingDate
            	related.CashManagementGroup 	= CashManagementGroup	
                related.CashCode   				= CashCode
			Instance Selection
                where (related.ClosedPeriodBalance)

        PreviousBankAccountBalanceRel
            one-to-many relation to BankAccountBalance
            delete restricted
            Field Mapping uses Set2
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode
			Instance Selection
				where (related.BankAccountBalance	< LocalCashPositionDate)

        CashLedgerTransactionByPeriodRel
            one-to-many relation to CashLedgerTransaction
            Field Mapping uses Set2
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode
                related.PeriodDate				= first PreviousBankAccountBalanceRel.BankAccountBalance

        BankAccountBalanceRangeRel
            one-to-many relation to BankAccountBalance
            Field Mapping uses symbolic key
            	related.CashManagementGroup		= CashManagementGroup	
                related.CashCode 				= CashCode
			Instance Selection
				where (related.BankAccountBalance	> first CashCodeCashPositionRel.CashCodeCashPosition)

		GeneralLedgerClosePeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses ByEndDate
				related.FinanceEnterpriseGroup		  	= CashManagementGroup
				related.GeneralLedgerCloseConfiguration = BankAccountNumber.LegalEntity.AccountingEntity.CloseConfiguration
			Instance Selection
				where (related.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date >= LocalCashPositionDate)

		CashCodeGroupDetailRel
			one-to-one relation to CashCodeGroupDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup	= CashManagementGroup
				related.CashCodeGroup		= CashCodeGroup
				related.CashCode			= CashCode

		EditGroupRel
			one-to-many relation to CashCodeFund
			Field Mapping uses ByEditLevel
				related.CashManagementGroup = CashManagementGroup
				related.CashCode 			= CashCode
				related.EditLevel			= 1 


		FSMBODConfigurationParameterRel
    		one-to-one relation to FSMBODConfigurationParameter
    		Field Mapping uses symbolic key
    			related.FSMBODConfigurationParameter	= LocalConfigurationParameter
		
		FSMBODConfigurationRel
        	one-to-one relation to FSMBODConfiguration
			Field Mapping uses symbolic key
            	related.FSMBODConfiguration.Verb 		= 1
            	related.FSMBODConfiguration.Noun 		= "CodeDefinition"
            	related.FSMBODConfiguration.Direction 	= 1
		
		FSMInboundBODTrackerRel
            one-to-one relation to FSMInboundBODTracker
            Field Mapping uses symbolic key
                related.FSMInboundBODTracker	= LocalFSMInboundBODTracker


		LocalCompanyCashCodeRel						
			one-to-one relation to CompanyCashCode
				Field Mapping uses symbolic key
					related.Company = LocalCompany
					related.CashCode = CashCode
					
	Form Invokes
        BankFundTransferDetails
            if (BankFundTransferRel exists)
                invoke Update BankFundTransferRel
            else
                invoke Create BankFundTransferRel

    Sets
        Set3
            indexed
            Sort Order
            	CashManagementGroup	
                FinancialInstitution
                FinancialInstitutionBranch
                BankAccountNumber
                CashCode

		ByCurrentBalance
			indexed
			Sort Order
				CashManagementGroup
				CurrentReconciledBalance descending
				CashCode

        ByBankAccount
            indexed
            Sort Order
            	CashManagementGroup	
                BankAccountNumber
                CashCode
                
    Field Rules
        FinancialInstitution
        	initial value is BankAccountNumber.FinancialInstitution
        	force default to BankAccountNumber.FinancialInstitution
        	
        FinancialInstitutionBranch
            initial value is BankAccountNumber.FinancialInstitutionBranch
            force default to BankAccountNumber.FinancialInstitutionBranch
            
        SendingFinancialInstitution
        	initial value is BankAccountNumber.SendingFinancialInstitution
        	force default to BankAccountNumber.SendingFinancialInstitution
        	
        SendingFinancialInstitutionBranch
        	initial value is BankAccountNumber.SendingFinancialInstitutionBranch
        	force default to BankAccountNumber.SendingFinancialInstitutionBranch
        	
        BankAccountNumber
            required


			constraint (BankAccountNumber.FinancialInstitutionBranch	= FinancialInstitutionBranch)
				"CashManagementAccountNotValidForFinancialInstitutionBranch"
			if (BankAccountNumber changed
			and CashCode	= old BankAccountNumber.CashCode)
				cannot be changed
					"CashCodeIsUsedForPostingCashCodeOnAccount<old BankAccountNumber>;CannotBeChanged"

		BankAccountValidationType
			force default to BankAccountNumber.BankAccountValidationType
									
        Currency
        	force default to BankAccountNumber.Currency
            required
            
        CurrencyNumberOfDecimals
			force default to Currency.NumberOfDecimals        	

        Active
            default to true

		Description
			default to FinancialInstitutionBranch













		ReconciliationStatement
			default to false
			
		ApprovalCode






			constraint (!ApprovalCode.ApprovalCodeUsesMaximumAmounts)
				"CannotSelectApprovalCodesSetUpForCashLedgerPaymentApproval"

		Sepa
			force default to BankAccountNumber.Sepa
			
		SwiftID
			force default to BankAccountNumber.SwiftID
			
		EditAvailableFunds
			if (EditAvailableFunds)
				constraint (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting)
					"MustBeUsingFundAccountingToEditAvailableFunds"
			constraint (!RestrictedFunds)
				"CannotSelectBothEditAvailableFundsAndRestrictedFunds"		

		RestrictedFunds
			if (RestrictedFunds)
				constraint (CashManagementGroup.FinanceEnterpriseGroup.FundAccounting)
					"MustBeUsingFundAccountingToRestrictedFunds"

		EscheatmentNoticeIDMTemplate
			if (UseIDM)
				required
				constraint (EscheatmentNoticeIDMTemplate.IDMDocumentType.EscheatmentNotice)
					"InvalidTemplate"

		EscheatmentNoticeIDMEmailAddress
			initial value is config.DefaultFromEmailAddress
				when (UseIDM)
			if (UseIDM)
				default to config.DefaultFromEmailAddress

		EscheatmentNoticeIDMEmailSubject
			initial value is "Escheatment Notice {CashCode} {PayeeName} as of {ToIssueDate}"
				when (UseIDM)
			if (UseIDM)
				default to "Escheatment Notice {CashCode} {PayeeName} as of {ToIssueDate}"
	Rule Blocks	
		TriggerCashCodeServiceRules
    		trigger "CashCodeService" PA service
				resume on error
				title is "EG:<CashManagementGroup>CashCode:<CashCode>"
				Criteria
					CashManagementGroup
				Variables
					BODActionCode
					CashManagementGroup
					include persistent fields from CashCode
					LocalBODCurrentTimeStamp.OutputBODCurrentTimeStamp
						variable name is CurrentTimeStamp
						
	Actions
		Create is a Create Action

		Update is an Update Action

		BypassRulesUpdate is an Update Action
			default label is untranslatable
			restricted
			refresh and lock this instance
			bypass field rules

		Delete is a Delete Action
			Entrance Rules
				constraint (CbgrpccRel	!exists)
					"CashCodeBelongsToOneOrMoreCashCodeGroups;CannotDelete"
				if (CashManagementGroup.BODTrigger)
					BODActionCode	= BODActionCode.Delete
					increment bod id.VariationID
					include TriggerCashCodeServiceRules


		InitiateCashLedgerProcess is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				CashLedgerService
				Criterion1	is Alpha size 15	
				Criterion2	is Alpha size 15
				Criterion3	is Alpha size 15
	            PayablesCompany
	            PayablesInvoice
				PayablesInvoicePayment
				CashCode
				CashLedgerSourceRecord
				CashLedgerPayablesPayment
				CashLedgerPayment
				BankTransactionCode
				CashPaymentFormat
				BankEntity
				CashLedgerCashReceiptHeader
				CashLedgerTransaction
				Document								is Alpha size 20
				Batch									is Alpha size 14
				SourceCode								is Alpha size 2
				SystemCode								is a GeneralLedgerSystemCode

				ActionCode								is Alpha size 1
					States
						Create  value is "C"
						Update	value is "U"
						Delete	value is "D"
				ResumeOnError							is Boolean
			Parameter Rules
				CashLedgerService
					required
			Local Fields
				LocalTitle 				is Alpha size 255
			
			Action Rules
				
				LocalTitle = CashLedgerService

				if (ResumeOnError)
					trigger CashLedgerService PA service
						resume on error
						title is "<LocalTitle>"
						Criteria
							Criterion1
							Criterion2
							Criterion3
						Variables
							ActionCode
							CashLedgerSourceRecord
							include persistent fields from CashCode
							include persistent fields from CashLedgerPayablesPayment
							include persistent fields from CashLedgerPayment
							include persistent fields from BankTransactionCode
							include persistent fields from CashPaymentFormat
							include persistent fields from BankEntity
							include persistent fields from CashLedgerCashReceiptHeader

							include persistent fields from CashLedgerTransaction
							NetBalance
								variable name is CashCodeNetBalance
							include persistent fields from PayablesCompany
							include persistent fields from PayablesCompany.GeneralLedgerCompany
							include persistent fields from PayablesInvoice
							include persistent fields from PayablesInvoicePayment

							PayablesInvoicePayment.OpenBasePayment
								variable name is PayablesInvoicePaymentOpenBasePayment
							PayablesInvoicePayment.OpenTransactionPayment
								variable name is PayablesInvoicePaymentOpenTransactionPayment
							PayablesInvoicePayment.BaseNetPayment
								variable name is PayablesInvoicePaymentBaseNetPayment
							PayablesInvoicePayment.TransactionNetPayment
								variable name is PayablesInvoicePaymentTransactionNetPayment
							PayablesInvoicePayment.PaymentStatus
								variable name is PayablesInvoicePaymentPaymentStatus
							PayablesInvoicePayment.BankStatus
								variable name is PayablesInvoicePaymentBankStatus
							PayablesInvoicePayment.NoReinstate
								variable name is PayablesInvoicePaymentNoReinstate
							PayablesInvoicePayment.PaidDocumentDate
								variable name is PayablesInvoicePaymentPaidDocumentDate
							PayablesInvoicePayment.DraftAccrued
								variable name is PayablesInvoicePaymentDraftAccrued
							PayablesInvoicePayment.ApInvoiceDate
								variable name is PayablesInvoicePaymentApInvoiceDate
							PayablesInvoicePayment.ApPoNumber
								variable name is PayablesInvoicePaymentApPoNumber
							PayablesInvoicePayment.ApInvoiceType
								variable name is PayablesInvoicePaymentApInvoiceType
							PayablesInvoicePayment.ApBaseInvoice
								variable name is PayablesInvoicePaymentApBaseInvoice
							PayablesInvoicePayment.ApFormatNumber
								variable name is PayablesInvoicePaymentApFormatNumber

							include persistent fields from PayablesInvoicePayment.InvoiceCurrency.Currency
							Document
							Batch
							SourceCode
							SystemCode
							CurrentDate
				else
					trigger CashLedgerService PA service
						title is "<LocalTitle>"
						Criteria
							Criterion1
							Criterion2
							Criterion3
						Variables
							ActionCode
							include persistent fields from CashCode
							include persistent fields from CashLedgerPayablesPayment
							include persistent fields from CashLedgerPayment
							include persistent fields from BankTransactionCode
							include persistent fields from CashPaymentFormat
							include persistent fields from BankEntity
							include persistent fields from CashLedgerCashReceiptHeader

							include persistent fields from CashLedgerTransaction
							NetBalance
								variable name is CashCodeNetBalance
							include persistent fields from PayablesCompany
							include persistent fields from PayablesCompany.GeneralLedgerCompany
							include persistent fields from PayablesInvoice
							include persistent fields from PayablesInvoicePayment

							PayablesInvoicePayment.OpenBasePayment
								variable name is PayablesInvoicePaymentOpenBasePayment
							PayablesInvoicePayment.OpenTransactionPayment
								variable name is PayablesInvoicePaymentOpenTransactionPayment
							PayablesInvoicePayment.BaseNetPayment
								variable name is PayablesInvoicePaymentBaseNetPayment
							PayablesInvoicePayment.TransactionNetPayment
								variable name is PayablesInvoicePaymentTransactionNetPayment
							PayablesInvoicePayment.PaymentStatus
								variable name is PayablesInvoicePaymentPaymentStatus
							PayablesInvoicePayment.BankStatus
								variable name is PayablesInvoicePaymentBankStatus
							PayablesInvoicePayment.NoReinstate
								variable name is PayablesInvoicePaymentNoReinstate
							PayablesInvoicePayment.PaidDocumentDate
								variable name is PayablesInvoicePaymentPaidDocumentDate
							PayablesInvoicePayment.DraftAccrued
								variable name is PayablesInvoicePaymentDraftAccrued
							PayablesInvoicePayment.ApInvoiceDate
								variable name is PayablesInvoicePaymentApInvoiceDate
							PayablesInvoicePayment.ApPoNumber
								variable name is PayablesInvoicePaymentApPoNumber
							PayablesInvoicePayment.ApInvoiceType
								variable name is PayablesInvoicePaymentApInvoiceType
							PayablesInvoicePayment.ApBaseInvoice
								variable name is PayablesInvoicePaymentApBaseInvoice
							PayablesInvoicePayment.ApFormatNumber
								variable name is PayablesInvoicePaymentApFormatNumber

							include persistent fields from PayablesInvoicePayment.InvoiceCurrency.Currency
							Document
							Batch
							SourceCode
							SystemCode
							CurrentDate

		ReserveManualChecks is an Instance Action
			completion message is "<CompletionMessage>"
			Parameters
				PrmVendorGroup				is a VendorGroup
        		PrmPayGroup     			is a PayGroup
        		PrmBankTransactionCode		is a BankTransactionCode
        		PrmBeginPaymentRange		is a TransactionNumber
				PrmEndOfPaymentRange		is a TransactionNumber
 
 			Parameter Rules
 				PrmVendorGroup
 					required
 						"VendorGroupIsRequired"
		 		PrmPayGroup
		 			required
		 				"PayGroupIsRequired"
				PrmBankTransactionCode
		 			required
		 				"BankTransactionCodeIsRequired"
 				PrmBeginPaymentRange
		 			required
		 				"PaymentDateRangeIsRequired"
		 			constraint (PrmBeginPaymentRange is numeric)
						"PaymentNumberMustBeNumeric"		 					
 				PrmEndOfPaymentRange
 					if (PrmEndOfPaymentRange entered)
		 				constraint (PrmEndOfPaymentRange is numeric)
							"PaymentNumberMustBeNumeric"
					else
						PrmEndOfPaymentRange = PrmBeginPaymentRange	
						
        	Action Rules
				if (DefaultPostCompany not entered)						
					LocalCompany = PrmPayGroup.PostCompany
					constraint (LocalCompanyCashCodeRel exists)
						"CompanyCashCodeRelationshipDoesNotExistForCashCode:<CashCode>AndCompany<PrmPayGroup.PostCompany>"
				LocalPaymentNumber 			= PrmBeginPaymentRange
				LocalEndPaymentNumber		= PrmEndOfPaymentRange
				LocalReserveCount			= LocalEndPaymentNumber - LocalPaymentNumber + 1
				LocalBankTransactionCode	= PrmBankTransactionCode									
				constraint (LocalEndPaymentNumber >= LocalPaymentNumber)
					"BeginningPaymentRange<LocalPaymentNumber>CannotBeGreaterThanEndingRange<LocalEndPaymentNumber>"
				constraint (LocalReserveCount <= 100)
					"CannotReserveMoreThan_100Payments"	
				constraint (PrmBankTransactionCode.ManualPaymentNumberReservation)
					"ManualReservationNotRequiredForThisPaymentCode"	
				
        		while (LocalPaymentNumber <= LocalEndPaymentNumber)
        			LocalTransactionNumber = LocalPaymentNumber
        			constraint (CashLedgerPayablesPaymentsRel not exists)
						"PaymentNumber<LocalTransactionNumber>AlreadyExists"	
					invoke ReserveManualChecks CashLedgerPayablesPayment
						invoked.CashManagementGroup 	= CashManagementGroup
		        		invoked.CashCode				= CashCode
		        		invoked.CashLedgerPayablesPayment.BankTransactionCode	= LocalBankTransactionCode
		        		invoked.TransactionNumber		= LocalTransactionNumber
		        		invoked.VendorGroup				= PrmVendorGroup
		        		invoked.PayGroup				= PrmPayGroup
					increment LocalPaymentNumber
			Exit Rules
				CompletionMessage = ManualFormsReservationMessage
								
		VoidManualChecks is an Instance Action
			completion message is "<CompletionMessage>"
			Parameters
				PrmVendorGroup				is a VendorGroup
        		PrmPayGroup     			is a PayGroup
        		PrmBankTransactionCode		is a BankTransactionCode
        		PrmBeginPaymentRange		is a TransactionNumber
				PrmEndOfPaymentRange		is a TransactionNumber
 
 			Parameter Rules
 				PrmVendorGroup
 					required
 						"VendorGroupIsRequired"
		 		PrmPayGroup
		 			required
		 				"PayGroupIsRequired"
				PrmBankTransactionCode
		 			required
		 				"BankTransactionCodeIsRequired"
 				PrmBeginPaymentRange
		 			required
		 				"PaymentDateRangeIsRequired"
		 			constraint (PrmBeginPaymentRange is numeric)
						"PaymentNumberMustBeNumeric"		 					
 				PrmEndOfPaymentRange
 					if (PrmEndOfPaymentRange entered)
		 				constraint (PrmEndOfPaymentRange is numeric)
							"PaymentNumberMustBeNumeric"
					else
						PrmEndOfPaymentRange = PrmBeginPaymentRange
							
        	Action Rules
				LocalPaymentNumber 			= PrmBeginPaymentRange
				LocalEndPaymentNumber		= PrmEndOfPaymentRange
				LocalVoidCount				= LocalEndPaymentNumber - LocalPaymentNumber + 1
				LocalBankTransactionCode	= PrmBankTransactionCode												
				confirmation required		
					"YouAreAboutToVoid<LocalVoidCount>Payments,AreYouSure?"
				constraint (LocalEndPaymentNumber >= LocalPaymentNumber)
					"BeginningPaymentRange<LocalPaymentNumber>CannotBeGreaterThanEndingRange<LocalEndPaymentNumber>"
				constraint (LocalReserveCount <= 100)
					"CannotVoidMoreThan_100Payments"	
				
        		while (LocalPaymentNumber <= LocalEndPaymentNumber)
        			LocalTransactionNumber = LocalPaymentNumber
					if (CashLedgerPayablesPaymentsRel exists)
						constraint (!CashLedgerPayablesPaymentsRel.Status.ReservedForManualPayment)
							"MustUnreservePayment<LocalTransactionNumber>BeforeVoiding"	 
        				constraint (CashLedgerPayablesPaymentsRel.Status.ReservedForManualPayment)
							"PaymentNumberExistsUseCashLedgerTransactionToVoid"	
					invoke VoidManualChecks CashLedgerPayablesPayment
						invoked.CashManagementGroup 	= CashManagementGroup
		        		invoked.CashCode				= CashCode
		        		invoked.CashLedgerPayablesPayment.BankTransactionCode	= LocalBankTransactionCode
		        		invoked.TransactionNumber		= LocalTransactionNumber
		        		invoked.VendorGroup				= PrmVendorGroup
		        		invoked.PayGroup				= PrmPayGroup
					increment LocalPaymentNumber
			Exit Rules
				CompletionMessage = ManualFormsVoidMessage
																						
		UpdateCashCodeBalances is an Instance Action
			default label is untranslatable
			restricted
			refresh and lock this instance		
			Parameters
        		PrmBankTransactionCode	is a BankTransactionCode
        		PrmCompany				is a GeneralLedgerCompany
				PrmIssuedCount			is like NbrOfTrans
				PrmIssuedAmount			is an InternationalAmount
 
 			Parameter Rules
 				PrmBankTransactionCode
 					required
		 		PrmCompany
		 			required
				PrmIssuedCount
		 			required
 				PrmIssuedAmount
		 			required
							
        	Action Rules
				if (PrmBankTransactionCode.AutomaticReconciliation)
					CurrentReconciledBalance	-= PrmIssuedAmount	
				else
					UnreconciledBalance			-= PrmIssuedAmount
					UnreconciledRecordCount		+= PrmIssuedCount		
				
				if (SummarizeLedgerBalances)
					initialize LocalBankTransactionCode
					initialize LocalCompany
					if (CashLedgerConsolidationCashCodeRel	!exists)
						invoke ForwardPreviousDayBalances first CashLedgerConsolidationsPreviousDayRel
					LocalBankTransactionCode	= PrmBankTransactionCode
					LocalCompany				= PrmCompany
					if (CashLedgerConsolidationCashCodeRel exists)
						invoke UpdateCurrentDayBalances CashLedgerConsolidationCashCodeRel
							invoked.PrmIssuedCount		= PrmIssuedCount
							invoked.PrmIssuedAmount		= PrmIssuedAmount
					else
						invoke CreateCurrentDayBalance CashLedgerConsolidation
							invoked.CashManagementGroup							= CashManagementGroup
							invoked.CashCode									= CashCode
							invoked.CashLedgerConsolidation.BankTransactionCode	= PrmBankTransactionCode
							invoked.CashLedgerConsolidation.Company				= PrmCompany
							invoked.CashLedgerConsolidation.LastMaintenanceDate	= current corporate date
							invoked.LastMaintenanceTime 						= current timestamp
							invoked.IssuedAmount								= PrmIssuedAmount
							invoked.IssuedCount									= PrmIssuedCount
						

		UpdateCashCodeCashPosition is a Set Action		
			Parameters
				PrmCashManagementGroup		is a CashManagementGroup
				PrmCashManagementAccount	is a CashManagementAccount
					default label is "CashManagementAccount"
				PrmCashCode					is a CashCode
					default label is "CashCode"
				CashCodeGroup
					default label is "CashCodeGroup"
				PrmCashPositionDate			is Date
					default label is "CashPositionDate"
				PrmAllPeriodTransInPostedBalance	is Boolean
					default label is "IncludeAllPeriodTransactionsInPostedBalance"

			Parameter Rules
				PrmCashManagementGroup
					required
				PrmCashManagementAccount
					if (PrmCashCode		!entered
					and CashCodeGroup	!entered)
						required
							"CashManagementAccountOrCashCodeOrCashCodeGroupRequired"
					constraint (PrmCashCode		!entered
					and         CashCodeGroup	!entered)
						"SelectOnlyCashManagementAccountOrCashCodeOrCashCodeGroup"
				PrmCashCode 	 
					constraint (PrmCashManagementAccount	!entered
					and         CashCodeGroup			!entered)
						"SelectOnlyCashManagementAccountOrCashCodeOrCashCodeGroup"
				PrmCashPositionDate		 
					required
					initial value is current corporate date
					default to current corporate date
											
			Local Fields
				
			Instance Selection
				where (CashManagementGroup		= PrmCashManagementGroup
				and   (PrmCashManagementAccount entered
				and    BankAccountNumber		= PrmCashManagementAccount
				or    (PrmCashCode 				entered
				and	   CashCode					= PrmCashCode)
				or    (CashCodeGroup			entered
				and    CashCodeGroup 			= CashCodeGroup
				and    CashCodeGroupDetailRelExists)))

			Sort Order

				
			Action Rules								
				Instance Rules
					invoke RunCashCodeCashPosition
						invoked.PrmCashPositionDate					= PrmCashPositionDate
						invoked.PrmAllPeriodTransInPostedBalance	= PrmAllPeriodTransInPostedBalance

		RunCashCodeCashPosition is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmCashPositionDate					is Date
				PrmAllPeriodTransInPostedBalance	is Boolean
 
 			Parameter Rules
				PrmCashPositionDate
		 			required
							
        	Action Rules
        		LocalCashPositionDate	= PrmCashPositionDate
				if  (CashCodeCashPositionRel 		!exists
				and  PreviousBankAccountBalanceRel	exists
				and  !first PreviousBankAccountBalanceRel.HasRelatedCashCodeCashPosition)
					if (CashLedgerTransactionByPeriodRel exists)
						LocalCompany	= first CashLedgerTransactionByPeriodRel.Company.OrigCompany
					else
						LocalCompany	= first CbcpycashRel.Company
					invoke Create CashCodeCashPosition
						invoked.CashManagementGroup						= CashManagementGroup
						invoked.CashCode								= CashCode
						invoked.Company									= LocalCompany
						invoked.CashCodeCashPosition					= first PreviousBankAccountBalanceRel.BankAccountBalance
						invoked.CashManagementAccount					= BankAccountNumber
						invoked.ClosedPeriodBalance						= true
						invoked.LastPeriodBalance.TransactionAmount		= first PreviousBankAccountBalanceRel.BeginningBalance
    					invoked.NetPeriodAmount.TransactionAmount		= first PreviousBankAccountBalanceRel.TransactionBalance
    					invoked.DebitPeriodAmount.TransactionAmount		= first PreviousBankAccountBalanceRel.DebitBalance
    					invoked.CreditPeriodAmount.TransactionAmount	= first PreviousBankAccountBalanceRel.CreditBalance
						invoked.EndingBalance.TransactionAmount			= first PreviousBankAccountBalanceRel.EndingBalance
				else
	        		if (first CbbalanceRel.BankAccountBalance > first CashCodeCashPositionRel.CashCodeCashPosition)
	        			for each BankAccountBalanceRangeRel
							LocalCashPositionDate	= each.BankAccountBalance
	        				LocalPostingDate		= first GeneralLedgerClosePeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date
							invoke UpdateCashCodeCashPosition CashLedgerTransaction
								run after AsyncId
								assign async action request id to AsyncId
								invoked.PrmCashManagementGroup	= CashManagementGroup
								invoked.PrmCashCode				= CashCode
								invoked.PrmCashPositionDate		= each.BankAccountBalance
								invoked.PrmPostingPeriodDate	= LocalPostingDate
			Exit Rules
				LocalCashPositionDate	= PrmCashPositionDate
				if (PrmAllPeriodTransInPostedBalance)
					LocalPostingDate		= first GeneralLedgerClosePeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod.Date
				else
					LocalPostingDate		= PrmCashPositionDate
				invoke UpdateCashCodeCashPosition CashLedgerTransaction
					run after AsyncId
					invoked.PrmCashManagementGroup	= CashManagementGroup
					invoked.PrmCashCode				= CashCode
					invoked.PrmCashPositionDate		= PrmCashPositionDate
					invoked.PrmPostingPeriodDate	= LocalPostingDate
		
		UpdateOutstandingBalanceAndCount is an Instance Action		
			Action Rules
				confirmation required
					"ThisActionWillUpdateTheOutstandingBalanceAndCountWithCurrentCountsOfOpenCashLedgerTransactions;Continue?"
				initialize UnreconciledBalance
				initialize UnreconciledRecordCount
				for each CashLedgerTransactionsOpenRel
					if (each.BankTransactionSign.BankDebit)
						UnreconciledBalance	-= each.IssuedBankAmount
					else
						UnreconciledBalance	+= each.IssuedBankAmount
					UnreconciledRecordCount	+= 1

		UpdateBODIdFields is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules
				if (bod id.DocumentID != PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision != PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord != PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
		
		SendCashCodeBODNativeLPL is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				send ion bod
					bod is CashCodeXML
					bod type is "Sync.CodeDefinition"
					document id is DerivedDocumentID
					variation id is	BODVariationID
		
		TriggerCashCodeNativeLPLBOD is an Instance Action
			restricted
			Entrance Rules
			Parameters
			Action Rules
				invoke NativeLPLBODTriggerChecks FSMBODConfigurationRel
					invoked.PrmVerb 					= 1
					invoked.PrmNoun						= "CodeDefinition"
					invoked.PrmDirection				= 1
					invoked.PrmTriggerFrom				= "CashCode"
					invoked.PrmFinanceEnterpriseGroup	= CashManagementGroup
					invoked.PrmMainUserTemplate			= "IONSyncCodeDefination_CashCode_CashCode_ST"
				LocalNativeLPLBODTrigger = FSMBODConfigurationRel.NativeLPLBODTrigger
				LocalBODTrigger = true
				if(CashManagementGroup.BODTrigger and LocalNativeLPLBODTrigger)
					if(FSMInboundBODTracker not entered)
						invoke Create FSMInboundBODTracker
							assign result to NewBODTracker
							invoked.Verb 					= 1
							invoked.Noun 					= "CodeDefinition"					
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.FinanceEnterpriseGroup	= CashManagementGroup
							invoked.Direction				= 1
							invoked.Reference2				= "CashCode"
							invoked.Reference3				= CashCode
							initialize invoked.Error			
							initialize invoked.ErrorMessage					
						LocalFSMInboundBODTracker	= NewBODTracker.FSMInboundBODTracker
					else 
						LocalFSMInboundBODTracker		= FSMInboundBODTracker
						invoke Update FSMInboundBODTrackerRel
							invoked.FinanceEnterpriseGroup	= CashManagementGroup
							invoked.BODDocumentID			= DerivedDocumentID
							invoked.BODVariationID			= BODVariationID
							invoked.Status					= 1
							invoked.StartDate				= system current timestamp
							invoked.Direction				= 1
							invoked.Reference2				= "CashCode"
							invoked.Reference3				= CashCode
							initialize invoked.Error			
							initialize invoked.ErrorMessage
					invoke SendCashCodeBODNativeLPL
						resume on error
	                   		Error            							= true
	                        ErrorMessage     							= error message
	                if(Error)
						invoke Update FSMInboundBODTrackerRel
							invoked.Error 								= Error
							invoked.ErrorMessage 						= ErrorMessage
							invoked.Status								= 2
							invoked.CloseDate							= system current timestamp
							invoked.BODXML								= CashCodeXML
							invoked.BODID								= DerivedBODFormattedId
					else
						invoke Update FSMInboundBODTrackerRel
							invoked.Status								= 3
							invoked.CloseDate							= system current timestamp
							invoked.BODID								= DerivedBODFormattedId
							invoked.BODXML								= CashCodeXML	
					
		BODDataInitialLoadCashCodeCriteria is a Set Action
			default label is untranslatable
			restricted
			Parameters
				PrmCashManagementGroup			is a CashManagementGroup
				PrmFromCashCode					is a CashCode
				PrmToCashCode					is a CashCode
			Parameter Rules
			Instance Selection			
				where  (((PrmCashManagementGroup entered
				and		 PrmCashManagementGroup = CashManagementGroup)
				or		 PrmCashManagementGroup not entered)
				and    ((PrmFromCashCode entered
				and		 PrmFromCashCode <= CashCode)
				or		 PrmFromCashCode not entered)
				and    ((PrmToCashCode entered
				and		 PrmToCashCode >= CashCode)
				or		 PrmToCashCode not entered))
			Sort Order
			Action Rules								
				Instance Rules	
					if (CashManagementGroup.BODTrigger)
						BODActionCode = BODActionCode.Create				
						increment bod id.VariationID
						include TriggerCashCodeServiceRules

					
					
	Action Exit Rules
		if (CashManagementGroup.BODTrigger and !LocalBODTrigger)
			if (!action type.Delete)
				if (action != "UpdateBODIdFields") 
					BODActionCode	= BODActionCode.Update
					if (action type.Create)
						BODActionCode	= BODActionCode.Create				
					else
						if (action type.Delete)
							BODActionCode	= BODActionCode.Delete
					increment bod id.VariationID
					include TriggerCashCodeServiceRules	

