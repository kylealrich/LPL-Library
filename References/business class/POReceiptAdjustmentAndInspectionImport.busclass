POReceiptAdjustmentAndInspectionImport is a BusinessClass
    owned by ma
    prefix is MRJ
    sql name is PORAAndInspectionImport
    classic name is MACRECADJ

    Ontology
        symbolic key is POReceiptAdjustmentAndInspectionImport
            classic set name is MRJSET1
            classic name for POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber is REC-REF-NBR
            classic name for POReceiptAdjustmentAndInspectionImport.DocumentType is DOC-TYPE
            classic name for POReceiptAdjustmentAndInspectionImport.LineNumber is LINE-NBR

    Patterns
        implements ContextualParent
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

    Persistent Fields
		RunGroup
        DocumentNumberNumeric
            classic name is DOC-NBR-NUM
        AdjustmentReferenceNumber  is a ReferenceNumber
            classic name is ADJ-REF-NBR
        PurchaseOrderReceiptImport is like PurchaseOrderReceiptImport
            classic name is REFERENCE-NO
        PurchaseOrderReceipt
            classic name is REC-NUMBER
        OriginalReferenceNumber    is a ReferenceNumber
            classic name is ORIG-REF-NO
        PurchaseOrder
        Location                   is an InventoryLocation
        Description
        MatchDetailKey
        VendorItem				   is like VendorItem
            classic name is VEN-ITEM
        ItemType
        OperatorID                 is an Operator 
        	holds pii
            classic name is OPER-ID
        TransactionDate            is TimeStamp
            classic name is TRANS-DATE
        AdjustedQuantity           is a Quantity
            classic name is ADJ-QTY
        OriginalUnitCost           is an InternationalCost
            classic name is ORIG-UNIT-CST
        ReceivedUOM                is a UnitOfMeasure
            classic name is ENT-REC-UOM
        ReceivedUOMMultiplier      is an UOMMultiplier
            classic name is ENT-RUOM-MULT
        CatchWeightQuantity        is a Quantity
            classic name is CATCH-WT-QTY
        Bin
        RecComments                is an ErsComments
        Status                     is Numeric size 1
        UpdateDate                 is TimeStamp
        VendorPriceUOMQuantity     is a Quantity
            classic name is VPRI-QUANTITY
        VendorPriceUOM             is a UnitOfMeasure
            classic name is VPRI-UOM
        VendorPriceUOMMultiplier   is an UOMMultiplier
            classic name is VPRI-UOM-MULT
        VendorBuyUOM               is a UnitOfMeasure
            classic name is VBUY-UOM
        VendorBuyUOMMultiplier     is an UOMMultiplier
            classic name is VBUY-UOM-MULT
        TaxCode
        TaxUsageCode
            classic name is TAX-USAGE-CD
        RecordInError              is Boolean
            classic name is RECORD-ERROR
        BinGLN                     is like GlobalLocationNumber
        GlobalDocumentType
            classic name is GLBL-DOC-TYPE
        GlobalLineType
            classic name is GLBL-LINE-TYPE
        DistributionAccount        is a FinanceCodeBlock
            classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
            classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT
		ErrorMessage				is Alpha 150
        AdjustmentAndInspection		is like POReceiptAdjustmentAndInspection
        AdjustmentAndInspectionLine is like POReceiptAdjustmentAndInspectionLine
		PurchaseOrderReceiptLine
		InterfaceRun				is a PurchaseOrderReceiptInterfaceResult
			delete ignored          

	Local Fields
    	InterfacedAdjustmentAndInspection		is a POReceiptAdjustmentAndInspection view
    	InterfacedAdjustmentAndInspectionLine	is a POReceiptAdjustmentAndInspectionLine view
    	InterfacedReceiptLine					is a PurchaseOrderReceiptLine view
    	
		LocalInterfaceResult 					is a PurchaseOrderReceiptInterfaceResult


						
		ErrorOccurred							is Boolean
		LocalErrorMessage						is Alpha 150
		LocalErrorRunGroup						is a RunGroup


		LocalPurchaseOrderReceipt				is like PurchaseOrderReceipt
		
	Transient Fields
		TransientDocumentType					is AlphaUpper size 2
	        States
	            ReceivingAdjustment         value is "RA"
	            LineOnlyAddition            value is "LO"
		TransientReceiptReferenceNumber			is a ReferenceNumber
	Derived Fields
		DetailCount is a DerivedField
			type is Numeric 10 
			return instance count of POReceiptInventoryDetailImportRel 
		DerivedRecordInError is a DerivedField
			type is Alpha size 3
			default label is "RecordInError"
			if (RecordInError)
				return "Yes"
			else
				return "No"
	Field Rules
		RunGroup
			required
		AdjustmentReferenceNumber
			required
			
	Relations
        POReceiptInventoryDetailImportRel
            one-to-many relation to POReceiptInventoryDetailImport
            Field Mapping uses symbolic key
                related.Company                    					= Company
                related.PurchaseOrderReceiptImport 					= AdjustmentReferenceNumber
			Instance Selection
            	where (related.POReceiptInventoryDetailImport.LineNumber 	= POReceiptAdjustmentAndInspectionImport.LineNumber)

    	PurchaseOrderReceiptByReferenceRel
			one-to-many relation to PurchaseOrderReceipt
			Field Mapping uses Set11
				related.Company									= Company
				related.ReferenceNumber							= POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber

    	ReferenceEqualsReceiptRel
			one-to-one relation to PurchaseOrderReceipt
			Field Mapping uses symbolic key
				related.Company									= Company
				related.PurchaseOrderReceipt					= POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber

    	LocalPurchaseOrderReceiptRel
			one-to-one relation to PurchaseOrderReceipt
			Field Mapping uses symbolic key
				related.Company									= Company
				related.PurchaseOrderReceipt					= LocalPurchaseOrderReceipt

    	AdjustmentAndInspectionRel
			one-to-one relation to POReceiptAdjustmentAndInspection
			Field Mapping uses symbolic key
				related.Company									= Company
				related.AdjustmentInspectionDocumentType		= POReceiptAdjustmentAndInspectionImport.DocumentType
				related.POReceiptAdjustmentAndInspection		= AdjustmentAndInspection

    	AdjustmentAndInspectionLineRel
			one-to-one relation to POReceiptAdjustmentAndInspectionLine
			Field Mapping uses symbolic key
				related.Company									= Company
				related.AdjustmentInspectionDocumentType		= POReceiptAdjustmentAndInspectionImport.DocumentType
				related.POReceiptAdjustmentAndInspection		= AdjustmentAndInspection
				related.POReceiptAdjustmentAndInspectionLine	= AdjustmentAndInspectionLine

    	LocalInterfaceResultsRel
			one-to-one relation to PurchaseOrderReceiptInterfaceResult
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup					= actor.context.FinanceEnterpriseGroup
				related.PurchaseOrderReceiptInterfaceResult		= LocalInterfaceResult

    Sets

        Set2
            indexed
            Sort Order
                RecordInError
                Company
                POReceiptAdjustmentAndInspectionImport

        ByRunGroup
            Sort Order
				RunGroup
                Company
                POReceiptAdjustmentAndInspectionImport

        ByInterfaceRun
            Sort Order
            	InterfaceRun
				RunGroup
                Company
                POReceiptAdjustmentAndInspectionImport
	Conditions
		RecordExists
			restricted
			when (POReceiptAdjustmentAndInspectionImport exists)
		
	Actions
		Create is a Create Action
			Action Rules
				if (POReceiptAdjustmentAndInspectionImport.DocumentType not entered)
					POReceiptAdjustmentAndInspectionImport.DocumentType = TransientDocumentType
					initialize TransientDocumentType
					
				if (POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber not entered)
					POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber = TransientReceiptReferenceNumber
					initialize TransientReceiptReferenceNumber
		
		CreateNoRules is a Create Action
			restricted
			bypass field rules

		Update is an Update Action
			Action Rules
				if (POReceiptAdjustmentAndInspectionImport.DocumentType not entered)
					POReceiptAdjustmentAndInspectionImport.DocumentType = TransientDocumentType
					initialize TransientDocumentType
					
				if (POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber not entered)
					POReceiptAdjustmentAndInspectionImport.ReceiptReferenceNumber = TransientReceiptReferenceNumber
					initialize TransientReceiptReferenceNumber
		
		Delete is a Delete Action
			Entrance Rules
				if  (AdjustmentAndInspection entered)
					confirmation required
						"AdjustmentIsPartiallyInterfacedAndThoseRecordsWillAlsoBeDeleted"
					invoke Unreleased.Delete AdjustmentAndInspectionRel

				invoke Delete POReceiptInventoryDetailImportRel

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules

		DeleteAllTransactionsForRunGroup is a Set Action
			confirmation required
			Parameters
				PrmRunGroup				  is AlphaUpper 30
					default label is "RunGroup"
			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = actor.context.FinanceEnterpriseGroup
				and    AdjustmentAndInspection not entered
				and    PurchaseOrderReceiptLine not entered)
			Action Rules
				Instance Rules
				    invoke FastDelete

 		SetError is an Instance Action
 			restricted
 			Parameters
 				PrmErrorMessage			is Alpha 150
 				PrmInterfaceResult		is like PurchaseOrderReceiptInterfaceResult
			Action Rules
				RecordInError					= true
				ErrorMessage					= PrmErrorMessage
				
				LocalInterfaceResult = PrmInterfaceResult
				
				invoke Update LocalInterfaceResultsRel
					invoked.Status = 2

				if  (LocalInterfaceResultsRel.MoveErrorsToNewRunGroup)
				
					if  (LocalInterfaceResultsRel.ErrorRunGroup not entered)
						LocalErrorRunGroup					= LocalInterfaceResultsRel.ErrorRunGroupPrefix + "ERRORS_" + RunGroup

						invoke Update LocalInterfaceResultsRel
							invoked.ErrorRunGroup			= LocalErrorRunGroup
					else
						LocalErrorRunGroup					= LocalInterfaceResultsRel.ErrorRunGroup
						
					RunGroup								= LocalErrorRunGroup

					
		InterfaceAdjustmentsAndSubstitutions is a Set Action
			Parameters
				PrmFinanceEnterpriseGroup   is an FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmRunGroup					is a RunGroup
					default label is "RunGroup"
				PrmCompany					is a PayablesCompany
				ReleaseReceipts				is Boolean


				IncludeRecordsInError		is Boolean
				MoveErrorsToNewRunGroup  	is Boolean
					default label is "MoveErrorsToNewRunGroup"
				PrmErrorRunGroupPrefix		is AlphaUpper 15
					default label is "ErrorRunGroupPrefix"
				PrmInterfaceRun				is like PayablesInvoiceInterfaceResult

				
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
				PrmRunGroup
					required
						"RunGroupIsRequired"
			Local Fields
				LocalInstanceCount				is Numeric 10
				LocalSetInterfaceResult			is a PurchaseOrderReceiptInterfaceResult view 	
				LocalDetailCount                is Numeric 12
				LocalAdjustmentCount            is Numeric 12
				LocalFailedAdjustmentCount      is Numeric 12

			Instance Selection
				where (RunGroup	= PrmRunGroup
                and    Company.FinanceEnterpriseGroup    = actor.context.FinanceEnterpriseGroup
				and    (PrmCompany            			  not entered
				or      Company	= PrmCompany))

			Sort Order
				RunGroup
				Company
				PurchaseOrderReceiptImport

			Action Rules

				Empty Set Rules
					invoke Create PurchaseOrderReceiptInterfaceResult
						invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
						invoked.RunTime						= current timestamp
						invoked.RunGroup					= PrmRunGroup
						invoked.RunType						= 2
						invoked.Company						= PrmCompany
						invoked.IncludeRecordsInError		= IncludeRecordsInError
				        invoked.ReleaseReceipts				= ReleaseReceipts
						invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
						invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix
						invoked.Status						= 1 
				
				RunGroup Set Rules
					Entrance Rules
						ErrorOccurred							= false
						invoke Create PurchaseOrderReceiptInterfaceResult
							assign result to LocalSetInterfaceResult
							invoked.FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
							invoked.RunTime						= current timestamp
							invoked.RunGroup					= PrmRunGroup
							invoked.RunType						= 2
							invoked.Company						= PrmCompany
							invoked.IncludeRecordsInError		= IncludeRecordsInError
					        invoked.ReleaseReceipts				= ReleaseReceipts
							invoked.MoveErrorsToNewRunGroup		= MoveErrorsToNewRunGroup
							invoked.ErrorRunGroupPrefix			= PrmErrorRunGroupPrefix

					Exit Rules
						invoke Update LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
							invoked.RecordsProcessed					= LocalInstanceCount
							invoked.InterfaceCounts.PassedHeaderCount   = LocalAdjustmentCount         
							invoked.InterfaceCounts.FailedHeaderCount   = LocalFailedAdjustmentCount      
							invoked.InterfaceCounts.DetailCount         = LocalDetailCount
							if (!LocalSetInterfaceResult.Status = 2)
								invoked.Status        			= 1

				Instance Rules
					if   (IncludeRecordsInError
					or    !RecordInError)
						
						initialize ErrorMessage
						InterfaceRun									= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
						RecordInError									= false
						ErrorOccurred									= false
						LocalInterfaceResult 							= LocalSetInterfaceResult.PurchaseOrderReceiptInterfaceResult
						LocalDetailCount                               += DetailCount 
						LocalAdjustmentCount                           += 1 

						initialize ErrorOccurred

						LocalPurchaseOrderReceipt					= first PurchaseOrderReceiptByReferenceRel.PurchaseOrderReceipt
						if (LocalPurchaseOrderReceipt not entered)
							LocalPurchaseOrderReceipt				= ReferenceEqualsReceiptRel.PurchaseOrderReceipt

						if  (POReceiptAdjustmentAndInspectionImport.DocumentType.ReceivingAdjustment)
							if (AdjustmentAndInspection entered)
								invoke FastUpdate AdjustmentAndInspectionRel
									invoked.OriginatingInterfaceRun			= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
							else
								invoke Unreleased.CreateAdjustment POReceiptAdjustmentAndInspection
									assign result to InterfacedAdjustmentAndInspection
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
										except invoked.Status
										
									invoked.Company								= Company
									invoked.AdjustmentInspectionDocumentType	= POReceiptAdjustmentAndInspectionImport.DocumentType
									invoked.ReferenceNumber						= AdjustmentReferenceNumber
									invoked.PurchaseOrderReceipt				= LocalPurchaseOrderReceipt
									invoked.DocumentDate						= TransactionDate
										
									invoked.InterfaceInProcess					= true
									invoked.OriginatingInterfaceRun				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
									invoked.BypassCreateLines					= true

								if (!ErrorOccurred)
									AdjustmentAndInspection						= InterfacedAdjustmentAndInspection.POReceiptAdjustmentAndInspection
									
							if  (!ErrorOccurred)
								if (AdjustmentAndInspectionLine not entered)

									invoke CreateAdjustment Unreleased POReceiptAdjustmentAndInspectionLine
										assign result to InterfacedAdjustmentAndInspectionLine
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
											
										invoked.Company								= Company
										invoked.AdjustmentInspectionDocumentType	= POReceiptAdjustmentAndInspectionImport.DocumentType
										invoked.POReceiptAdjustmentAndInspection	= AdjustmentAndInspectionRel.POReceiptAdjustmentAndInspection
										invoked.ReferenceNumber						= AdjustmentReferenceNumber
										invoked.PurchaseOrderReceipt				= LocalPurchaseOrderReceipt
										invoked.DocumentDate						= TransactionDate
											
										invoked.POReceiptAdjustmentAndInspectionLine = POReceiptAdjustmentAndInspectionImport.LineNumber
										invoked.PurchaseOrderReceiptLine			= POReceiptAdjustmentAndInspectionImport.LineNumber
										if  (PurchaseOrder entered)
											invoked.PurchaseOrder					= PurchaseOrder
										else
											invoked.PurchaseOrder					= LocalPurchaseOrderReceiptRel.PurchaseOrder
										invoked.PurchaseOrderLine					= POReceiptAdjustmentAndInspectionImport.LineNumber
										invoked.UpdateDate							= UpdateDate
										invoked.Quantity							= AdjustedQuantity
										if (POReceiptInventoryDetailImportRel exists)
											invoked.InterfaceHasDetails		= true										
									
									if (!ErrorOccurred)
										AdjustmentAndInspectionLine					= InterfacedAdjustmentAndInspectionLine.POReceiptAdjustmentAndInspectionLine
								if (!ErrorOccurred)

									invoke UpdateAdjustment Unreleased AdjustmentAndInspectionLineRel
										resume on error
											ErrorOccurred		= true
											LocalErrorMessage	= error message
										invoked.Bin									= Bin
										invoked.Quantity							= AdjustedQuantity
										invoked.CatchWeightQuantity					= CatchWeightQuantity
										invoked.Status								= Status

						else
						if  (POReceiptAdjustmentAndInspectionImport.DocumentType.LineOnlyAddition)
							if (PurchaseOrderReceiptLine entered)
								InterfacedReceiptLine						= PurchaseOrderReceiptLine
								invoke FastUpdate PurchaseOrderReceiptLine
									invoked.OriginatingInterfaceRun			= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
							else														
								invoke CreateFromInterface PurchaseOrderReceiptLine
									assign result to InterfacedReceiptLine
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from this instance
										except invoked.Status
										
									invoked.Company								= Company
									invoked.ReferenceNumber						= AdjustmentReferenceNumber
									invoked.PurchaseOrderReceipt				= LocalPurchaseOrderReceipt

									invoked.PurchaseOrderReceiptLine			= POReceiptAdjustmentAndInspectionImport.LineNumber
									if  (PurchaseOrder entered)
										invoked.PurchaseOrder					= PurchaseOrder
									else
										invoked.PurchaseOrder					= LocalPurchaseOrderReceiptRel.PurchaseOrder
									invoked.PurchaseOrderLine					= POReceiptAdjustmentAndInspectionImport.LineNumber
									invoked.EnteredReceivedQuantity				= AdjustedQuantity
										
									invoked.InterfaceInProcess					= true
									invoked.OriginatingInterfaceRun				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
									if (POReceiptInventoryDetailImportRel exists)
										invoked.InterfaceHasDetails		= true								
								
								if (!ErrorOccurred)
									PurchaseOrderReceiptLine					= InterfacedReceiptLine.PurchaseOrderReceiptLine
	
						if (!ErrorOccurred)
							for each POReceiptInventoryDetailImportRel
								invoke Create PurchaseOrderTransactionDetail
									resume on error
										ErrorOccurred		= true
										LocalErrorMessage	= error message
									fill in fields from each

									invoked.PurchaseOrderReceipt											= LocalPurchaseOrderReceipt



									if  (POReceiptAdjustmentAndInspectionImport.DocumentType.ReceivingAdjustment)
										invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= AdjustmentAndInspectionRel.POReceiptAdjustmentAndInspection
										invoked.SourceDocumentAlpha												= AdjustmentAndInspectionRel.POReceiptAdjustmentAndInspection
										invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= "RA"
									else
										invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= LocalPurchaseOrderReceipt
										invoked.SourceDocumentAlpha												= LocalPurchaseOrderReceipt
										invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= "PO"

									invoked.PurchaseOrderTransactionDetail.LineNumber						= each.POReceiptInventoryDetailImport.LineNumber
									invoked.PurchaseOrderTransactionDetail.TransactionSequence				= each.POReceiptInventoryDetailImport.Sequence
									
								if (ErrorOccurred)
									end for each
								else
									invoke FastDelete each
									
						LocalInstanceCount			+= 1

						if (ErrorOccurred)
							invoke SetError
								invoked.PrmErrorMessage					= LocalErrorMessage
								invoked.PrmInterfaceResult				= LocalInterfaceResult.PurchaseOrderReceiptInterfaceResult
						
							LocalAdjustmentCount      	-= 1
							LocalFailedAdjustmentCount 	+= 1
						else
						if (!ErrorOccurred)
							if  (POReceiptAdjustmentAndInspectionImport.DocumentType.ReceivingAdjustment)
								invoke Unreleased.FastUpdate AdjustmentAndInspectionRel
									invoked.InterfaceInProcess				= false
		
								if  (ReleaseReceipts)
									invoke ReleaseAdjustment Unreleased AdjustmentAndInspectionRel
										resume on error
											ErrorOccurred 				= true
											ErrorMessage				= error message
									


	
							else		
							if  (POReceiptAdjustmentAndInspectionImport.DocumentType.LineOnlyAddition)

								invoke FastUpdate InterfacedReceiptLine.PurchaseOrderReceiptLine
									invoked.InterfaceInProcess				= false

								if  (ReleaseReceipts)
									invoke Release InterfacedReceiptLine.PurchaseOrderReceiptLine
										resume on error
											ErrorOccurred 				= true
											ErrorMessage				= error message
									



							invoke FastDelete 
				
