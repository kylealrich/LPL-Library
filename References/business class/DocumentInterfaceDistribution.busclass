DocumentInterfaceDistribution is a BusinessClass
	owned by ma
	prefix is DIDS

	Ontology
		symbolic key is DocumentInterfaceDistribution

    Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated

	Persistent Fields
		DistributionAmount       	is an InternationalAmount
		DistributionReference    	is a Reference
			default label is "Reference"
		Description
		DistributionAccount      is a FinanceCodeBlock
			default label is "FinanceStructure"

		TaxIndicator
		TaxCode
		ProductTaxCategory
		TaxUsageCode
		TaxableAmount				is an InternationalAmount
		TaxRate						is a ChrgRate
		LineType					is AlphaUpper size 1
		AddOnCharge					is like AddOnCharge
		AOCAmount					is an InternationalAmount	
		ICNCode
		StockWeight
			default label is "Weight"
		SupplementaryQuantity		is a Quantity

		HSNSACCode
        UnitAmount                  is an InternationalAmount

        PurchaseOrder               is like PurchaseOrder 
		InterfacedPurchaseOrder		is like PurchaseOrderImport
		InterfacedPOCode			is like POCode
		PurchaseOrderLine 			is like PurchaseOrderLine 
		Contract 					is like Contract 
		ContractLine 				is like ContractLine 
		Item 			            is like Item				
		VendorItem 					is like VendorItem 
	Context Fields	
		DocumentInterfaceDetail 
		DocumentInterfaceAddOnCharge

	Local Fields
    	ImportedDistribution			is a PayablesInvoiceDistribution view
    	LocalDistribution				is like PayablesInvoiceDistribution

		LocalErrorMessage				is Alpha 150

		NextSentence					is Boolean
		
	Derived Fields

	Conditions
		RecordInError
			when (DocumentInterfaceErrorRel exists)
		
		IsHSNSACCodeEnabled
			restricted
			when (DocumentInterfaceInvoice.PayablesCompanyRel.Company.GeneralLedgerCompany.RequireHSNSACCode)
			
	Relations
		DocumentInterfaceErrorRel
			one-to-many relation to DocumentInterfaceError
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice
				related.DocumentInterfaceDetail			= blank
				related.DocumentInterfaceDistribution	= DocumentInterfaceDistribution

		LocalDistributionRel
			one-to-one relation to PayablesInvoiceDistribution
			Field Mapping uses symbolic key
				related.Company							= DocumentInterfaceInvoice.Company
				related.PayablesInvoice					= DocumentInterfaceInvoice.SystemAssignedInvoice
				related.PayablesInvoiceDistribution		= LocalDistribution

		LastDistribRel
			one-to-many relation to DocumentInterfaceDistribution
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.DocumentInterfaceInvoice		= DocumentInterfaceInvoice

		TaxEntityRel		
			one-to-one relation to TaxEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.TaxEntity						= DocumentInterfaceInvoice.PayablesCompanyRel.Company.AccountingEntity

		EntityTaxCodeRel		
			one-to-one relation to EntityTaxCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= FinanceEnterpriseGroup
				related.TaxEntity						= DocumentInterfaceInvoice.PayablesCompanyRel.Company.AccountingEntity
				related.TaxCode							= TaxCode

	Sets
				
	Field Rules

	Rule Blocks

	Actions
		Create is a Create Action
			bypass field rules
			Entrance Rules
				DocumentInterfaceDistribution = last LastDistribRel.DocumentInterfaceDistribution + 1

				if (DocumentInterfaceDetail exists)

					PurchaseOrder				= DocumentInterfaceDetail.PurchaseOrder 
					InterfacedPurchaseOrder  	= DocumentInterfaceDetail.InterfacedPurchaseOrder 
					InterfacedPOCode			= DocumentInterfaceDetail.InterfacedPOCode
					PurchaseOrderLine 			= DocumentInterfaceDetail.PurchaseOrderLine 
					Contract 					= DocumentInterfaceDetail.DocumentInterfaceInvoice.ServiceContract 
					ContractLine 				= DocumentInterfaceDetail.ContractLine 
					Item						= DocumentInterfaceDetail.Item
					VendorItem 					= DocumentInterfaceDetail.VendorItem

		Update is an Update Action

		FastUpdate is an Update Action
			restricted
			bypass field rules

		Delete is a Delete Action

		FastDelete is a Delete Action
			restricted
			bypass relational integrity rules
					
		InterfaceDistributions is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup   is a FinanceEnterpriseGroup
				PrmCompany					is like PayablesCompany
		        InvoiceProcess          	is AlphaUpper size 1
		            States
		                Add            value is "A"
		                MatchSubmitOrRelease value is "R"
				PrmProcessType				is AlphaUpper size 1
		            States
		                MatchInvoices    	value is "M"
		                ExpenseInvoices		value is "E"
		                Both				value is "B"
				PrmVendorClass				is like VendorClass		                
				PrmDocumentInterfaceInvoice is like DocumentInterfaceInvoice
				PrmInterfaceRun				is a PayablesInvoiceInterfaceResult
				
			Local Fields
		        LocalTransactionTotal 			is an InternationalAmount	
		        LocalFunctionalTotal 			is an InternationalAmount
				LocalAlternateTotal  			is an InternationalAmount
				LocalAlternateTotal2 			is an InternationalAmount
				LocalAlternateTotal3			is an InternationalAmount
				LocalToFunctionalTotal			is an InternationalAmount
				LocalToAlternateTotal			is an InternationalAmount
				LocalToAlternateTotal2			is an InternationalAmount
				LocalToAlternateTotal3			is an InternationalAmount
				LocalProjectTotal				is an InternationalAmount
				LocalReportTotal1    			is an InternationalAmount
				LocalReportTotal2    			is an InternationalAmount
				LocalReportTotal3				is an InternationalAmount
				LocalReportTotal4				is an InternationalAmount
				LocalReportTotal5				is an InternationalAmount
				
				ErrorOccurred					is Boolean

				LocalInterfacedDistributionsCount		is Numeric 10

			Instance Selection
				where (DocumentInterfaceInvoice.InterfaceRunID = PrmInterfaceRun
				and    DocumentInterfaceInvoice.SystemAssignedInvoice entered)

			Sort Order
				FinanceEnterpriseGroup
				DocumentInterfaceInvoice

			Action Rules

				Empty Set Rules
						
					invoke InvoiceUpdate DocumentInterfaceInvoice
						invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
						invoked.PrmCompany					= PrmCompany
						invoked.InvoiceProcess				= InvoiceProcess
						invoked.PrmProcessType				= PrmProcessType
						invoked.PrmVendorClass				= PrmVendorClass
						invoked.PrmDocumentInterfaceInvoice = PrmDocumentInterfaceInvoice
						invoked.PrmInterfaceRun				= PrmInterfaceRun

				Set Rules
						
					Exit Rules
						invoke Update PrmInterfaceRun
							invoked.InterfacedDistributionsCount		+= LocalInterfacedDistributionsCount

						invoke InvoiceUpdate DocumentInterfaceInvoice
							invoked.PrmFinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup
							invoked.PrmCompany					= PrmCompany
							invoked.InvoiceProcess				= InvoiceProcess
							invoked.PrmProcessType				= PrmProcessType
							invoked.PrmVendorClass				= PrmVendorClass
							invoked.PrmDocumentInterfaceInvoice = PrmDocumentInterfaceInvoice
							invoked.PrmInterfaceRun				= PrmInterfaceRun

				DocumentInterfaceInvoice Set Rules
				
					Entrance Rules
						ErrorOccurred = false
						if (DocumentInterfaceInvoice.RecordInError)
							ErrorOccurred = true
						
			            initialize LocalTransactionTotal
			            initialize LocalFunctionalTotal
						initialize LocalAlternateTotal
						initialize LocalAlternateTotal2
						initialize LocalAlternateTotal3
						initialize LocalToFunctionalTotal
						initialize LocalToAlternateTotal
						initialize LocalToAlternateTotal2
						initialize LocalToAlternateTotal3
						initialize LocalProjectTotal
						initialize LocalReportTotal1
						initialize LocalReportTotal2
						initialize LocalReportTotal3
						initialize LocalReportTotal4
						initialize LocalReportTotal5
		 					
					Exit Rules

						if (LocalTransactionTotal entered)
							invoke FastUpdate DocumentInterfaceInvoice.PayablesInvoiceRel
								invoked.TotalDistributionAmount.TransactionTotal		+= LocalTransactionTotal
								invoked.TotalDistributionAmount.FunctionalTotal			+= LocalFunctionalTotal
								invoked.TotalDistributionAmount.AlternateTotal			+= LocalAlternateTotal
								invoked.TotalDistributionAmount.AlternateTotal2			+= LocalAlternateTotal2
								invoked.TotalDistributionAmount.AlternateTotal3			+= LocalAlternateTotal3
								invoked.TotalDistributionAmount.ToFunctionalTotal		+= LocalToFunctionalTotal
								invoked.TotalDistributionAmount.ToAlternateTotal		+= LocalToAlternateTotal
								invoked.TotalDistributionAmount.ToAlternateTotal2		+= LocalToAlternateTotal2
								invoked.TotalDistributionAmount.ToAlternateTotal3		+= LocalToAlternateTotal3
								invoked.TotalDistributionAmount.ProjectTotal			+= LocalProjectTotal
								invoked.TotalDistributionAmount.ReportTotal1			+= LocalReportTotal1
								invoked.TotalDistributionAmount.ReportTotal2			+= LocalReportTotal2
								invoked.TotalDistributionAmount.ReportTotal3			+= LocalReportTotal3
								invoked.TotalDistributionAmount.ReportTotal4			+= LocalReportTotal4
								invoked.TotalDistributionAmount.ReportTotal5			+= LocalReportTotal5


				Instance Rules
					LocalInterfacedDistributionsCount						+= 1

					if (!ErrorOccurred)
					
						invoke CreateImport PayablesInvoiceDistribution
							assign result to ImportedDistribution
							resume on error
								ErrorOccurred		= true
								LocalErrorMessage	= error message
							fill in fields from this instance
							invoked.Company											= DocumentInterfaceInvoice.Company
							invoked.PayablesInvoice									= DocumentInterfaceInvoice.SystemAssignedInvoice
							invoked.Vendor											= DocumentInterfaceInvoice.Vendor
							invoked.DistributionType 								= "D"
							invoked.DistributionDate								= DocumentInterfaceInvoice.PayablesInvoiceRel.DistributionDate
							invoked.PostingOption									= DocumentInterfaceInvoice.PayablesInvoiceRel.Vendor.VendorClass.PostingOption
							invoked.InvoiceCurrency									= DocumentInterfaceInvoice.PayablesInvoiceRel.InvoiceCurrency
							invoked.GLTCurrencyCode									= DocumentInterfaceInvoice.PayablesInvoiceRel.InvoiceCurrency
							invoked.DistributionAccount								= DistributionAccount
							invoked.GLFinanceCodeBlock								= DistributionAccount
							invoked.GLTransactionAmount 							= DistributionAmount
							invoked.GLTSystem										= "AP"
							invoked.TransactionDate									= DocumentInterfaceInvoice.PayablesInvoiceRel.DistributionDate
							invoked.Description										= Description
							invoked.AddOnCharge										= AddOnCharge
							invoked.TransientAOCAmount								= AOCAmount
							if (DistributionReference entered)
								invoked.DistributionReference						= DistributionReference



							invoked.TransientBypassErrorUpdate 						= true
	
						if (ErrorOccurred)

							invoke Create DocumentInterfaceError
								invoked.FinanceEnterpriseGroup						= FinanceEnterpriseGroup
								invoked.DocumentInterfaceInvoice 					= DocumentInterfaceInvoice
								invoked.DocumentInterfaceDistribution 				= DocumentInterfaceDistribution
								invoked.ErrorMessage								= LocalErrorMessage

							invoke Update PrmInterfaceRun
								invoked.Status 										= 2
										
						else
							LocalDistribution										= ImportedDistribution.PayablesInvoiceDistribution

							invoke FastDelete

							if  (TaxCode entered		
							and  !TaxIndicator.Taxable
							and ((TaxEntityRel.UseTaxCodeAccounts
							and  EntityTaxCodeRel.AccruedOrInvoiced.Accrued)
							or  (!TaxEntityRel.UseTaxCodeAccounts
							and  TaxEntityRel.AccruedOrInvoiced.Accrued)))
								NextSentence = true
							else
								LocalTransactionTotal   += LocalDistributionRel.DistributionAmount.CurrencyAmount
								LocalFunctionalTotal 	+= LocalDistributionRel.DistributionAmount.FunctionalAmount.EnteredCurrencyAmount
								LocalAlternateTotal  	+= LocalDistributionRel.DistributionAmount.AlternateAmount.EnteredCurrencyAmount
								LocalAlternateTotal2 	+= LocalDistributionRel.DistributionAmount.AlternateAmount2.EnteredCurrencyAmount
								LocalAlternateTotal3 	+= LocalDistributionRel.DistributionAmount.AlternateAmount3.EnteredCurrencyAmount
								LocalToFunctionalTotal 	+= LocalDistributionRel.DistributionAmount.ToFunctionalAmount.EnteredCurrencyAmount
								LocalToAlternateTotal  	+= LocalDistributionRel.DistributionAmount.ToAlternateAmount.EnteredCurrencyAmount
								LocalToAlternateTotal2 	+= LocalDistributionRel.DistributionAmount.ToAlternateAmount2.EnteredCurrencyAmount
								LocalToAlternateTotal3 	+= LocalDistributionRel.DistributionAmount.ToAlternateAmount3.EnteredCurrencyAmount
								LocalProjectTotal 		+= LocalDistributionRel.DistributionAmount.ProjectAmount.EnteredCurrencyAmount
								LocalReportTotal1    	+= LocalDistributionRel.DistributionAmount.ReportAmount1.EnteredCurrencyAmount
								LocalReportTotal2    	+= LocalDistributionRel.DistributionAmount.ReportAmount2.EnteredCurrencyAmount
								LocalReportTotal3    	+= LocalDistributionRel.DistributionAmount.ReportAmount3.EnteredCurrencyAmount
								LocalReportTotal4    	+= LocalDistributionRel.DistributionAmount.ReportAmount4.EnteredCurrencyAmount
								LocalReportTotal5    	+= LocalDistributionRel.DistributionAmount.ReportAmount5.EnteredCurrencyAmount


