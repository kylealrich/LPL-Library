BankReconciliationTemplate is a BusinessClass
	owned by cashmgmt
	prefix is CBRT

	Ontology
		symbolic key is BankReconciliationTemplate

	Patterns

	Persistent Fields
		Description 						is Alpha 100
		RemoveAlphaCharacters				is Boolean
		RemoveLeadingZeros					is Boolean
		LineMatchField						is Alpha 200	
			Text Variables
				TransactionNumber
				TransactionDate
				TransactionCode
				TransactionAmount

				CashTransactionCategory

				CashLedgerTransactionIdentifier
				TransactionDataOne
				TransactionDataTwo
				TransactionDataThree
				TransactionDataFour

				SystemCode
				Reference
		TransactionNumberSource				is Numeric 2
			default label is "VariableSourceOnStatement"
			States
				RelatedReference	value is 0	
				BankReference		value is 1
				TextReference		value is 2
		Active

	Derived Fields
		LikeTransactionNumberSource is a DerivedField
			type is AlphaUpper 20
			restricted
			return "*" + "TRANSACTIONNUMBER" + "*"

		LikeCashLedgerTransactionIdentifierSource is a DerivedField
			type is AlphaUpper 35
			restricted
			return "*" + "CASHLEDGERTRANSACTIONIDENTIFIER" + "*"
		
		LikeTransactionDataOneSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATAONE" + "*"
		
		LikeTransactionDataTwoSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATATWO" + "*"
		
		LikeTransactionDataThreeSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATATHREE" + "*"
		
		LikeTransactionDataFourSource is a DerivedField
			type is AlphaUpper 25
			restricted
			return "*" + "TRANSACTIONDATAFOUR" + "*"
		
	Field Rules
		Description
			required
		
		RemoveLeadingZeros
			initial value is true
		
		RemoveAlphaCharacters
			initial value is true
			if (UsesCashLedgerTransactionIdentifierVariable)
				RemoveAlphaCharacters = false
				
        LineMatchField
			required
        			
    Conditions
		UsesTransactionNumberVariable
			restricted
			when (LineMatchField like LikeTransactionNumberSource)
			
		UsesCashLedgerTransactionIdentifierVariable
			restricted
			when (LineMatchField like LikeCashLedgerTransactionIdentifierSource)
			
		UsesTransactionDataOneVariable
			restricted
			when (LineMatchField like LikeTransactionDataOneSource)
			
		UsesTransactionDataTwoVariable
			restricted
			when (LineMatchField like LikeTransactionDataTwoSource)
			
		UsesTransactionDataThreeVariable
			restricted
			when (LineMatchField like LikeTransactionDataThreeSource)
			
		UsesTransactionDataFourVariable
			restricted
			when (LineMatchField like LikeTransactionDataFourSource)
			
		DisplayVariableSourceOnStatement
			restricted
			when (UsesTransactionNumberVariable
			or	  UsesCashLedgerTransactionIdentifierVariable
			or	  UsesTransactionDataOneVariable
			or	  UsesTransactionDataTwoVariable
			or	  UsesTransactionDataThreeVariable
			or	  UsesTransactionDataFourVariable)
			
		DisplayRemoveAlphaCharacters 
			restricted
			when (!UsesCashLedgerTransactionIdentifierVariable)
			
		SecurityGroupAllowsAccess
			restricted
			when ((CashManagementAccount entered
			and    CashManagementAccount.SecurityGroupAllowsAccess)
			or     CashManagementAccount !entered)
			
    Relations
    	BankStatementReconciliationRel
    		one-to-many relation to BankStatementReconciliation
    		Field Mapping uses symbolic key
				related.CashManagementGroup		= CashManagementGroup
				related.CashManagementAccount	= CashManagementAccount
			Instance Selection
				where (!related.SummaryRecord
				and     related.ReconMatchNumber !entered)

	Attach Rules
		constraint (Active)
			"BankReconciliationTemplateIsInactive"
			
	Actions
		UpdateDetailsWithUpdatedTemplate is an Instance Action	
			Action Rules
				invoke UpdateDetailsWithUpdatedTemplate BankStatementReconciliation	
					invoked.PrmCashManagementGroup		= CashManagementGroup
					invoked.PrmCashManagementAccount	= CashManagementAccount




		Create is an Action
		
		Update is an Action
		
		Delete is an Action
