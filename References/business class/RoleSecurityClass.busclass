RoleSecurityClass is a BusinessClass
	owned by security
	prefix is ROA
	stored in environment
	framework type is Security

	Ontology
		symbolic key is RoleSecurityClass

	Patterns
		implements CRUD
		implements SecurityCache
		disable AuditIndex
		implements AuditLogEntryActions

	Persistent Fields
		Description is a LSDescription

	Transient Fields
		DisablePermissionBOD 	is Boolean 
		
	Field Rules
		DataArea
			initial value is parentcontext.dataarea			
			default to parentcontext.dataarea
		
		DisablePermissionBOD
			default to true		

	Field Groups
        InterfaceFields
			Role
			DataArea
			SecurityClass


	Derived Fields
		IsBODPermissionEnabled is a NativeField
			type is Boolean
			default label is "BODPermissionEnabled"
			
	Conditions
		IsConfigurableFeaturesEnabled
			default label is "ConfigurableFeaturesEnabled"
			when (config.configurablefeaturesenabled = "true")

        UpdatedLastFourteenDays
            when (current timestamp - update stamp <= 1209600)  

        RecentConfigurationChanges
            when (UpdatedLastFourteenDays)

        ActorIsUpdateActor
            when (authenticated actor = update stamp.actor)

 	Relations
		ConfigurableFeaturesEnabledRel
			one-to-many relation to ConfigurationParameter
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ConfigurationParameter.ConfigurationID = "config"
				and    related.ConfigurationParameter.Name = "configurablefeaturesenabled"
				and    related.Value = "true")
           
        ConfigurationParameterRel is a ConfigurationParameter set
        
	Sets
		BySecurityClass
			duplicates
			Sort Order
				SecurityClass

		ByRole
			duplicates
			Sort Order
				Role

		ByRoleAndSecurityClass
			Sort Order
				Role
				DataArea
				SecurityClass

		BySecurityClassDataArea
			Sort Order
				SecurityClass
				DataArea
				Role
				
		ByDataAreaSecurityClass
			duplicates
			Sort Order
				DataArea
				SecurityClass
				
		ByDataArea
			Sort Order
				DataArea
				Role
				SecurityClass

		ByRecentlyUpdated
			Sort Order
			    update stamp
			    Role
                DataArea
                SecurityClass
											
	Actions

        Create is an Action

			    		
        Update is an Update Action


        Delete is an Action
			Parameters
				DisablePermissionBODParam is Boolean
					default label is "DisablePermissionBOD"
			Parameter Rules
				DisablePermissionBODParam
					default to true
					DisablePermissionBOD = DisablePermissionBODParam
    		Action Rules
    		    constraint(com.lawson.apps.security.base.Security_ActionBase.isEqualIgnoreCase(parentcontext.dataarea, RoleSecurityClass.DataArea))
    		        "CannotDelete<RoleSecurityClass.SecurityClass>FromADifferentDataArea"
    		        
        DeleteRoleSecurityClass is a Delete Action
        	restricted

		ReplaceSecurityClass is an Instance Action
		    Parameters
		        NewSecurityClass is a SecurityClass
		    Parameter Rules
		        NewSecurityClass
		            required
		    Action Rules
                invoke Update RoleSecurityClass
                    invoked.SecurityClass = NewSecurityClass

	    SendBODForAllRolePermissions is a Set Action
			valid when (IsBODPermissionEnabled)
					













 											
