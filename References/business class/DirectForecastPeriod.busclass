DirectForecastPeriod is a BusinessClass
	owned by cashmgmt
	prefix is CMDFP	

	Ontology
		symbolic key is DirectForecastPeriod
		
	Persistent Fields								
		PeriodDateRange 		is a DateRange

	Local Fields
		LocalLastYear					is Numeric 1
		LocalForecastPeriod				is Numeric 2
		LocalCashForecast				is a CashForecast
		LocalCashManagementAccount		is like CashManagementAccount
		LocalCashTransactionCategory	is a CashTransactionCategory
		LocalDate 						is Date
		
	Derived Fields
		DerivedFromCurrency	is a DerivedField
			type is like FromCurrency
			if (DirectForecast.ForecastBy.ReportCurrency)		
				return DirectForecast.ReportCurrency
			if (DirectForecast.ForecastBy.CompanyCurrency
			and DirectForecast.GeneralLedgerCompany entered)
				return DirectForecast.GeneralLedgerCompany.Currency
			else
				return DirectForecast.CashManagementAccount.Currency

		PeriodPayments is a DerivedField
			type is like InternationalAmount
			restricted
			if (DirectForecast.GeneralLedgerCompany entered
			and DirectForecast.CashManagementAccount entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum DisbursementSummaryByDateRel.DerivedCompanyAmount)
				if (DirectForecast.ForecastBy.CashAccountCurrency)
					return (sum DisbursementSummaryByDateRel.DerivedAccountAmount)
				return (sum DisbursementSummaryByDateRel.ReportAmount)
			else
			if (DirectForecast.GeneralLedgerCompany entered
			and DirectForecast.CashAccountGroup entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum DisbursementSummaryAccountGroupByDateRel.DerivedCompanyAmount)
				return (sum DisbursementSummaryAccountGroupByDateRel.ReportAmount)
			else
			if (DirectForecast.GeneralLedgerCompany entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum CompanyDisbursementSummaryByDateRel.DerivedCompanyAmount)
				if (DirectForecast.ForecastBy.CashAccountCurrency)
					return (sum CompanyDisbursementSummaryByDateRel.DerivedAccountAmount)
				return (sum CompanyDisbursementSummaryByDateRel.ReportAmount)
			else
				if (DirectForecast.CashAccountGroup entered)
					return (sum CashAccountGroupDisbursementSummaryByDateRel.ReportAmount)
				else
					if (DirectForecast.ForecastBy.CashAccountCurrency)
						return (sum CashManagementAccountDisbursementSummaryByDateRel.DerivedAccountAmount)
					return (sum CashManagementAccountDisbursementSummaryByDateRel.ReportAmount)

		PeriodReceipts is a DerivedField
			type is like InternationalAmount
			if (DirectForecast.GeneralLedgerCompany entered
			and DirectForecast.CashManagementAccount entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum ReceiptSummaryByDateRel.DerivedCompanyAmount)
				if (DirectForecast.ForecastBy.CashAccountCurrency)
					return (sum ReceiptSummaryByDateRel.DerivedAccountAmount)
				return (sum ReceiptSummaryByDateRel.ReportAmount)
			else
			if (DirectForecast.GeneralLedgerCompany entered
			and DirectForecast.CashAccountGroup entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum ReceiptSummaryAccountGroupByDateRel.DerivedCompanyAmount)
				return (sum ReceiptSummaryAccountGroupByDateRel.ReportAmount)
			else
			if (DirectForecast.GeneralLedgerCompany entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum CompanyReceiptSummaryByDateRel.DerivedCompanyAmount)
				if (DirectForecast.ForecastBy.CashAccountCurrency)
					return (sum CompanyReceiptSummaryByDateRel.DerivedAccountAmount)
				return (sum CompanyReceiptSummaryByDateRel.ReportAmount)
			else
				if (DirectForecast.CashAccountGroup entered)
					return (sum CashAccountGroupReceiptSummaryByDateRel.ReportAmount)
				else
					if (DirectForecast.ForecastBy.CashAccountCurrency)
						return (sum CashManagementAccountReceiptSummaryByDateRel.DerivedAccountAmount)
					return (sum CashManagementAccountReceiptSummaryByDateRel.ReportAmount)

		PeriodEncumbrance is a DerivedField
			type is like InternationalAmount
			restricted
			if (DirectForecast.GeneralLedgerCompany entered
			and DirectForecast.CashManagementAccount entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum EncumbranceSummaryByDateRel.DerivedCompanyAmount)
				if (DirectForecast.ForecastBy.CashAccountCurrency)
					return (sum EncumbranceSummaryByDateRel.DerivedAccountAmount)
				return (sum EncumbranceSummaryByDateRel.ReportAmount)
			else
			if (DirectForecast.GeneralLedgerCompany entered
			and DirectForecast.CashAccountGroup entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum EncumbranceSummaryAccountGroupByDateRel.DerivedCompanyAmount)
				return (sum EncumbranceSummaryAccountGroupByDateRel.ReportAmount)
			else
			if (DirectForecast.GeneralLedgerCompany entered)
				if (DirectForecast.ForecastBy.CompanyCurrency)
					return (sum CompanyEncumbranceSummaryByDateRel.DerivedCompanyAmount)
				else
					return (sum CompanyEncumbranceSummaryByDateRel.DerivedAccountAmount)
			else
				if (DirectForecast.CashAccountGroup entered)
					return (sum CashAccountGroupEncumbranceSummaryByDateRel.ReportAmount)
				if (DirectForecast.ForecastBy.CashAccountCurrency)
					return (sum CashManagementAccountEncumbranceSummaryByDateRel.DerivedAccountAmount)
				return (sum CashManagementAccountEncumbranceSummaryByDateRel.ReportAmount)

		PeriodDisbursements is a DerivedField
			type is like InternationalAmount
			if (DirectForecast.IncludePayables
			and DirectForecast.IncludeProcurement) 
				return (PeriodPayments + PeriodEncumbrance)
			else
			if (DirectForecast.IncludePayables)
				return (PeriodPayments)
			else
			if (DirectForecast.IncludeProcurement) 
				return (PeriodEncumbrance)

		NetPeriodActivity is a DerivedField
			type is like InternationalAmount
			if (DirectForecast.IncludeReceivables) 
				return (PeriodDisbursements + PeriodReceipts)
			else
				return (PeriodDisbursements)
			 
		HistoricalFromDate is a DerivedField
			type is Date
			restricted
			LocalLastYear = 1
			initialize HistoricalFromDate
			if (DirectForecastPeriod = 1)
				return ((PeriodDateRange.End - DirectForecast.PeriodDays1)- LocalLastYear as years)
			else
			if (DirectForecastPeriod <= DirectForecast.RangeCount)
				return (PeriodDateRange.Begin - LocalLastYear as years)
	
		HistoricalToDate is a DerivedField
			type is Date
			restricted
			LocalLastYear = 1
			return (PeriodDateRange.End - LocalLastYear as years)

		SecondPeriodStart is a DerivedField
			type is Numeric 3
			restricted
			return (DirectForecast.PeriodDays1 + 1)

		ThirdPeriodStart is a DerivedField
			type is Numeric 3
			restricted
			return (DirectForecast.PeriodDays2 + 1)

		FourthPeriodStart is a DerivedField
			type is Numeric 3
			restricted
			return (DirectForecast.PeriodDays3 + 1)

		LessThanPeriod is a MessageField
			restricted
			"LessThan<DirectForecast.PeriodDays1>"

		SecondRangePeriod is a MessageField
			restricted
			"<SecondPeriodStart>To<DirectForecast.PeriodDays2>"

		ThirdRangePeriod is a MessageField
			restricted
			"<ThirdPeriodStart>To<DirectForecast.PeriodDays3>"

		FourthRangePeriod is a MessageField
			restricted
			"<FourthPeriodStart>To<DirectForecast.PeriodDays4>"

		AbovePeriod is a MessageField
			restricted
			"Above<DirectForecast.PeriodEndInRange>"

		ForecastPeriod is a DerivedField
			type is Alpha 20
			if (DirectForecastPeriod = 1)
				return LessThanPeriod 
			else
			if (DirectForecastPeriod > DirectForecast.RangeCount)
				return AbovePeriod
			else
			if (DirectForecastPeriod = 2)
				return SecondRangePeriod
			else
			if (DirectForecastPeriod = 3)
				return ThirdRangePeriod
			else
			if (DirectForecastPeriod = 4)
				return FourthRangePeriod
			
	Transient Fields
		TransientFromCurrency	is a FromCurrency
			derive value from DerivedFromCurrency

	Relations
		DisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company				  	= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "AP"
				related.CashManagementAccount 	= DirectForecast.CashManagementAccount
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange)

		DisbursementSummaryAccountGroupByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company				  	= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "AP"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)

		CompanyDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company	   				= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode = "AP"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount entered)

		CashManagementAccountDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses ByDueDate
				related.CashManagementAccount 	= DirectForecast.CashManagementAccount
				related.GeneralLedgerSystemCode	= "AP"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange)

		CashAccountGroupDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses BySystemCodeAccount
				related.GeneralLedgerSystemCode	= "AP"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)

		ReceiptSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company				  	= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "AR"
				related.CashManagementAccount 	= DirectForecast.CashManagementAccount
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange)

		ReceiptSummaryAccountGroupByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company				  	= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "AR"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)

		CompanyReceiptSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company	   				= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode = "AR"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount entered)

		CashManagementAccountReceiptSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses ByDueDate
				related.CashManagementAccount 	= DirectForecast.CashManagementAccount
				related.GeneralLedgerSystemCode	= "AR"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange)

		CashAccountGroupReceiptSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses BySystemCodeAccount
				related.GeneralLedgerSystemCode	= "AR"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)

		EncumbranceSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company				  	= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "PO"
				related.CashManagementAccount 	= DirectForecast.CashManagementAccount
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange)

		EncumbranceSummaryAccountGroupByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company				  	= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "PO"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)

		CompanyEncumbranceSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company	   				= DirectForecast.GeneralLedgerCompany
				related.GeneralLedgerSystemCode	= "PO"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount entered)

		CashManagementAccountEncumbranceSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses ByDueDate
				related.CashManagementAccount 	= DirectForecast.CashManagementAccount
				related.GeneralLedgerSystemCode	= "PO"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange)

		CashAccountGroupEncumbranceSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses BySystemCodeAccount
				related.GeneralLedgerSystemCode	= "PO"
			Instance Selection
				where (related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)

		CompanyAccountTotalDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses ByCompany
				related.Company	 			  = DirectForecast.GeneralLedgerCompany
				related.CashManagementAccount = DirectForecast.CashManagementAccount
			Instance Selection
				where (!related.GeneralLedgerSystemCode = "AR" 
				and	   related.DirectForecastSummary within PeriodDateRange)
				
		CompanyAccountGroupTotalDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses ByCompany
				related.Company	 			  = DirectForecast.GeneralLedgerCompany
			Instance Selection
				where (!related.GeneralLedgerSystemCode = "AR" 
				and	   related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)
				
		CompanyTotalDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses symbolic key
				related.Company = DirectForecast.GeneralLedgerCompany
			Instance Selection
				where (!related.GeneralLedgerSystemCode = "AR" 
				and	   related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount entered)
		
		CashManagementAccountTotalDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses ByCashManagementAccount
				related.CashManagementAccount = DirectForecast.CashManagementAccount
			Instance Selection
				where (!related.GeneralLedgerSystemCode = "AR" 
				and	   related.DirectForecastSummary within PeriodDateRange)
		
		CashAccountGroupTotalDisbursementSummaryByDateRel
			one-to-many relation to DirectForecastSummary
			Field Mapping uses BySystemCodeAccount
			Instance Selection
				where (!related.GeneralLedgerSystemCode = "AR" 
				and	   related.DirectForecastSummary within PeriodDateRange
				and    related.CashManagementAccount within DirectForecast.CashAccountGroup)
		
		DisbursementByCompanyAccountRel
			if (DirectForecast.IncludePayables
			and DirectForecast.IncludeProcurement) 
				CompanyAccountTotalDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludePayables)
				DisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludeProcurement) 
				EncumbranceSummaryByDateRel
				
		DisbursementByCompanyAccountGroupRel
			if (DirectForecast.IncludePayables
			and DirectForecast.IncludeProcurement) 
				CompanyAccountGroupTotalDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludePayables)
				DisbursementSummaryAccountGroupByDateRel
			else
			if (DirectForecast.IncludeProcurement) 
				EncumbranceSummaryAccountGroupByDateRel
				
		DisbursementByCompanyRel
			if (DirectForecast.IncludePayables
			and DirectForecast.IncludeProcurement) 
				CompanyTotalDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludePayables)
				CompanyDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludeProcurement) 
				CompanyEncumbranceSummaryByDateRel
		
		DisbursementByCashManagementAccountRel
			if (DirectForecast.IncludePayables
			and DirectForecast.IncludeProcurement) 
				CashManagementAccountTotalDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludePayables)
				CashManagementAccountDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludeProcurement) 
				CashManagementAccountEncumbranceSummaryByDateRel
	
		DisbursementByAccountGroupRel
			if (DirectForecast.IncludePayables
			and DirectForecast.IncludeProcurement) 
				CashAccountGroupTotalDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludePayables)
				CashAccountGroupDisbursementSummaryByDateRel
			else
			if (DirectForecast.IncludeProcurement) 
				CashAccountGroupEncumbranceSummaryByDateRel

		CashForecastCategoryRel
			one-to-one relation to CashForecastCategory
			Field Mapping uses symbolic key
				related.CashManagementGroup							 = CashManagementGroup
				related.CashForecast								 = LocalCashForecast
				related.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory

		CashForecastDetailRel 
			one-to-many relation to CashForecastDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup							 = CashManagementGroup
				related.CashForecast								 = LocalCashForecast
				related.CashForecastAccount.CashManagementAccount	 = LocalCashManagementAccount
				related.CashForecastPeriod							 = LocalDate
				related.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory

		CashForecastPeriodAmountCubeRel
			one-to-one relation to AnalyticCube
	        Field Mapping uses AnalyticCubeSet
	        	related.BusinessClass = "CashForecastPeriodAmount"

	
	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		Purge is a Purge Action
  			restricted

		AddToForecast is an Instance Action
			Parameters
				PrmCashManagementGroup	 		is a CashManagementGroup
				PrmCashForecast			  		is a CashForecast
				PrmCashForecastAccount			is a CashForecastAccount
				PrmCashForecastCategory 		is a CashTransactionCategory
				PrmForecastDate				   	is Date
				PrmCashFlow						is Numeric 1
					default label is "CashFlowType"
					States
						CashInflow		value is 1
						CashOutflow		value is 2
				PrmAction						is Numeric 1
					default label is "Action"
					States	
						OverrideForecastAmount		value is 1
						AppendForecastAmount		value is 2
				PrmCashInFlowAmount				is a InternationalAmount
				PrmCashOutFlowAmount			is a InternationalAmount
						
			Parameter Rules
				PrmCashManagementGroup
					required
						"CashManagementGroupIsRequired"						
				PrmCashForecast
					required
						"CashForecastIsRequired"
				PrmCashForecastAccount
					required
						"CashForecastAccountIsRequired"
				PrmCashForecastCategory					
					required
						"CashForecastCategoryIsRequired"	
				PrmForecastDate
					initial value is PeriodDateRange.End
					constraint (PrmForecastDate within PrmCashForecast.ForecastDateRange)
						"ForecastDateIsNotWithinRangeOfCashForecast<PrmCashForecast>"
					required
						"ForecastDateIsRequired"			
				PrmCashFlow
					required
						"CashFlowTypeIsRequired"
				PrmAction	
					required
						"ActionIsRequired"
				PrmCashInFlowAmount
					initial value is PeriodReceipts
					if (PrmCashFlow.CashInflow)
						required
							"CashInflowCannotBeZero"					
				PrmCashOutFlowAmount
					initial value is  PeriodDisbursements
					if (PrmCashFlow.CashOutflow)
						required
							"CashOutflowCannotBeZero"

			Entrance Rules
				LocalCashForecast = PrmCashForecast
				LocalCashTransactionCategory = PrmCashForecastCategory
				LocalCashManagementAccount = PrmCashForecastAccount.CashManagementAccount

				if (!CashForecastCategoryRel exists)
					invoke Create CashForecastCategory
						invoked.CashManagementGroup							   = CashManagementGroup
						invoked.CashForecast								   = LocalCashForecast
						invoked.CashForecastCategory.CashTransactionCategory   = LocalCashTransactionCategory
						invoked.ParentForecastCategory.CashTransactionCategory = LocalCashTransactionCategory.ParentTransactionCategory

			Action Rules	
				LocalDate = PrmForecastDate
				invoke Create CashForecastDetail
					invoked.CashManagementGroup							 = CashManagementGroup
					invoked.CashForecast								 = LocalCashForecast
					invoked.CashForecastAccount.CashManagementAccount	 = LocalCashManagementAccount
					invoked.CashForecastPeriod							 = LocalDate
					invoked.CashForecastCategory.CashTransactionCategory = LocalCashTransactionCategory
					invoked.Description								 	 = "SummarizedTransaction"
					invoked.ForecastAmountAction						 = PrmAction
					if (PrmCashFlow.CashInflow)
						invoked.ForecastAmount = PrmCashInFlowAmount
					else
						invoked.ForecastAmount = PrmCashOutFlowAmount
					invoked.IsSystemCalculated		= true

			Exit Rules
				if (LocalCashForecast.HaveBuiltForecast)
  					invoke ScheduledRefresh CashForecastPeriodAmountCubeRel in background
