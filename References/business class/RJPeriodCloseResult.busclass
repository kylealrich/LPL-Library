RJPeriodCloseResult is a BusinessClass
	default label is "RecurringJournalPeriodCloseResult"
	owned by GeneralLedger
	prefix is RJCLR
	
	Ontology
		symbolic key is RJPeriodCloseResult

	Patterns
	
	Persistent Fields
		RunTime					is TimeStamp
		AccountingEntityGroup
	
	Field Rules

	Derived Fields	
		Status is a DerivedField
			type is Alpha 50
			if(DetailsExists)
				return "Incomplete"
			else
				return "Complete"
		
		AccountingEntityOption is a DerivedField
			type is Alpha 10
			return "Group"
			
	Conditions
		DetailsExists
			restricted
			when (RJPeriodCloseResultDetails set exists)
	
	Sets
		ByDescendingRunTime
			Sort Order
				FinanceEnterpriseGroup
				RunTime descending
				RJPeriodCloseResult
				
	Actions
		Create is an Action
			restricted
		Update is an Action
			restricted
		Delete is an Action
		Purge is a Purge Action	
			restricted
		
		PurgeData is a Set Action
			restricted
			confirmation required
				"AllRecordsWithinEnteredParametersWillBePurged.Continue?"
			Parameters
				PrmFinanceEnterpriseGroup		is a FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmStatus 							is Numeric 1
					default label is "Status"
					States
						Incomplete				value is 1
						Complete				value is 2
						All						value is 3

			Parameter Rules
				PrmFinanceEnterpriseGroup
					initial value is actor.context.FinanceEnterpriseGroup
        			required
				PrmStatus	
					required
			
			Local Fields
				LocalActor							is an Actor

			Instance Selection
				where (FinanceEnterpriseGroup = PrmFinanceEnterpriseGroup)
			
			Action Rules
				Set Rules
					Exit Rules
						LocalActor = actor
						send notification
							to LocalActor
							description is "PurgeRecurringJournalPeriodCloseResultCompleted"

				Instance Rules
					if (PrmStatus.Incomplete)
						if (DetailsExists)
							invoke Purge
					else
					if (PrmStatus.Complete)
						if (!DetailsExists)
							invoke Purge
					else
					if(PrmStatus.All)
						invoke Purge
