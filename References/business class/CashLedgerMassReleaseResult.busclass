CashLedgerMassReleaseResult is a BusinessClass
	owned by cb
	prefix is CLMRR
	Ontology
		symbolic key is CashLedgerMassReleaseResult
		
	Persistent Fields
		CreateDate					is TimeStamp
		Status						is Numeric 1
			States
				Processing			value is 1
				NoRecordsToProcess	value is 2
				Completed 			value is 4
				CompletedWithErrors	value is 5
        ReleaseType             	is Numeric size 2
            States
                Transactions  		value is 1
                Payments  			value is 2
                ReturnedPayments 	value is 3
                CashReceipts		value is 4
				BankFundTransfers	value is 5
				EFTExtracts			value is 6
        CashCode											
        CashCodeGroup										
        BankTransactionCode									
		UseSystemDate				is Boolean				
		EnterDate					is Date					
		Days						is Numeric 3			
		ReleaseDateRange			is a DateRange			
		TransNbrRangeUsingLike								
		GeneralLedgerSourceCode		is a GeneralLedgerEvent	
			default label is "GlobalLedgerSourceCode"
		TransferType             	is AlphaUpper size 1	
			States
                MultipleDeposits    value is "1"
                MultipleWithdrawals value is "2"
                Both				value is "3"		
		PrenoteStatus				is AlphaUpper 1			
			States
				NotPrenotifiedToAccepted	value is "N"
				PrenotifiedToAccepted		value is "P"
				BothToAccepted				value is "B"
				NoChange					value is blank
		
	Field Rules
		CreateDate
            default to current timestamp 
		
		Status
			initial value is 1	
			default to 1
			
	Relations
		CashLedgerTransactionErrorForTransactionsRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByMassReleaseTransactionsErrorSource			
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 4	
				related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult

		CashLedgerTransactionErrorForPaymentsRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByMassReleasePaymentsErrorSource				
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 5	
				related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
		
		CashLedgerTransactionErrorForReturnedPaymentsRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByMassReleaseReturnedPaymentsErrorSource		
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 6	
				related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				
		CashLedgerTransactionErrorForCashReceiptsRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByMassReleaseCashReceiptsErrorSource			
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 7	
				related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
								
		CashLedgerTransactionErrorForBankFundTransfersRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByMassReleaseBankFundTransfersErrorSource	
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 8	
				related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
						
		CashLedgerTransactionErrorForEFTExtractsRel
			one-to-many relation to CashLedgerTransactionError
			Field Mapping uses ByMassReleaseEFTExtractsErrorSource			
				related.CashManagementGroup			= CashManagementGroup
				related.ErrorSource					= 9	
				related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
		

		TransactionsWithErrorsRel
			one-to-many relation to CashLedgerTransaction	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    related.Status.Unreleased)

		PaymentsWithErrorsRel	
			one-to-many relation to CashLedgerPayment	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    related.Status.Unreleased)
	
		ReturnedPaymentsWithErrorsRel
			one-to-many relation to CashLedgerReturnedPayment	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    related.Status.Unreleased)
				
		CashReceiptsWithErrorsRel	
			one-to-many relation to CashLedgerCashReceiptHeader	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    related.Status.Unreleased)
				
		BankFundTransfersWithErrorsRel
			one-to-many relation to CashLedgerFundTransferHeader	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    related.Status.Unreleased)
				
		EFTExtractsWithErrorsRel
			one-to-many relation to CashLedgerElectronicFundsTransferTransaction	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    related.Status.Unreleased)


		TransactionsReleasedRel
			one-to-many relation to CashLedgerTransaction
			Field Mapping uses symbolic key
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    !related.Status.Unreleased)

		PaymentsReleasedRel
			one-to-many relation to CashLedgerPayment
			Field Mapping uses symbolic key
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    !related.Status.Unreleased)

		ReturnedPaymentsReleasedRel
			one-to-many relation to CashLedgerReturnedPayment	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    !related.Status.Unreleased)
		
		CashReceiptsReleasedRel
			one-to-many relation to CashLedgerCashReceiptHeader	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    !related.Status.Unreleased)
		
		BankFundTransfersReleasedRel
			one-to-many relation to CashLedgerFundTransferHeader	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    !related.Status.Unreleased)
								
		EFTExtractsReleasedRel
			one-to-many relation to CashLedgerElectronicFundsTransferTransaction	
			Field Mapping uses symbolic key			
				related.CashManagementGroup					= CashManagementGroup
			Instance Selection	
				where (related.CashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
				and    !related.Status.Unreleased)
		
	Conditions
		EventCodeValid
			restricted
			when (ReleaseType.Payments
			or	  ReleaseType.ReturnedPayments
			or    ReleaseType.CashReceipts
			or    ReleaseType.EFTExtracts)
				
		PrenoteValid
			restricted
			when (ReleaseType.Payments
			or    ReleaseType.EFTExtracts)
			
		TransNbrRangeValid
			restricted
			when (ReleaseType.Transactions
			or	  ReleaseType.BankFundTransfers)
			
		TransferTypeValid
			restricted
			when (ReleaseType.BankFundTransfers)

		ReleaseTypeIsTransactions
			restricted
			when (ReleaseType.Transactions)
		ReleaseTypeIsPayments
			restricted
			when (ReleaseType.Payments)
		ReleaseTypeIsReturnedPayments
			restricted
			when (ReleaseType.ReturnedPayments)
		ReleaseTypeIsCashReceipts
			restricted
			when (ReleaseType.CashReceipts)
		ReleaseTypeIsBankFundTransfers
			restricted
			when (ReleaseType.BankFundTransfers)
		ReleaseTypeIsEFTExtracts
			restricted
			when (ReleaseType.EFTExtracts)

		TransactionErrorsExist
			restricted
			when (CashLedgerTransactionErrorForTransactionsRel exists)
		PaymentErrorsExist
			restricted
			when (CashLedgerTransactionErrorForPaymentsRel exists)
		ReturnedPaymentErrorsExist
			restricted
			when (CashLedgerTransactionErrorForReturnedPaymentsRel exists)
		CashReceiptErrorsExist
			restricted
			when (CashLedgerTransactionErrorForCashReceiptsRel exists)
		BankFundTransferErrorsExist
			restricted
			when (CashLedgerTransactionErrorForBankFundTransfersRel exists)
		EFTExtractErrorsExist
			restricted
			when (CashLedgerTransactionErrorForEFTExtractsRel exists)

		TransactionRecordsReleased
			restricted
			when (TransactionsReleasedRel exists)
		PaymentRecordsReleased
			restricted
			when (PaymentsReleasedRel exists)
		ReturnedPaymentRecordsReleased
			restricted
			when (ReturnedPaymentsReleasedRel exists)
		CashReceiptRecordsReleased
			restricted
			when (CashReceiptsReleasedRel exists)
		BankFundTransferRecordsReleased
			restricted
			when (BankFundTransfersReleasedRel exists)
		EFTExtractRecordsReleased
			restricted
			when (EFTExtractsReleasedRel exists)
					
	Derived Fields
		ReleaseCount is a DerivedField
			type is Numeric 5

			if (ReleaseType.Transactions)
				return instance count of TransactionsReleasedRel
			else	
			if (ReleaseType.Payments)
				return instance count of PaymentsReleasedRel
			else
			if (ReleaseType.ReturnedPayments)
				return instance count of ReturnedPaymentsReleasedRel
			else	
			if (ReleaseType.CashReceipts)
				return instance count of CashReceiptsReleasedRel
			else	
			if (ReleaseType.BankFundTransfers)
				return instance count of BankFundTransfersReleasedRel
			else	
			if (ReleaseType.EFTExtracts)
				return instance count of EFTExtractsReleasedRel
				
		ErrorCount is a DerivedField
			type is Numeric 5

			if (ReleaseType.Transactions)
				return instance count of CashLedgerTransactionErrorForTransactionsRel
			else	
			if (ReleaseType.Payments)
				return instance count of CashLedgerTransactionErrorForPaymentsRel
			else
			if (ReleaseType.ReturnedPayments)
				return instance count of CashLedgerTransactionErrorForReturnedPaymentsRel
			else	
			if (ReleaseType.CashReceipts)
				return instance count of CashLedgerTransactionErrorForCashReceiptsRel
			else	
			if (ReleaseType.BankFundTransfers)
				return instance count of CashLedgerTransactionErrorForBankFundTransfersRel
			else	
			if (ReleaseType.EFTExtracts)
				return instance count of CashLedgerTransactionErrorForEFTExtractsRel
																	
	Sets
    	ByCreateDate
    		Sort Order
    			CreateDate				descending
    			CashManagementGroup
    			CashLedgerMassReleaseResult
 
 		ByReleaseType
 			Sort Order
 				ReleaseType
 				CreateDate				descending
    			CashManagementGroup
    			CashLedgerMassReleaseResult
    			  			

	Local Fields
		
    Attach Rules

	Rule Blocks

	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		StatusUpdate is an Update Action
			default label is untranslatable
			restricted
			Action Rules
				if (ReleaseType.Transactions)
					if (TransactionErrorsExist)
						Status = Status.CompletedWithErrors
					else
						Status = Status.Completed
				else
				if (ReleaseType.Payments)
					if (PaymentErrorsExist)
						Status = Status.CompletedWithErrors
					else
						Status = Status.Completed
				else
				if (ReleaseType.ReturnedPayments)
					if (ReturnedPaymentErrorsExist)
						Status = Status.CompletedWithErrors
					else
						Status = Status.Completed
				else
				if (ReleaseType.CashReceipts)
					if (CashReceiptErrorsExist)
						Status = Status.CompletedWithErrors
					else
						Status = Status.Completed
				else
				if (ReleaseType.BankFundTransfers)
					if (BankFundTransferErrorsExist)
						Status = Status.CompletedWithErrors
					else
						Status = Status.Completed
				else
				if (ReleaseType.EFTExtracts)
					if (EFTExtractErrorsExist)
						Status = Status.CompletedWithErrors
					else
						Status = Status.Completed
						
		Delete is a Delete Action
			Entrance Rules
				if (ReleaseType.Transactions)
					if (TransactionErrorsExist)
						invoke RemoveCashLedgerMassReleaseResult TransactionsWithErrorsRel
						invoke Delete CashLedgerTransactionErrorForTransactionsRel
					if (TransactionRecordsReleased)
						invoke RemoveCashLedgerMassReleaseResult TransactionsReleasedRel
				else
				if (ReleaseType.Payments)
					if (PaymentErrorsExist)
						invoke RemoveCashLedgerMassReleaseResult PaymentsWithErrorsRel
						invoke Delete CashLedgerTransactionErrorForPaymentsRel
					if (PaymentRecordsReleased)
						invoke RemoveCashLedgerMassReleaseResult PaymentsReleasedRel
				else
				if (ReleaseType.ReturnedPayments)
					if (ReturnedPaymentErrorsExist)
						invoke RemoveCashLedgerMassReleaseResult ReturnedPaymentsWithErrorsRel
						invoke Delete CashLedgerTransactionErrorForReturnedPaymentsRel
					if (ReturnedPaymentRecordsReleased)
						invoke RemoveCashLedgerMassReleaseResult ReturnedPaymentsReleasedRel
				else
				if (ReleaseType.CashReceipts)
					if (CashReceiptErrorsExist)
						invoke RemoveCashLedgerMassReleaseResult CashReceiptsWithErrorsRel
						invoke Delete CashLedgerTransactionErrorForCashReceiptsRel
					if (CashReceiptRecordsReleased)
						invoke RemoveCashLedgerMassReleaseResult CashReceiptsReleasedRel
				else
				if (ReleaseType.BankFundTransfers)
					if (BankFundTransferErrorsExist)
						invoke RemoveCashLedgerMassReleaseResult BankFundTransfersWithErrorsRel
						invoke Delete CashLedgerTransactionErrorForBankFundTransfersRel
					if (BankFundTransferRecordsReleased)
						invoke RemoveCashLedgerMassReleaseResult BankFundTransfersReleasedRel
				else
				if (ReleaseType.EFTExtracts)
					if (EFTExtractErrorsExist)
						invoke RemoveCashLedgerMassReleaseResult EFTExtractsWithErrorsRel
						invoke Delete CashLedgerTransactionErrorForEFTExtractsRel
					if (EFTExtractRecordsReleased)
						invoke RemoveCashLedgerMassReleaseResult EFTExtractsReleasedRel
												
		RerunMassReleaseForTransactionErrors is an Instance Action
			valid when (TransactionErrorsExist)
			Entrance Rules
				if (CashLedgerTransactionErrorForTransactionsRel exists)			
					invoke RerunMassReleaseForErrors CashLedgerTransaction in background
						invoked.PrmCashManagementGroup			= CashManagementGroup
						invoked.PrmCashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
								
								
		RerunMassReleaseForPaymentErrors is an Instance Action
			valid when (PaymentErrorsExist)
			Entrance Rules
				if (CashLedgerTransactionErrorForPaymentsRel exists)			
					invoke RerunMassReleaseForErrors CashLedgerPayment in background
						invoked.PrmCashManagementGroup			= CashManagementGroup
						invoked.PrmCashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
																
		RerunMassReleaseForCashReceiptErrors is an Instance Action									
			valid when (CashReceiptErrorsExist)
			Entrance Rules
				if (CashLedgerTransactionErrorForCashReceiptsRel exists)			
					invoke RerunMassReleaseForErrors CashLedgerCashReceiptHeader in background
						invoked.PrmCashManagementGroup			= CashManagementGroup
						invoked.PrmCashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
															
		RerunMassReleaseForReturnedPaymentErrors is an Instance Action
			valid when (ReturnedPaymentErrorsExist)
			Entrance Rules
				if (CashLedgerTransactionErrorForReturnedPaymentsRel exists)			
					invoke RerunMassReleaseForErrors CashLedgerReturnedPayment in background
						invoked.PrmCashManagementGroup			= CashManagementGroup
						invoked.PrmCashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
															
		RerunMassReleaseForEFTExtractErrors is an Instance Action
			valid when (EFTExtractErrorsExist)
			Entrance Rules
				if (CashLedgerTransactionErrorForEFTExtractsRel exists)			
					invoke RerunMassReleaseForErrors CashLedgerElectronicFundsTransferTransaction in background
						invoked.PrmCashManagementGroup			= CashManagementGroup
						invoked.PrmCashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
															
		RerunMassReleaseForBankFundTransferErrors is an Instance Action
			valid when (BankFundTransferErrorsExist)
			Entrance Rules
				if (CashLedgerTransactionErrorForBankFundTransfersRel exists)			
					invoke RerunMassReleaseForErrors CashLedgerFundTransferHeader in background
						invoked.PrmCashManagementGroup			= CashManagementGroup
						invoked.PrmCashLedgerMassReleaseResult	= CashLedgerMassReleaseResult
															
				
