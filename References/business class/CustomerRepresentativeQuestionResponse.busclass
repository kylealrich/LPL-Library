CustomerRepresentativeQuestionResponse is a BusinessClass
    owned by ar
    sql name is CustomerRepQuestionResponse
    prefix is CRQR

    Ontology
    	part of CustomerRepresentative
    		relative key is CustomerGroupQuestion
    
    Patterns
        implements TemplateDriven by CustomerGroupQuestion
        	create instance
        		when (CustomerRepresentativeQuestionAnswer entered)
        		
    Persistent Fields
		CustomerRepresentativeQuestionAnswer
    	Attachment				
   
    Derived Fields
    	CustomerRepresentativeResponseAnswers is a ConditionalField
    		type is Alpha size 250
    		if (CustomerGroupQuestion.ResponseType.Text)
    			CustomerRepresentativeQuestionAnswer.TextValue
    		else
    		if (CustomerGroupQuestion.ResponseType.Number)
    			CustomerRepresentativeQuestionAnswer.NumericValue
    		else 
    		if (CustomerGroupQuestion.ResponseType.Date)
    			DerivedDate
    		else
    		if (CustomerGroupQuestion.ResponseType.List)
    			CustomerRepresentativeQuestionAnswer.CustomerGroupQuestionValue
    		else
    		if (CustomerGroupQuestion.ResponseType.YesNo)
    			if (CustomerRepresentativeQuestionAnswer.YesNoValue.Yes)
    				YesText
    			else
    				NoText
    		else
    			YesNoText
    			
    	YesNoText is a ConditionalField
			type is Alpha size 255
			restricted
			if (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.Yes)
				YesText + ". " + CustomerRepresentativeQuestionAnswer.YesNoText.Text
			else
				NoText + ". " + CustomerRepresentativeQuestionAnswer.YesNoText.Text

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"

		DerivedDate is a StringField
			type is Alpha 10
			restricted
			CustomerRepresentativeQuestionAnswer.DateValue month
			"/"
			CustomerRepresentativeQuestionAnswer.DateValue day
			"/"
			CustomerRepresentativeQuestionAnswer.DateValue year
			
		ResponseRequiredMessage is a MessageField
			restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
        	restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
        	restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsYes"
            
        NoAttachmentMessage is a MessageField
        	restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsNo"
            
        YesOrNoAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsYes"
            
        NoAttachmentOrTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsNo"
            
        AttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsYes"
            
        NoAttachmentAndTextMessage is a MessageField
        	restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsNo"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
            restricted
            if (CustomerGroupQuestion.ResponseRules.ResponseRequired
            or  CustomerGroupQuestion.YesNoResponseRules.ResponseRequired)
                ResponseRequiredMessage        
            else
            if (CustomerGroupQuestion.ResponseRules.ResponseRequiredAndAttachmentRequired
            or  CustomerGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
                ResponseAndAttachmentRequiredMessage
            else
            if (CustomerGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
                YesAttachmentMessage
            else
            if (CustomerGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                NoAttachmentMessage
            else
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
                YesOrNoAndTextMessage
            else
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
                AttachmentOrTextMessage
            else
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                YesAttachmentOrTextMessage
            else
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
                NoAttachmentOrTextMessage
            else 
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
                AttachmentAndTextMessage
            else
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                YesAttachmentAndTextMessage
            else
            if (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
                NoAttachmentAndTextMessage
            else
                blank

    Conditions
        HasResponseAttachment
        	restricted
            when (Attachment entered)
        
        AttachmentAlwaysRequired 
        	restricted
            when (CustomerGroupQuestion.AlwaysRequireResponseAttachment
            and  (CustomerGroupQuestion.NonYesNoResponseType
            or    CustomerGroupQuestion.ResponseType.YesNo))
            
       	FromRequiredQuestion
       		restricted
       		when (CustomerGroupQuestion.ResponseRequired)
       		
    Field Rules
    	CustomerRepresentativeQuestionAnswer
            if (CustomerGroupQuestion.ResponseRequired)
                required
                    "MustAnswerAllRequiredQuestions"
            if (CustomerGroupQuestion.ResponseType.YesNoText
            and !HasResponseAttachment
            and (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
            or  (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.Yes
            and  CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
            or  (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.No
            and  CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
                constraint (CustomerRepresentativeQuestionAnswer.YesNoText.Text entered)
                    "MustEnterEitherTextOrAnAttachmentForQuestion<CustomerGroupQuestion.QuestionText>"
                    
            if (CustomerGroupQuestion.ResponseType.YesNo
            and (CustomerRepresentativeQuestionAnswer.YesNoValue.Yes
            and  CustomerGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
            or  (CustomerRepresentativeQuestionAnswer.YesNoValue.No
            and  CustomerGroupQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<CustomerGroupQuestion.QuestionText>"    
            
            if (AttachmentAlwaysRequired
            and !CustomerGroupQuestion.ResponseType.YesNoText)
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<CustomerGroupQuestion.QuestionText>"
                       
            if  (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
            or   CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.Yes
            and  CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.No
            and  CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (CustomerRepresentativeQuestionAnswer.YesNoText.Text entered)
                    "MustEnterTextForQuestion<CustomerGroupQuestion.QuestionText>"
                    
            if  (CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.Yes
            and  CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo.No
            and  CustomerGroupQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<CustomerGroupQuestion.QuestionText>"
        Attachment
            if (CustomerGroupQuestion.AlwaysRequireResponseAttachment)
                required
                    "MustAttachADocumentForAllQuestionsRequiringAnAttachment"
            if (!CustomerRepresentativeQuestionAnswer entered)
                cannot be entered
                    "MustAnswerQuestionToAttachADocument"

    Rule Blocks
		YesNoTextRule
			if (CustomerGroupQuestion.ResponseType.YesNoText)
				if (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo entered)
					constraint (CustomerRepresentativeQuestionAnswer.YesNoText.Text entered)
						"MustAlsoEnterTextWithYour_Yes/_NoResponse"
				if (CustomerRepresentativeQuestionAnswer.YesNoText.Text entered)
					constraint (CustomerRepresentativeQuestionAnswer.YesNoText.YesNo entered)
						"MustAlsoSelectA_YesOr_NoWithYourTextResponse"
					
    Actions
		Create is a Create Action
     		Action Rules
				include YesNoTextRule
 											
     	Update is an Update Action
			valid when (CustomerRepresentative.IsUpdatable)
     		Action Rules
				include YesNoTextRule
     						
     	Delete is a Delete Action
			valid when (CustomerRepresentative.IsUpdatable)
