TrueCostResourceIntensityFactorDataInterface is a BusinessClass
	owned by truecost
	sql name is WeightDataInterface
	prefix is TCRIF
	default label is "DriverWeightDataInterfaceMaintenance"
	Ontology
		symbolic key is TrueCostResourceIntensityFactorDataInterface
		
	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		TrueCostResourceIntensityFactor
			default label is "DriverWeight"
		Value								is a TrueCostValue
		AccountingEntity				is like AccountingEntity
			default label is "<TrueCostConfiguration.AccountingEntityLabel>"
		AccountingUnit					is like AccountingUnit
			default label is "<TrueCostConfiguration.AccountingUnitLabel>"
		GeneralLedgerChartAccount		is like GeneralLedgerChartAccount
			restricted
			default label is "<TrueCostConfiguration.AccountLabel>"
		Project							is like Project
			default label is "<TrueCostConfiguration.ProjectLabel>"
		FinanceDimension1				is like FinanceDimension1
			default label is "<TrueCostConfiguration.FinanceDimension1Label>"
		FinanceDimension2				is like FinanceDimension2
			default label is "<TrueCostConfiguration.FinanceDimension2Label>"
		FinanceDimension3				is like FinanceDimension3
			default label is "<TrueCostConfiguration.FinanceDimension3Label>"
		FinanceDimension4				is like FinanceDimension4
			default label is "<TrueCostConfiguration.FinanceDimension4Label>"
		FinanceDimension5				is like FinanceDimension5
			default label is "<TrueCostConfiguration.FinanceDimension5Label>"
		FinanceDimension6				is like FinanceDimension6
			default label is "<TrueCostConfiguration.FinanceDimension6Label>"
		FinanceDimension7				is like FinanceDimension7
			default label is "<TrueCostConfiguration.FinanceDimension7Label>"
		FinanceDimension8				is like FinanceDimension8
			default label is "<TrueCostConfiguration.FinanceDimension8Label>"
		FinanceDimension9				is like FinanceDimension9
			default label is "<TrueCostConfiguration.FinanceDimension9Label>"
		FinanceDimension10				is like FinanceDimension10
			default label is "<TrueCostConfiguration.FinanceDimension10Label>"
		EffectiveDate						is Date
		ErrorDescription					is Alpha 60
	
	Local Fields
		LocalTrueCostConfiguration			is like TrueCostConfiguration
		LocalRunGroup						is like RunGroup
	
		LocalAccountingEntity			is a AccountingEntity			
		LocalAccountingUnit             is a AccountingUnit           
		LocalGeneralLedgerChartAccount  is a GeneralLedgerChartAccount
		LocalProject                    is a Project                  
		LocalFinanceDimension1          is a FinanceDimension1        
		LocalFinanceDimension2          is a FinanceDimension2        
		LocalFinanceDimension3          is a FinanceDimension3        
		LocalFinanceDimension4          is a FinanceDimension4        
		LocalFinanceDimension5          is a FinanceDimension5        
		LocalFinanceDimension6          is a FinanceDimension6        
		LocalFinanceDimension7          is a FinanceDimension7        
		LocalFinanceDimension8          is a FinanceDimension8        
		LocalFinanceDimension9          is a FinanceDimension9        
		LocalFinanceDimension10         is a FinanceDimension10
		
	Field Rules
		TrueCostResourceIntensityFactor
			required
			
		AccountingEntity
			required

		EffectiveDate
			required
			
	Derived Fields
		DimensionCode        is a StringField
            type is Alpha 105
            restricted
            LocalAccountingEntity.DerivedSequenceNumber
            LocalAccountingUnit.DerivedSequenceNumber
            LocalGeneralLedgerChartAccount.DerivedSequenceNumber
            LocalProject.DerivedSequenceNumber
			LocalFinanceDimension1.DerivedSequenceNumber
			LocalFinanceDimension2.DerivedSequenceNumber
			LocalFinanceDimension3.DerivedSequenceNumber
			LocalFinanceDimension4.DerivedSequenceNumber
			LocalFinanceDimension5.DerivedSequenceNumber
			LocalFinanceDimension6.DerivedSequenceNumber
			LocalFinanceDimension7.DerivedSequenceNumber
			LocalFinanceDimension8.DerivedSequenceNumber
			LocalFinanceDimension9.DerivedSequenceNumber
			LocalFinanceDimension10.DerivedSequenceNumber

	Conditions
		HasError
			restricted
			when (ErrorDescription entered)
	Relations
		AccountingEntityRel
			one-to-one relation to AccountingEntity
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	=	TrueCostConfiguration	
				related.AccountingEntity		=	AccountingEntity
		
		TrueCostResourceIntensityFactorRel
			one-to-one relation to TrueCostResourceIntensityFactor
			Field Mapping uses symbolic key
				related.TrueCostConfiguration					= TrueCostConfiguration
				related.TrueCostResourceIntensityFactor			= TrueCostResourceIntensityFactor
		
		TrueCostResourceIntensityFactorDataRel
			one-to-one relation to TrueCostResourceIntensityFactorData
			Field Mapping uses symbolic key
				related.TrueCostConfiguration					= TrueCostConfiguration
				related.TrueCostResourceIntensityFactor			= TrueCostResourceIntensityFactor
				related.TrueCostResourceIntensityFactorData		= DimensionCode
				related.EffectiveDate							= EffectiveDate
			
		RunGroupParameterRel
			one-to-many relation to TrueCostResourceIntensityFactorDataInterface
			Field Mapping uses symbolic key
				related.TrueCostConfiguration										= LocalTrueCostConfiguration
			Instance Selection
				where(related.TrueCostResourceIntensityFactorDataInterface.RunGroup	= LocalRunGroup)
		CopyRunGroupRel
			one-to-many relation to TrueCostResourceIntensityFactorDataInterface
			Field Mapping uses symbolic key
				related.TrueCostConfiguration										= LocalTrueCostConfiguration
			Instance Selection
				where(related.TrueCostResourceIntensityFactorDataInterface.RunGroup = LocalRunGroup)
				
	Actions
		Create is a Create Action
			Entrance Rules			
			Action Rules
						
		Update is an Update Action
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted
		
		InterfaceWeightData		is a Set Action
			Parameters				
				PrmTrueCostConfiguration 		is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroup						is a RunGroup
					default label is "RunGroup"
						
			Parameter Rules		
				PrmTrueCostConfiguration
					required					
				PrmRunGroup
					required
					LocalTrueCostConfiguration	=	PrmTrueCostConfiguration
					LocalRunGroup				=	PrmRunGroup
					constraint(RunGroupParameterRel exists)
						"RunGroupDoesNotExist"
			
			Instance Selection				
				where(TrueCostConfiguration = TrueCostConfiguration
				and	TrueCostResourceIntensityFactorDataInterface.RunGroup = PrmRunGroup)
			
			Local Fields
				LocalTotalCount		is Numeric 12
				LocalSuccessCount	is Numeric 12
				LocalFailedCount	is Numeric 12
				LocalActor			is an Actor
						
			Action Rules
				Set Rules
					Exit Rules
						LocalFailedCount = LocalTotalCount - LocalSuccessCount
						LocalActor = actor
						send notification
							to LocalActor
							description is "ImportTo_True\Cost_Weight\DataHasCompleted"
							priority is high
							detail is "NumberOfSuccessfulRecords<LocalSuccessCount>.___NumberOfFailedRecords<LocalFailedCount>___TotalRecordsProcessed<LocalSuccessCount>"
				
				Instance Rules
					LocalTotalCount += 1
					initialize ErrorDescription
				

					LocalAccountingEntity			= AccountingEntity			
					LocalAccountingUnit             = AccountingUnit           
					LocalGeneralLedgerChartAccount  = GeneralLedgerChartAccount
					LocalProject                    = Project                  
					LocalFinanceDimension1          = FinanceDimension1        
					LocalFinanceDimension2          = FinanceDimension2        
					LocalFinanceDimension3          = FinanceDimension3        
					LocalFinanceDimension4          = FinanceDimension4        
					LocalFinanceDimension5          = FinanceDimension5        
					LocalFinanceDimension6          = FinanceDimension6        
					LocalFinanceDimension7          = FinanceDimension7        
					LocalFinanceDimension8          = FinanceDimension8        
					LocalFinanceDimension9          = FinanceDimension9        
					LocalFinanceDimension10         = FinanceDimension10 

					invoke Create TrueCostResourceIntensityFactorData
						resume on error
							ErrorDescription = error message
						invoked.TrueCostConfiguration					= TrueCostConfiguration
						invoked.TrueCostResourceIntensityFactor			= TrueCostResourceIntensityFactor
						invoked.TrueCostResourceIntensityFactorData		= DimensionCode
						invoked.AccountingEntity						= AccountingEntity
						invoked.AccountingUnit							= AccountingUnit
						invoked.GeneralLedgerChartAccount				= GeneralLedgerChartAccount
						invoked.Project									= Project
						invoked.FinanceDimension1						= FinanceDimension1
						invoked.FinanceDimension2						= FinanceDimension2
						invoked.FinanceDimension3						= FinanceDimension3
						invoked.FinanceDimension4						= FinanceDimension4
						invoked.FinanceDimension5						= FinanceDimension5
						invoked.FinanceDimension6						= FinanceDimension6
						invoked.FinanceDimension7						= FinanceDimension7
						invoked.FinanceDimension8						= FinanceDimension8
						invoked.FinanceDimension9						= FinanceDimension9
						invoked.FinanceDimension10						= FinanceDimension10
						invoked.EffectiveDate							= EffectiveDate 
						invoked.Value									= Value
							
					if (ErrorDescription not entered)
						invoke Delete
						LocalSuccessCount += 1
								
		DeleteAllTransactionsForRunGroup is a Set Action
			Parameters
				PrmTrueCostConfiguration				is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroup						is a RunGroup
					default label is "RunGroup"
			Parameter Rules
				PrmRunGroup
					required
					LocalTrueCostConfiguration	= PrmTrueCostConfiguration
					LocalRunGroup				= PrmRunGroup				
					constraint(RunGroupParameterRel exists)
						"RunGroupDoesNotExist"
			Instance Selection				
				where(TrueCostConfiguration 								= TrueCostConfiguration
				and	TrueCostResourceIntensityFactorDataInterface.RunGroup 	= PrmRunGroup)
			
			Action Rules
				Instance Rules
					invoke Delete		
					
		CopyRunGroup					is a Set Action
			
			Parameters
				PrmTrueCostConfiguration				is a TrueCostConfiguration
					default label is "True\CostConfiguration"
				PrmRunGroupFrom			is a RunGroup
					default label is "CopyFromRunGroup"
				PrmRunGroupTo			is a RunGroup
					default label is "CopyToRunGroup"
					
			Parameter Rules
				PrmTrueCostConfiguration
					required
				PrmRunGroupFrom
					required
					LocalTrueCostConfiguration	=	PrmTrueCostConfiguration
					LocalRunGroup				=	PrmRunGroupFrom
					constraint (RunGroupParameterRel exists)
						"SourceRunGroupDoesNotExist"
					constraint (PrmRunGroupFrom != PrmRunGroupTo)
						"CopyFromRunGroupCannotBeTheSameAsCopyToRunGroup"
				PrmRunGroupTo
					required
					
			Instance Selection
				where (TrueCostConfiguration = TrueCostConfiguration
				and TrueCostResourceIntensityFactorDataInterface.RunGroup = PrmRunGroupFrom)
				
			Action Rules
				
				Instance Rules
					invoke Create TrueCostResourceIntensityFactorDataInterface
						fill in fields from this instance
							except invoked.RunGroup
						invoked.RunGroup				= PrmRunGroupTo
					
