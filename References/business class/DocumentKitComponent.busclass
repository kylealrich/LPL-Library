DocumentKitComponent is a BusinessClass
    owned by wh
    prefix is CKT
    classic name is COCOMP

	Ontology
		symbolic key is DocumentKitComponent
			classic set name is CKTSET1
			classic name for DocumentKitComponent.DocumentNumber is DOC-NBR
			classic name for DocumentKitComponent.LineNumber is LINE-NBR
			classic name for DocumentKitComponent.ComponentSequence is COMPONENT-SEQ

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        DocumentNumberNumeric
            classic name is DOC-NBR-NUM
        OptSeq                is a WarehouseSequence
        Item
        OptionCode            is AlphaUpper size 1
            classic name is OPTION-CD
            States
                ReviewRequired     value is "R"
                OptionDoesNotExist value is "N"
                OptionExists       value is "E"
        Quantity

    Derived Fields

        DisplayQuantity  	is a DerivedField
            type is like Quantity
            	precision is Item.NumberOfDecimalsQuantity
        	restricted
            classic name is DISP-QTY
            return (Quantity * CustomerOrderLineRel.OrderQuantity * CustomerOrderLineRel.SellToStock)
        
        ReviewRequiredMessage is a MessageField
        	restricted
        	"ReviewRequired"
        	
        OptionSelectedMessage is a MessageField
        	restricted
        	"OptionSelected"
        	
        DerivedOptionCode	is a DerivedField
        	type is AlphaUpper size 50
        	if (OptionCode.ReviewRequired)
        		return ReviewRequiredMessage
        	else
        	if (OptionCode.OptionExists)
        		return OptionSelectedMessage
        		
    Conditions

        IsBinTracked
        	restricted
            when (ItemLocationRel.BinTracked)

        ReviewRequired
        	restricted
            classic name is REVIEW-REQD
            when (OptionCode.ReviewRequired)

    Relations

        CustomerOrderLineRel
            one-to-one relation to CustomerOrderLine
            Field Mapping uses symbolic key
                related.Company           = Company
                related.CustomerOrder     = DocumentKitComponent.DocumentNumber 
                related.CustomerOrderLine = DocumentKitComponent.LineNumber

        BillingCompanyRel
            one-to-one relation to BillingCompany
            Field Mapping uses symbolic key
                related.Company = Company

        ItemLocationRel
            one-to-one relation to ItemLocation
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = CustomerOrderLineRel.InventoryLocation
                related.Item              = Item

        KititemRel
            one-to-one relation to Item
            Field Mapping uses symbolic key
                related.ItemGroup = Company.ItemGroup
                related.Item      = CustomerOrderLineRel.Item

    Sets

        Set2
            indexed
            Instance Selection
                where (ReviewRequired)
            Sort Order
                Company
                DemandSystemCode
                DocumentKitComponent.DocumentNumber
                DocumentKitComponent.LineNumber
                DocumentKitComponent.ComponentSequence

        Set4
            indexed
            Sort Order
                Company
                DemandSystemCode
                DocumentKitComponent.DocumentNumber
                DocumentKitComponent.ComponentSequence
                DocumentKitComponent.LineNumber

	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
			
		UpdateOptionCode is an Instance Action
			default label is "Update"	
			Action Rules
				if (OptionCode.ReviewRequired)
					OptionCode = "E"
					
		Delete is a Delete Action
			restricted
