ContractGPOParticipant is a BusinessClass
	owned by po
	
	prefix is CXPX
	
	Ontology
		symbolic key is ContractGPOParticipant
	
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       		
	Persistent Fields
		GPOMembership	                      is Alpha 20                      
		MembershipName                        is Alpha 100                     
		ItemSupplier                          is like GPOSupplierID                 
		GPOContractNumber                                                      
		TierLevel                             is like ContractImportTier       
		StartDate                             is Date                          
		EndDate                               is Date	                       



		TierAccessID                          is Numeric 1                     
			States
				Unrestricted                  value is 1
				Restricted                    value is 2
				Exclusive                     value is 3
		COID                                  is Alpha 7                      
		TierDefinition                        is Text                         
		MembershipEligibleDate                is Date                         
		RecordModifiedTimestamp               is Alpha 20		              
		TierName                              is Alpha size 192                
	
	Local Fields
		LocalContractGroup                    is a ContractGroup
		LocalGPOContractNumber                is a GPOContractNumber
		LocalContractImport                   is like ContractImport
		LocalCompany                          is like InventoryCompany
		LocalLocation                         is like InventoryLocation
		LocalRequestingLocation               is like RequestingLocation
		LocalPricingGroup                     is like PricingGroup
		LocalSupplier                         is like Supplier
		LocalManufacturerSupplier             is like GPOSupplierID
		LocalStartDate                        is Date
		LocalEndDate                          is Date
		LocalMarkupPercent                    is a Percent
		LocalDistributorContract              is like Contract
		LocalContract                         is like Contract
	
	Conditions
		HasDistributorRecord
			restricted
			when (ContractGPODistributorRel exists)
	
		HasDistributorOnlyRecord
			restricted
			when (ContractGPODistributorOnlyRel exists)

		ContractImportTierDistributorRelExists
			restricted
			when (ContractImportTierDistributorRel exists)

		ContractImportTierMemberRelExists
			restricted
			when (ContractImportTierMemberRel exists)
				
		ContractImportTierMemberByLocationRelExists
			restricted
			when (ContractImportTierMemberByLocationRel exists)

		HasDistributorMemberOnlyRecord
			restricted
			when (ContractGPODistributorMappedMemberManufacturerRel exists)

		HasSomeDistributorRecord
			restricted
			when (HasDistributorRecord
			or    HasDistributorOnlyRecord
			or    HasDistributorMemberOnlyRecord)

		MappedToMember
			when (ContractImportGPOMembershipRel.ParticipantEntered)
			
		MappedToSupplier
			restricted
			when (first ContractImportGPOSupplierRel.SupplierMapped)
		
		MappedToSupplierOrNoLines
			restricted
			when (first ContractImportGPOSupplierRel.SupplierMapped
			or    ContractGPOItemRel !exists)
	
		HasMixedTiers  
			restricted
			when (MixedTiersRel exists)
		
		InterfaceContractExists
			restricted
			when (ContractImportRel exists)
	
	Relations
	
		MixedTiersRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber                 = GPOContractNumber
			Instance Selection
				where (related.TierLevel                 != TierLevel
				and    related.ContractGPOParticipant    != ContractGPOParticipant)

		ContractImportTierMemberRel
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByMembership
				related.ContractGroup                     = LocalContractGroup
				related.GPOContractNumber                 = LocalGPOContractNumber
				related.ContractImportGPOMembership       = GPOMembership
				
		ContractImportTierMemberRelWithLocation
			one-to-many relation to ContractImportTierMember
			Field Mapping uses ByMembership
				related.ContractGroup                     = LocalContractGroup
				related.GPOContractNumber                 = LocalGPOContractNumber
			Instance Selection
				where (related.ContractImportTierMember.Company    		= LocalCompany
				and    related.ContractImportTierMember.Location       	= LocalLocation
				and    related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				and    related.ContractImportTierMember.PricingGroup   	= LocalPricingGroup)							

		ContractImportTierMemberByLocationRel
			one-to-one relation to ContractImportTierMember
			Field Mapping uses symbolic key
				related.ContractGroup                     		= LocalContractGroup
				related.ContractImport                    		= LocalContractImport
				related.ContractImportTierMember.Company    	= LocalCompany
				related.ContractImportTierMember.Location       = LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   = LocalPricingGroup

		ContractImportTierDistributorRel
			one-to-many relation to ContractImportTierDistributor
			Field Mapping uses symbolic key
				related.ContractGroup                     		= LocalContractGroup
				related.ContractImport                    		= LocalContractImport
				related.ContractImportTierMember.Company    	= LocalCompany
				related.ContractImportTierMember.Location       = LocalLocation
				related.ContractImportTierMember.RequestingLocation = LocalRequestingLocation
				related.ContractImportTierMember.PricingGroup   = LocalPricingGroup                  
				related.ContractImportTierDistributor           = LocalSupplier 

		ContractDistributorPricingMemberRel
			one-to-many relation to ContractDistributorPricingMember
			Field Mapping uses symbolic key
				related.ContractGroup                     						= LocalContractGroup
				related.Contract                    							= LocalDistributorContract
				related.ContractDistributorPricingMember.Company                = LocalCompany
				related.ContractDistributorPricingMember.Location       		= LocalLocation
				related.ContractDistributorPricingMember.RequestingLocation 	= LocalRequestingLocation
				related.ContractDistributorPricingMember.PricingGroup   		= LocalPricingGroup                  
				related.ContractDistributorPricingMember.ManufacturerContract	= LocalContract

		ContractImportDistributorRel
			one-to-many relation to ContractImportDistributor
			Field Mapping uses ByDistributorSupplier
				related.ContractGroup                    = LocalContractGroup
				related.GPODistributorSupplier           = ItemSupplier
				related.ContractImport                   = LocalContractImport

		ContractImportDistributorDuplicateRel
			one-to-many relation to ContractImportDistributor
			Field Mapping uses symbolic key
				related.ContractGroup                    = LocalContractGroup
				related.ContractImport                   = LocalContractImport
				related.ContractImportDistributor        = LocalSupplier 

		ContractGPODistributorRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByGPOContractMemberSupplierNumber
				related.GPOContractNumber                 = GPOContractNumber
				related.ContractImportGPOMembership       = GPOMembership
				related.ItemSupplier                      = ItemSupplier                

		ContractGPODistributorOnlyRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses BySupplierOnly
				related.ItemSupplier                      = ItemSupplier
				related.ContractImportGPOMembership       = GPOMembership
		
		ContractGPODistributorMappedMemberManufacturerRel
			one-to-many relation to ContractGPODistributor
			Field Mapping uses ByMember
				related.ContractImportGPOMembership       = GPOMembership  
			Instance Selection
				where (related.NoContractNumber
				and    related.ManufacturerSupplierEntered
				and    related.ManufacturerSupplier        = LocalManufacturerSupplier)

		ContractImportGPOMembershipRel
			one-to-one relation to ContractImportGPOMembership
			Field Mapping uses symbolic key
				related.ContractImportGPOMembership       = GPOMembership
				
		ContractImportGPOSupplierRel
			one-to-many relation to ContractImportGPOSupplier
			Field Mapping uses BySupplierFirst
				related.ContractImportGPOSupplier         = ItemSupplier
				
		ContractGPOItemRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses BySupplier
				related.GPOContractNumber                 = GPOContractNumber
				related.ItemSupplier                      = ItemSupplier

		ContractImportRel
			one-to-many relation to ContractImport
			Field Mapping uses ByGPOContractNumber
				related.GPOContractNumber                 = GPOContractNumber
	
		ContractGPOTierRel  
			one-to-many relation to ContractGPOTier
			Field Mapping uses ByTierLevel
				related.TierLevel                         = TierLevel
	
		ContractGroupRel
			one-to-one relation to ContractGroup
			Field Mapping uses symbolic key
				related.ContractGroup                     = LocalContractGroup

	Sets
		ByGPOContractNumber
			Sort Order
				GPOContractNumber
				GPOMembership
				ItemSupplier
				ContractGPOParticipant
	
		ByMember
			Sort Order
				GPOMembership
				ContractGPOParticipant

		ByItemSupplier
			Sort Order
				GPOContractNumber
				ItemSupplier
				ContractGPOParticipant

		ByGPOContractNumberAndDate
			Sort Order
				GPOContractNumber
				GPOMembership
				ItemSupplier
				ContractGPOParticipant
				StartDate
				
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Delete

        FindNewParticipants is a Set Action
        	restricted
        	Parameters
        		ParmContractGroup    		is a ContractGroup
        		ParmGPOSupplier     		is like Supplier
        		ParmGPOContractNumber       is a GPOContractNumber
        		ParmContractImport          is a ContractImport
				StandardMarkupCalculation   is Boolean

        	Instance Selection
        		where (GPOContractNumber 				= ParmGPOContractNumber)

			Action Rules
				Instance Rules
					LocalContractGroup 		= ParmContractGroup
					LocalGPOContractNumber 	= ParmGPOContractNumber
					LocalContractImport     = ParmContractImport
					LocalCompany            = ContractImportGPOMembershipRel.ParticipantLocation.Company
					LocalLocation           = ContractImportGPOMembershipRel.ParticipantLocation.Location
					LocalRequestingLocation = ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation
					LocalPricingGroup       = ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup
					LocalSupplier           = first ContractImportGPOSupplierRel.ContractSupplier 
					LocalManufacturerSupplier	= first ContractImportRel.ManufacturerSupplier
					if (MappedToMember)
						if (ContractImportTierMemberRel !exists
						and ContractImportTierMemberByLocationRel !exists)
							invoke IndirectCreate ContractImportTierMember
								invoked.ContractGroup            					= ParmContractGroup
								invoked.ContractImport           					= ParmContractImport
								invoked.ContractImportTierMember.Company 			= ContractImportGPOMembershipRel.ParticipantLocation.Company
								invoked.ContractImportTierMember.Location 			= ContractImportGPOMembershipRel.ParticipantLocation.Location
								invoked.ContractImportTierMember.RequestingLocation = ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation
								invoked.ContractImportTierMember.PricingGroup 		= ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup
								invoked.Tier          	         					= TierLevel
								invoked.LocalTierName                               = TierName
								invoked.LocalTierDescription                        = TierDefinition								
								invoked.ContractImportGPOMembership 				= GPOMembership
								invoked.GPOContractNumber        					= ParmGPOContractNumber
	                            invoked.EffectiveDate            					= StartDate
	                            invoked.ExpirationDate           					= EndDate
	                            invoked.FromNewParticipant                          = true

	                    else                                    
						if ((ContractImportTierMemberRel exists
						or   ContractImportTierMemberByLocationRel exists)
						and  LocalManufacturerSupplier != ItemSupplier
						and  MappedToSupplier
						and  first ContractImportRel.IsManufacturerContract
						and  first ContractImportRel.DoNotCreateDistributorContract = false
						and  ContractGPOItemRel exists
						and  ContractImportTierDistributorRel !exists
						and  HasSomeDistributorRecord)
							if (HasDistributorRecord)
								LocalStartDate = first ContractGPODistributorRel.StartDate
								LocalEndDate   = first ContractGPODistributorRel.EndDate
								if (ContractGroupRel.GPOIsHealthTrust
								or  ContractGroupRel.GPOIsHealthTrust2021)
        							LocalMarkupPercent  = first ContractGPODistributorRel.MarkupPercent/100 										
							else
							if (HasDistributorOnlyRecord)
								LocalStartDate = first ContractGPODistributorOnlyRel.StartDate
								LocalEndDate   = first ContractGPODistributorOnlyRel.EndDate
								if (ContractGroupRel.GPOIsHealthTrust
								or  ContractGroupRel.GPOIsHealthTrust2021)
        							LocalMarkupPercent  = first ContractGPODistributorRel.MarkupPercent/100 									
							else
							if (ContractGroupRel.GPOIsVizient
							and HasDistributorMemberOnlyRecord)
								LocalStartDate = first ContractGPODistributorMappedMemberManufacturerRel.StartDate
								LocalEndDate   = first ContractGPODistributorMappedMemberManufacturerRel.EndDate
							if (ContractImportDistributorDuplicateRel !exists)
								invoke Create ContractImportDistributor
									invoked.ContractGroup                                   = LocalContractGroup
									invoked.ContractImport                                  = LocalContractImport
			            			invoked.ContractImportDistributor                       = LocalSupplier
									invoked.GPODistributorSupplier                          = ItemSupplier
									invoked.EffectiveDate                                   = LocalStartDate
		            				invoked.ExpirationDate                                  = LocalEndDate
									invoked.StandardMarkupCalculation                       = StandardMarkupCalculation
							invoke Create ContractImportTierDistributor
								invoked.ContractGroup                                   = LocalContractGroup
								invoked.ContractImport                                  = LocalContractImport
								invoked.ContractImportTierMember.Company                = LocalCompany                         						
		            			invoked.ContractImportTierMember.Location               = LocalLocation 
		            			invoked.ContractImportTierMember.RequestingLocation     = LocalRequestingLocation
		            			invoked.ContractImportTierMember.PricingGroup		    = LocalPricingGroup
		            			invoked.ContractImportTierDistributor                   = LocalSupplier
        						invoked.ContractImportGPOMembership                     = GPOMembership
        						if (ContractGroupRel.GPOIsHealthTrust)
        							invoked.MarkupPercent                               = LocalMarkupPercent 
							if (ContractImportTierMemberRel.PurchaseMethod = 1)
								invoke Update ContractImportTierMemberRel
									invoked.PurchaseMethod = 3
							if (ContractImportDistributorRel.DistributorContractNumber entered
							and !ContractImportDistributorRel.DistributorContractNumber.ContractStatus.Closed
							and  ContractImportDistributorRel.LinesExist
							and ((LocalMarkupPercent !entered)
							or  (LocalMarkupPercent entered
							and  LocalMarkupPercent = ContractImportDistributorRel.MarkupPercent)))
								LocalDistributorContract = ContractImportDistributorRel.DistributorContractNumber























								if (ContractImportTierMemberRel.ContractTierMemberRel.PurchaseMethod = 1)
									invoke UpdateContractTierMember ContractImportTierMemberRel.ContractTierMemberRel
										invoked.PurchaseMethod = 3
								
								LocalContract = first ContractImportRel.DerivedToContract
								if (ContractDistributorPricingMemberRel !exists)
									invoke Create ContractDistributorPricingMember 
					            		invoked.ContractGroup								 			= LocalContractGroup
					            		invoked.Contract									 			= LocalDistributorContract
					            		invoked.ContractDistributorPricingMember.Company	 			= LocalCompany
					            		invoked.ContractDistributorPricingMember.Location	 			= LocalLocation
					            		invoked.ContractDistributorPricingMember.RequestingLocation		= LocalRequestingLocation
					            		invoked.ContractDistributorPricingMember.PricingGroup           = LocalPricingGroup
					            		invoked.ContractDistributorPricingMember.ManufacturerContract 	= first ContractImportRel.DerivedToContract
					            		invoked.MfgContractTierInfo.MfgContract				 			= first ContractImportRel.DerivedToContract
					            		if (ContractGroupRel.GPOIsVizient
					            		or  ContractGroupRel.GPOIsHealthTrust)
				            				invoked.MfgContractTierInfo.Tier				 			= TierLevel
				            			else
				            			if (ContractGroupRel.GPOIsHealthTrust2021)
				            				invoked.MfgContractTierInfo.Tier                           = ContractImportTierMemberRel.Tier
				            			invoked.PricingIdentifier                                   	= ContractImportDistributorRel.DistributorPricingCode
					            		invoked.FromImport                                              = true	 
										
								if (first ContractImportTierMemberRel.ContractTierMemberRel.Released)
									invoke ExternalReleaseMembers ContractImportDistributorRel.DistributorContractNotLocalRel

						else
						if  (ContractGroupRel.GPOIsVizient
						and  ContractImportTierMemberRel exists
						and  ContractGPOParticipant 					= ItemSupplier
						and  ContractImportTierMemberRel.EffectiveDate 	= StartDate
						and  ContractImportTierMemberRel.Tier          != TierLevel)
							invoke Update ContractImportTierMemberRel
								invoked.Tier = blank
							
		ProcessNewMembers is a Set Action
			restricted
			Parameters
				ParmContractGroup   is a ContractGroup
				ParmGPOMembership   is a ContractImportGPOMembership
				AutoUpdateContracts is Boolean
				
			Instance Selection
				where (((ParmGPOMembership !entered)
				or      (ParmGPOMembership entered
				and      ParmGPOMembership = GPOMembership))
				and      InterfaceContractExists
				and 	 ParmContractGroup = first ContractImportRel.ContractGroup
				and      MappedToMember)  
				
			Action Rules
			
				Instance Rules

					LocalContractGroup   	= ParmContractGroup
					LocalGPOContractNumber  = GPOContractNumber
					LocalCompany            = ContractImportGPOMembershipRel.ParticipantLocation.Company
					LocalLocation           = ContractImportGPOMembershipRel.ParticipantLocation.Location
					LocalRequestingLocation = ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation
					LocalPricingGroup       = ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup
					if (ContractImportTierMemberRel !exists
					and ContractImportTierMemberRelWithLocation !exists)

						invoke IndirectCreate ContractImportTierMember
							invoked.ContractGroup     								= ParmContractGroup
							invoked.ContractImport    								= first ContractImportRel.ContractImport
							invoked.ContractImportTierMember.Company                = ContractImportGPOMembershipRel.ParticipantLocation.Company
							invoked.ContractImportTierMember.Location               = ContractImportGPOMembershipRel.ParticipantLocation.Location
							invoked.ContractImportTierMember.RequestingLocation     = ContractImportGPOMembershipRel.ParticipantLocation.RequestingLocation
							invoked.ContractImportTierMember.PricingGroup           = ContractImportGPOMembershipRel.ParticipantLocation.PricingGroup
							invoked.Tier                                            = TierLevel  
							invoked.LocalTierName                                   = TierName
							invoked.LocalTierDescription	                        = TierDefinition
							invoked.ContractImportGPOMembership                     = GPOMembership 
							invoked.GPOContractNumber                               = GPOContractNumber  				
 							invoked.EffectiveDate            						= StartDate
                            invoked.ExpirationDate           						= EndDate
							invoked.Updated                                         = true
							invoked.FromNewMember                                   = AutoUpdateContracts
                        	if (ParmGPOMembership !entered)
                        		invoked.MultipleMembers                             = true                               

					else
					if  (ParmContractGroup.GPOIsVizient
					and  ContractImportTierMemberRel exists
					and  ContractGPOParticipant 						= ItemSupplier
					and  ContractImportTierMemberRel.EffectiveDate 		= StartDate
					and  ContractImportTierMemberRel.Tier          	   != TierLevel)
						invoke Update ContractImportTierMemberRel
							invoked.Tier = blank					
				
		FindNewMembers is a Set Action
			restricted
			Parameters
				ContractGroup
				
			Action Rules
			
				Instance Rules
				
					if (ContractImportGPOMembershipRel !exists)
						invoke Create ContractImportGPOMembership
							invoked.ContractGroup     			= ContractGroup
							invoked.ContractImportGPOMembership = GPOMembership
							invoked.Name                        = MembershipName

		FixGPOContractNumber is a Set Action
			default label is untranslatable
			restricted
			
			Instance Selection
				where (GPOContractNumber = "NULL")

			Action Rules
				
				Instance Rules
					initialize GPOContractNumber
	
