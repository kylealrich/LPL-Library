ContractGPOSupplier is a BusinessClass
	owned by po

	
	prefix is CXXX
	
	Ontology
		symbolic key is ContractGPOSupplier
		
	Patterns
        disable AuditIndex
		disable Auditing 
       	disable EffectiveDated
       		
	
	Persistent Fields
		GPOContractNumber                                          
		GPOSupplier                     is a GPOSupplierID             
		Name                            is Alpha 250               
		Address1                        is an AddressLine          
		Address2                        is an AddressLine		   
		Municipality                                               
			default label is "City"
		StateProvince                   is like StateProvince      
			default label is "State"
		PostalCode		                                           
		Active                          is Numeric 1               

		VendorTypeID                    is Numeric 2               
			States
				MfrOnlySuppliesForSelf  			value is 1
				SupplierOnly                    	value is 2
				MfrSuppliesForSelfAndOthers     	value is 3
				ServiceOnly                     	value is 4
				Unknown                         	value is 5
				ReprocDoesNotSupplyOriginalProduct	value is 7
				RemfrDoesSupplyOriginalProduct  	value is 9
				MfrOnlyDoesNotSupplyAnything    	value is 11	
		LegalName                 		is Alpha 250    
		MinorityIndicator               is Boolean     
		ParentVendorNumber              is like Supplier 
		VendorTaxID                     is AlphaUpper size 25 
			holds pii
		WebAddress                      is a URL       
		DoingBusinessAsName             is Alpha 100   
		ContractDistributorEffectiveDate is Date       
		ContractDistributorExpireDate   is Date        
		RecordModifiedTimestamp         is Alpha 20   
			
	Local Fields
		LocalGPOSupplier                is like GPOSupplierID
		LocalSupplierGroup              is like SupplierGroup

	Conditions
		ActiveMismatch
			restricted
			when (ContractImportGPOSupplierRel exists
			and (Active = 1 
			and  ContractImportGPOSupplierRel.Active = false)
			or  (Active = 0
			and  ContractImportGPOSupplierRel.Active = true))
			
		ImportGPOSupplierExists
			restricted
			when (ContractImportGPOSupplierRel exists)
			
		MappedToSupplierOrNoLines
			restricted
			when (ContractImportGPOSupplierRel.SupplierMapped
			or    NoItems
			or    NoParticipants)

		SupplierMapped
			restricted
			when (ContractImportGPOSupplierRel.SupplierMapped)
		
		SupplierMappedForNewDistributor
			restricted
			when (first ContractImportGPONewDistributorRel.SupplierMapped)
		
		NoItems
			restricted
			when (ContractGPOItemRel !exists)
			
		HasItems
			restricted
			when (ContractGPOItemRel exists)
		
		NoParticipants
			restricted
			when (ContractGPOParticipantRel !exists)

	Relations
	
		ContractImportGPOSupplierRel
			one-to-one relation to ContractImportGPOSupplier
			Field Mapping uses symbolic key
				related.SupplierGroup  				= LocalSupplierGroup
				related.ContractImportGPOSupplier   = LocalGPOSupplier
				
		ContractImportGPONewDistributorRel
			one-to-many relation to ContractImportGPOSupplier
			Field Mapping uses BySupplierFirst
				related.ContractImportGPOSupplier   = GPOSupplier

		ContractGPOItemRel
			one-to-many relation to ContractGPOItem
			Field Mapping uses BySupplier
				related.GPOContractNumber           = GPOContractNumber
				related.ItemSupplier                = GPOSupplier		

		ContractGPOParticipantRel
			one-to-many relation to ContractGPOParticipant
			Field Mapping uses ByItemSupplier
				related.GPOContractNumber           = GPOContractNumber
				related.ItemSupplier                = GPOSupplier				

		ContractGroupRel
			one-to-one relation to ContractGroup
			Field Mapping uses symbolic key
				related.ContractGroup               = LocalSupplierGroup

	Sets
		ByGPOSupplier
			Sort Order
				GPOSupplier
				ContractGPOSupplier
				
		ByGPOContractNumber
			Sort Order
				GPOContractNumber
				GPOSupplier
				ContractGPOSupplier
	
	Field Rules
	
	Actions
	
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action
			restricted
		
		MassDelete is a Set Action
			disable checkpoint
			
			Action Rules
			
				Instance Rules
				
					invoke Delete

		LoadAndUpdateSuppliers is a Set Action
			restricted
			Parameters
				SupplierGroup
			
			Action Rules
			
				Instance Rules
					LocalSupplierGroup     = SupplierGroup
					LocalGPOSupplier       = GPOSupplier
					if (ContractImportGPOSupplierRel !exists)
						invoke Create ContractImportGPOSupplier					
							invoked.SupplierGroup      			= SupplierGroup
							invoked.ContractImportGPOSupplier   = GPOSupplier
							invoked.Name               			= Name
							invoked.Address1           			= Address1
							invoked.Address2           			= Address2
							invoked.Municipality       			= Municipality
							invoked.StateProvince      			= StateProvince
							invoked.PostalCode         			= PostalCode
							invoked.TaxID                       = VendorTaxID
					
					else
					if  (ActiveMismatch
					and (ContractGroupRel.GPOUsed = 1
					or   ContractGroupRel.GPOUsed = 2
					or   ContractGroupRel.GPOUsed = 3))
						invoke UpdateFast ContractImportGPOSupplierRel
							if (Active = 1)
								invoked.Active    = true
							else
							if (Active = 0)
								invoked.Active    = false
	
		FixGPOContractNumber is a Set Action
			default label is untranslatable
			restricted
			
			Instance Selection
				where (GPOContractNumber = "NULL")

			Action Rules
				
				Instance Rules
					initialize GPOContractNumber
	
