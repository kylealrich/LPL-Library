PfiTimerQueue is a BusinessClass
    owned by pfi
    prefix is PfiTQ
    default label is "Timer"
    disable data area copy

    Ontology
        symbolic key is PfiTimerQueue

    Patterns
        implements CRUD
        disable AsOfDateProcessing
        disable Auditing
        disable EffectiveDated
        implements IncrementalReplication
            indicator field is IsReplicated
                replicate when false
                    then set to true

    Persistent Fields
        WaitType is Numeric size 2
            States
                Seconds value is 1
                Minutes value is 2
                Hours value is 3
                Days value is 4
        WaitTime is Numeric size 6
            default label is "WaitTime"
        ConfigName	is Alpha size 200
        StartDate    is TimeStamp
        EndDate      is TimeStamp
        Status is Numeric size 1
            States
                Waiting     value is 1
                Completed	value is 2
                Cancelled	value is 3
                Forced      value is 4
        IsReplicated is Boolean
            default label is untranslatable

    Derived Fields

        CurrentTime is a DerivedField
        	type is TimeStamp
        	return system current timestamp

        RemainingTime is a DerivedField   
            type is Numeric 10 
            return (WaitTime - (system current timestamp - StartDate))

        DaysInRemainingTime is a DerivedField    
            type is Numeric 6
            DaysInRemainingTime = ((RemainingTime / 86400) - .5)
            if (DaysInRemainingTime < 0)
                DaysInRemainingTime = 0

        HoursInRemainingTime is a DerivedField    
            type is Numeric 2
            HoursInRemainingTime = (((RemainingTime - (DaysInRemainingTime * 86400)) / 3600) - .5)
            if (HoursInRemainingTime < 0)
                HoursInRemainingTime = 0

        MinutesInRemainingTime is a DerivedField  
            type is Numeric 2
            MinutesInRemainingTime = ((((RemainingTime - (DaysInRemainingTime * 86400)) - (HoursInRemainingTime * 3600)) / 60) - .5)
            if (MinutesInRemainingTime < 0)
                MinutesInRemainingTime = 0

        SecondsInRemainingTime is a DerivedField  
            type is Numeric 2
            SecondsInRemainingTime = ((((RemainingTime - (DaysInRemainingTime * 86400)) - (HoursInRemainingTime * 3600)) - (MinutesInRemainingTime * 60)) - .5)
            if (SecondsInRemainingTime < 0)
                SecondsInRemainingTime = 0



        DaysHoursMinutesSecondsInRemainingTime is a MessageField
            "<DaysInRemainingTime>Days<HoursInRemainingTime>Hours<MinutesInRemainingTime>Minutes<SecondsInRemainingTime>Seconds"

        HoursMinutesSecondsInRemainingTime is a MessageField
            "<HoursInRemainingTime>Hours<MinutesInRemainingTime>Minutes<SecondsInRemainingTime>Seconds"

        MinutesSecondsInRemainingTime is a MessageField
            "<MinutesInRemainingTime>Minutes<SecondsInRemainingTime>Seconds"

        SecondsInRemainingTimeMessage is a MessageField
        	"<SecondsInRemainingTime>Seconds"

        OneSecondInElapsedTimeMessage is a MessageField
            "1Second"

        RemainingTimeComponentsText is a DerivedField
            type is Alpha size 40
            if (DaysInRemainingTime > 0)
                return DaysHoursMinutesSecondsInRemainingTime
            else
            if (HoursInRemainingTime > 0)
                return HoursMinutesSecondsInRemainingTime
            else
            if (MinutesInRemainingTime > 0)
                return MinutesSecondsInRemainingTime
            else
            if (SecondsInRemainingTime > 0)
                return SecondsInRemainingTimeMessage
            else
                if (StartDate != blank)
                    return "< " + OneSecondInElapsedTimeMessage 
                else
                    return ""

        DaysInWaitTime is a DerivedField    
            type is Numeric 6
            DaysInWaitTime = ((WaitTime / 86400) - .5)
            if (DaysInWaitTime < 0)
                DaysInWaitTime = 0

        HoursInWaitTime is a DerivedField    
            type is Numeric 2
            HoursInWaitTime = (((WaitTime - (DaysInWaitTime * 86400)) / 3600) - .5)
            if (HoursInWaitTime < 0)
                HoursInWaitTime = 0

        MinutesInWaitTime is a DerivedField  
            type is Numeric 2
            MinutesInWaitTime = ((((WaitTime - (DaysInWaitTime * 86400)) - (HoursInWaitTime * 3600)) / 60) - .5)
            if (MinutesInWaitTime < 0)
                MinutesInWaitTime = 0

        SecondsInWaitTime is a DerivedField  
            type is Numeric 2
            SecondsInWaitTime = ((((WaitTime - (DaysInWaitTime * 86400)) - (HoursInWaitTime * 3600)) - (MinutesInWaitTime * 60)) - .5)
            if (SecondsInWaitTime < 0)
                SecondsInWaitTime = 0



        DaysHoursMinutesSecondsInWaitTime is a MessageField
            "<DaysInWaitTime>Days<HoursInWaitTime>Hours<MinutesInWaitTime>Minutes<SecondsInWaitTime>Seconds"

        HoursMinutesSecondsInWaitTime is a MessageField
            "<HoursInWaitTime>Hours<MinutesInWaitTime>Minutes<SecondsInWaitTime>Seconds"

        MinutesSecondsInWaitTime is a MessageField
            "<MinutesInWaitTime>Minutes<SecondsInWaitTime>Seconds"

		SecondsInWaitMessage is a MessageField
        	"<SecondsInWaitTime>Seconds"

        WaitTimeComponentsText is a DerivedField
            type is Alpha size 40
            if (DaysInWaitTime > 0)
                return DaysHoursMinutesSecondsInWaitTime
            else
            if (HoursInWaitTime > 0)
                return HoursMinutesSecondsInWaitTime
            else
            if (MinutesInWaitTime > 0)
                return MinutesSecondsInWaitTime
            else
            if (SecondsInWaitTime > 0)
                return SecondsInWaitMessage
            else
                if (StartDate != blank)
                    return "< " + OneSecondInElapsedTimeMessage 
                else
                    return ""

    Sets

        ByStatusTimereQueue
            sql name is PfiTQ01
            indexed
            Sort Order
            	Status
                PfiWorkunit
                PfiActivity

        ByIsReplicatedUniqueID
            sql name is PfiTQ02
            indexed
            Sort Order
                IsReplicated
                UniqueID

    Actions

        Create is a Create Action

        Update is an Update Action

            Exit Rules
                IsReplicated = false

        Delete is a Delete Action
