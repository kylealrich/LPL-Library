VatPurchaseSaleTransaction is a BusinessClass
    owned by tx
    prefix is TAS
    classic name is TXRPTTRANS

    Ontology
        symbolic key is VatPurchaseSaleTransaction
            classic set name is TASSET1
            classic name for VatPurchaseSaleTransaction.SystemCode is SYSTEM-CD
            classic name for VatPurchaseSaleTransaction.TransactionType is TRANS-TYPE
            classic name for VatPurchaseSaleTransaction.VendorCustomer is VEND-CUST

    Patterns
        implements StaticJava
        disable AuditIndex

    Persistent Fields

        ProcessLevel              is like PayablesProcessLevel 
            classic name is PROC-LEVEL
        VoucherNumber			  is AlphaUpper 14				
            classic name is VOUCHER-NBR
        JournalBook			is AlphaUpper 12					
            classic name is JRNL-BOOK-NBR
            restricted
        JournalBookSequenceNumber is a JbkSeqNbr
            classic name is JBK-SEQ-NBR
            restricted
        TransactionCurrency       is a Currency
            classic name is TRAN-CURRENCY
        PostDate                  is Date
        BaseTaxable               is an InternationalAmount
        BaseTaxAmount             is an InternationalAmount
            classic name is BASE-TAX-AMT
        BaseInvoiceAmount         is an InternationalAmount
            classic name is BASE-INV-AMT
        Source

	Transient Fields	 
		PostDateYear				is a Year
			derive value from PostDate year
				
    Context Fields
		PostDateRange			is a DateRange
		
	Local Fields
		LocalStartDate 				is Date
		LocalEndDate				is Date
		BeginPeriod					is a Period
		EndPeriod					is a Period
		LocalBeginYear 				is a TaxCalendar
		LocalBeginPeriod			is a TaxCalendarPeriod
		BeginYear					is a Year
		EndYear						is a Year
		BlankSpace					is Alpha 40
		RecordCount					is Numeric 10
		AccountsReceivableCount		is Numeric 10
		AccountsPayableRecordCount	is Numeric 10
		BillingCount				is Numeric 10
		BillingAndRevenueCount		is Numeric 10	 
		CashLedgerCountInput		is Numeric 10
		CashLedgerCountOutput		is Numeric 10
		TaxRecordCountInput			is Numeric 10	
		TaxRecordCountOutput		is Numeric 10					
		LocalActor					is an Actor		
		LocalProcessLevel           is a ProcessLevel
		RunDateRange				is a DateRange					
		ExitRuleIsAt				is Alpha 9						
		CompletionMessage			is Alpha 50						
		LocalFEG					is like FinanceEnterpriseGroup  
		LocalCreatePurgeReport      is Boolean						
		LocalTaxEntity				is like TaxEntity				
		LocalSystemCode				is like GeneralLedgerSystemCode	
		LocalPrmSource				is AlphaUpper 1					
	        States
	            Both    value is "B"
	            Input   value is "I"
	            Output  value is "O"	
		LocalSource					is AlphaUpper 1					
	        States

	            Input   value is "I"
	            Output  value is "O"	
		LocalEndPeriod				is Alpha 2	
		LocalEndYear				is a Year
		LocalPreviousPeriod			is a TaxCalendarPeriod		
		LocalPeriodDate				is Date										
		LocalStartDate2				is Date
		LocalStartDate3				is Date		
	Conditions
		WithinPostDateRange
			restricted
			when (PostDateRange not entered
			or	 (PostDateRange entered
			and	  PostDate within PostDateRange)
			and  (PostDateYear = PostDate year
			or    PostDateYear not entered))
			
    Relations

        VatPurchaseSaleDistributionsRel
            classic name is TXRPTDIST
            one-to-many relation to VatPurchaseSaleDistribution
            delete cascades
            Field Mapping uses symbolic key

				related.FinanceEnterpriseGroup	= TaxEntity.FinanceEnterpriseGroup
				related.TaxEntity 				= TaxEntity
            Instance Selection
                where (related.VatPurchaseSaleDistribution.SystemCode = VatPurchaseSaleTransaction.SystemCode
                and   related.VatPurchaseSaleDistribution.Document = VatPurchaseSaleTransaction.Document
                and   related.VatPurchaseSaleDistribution.Suffix = VatPurchaseSaleTransaction.Suffix
                and   related.VatPurchaseSaleDistribution.TransactionType = VatPurchaseSaleTransaction.TransactionType)


        VatPurchaseSaleDistributionsRel2	
            one-to-many relation to VatPurchaseSaleDistribution

            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= TaxEntity.FinanceEnterpriseGroup
            Instance Selection
            	where (related.TaxEntity 				= TaxEntity)
            	
       	VatPurchaseSaleDistributionsRel3	
            one-to-many relation to VatPurchaseSaleDistribution

            Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	= TaxEntity.FinanceEnterpriseGroup
            Instance Selection
            	where (related.TaxEntity 								= TaxEntity
            	and    related.VatPurchaseSaleDistribution.SystemCode 	= VatPurchaseSaleTransaction.SystemCode
            	and    related.VatPurchaseSaleDistribution.Document 	= VatPurchaseSaleTransaction.Document)     	

        PreviousTaxCalendarRel
        	one-to-one relation to TaxCalendar
        	Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup 
        		related.TaxCalendar.TaxEntity  = LocalBeginYear.TaxEntity 
        		related.TaxCalendar.TaxYear    = LocalBeginYear.TaxYear - 1 year
            	
        TaxCalendarPeriodRel
        	one-to-many relation to TaxCalendarPeriod
        	Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup	= actor.context.FinanceEnterpriseGroup
        		related.TaxCalendar.TaxEntity	= LocalBeginYear.TaxEntity
        		related.TaxCalendar.TaxYear		= LocalBeginYear.TaxYear
        	Instance Selection
        		where (related.TaxCalendarPeriod = LocalBeginPeriod)

       	TaxCalendarRel								
        	one-to-one relation to TaxCalendar
        	Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup 
        		related.TaxCalendar.TaxEntity  = LocalBeginYear.TaxEntity 
        		related.TaxCalendar.TaxYear    = LocalBeginYear.TaxYear  
        		            	
       	TaxCalendarPreviousPeriodRel	
        	one-to-many relation to TaxCalendarPeriod
        	Field Mapping uses symbolic key
        		related.FinanceEnterpriseGroup		= actor.context.FinanceEnterpriseGroup	
        		related.TaxCalendar.TaxEntity		= LocalBeginYear.TaxEntity	
        		related.TaxCalendar.TaxYear			= LocalBeginYear.TaxYear	
        	Instance Selection
        		where (related.TaxCalendarPeriod	= LocalBeginPeriod - 1)	
        		
		ReceivableProcessLevelRel
			one-to-many relation to ReceivableProcessLevel
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ReceivableProcessLevel = LocalProcessLevel)
				
		PayablesProcessLevelRel
			one-to-many relation to PayablesProcessLevel
			Field Mapping uses symbolic key
			Instance Selection
				where (related.PayablesProcessLevel = LocalProcessLevel)

 		JournalDetailPurgeReportRel									
			one-to-many relation to JournalDetailPurgeReport
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup						= TaxEntity.FinanceEnterpriseGroup	

 		DeleteJournalDetailPurgeReportRel							
			one-to-many relation to JournalDetailPurgeReport
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup						= LocalFEG	
			Instance Selection
				where (related.TaxEntity 							= LocalTaxEntity)
				
       	TaxConfigurationRel
        	one-to-one relation to TaxConfiguration	
        	Field Mapping uses symbolic key
        		related.TaxConfiguration = LocalFEG	
        						
	Derived Fields
		TimestampLabel is a LabelField
			"Date:"	
		BeginDateLabel is a LabelField				
			"BeginDate:"							
		EndDateLabel is a LabelField				
			"EndDate:"								
		CompletionMessageLabel is a LabelField		
			"CompletionMessage:"		            

						
		DerivedTimestamp is a DerivedField
			type is TimeStamp 
			return current timestamp
		PreviousPeriodDisplay is a StringField
			type is Alpha 30
			"PreviousPeriod:_" + LocalPreviousPeriod
		PeriodRangeDisplay is a StringField	  
			type is Alpha 30
			LocalBeginPeriod + " " + LocalBeginYear.TaxYear + " - " + LocalEndPeriod + " " + LocalEndYear
		DateRangeDisplay is a StringField	  											
			type is Alpha 30
			RunDateRange.Begin + " - " + RunDateRange.End  

		DerivedPeriodBeginDate is a DerivedField
			type is Date
			restricted
			return DateAlpha8

		DateAlpha8 is a StringField
			type is Alpha 8
			restricted
			BeginYear						
			"-"
			"01"							
			"-"
			LocalBeginPeriod				
													
		PurgeReportTotals is a StringField	 
			type is RichText
			TaxTableHeader
			PurgeReportHeader
			PurgeReportDetail
			TaxTableEnd
			TotalsHeader
			TotalsSourceInputHeader
			TotalsSourceInputDetail
			TotalsEnd
			
		TaxTableHeader is a StringField	 
			type is RichText
			"<table>"
			
		PurgeReportHeader is a StringField	 
			type is RichText
			"  <tr> " 
			"	<th colspan=2> Journal Detail Purge Report </th> "	
			"  </tr>"
			" <tr> "
			"	<th colspan=2>" TimestampLabel + DerivedTimestamp  "</th> "
			" </tr>"
			" <tr> "
			"	<th colspan=2>" BeginDateLabel + LocalStartDate  "</th> "
			" </tr>"		
			" <tr> "
			"	<th colspan=2>" EndDateLabel + LocalEndDate  "</th> "
			" </tr>"						
			" <tr> "			
			"	<th colspan=2>==================================================================================</th> "				
			" </tr>"
			" <tr> "			
			"	<th colspan=2>" CompletionMessageLabel + CompletionMessage "</th> "				
			" </tr>"			
			" <tr> "			
			"	<th colspan=2; text-align: left;>Parameters</th> "			 	
			" </tr>"
		PurgeReportDetail is a StringField	 
			type is RichText
			"  <tr>                      					"				 
			"	<td text-align: right;>Finance Enterprise Group: </td> 	"
			"	<td>" LocalFEG						"</td>  "			 	
			"  </tr>    								   	"				
			"  <tr>                      					"				 
			"	<td text-align: right;>Tax Entity: </td> 	"
			"	<td>" LocalTaxEntity						"</td>  "			 	
			"  </tr>    								   	"			
			"  <tr>                                        	"			
			"	<td text-align: right;>Process Level: </td>  "
			"	<td>" LocalProcessLevel 					"</td> 	"				
			"  </tr>    								   	"		
			"  <tr>                                        	"									
			"	<td text-align: right;>Source: </td>        "
			"	<td>" LocalPrmSource						"</td> 	"			
			"  </tr>    								   	"
			"  <tr>                                        	"								
			"	<td text-align: right;>Tax Period Range/Year</td>  "
			"	<td>" PeriodRangeDisplay	        "</td>  "			 
			"  </tr>    								   	"	








			"  </tr>    								   	"				 		
			"  <tr>                                        	"										
			"	<td text-align: right;>Create Purge Report</td>  "
			"	<td>" LocalCreatePurgeReport        "</td>  "			
			"  </tr>    								   	"				 	
					
		TaxTableEnd is a StringField	 
			type is RichText
			"</table>"


		TotalsHeader is a StringField	 
			type is RichText
			"<table>   "
			
		TotalsSourceInputHeader is a StringField	 
			type is RichText
			" <h1> "
			"  <tr>                                        	"
			"	<th colspan=2></th>          				"	  
			"  </tr>     									"
			"  <tr>                                        	"
			"	<th colspan=2; text-align: left;>Source: Input</th>          	"	 
			"  </tr>                                       	"
			" </h1> "
		TotalsSourceInputDetail is a StringField	 
			type is RichText

			"  <tr>                                        	"			
			"	<td>Total Accounts Payable Records Purged</td>	"
			"	<td>" AccountsPayableRecordCount"</td>     	"			 	
			"  </tr>    								   	"	
			"  <tr>                                        	"					
			"	<td>Total Cash Ledger Records Purged</td>       "
			"	<td>" CashLedgerCountInput 	   		"</td>  "			
			"  </tr>    								   	"
			"	<td>Total Tax Records Purged</td>              "	
			"	<td>" TaxRecordCountInput 	    	"</td> 	"						
			"  </tr>                                       	"			
			"  <tr>                                        	"			
			"	<td></td>           						"
			"	<td>"BlankSpace						"</td> 	"				
			"  </tr>    								   	"				
			" <h1> 											"
			"  <tr>                                        	"
			"	<th colspan=2; text-align: left;>Source: Output</th>      	"	 
			"  </tr>                                       	"
			" </h1> 										"			
			"  <tr>                                        	"			
			"	<td>TotalAccounts Receivables Records Purged</td>           	"
			"	<td>" AccountsReceivableCount 		"</td> 	"				
			"  </tr>    								   	"		
			"  <tr>                                        	"									
			"	<td>Total Billing Records Purged</td>   "
			"	<td>" BillingCount 					"</td> 	"			
			"  </tr>    								   	"
			"  <tr>                                        	"								
			"	<td>Total Billing And Revenue Records Purged</td>                "
			"	<td>" BillingAndRevenueCount 		"</td>  "			
			"  </tr>    								   	"						
			"  <tr>                                        	"			
			"	<td>Total Tax Records Purged</td>              "	
			"	<td>" TaxRecordCountOutput 	    	"</td> 	"						
			"  </tr>                                       	"
			"  <tr>                                        	"					
			"	<td>Total Cash Ledger Records Purged</td>       "
			"	<td>" CashLedgerCountOutput 		"</td>  "			
			"  </tr>    								   	"
			" <h1> 											"
			"  <tr>                                        	"
			"	<th colspan=2; text-align: left;>===============================</th>"	 
			"  </tr>                                       	"
			" </h1> 										"		
			" <h2> 											"
			"  <tr>                                        	"											
			"	<td>Total Transaction Records Purged</td>   "	
			"	<td>" RecordCount 	    			"</td> 	"						
			"  </tr>                                       	"
			" </h2> 										"
			
		TotalsEnd is a StringField	 
			type is RichText
			"</table>                                      	"

		ComputeYearStartDate is a StringField
			type is Alpha size 10
			LocalBeginYear.TaxYear 
			"0101"
			
		StartOfYear is a DerivedField
			type is Date
			return ComputeYearStartDate
			
	Rule Blocks
		CountRecords	
			if (LocalSource.Input
			or  LocalPrmSource.Both)			
				if (VatPurchaseSaleTransaction.SystemCode = "AP")
					AccountsPayableRecordCount +=1
				if (VatPurchaseSaleTransaction.SystemCode = "TX"
				and LocalSource.Input)
					TaxRecordCountInput	+=1
				if (VatPurchaseSaleTransaction.SystemCode = "CB"
				and LocalSource.Input)
		            CashLedgerCountInput +=1
	              						
			if (LocalSource.Output
			or  LocalPrmSource.Both)			
				if (VatPurchaseSaleTransaction.SystemCode = "AR")
					AccountsReceivableCount	+=1	 
				if (VatPurchaseSaleTransaction.SystemCode = "BR")
					BillingAndRevenueCount +=1
				if (VatPurchaseSaleTransaction.SystemCode = "BL")							
					BillingCount +=1	
					
				if (VatPurchaseSaleTransaction.SystemCode = "TX"
				and LocalSource.Output)						
					TaxRecordCountOutput +=1
										
				if (VatPurchaseSaleTransaction.SystemCode = "CB"
				and LocalSource.Output)
		        	CashLedgerCountOutput +=1
               
		CreateJournalDetailPurgeReport
			invoke Create JournalDetailPurgeReport	
				fill in fields from this instance

				invoked.FinanceEnterpriseGroup	 	= LocalFEG

				invoked.TaxEntity					= LocalTaxEntity
				invoked.CustomerGroup				= CustomerGroup


				invoked.ProcessLevel				= ProcessLevel
				invoked.Source						= LocalSource			
				invoked.BeginYear					= LocalBeginYear.TaxYear	
				invoked.BeginPeriod					= LocalBeginPeriod
				invoked.EndYear						= LocalEndYear
				invoked.EndPeriod					= LocalEndPeriod

				invoked.Results 					= PurgeReportTotals				

    Sets

        Set2
            indexed
            Sort Order

				TaxEntity
                VoucherNumber
                JournalBook
                JournalBookSequenceNumber
                VatPurchaseSaleTransaction.Document
                VatPurchaseSaleTransaction.Suffix
                VatPurchaseSaleTransaction.TransactionType
                VatPurchaseSaleTransaction.VendorCustomer

	Actions
		Create is an Action
		Update is an Action
		
		Delete is a Delete Action
			Entrance Rules
				invoke Delete VatPurchaseSaleDistributionsRel2
				
		Purge is a Purge Action
			restricted
		
		PurgeVatPurchaseSaleTransaction is a Set Action
			default label is "JournalDetailPurge" 
			Parameters 
				PrmFinanceEnterpriseGroup is a FinanceEnterpriseGroup			 
					default label is "FinanceEnterpriseGroup"				
				PrmTaxEntity		is a TaxEntity
					default label is "TaxEntity"						
				PrmProcessLevel is like ProcessLevel
					default label is "ProcessLevel"					
				PrmSource is AlphaUpper size 1
			        States
			            Both    value is "B"
			            Input   value is "I"
			            Output  value is "O"			 
					default label is "Source"								
				PrmBeginYear	is a TaxCalendar
					default label is "BeginYear"
				PrmBeginPeriod	is a TaxCalendarPeriod
					context of PrmBeginYear
					default label is "BeginPeriod"
				PrmEndYear		is a TaxCalendar
					default label is "EndYear"
				PrmEndPeriod	is a TaxCalendarPeriod
					context of PrmEndYear
					default label is "EndPeriod"
				CreatePurgeReport is Boolean									
					default label is "CreatePurgeReport"
			Parameter Rules
				PrmFinanceEnterpriseGroup
					default to actor.context.FinanceEnterpriseGroup
					required			
					LocalFEG = PrmFinanceEnterpriseGroup						
					if (LocalFEG not entered)									
						LocalFEG = TaxEntity.FinanceEnterpriseGroup				
					if (LocalFEG not entered)									
						LocalFEG = actor.context.FinanceEnterpriseGroup 		
					
				PrmTaxEntity  
					required
					LocalTaxEntity = PrmTaxEntity	
					
				PrmSource
					default to PrmSource.Both
					if (PrmProcessLevel entered)
						LocalProcessLevel = PrmProcessLevel
						if (PrmSource.Input)
							constraint (PayablesProcessLevelRel exists)
								"PayablesProcessLevelDoesNotExist"
						else
						if (PrmSource.Output)
							constraint (ReceivableProcessLevelRel exists)
								"ReceivableProcessLevelDoesNotExist"
						else
						if (PrmSource.Both)
							constraint (PayablesProcessLevelRel exists
							or ReceivableProcessLevelRel exists)
								"ReceivableProcessLevelOrPayablesProcessLevelDoesNotExist"
					PrmBeginYear.TaxEntity = PrmTaxEntity
					constraint (PrmBeginYear.TaxYear entered)
						"BeginYearIsRequired"
					LocalBeginYear = PrmBeginYear			
					BeginYear = PrmBeginYear.TaxYear		

					LocalPrmSource = PrmSource 				
					
				PrmBeginPeriod
					required
					LocalBeginPeriod = PrmBeginPeriod
					if (PrmBeginPeriod = 1)
						if (PreviousTaxCalendarRel exists) 
							if (PreviousTaxCalendarRel.last TaxCalendarPeriod set.PeriodEndDate entered)
								LocalStartDate = PreviousTaxCalendarRel.last TaxCalendarPeriod set.PeriodEndDate + 1 day
						else
							LocalStartDate = StartOfYear	
					else
						if (TaxCalendarPeriodRel.PreviousPeriodRel exists)
							if (TaxCalendarPeriodRel.PreviousPeriodRel.PeriodEndDate entered)
								LocalStartDate = TaxCalendarPeriodRel.PreviousPeriodRel.PeriodEndDate + 1 day	 
					if (LocalStartDate not entered)
						LocalStartDate = TaxCalendarPeriodRel.PriorPeriodEndDate
						if (LocalStartDate not entered)	
							if (PrmBeginPeriod = PrmEndPeriod)										 
								LocalStartDate2 = PrmBeginPeriod.PeriodEndDate - 1 month
								if (LocalStartDate2 entered)	 
									LocalStartDate = LocalStartDate2		 
								if (TaxCalendarPreviousPeriodRel exist)
									LocalStartDate3 = TaxCalendarPreviousPeriodRel.PeriodEndDate
							else
							if (PrmBeginPeriod.PeriodEndDate entered)						
								LocalStartDate = PrmBeginPeriod.PeriodEndDate + 1			 
								LocalPeriodDate = PrmBeginPeriod.PeriodEndDate 				 
							LocalRunDateRange.Begin = LocalStartDate						
							LocalBeginPeriod = PrmBeginPeriod								
						LocalPreviousPeriod = PrmBeginPeriod - 1
						LocalPevPeriod = LocalPreviousPeriod																							 
						LocalPriorPeriodEndDate = LocalPevPeriod.PeriodEndDate + 1 day																 
						if (LocalPriorPeriodEndDate entered)
							LocalStartDate = LocalPriorPeriodEndDate
							LocalRunDateRange.Begin = LocalStartDate						
						if (LocalStartDate not entered)
							LocalStartDate = DerivedPeriodBeginDate	

					PrmEndYear.TaxEntity = PrmTaxEntity
					if (PrmEndYear.TaxYear not entered
					and PrmEndPeriod not entered)
						PrmEndYear.TaxYear = PrmBeginYear.TaxYear
						PrmEndPeriod = PrmBeginPeriod
					if (PrmEndYear.TaxYear not entered)
						constraint (PrmEndPeriod not entered)
							"EndingTaxPeriodEntered;MustEnterEndingTaxYear"
					constraint (PrmBeginYear <= PrmEndYear)
						"BeginYearCannotBeGreaterThanEndYear"
					if (LocalStartDate entered)								
						LocalRunDateRange.Begin = LocalStartDate			
				
				PrmEndYear													
					LocalEndYear = PrmEndYear.TaxYear						
						
				PrmEndPeriod
					if (PrmEndPeriod entered
					and PrmBeginYear = PrmEndYear)
						constraint (PrmBeginPeriod <= PrmEndPeriod)
							"BeginPeriodCannotBeGreaterThanEndPeriodIfWithinTheSameYear"
					if (PrmEndPeriod not entered)
						constraint (PrmEndYear.TaxYear not entered)
							"EndingTaxYearEntered;MustEnterEndingTaxPeriod"
					if (PrmEndPeriod.PeriodEndDate entered)
						LocalEndDate = PrmEndPeriod.PeriodEndDate
					LocalRunDateRange.End = LocalEndDate				
					LocalEndPeriod = PrmEndPeriod						

				CreatePurgeReport
					initial value is true
					default to true
										
			Local Fields
				PurgedTransactionsRecordCount is Numeric size 27
				ApCount is Numeric 10												 
				ArCount is Numeric 10										
				BrCount is Numeric 10										
				BlCount is Numeric 10										
				TxCountIn is Numeric 10								
				TxCountOut is Numeric 10				
				CbCountIn is Numeric 10									
				CbCountOut is Numeric 10				
				LocalRunDateRange is a DateRange					 
				LocalDisplayMonitor is RichText					   
				LocalPevPeriod	is a TaxCalendarPeriod
					context of PrmBeginYear
				LocalPriorPeriodEndDate	is Date
								
			Instance Selection
				include deleted records
				where (TaxEntity.FinanceEnterpriseGroup	= PrmFinanceEnterpriseGroup						
				and    TaxEntity = PrmTaxEntity
				and   (Source = PrmSource
				or    (PrmSource.Both  
				and   (Source.InputAccountsPayable or  Source.OutputAccountsReceivable))			 	
				or     PrmSource not entered) 	
				and   (ProcessLevel = PrmProcessLevel
				or     PrmProcessLevel not entered)
				and	  PostDate within LocalRunDateRange)		




			 
			Sort Order

			Accumulators


					
			Action Rules								
				Empty Set Rules
					LocalActor = actor
					send notification
						to LocalActor
						description is "NoRecordsFoundToPurgeFor"
						priority is high

				Set Rules
					Entrance Rules			 
						RunDateRange.Begin = LocalRunDateRange.Begin
						RunDateRange.End = LocalRunDateRange.End
						LocalFEG = PrmFinanceEnterpriseGroup						
						LocalTaxEntity = PrmTaxEntity
						invoke Delete DeleteJournalDetailPurgeReportRel 			
					Exit Rules
						LocalCreatePurgeReport = CreatePurgeReport					
						CompletionMessage ="Journal Detail Purge Completed"		    
						RunDateRange.Begin = LocalRunDateRange.Begin				
						RunDateRange.End = LocalRunDateRange.End					
						AccountsPayableRecordCount = ApCount 			 
						AccountsReceivableCount = ArCount  				 
						BillingAndRevenueCount = BrCount  				 
						BillingCount = BlCount  						 
						TaxRecordCountOutput = TxCountOut  				 
						TaxRecordCountInput	= TxCountIn					 			
						CashLedgerCountInput = CbCountIn  				 
						CashLedgerCountOutput = CbCountOut				 
						RecordCount = PurgedTransactionsRecordCount
						include CreateJournalDetailPurgeReport				 

						LocalActor = actor
						send notification
							to LocalActor
							description is "JournalDetailPurgeHasBeenCompleted"
							priority is high
							detail is "JournalDetailPurgeHasBeenCompletedFor<PurgedTransactionsRecordCount>Records" 





				Instance Rules
					LocalPrmSource = PrmSource  
					LocalSource = Source

					if (LocalPrmSource.Input)
						if (VatPurchaseSaleTransaction.SystemCode = "AP")	 
							ApCount +=1										 
						if (VatPurchaseSaleTransaction.SystemCode = "TX")
							TxCountIn +=1
						if (VatPurchaseSaleTransaction.SystemCode = "CB")
							CbCountIn +=1
			            

					if (LocalPrmSource.Output)
						if (VatPurchaseSaleTransaction.SystemCode = "AR")
							ArCount +=1										 	
						if (VatPurchaseSaleTransaction.SystemCode = "BR")
							BrCount +=1										 
						if (VatPurchaseSaleTransaction.SystemCode = "BL")
							BlCount +=1										 			
						if (VatPurchaseSaleTransaction.SystemCode = "TX")
							TxCountOut +=1													
						if (VatPurchaseSaleTransaction.SystemCode = "CB")
							CbCountOut +=1


					if (LocalPrmSource.Both)
						if (VatPurchaseSaleTransaction.SystemCode = "AP")	 
							ApCount +=1										 	
						if (VatPurchaseSaleTransaction.SystemCode = "AR")
							ArCount +=1										 	
						if (VatPurchaseSaleTransaction.SystemCode = "BL")
							BlCount +=1										 										 
						if (VatPurchaseSaleTransaction.SystemCode = "BR")
							BrCount +=1										 	
							
						if (VatPurchaseSaleTransaction.SystemCode = "CB")
							if (LocalSource.Input)
								CbCountIn +=1	
							else
								CbCountOut +=1
						if (VatPurchaseSaleTransaction.SystemCode = "TX")
							if (LocalSource.Input)
								TxCountIn +=1
							else
								TxCountOut +=1						
				        	
					increment PurgedTransactionsRecordCount
					invoke Purge
		

					
