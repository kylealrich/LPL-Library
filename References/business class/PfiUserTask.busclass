PfiUserTask is a BusinessClass
    owned by pfi
    prefix is PfiUT
    default label is "UserTask"
    framework type is ProcessFlow

    Ontology
        symbolic key is PfiUserTask

    Patterns
        implements CRUD
        implements LightweightAuditing
        disable AuditIndex
        disable AsOfDateProcessing
        disable EffectiveDated
        implements AuditLogEntryActions

    Persistent Fields

        PfiDateRange
        FilterCategory            is Boolean
            default label is "FilterIsEnabled"
        DisplayAll                is Boolean




        UserProfileFilterCategory is Boolean
        NotifyOption is a PfiNotifyOption

    Conditions

        IsGroupTask
            default label is "TaskTypeIsGroup"
            when (PfiTask.TaskType.Group)

        IsUserTaskValid
            default label is "UserTaskIsValid"
            when ((PfiDateRange.StartDate = blank or current date >= PfiDateRange.StartDate)
            and   (PfiDateRange.StopDate = blank or current date < PfiDateRange.StopDate))

        FilteringDisabled
            when ((not FilterCategory) or (not UserProfileFilterCategory))

        IsValidTask
            default label is "TaskIsValid"
            when (PfiUserProfile = actor and IsUserTaskValid)

    Derived Fields

        QueueTaskCount is a DerivedField
            type is Numeric size 5
            default label is "WorkItemCount"
            return instance count of PfiWorkItemRel

        TaskDescription is a DerivedField
            type is Alpha size 100 
            default label is "Task"
            return PfiTask.TaskDescription

        ProxyCount is a DerivedField
            type is Numeric size 5
            return instance count of PfiProxyRel

    Relations

        PfiUserCategoryRel
            one-to-many relation to PfiUserCategory
            Field Mapping uses symbolic key
                related.PfiUserProfile   = PfiUserProfile
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType

        PfiQueueTaskRel
            one-to-many relation to PfiQueueTask
            Field Mapping uses ByTaskWorkunitActivityQueueAssignment
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType

        PfiWorkItemRel
            one-to-many relation to PfiQueueTask
            Field Mapping uses ByTaskWorkunitActivityQueueAssignment
                related.PfiTask.TaskName = PfiTask.TaskName
                related.PfiTask.TaskType = PfiTask.TaskType
            Instance Selection
                where (related.IsCurrentActionWaitingSnapshot
                and    related.IsValidFilter)

        PfiProxyRel
            one-to-many relation to PfiProxy
            Field Mapping uses ByProxiedUserAndTask
                related.PfiProxyUserTask.ProxiedUser = PfiUserProfile
                related.PfiProxyUserTask.ProxiedTask = PfiTask

    Sets

        ByUserProfileStartDateStopDateTask
            sql name is PfiUT01
            indexed
            Sort Order
                PfiUserProfile
                PfiDateRange.StartDate
                PfiDateRange.StopDate
                PfiTask

        ByUserTaskFilterDisabled
            sql name is PfiUT02
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                PfiUserProfile
            Instance Selection
                where (FilteringDisabled)

        ByTaskNameTaskTypeUserProfile
            sql name is PfiUT03
            indexed
            Sort Order
                PfiTask.TaskName
                PfiTask.TaskType
                PfiUserProfile

        ByTaskTypeTaskNameUserProfile
            sql name is PfiUT04
            indexed
            Sort Order
                PfiTask.TaskType
                PfiTask.TaskName
                PfiUserProfile

        ByStartDate
            sql name is PfiUT05
            indexed
            Sort Order
                PfiDateRange.StartDate
                PfiTask
                PfiUserProfile
        
        ByStopDate
            sql name is PfiUT06
            indexed
            Sort Order
                PfiDateRange.StopDate
                PfiTask
                PfiUserProfile

        ByFilterCategory
            sql name is PfiUT07
            indexed
            Sort Order
                FilterCategory
                PfiTask
                PfiUserProfile

        ByNotifyOption
            sql name is PfiUT08
            indexed
            Sort Order
                NotifyOption
                PfiTask
                PfiUserProfile

    Field Rules

        PfiTask
            default PfiTask.TaskType to PfiTask.TaskType.Group

        FilterCategory
            initial value is false

    Actions

        Create is a Create Action

            Action Rules
                UserProfileFilterCategory = PfiUserProfile.FilterCategory

        Update is an Update Action

        Delete is a Delete Action
