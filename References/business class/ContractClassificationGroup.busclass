ContractClassificationGroup is a BusinessClass
	owned by po
	
	prefix is ConCG
	
	Ontology
        symbolic key is ContractClassificationGroup
	
	Patterns
	
	Persistent Fields
		Description
	
	Conditions
		HasClassificationContext
			restricted
			when (ClassificationActorContextRel exists)
			
		HasSubclassificationContext
			restricted
			when (SubclassificationActorContextRel exists)

		HasMembersWithSubclassifications 
			restricted 
			when (MembersWithSubclassificationsRel exists)

		DoesNotHaveMembersWithSubclassifications 
			restricted 
			when (MembersWithSubclassificationsRel !exists)
	
	Relations
		ContextPropertyRel
			one-to-one relation to ContextProperty
			Field Mapping uses ByKeyField
				related.KeyField = "ContractClassificationGroup"
				related.DataArea = parentcontext.dataarea
				
		ActorContextRel
			one-to-many relation to ActorContext
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ContextProperty = ContextPropertyRel.ContextProperty
				and    related.Value 		   = ContractClassificationGroup)
				
		ClassificationPropertyRel
			one-to-one relation to ContextProperty
			Field Mapping uses ByKeyField
				related.KeyField = "ContractClassification"
				related.DataArea = parentcontext.dataarea
				
		ClassificationActorContextRel
			one-to-many relation to ActorContext
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ContextProperty = ClassificationPropertyRel.ContextProperty)
				
		SubclassificationPropertyRel
			one-to-one relation to ContextProperty
			Field Mapping uses ByKeyField
				related.KeyField = "ContractSubclassification"
				related.DataArea = parentcontext.dataarea
				
		SubclassificationActorContextRel
			one-to-many relation to ActorContext
			Field Mapping uses symbolic key
			Instance Selection
				where (related.ContextProperty = SubclassificationPropertyRel.ContextProperty)
		
		ContractClassificationGroupMembersRel
			one-to-many relation to ContractClassificationGroupMember
			Field Mapping uses symbolic key	
				related.ContractGroup         		= ContractGroup
				related.ContractClassificationGroup = ContractClassificationGroup

		MembersWithSubclassificationsRel 
			one-to-many relation to ContractClassificationGroupMember
			Field Mapping uses symbolic key	
				related.ContractGroup         		= ContractGroup
				related.ContractClassificationGroup = ContractClassificationGroup
			Instance Selection 
				where (related.ContractSubclassification entered)	
	Field Rules
		Description
			required
	
