StudentActivitySchool is a BusinessClass
    owned by studentactivities
    prefix is SAOR

    Ontology
    	symbolic key is StudentActivitySchool

 	Patterns
		implements ContextualParent

    Persistent Fields
		SchoolName					is Alpha 60
		Address						is a PostalAddressV2	
			holds pii
		StudentActivitySchoolType
		CurrentPeriod				is a GeneralLedgerClosePeriod	
		DefaultAccounting			is a FinanceCodeBlock
		DefaultBankAccount			is a StudentActivityBankAccount
		StudentActivityBackOfficeFields

    	SchoolLogo					is an Attachment
		AllowReceiptNumberOverride	is Boolean
		LastReceiptNumber			is Numeric 10
		ReceiptDocumentTemplate
		Active
		DefaultAccountingEntity		is an AccountingEntity


	Derived Fields
		JournalDescription is a MessageField
			"<StudentActivitySchool>_<CurrentPeriod>Closing"			

		DistrictJournalDescription is a MessageField
			"StudentActivity<CurrentPeriod>Closing"
			
		ReportDate is a MessageField
			"<current timestamp>"
					
		CurrentReceiptNumber is a DerivedField
			type is Numeric 10
			return (LastReceiptNumber + 1)
			
		SchoolDisplay is a LabelField
			"School:<StudentActivitySchool>"
			
		SchoolNameDisplay is a LabelField
			"SchoolName:<SchoolName>"
			
		CurrentPeriodDisplay is a LabelField
			"CurrentPeriod:<CurrentPeriod.GeneralLedgerCalendarPeriod.Date>"
			
		CurrencyDisplay is a LabelField
			"Currency:<DefaultAccountingEntity.FunctionalCurrency>"
			
		SchoolTypeDisplay is a LabelField
			"SchoolType:<StudentActivitySchoolType>"
			
		AccountingEntityDisplay is a LabelField
			"AccountingEntity:<DefaultAccountingEntity>"
			
		ActivityDisplay is a LabelField
			"Active:<Active>"
			
	Conditions
		SchoolRecordExists
			when (StudentActivitySchool exists)



		
	Sets
		BySchoolType
			Sort Order
				StudentActivityDistrict
				StudentActivitySchoolType
				StudentActivitySchool

	Relations
		CalendarPeriodsRel
    		one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= StudentActivityDistrict
				related.GeneralLedgerCloseConfiguration		= StudentActivityDistrict.SchoolCalendarPeriodGroup
			Instance Selection
    			where (related.GeneralLedgerClosePeriod	< CurrentPeriod)
    			
		PreviousCalendarPeriodRel
    		one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= StudentActivityDistrict
				related.GeneralLedgerCloseConfiguration		= StudentActivityDistrict.SchoolCalendarPeriodGroup
			Instance Selection
    			where (related.GeneralLedgerClosePeriod	< CurrentPeriod)
    			








		NextCalendarPeriodRel
			one-to-many relation to GeneralLedgerClosePeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup				= StudentActivityDistrict
				related.GeneralLedgerCloseConfiguration		= StudentActivityDistrict.SchoolCalendarPeriodGroup
			Instance Selection
    			where (related.GeneralLedgerClosePeriod	> CurrentPeriod)
    			








			
		StudentActivityClosedPeriodRel
			one-to-one relation to StudentActivityClosedPeriod
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup			= StudentActivityDistrict


				related.StudentActivityClosedPeriod		= CurrentPeriod.GeneralLedgerCalendarPeriod









		StudentActivityBalanceRel is a StudentActivityBalance set
			Instance Selection
				where (related.StudentActivityClosedPeriod	= last PreviousCalendarPeriodRel.GeneralLedgerClosePeriod
				and   !related.StudentActivityAccount.Active
				and    related.PeriodEndingBalance entered)
				






		StudentActivityBalanceDetailRel is a StudentActivityBalanceDetail set
			Instance Selection
				where (related.StudentActivitySchool		= StudentActivitySchool
				and    related.StudentActivityClosedPeriod	= last PreviousCalendarPeriodRel.GeneralLedgerClosePeriod
				and   !related.StudentActivityAccount.Active
				and    related.PeriodEndingBalance entered)

		SchoolActivityAccountBalanceRel is a SchoolActivityAccountBalance set
			Instance Selection
				where (related.StudentActivitySchool		= StudentActivitySchool
				and    related.StudentActivityClosedPeriod	= last PreviousCalendarPeriodRel.GeneralLedgerClosePeriod
				and   !related.SchoolActivityAccount.StudentActivityAccount.Active
				and    related.PeriodEndingBalance entered)

		CurrentStudentActivityBalanceRel is a StudentActivityBalance set
			Instance Selection
				where (related.StudentActivityClosedPeriod	= CurrentPeriod)

		CurrentStudentActivityBalanceDetailRel is a StudentActivityBalanceDetail set
			Instance Selection
				where (related.StudentActivitySchool		= StudentActivitySchool
				and    related.StudentActivityClosedPeriod	= CurrentPeriod)

		CurrentSchoolActivityAccountBalanceRel is a SchoolActivityAccountBalance set
			Instance Selection
				where (related.StudentActivitySchool		= StudentActivitySchool
				and    related.StudentActivityClosedPeriod	= CurrentPeriod)
		StudentActivityAccountRel
			one-to-many relation to StudentActivityAccount
			Field Mapping uses symbolic key
				related.StudentActivityDistrict	= StudentActivityDistrict
			Instance Selection
				where (related.Active)

		StudentActivityAccountCodeRel
			one-to-many relation to StudentActivityAccountCode
			Field Mapping uses symbolic key
				related.StudentActivityDistrict	= StudentActivityDistrict
			Instance Selection
				where (related.Active)

		DetailStudentActivityAccountRel
			one-to-many relation to StudentActivityAccount
			Field Mapping uses symbolic key
				related.StudentActivityDistrict	= StudentActivityDistrict
			Instance Selection
				where (related.Active
				and    related.ActivityLevel.Detail)













		
		StudentActivityJournalRel
			one-to-one relation to StudentActivityJournal
			Field Mapping uses symbolic key
				related.StudentActivityDistrict				= StudentActivityDistrict
				related.StudentActivityClosedPeriod			= CurrentPeriod
 				related.StudentActivitySchool				= StudentActivitySchool
 				












			
		SchoolActivityAccountRel is a SchoolActivityAccount set
			Instance Selection
				where (related.SchoolActivityAccount.StudentActivityAccount.ActivityLevel.Detail
				and    related.SchoolActivityAccount.Active)

		GroupSchoolActivityAccountRel is a SchoolActivityAccount set
			Instance Selection
				where (related.SchoolActivityAccount.StudentActivityAccount.ActivityLevel.Group)
				
		StudentRecordRel is a StudentRecord(StudentActivitySchool) set

		StudentActivityGeneralLedgerSystemCodeRel
			one-to-one relation to GeneralLedgerSystemCode
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup                  = StudentActivityDistrict
				related.GeneralLedgerSystemCode                 = "SA"
				
		StudentActivityTransferRel is a StudentActivityTransfer set
				
		StudentActivityVendorRel
			one-to-many relation to StudentActivityVendor
			Field Mapping uses symbolic key
				related.StudentActivityDistrict	= StudentActivityDistrict
				
		StudentActivityClubSponsorRel is a StudentActivityClubSponsor set
				
		SchoolActivityAccountGroupRel is a SchoolActivityAccount set
			Instance Selection
				where (related.SchoolActivityAccount.Active)
				
		StudentActivitySchoolClassRel is a StudentActivitySchoolClass set
		
		StudentActivityBankTransactionRel
			one-to-many relation to StudentActivityBankTransaction					
				Field Mapping uses symbolic key
					related.StudentActivityDistrict = StudentActivityDistrict
					related.StudentActivitySchool	= StudentActivitySchool
				
		StudentActivityBankAccountRel is a StudentActivityBankAccount set
		
		StudentActivityBankStatementRel
			one-to-many relation to StudentActivityBankStatement
				Field Mapping uses symbolic key
					related.StudentActivityDistrict = StudentActivityDistrict
					related.StudentActivitySchool	= StudentActivitySchool
			

	Field Rules
		Address
			if (Address.DeliveryAddress.AddressLine1 entered
			and !Address.Country entered)
				Address.Country = StudentActivityDistrict.EnterpriseGroup.DefaultCountry


		StudentActivityBackOfficeFields
			initial value is StudentActivityDistrict.StudentActivityBackOfficeFields
			default to StudentActivityDistrict.StudentActivityBackOfficeFields

		CurrentPeriod
			if (SchoolClosedPeriod set exist)
				cannot be changed
					"CannotChangeCurrentPeriod;ClosedPeriodsExist"
			else
				required
				
		DefaultAccountingEntity
			initial value is StudentActivityDistrict.AccountingEntity
			required
			





	Attach Rules
		constraint (Active)
			"SchoolIsInactive"
 
 	Actions
 		Create is a Create Action
 		
 		Update is an Update Action
			Exit Rules
				if (DefaultAccounting changed)
					invoke Update SchoolActivityAccount set
						initialize invoked.GLAccount
 		
 		Delete is a Delete Action
 
		ClosePeriod is an Instance Action
				
			Local Fields
				LocalGeneralLedgerCloseConfiguration		is a GeneralLedgerCloseConfiguration
				LocalStudentActivityClosedPeriod			is a StudentActivityClosedPeriod
				LocalStudentActivityAccount					is a StudentActivityAccount
				LocalStudentActivityBalance					is a StudentActivityBalance
				LocalSchoolClosedPeriod						is a SchoolClosedPeriod
				LocalStudentActivitySchoolType				is a StudentActivitySchoolType
				LocalSchoolActivityAccount					is a SchoolActivityAccount
				LocalClosedPeriodBySchoolType				is a ClosedPeriodBySchoolType
				LocalStudentActivityBalanceDetail			is a StudentActivityBalanceDetail
				LocalSchoolActivityAccountBalance			is a SchoolActivityAccountBalance
				LocalStudentActivitySchool					is a StudentActivitySchool
				
			Action Rules
				confirmation required
					"ClosePeriod<CurrentPeriod>Ending<CurrentPeriod.GeneralLedgerCalendarPeriod.Date>?"

				constraint (NextCalendarPeriodRel exists)
					"NextPeriodNotDefinedForGeneralLedgerCloseConfiguration<StudentActivityDistrict.SchoolCalendarPeriodGroup>"
				
				LocalGeneralLedgerCloseConfiguration		= StudentActivityDistrict.SchoolCalendarPeriodGroup
				LocalStudentActivityClosedPeriod			= CurrentPeriod.GeneralLedgerCalendarPeriod
				LocalStudentActivitySchool					= StudentActivitySchool
				
				if (!StudentActivityClosedPeriodRel exists)
					invoke Update LocalStudentActivityClosedPeriod
						invoked.FinanceEnterpriseGroup				= StudentActivityDistrict
						invoked.StudentActivityDistrict				= StudentActivityDistrict
						
					for each DetailStudentActivityAccountRel
						LocalStudentActivityAccount					= each.StudentActivityAccount
						invoke Update LocalStudentActivityBalance
						
					for each StudentActivityBalanceRel
						LocalStudentActivityAccount					= each.StudentActivityAccount
						invoke Update LocalStudentActivityBalance
					
				invoke Create Unreleased StudentActivityJournal
					invoked.StudentActivityDistrict				= StudentActivityDistrict
					invoked.StudentActivityClosedPeriod			= CurrentPeriod.GeneralLedgerCalendarPeriod
					invoked.StudentActivitySchool				= StudentActivitySchool
					invoked.Description							= JournalDescription
					invoked.PostDate							= CurrentPeriod.GeneralLedgerCalendarPeriod.Date
				
				invoke Update LocalSchoolClosedPeriod
				
				if (StudentActivitySchoolType entered)
					LocalStudentActivitySchoolType				= StudentActivitySchoolType
					invoke Update LocalClosedPeriodBySchoolType
				
				for each SchoolActivityAccountRel
					LocalStudentActivityAccount					= each.SchoolActivityAccount.StudentActivityAccount
					invoke Update LocalStudentActivityBalanceDetail
				
				for each StudentActivityBalanceDetailRel
					LocalStudentActivityAccount					= each.StudentActivityAccount
					invoke Update LocalStudentActivityBalanceDetail
				
				for each SchoolActivityAccountRel
					LocalSchoolActivityAccount					= each.SchoolActivityAccount
					invoke Update LocalSchoolActivityAccountBalance
					
				for each SchoolActivityAccountBalanceRel
					LocalSchoolActivityAccount					= each.SchoolActivityAccount
  					invoke Update LocalSchoolActivityAccountBalance
				
				invoke CloseReceiptDetails Deposited StudentActivityReceiptDetail
 					invoked.PrmStudentActivityDistrict	= StudentActivityDistrict
 					invoked.PrmStudentActivitySchool	= StudentActivitySchool
 					invoked.PrmCloseDate				= CurrentPeriod.GeneralLedgerCalendarPeriod.Date

  				invoke CloseBankDetails Released StudentActivityBankTransDetail
 					invoked.PrmStudentActivityDistrict	= StudentActivityDistrict
 					invoked.PrmStudentActivitySchool	= StudentActivitySchool
 					invoked.PrmCloseDate				= CurrentPeriod.GeneralLedgerCalendarPeriod.Date
 
   				invoke CloseTransferDetails Released StudentActivityTransferDetail
 					invoked.PrmStudentActivityDistrict	= StudentActivityDistrict
 					invoked.PrmStudentActivitySchool	= StudentActivitySchool
 					invoked.PrmCloseDate				= CurrentPeriod.GeneralLedgerCalendarPeriod.Date
				
				if (not StudentActivityDistrict.DistrictLevelPosting)
					invoke Release Unreleased StudentActivityJournalRel
					invoke Journalize Released StudentActivityJournalRel
						invoked.SchoolDistrict	= StudentActivityDistrict
						invoked.School			= StudentActivitySchool
						invoked.PostThruDate	= CurrentPeriod.GeneralLedgerCalendarPeriod.Date
					
				CurrentPeriod = first NextCalendarPeriodRel.GeneralLedgerClosePeriod

		ReopenLastClosedPeriod is an Instance Action
			
			Local Fields
				LocalSchoolCalendar					is a SystemCalendar
				LocalSchoolCalendarGroup			is a SystemCalendarPeriodGroup
 				LocalStudentActivityClosedPeriod	is a StudentActivityClosedPeriod
				LocalSchoolClosedPeriod				is a SchoolClosedPeriod
				LocalJournalizeGroup				is a JournalizeGroup

			Entrance Rules
				if (not StudentActivityDistrict.DistrictLevelPosting)
					invoke IncrementLastJournalizeGroup StudentActivityGeneralLedgerSystemCodeRel
			        LocalJournalizeGroup					= StudentActivityGeneralLedgerSystemCodeRel.DerivedJournalizeGroup

 			Action Rules

				confirmation required
					"ReopenPeriod<last PreviousCalendarPeriodRel.GeneralLedgerClosePeriod.GeneralLedgerCalendarPeriod>PeriodName<last PreviousCalendarPeriodRel.GeneralLedgerClosePeriod.PeriodName>"

				if (StudentActivityDistrict.DistrictLevelPosting)
					constraint (!StudentActivityJournalRel.Status.Posted)
						"TransactionsAlreadyPostedToGLForAllSchools_-_CannotReopenPeriod"

				LocalSchoolCalendar					= StudentActivityDistrict.SchoolCalendar
				LocalSchoolCalendarGroup			= StudentActivityDistrict.SchoolCalendarPeriodGroup
 				LocalStudentActivityClosedPeriod	= last PreviousCalendarPeriodRel.GeneralLedgerClosePeriod
				
 				invoke OpenReceiptDetails Closed StudentActivityReceiptDetail
 					invoked.PrmStudentActivityDistrict	= StudentActivityDistrict
 					invoked.PrmStudentActivitySchool	= StudentActivitySchool
 					invoked.PrmClosedPeriod				= LocalStudentActivityClosedPeriod

  				invoke OpenBankDetails Closed StudentActivityBankTransDetail
 					invoked.PrmStudentActivityDistrict	= StudentActivityDistrict
 					invoked.PrmStudentActivitySchool	= StudentActivitySchool
 					invoked.PrmClosedPeriod				= LocalStudentActivityClosedPeriod
 
   				invoke OpenTransferDetails Closed StudentActivityTransferDetail
 					invoked.PrmStudentActivityDistrict	= StudentActivityDistrict
 					invoked.PrmStudentActivitySchool	= StudentActivitySchool
 					invoked.PrmClosedPeriod				= LocalStudentActivityClosedPeriod

 			Exit Rules
 				CurrentPeriod 	= LocalStudentActivityClosedPeriod		

  				invoke DeleteIfNoAdjustmentsExist CurrentSchoolActivityAccountBalanceRel
 							
 				invoke DeleteIfNoAdjustmentsExist CurrentStudentActivityBalanceDetailRel
 				
 				invoke DeleteIfNoDetailsExist CurrentStudentActivityBalanceRel			
				
				invoke DeleteIfNoDetailsExist LocalSchoolClosedPeriod

				invoke ProcessJournalForReopenedPeriod StudentActivityJournalRel
					invoked.PrmJournalizeGroup = LocalJournalizeGroup

 				invoke DeleteIfNoDetailsExist LocalStudentActivityClosedPeriod

				if (not StudentActivityDistrict.DistrictLevelPosting)
					invoke InitiateJournalizeForRunGroup StudentActivityDistrict.FinanceEnterpriseGroup in background
						invoked.PrmJournalizeGroup			= LocalJournalizeGroup		

				




















































































 				






















































 				
