ContractLineAttachment is a BusinessClass
	owned by po
	prefix is Conta

	Ontology
		part of ContractLine
			relative key is Sequence

	Persistent Fields
		Attachment		is an AlternateAttachment
		Name
		Description		is a Description3
		SSAttachment	is Boolean
		SupplierCanView
		EmailAttachment	is Boolean
		Source			is Numeric size 2
			States
				Manual					value is 1
				Event					value is 2
				EventQuestionResponse	value is 3

	Local Fields
		LocalAttributeCtr	is Numeric 2			

	Conditions
		EligibleForSourcing
			restricted
			when (Contract.SourcingEligible)
		CanCreateUpdateDelete
			restricted
			when (!ContractLine.ContractLineState.Closed)
		LineAttachmentExists
			restricted
			when (Sequence > 0)

	Field Rules
		Sequence
			autosequence
		Attachment
			required

		Source
			default to Source.Manual
		Name
			default to Attachment.Title

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment	

	Sets
		ByTitleAttachment
			duplicates
			Sort Order
				ContractGroup
				Contract
				ContractLine
				Attachment.Title

		BySequence
			Sort Order
  				ContractGroup
  				Contract
				ContractLine
				Sequence

	Actions
		Create is a Create Action
			valid when (CanCreateUpdateDelete)
			Field Rules
				SSAttachment
					if (!Contract.CreateByCopy)
						default to true
				EmailAttachment
					default to true
				SupplierCanView
					if (Contract.ContractSubclassification entered)
						default to Contract.ContractSubclassification.SupplierCanView
					else
						default to Contract.ContractClassification.SupplierCanView

		Update is an Update Action
			valid when (CanCreateUpdateDelete)

		Delete is a Delete Action
			valid when (CanCreateUpdateDelete)

		Purge is a Purge Action
			restricted

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
														
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
