ForeignTradeAndPayments is a BusinessClass
	owned by tx
	prefix is FTAP
	
	Ontology
		symbolic key is ForeignTradeAndPayments
	
	Patterns
        implements StaticJava
        disable AuditIndex
	
	Persistent Fields
		CustomerGroup
        	disable surrogates
        Customer
        PayGroup
        VendorGroup
        	disable surrogates
        Vendor
        PaymentDate            			is Date
		TypeOfPayment					is AlphaUpper size 1
			States
                CreditMemo value is "C"
                DebitMemo  value is "D"
        TransactionNumber
        IncomingPaymentAmount   		is an InternationalAmount
        OutgoingPaymentAmount			is an InternationalAmount
        CountryCode						is a Country
        VatExempt						is AlphaUpper size 1
			States
				TaxExempt value is "E"
				VatTax    value is "V"
		ForeignEconomicRegulatoryCode
        ReportDate          			is TimeStamp
        System							is a GeneralLedgerSystemCode
		Reported						is Boolean
			restricted
			protected

	Derived Fields
		CompanyName						is a DerivedField 
			type is like Name
			holds pii
			return Company.Name

		PaymentNumber					is a DerivedField
			type is AlphaRight 22
			if (TypeOfPayment.CreditMemo)
				return TransactionNumber
			else
				return ForeignTradeAndPayments.TransactionIDNumber

		Description						is a DerivedField
			type is like Description
			return ForeignEconomicRegulatoryCode.Description

		LandCode						is a DerivedField
			type is like Country
			return CountryCode

		PaymentType is a DerivedField
			type is Numeric 1
			if (System = "AR")
				if (ForeignEconomicRegulatoryCode.FercType.ServicesOrTransfers)
					return 1
				else
				if (ForeignEconomicRegulatoryCode.FercType.CapitalFlows)
					return 3
				else
				if (ForeignEconomicRegulatoryCode.FercType.TradeInGoods)
					return 5
			else
				if (ForeignEconomicRegulatoryCode.FercType.ServicesOrTransfers)
					return 2
				else
				if (ForeignEconomicRegulatoryCode.FercType.CapitalFlows)
					return 4
				else
				if (ForeignEconomicRegulatoryCode.FercType.TradeInGoods)
					return 6

		PaymentTypeDescription is a DerivedField
			type is MessageField
			if (System = "AR")
				if (ForeignEconomicRegulatoryCode.FercType.ServicesOrTransfers)
					return "Incoming services or transfers"
				else
				if (ForeignEconomicRegulatoryCode.FercType.CapitalFlows)
					return "Incoming capital flows"
				else
				if (ForeignEconomicRegulatoryCode.FercType.TradeInGoods)
					return "Incoming trade in goods"
			else
				if (ForeignEconomicRegulatoryCode.FercType.ServicesOrTransfers)
					return "Outgoing services or transfers"
				else
				if (ForeignEconomicRegulatoryCode.FercType.CapitalFlows)
					return "Outgoing capital flows"
				else
				if (ForeignEconomicRegulatoryCode.FercType.TradeInGoods)
					return "Outgoing trade in goods"


		DerivedVendorCustomer is a DerivedField
			type is Numeric 9
			default label is "Vendor / Customer"
			if (System = "AR")
				return Customer
			else
				return Vendor

		DerivedVendorName is a DerivedField 
			type is like VendorName
			holds pii
			default label is "Vendor / Customer Description"
			if (System = "AR")
				return Customer.Name
			else
				return Vendor.VendorName

		FunctionalCurrency is a DerivedField
			type is like Currency
			return Company.AccountingEntity.FunctionalCurrency

	Relations
        PayablesInvoicePaymentRel
            one-to-many relation to PayablesInvoicePayment
            Field Mapping uses Set6
                related.PayGroup            = PayGroup
                related.BankTransactionCode = ForeignTradeAndPayments.BankTransactionCode
                related.TransactionIDNumber = ForeignTradeAndPayments.TransactionIDNumber

		PayablesInvoiceRel
			one-to-one relation to PayablesInvoice
			Field Mapping uses symbolic key
				related.Company			        = Company
            	related.PayablesInvoice			= PayablesInvoicePaymentRel.PayablesInvoice

        ReceivableApplicationRel
            classic name is ARAPPLIED
            one-to-many relation to ReceivableApplication
            Field Mapping uses Set2
                related.CreditTransaction.CreditCompany  = Company
                related.CreditTransaction.CreditBatch    = ForeignTradeAndPayments.ReceivablePaymentHeader
                related.CreditTransaction.CreditSequence = ForeignTradeAndPayments.ReceivablePayment

		ReceivableInvoiceDetailRel
            one-to-one relation to ReceivableInvoiceDetail
            Field Mapping uses symbolic key
                related.Company                                             = Company
                related.ReceivableInvoiceDetail.ReceivableInvoiceDetailType = ReceivableApplicationRel.ReceivableApplication.TransType
                related.ReceivableInvoiceDetail.Invoice                     = ReceivableApplicationRel.ReceivableApplication.Invoice
                related.ReceivableInvoiceDetail.PaymentSeq                  = ReceivableApplicationRel.ReceivableApplication.PaymentSeq

        CashLedgerPaymentRel
            one-to-one relation to CashLedgerPayment
            Field Mapping uses symbolic key
				related.CashManagementGroup						= ForeignTradeAndPayments.CashCode.CashManagementGroup
				related.CashCode								= ForeignTradeAndPayments.CashCode
            	related.CashLedgerPayment.BankTransactionCode	= ForeignTradeAndPayments.BankTransactionCode
                related.CashLedgerPayment.TransactionIDNumber	= ForeignTradeAndPayments.TransactionIDNumber

	Sets
        Set2
            indexed
            Sort Order
                Company
                ForeignTradeAndPayments.ReceivablePaymentHeader
                ForeignTradeAndPayments.ReceivablePayment
                ForeignTradeAndPayments.CashCode
                ForeignTradeAndPayments.CashLedgerSourceRecord
                ForeignTradeAndPayments.BankTransactionCode
                ForeignTradeAndPayments.TransactionIDNumber

        Set3
            indexed
            Sort Order
                Company
                Reported
                PaymentDate
                ForeignTradeAndPayments.CashCode
                ForeignTradeAndPayments.CashLedgerSourceRecord
                ForeignTradeAndPayments.BankTransactionCode
                ForeignTradeAndPayments.TransactionIDNumber
                ForeignTradeAndPayments.ReceivablePaymentHeader
                ForeignTradeAndPayments.ReceivablePayment

        Set4
            indexed
            Sort Order
                Company
                System
                Reported
                ForeignTradeAndPayments.CashCode
                ForeignTradeAndPayments.CashLedgerSourceRecord
                ForeignTradeAndPayments.BankTransactionCode
                ForeignTradeAndPayments.TransactionIDNumber
                ForeignTradeAndPayments.ReceivablePaymentHeader
                ForeignTradeAndPayments.ReceivablePayment

	Field Rules

	Actions
		Create is a Create Action
			restricted
		
		Update is an Update Action
			restricted
		
		Delete is a Delete Action				
			restricted
			
		CreateReport is a Set Action
			restricted
			Parameters
				PrmCompany			is a GeneralLedgerCompany
				PrmDateRange		is a DateRange
				PrmReportDate		is TimeStamp
				PrmDescription		is AlphaUpper 50
				PrmReportGenerated	is TimeStamp

			Parameter Rules
				PrmDescription
					initial value is "ForeignTradeAndPaymentReport"
					default to "ForeignTradeAndPaymentReport"

			Local Fields
				LocalPaymentDateRange						is a DateRange
				LocalForeignTradeAndPaymentsReportHeader	is a ForeignTradeAndPaymentsReportHeader view
				LocalActor									is an Actor

			Instance Selection
				where (Company = PrmCompany
				and    !Reported
				and    PaymentDate within PrmDateRange)
			
			Action Rules
				Empty Set Rules
					LocalActor = actor
					send notification 
						to LocalActor
						description is "NoRecordsToProcessForForeignTradeAndPaymentReport"
						priority is high
						detail is "NoRecordsToProcessForCompany<PrmCompany>DateRange<PrmDateRange.Begin>-<PrmDateRange.End>"
					
				Set Rules
					Entrance Rules
						invoke Create ForeignTradeAndPaymentsReportHeader
							assign result to LocalForeignTradeAndPaymentsReportHeader
							invoked.Company			= Company
							invoked.Description		= PrmDescription
							invoked.ReportGenerated	= PrmReportGenerated

				Instance Rules
					invoke Create ForeignTradeAndPaymentsReportDetail
						invoked.Company						= Company	
						invoked.ForeignTradeAndPaymentsReportHeader	= LocalForeignTradeAndPaymentsReportHeader.ForeignTradeAndPaymentsReportHeader
						invoked.CashCode					= ForeignTradeAndPayments.CashCode
						invoked.CashLedgerSourceRecord		= ForeignTradeAndPayments.CashLedgerSourceRecord
						invoked.BankTransactionCode			= ForeignTradeAndPayments.BankTransactionCode
						invoked.TransactionIDNumber			= ForeignTradeAndPayments.TransactionIDNumber
						invoked.ReceivablePaymentHeader		= ForeignTradeAndPayments.ReceivablePaymentHeader
						invoked.ReceivablePayment			= ForeignTradeAndPayments.ReceivablePayment
						invoked.CustomerGroup				= CustomerGroup
        				invoked.Customer					= Customer						
						invoked.PayGroup					= PayGroup
						invoked.VendorGroup					= VendorGroup
						invoked.Vendor						= Vendor
						invoked.PaymentDate					= PaymentDate
						invoked.TypeOfPayment				= TypeOfPayment
						invoked.TransactionNumber			= TransactionNumber
						invoked.IncomingPaymentAmount		= IncomingPaymentAmount
						invoked.OutgoingPaymentAmount		= OutgoingPaymentAmount
						invoked.CountryCode					= CountryCode
						invoked.VatExempt					= VatExempt
						if (System = "AP")
							invoked.ForeignEconomicRegulatoryCode	= PayablesInvoiceRel.ForeignEconomicRegulatoryCode
						else
						if (System = "AR")
							invoked.ForeignEconomicRegulatoryCode	= ReceivableInvoiceDetailRel.ForeignEconomicRegulatoryCode
						else
						if (System = "CB")
							invoked.ForeignEconomicRegulatoryCode	= CashLedgerPaymentRel.ForeignEconomicRegulatoryCode
						invoked.ReportDate					= PrmReportDate
						invoked.System						= System

			        ReportDate	= PrmReportDate
					Reported	= true
			
					
					
	
				
			
						
	
