SourcingEventLineQuestionResponse is a BusinessClass
    owned by ss
    prefix is RLQ
	sql name is EventLineQuestionResponse 
	
    Ontology
    	part of SourcingEventLineResponse 
    		relative key is SourcingEventLineQuestion
    			
   	Patterns
        implements TemplateDriven by SourcingEventLineQuestion
        	create instance
        		when (SourcingEventLineQuestionAnswer entered)
        		      
    Persistent Fields
    	SourcingEventLineQuestionAnswer
    	Attachment				
    	FromTechnicalProposal is Boolean
		CMAttachment is Boolean
		    	
	Sets
    	ByQuestion
    		duplicates
    		Sort Order
    			Company
    			SourcingEvent
    			SourcingEventLine
    			SourcingEventLineQuestion
    			SourcingEventLineResponse
	
	Field Rules
    	SourcingEventLineQuestionAnswer
    		if (SourcingEventLineQuestion.ResponseRequired)
				if (!SourcingEventLineResponse.ResponseAmount.NoBid)
	    			required
	    				"MustAnswerAllRequiredQuestions"
	    	if (SourcingEventLineQuestion.ResponseType.YesNoText
            and !HasResponseAttachment
            and (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired
            or  (SourcingEventLineQuestionAnswer.YesNoText.YesNo.Yes
            and  SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes) 
            or  (SourcingEventLineQuestionAnswer.YesNoText.YesNo.No
            and  SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo))) 
                constraint (SourcingEventLineQuestionAnswer.YesNoText.Text entered)
                    "MustEnterEitherTextOrAnAttachmentForQuestion<SourcingEventLineQuestion.QuestionText>"
                    
            if (SourcingEventLineQuestion.ResponseType.YesNo
            and (SourcingEventLineQuestionAnswer.YesNoValue.Yes
            and  SourcingEventLineQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
            or  (SourcingEventLineQuestionAnswer.YesNoValue.No
            and  SourcingEventLineQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo))
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<SourcingEventLineQuestion.QuestionText>"    
            
            if (AttachmentAlwaysRequired
            and !SourcingEventLineQuestion.ResponseType.YesNoText)
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<SourcingEventLineQuestion.QuestionText>"
                       
            if  (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired 
            or   SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (SourcingEventLineQuestionAnswer.YesNoText.YesNo.Yes
            and  SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (SourcingEventLineQuestionAnswer.YesNoText.YesNo.No
            and  SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (SourcingEventLineQuestionAnswer.YesNoText.Text entered)
                    "MustEnterTextForQuestion<SourcingEventLineQuestion.QuestionText>"
                    
            if  (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired
            or  (SourcingEventLineQuestionAnswer.YesNoText.YesNo.Yes
            and  SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes) 
            or  (SourcingEventLineQuestionAnswer.YesNoText.YesNo.No
            and  SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo))  
                constraint (HasResponseAttachment)
                    "AttachmentIsRequiredForQuestion<SourcingEventLineQuestion.QuestionText>" 
	    			
	    Attachment
    		if (SourcingEventLineQuestion.AlwaysRequireResponseAttachment)
	    		required
	    			"MustAttachADocumentForAllQuestionsRequiringAnAttachment"
	    	if (!SourcingEventLineQuestionAnswer entered)
	    		cannot be entered
	    			"MustAnswerQuestionToAttachADocument"
	    
	    CMAttachment
	    	if (!SourcingEvent.CreateByCopy
            and  HasQuestionAnswerAttachment
            and  SourcingEventLineQuestion.CMQuestion)
            	default to true
	    		
	Derived Fields
		ResponseAnswer is a ConditionalField
			type is Alpha size 250
			if (SourcingEventLineQuestion.ResponseType.Text)
				SourcingEventLineQuestionAnswer.TextValue
			else
			if (SourcingEventLineQuestion.ResponseType.Number)
				SourcingEventLineQuestionAnswer.NumericValue
			else
			if (SourcingEventLineQuestion.ResponseType.Date)
				DerivedDate
			else
			if (SourcingEventLineQuestion.ResponseType.List)
				SourcingEventLineQuestionAnswer.SourcingEventLineQuestionValue
			else
			if (SourcingEventLineQuestion.ResponseType.YesNo)
				if (SourcingEventLineQuestionAnswer.YesNoValue.Yes)
					YesText
				else
					NoText
			else
				YesNoTextValue
	
		YesNoTextValue is a StringField
			type is Alpha size 260
			restricted
			YesNoText
			"-"
			SourcingEventLineQuestionAnswer.YesNoText.Text

		YesNoText is a ConditionalField
			type is Text
			restricted
			if (SourcingEventLineQuestionAnswer.YesNoText.YesNo = 1)
				YesText
			else
				NoText

		YesText is a MessageField
			restricted
			"Yes"
		
		NoText is a MessageField
			restricted
			"No"

		DerivedDate is a StringField
			type is Alpha 10
			restricted
			SourcingEventLineQuestionAnswer.DateValue month
			"/"
			SourcingEventLineQuestionAnswer.DateValue day
			"/"
			SourcingEventLineQuestionAnswer.DateValue year

		QuestionScore is a DerivedField
			type is like InternationalAmount
			restricted
			if (!SourcingEventLineResponse.ResponseAmount.NoBid)
				if (SourcingEventLineQuestion.ResponseType.YesNoText)
					return (first AllocQuestionYesNoTextRel.ScoreAllocation * SourcingEventLineQuestion.QuestionWeighting)
				else
					return (first AllocQuestionAnswerRel.ScoreAllocation * SourcingEventLineQuestion.QuestionWeighting)
			else
				return 0 
				
		LineDetail is a MessageField
			"<SourcingEventLine.Name>"
			
		SetSendToCM is a ConditionalField
			type is Text
			restricted
			if (CMAttachment)
				SetToNoMessage
			else
				SetToYesMessage
		
		SetToNoMessage is a MessageField
			restricted
   			"Do_Not_Email_Attachment"
   			
   		SetToYesMessage is a MessageField
			restricted
   			"Email_Attachment_On_Contract"
   			
   		ResponseRequiredMessage is a MessageField
			restricted
            "ResponseIsRequired"
        
        ResponseAndAttachmentRequiredMessage is a MessageField
			restricted
            "BothResponseAndAttachmentAreRequired"
        
        YesAttachmentMessage is a MessageField
			restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsYes"
            
        NoAttachmentMessage is a MessageField
			restricted
            "YesOrNoRequired;AttachmentRequiredIfAnswerIsNo"
            
        YesOrNoAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;TextIsRequired"
        
        AttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherAttachmentOrTextIsRequired"
            
        YesAttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsYes"
            
        NoAttachmentOrTextMessage is a MessageField
			restricted
            "YesOrNoRequired;EitherTextOrAttachmentIsRequiredIfAnswerIsNo"
            
        AttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequired"
        
        YesAttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsYes"
            
        NoAttachmentAndTextMessage is a MessageField
			restricted
            "YesOrNoRequired;BothTextAndAttachmentAreRequiredIfAnswerIsNo"
        
        ConditionalAnswerLabel is a ConditionalField
            type is Alpha 125
			restricted
            if (SourcingEventLineQuestion.ResponseRules.ResponseRequired
            or  SourcingEventLineQuestion.YesNoResponseRules.ResponseRequired)
                ResponseRequiredMessage        
            else
            if (SourcingEventLineQuestion.ResponseRules.ResponseRequiredAndAttachmentRequired
            or  SourcingEventLineQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequired)
                ResponseAndAttachmentRequiredMessage
            else
            if (SourcingEventLineQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfYes)
                YesAttachmentMessage
            else
            if (SourcingEventLineQuestion.YesNoResponseRules.ResponseRequiredAndAttachmentRequiredIfNo)
                NoAttachmentMessage
            else
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextResponseRequired)
                YesOrNoAndTextMessage
            else
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequired)
                AttachmentOrTextMessage
            else
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfYes)
                YesAttachmentOrTextMessage
            else
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextOrAttachmentRequiredIfNo)
                NoAttachmentOrTextMessage
            else 
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequired)
                AttachmentAndTextMessage
            else
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfYes)
                YesAttachmentAndTextMessage
            else
            if (SourcingEventLineQuestion.YesNoTextResponseRules.ResponseRequiredAndTextAndAttachmentRequiredIfNo)
                NoAttachmentAndTextMessage
            else
                blank

	Conditions
		HasQuestionAttachment
			restricted
			when (SourcingEventLineQuestion.Attachment entered)
		HasQuestionAnswerAttachment
			when (Attachment entered)
			
		CanSetCMToNo
			restricted
			when (SourcingEvent.ContractOutputExists 
			and   CMAttachment
			and   HasQuestionAnswerAttachment
			and   SourcingEventLineQuestion.CMQuestion)
			
		CanSetCMToYes
			restricted
			when (SourcingEvent.ContractOutputExists
			and   CMAttachment = false
			and   HasQuestionAnswerAttachment
			and   SourcingEventLineQuestion.CMQuestion)
			
		DisplayCMAttachmentButton
			restricted
			when (SourcingEvent.ContractOutputExists
			and   HasQuestionAnswerAttachment
			and   SourcingEvent.Status.Open
			and   SourcingEventLineQuestion.CMQuestion)
			
		HasResponseAttachment
			restricted
            when (Attachment entered)
        
        AttachmentAlwaysRequired   
			restricted
            when (SourcingEventLineQuestion.AlwaysRequireResponseAttachment
            and  (SourcingEventLineQuestion.NonYesNoResponseType
            or    SourcingEventLineQuestion.ResponseType.YesNo))

	Relations
		AllocQuestionAnswerRel 
			one-to-many relation to SourcingEventLineQuestionAllocation
			Field Mapping uses symbolic key
				related.Company																= Company
				related.SourcingEvent														= SourcingEvent
				related.SourcingEventLine													= SourcingEventLine
				related.SourcingEventLineQuestion 											= SourcingEventLineQuestion
				related.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer = SourcingEventLineQuestionAnswer
			Instance Selection
				where (SourcingEventLineQuestion.EligibleForWeighting)				
						
		AllocQuestionYesNoTextRel 
			one-to-many relation to SourcingEventLineQuestionAllocation
			Field Mapping uses symbolic key
				related.Company						= Company
				related.SourcingEvent				= SourcingEvent
				related.SourcingEventLine			= SourcingEventLine
				related.SourcingEventLineQuestion	= SourcingEventLineQuestion
			Instance Selection
				where (SourcingEventLineQuestion.EligibleForWeighting
				and    related.SourcingEventLineQuestionAllocation.SourcingEventLineQuestionAnswer.YesNoText.YesNo = SourcingEventLineQuestionAnswer.YesNoText.YesNo)

		EventLineQuestionsAndResponseRel
			one-to-many relation to SourcingEventLineQuestionResponse
			Field Mapping uses ByQuestion
			Instance Selection
				where (related.Company					= Company
				and related.SourcingEvent				= SourcingEvent
				and related.SourcingEventLine			= SourcingEventLine
				and related.SourcingEventLineQuestion	= SourcingEventLineQuestion				
				and related.SourcingEventLineResponse	= SourcingEventLineResponse)
				
	Actions
		Create is a Create Action
     		Action Rules
 				if (!SourcingEventResponse.HasCurrentModificationRequests)
 					constraint (SourcingEventResponse.CanUpdateResponse)
 						"BuyerCannotCreateQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEvent.BestAndFinalStarted
	 				and !SourcingEvent.StepTwoStarted
	 				and !SourcingEventResponse.HasCurrentModificationRequests)
	 					constraint (SourcingEvent.CanRespond)  
		 					"CanOnlyRespondBetweenEventOpenDateAndTimeAndCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotCreateIfResponseHasBeenSubmitted"

     	Update is an Update Action
     		Action Rules
 				if (!SourcingEventResponse.HasCurrentModificationRequests)
 					constraint (SourcingEventResponse.CanUpdateResponse)
 						"BuyerCannotUpdateQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 				if (!SourcingEvent.BestAndFinalStarted
	 				and !SourcingEvent.StepTwoStarted
	 				and !SourcingEventResponse.HasCurrentModificationRequests)
	 					constraint (SourcingEvent.CanRespond)  
	 						"CannotUpdateResponseAfterEventCloseDateAndTime"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotUpdateIfResponseHasBeenSubmitted"
 				if (SourcingEventLineQuestionAnswer changed
 				or  Attachment changed)
 					constraint (!FromTechnicalProposal)
	 					"CannotChangeAnswerWhichWasPartOfAnApprovedTechnicalProposal"

     	Delete is a Delete Action
    		Action Rules
 				constraint (SourcingEventResponse.CanUpdateResponse)
 					"BuyerCannotDeleteQuestionsForAResponseEnteredByASupplier"
 				if (!SourcingEventResponse.ManualResponse)
	 				constraint (!SourcingEvent.NotificationStatus.Amended)
    					"AmendmentInProgress;PleaseTryAgainLater"
	 			constraint (SourcingEventResponse.Status.Draft)  
 					"CannotDeleteIfResponseHasBeenSubmitted"
 				constraint (!FromTechnicalProposal)
	 				"CannotDeleteAnswerWhichWasPartOfAnApprovedTechnicalProposal"
 		
 		UpdateFromAcceptTechnicalProposal is an Update Action
	 		restricted
	 		
	 	UpdateCMAttachment is an Instance Action
	 		
	 		Action Rules
	 			if (CanSetCMToNo)
	 				CMAttachment = false
	 			else
	 			if (CanSetCMToYes)
	 				CMAttachment = true
