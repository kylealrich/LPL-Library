ReportDefinition is a BusinessClass
    owned by report
    prefix is RDEF
    
    representative text is "<DerivedName>"

    Ontology
    	symbolic key is ReportDefinition

	Patterns
        disable AsOfDateProcessing
        disable EffectiveDated

    Persistent Fields
    	ReportName				is Alpha size 200
    		translatable
		DeliveredReportName		is Alpha size 200
    	Description 			is Text
    		translatable
    	DeliveredDescription 	is Text
    	Category 				is a ReportCategory
    	SubCategory				is a ReportSubCategory
    	ReportWebAppOccurs
    	Inactive				is Boolean
    	Validity				is Text 

    	Visibility				is Text 

    	

    	ExternalParameters		is JSONObject 



		Errors					is Text
		
	Local Fields
		SpecialChar
	
	Create Rules
		constraint (CanEdit)
			"CreateNotAllowed"
	
	Delete Rules
		constraint (CanEdit)
			"DeleteNotAllowed"

	Field Rules
		ReportName
			required
			
			if (not TransFromUserFolder and ReportName changed and action type.Update and ReportDefinition.UserFolder entered and ReportDefinition.UserFolder exists)
				invoke UpdateNoRules ReportDefinition.UserFolder
					invoked.Name = ReportName translations
					
		SubCategory
			if (not TransFromUserFolder and ReportName changed and action type.Update and ReportDefinition.UserFolder entered and ReportDefinition.UserFolder exists)
				invoke ChangeSubCategory ReportDefinition.UserFolder
					invoked.ParamCategory = SubCategory 
			
		Validity
			if (Validity entered)
				if (TransLenient)
					if (!(com.lawson.rdtech.type.Field.startsWith(Validity, "(actor.context."))
					and !(com.lawson.rdtech.type.Field.startsWith(Validity, "(session.key."))
					and !(com.lawson.rdtech.type.Field.startsWith(Validity, "(actor.agent("))
					and !(com.lawson.rdtech.type.Field.startsWith(Validity, "(not actor.context.") )
					and !(com.lawson.rdtech.type.Field.startsWith(Validity, "(not session.key."))
					and !(com.lawson.rdtech.type.Field.startsWith(Validity, "(not actor.agent("))
					and !(com.lawson.rdtech.type.Field.equalStrings(Validity, "(true)"))
					and !(com.lawson.rdtech.type.Field.equalStrings(Validity, "(false)")))
						if (Errors entered)
							Errors += SpecialChar.NewLine + "Validity cleared due to being invalid. Can only be a simple Actor Context or Session Key condition. Value: " + Validity
						else
							Errors = SpecialChar.NewLine + "Validity cleared due to being invalid. Can only be a simple Actor Context or Session Key condition. Value: " + Validity
							
						log "ValiditySkippedDueToBeingInvalid.CanOnlyBeASimpleActorContextOrSessionKeyCondition.Value:<Validity>"
						initialize Validity
				else
					constraint (com.lawson.rdtech.type.Field.startsWith(Validity, "(actor.context.") 
					or          com.lawson.rdtech.type.Field.startsWith(Validity, "(session.key.")
					or          com.lawson.rdtech.type.Field.startsWith(Validity, "(actor.agent(")
					or          com.lawson.rdtech.type.Field.startsWith(Validity, "(not actor.context.") 
					or          com.lawson.rdtech.type.Field.startsWith(Validity, "(not session.key.")
					or          com.lawson.rdtech.type.Field.startsWith(Validity, "(not actor.agent(")
					or          com.lawson.rdtech.type.Field.equalStrings(Validity, "(true)")
					or          com.lawson.rdtech.type.Field.equalStrings(Validity, "(false)"))
						"ValidityCanOnlyBeASimpleActorContextOrSessionKeyCondition"
			
				if (Validity entered)
					constraint (IsValidValidityCondition)
						"InvalidCondition"

		Visibility
			if (Visibility entered)
				if (TransLenient)
					if (!(com.lawson.rdtech.type.Field.startsWith(Visibility, "(actor.context."))
					and !(com.lawson.rdtech.type.Field.startsWith(Visibility, "(session.key."))
					and !(com.lawson.rdtech.type.Field.startsWith(Visibility, "(actor.agent("))
					and !(com.lawson.rdtech.type.Field.startsWith(Visibility, "(not actor.context.")) 
					and !(com.lawson.rdtech.type.Field.startsWith(Visibility, "(not session.key."))
					and !(com.lawson.rdtech.type.Field.startsWith(Visibility, "(not actor.agent("))
					and !(com.lawson.rdtech.type.Field.equalStrings(Visibility, "(true)"))
					and !(com.lawson.rdtech.type.Field.equalStrings(Visibility, "(false)")))
						if (Errors entered)
							Errors += SpecialChar.NewLine + "Visibility cleared due to being invalid. Can only be A simple Actor Context or Session Key condition. Value: " + Visibility
						else
							Errors = "Visibility cleared due to being invalid. Can only be A simple Actor Context or Session Key condition. Value: " + Visibility
						
						log "VisibilityClearedDueToBeingInvalid.CanOnlyBeASimpleActorContextOrSessionKeyCondition.Value:<Visibility>"
						initialize Visibility
				else
					constraint (com.lawson.rdtech.type.Field.startsWith(Visibility, "(actor.context.") 
					or          com.lawson.rdtech.type.Field.startsWith(Visibility, "(session.key.")
					or          com.lawson.rdtech.type.Field.startsWith(Visibility, "(actor.agent(")
					or          com.lawson.rdtech.type.Field.startsWith(Visibility, "(not actor.context.") 
					or          com.lawson.rdtech.type.Field.startsWith(Visibility, "(not session.key.")
					or          com.lawson.rdtech.type.Field.startsWith(Visibility, "(not actor.agent(")
					or          com.lawson.rdtech.type.Field.equalStrings(Visibility, "(true)")
					or          com.lawson.rdtech.type.Field.equalStrings(Visibility, "(false)"))
						"VisibilityCanOnlyBeASimpleActorContextOrSessionKeyCondition"
			
				if (Visibility entered)
					constraint (IsValidVisibilityCondition)
						"InvalidCondition"
						
		ExternalParameters
			if (ReportDefinition.Type.EPM or ReportDefinition.Type.WFM)
				required
				
    Transient Fields
    	Sharing				is Numeric size 1 
			States
				Private		value is 1
				Group		value is 2
				Public		value is 3
				Role		value is 4
				RoleGroup	value is 5
			derive value from ReportDefinition.UserFolder.SharingOption
			
		TransFromUserFolder is Boolean 
			default label is "FromUserFolder"
			protected
			
		TransLenient is Boolean
			default label is "LenientValidation"
			protected
			




			
	Derived Fields
		Owner is a DerivedField
			type is Actor
			
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.Owner
				
			return blank
			
		DerivedTypeLabel is a NativeField 
			type is MessageField
			default label is untranslatable:"Type"

			
		ReportMessage is a MessageField
			restricted
			"Report"

		GeneralSubcategoryMessage is a MessageField
			restricted
			"General"

		DerivedName is a DerivedField
			type is MessageField
			default label is "Identifier"
			DerivedName = DerivedTypeLabel
			
			if (ReportDefinition.UserFolder entered)
				DerivedName += "(" + ReportMessage + ")"
			
			if (BusViewFieldRequired)
				DerivedName += " " + ReportDefinition.BusinessView
				
				if (NameFieldRequired)
					DerivedName += "." + ReportDefinition.Name
			else
				if (NameFieldRequired)
					DerivedName += " " + ReportDefinition.Name

		DerivedResourceId is a DerivedField
			type is MessageField
			default label is "ResourceId"
			DerivedResourceId = ""
			if (BusViewFieldRequired)
				DerivedResourceId += ReportDefinition.BusinessView

				if (NameFieldRequired)
					DerivedResourceId += "." + ReportDefinition.Name
			else
				if (NameFieldRequired)
					DerivedResourceId += ReportDefinition.Name

		CatalogFavoriteId is a DerivedField
			type is MessageField
			CatalogFavoriteId = ""
			if (ReportDefinition.UserFolder entered)
				CatalogFavoriteId += ReportDefinition.UserFolder
			else
				if (BusViewFieldRequired)
					CatalogFavoriteId += ReportDefinition.BusinessView

					if (NameFieldRequired)
						CatalogFavoriteId += "." + ReportDefinition.Name
				else
					if (NameFieldRequired)
						CatalogFavoriteId += ReportDefinition.Name

		DerivedCategory is a DerivedField
			type is Alpha size 100
			default label is "Category"
			if (Category.TransDescription entered)
				return Category.TransDescription
			return Category

		DerivedSubCategory is a DerivedField
			type is Alpha size 100
			default label is "Subcategory"
			if (SubCategory.Description entered)
				return SubCategory.Description
			return GeneralSubcategoryMessage

		DerivedSharedWith is a DerivedField
			type is Alpha size 30
			default label is "SharedWith"
			if (ReportDefinition.UserFolder entered)
				if (ReportDefinition.UserFolder.Sharing.Private or ReportDefinition.UserFolder.Sharing.Public)
					return ReportDefinition.UserFolder.SharingTypeString
				else
					return ReportDefinition.UserFolder.SharingTypeString + ": " + ReportDefinition.UserFolder.SharedWith
			else
				return blank

		DerivedSharedWithForCatalog is a DerivedField
			type is Alpha size 100
			default label is "SharedWith"
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.SharedWith
			else
				return blank

		DerivedSharing is a DerivedField
			type is Alpha size 100
			default label is "Sharing"
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.Sharing
			else
				return "0"

		DerivedSharingTypeString is a DerivedField
			type is Alpha size 100
			default label is "SharingTypeString"
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.SharingTypeString
			else
				return blank

		DerivedUserFolderTimeStamp is a DerivedField
            type is TimeStamp
            if (ReportDefinition.UserFolder entered)
                return ReportDefinition.UserFolder.update stamp
            return blank

		IsValidValidityCondition is a NativeField
			type is Boolean
			restricted
			
		IsValidVisibilityCondition is a NativeField
			type is Boolean
			restricted
			
		IsValidValidityTrue is a NativeField
			type is Boolean
			restricted
			
		IsValidVisibilityTrue is a NativeField
			type is Boolean
			restricted
			
			
		FolderActor is a DerivedField
			type is Actor
			
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.Actor
				
			return blank
			
		FolderActorGroup is a DerivedField
			type is AlphaUpper 30
			
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.ActorGroup
				
			return blank

		FolderRole is a DerivedField
			type is Alpha size 80
			
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.Role
				
			return blank
		
		FolderRoleGroup is a DerivedField
			type is Alpha size 80
			
			if (ReportDefinition.UserFolder entered)
				return ReportDefinition.UserFolder.RoleGroup
				
			return blank
			
		SourceExists is a DerivedField
			type is Boolean
			
			if (IsFolder)
				if (ReportDefinition.UserFolder exists) 
					return true
				
				return false
			
			if (ReportDefinition.Type.List)
				if (BusinessListRel exists)
					return true
				
				return false
			
			if (ReportDefinition.Type.Page)
				if (BusinessPageRel exists)
					return true
				
				return false
 

        	if (ReportDefinition.Type.Task)
        		if (BusinessActionRel exists)
        			return true
        		
        		return false
        		
        	return true 
        	
        DeveloperMode is a NativeField
			type is Boolean
			restricted	
        	
        DerivedLID is a NativeField
        	type is Alpha size 250
        	
        DerivedBIProject is a NativeField
        	type is Alpha size 250
        	
        DerivedURL is a DerivedField
			type is Text 
			default label is "URL"
			
			if (ExternalParameters entered)
				DerivedURL = "?LogicalId=" + DerivedLID
				
				if (ReportDefinition.Type.EPM)
					DerivedURL += "&BIProject=" + DerivedBIProject
				else
				if (ReportDefinition.Type.WFM)
					DerivedURL += "&reportCatalogDrillback=true"
					
				DerivedURL += DerivedJsonToURL


				
				return DerivedURL
				
			return blank 
			
		DerivedJsonToURL is a NativeField
			type is Text
			restricted
			default label is untranslatable

		ReportNameChanged is a DerivedField
			type is Boolean
			if (DeliveredReportName != ReportName)
				return true
			else
				return false

		DescriptionChanged is a DerivedField
			type is Boolean
			if (DeliveredDescription != Description)
				return true
			else
				return false
				
		InternalExecution is a NativeField
		    type is Boolean
		    restricted
		    default label is untranslatable 















    
    Conditions
    	BusViewRelValid
    		default label is "BusinessViewType"
    		when (ReportDefinition.Type.List or ReportDefinition.Type.Task or ReportDefinition.Type.ManualReport or ReportDefinition.Type.EPM or ReportDefinition.Type.WFM)
    		
    	BusViewFieldRequired
    		default label is "BusinessViewRequired"
    		when (ReportDefinition.Type.List or ReportDefinition.Type.Page or ReportDefinition.Type.Task or ReportDefinition.Type.ManualReport or ReportDefinition.Type.EPM or ReportDefinition.Type.WFM)
    		
    	NameFieldRequired
    		default label is "NameRequired"
    		when (ReportDefinition.Type.List or ReportDefinition.Type.EPM or ReportDefinition.Type.WFM)
    		
    	IsFolder
    		default label is "Folder"
    		when (ReportDefinition.UserFolder entered)
    		
    	HasErrors
    		default label is "ErrorsExist"
    		when (Errors entered)

    	ValidForActor
    		when (not Inactive
			and (ReportDefinition.UserFolder not entered or ReportDefinition.UserFolder.AllReportsViewableForActor)
			and ((Validity not entered or IsValidValidityTrue)
			and (Visibility not entered or IsValidVisibilityTrue)))

    	ValidForT2V
    		restricted
    		when ((ReportNameChanged or DescriptionChanged or Inactive) and not IsFolder)
    	
    	CanEdit
    		restricted
    		default label is untranslatable
    		when (DeveloperMode or InternalExecution)

   		HasUserReportRecords
   			default label is "ExistingPrintFiles"
   			when (ReportDefinition.UserFolder entered and ReportDefinition.UserFolder.HasUserReportRecords)

    Actions
    	InternalCreate is a Create Action
    		restricted
    		valid when (CanEdit)
    		
    	Create is a Create Action
    		valid when (CanEdit) 
    		
    		Entrance Rules
				constraint (CanEdit)
					"CreationOfReportDefinitionRecordsIsInvalidOutsideOfDeveloperMode"
					
    			constraint (not Category.Inactive)
    				"Category<Category>IsInactiveAndCannotBeUsed"
    			
    			constraint (not SubCategory.Inactive)
    				"SubCategory<SubCategory>IsInactiveAndCannotBeUsed"
    		
    	Update is an Update Action
    		valid when (CanEdit) 
    	
    	CreateFromSnapshot is a Create Action
    		restricted
    		bypass field rules
    		
    	UpdateFromSnapshot is an Update Action 
    		restricted
    		bypass field rules
    		
    	Modify is an Update Action 
    		Field Rules
    			DeliveredReportName
    				cannot be changed
    				
		    	DeliveredDescription
		    		cannot be changed
    			
    			Category
    				cannot be changed
    			
    			SubCategory
    				cannot be changed
    			
    			ReportWebAppOccurs
    				cannot be changed
    			
    			Validity
    				cannot be changed
    			
    			Visibility
    				cannot be changed




















    	
    	Delete is a Delete Action
    		valid when (CanEdit) 
    		
    	Purge is a Purge Action
    		valid when (CanEdit) 
    	
    	View is an Update Action 
    	
    	ReassignReportOwner is an Instance Action
			valid when (IsFolder)
			Parameters
				PrmNewOwner is an Actor
					default label is "NewOwner"
			Parameter Rules
				PrmNewOwner
					required
						"MustEnterNewOwner"
			Action Rules
				invoke ReassignReportOwner ReportDefinition.UserFolder
					invoked.PrmNewOwner = PrmNewOwner
    	
    	Activate is an Instance Action
    		valid when (Inactive)
    		
    		Action Rules
    			Inactive = false
    			
    	Deactivate is an Instance Action
    		valid when (not Inactive)
    		
    		Action Rules
    			Inactive = true
    	
    	TagItem is an Instance Action
    		valid when (ReportDefinition.UserFolder entered)
			Parameters
				ParamConfigurationTag is a ConfigurationTag
					default label is "Tag"
			
			Entrance Rules
				if (Inactive)
					confirmation required
						"ReportDefinitionIsInactive,TagAnyway?"
							
			Action Rules
				invoke TagItem ReportDefinition.UserFolder
					invoked.ParamConfigurationTag = ParamConfigurationTag
    			
    	PurgeReportDefinitions is a Set Action
    		restricted
    		
    		Parameters
    			ParamCategory is a ReportCategory
    				default label is "Category"
    				
    			ParamPurgeMissingOnly is Boolean
    				default label is "PurgeOnlyWhenSourceMissing"
    		
    			ParamIncludeExternal is Boolean
    				default label is "IncludeExternal"
    				
    		Instance Selection
				where ((ParamCategory = blank or Category = ParamCategory) and (not ParamPurgeMissingOnly or not SourceExists) and (ParamIncludeExternal 
				or    (ReportDefinition.Type <= 7)))
				
			Sort Order is ByCategory
				
			Action Rules
				Instance Rules
					invoke Purge

    Sets
    	ByUserFolder 
			indexed
			Sort Order
				ReportDefinition.UserFolder
			Instance Selection
				where (IsFolder)	
				
		ByReportName
			indexed
			Sort Order
				ReportName
				ReportDefinition
		
		ByCategory
			indexed
			Sort Order
				Category
				SubCategory
				ReportDefinition

		ByBusinessView
			Sort Order
				ReportDefinition.BusinessView
				ReportDefinition.UserFolder
				ReportDefinition.Type
				ReportDefinition.Name
				
	Relations
		BusinessViewRel
			one-to-one relation to BusinessView
			valid when (BusViewRelValid)
			Field Mapping uses symbolic key
				related.BusinessView = ReportDefinition.BusinessView
				
		BusinessPageRel
			one-to-one relation to BusinessView
			valid when (ReportDefinition.Type.Page)
			Field Mapping uses symbolic key
				related.BusinessView = ReportDefinition.BusinessView
			
			
		BusinessListRel
			one-to-one relation to BusinessList
			valid when (ReportDefinition.Type.List)
			Field Mapping uses symbolic key
				related.BusinessView 	= ReportDefinition.BusinessView
				related.BusinessList 	= ReportDefinition.Name
				
		BusinessActionRel
			one-to-one relation to BusinessAction
			valid when (ReportDefinition.Type.Task)
			Field Mapping uses symbolic key
				related.BusinessView 	= ReportDefinition.BusinessView
				related.BusinessAction 	= ReportDefinition.Name
