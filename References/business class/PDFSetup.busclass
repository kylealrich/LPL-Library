PDFSetup is a BusinessClass
    owned by admincon
    prefix is PDFX
    store using com.lawson.rdtech.framework.repository.PDFSetupStorage

    Ontology
        symbolic key is PDFSetup

    Patterns
    	implements ReadOnly

    Transient Fields
        TFirstPageHeader             is an UserImage
            default label is "FirstPageHeader"
            derive value from FirstPageHeader

        TPageHeader                  is an UserImage
            default label is "PageHeader"
            derive value from PageHeader

        TPageFooter                  is an UserImage
            default label is "PageFooter"
            derive value from PageFooter

        TFillColorType is Numeric size 1
            default label is "FillColorType"
            derive value from ShadeFillColorType
            States
                PredefinedColor         value is 0
                RGB_Color               value is 1
                HexValue                value is 2

        TShadeFillColor              is Alpha size 250  
            default label is "ShadeFillColor"
            derive value from ShadeFillColor

        TFontColorType is Numeric size 1
            default label is "FontColorType"
            derive value from ShadeFontColorType
            States
                PredefinedColor         value is 0
                RGB_Color               value is 1
                HexValue                value is 2

        TShadeFontColor              is Alpha size 250  
            default label is "ShadeFontColor"
            derive value from ShadeFontColor

        TShadeFillColorRGB          is Alpha size up to 150
            default label is "ShadeFillColorRGB"
            derive value from ShadeFillColorRGB

        TShadeFontColorRGB          is Alpha size up to 150
            default label is "ShadeFontColorRGB"
            derive value from ShadeFontColorRGB

        TShadePreview                is RichText
            default label is "ShadePreview"
            derive value from ShadePreview

    Conditions
        FirstPageHeaderConfigured
            when (FirstPageHeaderRel exists)

        PageHeaderConfigured
            when (PageHeaderRel exists)

        PageFooterConfigured
            when (PageFooterRel exists)

        ListHeaderShadeConfigured
            when (ShadeFillColorConfigured or ShadeFontColorConfigured)

        ShadeFillColorConfigured
            when (ShadeFillColorRel exists)

        ShadeFontColorConfigured
            when (ShadeFontColorRel exists)

        ShadeFillColorConfiguredInHierarchyOnly
            when (!ShadeFillColorConfigured and ShadeFillColorConfigParameter != blank)

        ShadeFontColorConfiguredHierarchyOnly
            when (!ShadeFontColorConfigured and ShadeFontColorConfigParameter != blank)

    Derived Fields
       	FirstPageHeader is a DerivedField
       		type is Alpha size 250

            return FirstPageHeaderRel.UserImage

       	PageHeader is a DerivedField
       		type is Alpha size 250

            return PageHeaderRel.UserImage

        PageFooter is a DerivedField
            type is Alpha size 250

            return PageFooterRel.UserImage

        ShadeFillColorType is a DerivedField
            type is Numeric size 1

            if (ShadeFillColor = blank)
                return 0
            if (com.lawson.rdtech.type.Field.startsWith(ShadeFillColor, "rgb("))
                return 1
            if (com.lawson.rdtech.type.Field.startsWith(ShadeFillColor, "#"))
                return 2

        ShadeFillColor is a DerivedField
            type is Alpha size 250

            return ShadeFillColorRel.Value

        ShadeFontColorType is a DerivedField
            type is Numeric size 1

            if (ShadeFontColor = blank)
                return 0
            if (com.lawson.rdtech.type.Field.startsWith(ShadeFontColor, "rgb("))
                return 1
            if (com.lawson.rdtech.type.Field.startsWith(ShadeFontColor, "#"))
                return 2

        ShadeFontColor is a DerivedField
            type is Alpha size 250

            return ShadeFontColorRel.Value

        ShadeFontColorConfigParameter is a NativeField 
            type is Alpha size 250

        ShadeFillColorConfigParameter is a NativeField 
            type is Alpha size 250

        ShadePreview is a DerivedField
            type is RichText
            return "<div style='padding:12px;make width:100px; height:50px; background-color:" + ShadeFillColorRGB + "; color:" + ShadeFontColorRGB + "'>" + SampleShadeText + "</div>"

        ShadeFillColorRGB is a NativeField
            type is Alpha size up to 150

        ShadeFontColorRGB is a NativeField
            type is Alpha size up to 150

        SampleShadeText is a MessageField
            restricted
            "SampleListHeaderText"

        HexExampleColor is a DerivedField
            type is Alpha size 25
            return "#0771de"

    Actions
        ConfigureListHeaderFillStyle is an Instance Action
            disable multiple instance selection
            completion message is "ListHeaderFillStyleUpdated"

            Parameters
                ParamsFillType is Numeric size 1
                    default label is "ColorType"
                    States
                        PredefinedColor         value is 0
                        RGB_Color               value is 1
                        HexValue                value is 2

                ParamFillColor is Alpha size 300
                    default label is "Color"
                    States
                        Black			value is "black"
                        Blue			value is "blue"
                        Brown           value is "brown"
                        Gray            value is "gray"
                        Green           value is "green"
                        Jade            value is "jade"
                        Magenta         value is "magenta"
                        Maroon          value is "maroon"
                        Orange          value is "orange"
                        Purple          value is "purple"
                        Red             value is "red"
                        Turquoise       value is "turquoise"
                        Yellow          value is "yellow"
                        GrayFill        value is "gray_fill"
                        BlueShade       value is "blue_shade"

                ParamFillRGB is Alpha size 300
                    default label is "Color"

                ParamFillHexValue is Alpha size 300
                    default label is "Color"

                ParamsFontType is Numeric size 1
                    default label is "ColorType"
                    States
                        PredefinedColor         value is 0
                        RGB_Color               value is 1
                        HexValue                value is 2

                ParamFontColor is Alpha size 300
                    default label is "Color"
                    States
                        Black			value is "black"
                        White           value is "white"
                        Blue			value is "blue"
                        Brown           value is "brown"
                        Gray            value is "gray"
                        Green           value is "green"
                        Jade            value is "jade"
                        Magenta         value is "magenta"
                        Maroon          value is "maroon"
                        Orange          value is "orange"
                        Purple          value is "purple"
                        Red             value is "red"
                        Turquoise       value is "turquoise"
                        Yellow          value is "yellow"

                ParamFontRGB is Alpha size 300
                    default label is "Color"

                ParamFontHexValue is Alpha size 300
                    default label is "Color"

            Parameter Rules
                ParamsFillType
                    initial value is ShadeFillColorType

                ParamFillColor
					initial value is ShadeFillColorRel.Value
						when (TFillColorType.PredefinedColor)
                    if (ParamsFillType.PredefinedColor)
                        required

                ParamFillRGB
					initial value is ShadeFillColorRel.Value
						when (TFillColorType.RGB_Color)
                    if (ParamsFillType.RGB_Color)
                        required

                ParamFillHexValue
					initial value is ShadeFillColorRel.Value
						when (TFillColorType.HexValue)
                    if (ParamsFillType.HexValue)
                        required

                ParamsFontType
                    initial value is ShadeFontColorType

                ParamFontColor
					initial value is ShadeFontColorRel.Value
						when (TFontColorType.PredefinedColor)
                    if (ParamsFontType.PredefinedColor)
                        required

                ParamFontRGB
					initial value is ShadeFontColorRel.Value
						when (TFontColorType.RGB_Color)
                    if (ParamsFontType.RGB_Color)
                        required

                ParamFontHexValue
                    initial value is ShadeFontColorRel.Value
                        when (TFontColorType.HexValue)
                    if (ParamsFontType.HexValue)
                        required

            Action Rules
                if (!ShadeFillColorConfigured)
                    invoke Create ConfigurationParameter
                        invoked.ConfigurationParameter.ConfigurationID = "pdf"
                        invoked.ConfigurationParameter.Name = "pdf.shade.fill.color"
                        if (ParamsFillType.PredefinedColor)
                            invoked.Value = ParamFillColor
                        if (ParamsFillType.RGB_Color)
                            invoked.Value = ParamFillRGB
                        if (ParamsFillType.HexValue)
                            invoked.Value = ParamFillHexValue

                else
                    invoke Update ShadeFillColorRel
                        if (ParamsFillType.PredefinedColor)
                            invoked.Value = ParamFillColor
                        if (ParamsFillType.RGB_Color)
                            invoked.Value = ParamFillRGB
                        if (ParamsFillType.HexValue)
                            invoked.Value = ParamFillHexValue

                if (!ShadeFontColorConfigured)
                    invoke Create ConfigurationParameter
                        invoked.ConfigurationParameter.ConfigurationID = "pdf"
                        invoked.ConfigurationParameter.Name = "pdf.shade.font.color"
                        if (ParamsFontType.PredefinedColor)
                            invoked.Value = ParamFontColor
                        if (ParamsFontType.RGB_Color)
                            invoked.Value = ParamFontRGB
                        if (ParamsFontType.HexValue)
                            invoked.Value = ParamFontHexValue

                else
                    invoke Update ShadeFontColorRel
                        if (ParamsFontType.PredefinedColor)
                            invoked.Value = ParamFontColor
                        if (ParamsFontType.RGB_Color)
                            invoked.Value = ParamFontRGB
                        if (ParamsFontType.HexValue)
                            invoked.Value = ParamFontHexValue

        ConfigureFirstPageHeader is an Instance Action
            disable multiple instance selection
            completion message is "FirstPageHeaderUpdated"

            Parameters
                ParamImage is BinaryObject
                    default label is "Image"

                ParamDescription is a ConfigDescription
                    default label is "Description"

                ParamPreferredHeight is Numeric size 4
                    default label is "PreferredHeight"

                ParamPreferredWidth	is Numeric size 4
                    default label is "PreferredWidth"

                ParamStatus is Numeric size 1
                    States
                        Active				value is 0
                        Inactive			value is 1
                    default label is "Status"

            Parameter Rules
                ParamImage
                    required
                    initial value is FirstPageHeaderRel.Image

                ParamDescription
                    initial value is FirstPageHeaderRel.Description

                ParamPreferredHeight
                    initial value is FirstPageHeaderRel.PreferredHeight

                ParamPreferredWidth
                    initial value is FirstPageHeaderRel.PreferredWidth

                ParamStatus
                    initial value is FirstPageHeaderRel.Status

            Action Rules
                if (!FirstPageHeaderConfigured)
                    invoke Create UserImage
                        invoked.UserImage = "reportfirstpageheader"
                        invoked.Image = ParamImage
                        invoked.Description = ParamDescription
                        invoked.Status = ParamStatus
                        invoked.PreferredHeight = ParamPreferredHeight
                        invoked.PreferredWidth = ParamPreferredWidth
                else
                    invoke Update FirstPageHeaderRel
                        invoked.Image = ParamImage
                        invoked.Description = ParamDescription
                        invoked.Status = ParamStatus
                        invoked.PreferredHeight = ParamPreferredHeight
                        invoked.PreferredWidth = ParamPreferredWidth

        ConfigurePageHeader is an Instance Action
            disable multiple instance selection
            completion message is "PageHeaderUpdated"

            Parameters
                ParamImage is BinaryObject
                    default label is "Image"

                ParamDescription is a ConfigDescription
                    default label is "Description"

                ParamPreferredHeight is Numeric size 4
                    default label is "PreferredHeight"

                ParamPreferredWidth	is Numeric size 4
                    default label is "PreferredWidth"

                ParamStatus is Numeric size 1
                    States
                        Active				value is 0
                        Inactive			value is 1
                    default label is "Status"

            Parameter Rules
                ParamImage
                    required
                    initial value is PageHeaderRel.Image

                ParamDescription
                    initial value is PageHeaderRel.Description

                ParamPreferredHeight
                    initial value is PageHeaderRel.PreferredHeight

                ParamPreferredWidth
                    initial value is PageHeaderRel.PreferredWidth

                ParamStatus
                    initial value is PageHeaderRel.Status

            Action Rules
                if (!PageHeaderConfigured)
                    invoke Create UserImage
                        invoked.UserImage = "reportpageheader"
                        invoked.Image = ParamImage
                        invoked.Description = ParamDescription
                        invoked.Status = ParamStatus
                        invoked.PreferredHeight = ParamPreferredHeight
                        invoked.PreferredWidth = ParamPreferredWidth
                else
                    invoke Update PageHeaderRel
                        invoked.Image = ParamImage
                        invoked.Description = ParamDescription
                        invoked.Status = ParamStatus
                        invoked.PreferredHeight = ParamPreferredHeight
                        invoked.PreferredWidth = ParamPreferredWidth

        ConfigurePageFooter is an Instance Action
            disable multiple instance selection
            completion message is "PageFooterUpdated"

            Parameters
                ParamImage is BinaryObject
                    default label is "Image"

                ParamDescription is a ConfigDescription
                    default label is "Description"

                ParamPreferredHeight is Numeric size 4
                    default label is "PreferredHeight"

                ParamPreferredWidth	is Numeric size 4
                    default label is "PreferredWidth"

                ParamStatus is Numeric size 1
                    States
                        Active				value is 0
                        Inactive			value is 1
                    default label is "Status"

            Parameter Rules
                ParamImage
                    required
                    initial value is PageFooterRel.Image

                ParamDescription
                    initial value is PageFooterRel.Description

                ParamPreferredHeight
                    initial value is PageFooterRel.PreferredHeight

                ParamPreferredWidth
                    initial value is PageFooterRel.PreferredWidth

                ParamStatus
                    initial value is PageFooterRel.Status

            Action Rules
                if (!PageFooterConfigured)
                    invoke Create UserImage
                        invoked.UserImage = "reportpagefooter"
                        invoked.Image = ParamImage
                        invoked.Description = ParamDescription
                        invoked.Status = ParamStatus
                        invoked.PreferredHeight = ParamPreferredHeight
                        invoked.PreferredWidth = ParamPreferredWidth
                else
                    invoke Update PageFooterRel
                        invoked.Image = ParamImage
                        invoked.Description = ParamDescription
                        invoked.Status = ParamStatus
                        invoked.PreferredHeight = ParamPreferredHeight
                        invoked.PreferredWidth = ParamPreferredWidth

        DeleteFirstPageHeader is an Instance Action
            valid when (FirstPageHeaderConfigured)
            completion message is "FirstPageHeaderDeleted"
            confirmation required
                "AreYouSureYouWantToDeleteFirstPageHeaderConfiguration?"

            Action Rules
                invoke Delete FirstPageHeaderRel

        DeletePageHeader is an Instance Action
            valid when (PageHeaderConfigured)
            completion message is "PageHeaderDeleted"
            confirmation required
                "AreYouSureYouWantToDeletePageHeaderConfiguration?"

            Action Rules
                invoke Delete PageHeaderRel

        DeletePageFooter is an Instance Action
            valid when (PageFooterConfigured)
            completion message is "PageFooterDeleted"
            confirmation required
                "AreYouSureYouWantToDeletePageFooterConfiguration?"

            Action Rules
                invoke Delete PageFooterRel

        DeleteListHeaderShadeStyle is an Instance Action
            valid when (ListHeaderShadeConfigured)
            completion message is "ListHeaderShadeStyleDeleted"
            confirmation required
                "AreYouSureYouWantToDeleteListHeaderFillAndFontStyleConfiguration?"

            Action Rules
                if (ShadeFillColorConfigured)
                    invoke Delete ShadeFillColorRel
                if (ShadeFontColorConfigured)
                    invoke Delete ShadeFontColorRel

	Relations
        PDFSetupRel
            one-to-one relation to PDFSetup
            Field Mapping uses symbolic key
                related.PDFSetup = "PDFSetup"

        FirstPageHeaderRel
            one-to-one relation to UserImage
            Field Mapping uses symbolic key
                related.UserImage = "reportfirstpageheader" 

        PageHeaderRel
            one-to-one relation to UserImage
            Field Mapping uses symbolic key
                related.UserImage = "reportpageheader" 

        PageFooterRel
            one-to-one relation to UserImage
            Field Mapping uses symbolic key
                related.UserImage = "reportpagefooter" 

        ShadeFillColorRel
            one-to-one relation to ConfigurationParameter
            Field Mapping uses symbolic key
                related.ConfigurationParameter.ConfigurationID 	= "pdf"
                related.ConfigurationParameter.Name 			= "pdf.shade.fill.color"

        ShadeFontColorRel
            one-to-one relation to ConfigurationParameter
            Field Mapping uses symbolic key
                related.ConfigurationParameter.ConfigurationID 	= "pdf"
                related.ConfigurationParameter.Name 			= "pdf.shade.font.color"



