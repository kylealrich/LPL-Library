PaymentOutputFileHeader is a BusinessClass
	owned by cb
	prefix is POFH

	Ontology
		symbolic key is PaymentOutputFileHeader

	Patterns
		implements StaticJava
		disable AuditIndex
		implements BODId
		
	Persistent Fields
		FileName						is AlphaUpper 50
		CreateDate						is TimeStamp
		PayGroupOutput					is like PayGroup	
			default label is "PayGroup"
		CashCode
		CashCodeGroup
		BankTransactionCode
		RequisitionType					is AlphaUpper 2


















		PayablesProcessGroup
		ProcessFlowName					is Alpha 50
		WriteCSVHeader					is Boolean
		ConsolidateDetailAndRemittance	is Boolean
		FTPConfiguration			
		FTPHeaderFile					is Boolean
		FTPDetailFile					is Boolean
		FTPRemittanceFile				is Boolean
		HeaderFile						is a PaymentAttachment
		DetailFile						is a PaymentAttachment
		RemittanceFile					is a PaymentAttachment
		ConsolidatedFile				is a PaymentAttachment
		FlatFile						is a PaymentAttachment
		InProcess						is Boolean
		TriggerID						is Alpha size 50
		NeedToCreatePaymentFiles		is Alpha size 2
			States
				Yes				value is "Y"
				No				value is "N"
				NotApplicable	value is "NA"
		ProcessedByThirdParty			is Boolean
		KyribaJSONResponse 				is JSONObject
		GTreasuryJSONResponse 			is JSONObject	
		ErrorId							is Alpha up to 100
		KyribaFileAttachment 			is a MultiPart
		PaymentOutputMapping
    	TotalNumberOfRecords 			is Numeric 10	
    	TotalPaymentAmount 				is an InternationalAmount	
		TotalPayBankAmount				is an InternationalAmount	
		ReceiverBank					is Numeric 2				
			States
				BankOfAmerica			value is 1
				WellsFargo				value is 2
				JPMC					value is 3
				USBank					value is 4
		ReceiverEmailAddress					is Alpha 25
			holds pii
		
	Transient Fields
		TransientFromCurrency is a FromCurrency
			derive value from CashManagementAccount.Currency

	Local Fields
		LocalActionCode				is Alpha 40
		LocalPaymentOutputFileDetailRange is a PaymentOutputFileDetailRange 
		
	Derived Fields
		TotalRecordCount is a ComputeField
			type is Numeric 10	
			(instance count of PaymentOutputFileDetail set)

		TotalAmountDerived is a DerivedField
			type is like InternationalAmount
			restricted
			return (sum PaymentOutputFileDetail set.PaymentAmount)

		TotalAmountNumberOfDecimals is a StringField
			type is Alpha 4
			restricted
			"%."
			first PaymentOutputFileDetail set.BankAccountCurrency.NumberOfDecimals
			"f"

		TotalAmount is a StringField
			type is Alpha 23
			TotalAmountDerived using "<TotalAmountNumberOfDecimals>"

		CashCodeOrAccount is a DerivedField
			type is Alpha 50
			restricted
			if (CashCode not entered)
				return CashManagementAccount
			else
				return CashCode

		HeaderOutputFileName is a StringField
			type is Alpha 100
			FileName
			"-"
			CashCodeOrAccount
			"Header"

		DerivedDetailName is a DerivedField
			type is Alpha 15
			if (IsFlatFile)
				return "FlatFileDetail"
			return "Detail"

		DetailOutputFileName is a DerivedField
			type is Alpha 100
			if (CashPaymentFormatRel.PaymentFormat.InforTreasuryFile)
				return TreasuryDetailOutputFileName
			else
				return NonTreasuryDetailOutputFileName

		TreasuryDetailOutputFileName is a StringField
			type is Alpha 100
			restricted
			"AP"
			FileName
			"-"
			CashCodeOrAccount
			"Detail"

		NonTreasuryDetailOutputFileName is a StringField
			type is Alpha 100
			restricted
			FileName
			"-"
			CashCodeOrAccount
			"Detail"

		FlatFileDetailOutputFileName is a StringField
			type is Alpha 100
			FileName
			"-"
			CashCodeOrAccount
			"FlatFileDetail"

		RemittanceOutputFileName is a StringField
			type is Alpha 100
			FileName
			"-"
			CashCodeOrAccount
			"Remittance"

		FlatFileRemittanceOutputFileName is a StringField
			type is Alpha 100
			FileName
			"-"
			CashCodeOrAccount
			"FlatFileRemittance"

		FTPConfigurationName is a DerivedField
			type is Alpha 100
			if (FTPConfiguration.System)
				return "system"
			else
			if (FTPConfiguration.System1)
				return "system1"
			else
			if (FTPConfiguration.System2)
				return "system2"
			else
			if (FTPConfiguration.System3)
				return "system3"
			else
			if (FTPConfiguration.System4)
				return "system4"

		EmailInformation is a MessageField
			"OneOrMoreOfTheDetailHasBeenSentOutViaEmail"

		DerivedEmailFrom is a DerivedField
			type is like EmailAddress
			default label is "From"
			if (CashPaymentFormatRel.SupplementaryFromEmail entered)
				return CashPaymentFormatRel.SupplementaryFromEmail
			else
			if (PayGroupRel.RemittanceAdviceFromEmailAddress entered)
				return PayGroupRel.RemittanceAdviceFromEmailAddress
			else
			if (VendorGroupRel.RemittanceAdviceFromEmailAddress entered)
				return VendorGroupRel.RemittanceAdviceFromEmailAddress
			else
				return config.DefaultFromEmailAddress

		KyribaJSONRequest is a DerivedField
			type is JSONObject
			KyribaJSONRequest = template.KyribaRequest_Template document for this instance

		KyribaFileAttachmentName is a StringField
			type is Alpha 100
			restricted
			"infor-trigger-pymt-"
			PaymentOutputFileHeader
			"@infor.com"

		KyribaFileAttachmentFileName is a StringField
			type is Alpha 100
			restricted
			"infor-trigger-pymt-"
			PaymentOutputFileHeader
			".json"

		GTreasuryJSONRequest is a DerivedField		
			type is JSONObject
			GTreasuryJSONRequest = template.GTreasuryRequest_Template document for this instance

		GTreasuryCompanyId is a DerivedField		
			type is Alpha 250
			return CashManagementGroup.InforTreasurySubscriptionKey

	Conditions
		ShowHeaderFile
			restricted
			when (!IsFlatFile
			and   !CashPaymentFormatRel.PaymentFormat.InforTreasuryFile)

		HasHeaderAttachment
			restricted
			when (HeaderFile entered)

		HasDetailAttachment
			restricted
			when (DetailFile entered)

		HasRemittanceAttachment
			restricted
			when (RemittanceFile entered)

		HasFlatAttachment
			restricted
			when (FlatFile entered)

		IsFlatFile
			restricted
			when (FlatFileOutput set exists)

		CashCodeEntered
			restricted
			when (CashCode entered)

		CashCodeGroupEntered
			restricted
			when (CashCodeGroup entered)

		RemittanceOptionUsesIDMTemplate
			restricted
			when (CashPaymentFormatRel.UseIDMTemplate
			and PaymentOutputFileDetailRemittanceAdviceRel exists)

		IsIDMEmailRemittanceAdviceEnabled
			restricted
			when (CashPaymentFormatRel.UseIDMTemplate
			and PaymentOutputFileDetailEmailRemittanceEnabledRel exists)

		PaymentDetailHasBeenEmailed
			restricted
			when (PaymentOutputFileDetailEmailedRel exists)

		ValidForMarkCheckAsPrinted	
			restricted
			when (CashPaymentFormatRel.CheckVersioning
			and PaymentOutputFileDetailNotCheckPrintedRel exists)

		ValidForIDMCheckPrinting	
			restricted
			when (CashPaymentFormatRel.PaymentFormat.IDMOutputFile
			and   not PaymentOutputFileDetailNotAllWithIDMCheckRel exists)
			
		ValidForIDMRemittancePrinting
			restricted
			when ((CashPaymentFormatRel.PaymentFormat.IDMOutputFile
			or   CashPaymentFormatRel.UseIDMTemplate) 
			and (AllHaveSeparateRemittance 
			or   CashPaymentFormatRel.RemittanceOption.OverflowDocument))
			
		AllHaveSeparateRemittance
			restricted
			when (CashPaymentFormatRel.RemittanceOption.SeparateRemittance
			and   not PaymentOutputFileDetailNotAllWithRemittanceAdviceRel exists)

		ValidFormIDMProcessIDMOutputFile
			restricted
			when (CashPaymentFormatRel.PaymentFormat.IDMOutputFile
			or    CashPaymentFormatRel.UseIDMTemplate)
			
		InforTreasuryProcessing
			restricted
			when (CashPaymentFormatRel.PaymentFormat.InforTreasuryFile
			and   CashManagementGroup.InforTreasurySubscriptionKey entered)

		IsKyribaIntegration		
			restricted
			when (CashManagementGroup.InforTreasuryIntegrationType.Kyriba)

		IsGTreasuryIntegration	
			restricted
			when (CashManagementGroup.InforTreasuryIntegrationType.GTreasury)

		CreatedFromElectronicPaymentCreation	
			restricted
			when (!CashPaymentFormatRel.PaymentFormat.AchFile
			and   !CashPaymentFormatRel.PaymentFormat.BacsFile)
		ReceiverBankEntered
			restricted
			when (ReceiverBank entered)
		
		ReceiverEmailAddressEntered
			restricted
			when (ReceiverEmailAddress entered)
		

	Relations
		CashPaymentFormatRel
			one-to-one relation to CashPaymentFormat
			Field Mapping uses symbolic key
				related.CashManagementGroup 					= CashManagementGroup
				related.CashCode								= CashCode
				related.CashPaymentFormat.BankTransactionCode	= BankTransactionCode
				related.CashPaymentFormat.PayablesProcessGroup	= PayablesProcessGroup

		PaymentOutputFileDetailEmailedRel is a PaymentOutputFileDetail set
			Instance Selection
				where (related.IDMEmailSent)

		PaymentOutputFileDetailWithIDMCheckRel	is a PaymentOutputFileDetail set
			Instance Selection
				where (related.PaymentIDMUniqueId entered)

		PaymentOutputFileDetailNotCheckPrintedRel	is a PaymentOutputFileDetail set
			Instance Selection
				where (not related.CheckPrinted)

		PaymentOutputFileDetailRangeRel	is a PaymentOutputFileDetail set
			Instance Selection
				where (related.PaymentOutputFileDetail within LocalPaymentOutputFileDetailRange
				and related.PaymentIDMUniqueId entered)

		PaymentOutputFileDetailRemittanceAdviceRel is a PaymentOutputFileDetail set
			Instance Selection
				where (related.SupplementaryIDMUniqueId entered)

		PaymentOutputFileDetailRemittanceRangeRel is a PaymentOutputFileDetail set
			Instance Selection
				where (related.PaymentOutputFileDetail within LocalPaymentOutputFileDetailRange
				and related.SupplementaryIDMUniqueId entered)

		PaymentOutputFileDetailNotAllWithIDMCheckRel is a PaymentOutputFileDetail set
			Instance Selection
				where (related.PaymentIDMUniqueId not entered)

		PaymentOutputFileDetailNotAllWithRemittanceAdviceRel is a PaymentOutputFileDetail set
			Instance Selection
				where (related.SupplementaryIDMUniqueId not entered)
				
		PaymentOutputFileDetailRel is a PaymentOutputFileDetail set

		PaymentOutputFileDetailEmailRemittanceEnabledRel
			one-to-many relation to PaymentOutputFileDetail
			Field Mapping uses symbolic key
				related.CashManagementGroup 	= CashManagementGroup
				related.CashManagementAccount	= CashManagementAccount
				related.PaymentOutputFileHeader = PaymentOutputFileHeader
			Instance Selection
				where (related.SupplementaryIDMUniqueId entered
				and related.EmailRemittanceToVendor
				and related.IsVendorPrimaryContactEmailRemittanceAdviceEnabled)

		VendorGroupRel
			one-to-one relation to VendorGroup
			Field Mapping uses symbolic key
				related.VendorGroup = first PaymentOutputFileDetailRel.VendorGroupOutput

		PayGroupRel
			one-to-one relation to PayGroup
			Field Mapping uses symbolic key
				related.VendorGroup = VendorGroupRel.VendorGroup
				related.PayGroup    = PayGroupOutput
				
		CXMLConfigurationRel
			one-to-many relation to CXMLConfiguration
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup = VendorGroupRel.VendorGroup.FinanceEnterpriseGroup
			Instance Selection
				where (related.Default)

	Sets
		ByCreateDate
			Sort Order
				CreateDate				descending
				CashManagementAccount
				CashCode
				FileName
				PaymentOutputFileHeader

		ByCashCodeGroup
			Sort Order
				CashManagementGroup
				CashManagementAccount
				CashCodeGroup
				PaymentOutputFileHeader	descending

		ByFileName
			Sort Order
				FileName
				CreateDate				descending
				CashManagementAccount
				CashCode
				PaymentOutputFileHeader

		ByCashCode
			Sort Order
				CashCode
				CreateDate				descending
				FileName
				PaymentOutputFileHeader

		ByCashCodeGroupInProcess
			Instance Selection
				where (InProcess)
			Sort Order
				CashManagementGroup
				TriggerID
				CashCodeGroup
				CashManagementAccount
				PaymentOutputFileHeader

		ByProcessedByThirdParty
			Sort Order
				ProcessedByThirdParty	descending
				CashManagementGroup
				CashManagementAccount
				PaymentOutputFileHeader

	Field Rules
		CreateDate
			default to current timestamp 

		FileName
			default to BankTransactionCode

		RequisitionType
			if (BankTransactionCode.PaymentOutputOption.ElectronicFile
			or  BankTransactionCode.PaymentOutputOption.Swift
			or  BankTransactionCode.PaymentOutputOption.WireTransfer)
				RequisitionType	= "EP"
			else
				if (BankTransactionCode.PaymentOutputOption.PrintedDocument)
					RequisitionType	= "CQ"

		ProcessFlowName
			default to "ExportPaymentOutputFiles"

		WriteCSVHeader
			if (CashPaymentFormatRel.PaymentFormat.InforTreasuryFile)
				force default to false
			if (CashCode entered)
				default to CashPaymentFormatRel.WriteCSVHeader
			else
				default to CashManagementAccount.WriteCSVHeader

		ConsolidateDetailAndRemittance
			if (CashCode entered)
				default to CashPaymentFormatRel.ConsolidateDetailAndRemittance
			else
				default to CashManagementAccount.ConsolidateDetailAndRemittance

		FTPConfiguration
			if (CashCode entered)
				default to CashPaymentFormatRel.FTPConfiguration
				default to 1	
			else
				default to CashManagementAccount.FTPConfiguration
				default to 1	

		FTPHeaderFile
			if (CashCode entered)
				default to CashPaymentFormatRel.FTPHeaderFile
			else
				default to CashManagementAccount.FTPHeaderFile

		FTPDetailFile
			if (CashCode entered)
				default to CashPaymentFormatRel.FTPDetailFile
			else
				default to CashManagementAccount.FTPDetailFile

		FTPRemittanceFile
			if (CashCode entered)
				default to CashPaymentFormatRel.FTPRemittanceFile
			else
				default to CashManagementAccount.FTPRemittanceFile

		NeedToCreatePaymentFiles
			if (NeedToCreatePaymentFiles not entered)
				if (BankTransactionCode.PaymentOutputOption.NoOutput)
					NeedToCreatePaymentFiles = NeedToCreatePaymentFiles.NotApplicable
				else
					NeedToCreatePaymentFiles = NeedToCreatePaymentFiles.Yes

		PaymentOutputMapping
			force default to CashPaymentFormatRel.PaymentOutputMapping
				
	Actions
		Create is a Create Action
			restricted
			Action Rules
				InProcess = true

		Update is an Update Action
			restricted

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted

		FinishProcessing is an Instance Action
			default label is untranslatable
			restricted
			Action Rules
				if (PaymentOutputMapping entered)
					if (!PaymentOutputMapping.Delimiter entered)
						invoke UpdateDelimiter PaymentOutputMapping
							
				if (CashPaymentFormatRel.PaymentFormat.BODOutput)
					invoke TriggerCreditTransferPaymentOutputFile
				else
					if (InforTreasuryProcessing)
						if (IsKyribaIntegration)
							invoke TriggerKyribaPaymentFile
						else
						if (IsGTreasuryIntegration)	
							invoke TriggerGTreasuryPaymentFile
					else
						if (NeedToCreatePaymentFiles.Yes)
							trigger "APPaymentFiles" PA service
								resume on error
								title is "CreateAPPaymentFiles"
								Variables
									CashManagementGroup
									CashManagementAccount
									PaymentOutputFileHeader
									ProcessFlowName
							NeedToCreatePaymentFiles = NeedToCreatePaymentFiles.No
							if (CXMLConfigurationRel.PaymentRemittance)
								invoke CreatePaymentRemittanceRequest PaymentOutputFileDetail		
									invoked.PrmCashManagementGroup		= PaymentOutputFileHeader.CashManagementGroup
									invoked.PrmCashManagementAccount	= PaymentOutputFileHeader.CashManagementAccount
									invoked.PrmPaymentOutpuFileHeader	= PaymentOutputFileHeader







				InProcess = false

		CreatePaymentFiles is an Instance Action
			Local Fields
				TriggerError is Boolean
			Action Rules
				if(CashPaymentFormatRel.PaymentFormat.BODOutput)
					invoke TriggerCreditTransferPaymentOutputFile
				else
					if (PaymentOutputMapping entered)
						if (!PaymentOutputMapping.Delimiter entered)
							invoke UpdateDelimiter PaymentOutputMapping

					if (InforTreasuryProcessing)
						if (IsKyribaIntegration)
							invoke TriggerKyribaPaymentFile
						else
						if (IsGTreasuryIntegration)	
							invoke TriggerGTreasuryPaymentFile
					else
						trigger "APPaymentFiles" PA service
							resume on error
								TriggerError = true
							title is "CreateAPPaymentFiles"
							Variables
								CashManagementGroup
								CashManagementAccount
								PaymentOutputFileHeader
								ProcessFlowName
						constraint (!TriggerError)
							"AP_Payment_FilesServiceIsNotEnabled.ContactAdministrator."
			Exit Rules
				NeedToCreatePaymentFiles = NeedToCreatePaymentFiles.No

		ResetProcessedByThirdParty is an Instance Action	
			valid when (ProcessedByThirdParty)				
			Action Rules									
				ProcessedByThirdParty = false				

		PurgePaymentOutputRecords is a Set Action
			run in foreground
			completion message is "PurgeOfPaymentOutputRecordsHasCompleted"
			Parameters
				PrmCashManagementAccount	is like CashManagementAccount
				PrmCashManagementGroup		is a CashManagementAccount group
				PrmCreateDateRange			is a DateRange 
				
			Parameter Rules
				PrmCreateDateRange
					required
				PrmCashManagementAccount
					constraint (PrmCashManagementGroup not entered)
						"EnterEitherACashManagementAccountGroupOrACashManagementAccount"
				PrmCashManagementGroup
					constraint (PrmCashManagementAccount not entered)
						"EnterEitherACashManagementAcountGroupOrACashManagementAccount"

			Instance Selection
				where  (CreateDate date within PrmCreateDateRange
				and   ((PrmCashManagementGroup entered
				and     CashManagementAccount within PrmCashManagementGroup)
				or	   (PrmCashManagementAccount entered
				and     CashManagementAccount = PrmCashManagementAccount)))
	
			Action Rules
				Instance Rules
					invoke Purge

		ProcessIDMOutputFiles is an Instance Action
			valid when (ValidFormIDMProcessIDMOutputFile)
			Parameters
				PrmProcessAll				   is Boolean
					default label is "ProcessAll"
				PrmPaymentOutputFileDetailRange	is a PaymentOutputFileDetailRange
					default label is "PaymentOutputFileDetail"		
					
			Parameter Rules		
				PrmProcessAll
					initial value is true
							
				PrmPaymentOutputFileDetailRange
					if (not PrmProcessAll)
						required
						
						constraint (PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail entered
						and PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail entered)
							"Field_\FromAnd_\ToAreRequired"
							
						LocalPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange
													
			Action Rules
				invoke GeneratePaymentDetailFile PaymentOutputFileDetail			
					invoked.PrmCashManagementGroup		= PaymentOutputFileHeader.CashManagementGroup
					invoked.PrmCashManagementAccount	= PaymentOutputFileHeader.CashManagementAccount
					invoked.PrmPaymentOutputFileHeader	= PaymentOutputFileHeader
					invoked.PrmPaymentTemplate			= CashPaymentFormatRel.PaymentTemplate
					invoked.PrmPaymentFilename			= CashPaymentFormatRel.PaymentFilename
					invoked.PrmPaymentFromEmail			= CashPaymentFormatRel.PaymentFromEmail
					invoked.PrmPaymentPrinter			= CashPaymentFormatRel.PaymentPrinter
					invoked.PrmSupplementaryTemplate	= CashPaymentFormatRel.SupplementaryTemplate
					invoked.PrmSupplementaryFilename	= CashPaymentFormatRel.SupplementaryFilename
					invoked.PrmSupplementaryFromEmail	= CashPaymentFormatRel.SupplementaryFromEmail
					invoked.PrmSupplementaryPrinter		= CashPaymentFormatRel.SupplementaryPrinter
					invoked.PrmUsePaymentPrinter		= CashPaymentFormatRel.UsePaymentPrinter
					invoked.PrmUsePaymentEMail			= CashPaymentFormatRel.UsePaymentEMail
					invoked.PrmUseSupplementaryPrinter	= CashPaymentFormatRel.UseSupplementaryPrinter
					invoked.PrmUseSupplementaryEmail	= CashPaymentFormatRel.UseSupplementaryEmail			
					invoked.PrmConsolidateIDMNotification = true
					if (PrmProcessAll)
						invoked.PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail = first PaymentOutputFileDetailRel.PaymentOutputFileDetail
						invoked.PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail   = last  PaymentOutputFileDetailRel.PaymentOutputFileDetail 
					else
						invoked.PrmPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange


		GeneratePaymentFile is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmPaymentTemplate			is an IDMTemplate
				PrmPaymentFilename			is Alpha 20
				PrmPaymentFromEmail			is an EmailAddress 
					holds pii
				PrmPaymentPrinter			is an IDMPrinter
				PrmSupplementaryTemplate	is an IDMTemplate
				PrmSupplementaryFilename	is Alpha 20
				PrmSupplementaryFromEmail	is an EmailAddress 
					holds pii
				PrmSupplementaryPrinter		is an IDMPrinter
				PrmUsePaymentPrinter		is Boolean
				PrmUsePaymentEMail			is Boolean
				PrmUseSupplementaryPrinter	is Boolean
				PrmUseSupplementaryEmail	is Boolean

			Action Rules	
				invoke GeneratePaymentDetailFile PaymentOutputFileDetail
					invoked.PrmCashManagementGroup		= PaymentOutputFileHeader.CashManagementGroup
					invoked.PrmCashManagementAccount	= PaymentOutputFileHeader.CashManagementAccount
					invoked.PrmPaymentOutputFileHeader	= PaymentOutputFileHeader
					invoked.PrmPaymentTemplate			= PrmPaymentTemplate
					invoked.PrmPaymentFilename			= PrmPaymentFilename
					invoked.PrmPaymentFromEmail			= PrmPaymentFromEmail
					invoked.PrmPaymentPrinter			= PrmPaymentPrinter
					invoked.PrmSupplementaryTemplate	= PrmSupplementaryTemplate
					invoked.PrmSupplementaryFilename	= PrmSupplementaryFilename
					invoked.PrmSupplementaryFromEmail	= PrmSupplementaryFromEmail
					invoked.PrmSupplementaryPrinter		= PrmSupplementaryPrinter
					invoked.PrmUsePaymentPrinter		= PrmUsePaymentPrinter
					invoked.PrmUsePaymentEMail			= PrmUsePaymentEMail
					invoked.PrmUseSupplementaryPrinter	= PrmUseSupplementaryPrinter
					invoked.PrmUseSupplementaryEmail	= PrmUseSupplementaryEmail
					invoked.PrmConsolidateIDMNotification = true
					invoked.PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail = first PaymentOutputFileDetailRel.PaymentOutputFileDetail
					invoked.PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail   = last  PaymentOutputFileDetailRel.PaymentOutputFileDetail 
						
		EmailRemittanceAdvice is an Instance Action
			valid when (RemittanceOptionUsesIDMTemplate)
			Parameters
				PrmFromEmail is an EmailAddress
					holds pii
					default label is "From"
				
				PrmEmailSubject is Alpha 255
					default label is "Subject"
					Text Variables
						RemittanceAdviceFilename
						CashCode
						BankTransactionCode
						TransactionNumber
						PaymentDate
						VendorName
				
				PrmEmailTemplate is an IDMTemplate
					default label is "EmailTemplate"
					
			Parameter Rules
				PrmFromEmail
					initial value is DerivedEmailFrom
					required
				
				PrmEmailSubject
					initial value is PaymentOutputFileHeader.CashPaymentFormatRel.SupplementaryEmailSubject
						
				PrmEmailTemplate
					initial value is CashPaymentFormatRel.SupplementaryEmailTemplate
					constraint (PrmEmailTemplate.IDMDocumentType.PayablesPaymentOutputEmail)
						"InvalidTemplate"
			
			Action Rules
				constraint (IsIDMEmailRemittanceAdviceEnabled)
					"Remittance_AdviceEmailingIsDisabledForAll_Vendors.NoEmailWillBeSent."
				invoke SendRemittanceAdvice PaymentOutputFileDetail
					invoked.PrmCashManagementGroup		= PaymentOutputFileHeader.CashManagementGroup
					invoked.PrmCashManagementAccount	= PaymentOutputFileHeader.CashManagementAccount
					invoked.PrmPaymentOutpuFileHeader	= PaymentOutputFileHeader
					invoked.PrmSupplementaryTemplate	= CashPaymentFormatRel.SupplementaryTemplate
					invoked.PrmSupplementaryFilename	= CashPaymentFormatRel.SupplementaryFilename
					invoked.PrmFromEmail			    = PrmFromEmail
					invoked.PrmEmailSubject				= PrmEmailSubject
					invoked.PrmEmailTemplate			= PrmEmailTemplate
					invoked.PrmConsolidateIDMNotification = true


		PrintCheck is an Instance Action
			default label is "PrintChecks"
			valid when (ValidForIDMCheckPrinting)
			Parameters
				PrmPaymentPrinter 		   is an IDMPrinter
					default label is "PaymentPrinter"
				PrmIncludeRemittanceAdvice is Boolean
					default label is "IncludeRemittanceAdvice"
				PrmRemittanceAdvicePrinter is an IDMPrinter
					default label is "RemittanceAdvicePrinter"
				PrmPrintAll				   is Boolean
					default label is "PrintAll"
				PrmPaymentOutputFileDetailRange	is a PaymentOutputFileDetailRange
					default label is "PaymentOutputFileDetail"
					
			Parameter Rules
				PrmPaymentPrinter
					initial value is CashPaymentFormatRel.PaymentPrinter
					force default to CashPaymentFormatRel.PaymentPrinter
					required
						"PaymentPrinterNotDefinedInCashPaymentFormat"
					
				PrmRemittanceAdvicePrinter
					initial value is CashPaymentFormatRel.SupplementaryPrinter
					force default to CashPaymentFormatRel.SupplementaryPrinter
					if (PrmIncludeRemittanceAdvice)
						required
							"RemittanceAdvicePrinterNotDefinedInCashPaymentFormat"
				
				PrmPrintAll
					initial value is true
							
				PrmPaymentOutputFileDetailRange
					if (not PrmPrintAll)
						required
						
						constraint (PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail entered
						and PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail entered)
							"Field_\FromAnd_\ToAreRequired"
							
						LocalPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange
								
			Action Rules
				invoke PrintCheckSet PaymentOutputFileDetail
					invoked.PrmCashManagementGroup	   = CashManagementGroup
					invoked.PrmCashManagementAccount   = CashManagementAccount
					invoked.PrmPaymentOutputFileHeader = PaymentOutputFileHeader
					invoked.PrmPaymentPrinter          = PrmPaymentPrinter
					invoked.PrmIncludeRemittanceAdvice = PrmIncludeRemittanceAdvice
					invoked.PrmRemittanceAdvicePrinter = PrmRemittanceAdvicePrinter
					invoked.PrmPrintAll				   = PrmPrintAll
					if (PrmPrintAll)
						invoked.PrmInstanceCount	   = instance count of PaymentOutputFileDetailWithIDMCheckRel
						invoked.PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail = first PaymentOutputFileDetailWithIDMCheckRel.PaymentOutputFileDetail
						invoked.PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail   = last  PaymentOutputFileDetailWithIDMCheckRel.PaymentOutputFileDetail
					else
						invoked.PrmInstanceCount 	   = instance count of PaymentOutputFileDetailRangeRel
						invoked.PrmPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange
						

		PrintRemittanceAdvice is an Instance Action
			valid when (ValidForIDMRemittancePrinting)
			Parameters
				PrmRemittanceAdvicePrinter is an IDMPrinter
					default label is "RemittanceAdvicePrinter"

				PrmPrintAll				   is Boolean
					default label is "PrintAll"

				PrmPaymentOutputFileDetailRange	is a PaymentOutputFileDetailRange
					default label is "PaymentOutputFileDetail"
					
			Parameter Rules
				PrmRemittanceAdvicePrinter
					initial value is CashPaymentFormatRel.SupplementaryPrinter
					force default to CashPaymentFormatRel.SupplementaryPrinter
					required
						"RemittanceAdvicePrinterNotDefinedInCashPaymentFormat"

				PrmPrintAll
					initial value is true

				PrmPaymentOutputFileDetailRange
					if (not PrmPrintAll)
						required
						
						constraint (PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail entered
						and PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail entered)
							"Field_\FromAnd_\ToAreRequired"
							
						LocalPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange

			Action Rules
				invoke PrintRemittanceAdviceSet PaymentOutputFileDetail
					invoked.PrmCashManagementGroup	   = CashManagementGroup
					invoked.PrmCashManagementAccount   = CashManagementAccount
					invoked.PrmPaymentOutputFileHeader = PaymentOutputFileHeader
					invoked.PrmRemittanceAdvicePrinter = PrmRemittanceAdvicePrinter
					invoked.PrmPrintAll				   = PrmPrintAll
					if (PrmPrintAll)
						invoked.PrmInstanceCount	   = instance count of PaymentOutputFileDetailRemittanceAdviceRel
						invoked.PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail = first PaymentOutputFileDetailRemittanceAdviceRel.PaymentOutputFileDetail
						invoked.PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail   = last  PaymentOutputFileDetailRemittanceAdviceRel.PaymentOutputFileDetail
					else
						invoked.PrmInstanceCount 	   = instance count of PaymentOutputFileDetailRemittanceRangeRel
						invoked.PrmPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange

		MarkCheckAsPrinted is an Instance Action
			default label is "Mark_\Checks_as_\Printed"
			valid when (ValidForMarkCheckAsPrinted)
			Parameters
				PrmAllRecords     is Boolean
					default label is "AllDetailRecords"

				PrmPaymentOutputFileDetailRange	is a PaymentOutputFileDetailRange
					default label is "PaymentOutputFileDetail"			

			Parameter Rules
				PrmAllRecords
					initial value is true

				PrmPaymentOutputFileDetailRange
					if (not PrmAllRecords)
						required

						constraint (PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail entered)
							"FromFieldIsRequired"

						constraint (PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail entered)
							"ToFieldIsRequired"

						constraint (PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail >= PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail)
            				"ToFieldCannotPrecede_\FromField"

						LocalPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange
			Action Rules
				invoke MarkCheckAsPrintedSet PaymentOutputFileDetail
					invoked.PrmCashManagementGroup	   = CashManagementGroup
					invoked.PrmCashManagementAccount   = CashManagementAccount
					invoked.PrmPaymentOutputFileHeader = PaymentOutputFileHeader
					invoked.PrmAllRecords			   = PrmAllRecords
					if (PrmAllRecords)
						invoked.PrmPaymentOutputFileDetailRange.FromPaymentOutputFileDetail = first PaymentOutputFileDetailWithIDMCheckRel.PaymentOutputFileDetail
						invoked.PrmPaymentOutputFileDetailRange.ToPaymentOutputFileDetail   = last  PaymentOutputFileDetailWithIDMCheckRel.PaymentOutputFileDetail
					else
						invoked.PrmPaymentOutputFileDetailRange = PrmPaymentOutputFileDetailRange

		
		UpdateBODIdFields is an Instance Action
			default label is untranslatable
			restricted
			Parameters
				PrmDocumentID        is Alpha size 100
					default label is "DocumentID"
				PrmRevision          is Alpha size 22
					default label is "Revision"	
				PrmSystemOfRecord    is Alpha size 1
					default label is "SystemOfRecord"
			Action Rules			
				if (bod id.DocumentID 			!= PrmDocumentID)
					bod id.DocumentID			= PrmDocumentID
				if (bod id.Revision 			!= PrmRevision)
					bod id.Revision				= PrmRevision
				if (bod id.SystemOfRecord 		!= PrmSystemOfRecord)
					bod id.SystemOfRecord		= PrmSystemOfRecord
					
		TriggerCreditTransferPaymentOutputFile is an Instance Action  
		 	default label is untranslatable
		 	restricted
			Action Rules
				if (CashManagementGroup.FinanceEnterpriseGroup.BODTrigger)
					if (action != "UpdateBODIdFields")
						LocalActionCode = ActionCode.Update				
						increment bod id.VariationID
						trigger "PaymentOutputFileService" PA service
							resume on error
							title is "CMG:<CashManagementGroup>BTC:<BankTransactionCode>CC:<CashCode>"
							Criteria
								CashManagementGroup
								BankTransactionCode
							Variables
								include persistent fields from PaymentOutputFileHeader
								LocalActionCode
									variable name is ActionCode
								TotalRecordCount
									variable name is TotalRecordCount
								TotalAmount
									variable name is TotalAmount
									
		TriggerKyribaPaymentFile is an Instance Action
		 	default label is untranslatable
		 	restricted
			Action Rules
				KyribaFileAttachment.Name = KyribaFileAttachmentName
				KyribaFileAttachment.FileName = KyribaFileAttachmentFileName
				KyribaFileAttachment.Content = KyribaJSONRequest
				KyribaFileAttachment.ContentType = "application/json"
				invoke RunPaymentActualsWithFile KyribaDataHub
					invoked.Request												= KyribaFileAttachment
					invoked.KYRIBADATAHUBWSIHTTPHEADEROCPAPIMTRACE 				= "true"
					invoked.KYRIBADATAHUBWSIHTTPHEADEROCPAPIMSUBSCRIPTIONKEY 	= CashManagementGroup.InforTreasurySubscriptionKey
					KyribaJSONResponse											= result.Response
				ErrorId = KyribaJSONResponse select "$['errorId']"

		TriggerGTreasuryPaymentFile is an Instance Action	
		 	default label is untranslatable
		 	restricted
			Action Rules
				invoke PaymentTrigger GTreasuryCallout
					invoked.GTREASURYCALLOUTWSIHTTPHEADERCONTENTTYPE  	= "application/json"
					invoked.Request								    	= GTreasuryJSONRequest
					GTreasuryJSONResponse				    			= result.Response
					



                ErrorId = GTreasuryJSONResponse select "$.errors.message"
