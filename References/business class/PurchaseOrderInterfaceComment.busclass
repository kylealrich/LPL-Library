PurchaseOrderInterfaceComment is a BusinessClass
	owned by po
	prefix is PICMT
	
	Ontology
		part of PurchaseOrderInterface
			delete cascades
			relative key is SequenceNumber
			
	Patterns
		implements StaticJava
			
	Persistent Fields
		CommentTitle					is a CommentName
    	CommentType						is AlphaUpper size 1
    		States
                PrintOnInternalDocuments	value is "I"
                PrintOnReceivingDocument	value is "R"
                PrintOnPurchaseOrder		value is "P"
                InvoiceComments				value is "N"
                PrintOnDeliveryTicket		value is "D"
				DisplayOnlyOnInternalDocuments value is "S"
                DisplayOnly					value is "O"
    	CommentText
		Attachment						is an AlternateAttachment
	
	Field Rules
		SequenceNumber
			autosequence
				minimize contention
			
		CommentType
			required

		CommentTitle
			required
		
	Local Fields
		LocalAttributeCtr   is Numeric 2

	Conditions
    	HasAttachment
    		restricted
    		when (Attachment entered)
	
	Relations
	
	Sets
		ByCommentType
			Sort Order
				Company	
				PurchaseOrderInterface
				CommentType
				SequenceNumber

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment
				
	Actions
		Create is a Create Action

		Update is an Update Action
		
		Delete is a Delete Action			
	
		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField with Attachment							

		MoveAttachmentsToIDM is a Set Action
			default label is untranslatable
			restricted
			
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop
