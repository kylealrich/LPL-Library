AnalyticsUpgradeActiveSpace is a BusinessClass
	owned by filecreation
	
	prefix is Augas
	
	Ontology
		symbolic key is AnalyticsUpgradeActiveSpace
		
	
	Patterns
		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		Description 							is Alpha size 300
		Status 									is Numeric size 1
			States
				InProgress  value is 1
				Completed	value is 2
				Canceled	value is 3
		CreationDate 							is Date
		CreatedBy								is Alpha size 200
		MergeContentCustomization				is Numeric size 1
			States
				DoneMerge		value is 1
				NoCustomization value is 2	
		MergeModelCustomization					is Numeric size 1
			States
				DoneMerge		value is 1
				NoCustomization value is 2
		CurrentStep								is Alpha size 6
		UpgradeActiveSpacesIsCompleted			is Boolean
		LoadDataToStagingIsCompleted			is Boolean
		SMDToATCWorkUnit						is a PfiWorkunit
		PreUpgradeStepsCompleted				is Boolean
	
	Field Rules
		Description
			default to "UpgradeWithActiveSpaces"
		CreationDate
			default to current corporate date
		CreatedBy
			default to actor
		Status
			default to Status.InProgress
			if(LoadDataToStagingIsCompleted)
				Status=Status.Completed
		CurrentStep
			default to "0/3"
			if(UpgradeActiveSpacesIsCompleted)
				CurrentStep="1/3"
			if(MergeContentCustomization entered and MergeModelCustomization entered)
				CurrentStep="2/3"
			if(LoadDataToStagingIsCompleted)
				CurrentStep="3/3"
				
		UpgradeActiveSpacesIsCompleted
			if(UpgradeActiveSpacesIsCompleted)
				constraint (last LatestSMDtoATCRel exists and last LatestSMDtoATCRel.Status entered )
					"CannotProceedFutherWithoutRunningTheFlow"
				constraint (last LatestSMDtoATCRel.Status.Completed)
					"WorkUnitStatusShouldBeCompleted"
		LoadDataToStagingIsCompleted
			if(LoadDataToStagingIsCompleted)
				constraint (last DataFlowToAnalyticsFLRel.Status entered)
					"CannotProceedFutherWithoutRunningTheFlow"
				constraint (last DataFlowToAnalyticsFLRel.Status.Completed)
					"WorkunitStatusShouldBeCompleted"
	
	Derived Fields
				
		DerivedDataFlowToAnalyticsFullLoadServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsDataFlowToAnalyticsFullLoadServiceDefRel.Value=blank)
				return "DataFlowToAnalyticsFullLoad"
			else
				return AnalyticsDataFlowToAnalyticsFullLoadServiceDefRel.Value
						
		DerivedSMDtoATCUpgradeServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsSMDtoATCUpgradeServiceDefRel.Value=blank)
				return "AnalyticsUpgradeSMDtoATC"
			else
				return AnalyticsSMDtoATCUpgradeServiceDefRel.Value

		DerivedProductVersionSTCDef is a DerivedField
			type is Alpha size 200
			restricted
			return AnalyticsConfigParaTenantIDRel.Value+"-STC-CSF-Consumer"
			
		DerivedProductVersionSMDDef is a DerivedField
			type is Alpha size 200
			restricted
			return AnalyticsConfigParaTenantIDRel.Value+"-SMD-CSF-Consumer"
			
		DerivedProductVersionATCDef is a DerivedField
			type is Alpha size 200
			restricted
			return AnalyticsConfigParaTenantIDRel.Value+"-CSF-Consumer"

		CancelMessage is a MessageField
            restricted
            "ThisActionWillCancelTheProcess.DoYouWantToContinue?"

		DerivedUpdateIONSchemasServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsUpdateIONSchemasServiceDefRel.Value not entered)
				return "UpdateIONSchemas"
			else
				return AnalyticsUpdateIONSchemasServiceDefRel.Value
 
		DerivedDataFlowToAnalyticsIncrementalLoadServiceDef is a DerivedField
			type is Alpha size 200
			restricted
			if(AnalyticsDataFlowToAnalyticsIncrementalLoadServiceDefRel.Value not entered)
				return "DataFlowToAnalyticsIncrementalLoad"
			else
				return AnalyticsDataFlowToAnalyticsIncrementalLoadServiceDefRel.Value
			
	Conditions
	
		IsUpgradeActiveSpacesIsCompleted
			when (UpgradeActiveSpacesIsCompleted entered)
			
		IsMergeCustomizationIsCompleted
			when (MergeContentCustomization entered and MergeModelCustomization entered)
		
		IsLoadDataToStagingIsCompleted
			when(LoadDataToStagingIsCompleted entered)

		IsUpgradeCompleted
			when (ProductVersionSMDRel.Value>ProductVersionATCRel.Value 
			and ProductVersionATCRel exists 
			and ProductVersionSMDRel exists 
			and ProductVersionSMDRel.Value != "missing"
			and ProductVersionATCRel.Value != "missing" 
			and UpdateNotRequired=false)

		IsDisableButtonText
			when (last AnalyticsUpgradeActiveSpaceRel.Status.InProgress)	

		UpdateNotRequired
			when(ProductVersionATCRel.Value contains "11.0.7")

		IsEnableButton
			when (not last AnalyticsUpgradeActiveSpaceRel.Status.InProgress 
			and not ProductVersionATCRel.UpdateNotRequired
			and not last AnalyticsUpgradeStagingRel.Status.InProgress
			and not last AnalyticsCopyToAnotherTenantRel.Status.InProgress
			and not last PostPfiWorkunitsRel.Status.Processing 
			and not last DataFlowToAnalyticsFLRel.Status.Processing 
			and not last DataFlowToAnalyticsILRel.Status.Processing)

		IsStagingProcessRunning
			when (last AnalyticsUpgradeStagingRel.Status.InProgress)

		IsCopyToAnotherTenantRunning
			when (last AnalyticsCopyToAnotherTenantRel.Status.InProgress)

		IsSMDtoATCWorkunitExists
			when(SMDToATCWorkUnit > 0)

		IsFullLoadWorkunitExists
			when(DataFlowToAnalyticsFLRel exists)
		
	Relations
			
		AnalyticsConfigParaTenantIDRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "TenantID"

		AnalyticsStandUpStagingRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "StandUpStaging"

		ProductVersionSMDRel
			one-to-many relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
			Instance Selection
				where (related.AnalyticsConsoleConfigurations = DerivedProductVersionSMDDef)

		ProductVersionATCRel
			one-to-many relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
			Instance Selection
				where (related.AnalyticsConsoleConfigurations = DerivedProductVersionATCDef)
		
		AnalyticsSMDtoATCUpgradeServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "AnalyticsUpgradeSMDToATCServiceDef"
						
		AnalyticsDataFlowToAnalyticsFullLoadServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "DataFlowToAnalyticsFullLoadServiceDef"
						
		DataFlowToAnalyticsFLRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedDataFlowToAnalyticsFullLoadServiceDef
		
		DataFullLoadPfiTriggerRel 
			one-to-many relation to PfiTrigger
			Field Mapping uses ByServiceName
				related.ServiceName=DerivedDataFlowToAnalyticsFullLoadServiceDef
				
		SMDtoATCUpgradePfiTriggerRel 
			one-to-many relation to PfiTrigger
			Field Mapping uses ByServiceName
				related.ServiceName = DerivedSMDtoATCUpgradeServiceDef
		
		SMDtoATCUpgradeRel
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition=DerivedSMDtoATCUpgradeServiceDef

		AnalyticsUpgradeActiveSpaceRel		
			one-to-many relation to AnalyticsUpgradeActiveSpace
			Field Mapping uses symbolic key

		PfiTaskRel
			one-to-one relation to PfiTask
			Field Mapping uses symbolic key
				related.PfiTask.TaskName="BirstSystemAdmin"
				related.PfiTask.TaskType=PfiTaskType.Group
		
		PfiUserCategoryRel
            one-to-many relation to PfiUserTask
            Field Mapping uses ByTaskNameTaskTypeUserProfile
				related.PfiTask.TaskName="BirstSystemAdmin"
				related.PfiTask.TaskType =PfiTaskType.Group 

		AnalyticsCopyToAnotherTenantRel		
			one-to-many relation to AnalyticsCopyToAnotherTenant
			Field Mapping uses symbolic key
			
		AnalyticsUpgradeStagingRel		
			one-to-many relation to AnalyticsUpgradeStaging
			Field Mapping uses symbolic key

		LatestSMDtoATCRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedSMDtoATCUpgradeServiceDef
				related.PfiWorkunit = SMDToATCWorkUnit

		AnalyticsUpdateIONSchemasServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "UpdateIONSchemasServiceDef"

		AnalyticsDataFlowToAnalyticsIncrementalLoadServiceDefRel
			one-to-one relation to AnalyticsConsoleConfigurations
			Field Mapping uses symbolic key
				related.AnalyticsConsoleConfigurations = "DataFlowToAnalyticsIncrementalLoadServiceDef"

		PostPfiWorkunitsRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedUpdateIONSchemasServiceDef

		DataFlowToAnalyticsILRel 
			one-to-many relation to PfiWorkunit
			Field Mapping uses ByServDefWorkunit
				related.ServiceDefinition= DerivedDataFlowToAnalyticsIncrementalLoadServiceDef
			
	Actions
		
		Create is an Action
		
		Update is an Action
		
		Delete is an Action
		
		UpdateVersion is a Set Action
			no records message is "ProductVersionUpdatedSuccessfully"
			run in foreground
			Instance Selection
				where(false)
			Action Rules
				Empty Set Rules
					invoke UpdateProductVersion AnalyticsConsoleConfigurations
				
		StartSMDToATCUpgrade is a Set Action
			no records message is "Started"
			restricted
			run in foreground
			Instance Selection
				where(false)
			Action Rules
				Empty Set Rules
					constraint(PfiTaskRel exists)
						"Birst_System_Admin_has_not_yet_created._Please_click_the_Create_Birst_System_Admin_task_group_first."
					constraint(PfiUserCategoryRel exists)
						"BirstSystemAdminHasNoUserAdded,PleaseClickThe_Add_UserIn_Birst_System_Admin_GroupAndAssignAtLeastOneUser."
					constraint(SMDtoATCUpgradePfiTriggerRel exists)
						"<DerivedSMDtoATCUpgradeServiceDef>IsNotYetConfigured.PleaseClickThe_Create_Service_Trigger_In_Integration_Architect_Analytics_Console_Dashboard."
					invoke Start SMDtoATCUpgradePfiTriggerRel
					invoke Update last AnalyticsUpgradeActiveSpaceRel
						invoked.SMDToATCWorkUnit=SMDtoATCUpgradePfiTriggerRel.TriggeredWorkunit


		StartDataFlowFullLoad is a Set Action
			no records message is "Started"
			restricted
			run in foreground
			Instance Selection
				where(false)
			Action Rules
				Empty Set Rules
					constraint(PfiTaskRel exists)
						"Birst_System_Admin_has_not_yet_created._Please_click_the_Create_Birst_System_Admin_task_group_first."
					constraint(PfiUserCategoryRel exists)
						"BirstSystemAdminHasNoUserAdded,PleaseClickThe_Add_UserIn_Birst_System_Admin_GroupAndAssignAtLeastOneUser."
					constraint(DataFullLoadPfiTriggerRel exists)
						"<DerivedDataFlowToAnalyticsFullLoadServiceDef>IsNotYetConfigured.PleaseClickThe'Create_Service_Trigger'In_Integration_Architect-Analytics_Console-Dashboard."
					invoke Start DataFullLoadPfiTriggerRel
					invoke Update AnalyticsStandUpStagingRel
						invoked.Value=false
		
		Cancel is an Instance Action
			confirmation required
				"<CancelMessage>"
			completion message is "TheProcessHasBeenCanceledSuccessfully"
			Action Rules
           		invoke Update
           			invoked.Status=Status.Canceled
