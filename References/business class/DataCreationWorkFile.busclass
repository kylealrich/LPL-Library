DataCreationWorkFile is a BusinessClass
	owned by framework
	prefix is ODSW

	Patterns
		disable Auditing
		disable EffectiveDated
		disable AsOfDateProcessing

	Persistent Fields
		ReplicationSet		is like ReplicationSet
		BusinessClass		is like BusinessClass

	Derived Fields
		AddAllFieldsError is a MessageField
			"AddAllPersistentFieldsError:_"

		AddAllFieldsSuccess is a MessageField
			"AddedAllPersistentFields"

	Local Fields	
		LocalReplicationSet		is like ReplicationSet
		LocalBusinessClass		is like BusinessClass
	
	Sets
		ByBusinessClass
			primary
			indexed
			Sort Order
				ReplicationSet
				BusinessClass

	Relations
		LocalRepSetBCRel
			one-to-many relation to RepSetBC
			Field Mapping uses ByBusinessClass
				related.ReplicationSet	= LocalReplicationSet
				related.BusinessClass	= LocalBusinessClass
		
	Actions
		Create is a Create Action
			restricted

		Update is an Update Action
			restricted

		AddAllFieldsForODS is a Set Action
			restricted
			Parameters
				PrmDataCreationResult		is like DataCreationResult
				PrmReplicationSet			is like ReplicationSet

			Local Fields	
				LocalError					is Boolean
				LocalErrorMessage			is Alpha size 250
				LocalFilterString       	is Alpha size 200

			Instance Selection
				where (ReplicationSet = PrmReplicationSet)

			Action Rules
				Set Rules
					Exit Rules
						LocalFilterString = "ReplicationSet=\"" + PrmReplicationSet + "\""
						invoke RunCommand PurgeData in background
							invoked.BusinessClass   = "DataCreationWorkFile"
							invoked.FilterString    = LocalFilterString

				Instance Rules
					initialize LocalError
					initialize LocalErrorMessage

					LocalReplicationSet = ReplicationSet
					LocalBusinessClass = BusinessClass
					if (LocalRepSetBCRel exists) 
						invoke AddFields LocalRepSetBCRel
							resume on error
								LocalError = true
								LocalErrorMessage = error message
							invoked.AddPersistentFields								= true
							invoked.AddPersistentUserFields							= true
							invoked.ExplodeCompoundFields							= true
							invoked.ReplacePeriodsWithUnderscores					= true
							invoked.UseSQLName										= true
							invoked.AddPersistentFieldCubeDimensionValues			= true
							invoked.AddPersistentUserFieldCubeDimensionValues		= true
							invoked.SkipSchemaChangeConfirmation					= true

						if (LocalError)
							invoke Create DataCreationResultDetail
								invoked.DataCreationResult				= PrmDataCreationResult
								invoked.Status							= 2 
								invoked.Message							= AddAllFieldsError + LocalErrorMessage
								invoked.ReplicationSet					= LocalReplicationSet
								invoked.BusinessClass					= LocalBusinessClass
						else
							invoke Create DataCreationResultDetail
								invoked.DataCreationResult				= PrmDataCreationResult
								invoked.Status							= 1 
								invoked.Message							= AddAllFieldsSuccess
								invoked.ReplicationSet					= LocalReplicationSet
								invoked.BusinessClass					= LocalBusinessClass
					
					initialize LocalReplicationSet
					initialize LocalBusinessClass

		Delete is a Delete Action
			restricted

		Purge is a Purge Action
			restricted
