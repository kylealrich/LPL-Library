ReceivablePaymentNoticeDetail is a BusinessClass
	owned by ar
	prefix is NTW
	classic name is NOTICEWRK

	Ontology
		symbolic key is ReceivablePaymentNoticeDetail
			classic set name is NTWSET1

	Patterns
		implements StaticJava
		disable AuditIndex
		implements Archivable

	Persistent Fields

		NoticeVars  is a NoticeVarX4InNoticewrk
			classic name is NOTICE-VAR
		Amounts	 is an AmountX2InNoticewrk
			classic name is AMOUNT
		Date		is Date
		Description
		Currency
			classic name is CURRENCY-CD
		CommentWrks is a CommentWrkX4InNoticewrk
			classic name is COMMENT-WRK
		UpdateDate  is Date
		DatePaid	is Date
		ChrgRate
		RateDays	is Numeric size 4
		
	Local Fields
		LocalDetailFinanceTotal	is an InternationalAmount
		LocalRTMTotal			is an InternationalAmount
		LocalChargebackTotal	is an InternationalAmount

		LocalGeneralLedgerCompanyGroup				is a GeneralLedgerCompanyGroup
		LocalMajClass								is a CustomerMajorClass
		LocalMinClass								is a CustomerMinorClass
		LocalCreditAnalyst							is a CreditAnalyst
		LocalSalesRepresentative			   		is a SalesRepresentative
		LocalActor									is Actor
		
	Derived Fields
		DerivedTransDate is a DerivedField
			type is Date
			return NoticeVars.NoticeVar[2]
			
		DerivedDueDate is a DerivedField
			type is Date
			return NoticeVars.NoticeVar[3]
			
		DerivedDaysPastPaidLate is a DerivedField
			type is Numeric 5
			return NoticeVars.NoticeVar[4]
			
		DerivedFirstComment is a StringField
			type is Alpha 60
			CommentWrks.CommentWrk[1]
			
		DerivedFirstLineLanguageText is a StringField
			type is Alpha 40
			CommentWrks.CommentWrk[1]
		
		DerivedDetailFinanceTotal is a DerivedField
			type is like InternationalAmount
			initialize LocalDetailFinanceTotal
			for each DetailNoticeListRel
				LocalDetailFinanceTotal += each.Amounts.Amount[2]
			return LocalDetailFinanceTotal

		DerivedCBPaymentAmount is a DerivedField
			type is like InternationalAmount
			return DerivedFirstComment[41:53]
			
		DerivedChargebackTransactionText is a StringField
			type is Alpha 180
			NoticeVars.NoticeVar[2]
			" "
			NoticeVars.NoticeVar[3]
			" "
			NoticeVars.NoticeVar[4]
				
		DerivedRTMTotal is a DerivedField
			type is like InternationalAmount
			initialize LocalRTMTotal
			for each ReturnedPaymentRel
				LocalRTMTotal += each.Amounts.Amount[2]
			return LocalRTMTotal

		DerivedCBTotal is a DerivedField
			type is like InternationalAmount
			initialize LocalChargebackTotal
			for each ChargebackRel
				LocalChargebackTotal += each.Amounts.Amount[2]
			return LocalChargebackTotal
			
		DerivedChargebackGLDate is a DerivedField
			type is Date
			return DerivedFirstComment[25:32]
			
		DerivedStartDate is a DerivedField
			type is Date
			return NoticeVars.NoticeVar[3]
			
		DerivedCustBillToName is a DerivedField 
			type is like VendorName 
			holds pii
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.Name
			else
				return ReceivablePaymentNoticeDetail.Customer.Name
			
		DerivedCustBillToAddrLine1 is a DerivedField
			type is Alpha 40
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.DeliveryAddress.AddressLine1
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.DeliveryAddress.AddressLine1
			
		DerivedCustBillToAddrLine2 is a DerivedField
			type is Alpha 40
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.DeliveryAddress.AddressLine2
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.DeliveryAddress.AddressLine2
			
		DerivedCustBillToAddrLine3 is a DerivedField
			type is Alpha 40
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.DeliveryAddress.AddressLine3
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.DeliveryAddress.AddressLine3
			
		DerivedCustBillToAddrLine4 is a DerivedField
			type is Alpha 40
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.DeliveryAddress.AddressLine4
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.DeliveryAddress.AddressLine4
			
		DerivedCustBillToCity is a DerivedField
			type is Alpha 58
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.Municipality
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.Municipality
				
		DerivedCustBillToState is a DerivedField
			type is Alpha 12
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.StateProvince
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.StateProvince
				
		DerivedCustBillToPostalCode is a DerivedField
			type is Alpha 12
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.PostalCode
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.PostalCode
			
		DerivedCustBillToCountry is a DerivedField
			type is Alpha 58
			if (HasDefaultBillTo)
				return first CustomerDefaultBillToRel.PostalAddress.Country
			else
				return ReceivablePaymentNoticeDetail.Customer.PostalAddress.Country
				
		DerivedFinanceChargeLanguageText is a StringField
			type is Alpha 165
			DerivedFirstLineLanguageText
			" "
			CommentWrks.CommentWrk[2]
			" "
			CommentWrks.CommentWrk[3]
			" "
			CommentWrks.CommentWrk[4]
			
		DerivedLanguageText is a StringField
			type is Alpha 185
			CommentWrks.CommentWrk[2]
			" "
			CommentWrks.CommentWrk[3]
			" "
			CommentWrks.CommentWrk[4]
			
		DerivedRTMPaymentNumber is a DerivedField
			type is AlphaUpper 22
			return DerivedFirstComment[10:DerivedRTMPaymentLastCharacter]
			
		DerivedFirstChargebackInvoice is a DerivedField
			type is AlphaUpper 22
			return first ChargebackRel.NoticeVars.NoticeVar[1]
			
		DerivedChargebackPaymentHeader is a DerivedField
			type is Numeric 6
			return first ReceivableApplicationChargebackRel.CreditTransaction.CreditBatch
		
		DerivedChargebackPaymentNumber is a DerivedField
			type is AlphaUpper 22
			return DerivedFirstComment[9:18]
			
		DerivedMemoCBPaymentHeader is a DerivedField
			type is Numeric 6
			return first ReceivableApplicationMemoChargebackRel.CreditTransaction.CreditBatch
	
				
		DerivedMemoCBPaymentNumber is a DerivedField
			type is AlphaUpper 22
			return first ReceivableApplicationMemoChargebackRel.CreditTransaction.CreditNumber
			
		DerivedRTMTransactionOverview is a StringField
			type is AlphaUpper 45
			NoticeVars.NoticeVar[3]
			" "
			NoticeVars.NoticeVar[4]
			
		DerivedCBPaymentDecimals is a DerivedField
			type is like InternationalAmount
			return DerivedFirstComment[54:55] * .01
			
		DerivedChargebackPaymentTotal is a DerivedField
			type is like InternationalAmount
			return DerivedCBPaymentAmount + DerivedCBPaymentDecimals
			
		DerivedFirstCommentSize is a DerivedField
			type is Numeric 2
			return CommentWrks.CommentWrk[1] size 
			
		DerivedRTMPaymentLastCharacter is a DerivedField
			type is Numeric 2
			return (DerivedFirstCommentSize - 29)
			
		DerivedMemoChargebackTotal is a DerivedField
			type is like InternationalAmount
			return first MemoChargebackTransactionDetailsRel.Amounts.Amount[2]
			
				
	Context Fields
		RTMPayment
		
	Sets

		Set2
			indexed
			Sort Order
				Company
				UpdateDate
				ReceivablePaymentNoticeDetail.Customer
				ReceivablePaymentNoticeDetail.NoticeType
				ReceivablePaymentNoticeDetail.NoticeNbr
				ReceivablePaymentNoticeDetail.SeqNbr

		Set3
			indexed
			Sort Order
				Company
				ReceivablePaymentNoticeDetail.Customer
				ReceivablePaymentNoticeDetail.NoticeType
				ReceivablePaymentNoticeDetail.NoticeNbr
				ReceivablePaymentNoticeDetail.SeqNbr
				
	Conditions
		HasDefaultBillTo
			restricted
			when (CustomerDefaultBillToRel exists)
				
	Relations

		DetailNoticeListRel
			one-to-many relation to ReceivablePaymentNoticeDetail
			Field Mapping uses Set3
				related.Company										= Company
				related.ReceivablePaymentNoticeDetail.Customer 		= ReceivablePaymentNoticeDetail.Customer
				related.ReceivablePaymentNoticeDetail.NoticeType 	= "D" 
				related.ReceivablePaymentNoticeDetail.NoticeNbr	 	= ReceivablePaymentNoticeDetail.NoticeNbr

		ReturnedPaymentRel
			one-to-many relation to ReceivablePaymentNoticeDetail
			Field Mapping uses Set3
				related.Company										= Company
				related.ReceivablePaymentNoticeDetail.Customer 		= ReceivablePaymentNoticeDetail.Customer
				related.ReceivablePaymentNoticeDetail.NoticeType 	= "R" 
				related.ReceivablePaymentNoticeDetail.NoticeNbr 	= ReceivablePaymentNoticeDetail.NoticeNbr
			Instance Selection
				where (related.ReceivablePaymentNoticeDetail.SeqNbr > 1
				and	related.NoticeVars.NoticeVar[3] entered)
				
		ReceivableApplicationChargebackRel
			one-to-many relation to ReceivableApplication
			Field Mapping uses SequenceDescending
				related.Company		= Company
			Instance Selection
				where (related.ReceivableApplication.Invoice		= DerivedFirstChargebackInvoice
				and	related.CreditTransaction.CreditCompany		= Company
				and	related.CreditTransaction.CreditCustomer		= ReceivablePaymentNoticeDetail.Customer)
				
		ChargebackRel
			one-to-many relation to ReceivablePaymentNoticeDetail
			Field Mapping uses Set3
				related.Company										= Company
				related.ReceivablePaymentNoticeDetail.Customer 		= ReceivablePaymentNoticeDetail.Customer
				related.ReceivablePaymentNoticeDetail.NoticeType 	= "C" 
				related.ReceivablePaymentNoticeDetail.NoticeNbr 	= ReceivablePaymentNoticeDetail.NoticeNbr
			Instance Selection
				where (related.ReceivablePaymentNoticeDetail.SeqNbr > 1)
				
		ReceivableApplicationMemoChargebackRel
			one-to-many relation to ReceivableApplication
			Field Mapping uses Set5
				related.Company		= Company
			Instance Selection
				where (related.ReceivableApplication.Invoice		= ReceivablePaymentNoticeDetail.NoticeNbr
				and	related.CreditTransaction.CreditCompany		= Company
				and	related.CreditTransaction.CreditCustomer		= ReceivablePaymentNoticeDetail.Customer)
				
		ReceivablePaymentNoticeDetailMemoCBFirstSeqRel
			one-to-many relation to ReceivablePaymentNoticeDetail
			Field Mapping uses Set3
				related.Company										= Company
				related.ReceivablePaymentNoticeDetail.Customer 		= ReceivablePaymentNoticeDetail.Customer
				related.ReceivablePaymentNoticeDetail.NoticeType 	= "M" 
				related.ReceivablePaymentNoticeDetail.NoticeNbr 	= ReceivablePaymentNoticeDetail.NoticeNbr
				related.ReceivablePaymentNoticeDetail.SeqNbr 		= 1
				
		ReceivablePaymentHeaderRel
			one-to-one relation to ReceivablePaymentHeader
			Field Mapping uses Set6
				related.Company								= Company
				related.ReceivablePaymentHeader				= DerivedChargebackPaymentHeader
				
	   	CustomerDefaultBillToRel
			one-to-many relation to CustomerBillTo
			Field Mapping uses symbolic key
				related.CustomerGroup = ReceivablePaymentNoticeDetail.Customer.CustomerGroup
				related.Customer	  = ReceivablePaymentNoticeDetail.Customer
			Instance Selection
				where (related.DefaultBillTo)
				
		ReceivablePaymentChargebackRel
			one-to-many relation to ReceivablePayment
			Field Mapping uses Set7
				related.PaymentNumber = DerivedChargebackPaymentNumber
				related.Customer = ReceivablePaymentNoticeDetail.Customer
				related.Company = Company
			Instance Selection
				where (related.ReceivablePaymentHeader = DerivedChargebackPaymentHeader)
				
		ReceivablePaymentMemoCBRel
			one-to-many relation to ReceivablePayment
			Field Mapping uses Set7
				related.PaymentNumber = DerivedMemoCBPaymentNumber
				related.Customer = ReceivablePaymentNoticeDetail.Customer
				related.Company = Company
			Instance Selection
				where (related.ReceivablePaymentHeader = DerivedMemoCBPaymentHeader)

		ReceivableInvoiceDetailRel
			one-to-many relation to ReceivableInvoice
			Field Mapping uses Set4
				related.Company					= Company
				related.Customer				= ReceivablePaymentNoticeDetail.Customer
			Instance Selection
				where (related.ReceivableInvoice = ReceivablePaymentNoticeDetail.NoticeNbr)
				
		ReceivablePaymentHeaderMemoCBRel
			one-to-one relation to ReceivablePaymentHeader
			Field Mapping uses Set6
				related.Company								= Company
				related.ReceivablePaymentHeader				= DerivedMemoCBPaymentHeader
				
		MemoChargebackTransactionDetailsRel
			one-to-many relation to ReceivablePaymentNoticeDetail
			Field Mapping uses Set3
				related.Company										= Company
				related.ReceivablePaymentNoticeDetail.Customer 		= ReceivablePaymentNoticeDetail.Customer
				related.ReceivablePaymentNoticeDetail.NoticeType 	= "M" 
				related.ReceivablePaymentNoticeDetail.NoticeNbr 	= ReceivablePaymentNoticeDetail.NoticeNbr
				related.ReceivablePaymentNoticeDetail.SeqNbr 		= 2



		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		GeneralLedgerCompanyGroupRel
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= LocalGeneralLedgerCompanyGroup
				related.Company						= Company
		
		CustomerMajorClassRel
			one-to-one relation to CustomerMajorClass
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	 = GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.CustomerMajorClass 		 = LocalMajClass
				
		CustomerMinorClassRel
			one-to-one relation to CustomerMinorClass
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup	 = GeneralLedgerCompanyRel.FinanceEnterpriseGroup
				related.CustomerMajorClass 		 = LocalMajClass 
				related.CustomerMinorClass 		 = LocalMinClass
	
		ReceivableCreditAnalystRel
			one-to-many relation to CreditAnalyst
			Field Mapping uses symbolic key
				related.Company	 		= Company
				related.CreditAnalyst	= LocalCreditAnalyst
		
		ReceivableSalesRepresentativeRel
			one-to-many relation to SalesRepresentative
			Field Mapping uses symbolic key
				related.Company	 = Company
				related.SalesRepresentative = LocalSalesRepresentative
	
		CompanyCustomerRel
			one-to-one relation to CompanyCustomer
			Field Mapping uses symbolic key
				related.Company					= Company
				related.Customer				= ReceivablePaymentNoticeDetail.Customer
		
		GeneralLedgerCompanyGroupMembersRel
            one-to-many relation to GeneralLedgerCompanyGroupMember
            Field Mapping uses Set2
                related.GeneralLedgerCompanyGroup = LocalGeneralLedgerCompanyGroup
		
	Actions	
		Create	is a Create Action
		
		Update is an Update Action
		
		Delete	is a Delete Action
				
		Purge is a Purge Action
  			restricted
		
		ReceivablePaymentNoticeDetailRecordsPurge is a Set Action
			restricted
			Parameters
				PrmCompany 					is a ReceivableCompany
				PrmCompanyGroup				is a GeneralLedgerCompanyGroup
				PrmNoticeDate			 	is Date
				PrmCustomer 				is a Customer
				PrmMajClass				 is a CustomerMajorClass
				PrmMinClass		 		is a CustomerMinorClass
				PrmCreditAnlyst				is a CreditAnalyst
				PrmSalesman			  	is a SalesRepresentative
			
			Parameter Rules
				
				PrmCompany
					if (PrmCompanyGroup entered)
						cannot be entered
							"CannotEnterCompanyIfCompanyGroupEntered"
					
					constraint (PrmCompany.FinanceEnterpriseGroup.PurgeCutOffDate entered)
		            	"PurgeCutOffDateMustBeSetFor_Finance_Enterprise_Group<PrmCompany.FinanceEnterpriseGroup>"
							
				PrmCompanyGroup
					if (PrmCompany entered)
						cannot be entered
							"CannotEnterCompanyGroupIfCompanyEntered"
					
					if (PrmCompany not entered and PrmCompanyGroup not entered)
						required
							"CompanyOrCompanyGroupIsRequired"
					
					LocalGeneralLedgerCompanyGroup = PrmCompanyGroup

					constraint (GeneralLedgerCompanyGroupMembersRel exists)
						"CompanyGroup<PrmCompanyGroup>ContainsNoCompanies"
					
					constraint (first GeneralLedgerCompanyGroupMembersRel.Company.FinanceEnterpriseGroup.PurgeCutOffDate entered)
						"PurgeCutOffDateMustBeSetFor_Finance_Enterprise_Group<first GeneralLedgerCompanyGroupMembersRel.Company.FinanceEnterpriseGroup>"
			
				PrmCustomer
					if (PrmCustomer entered)
						constraint (PrmMajClass not entered or PrmCreditAnlyst not entered or PrmSalesman not entered)
							"CustomerEntered-DoNotEnterAnalyst,MajClassOrSalesrep"
				
				PrmMajClass
					if (PrmMinClass entered)
						required
							"MajorClassRequired;MinorClassEntered"
					LocalMajClass = PrmMajClass
					if (PrmMajClass entered) 
						constraint (CustomerMajorClassRel exists)
							"MajorClass<PrmMajClass>DoesNotExist"
								
				PrmMinClass
					if (PrmMinClass entered)
						LocalMinClass = PrmMinClass
						constraint (CustomerMajorClassRel exists)
							"MajorClass<PrmMajClass>DoesNotExist"
								
				PrmCreditAnlyst
					if (PrmCreditAnlyst entered)
						LocalCreditAnalyst = PrmCreditAnlyst
						constraint (ReceivableCreditAnalystRel exists)
							"CreditAnalystDoesNotExist"
							
				PrmSalesman 
					if (PrmSalesman entered)
						LocalSalesRepresentative = PrmSalesman
						constraint (ReceivableSalesRepresentativeRel exists)
							"SalesRepDoesNotExist"
				
				PrmNoticeDate

					initial value is actor.context.FinanceEnterpriseGroup.PurgeCutOffDate
					default to actor.context.FinanceEnterpriseGroup.PurgeCutOffDate
					
			Local Fields
				RecordCount			  		 is Numeric 10
			
			Instance Selection 
				include deleted records
				where ((PrmCompany not entered or Company = PrmCompany)
				and   (PrmCompanyGroup not entered or GeneralLedgerCompanyGroupRel exists)
				and   (PrmCustomer not entered or ReceivablePaymentNoticeDetail.Customer = PrmCustomer) 
				and	UpdateDate <= PrmNoticeDate)  
				


			
			Action Rules
				
				Empty Set Rules
					LocalActor = actor
					send notification
						to LocalActor
						description is "NoReceivablePaymentNoticeDetailRecordsFoundToPurge"
						priority is high
						detail is "ReceivablePaymentNoticeDetailPurged=<RecordCount>"
				Set Rules
					Exit Rules
						LocalActor = actor
						send notification
							to LocalActor
							description is "ReceivablePaymentNoticeDetailPurgeHasCompleted"
							priority is high
							detail is "ReceivablePaymentNoticeDetailPurged=<RecordCount>"
				
				Instance Rules
					if (PrmCreditAnlyst entered
					or  PrmMajClass entered
					or  PrmSalesman entered)
						if (PrmCreditAnlyst = CompanyCustomerRel.CreditAnalyst
						or  PrmMajClass = CompanyCustomerRel.CustomerMajorClass
						or  PrmSalesman = CompanyCustomerRel.SalesRepresentative)
							increment RecordCount
							invoke Purge
					else
						increment RecordCount 
						invoke Purge 
	
