GainLossResult is a BusinessClass
	owned by GeneralLedger
	
	prefix is GLGLR
	
	Ontology
		symbolic key is GainLossResult
		
	Patterns

 		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		RunTime					is TimeStamp
		Ledger
		AccountingEntity
		AccountingEntityGroup	
		Account					is a GeneralLedgerChartAccount
		AccountGroup			is a GeneralLedgerTotal group	
		DateRange
		AutoReverse				is Boolean
		Functional				is Boolean
		Alternate				is Boolean
		Alternate2				is Boolean
		Alternate3				is Boolean
		Project					is Boolean
		Report1					is Boolean
		Report2					is Boolean
		Report3					is Boolean
		Report4					is Boolean
		Report5					is Boolean
		PeriodOption			is Numeric 1
			States
				CurrentPeriod			value is 1
				PreviousPeriod			value is 2
				NextPeriod				value is 3
		RunOption				is Numeric 1
			States
				Revalue					value is 1
				Translation				value is 2
		RateOption				is Numeric 1
			States
				DailyRate				value is 0
				PeriodRate				value is 1
		PostOption				is Numeric 1
			States
				ZoneOrSystem			value is 0
				OriginatingDimension	value is 1
		BySystemCode			is Boolean
		Released				is Boolean
		ChangesOnly				is Boolean
		SaveCalculation			is Boolean
		ChartSection			is Numeric 1
			States
				BalanceSheet    	value is 1
				IncomeStatement 	value is 2
		OtherDimensions			is Boolean		

	Field Rules
	
	Conditions
		DetailsExists
			restricted
			when (GainLossResultDetail set exists)
			
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))

	Relations
		JournalsCreatedRel
			one-to-many relation to GeneralLedgerJournalControl
			Field Mapping uses ByGainLossRun
				related.FinanceEnterpriseGroup 	= FinanceEnterpriseGroup
				related.GainLossReportingBasis	= ReportingBasis
				related.GainLossRunSequence		= GainLossResult
				
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup   = actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity        = AccountingEntity
				
	Sets
		ByDescendingGainLossResult

			Sort Order
				FinanceEnterpriseGroup
				RunTime descending
				ReportingBasis
				GainLossResult
				 

	Actions
		Create is an Action
			restricted
		Update is an Action
			restricted
		Delete is an Action
		Purge is a Purge Action

		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis			is like ReportingBasis
					default label is "ReportingBasis"
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and	   ReportingBasis				= PrmReportingBasis)
			Set Is
				PrmFinanceEnterpriseGroup
				PrmReportingBasis
			Action Rules
				Empty Set Rules
				Instance Rules
					invoke Delete 
