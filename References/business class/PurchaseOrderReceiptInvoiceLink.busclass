PurchaseOrderReceiptInvoiceLink is a BusinessClass
    owned by po
    prefix is PMJ
    sql name is POrderReceiptInvoiceLink
    classic name is POMATCHOBJ

    Ontology
        symbolic key is PurchaseOrderReceiptInvoiceLink
            classic set name is PMJSET1
            classic name for PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt is REC-NUMBER
            classic name for PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine is LINE-NBR
            classic name for PurchaseOrderReceiptInvoiceLink.MatchObjectID is MATCH-OBJ-ID
            classic name for PurchaseOrderReceiptInvoiceLink.MatchSequence is MATCH-SEQ-NBR
            classic name for PurchaseOrderReceiptInvoiceLink.AddOnCharge is AOC-CODE

    Patterns
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        Vendor
        PayablesInvoice
        PurchaseOrder
        PurchaseOrderLine					
            classic name is PO-LINE-NBR
        Contract
        ContractLine
        Item
        MatchDetailKey
            classic name is MATCH-DTL-KEY
        MatchedQuantity               is like Quantity
            classic name is MATCHED-QTY
        MatchUnitCost                 is like InternationalCost
            classic name is MATCH-UNIT-CST
        Type                          is AlphaUpper size 2
        TransactionDate               is TimeStamp
            classic name is TRAN-DATE
        PoCostUpd                     is like InternationalCost
        ChargebackQuantity            is like Quantity
            classic name is CHARGEBACK-QTY
        WriteOffQuantity              is like Quantity
            classic name is WRITE-OFF-QTY
        ArchivedQuantity              is like Quantity
            classic name is ARCHIVE-QTY
        MemoQuantity                  is like Quantity
            classic name is MEMO-QTY
        MNRQuantityClearingReceipt    is a PurchaseOrderReceipt
            classic name is MNR-REC-NBR
        MatchedNotReceivedQuantity    is like Quantity
            classic name is MNR-QTY
        MatchedNotReceivedUnitCost    is like InternationalCost
            classic name is MNR-UCST
        MatchedNotReceivedTaxUnitCost is like InternationalCost
            classic name is MNR-TAX-UCST
        MatchedNotReceivedAmount      is like InternationalAmount
            classic name is MNR-AMT
		CatchWeightMatchQuantity	  is like Quantity
		CatchWeightChargebackQuantity is like Quantity
		CatchWeightArchivedQuantity	  is like Quantity
		CatchWeightMemoQuantity		  is like Quantity
		CatchWeightWriteOffQuantity	  is like Quantity
		CatchWeightMatchedNotReceivedQuantity		  is like Quantity
        MnrTolQty                     is like Quantity
        MnrTolUcst                    is like InternationalCost
        MnrTolTaxUc                   is like InternationalCost
        MnrTolAmt                     is like InternationalAmount
        IcvQty                        is like Quantity
        IcvUcst                       is like InternationalCost
        IcvTaxUcst                    is like InternationalCost
        IcvAmt                        is like InternationalAmount
        IcvTolQty                     is like Quantity
        IcvTolUcst                    is like InternationalCost
        IcvTolTaxUc                   is like InternationalCost
        IcvTolAmt                     is like InternationalAmount
        Status                        is Numeric size 1
            States
                Unreleased value is 0
                Released   value is 1
                Processed  value is 2
                Cancelled  value is 9
                All        value is blank
        Operator


        ReceivedDate                  is Date
            classic name is REC-DATE
        ReceiptCurrencyConversionRate is an EnteredCurrencyConversionRate
            classic name is REC-CNV-RATE

    Context Fields
		PurchaseOrderRange
		TransactionDateRange				is a DateRange
		GeneralLedgerCompanyGroup

	Local Fields
		LocalPurchaseOrder					is like PurchaseOrder
		LocalPurchaseOrderLine				is like PurchaseOrderLine
		WorkCost							is like InternationalCost
		WorkQuantity						is like Quantity
		LocalQuantity						is like Quantity
		LocalMatchAmount					is like InternationalAmount
		Counter								is Numeric 5
	
	Derived Fields
		MatchAmount is a DerivedField
			type is like InternationalAmount
			restricted
			return MatchedQuantity * MatchUnitCost

		DerivedQuantityRoundTo				is a DerivedField
    		type is Decimal 5.4
    		restricted
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 2)
    			return .01
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 1)
    			return .1
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 0)
    			return 1
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 3)
    			return .001
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 4)
    			return .0001

		DerivedCostRoundTo				is a DerivedField
    		type is Decimal 5.4
    		restricted
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 2)
    			return .01
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 1)
    			return .1
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 0)
    			return 1
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 3)
    			return .001
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 4)
    			return .0001
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 5)
    			return .00001
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 6)
    			return .000001
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 7)
    			return .0000001
    		else
    		if (Company.InventoryCompanyRel.NumberOfDecimalsQuantity = 8)
    			return .00000001
		
		DerivedItem						is a DerivedField
			type is like Item
			return PurchaseOrderLine.Item
		
		DerivedReceivedDate				is a DerivedField
			type is Date
			return PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt.ReceivedDate
		
		DerivedVendor					is a DerivedField
			type is like Vendor
			return PurchaseOrder.Vendor
		
		DerivedVendorName				is a DerivedField 
			type is like VendorName
			holds pii
			return PurchaseOrder.Vendor.VendorName
	
    Conditions

        MARecordType
            classic name is MA-REC-TYPE1
            restricted
            when (Type = "MA")

        HasMatchObjectID
            classic name is NOT-ZERO-MOBJI
            restricted
            when (PurchaseOrderReceiptInvoiceLink.MatchObjectID entered)

        NoMatchObjectID
            classic name is ZERO-MOBJID
            restricted
            when (PurchaseOrderReceiptInvoiceLink.MatchObjectID not entered)

        NoPurchaseorderReceipt
            classic name is ZERO-REC-NBR
            restricted
            when (PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt not entered)

		WithinTransactionDateRange
			restricted
			when (TransactionDateRange not entered
			or	  TransactionDate within TransactionDateRange)
									
		WithinPurchaseOrderRange
			restricted
			when (PurchaseOrderRange not entered
			or	  PurchaseOrder within PurchaseOrderRange)
									
        WithinCompanyGroupFilter
            restricted
            when (GeneralLedgerCompanyGroup not entered
            or    GLCompanyGroupMemberRel exists)
        
        GLCompanyGroupMemberRelExists
            restricted
            when (GLCompanyGroupMemberRel exists)

		ReceiptRecordType
            restricted
            when (Type = "RC")

		PurchaseOrderExists
			restricted
			when (PurchaseOrderRel exists)

		PurchaseOrderLineExists
			restricted
			when (PurchaseOrderLineRel exists)

    Relations
        PayablesInvoiceDetailRel
            one-to-many relation to PayablesInvoiceDetail
            Field Mapping uses ByCompanyVendorPayablesInvoice
                related.Company     		= Company
                related.Vendor      		= Vendor
                related.PayablesInvoice 	= PayablesInvoice

		MatchCompanyRel
			one-to-one relation to MatchCompany
			Field Mapping uses symbolic key
                related.Company     		= Company

		PurchaseOrderReceiptLineAOCRel
			one-to-one relation to PurchaseOrderReceiptLineAOC
			Field Mapping uses symbolic key
				related.Company						= Company
				related.PurchaseOrderReceipt		= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
				related.PurchaseOrderReceiptLine	= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine
				related.AddOnCharge					= PurchaseOrderReceiptInvoiceLink.AddOnCharge

        GLCompanyGroupMemberRel
            one-to-one relation to GeneralLedgerCompanyGroupMember
            Field Mapping uses symbolic key
                related.GeneralLedgerCompanyGroup    = GeneralLedgerCompanyGroup
                related.Company                        = Company

        LocalInvoiceAddOnChargeForPOLineRel
            one-to-many relation to PayablesInvoiceAddOnCharge
            Field Mapping uses symbolic key
                related.Company 			= Company
                related.PayablesInvoice		= PayablesInvoice
                related.PurchaseOrder		= LocalPurchaseOrder
                related.PurchaseOrderLine	= LocalPurchaseOrderLine

        PurchaseOrderRel
            one-to-one relation to PurchaseOrder
            Field Mapping uses symbolic key
                related.Company          = Company
                related.PurchaseOrder    = PurchaseOrder

		PurchaseOrderLineRel
			one-to-one relation to PurchaseOrderLine
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PurchaseOrder		= PurchaseOrder
				related.PurchaseOrderLine 	= PurchaseOrderLine

		POAndLineAOCRel
			one-to-one relation to PurchaseOrderAndLineAddOnCharge
			Field Mapping uses symbolic key
				related.Company				= Company
				related.PurchaseOrder		= PurchaseOrder
				related.PurchaseOrderLine	= PurchaseOrderLine
				related.AddOnCharge			= PurchaseOrderReceiptInvoiceLink.AddOnCharge
		
    Sets
        Set2
            indexed
            Sort Order
                Company
                PurchaseOrderReceiptInvoiceLink.MatchObjectID
                PurchaseOrderReceiptInvoiceLink.MatchSequence
                PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
                PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine
                PurchaseOrderReceiptInvoiceLink.AddOnCharge

        Set3
            indexed
            Instance Selection
                where (NoPurchaseorderReceipt)
            Sort Order
                Company
                PurchaseOrder
                PurchaseOrderLine
                TransactionDate descending
                PurchaseOrderReceiptInvoiceLink.MatchObjectID
                PurchaseOrderReceiptInvoiceLink.MatchSequence
                Type

        Set4
            indexed
            Instance Selection
                where (MARecordType)
            Sort Order
                Company
                PurchaseOrderReceiptInvoiceLink.MatchObjectID
                PurchaseOrderReceiptInvoiceLink.MatchSequence

                PayablesInvoice
                PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
                PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine
                PurchaseOrderReceiptInvoiceLink.AddOnCharge

        Set5
            indexed
            Sort Order
                Company
                PurchaseOrder
                PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine
                PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
                PurchaseOrderReceiptInvoiceLink.MatchObjectID
                PurchaseOrderReceiptInvoiceLink.MatchSequence
                Type
                Status
                PurchaseOrderReceiptInvoiceLink.AddOnCharge
                
		ByMatchObjectID
			indexed
			duplicates
            Sort Order
				Company
				PurchaseOrderReceiptInvoiceLink.MatchObjectID
				PurchaseOrderReceiptInvoiceLink.MatchSequence descending
  				
	Field Rules
		TransactionDate
			default to current corporate date
			
		Operator
			default to actor
			
	Rule Blocks
		DecimalCheck
			if (MatchedQuantity decimals > Company.InventoryCompanyRel.NumberOfDecimalsQuantity)
	    		round MatchedQuantity to nearest DerivedQuantityRoundTo
			if (MatchUnitCost decimals > Company.InventoryCompanyRel.NumberOfDecimalsCost)
	    		round MatchUnitCost to nearest DerivedCostRoundTo
		
	Create Rules
		if  (Type = "MA")
			if  (MatchedNotReceivedQuantity entered)
				Status									= 1
			else
				Status									= 2
			
	Actions
		Create is a Create Action
			restricted
			Entrance Rules
				include DecimalCheck

		CreateForPOCostMessage is a Create Action
			restricted
			Entrance Rules
				invoke GetNextMatchObjectID MatchCompanyRel
				include DecimalCheck
			Action Rules
				initialize PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
				PurchaseOrderReceiptInvoiceLink.MatchObjectID	= MatchCompanyRel.LastMatchObjectID
				initialize PurchaseOrderReceiptInvoiceLink.MatchSequence
				initialize PurchaseOrderReceiptInvoiceLink.AddOnCharge
							
		Update is an Update Action
		
		FastUpdate is an Update Action
			restricted
			bypass field rules
		
		Delete is a Delete Action
			restricted
			
		Purge is a Purge Action
			restricted
			
		Process is an Instance Action
			restricted
			Action Rules
				Status = Status.Processed

		UnmatchPhase2ReceiptLink is a Set Action
			restricted
			Parameters
				PrmPayablesCompany			is a PayablesCompany
				PrmPayablesInvoice			is a PayablesInvoice
				UnmatchFrom							is Alpha 4
					States
						MA70 value is "MA70"
						MA75 value is "MA75"
				PrmMatchObjectID			is an ObjId
			Parameter Rules
				PrmPayablesCompany
					required
				PrmPayablesInvoice
					required

			Instance Selection
				where (Company												= PrmPayablesCompany
				and    PurchaseOrderReceiptInvoiceLink.MatchObjectID		= PrmMatchObjectID)

			Action Rules
				Empty Set Rules
					invoke UnmatchPhase3InvoiceDetail PayablesInvoiceDetail
						invoked.PrmPayablesCompany		= PrmPayablesCompany
						invoked.PrmPayablesInvoice		= PrmPayablesInvoice
						invoked.UnmatchFrom 			= UnmatchFrom

				Set Rules
					Exit Rules
						invoke UnmatchPhase3InvoiceDetail PayablesInvoiceDetail
							invoked.PrmPayablesCompany		= PrmPayablesCompany
							invoked.PrmPayablesInvoice		= PrmPayablesInvoice
							invoked.UnmatchFrom 			= UnmatchFrom
						
				Instance Rules


					if  (PurchaseOrderReceiptInvoiceLink.MatchObjectID entered)
						if  (PayablesInvoice = PrmPayablesInvoice
						and  PurchaseOrderReceiptInvoiceLink.AddOnCharge not entered
						and !Status.Cancelled
						and  Type = "MA")
							if  (PurchaseOrderLine entered
							and  PurchaseOrderLine.ItemType.Service)
								invoke Delete 
							else
								if  (PurchaseOrderLine entered
								and (!PurchaseOrderLine.CostOption.NoCharge
								or    PurchaseOrderLine.AllowZeroCostInvoice))			
	
									invoke UpdateFromMatch PurchaseOrderLine											
										invoked.PurchaseOrderLineUpdateFromUnMatchProcess = true
										if (!this instance.PurchaseOrderLine.ItemType.Service)
											if (!PrmPayablesInvoice.CreditOrDebitMemo
											or   PrmPayablesInvoice.CreditForRebill)
												if (PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt entered)
													invoked.LocalUpdateFromMatchMatchedQuantity						= ((MatchedQuantity + MatchedNotReceivedQuantity + ChargebackQuantity + WriteOffQuantity) * -1)
												else
													invoked.LocalUpdateFromMatchMatchedQuantity						= MatchedQuantity * -1
											
											invoked.PurchaseOrderLineUpdateFromMatchMatchedNotReceivedQuantity		= MatchedNotReceivedQuantity * -1
											invoked.LocalUpdateFromMatchChargebackQuantity							= ChargebackQuantity * -1
											invoked.LocalUpdateFromMatchWriteOffQuantity							= WriteOffQuantity * -1
											invoked.LocalUpdateFromMatchMemoQuantity								= MemoQuantity * -1
											if ((this instance.PurchaseOrderLine.MatchedNotReceivedQuantity - MatchedNotReceivedQuantity) < 0)
												invoked.PurchaseOrderLineUpdateFromMatchMatchedNotReceivedQuantity		= MatchedNotReceivedQuantity * -1
			
										if (PrmPayablesInvoice.InvoiceType.CreditMemo
										and !PrmPayablesInvoice.CreditForRebill
										and PrmPayablesInvoice.Suffix not entered)
											initialize invoked.LocalUpdateFromMatchMatchedQuantity

									if  (UnmatchFrom.MA75
									and (PrmPayablesInvoice.GeneralLedgerSystemCodeRel.EncumbranceOption.Track
									or   PrmPayablesInvoice.GeneralLedgerSystemCodeRel.EncumbranceOption.TrackAndEdit))
										for each PurchaseOrderLine.CommittedPurchaseOrderLineDistributionsRel
											invoke InvoiceCancel each


								if (PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine not entered)
									invoke Delete 
								else

									invoke UpdateFromMatch PurchaseOrderReceiptInvoiceLink.PurchaseOrder  
										invoked.PrmMatchObjID					= PurchaseOrderReceiptInvoiceLink.MatchObjectID
										invoked.PrmVendorBuyMatchedQuantity		= MatchedQuantity
										invoked.PrmPOLineVendorBuyUnitCost		= PurchaseOrderLine.VendorBuyUnitCost


			
									if ((PurchaseOrderLine.ItemType.Inventoried
									or  PurchaseOrderLine.ItemType.NonStock)
									and PrmPayablesInvoice.MatchLevel.DetailMatch)
	
										LocalPurchaseOrder						= PurchaseOrder
										LocalPurchaseOrderLine					= PurchaseOrderLine



										initialize Counter
										for each PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.InventoryTransactionLineCostAdjustmentDescendingRel
											Counter += 1
											if (each.UnitCost < 0
											or  Counter > 2)
											    end for each
											else

		
												WorkCost = each.UnitCost * -1
												WorkQuantity = each.Quantity

												if  (WorkCost entered
												or   WorkQuantity entered)
													invoke UpdateForCostAdjustment each.ItemLocation
														invoked.PrmCostDifference			= WorkCost
														invoked.PrmQuantity					= WorkQuantity
														invoked.PrmDocumentNumber			= each.InventoryCostHistoryForCostAdjustmentRel.InventoryTransaction
														invoked.PrmLineNumber				= each.InventoryCostHistoryForCostAdjustmentRel.InventoryCostHistory.LineNumber
														if (LocalInvoiceAddOnChargeForPOLineRel exists)
															invoked.PrmAddOnChargeDifference	= true
														if (PrmPayablesInvoice.ProcessLevel.CostVarianceAccount entered)
															invoked.PrmOffsetAccount			= PrmPayablesInvoice.ProcessLevel.CostVarianceAccount
														else
															invoked.PrmOffsetAccount			= PrmPayablesInvoice.MatchCompanyRel.CostVarianceAccount
									
									if (!PrmPayablesInvoice.CreditOrDebitMemo)

										if (PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.CatchWeightQuantity entered)
											LocalQuantity		= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.MatchedQuantity  
										else 
											LocalQuantity		= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.MatchedQuantity

										invoke UpdateFromMatch PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine
											invoked.OpenToMatchQuantity				+= LocalQuantity
											
											if  (LocalQuantity < MatchedQuantity)
												invoked.MatchedQuantity			    -= LocalQuantity
											else
												initialize invoked.MatchedQuantity
												
											initialize invoked.MatchObjectID
											initialize invoked.MatchSequence
											initialize invoked.ArchivedWriteOffQuantity
											initialize invoked.PayablesInvoice

	

										if  (PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.VendorPriceUOMQuantity entered)
											LocalQuantity			= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.VendorPriceUOMQuantity
											LocalMatchAmount 		= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.OriginalUnitCost * PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.VendorPriceUOMQuantity
										else
											LocalQuantity			= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.EnteredReceivedQuantity
											LocalMatchAmount 		= PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.OriginalUnitCost * PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine.EnteredReceivedQuantity
		

										invoke UpdateFromMatch PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
											if ((invoked.MatchedAmount - LocalMatchAmount) < 0)
												initialize invoked.MatchedAmount
											else
												invoked.MatchedAmount -= LocalMatchAmount
											if ((invoked.HashQuantity - LocalQuantity) < 0)
												initialize invoked.HashQuantity
											else
												invoked.HashQuantity -= LocalQuantity	
											invoked.Status	= 1

									else
									if (PrmPayablesInvoice.CreditForRebill)

										LocalQuantity = MatchedQuantity * -1							
										invoke UpdateFromMatch PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceiptLine
											invoked.MatchObjectID												= PrmPayablesInvoice.MatchInvoiceReferenceRel.OriginalInvoice.MatchObjectID
											invoked.MatchSequence												= PrmPayablesInvoice.MatchInvoiceReferenceRel.OriginalInvoice.first PurchaseOrderReceiptLineRel.MatchSequence
											invoked.ArchivedQuantity											-= ArchivedQuantity   
											invoked.MatchedQuantity												-= MatchedQuantity 
											invoked.OpenToMatchQuantity											+= MatchedQuantity 
											if (invoked.OpenToMatchQuantity < 0)
												initialize invoked.OpenToMatchQuantity

										invoke UpdateFromMatch PurchaseOrderReceiptInvoiceLink.PurchaseOrderReceipt
											invoked.MatchedAmount -= (MatchedQuantity * MatchUnitCost)
											invoked.HashQuantity  -= LocalQuantity	

	
									if (PurchaseOrderReceiptInvoiceLink.AddOnCharge not entered)
										invoke Delete 
								
