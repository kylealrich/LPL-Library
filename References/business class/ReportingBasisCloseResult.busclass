ReportingBasisCloseResult is a BusinessClass
	owned by GeneralLedger
	
	prefix is RBCR
	
	Ontology
		symbolic key is ReportingBasisCloseResult
		
	Patterns

 		disable EffectiveDated
		disable AsOfDateProcessing
	
	Persistent Fields
		AccountingEntity
		AccountingEntityGroup
		RunTime					is TimeStamp
		CloseLedger				is a Ledger
		DateRange
		NewYear					is a GeneralLedgerCalendarPeriod
		NewYearDate				is Date
		CloseOption				is Numeric 1
			States
				CloseYear			value is 1
				NextYearBalance		value is 2
		ChangedTotalsOnly		is Boolean

	Field Rules

	Transient Fields
		TransientSkipAttachRule		is a SkipAttachRule
	
	Conditions
		DetailsExists
			restricted
			when (ReportingBasisCloseDetail set exists)
			
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))

	Relations
		ReportingBasisYearTotalRel
			one-to-many relation to ReportingBasisYearTotal
			Field Mapping uses ByBasisYearSequence
				related.FinanceEnterpriseGroup 					= FinanceEnterpriseGroup
				related.ReportingBasis							= ReportingBasis
				related.ReportingBasisYearTotal.EntityYear		= NewYear
				related.ReportingBasisYearTotal.SequenceNumber	= ReportingBasisCloseResult
		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup	= actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity		= AccountingEntity

	Sets
		ByDescendingBasisCloseResult
			Sort Order
				FinanceEnterpriseGroup
				RunTime descending
				ReportingBasis
				ReportingBasisCloseResult
				 

	Actions
		Create is an Action
			restricted
		Update is an Action
			restricted
		Delete is an Action
		Purge is a Purge Action

		DeleteData is a Set Action
			restricted
			Parameters
				PrmFinanceEnterpriseGroup	is like FinanceEnterpriseGroup
					default label is "FinanceEnterpriseGroup"
				PrmReportingBasis			is like ReportingBasis
					default label is "ReportingBasis"
			Instance Selection
				where (FinanceEnterpriseGroup		= PrmFinanceEnterpriseGroup
				and	   ReportingBasis				= PrmReportingBasis)
			Set Is
				PrmFinanceEnterpriseGroup
				PrmReportingBasis
			Action Rules
				Empty Set Rules
				Instance Rules
					invoke Delete 
