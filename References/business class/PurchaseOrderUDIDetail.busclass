PurchaseOrderUDIDetail is a BusinessClass
	owned by po
	prefix is POX
	classic name is POUDIDTL
	
	Ontology
		symbolic key is PurchaseOrderUDIDetail
            classic set name is POXSET1
            classic name for PurchaseOrderUDIDetail.PurchaseTransactionDocumentType is DOC-TYPE
            classic name for PurchaseOrderUDIDetail.DocumentNumberNumeric is DOC-NBR-NUM
            classic name for PurchaseOrderUDIDetail.LineNumber is LINE-NBR
            classic name for PurchaseOrderUDIDetail.TransactionSequence is TRANS-SEQ
		
	Patterns
		implements StaticJava
		disable AuditIndex
	
	Persistent Fields

		Location			is an InventoryLocation
			classic name is LOCATION
		Item
			classic name is ITEM
		SourceDocumentAlpha
			classic name is DOCUMENT
        GTIN				is an ItemGTIN
        	classic name is GTIN
        Manufacturer
        	classic name is MANUF-CODE
        	classic name is MANUF-DIVISION
        ManufacturerNumber
        	classic name is MANUF-NBR
        Lot       			is an ItemLot
			classic name is LOT
        SerialNumber    	is an ItemSerialNumber
        	delete ignored
        	classic name is SER-NBR
        ManufacturingDate	is Date
        ExpirationDate		is Date
        TransactionDate		is Date
        Quantity
        	precision is DerivedNumberOfDecimalsQuantity
        	classic name is QUANTITY
        UOM					is a UnitOfMeasure
        	classic name is UOM
		StockUOM			is a UnitOfMeasure
			classic name is STOCK-UOM
        UOMMultiplier
            classic name is UOM-MULT

	Local Fields

		LocalUpdateFromPOTransactionDtl	is Boolean
		LocalUpdateFromVendReturnLine	is Boolean
		LocalUDICount					is Numeric size 6 
		LocalOldLot						is AlphaUpper size 30
		LocalOldSerial					is AlphaUpper size 30
		GTINCheckDigit
		LocalValidCheckDigit            is Boolean
		UOMCalculation
				
	Context Fields
		ContextPurchaseOrderReceipt		is a PurchaseOrderReceipt
		ContextPurchaseOrderReceiptLine	is a PurchaseOrderReceiptLine
		ContextPOReceiptAdjustmentAndInspection		is a POReceiptAdjustmentAndInspection
		ContextPOReceiptAdjustmentAndInspectionLine	is a POReceiptAdjustmentAndInspectionLine

	Derived Fields

		DerivedNumberOfDecimalsQuantity is a ConditionalField
			type is Numeric size 1
			restricted
			if (Item exists)
				Item.NumberOfDecimalsQuantity
			else
				InventoryCompanyRel.NumberOfDecimalsQuantity

		DerivedGTINFiller is a DerivedField
			type is Alpha size 6
			restricted
			if (ItemGTINStructure8
			or  GTINStructureSize8)
				return "000000"
			else
			if (ItemGTINStructure12
			or  GTINStructureSize12)
				return "00"
			else
			if (ItemGTINStructure13
			or  GTINStructureSize13)
				return "0"
		
		DerivedGTINNumeric is a StringField
			type is Alpha size 14
			restricted
			DerivedGTINFiller
			GTIN

		DerivedItem				is a DerivedField
			type is like Item
			restricted
			if (IsReceivingType)
				return PurchaseOrderReceiptLineRel2.Item
			else
			if (IsReceivingAdjustmentType
			or  IsReceivingInspectionRejectionType)
				return POReceiptLineRel.Item
			else
			if (IsVendorReturnType)
				return VendorReturnLineRel.Item

		DerivedLocation					is a DerivedField
			type is like InventoryLocation
			restricted
			if (IsReceivingType)
				return PurchaseOrderReceiptLineRel2.ShipToLocation
			else
			if (IsReceivingAdjustmentType
			or	IsReceivingInspectionRejectionType)
				return POReceiptLineRel.ShipToLocation
			else
			if (IsVendorReturnType)
				return VendorReturnLineRel.VendorReturn.ReturnFromLocation

		DerivedSourceDocumentAlpha		is a DerivedField
			type is like SourceDocumentAlpha
			restricted
			if (IsReceivingType)
				return PurchaseOrderReceiptLineRel2.PurchaseOrderReceipt
			else
			if (IsReceivingAdjustmentType)
				return POReceiptAdjustmentLineRel.POReceiptAdjustmentAndInspectionLine.POReceiptAdjustmentAndInspection
			else
			if (IsReceivingInspectionRejectionType)
				return POReceiptInspectionLineRel.POReceiptAdjustmentAndInspectionLine.POReceiptAdjustmentAndInspection
			else
			if (IsVendorReturnType)
				return VendorReturnLineRel.ReturnNumberAlpha

		DerivedPOReceipt				is a DerivedField
			type is like PurchaseOrderReceipt
			restricted
			if (IsReceivingType)
				return PurchaseOrderReceiptLineRel2.PurchaseOrderReceipt
			else
			if (IsReceivingAdjustmentType)
				return POReceiptAdjustmentLineRel.PurchaseOrderReceipt
			else
			if (IsReceivingInspectionRejectionType)
				return POReceiptInspectionLineRel.PurchaseOrderReceipt
			else
			if (IsVendorReturnType)
				return VendorReturnLineRel.PurchaseOrderReceipt
		
		DerivedQuantityInReceivedUOM is a DerivedField
			type is like Quantity
			restricted
			if (POReceiptLineRel.ReceivedUOM = UOM)
				return Quantity
			else
				initialize UOMCalculation
				
				UOMCalculation.InputUOM    	 		= UOM
				UOMCalculation.InputToUOM  	 		= POReceiptLineRel.ReceivedUOM
				UOMCalculation.InputToUOMConversion = POReceiptLineRel.ReceivedUOMMultiplier
				UOMCalculation.InputQuantity 		= Quantity
				UOMCalculation.Method      	 		= UOMCalculation.Method.ConvertToAlternate
				
				return UOMCalculation.OutputQuantity
				
		DerivedManufacturingYear is a DerivedField
			type is AlphaUpper size 4
			restricted
			return ManufacturingDate year

		DerivedManufacturingMonth is a DerivedField
			type is Numeric size 2
			restricted
			return ManufacturingDate month
			
		DerivedManufacturingDay is a DerivedField
			type is Numeric size 2
			restricted
			return ManufacturingDate day
			
		DerivedManufacturingDate is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			DerivedManufacturingMonth
			DerivedManufacturingDay
			
		DerivedManufacturingDateWith0Month0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			0
			DerivedManufacturingMonth
			0
			DerivedManufacturingDay
			
		DerivedManufacturingDateWith0Month is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			0
			DerivedManufacturingMonth
			DerivedManufacturingDay
			
		DerivedManufacturingDateWith0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedManufacturingYear[3:4]
			DerivedManufacturingMonth
			0
			DerivedManufacturingDay
						
		DerivedManufacturingDate6 is a DerivedField
			type is AlphaUpper size 6
			restricted
			if (DerivedManufacturingMonth < 10
			and DerivedManufacturingDay < 10)
				return DerivedManufacturingDateWith0Month0Day
			else
			if (DerivedManufacturingMonth < 10)
				return DerivedManufacturingDateWith0Month
			else
			if (DerivedManufacturingDay < 10)
				return DerivedManufacturingDateWith0Day
			else
				return DerivedManufacturingDate

		DerivedExpirationYear is a DerivedField
			type is AlphaUpper size 4
			restricted
			return ExpirationDate year 

		DerivedExpirationMonth is a DerivedField
			type is Numeric size 2
			restricted
			return ExpirationDate month
			
		DerivedExpirationDay is a DerivedField
			type is Numeric size 2
			restricted
			return ExpirationDate day

		DerivedExpirationDate is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			DerivedExpirationMonth
			DerivedExpirationDay

		DerivedExpirationDateWith0Month0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			0
			DerivedExpirationMonth
			0
			DerivedExpirationDay
			
		DerivedExpirationDateWith0Month is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			0
			DerivedExpirationMonth
			DerivedExpirationDay
			
		DerivedExpirationDateWith0Day is a StringField
			type is AlphaUpper size 6
			restricted
			DerivedExpirationYear[3:4]
			DerivedExpirationMonth
			0
			DerivedExpirationDay
						
		DerivedExpirationDate6 is a DerivedField
			type is AlphaUpper size 6
			restricted
			if (DerivedExpirationMonth < 10
			and DerivedExpirationDay < 10)
				return DerivedExpirationDateWith0Month0Day
			else
			if (DerivedExpirationMonth < 10)
				return DerivedExpirationDateWith0Month
			else
			if (DerivedExpirationDay < 10)
				return DerivedExpirationDateWith0Day
			else
				return DerivedExpirationDate

		DerivedUDIOption1 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			"(10)"
			Lot
			"(21)"
			SerialNumber
				
		DerivedUDIOption2 is a StringField
			type is Alpha size up to 100
			restricted
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6

		DerivedUDIOption3 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6

			
		DerivedUDIOption4 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(10)"
			Lot
			
		DerivedUDIOption5 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(21)"
			SerialNumber
			
		DerivedUDIOption6 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			
		DerivedUDIOption7 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(10)"
			Lot
			
		DerivedUDIOption8 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(21)"
			SerialNumber
			
		DerivedUDIOption9 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6
			"(10)"
			Lot
			
		DerivedUDIOption10 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6
			"(21)"
			SerialNumber
			
		DerivedUDIOption11 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(10)"
			Lot
			"(21)"
			SerialNumber
						
		DerivedUDIOption12 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(17)"
			DerivedExpirationDate6
			"(10)"
			Lot
			"(21)"
			SerialNumber
			
		DerivedUDIOption13 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedExpirationDate6
			"(10)"
			Lot
			"(21)"
			SerialNumber

		DerivedUDIOption14 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			"(21)"
			SerialNumber
			
		DerivedUDIOption15 is a StringField
			type is Alpha size up to 100
			restricted	
			"(01)"
			DerivedGTINNumeric
			"(11)"
			DerivedManufacturingDate6
			"(17)"
			DerivedExpirationDate6
			"(21)"
			SerialNumber
			"(10)"
			Lot

		DerivedUDI is a DerivedField
			type is Alpha 100
			initialize DerivedUDI
			
			if (UDIOption1)
				DerivedUDI = DerivedUDIOption1
			else
			if (UDIOption2)
				DerivedUDI = DerivedUDIOption2
			else
			if (UDIOption3)
				DerivedUDI = DerivedUDIOption3
			else
			if (UDIOption4)
				DerivedUDI = DerivedUDIOption4
			else
			if (UDIOption5)
				DerivedUDI = DerivedUDIOption5
			else
			if (UDIOption6)
				DerivedUDI = DerivedUDIOption6
			else
			if (UDIOption7)
				DerivedUDI = DerivedUDIOption7
			else
			if (UDIOption8)
				DerivedUDI = DerivedUDIOption8
			else
			if (UDIOption9)
				DerivedUDI = DerivedUDIOption9
			else
			if (UDIOption10)
				DerivedUDI = DerivedUDIOption10
			else
			if (UDIOption11)
				DerivedUDI = DerivedUDIOption11
			else
			if (UDIOption12)
				DerivedUDI = DerivedUDIOption12
			else
			if (UDIOption13)
				DerivedUDI = DerivedUDIOption13
			else
			if (UDIOption14)
				DerivedUDI = DerivedUDIOption14
			else
			if (UDIOption15)
				DerivedUDI = DerivedUDIOption15

		DerivedQuantityUOMDisplay is a StringField
			type is Alpha 20
			default label is "Quantity"
			Quantity " " UOM

	Conditions

        IsUnreleased
        	restricted
	   		when (PurchaseOrderReceiptLineRel2.PurchaseOrderReceipt.Status.Unreleased
			or POReceiptAdjustmentLineRel.POReceiptAdjustmentAndInspection.Status.Unreleased
			or POReceiptInspectionLineRel.POReceiptAdjustmentAndInspection.Status.Unreleased
			or VendorReturnLineRel.VendorReturn.Status.Added)

		IsReceivingType
			restricted
			when (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.Receiving)
			
		IsReceivingAdjustmentType
			restricted
			when (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingAdjustment)
			
		IsReceivingInspectionRejectionType
			restricted
			when (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection)

		IsVendorReturnType
			restricted
			when (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.VendorReturn)
			
		ValidForDelete
			restricted
			when ((IsReceivingType
			and  PurchaseOrderReceiptLineRel2 exists
			and  PurchaseOrderReceiptLineRel2.Status.Entered)
			or  (IsReceivingAdjustmentType
			and  POReceiptAdjustmentLineRel exists
			and  POReceiptAdjustmentLineRel.POReceiptAdjustmentAndInspection.Status.Unreleased)
			or  (IsReceivingInspectionRejectionType
			and  POReceiptInspectionLineRel exists
			and  POReceiptInspectionLineRel.POReceiptAdjustmentAndInspection.Status.Unreleased)
			or  (IsVendorReturnType
			and  VendorReturnLineRel exists
			and  VendorReturnLineRel.VendorReturn.Status.Added))
									
        IsInventoried
        	restricted
            when (ItemLocationRel.InventoryTracked)

		IsUDITracked 
			restricted	
			when (ItemLocationRel.UDITracked.UDIRequiredAtReceipt
			and   IsInventoried)
						            
		IsLotTracked 
			restricted	
			when (ItemLocationRel.LotTracked.LotRequiredAtReceipt
			and   IsInventoried)

		IsLotRequiredAtReceiving 
			restricted	
			when (ItemLocationRel.LotTracked.LotRequiredAtReceipt
			and   IsInventoried)

		IsSerialTracked 
			restricted	
			when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt
			and   IsInventoried)

		IsSerialRequiredAtReceiving 
			restricted	
			when (ItemLocationRel.SerialTracked.SerialRequiredAtReceipt
			and   IsInventoried)

		IsUDIRequiredAtReceiving 
			restricted	
			when (ItemLocationRel.UDITracked.UDIRequiredAtReceipt
			and   IsInventoried)
			
		GTINRecordExists
			restricted
			when (ItemGTINRel exists)

		GTINSizeValid
			restricted
			when (GTIN size = 8
			or GTIN size = 12
			or GTIN size = 13
			or GTIN size = 14)

		GTINStructureSize8
			restricted
			when (GTIN size = 8)

		GTINStructureSize12
			restricted
			when (GTIN size = 12)

		GTINStructureSize13
			restricted
			when (GTIN size = 13)

		ItemUOMRecordExists
			restricted
			when (ItemUOMRel exists)
						
		PurchaseOrderUDIDetailExists
			restricted
			when (PurchaseOrderUDIDetailRel exists)
			
		ItemGTINStructure8
			restricted
			when (GTINRecordExists
			and   ItemGTINRel.ItemGTIN.GTINStructure.GTIN8)
			
		ItemGTINStructure12
			when (GTINRecordExists
			and   ItemGTINRel.ItemGTIN.GTINStructure.GTIN12)
			
		ItemGTINStructure13
			when (GTINRecordExists
			and   ItemGTINRel.ItemGTIN.GTINStructure.GTIN13)

		UDIOption1
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber entered)

		UDIOption2
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber not entered)
			
		UDIOption3
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber not entered)
			
		UDIOption4
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber not entered)
			
		UDIOption5
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber entered)

		UDIOption6
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber not entered)

		UDIOption7
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber not entered)
			
		UDIOption8
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber entered)
			
		UDIOption9
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber not entered)
			
		UDIOption10
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber entered)
											
		UDIOption11
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber entered)

		UDIOption12
			restricted
			when (GTIN entered
			and   ManufacturingDate not entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber entered)
											
		UDIOption13
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate not entered
			and   Lot entered
			and   SerialNumber entered)

		UDIOption14
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot not entered
			and   SerialNumber entered)
			
		UDIOption15
			restricted
			when (GTIN entered
			and   ManufacturingDate entered
			and   ExpirationDate entered
			and   Lot entered
			and   SerialNumber not entered)

		NeedUDIFields
			restricted
			when (ManufacturingDate not entered
			and   ExpirationDate not entered
			and   Lot not entered
			and   SerialNumber not entered)

        HasSerial
            restricted
            when (SerialNumber entered)

		IsForLotOrSerial
			restricted
			when (Lot entered
			or    SerialNumber entered)

		IsForLotSerialDocumentType
			restricted
			when (IsForLotOrSerial
			and   PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.Receiving)

		IsTrackingDetailBalanced
			restricted
			when (sum MSCMPurchaseOrderReceiptTrackingRel.Quantity = Quantity)

		IsValidForDelivery
			restricted
			when (POReceiptLineRel.PurchaseOrderReceipt.IsMSCMEnabled)
			
    Relations

        PurchaseOrderUDIDetailRel
            one-to-many relation to PurchaseOrderUDIDetail
            Field Mapping uses symbolic key
                related.Company = Company
			Instance Selection
            	where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.Receiving
            	and   related.PurchaseOrderUDIDetail.DocumentNumberNumeric			= PurchaseOrderUDIDetail.DocumentNumberNumeric
            	and   related.PurchaseOrderUDIDetail.LineNumber						= PurchaseOrderUDIDetail.LineNumber)
                            
        InventoryCompanyRel
            one-to-one relation to InventoryCompany
            required
            Field Mapping uses symbolic key
                related.Company = Company
                
        ItemLocationRel
            one-to-one relation to ItemLocation
            required
            Field Mapping uses symbolic key
                related.Company				= Company
                related.InventoryLocation	= DerivedLocation
                related.Item				= DerivedItem
                
        PurchaseOrderReceiptLineRel
            one-to-one relation to PurchaseOrderReceiptLine
            Field Mapping uses symbolic key
                related.Company                  = Company
                related.PurchaseOrderReceipt     = PurchaseOrderUDIDetail.DocumentNumberNumeric                
                related.PurchaseOrderReceiptLine = PurchaseOrderUDIDetail.LineNumber 

		PurchaseOrderReceiptLineRel2
            one-to-one relation to PurchaseOrderReceiptLine
            Field Mapping uses symbolic key
                related.Company								= Company
                related.PurchaseOrderReceipt				= PurchaseOrderUDIDetail.DocumentNumberNumeric                
                related.PurchaseOrderReceiptLine			= PurchaseOrderUDIDetail.LineNumber

        POReceiptLineRel
            one-to-one relation to PurchaseOrderReceiptLine
            Field Mapping uses symbolic key
                related.Company                  = Company
                related.PurchaseOrderReceipt     = DerivedPOReceipt                
                related.PurchaseOrderReceiptLine = PurchaseOrderUDIDetail.LineNumber
                                                             
       	POReceiptInspectionLineRel
            one-to-one relation to POReceiptAdjustmentAndInspectionLine
            Field Mapping uses symbolic key
                related.Company                  			 = Company
                related.AdjustmentInspectionDocumentType 	 = AdjustmentInspectionDocumentType.ReceiptInspection
                related.POReceiptAdjustmentAndInspection     = PurchaseOrderUDIDetail.DocumentNumberNumeric                
                related.POReceiptAdjustmentAndInspectionLine = PurchaseOrderUDIDetail.LineNumber          
			
		POReceiptAdjustmentLineRel
            one-to-one relation to POReceiptAdjustmentAndInspectionLine
            Field Mapping uses symbolic key
                related.Company                  			 = Company
                related.AdjustmentInspectionDocumentType 	 = AdjustmentInspectionDocumentType.ReceiptAdjustment
                related.POReceiptAdjustmentAndInspection     = PurchaseOrderUDIDetail.DocumentNumberNumeric                
                related.POReceiptAdjustmentAndInspectionLine = PurchaseOrderUDIDetail.LineNumber

        VendorReturnLineRel
            one-to-one relation to VendorReturnLine
            Field Mapping uses symbolic key
                related.Company          					= Company
                related.VendorReturn     					= PurchaseOrderUDIDetail.DocumentNumberNumeric
                related.VendorReturnLine 					= PurchaseOrderUDIDetail.LineNumber

        POReceiptUDIDetailRel  
            one-to-many relation to PurchaseOrderUDIDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.Receiving
                and   related.PurchaseOrderUDIDetail.DocumentNumberNumeric	= PurchaseOrderUDIDetail.DocumentNumberNumeric
                and   related.PurchaseOrderUDIDetail.LineNumber				= PurchaseOrderUDIDetail.LineNumber) 
                          
        POAdjustmentUDIDetailRel  
            one-to-many relation to PurchaseOrderUDIDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingAdjustment
                and   related.PurchaseOrderUDIDetail.DocumentNumberNumeric	= PurchaseOrderUDIDetail.DocumentNumberNumeric
                and   related.PurchaseOrderUDIDetail.LineNumber				= PurchaseOrderUDIDetail.LineNumber) 

        PORejectionUDIDetailRel  
            one-to-many relation to PurchaseOrderUDIDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection
                and   related.PurchaseOrderUDIDetail.DocumentNumberNumeric	= PurchaseOrderUDIDetail.DocumentNumberNumeric
                and   related.PurchaseOrderUDIDetail.LineNumber				= PurchaseOrderUDIDetail.LineNumber) 

        VendorReturnUDIDetailRel  
            one-to-many relation to PurchaseOrderUDIDetail
            Field Mapping uses symbolic key
                related.Company = Company
            Instance Selection
                where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.VendorReturn
                and   related.PurchaseOrderUDIDetail.DocumentNumberNumeric	= PurchaseOrderUDIDetail.DocumentNumberNumeric
                and   related.PurchaseOrderUDIDetail.LineNumber				= PurchaseOrderUDIDetail.LineNumber) 

   		ItemGTINRel
			one-to-one relation to ItemGTIN
   			Field Mapping uses symbolic key
   				related.ItemGroup			= InventoryCompanyRel.ItemGroup
				related.Item				= DerivedItem
   				related.ItemGTIN			= GTIN

    	ItemUOMRel						
    		one-to-one relation to ItemUOM
    		Field Mapping uses symbolic key
    			related.ItemGroup		= InventoryCompanyRel.ItemGroup
    			related.Item			= DerivedItem
    			related.UnitOfMeasure	= UOM
    			
    	PurchaseOrderTransactionDetailRel						
    		one-to-many relation to PurchaseOrderTransactionDetail
    		Field Mapping uses ByLine
    			related.Company																	= Company
      		Instance Selection
    			where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseOrderUDIDetail.PurchaseTransactionDocumentType
    			and    related.PurchaseOrderTransactionDetail.DocumentNumberNumeric				= PurchaseOrderUDIDetail.DocumentNumberNumeric
    			and    related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderUDIDetail.LineNumber
      			and	   related.Lot																= Lot
      			and	   related.Serial															= SerialNumber)

    	PurchaseOrderTransactionDetailUpdateRel						
    		one-to-many relation to PurchaseOrderTransactionDetail
    		Field Mapping uses ByLine
    			related.Company																	= Company
    		Instance Selection
    			where (related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseOrderUDIDetail.PurchaseTransactionDocumentType
    			and    related.PurchaseOrderTransactionDetail.DocumentNumberNumeric				= PurchaseOrderUDIDetail.DocumentNumberNumeric
    			and    related.PurchaseOrderTransactionDetail.LineNumber						= PurchaseOrderUDIDetail.LineNumber
      			and	   related.Lot																= LocalOldLot
      			and	   related.Serial															= LocalOldSerial)

    	VendorReturnLineDetailRel						
    		one-to-many relation to VendorReturnLineDetail
    		Field Mapping uses ByLine
    			related.Company													= Company
      		Instance Selection
    			where (related.VendorReturnLineDetail.VendorReturn				= PurchaseOrderUDIDetail.DocumentNumberNumeric
    			and    related.VendorReturnLineDetail.VendorReturnLine			= PurchaseOrderUDIDetail.LineNumber
      			and	   related.Lot												= Lot
      			and	   related.Serial											= SerialNumber)

    	VendorReturnLineDetailUpdateRel						
    		one-to-many relation to VendorReturnLineDetail
    		Field Mapping uses ByLine
    			related.Company													= Company
      		Instance Selection
    			where (related.VendorReturnLineDetail.VendorReturn				= PurchaseOrderUDIDetail.DocumentNumberNumeric
    			and    related.VendorReturnLineDetail.VendorReturnLine			= PurchaseOrderUDIDetail.LineNumber
      			and	   related.Lot												= LocalOldLot
      			and	   related.Serial											= LocalOldSerial)

        PurchaseOrderUDIDetailsSerialRels  
	        one-to-many relation to PurchaseOrderUDIDetail
	        Field Mapping uses symbolic key
	            related.Company = Company
	        Instance Selection
	            where (related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType	= PurchaseOrderUDIDetail.PurchaseTransactionDocumentType
	            and   related.PurchaseOrderUDIDetail.DocumentNumberNumeric				= PurchaseOrderUDIDetail.DocumentNumberNumeric
	            and   related.PurchaseOrderUDIDetail.LineNumber							= PurchaseOrderUDIDetail.LineNumber
	            and   related.SerialNumber												= SerialNumber)  

		MSCMPurchaseOrderReceiptTrackingRel
			one-to-many relation to MobileSupplyChainPurchaseOrderReceiptTracking
			Field Mapping uses symbolic key
				related.Company															= Company
				related.PurchaseOrderReceipt											= PurchaseOrderUDIDetail.DocumentNumberNumeric
				related.PurchaseOrderReceiptLine										= PurchaseOrderUDIDetail.LineNumber
				related.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= blank
				related.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= blank
				related.PurchaseOrderTransactionDetail.LineNumber						= blank
				related.PurchaseOrderTransactionDetail.TransactionSequence				= blank
				related.PurchaseOrderUDIDetail.PurchaseTransactionDocumentType			= PurchaseOrderUDIDetail.PurchaseTransactionDocumentType
				related.PurchaseOrderUDIDetail.DocumentNumberNumeric					= PurchaseOrderUDIDetail.DocumentNumberNumeric
				related.PurchaseOrderUDIDetail.LineNumber								= PurchaseOrderUDIDetail.LineNumber
				related.PurchaseOrderUDIDetail.TransactionSequence						= PurchaseOrderUDIDetail.TransactionSequence
    Rule Blocks
    	UpdateDetailsInBalance
    		if (IsReceivingType)
    			invoke Update PurchaseOrderReceiptLineRel
    				invoked.TransientDisallowTxnDetailCallback = true
    		
    		if (IsReceivingInspectionRejectionType)
    			invoke SetDetailsInBalance POReceiptInspectionLineRel
    		else
    		if (IsReceivingAdjustmentType)
    			invoke SetDetailsInBalance POReceiptAdjustmentLineRel
    		else
    		if (IsVendorReturnType)
    			invoke SetDetailsInBalance VendorReturnLineRel
    		

		ValidateGTIN
			initialize GTINCheckDigit
			GTINCheckDigit.ItemGTIN			= GTIN
			LocalValidCheckDigit        	= GTINCheckDigit.ValidCheckDigit
			constraint (LocalValidCheckDigit = true)
				"GTINCheckDigitIsInvalid"

		CreatePurchaseOrderTransactionDetail
			invoke Create PurchaseOrderTransactionDetailRel
				invoked.Company															= Company
				invoked.PurchaseOrderTransactionDetail.PurchaseTransactionDocumentType	= PurchaseOrderUDIDetail.PurchaseTransactionDocumentType
				invoked.PurchaseOrderTransactionDetail.DocumentNumberNumeric			= PurchaseOrderUDIDetail.DocumentNumberNumeric
				invoked.PurchaseOrderTransactionDetail.PurchaseOrderReceipt				= PurchaseOrderUDIDetail.DocumentNumberNumeric
				invoked.PurchaseOrderTransactionDetail.SourceDocumentAlpha				= PurchaseOrderUDIDetail.SourceDocumentAlpha
				invoked.PurchaseOrderTransactionDetail.LineNumber		    			= PurchaseOrderUDIDetail.LineNumber
				if (IsSerialRequiredAtReceiving)
					invoked.Serial														= PurchaseOrderUDIDetail.SerialNumber				
					initialize invoked.Lot
					initialize invoked.LotExpirationDate	
				else
				if (IsLotRequiredAtReceiving)
					invoked.Lot															= PurchaseOrderUDIDetail.Lot
					invoked.LotExpirationDate											= PurchaseOrderUDIDetail.ExpirationDate
					initialize invoked.Serial
				invoked.Quantity														= PurchaseOrderUDIDetail.DerivedQuantityInReceivedUOM
				invoked.Item															= PurchaseOrderUDIDetail.Item

		UpdatePurchaseOrderTransactionDetail
			invoke Update PurchaseOrderTransactionDetailUpdateRel
				if (IsSerialRequiredAtReceiving)
					invoked.Serial														= PurchaseOrderUDIDetail.SerialNumber				
					initialize invoked.Lot
					initialize invoked.LotExpirationDate
				else
				if (IsLotRequiredAtReceiving)
					invoked.Lot															= PurchaseOrderUDIDetail.Lot
					invoked.LotExpirationDate											= PurchaseOrderUDIDetail.ExpirationDate
					initialize invoked.Serial
				invoked.Quantity														= PurchaseOrderUDIDetail.DerivedQuantityInReceivedUOM
				invoked.LocalUpdateFromPOUDIDtl											= true

		CreateVendorReturnLineDetail
			invoke Create VendorReturnLineDetailRel
				invoked.Company													= Company
				invoked.VendorReturnLineDetail.VendorReturn						= PurchaseOrderUDIDetail.DocumentNumberNumeric
				invoked.VendorReturnLineDetail.VendorReturnLine			    	= PurchaseOrderUDIDetail.LineNumber
				invoked.DocumentType											= PurchaseOrderUDIDetail.PurchaseTransactionDocumentType
				invoked.InventoryLocation										= PurchaseOrderUDIDetail.Location
				invoked.Item													= PurchaseOrderUDIDetail.Item
				invoked.UnitOfMeasure											= PurchaseOrderUDIDetail.UOM
				invoked.Quantity												= PurchaseOrderUDIDetail.Quantity
				if (IsLotRequiredAtReceiving)
					invoked.Lot													= PurchaseOrderUDIDetail.Lot
					invoked.LotExpirationDate									= PurchaseOrderUDIDetail.ExpirationDate
				if (IsSerialRequiredAtReceiving)
					invoked.Serial												= PurchaseOrderUDIDetail.SerialNumber				
				if (IsInventoried and ItemLocationRel.BinTracked)
					invoked.LocalBypassBinValidation							= true

		UpdateVendorReturnLineDetail
			invoke Update VendorReturnLineDetailUpdateRel
				if (IsLotRequiredAtReceiving)
					invoked.Lot													= PurchaseOrderUDIDetail.Lot
					invoked.LotExpirationDate									= PurchaseOrderUDIDetail.ExpirationDate
				if (IsSerialRequiredAtReceiving)
					invoked.Serial												= PurchaseOrderUDIDetail.SerialNumber				
				invoked.Quantity												= PurchaseOrderUDIDetail.Quantity
				invoked.LocalUpdateFromPOUDIDtl									= true
				if (IsInventoried and ItemLocationRel.BinTracked)
					invoked.LocalBypassBinValidation							= true
	
	Field Rules
	
		Location
			default to DerivedLocation	

		SourceDocumentAlpha
			default to DerivedSourceDocumentAlpha
			
		GTIN
			required
			constraint (GTIN is numeric)
				"GTINMustBeNumeric"

			constraint (GTINSizeValid)
				"GTINSizeIsInvalid"
								
			include ValidateGTIN
			







		Manufacturer
			if (Manufacturer not entered)
				default to ItemGTINRel.Manufacturer
				
		ManufacturerNumber
			if (ManufacturerNumber not entered)
				default to ItemGTINRel.ManufacturerNumber 

		Quantity
			required
			if (HasSerial)
				if (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingAdjustment 
				and POReceiptAdjustmentLineRel.Quantity < 0)
					force default to -1
				else
					if (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection
					or  PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingAdjustment 
					or  PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.Receiving)
						force default to 1
											
		Lot
			if	(Lot not entered
			and IsLotTracked)
				constraint (IsLotRequiredAtReceiving)
					"MustEnterLotNumberForLotTrackedItem"

			if (ExpirationDate entered)
				required
					"LotIsRequiredWhenExpirationDateIsEntered"					

		ExpirationDate
			if (Lot entered)
				required
					"ExpirationDateIsRequiredWhenLotIsEntered"
		
			if (ExpirationDate entered)
				constraint (ExpirationDate not < current corporate date)
					"ExpirationDateCannotBePriorToCurrentDate"
					
		SerialNumber
			if (IsSerialRequiredAtReceiving)
				required
					"MustEnterSerialNumberForSerialTrackedItem"
				
				if (IsReceivingInspectionRejectionType
				or  POReceiptAdjustmentLineRel.Quantity < 0)
					constraint (SerialNumber exists)
						"SerialNumber<SerialNumber>DoesNotExist"


				else
				if (IsReceivingType
				or  POReceiptAdjustmentLineRel.Quantity > 0)
					constraint (SerialNumber not exists)
						"SerialNumber<SerialNumber>AlreadyExists"        								//"@e.ICED.35"




		UOM
			required
			
			if (IsReceivingType)
				if (IsUDIRequiredAtReceiving and IsSerialRequiredAtReceiving)
					force default to POReceiptLineRel.ReceivedUOM
			
			if (UOM entered)
				constraint (ItemUOMRecordExists)
					"<UOM>IsNotAValidUOMForTheReceivedItem"
			else
			if (GTIN entered)
				default to ItemGTINRel.UnitOfMeasure

		UOMMultiplier
			if (UOMMultiplier not entered
			or  UOM changed)
				default to ItemUOMRel.UOMConversion

    Sets

        ByItemLocationLotSerial
            indexed
            duplicates
            Instance Selection
                where (IsForLotSerialDocumentType)
            Sort Order
                Company
                Item
                Location
				Lot
				SerialNumber
	
	Actions
		Create is a Create Action
			valid when (IsUnreleased)

			Entrance Rules
				if (PurchaseOrderUDIDetail.TransactionSequence	= 0)
					PurchaseOrderUDIDetail.TransactionSequence = 1

				if (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.Receiving and POReceiptUDIDetailRel exists)
					LocalUDICount = instance count of POReceiptUDIDetailRel
					PurchaseOrderUDIDetail.TransactionSequence = LocalUDICount + 1
				else
				if (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingAdjustment and POAdjustmentUDIDetailRel exists)
					LocalUDICount = instance count of POAdjustmentUDIDetailRel
					PurchaseOrderUDIDetail.TransactionSequence = LocalUDICount + 1
				else
				if (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.ReceivingInspectionRejection and PORejectionUDIDetailRel exists)
					LocalUDICount = instance count of PORejectionUDIDetailRel
					PurchaseOrderUDIDetail.TransactionSequence = LocalUDICount + 1
				else
				if (PurchaseOrderUDIDetail.PurchaseTransactionDocumentType.VendorReturn and VendorReturnUDIDetailRel exists)
					LocalUDICount = instance count of VendorReturnUDIDetailRel
					PurchaseOrderUDIDetail.TransactionSequence = LocalUDICount + 1

			Field Rules
				Item
					default to DerivedItem
				StockUOM
					default to Item.StockUOM
				TransactionDate = current date

			Action Rules
				if (GTIN entered) 
					constraint (!NeedUDIFields)
						"AtLeast1ProductIdentifierIsRequired"

				if(HasSerial)
           			constraint (PurchaseOrderUDIDetailsSerialRels not exists) 
						"SerialNumberIsAlreadyOnThisReceiver"
							
			Exit Rules

				include UpdateDetailsInBalance
			
				if ((IsLotRequiredAtReceiving
				or   IsSerialRequiredAtReceiving)
				and IsInventoried)
					if ((IsReceivingType
					or   IsReceivingAdjustmentType
					or   IsReceivingInspectionRejectionType)
					and  PurchaseOrderTransactionDetailRel not exists)
						include CreatePurchaseOrderTransactionDetail							
					if  (IsVendorReturnType)
						if	(VendorReturnLineDetailRel not exists)
							include CreateVendorReturnLineDetail
						else
							include UpdateVendorReturnLineDetail
								
				if (IsUDIRequiredAtReceiving
				and !IsLotRequiredAtReceiving and !IsSerialRequiredAtReceiving
				and IsInventoried)							
					if  (IsVendorReturnType)
						invoke Update VendorReturnLineRel

		Update is an Update Action

			Field Rules

				Lot
				    if (Lot changed)
				        LocalOldLot		= old Lot
				        						
				SerialNumber
				    if (SerialNumber changed)
				        LocalOldSerial	= old SerialNumber

				ExpirationDate
					if (ExpirationDate changed
					and LocalOldLot		= blank
					and LocalOldSerial	= blank)
					    LocalOldLot		= Lot
					    LocalOldSerial	= SerialNumber

				Quantity
					if (Quantity changed
					and LocalOldLot		= blank
					and LocalOldSerial	= blank)
					    LocalOldLot		= Lot
					    LocalOldSerial	= SerialNumber

			Action Rules
				if (GTIN entered) 
					constraint (!NeedUDIFields)
						"AtLeast1ProductIdentifierIsRequired"
		
			Exit Rules

				include UpdateDetailsInBalance
			
				if ((IsLotRequiredAtReceiving
				or   IsSerialRequiredAtReceiving)
				and IsInventoried)
					if ((IsReceivingType
					or   IsReceivingAdjustmentType
					or   IsReceivingInspectionRejectionType)
					and  PurchaseOrderTransactionDetailUpdateRel exists
					and !LocalUpdateFromPOTransactionDtl)
						include UpdatePurchaseOrderTransactionDetail							
					if  (IsVendorReturnType
					and  VendorReturnLineDetailUpdateRel exists
					and !LocalUpdateFromVendReturnLine)
						include UpdateVendorReturnLineDetail
		
				if (IsUDIRequiredAtReceiving
				and !IsLotRequiredAtReceiving and !IsSerialRequiredAtReceiving
				and IsInventoried)							
					if  (IsVendorReturnType)
						invoke Update VendorReturnLineRel

		Delete is a Delete Action
			valid when (ValidForDelete)

			Entrance Rules
				if (IsVendorReturnType)
					LocalOldLot		= Lot
					LocalOldSerial	= SerialNumber			
						
			Exit Rules
				if ((IsLotRequiredAtReceiving
				or  IsSerialRequiredAtReceiving
				or  IsUDIRequiredAtReceiving)
				and IsInventoried)
					if ((IsReceivingType
					or   IsReceivingAdjustmentType
					or   IsReceivingInspectionRejectionType)
					and  PurchaseOrderTransactionDetailUpdateRel exists
					and !LocalUpdateFromPOTransactionDtl)
						include UpdateDetailsInBalance							
					if  (IsVendorReturnType
					and  VendorReturnLineDetailUpdateRel exists
					and !LocalUpdateFromVendReturnLine)
						include UpdateVendorReturnLineDetail

				if (IsUDIRequiredAtReceiving
				and !IsLotRequiredAtReceiving and !IsSerialRequiredAtReceiving
				and IsInventoried)							
					if  (IsVendorReturnType)
						invoke Update VendorReturnLineRel
	
