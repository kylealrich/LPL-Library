KitchenOrderInstruction is a BusinessClass
	owned by recipe
	
	prefix is KOI
	
	Ontology
		symbolic key is KitchenOrderInstruction
	
	Patterns
		disable AuditIndex
	
	Persistent Fields
		Sequence
			disable Auditing
		Instruction				is a Description3
		Attachment				is an AlternateAttachment
	
	Local Fields
		LocalAttributeCtr   is Numeric 2			

	Create Rules  
		include IDM.CreateRules 
			replace AttachmentField with Attachment
							
	Delete Rules
		include IDM.DeleteNoArchiveRules
			replace AttachmentField with Attachment

	Commit Rules
		include IDM.CommitRules
			replace AttachmentField with Attachment

	Field Rules
		Instruction
			required
	
	Sets
		BySequence
			duplicates
			Sort Order
				InventoryLocation
				KitchenOrder
				Sequence
				KitchenOrderInstruction
	
	Actions
		Create is a Create Action
			valid when (KitchenOrder.Status.Open)

		Update is an Update Action
			valid when (KitchenOrder.Status.Open)
		
		Delete is a Delete Action
			valid when (KitchenOrder.Status.Open)
			Entrance Rules
				invoke SystemUpdate KitchenOrder
					invoked.InstructionCount		-= 1
					
		SystemUpdate is an Update Action
			restricted
			bypass field rules

		UploadToIDM is an Instance Action  
			valid when (Attachment.ValidForIDMUpload)
			Action Rules
				include IDM.MoveAttachmentsToIDM
					replace AttachmentField   with Attachment	
						
									
		MoveAttachmentsToIDM is a Set Action
			restricted
			Instance Selection
				where (Attachment.IsLocal)

			Accumulators
				InstanceCount

			Action Rules
				Instance Rules
					increment InstanceCount	

					include IDM.MoveAttachmentsToIDM
						replace AttachmentField   with Attachment			

					commit transaction

					if (InstanceCount = config.IDM_MOVE_ATTACHMENTS_LIMIT)
						end set action instance loop	
			
