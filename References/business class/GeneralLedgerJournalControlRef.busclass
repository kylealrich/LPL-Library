GeneralLedgerJournalControlRef is a BusinessClass
	default label is "GlobalLedgerJournalControlRef"
	owned by GeneralLedger
	
	prefix is GLJCR
	
	Ontology
		symbolic key is GeneralLedgerJournalControlRef
		
	Patterns
	
	Persistent Fields
		FinanceEnterpriseGroup
	    ReportingBasis
	    AccountingEntity
		GeneralLedgerClosePeriod
        GeneralLedgerJournalControl
		JournalReferenceNumber 		is AlphaUpper 50
			protected
        Status            			is Numeric 1
            States
                Unreleased	  		value is 0
                Released			value is 1
                PendingApproval		value is 2
                CopyInProcess		value is 3
                PostInProcess		value is 4
                Posted              value is 5
                Deleted				value is 6		
                Report				value is 7
		Ledger
		System						is a GeneralLedgerSystemCode
		PostingDate
		BasisYear					is a GeneralLedgerCalendarYear
		ReportingBasisYear														
		ReportingBasisYearEndJournal											
        GeneralLedgerCalendarPeriod												
		JournalReferenceSequence    is Numeric 14	
   
	Derived Fields
		DerivedDescription	is a DerivedField
			type is Alpha size up to 60    
			if (HasJournalReferenceNumber)
				return GeneralLedgerJournalControl.Description
			else
				return ReportingBasisYearEndJournal.Description
		
		DerivedFunctionalDebitAmount is a DerivedField
			type is Decimal size 21.3
			if (HasJournalReferenceNumber)
				return GeneralLedgerJournalControl.FunctionalDebitAmount
			else
				return ReportingBasisYearEndJournal.FunctionalDebitAmount
		
		DerivedFunctionalCreditAmount is a DerivedField
			type is Decimal size 21.3
			if (HasJournalReferenceNumber)
				return GeneralLedgerJournalControl.FunctionalCreditAmount
			else
				return ReportingBasisYearEndJournal.FunctionalCreditAmount		
		
		DerivedCurrency is a DerivedField
			type is AlphaUpper size 5
			if (HasJournalReferenceNumber)
				return GeneralLedgerJournalControl.Currency
			else
				return ReportingBasisYearEndJournal.AccountingEntity.FunctionalCurrency			
	Field Rules
	
	Sets
		ByPostingDate
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				BasisYear
				AccountingEntity
				GeneralLedgerCalendarPeriod
				PostingDate 
				GeneralLedgerJournalControl 	
				ReportingBasisYear
				ReportingBasisYearEndJournal

		ByGeneralLedgerJournalControl
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				AccountingEntity	
				BasisYear
				GeneralLedgerJournalControl
				ReportingBasis

		ByLedger
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				AccountingEntity
				BasisYear
				Ledger
				PostingDate 
				GeneralLedgerJournalControl				
	
		ByLedgerYearEnd
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				AccountingEntity
				BasisYear
				Ledger
				ReportingBasisYear				
				ReportingBasisYearEndJournal							
						
		BySystem
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				AccountingEntity
				BasisYear
				System
				PostingDate 
				GeneralLedgerJournalControl							
		
		BySystemYearEnd
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				AccountingEntity
				BasisYear
				System		
				ReportingBasisYear 
				ReportingBasisYearEndJournal	

		ByYearEndJournal
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				AccountingEntity
				GeneralLedgerJournalControl
				ReportingBasisYear				
				ReportingBasisYearEndJournal	

		ByJournalReferenceSequence
			not indexed
			duplicates
			Sort Order
				FinanceEnterpriseGroup
				ReportingBasis
				BasisYear
				AccountingEntity
				JournalReferenceSequence
						
	Conditions
		HasJournalReferenceNumber
			when (GeneralLedgerJournalControl != 0)
		SecurityGroupAllowsAccess
			when (actor.context.AccountingEntitySecurityGroup = blank
			or   (AccountingEntitySecurityGroupMemberRel exists))
			
	Relations
		ReportingBasisYearEndJournalTransactionRel
			one-to-many relation to ReportingBasisYearEndJournalTransaction
			Field Mapping uses symbolic key
				related.FinanceEnterpriseGroup		= FinanceEnterpriseGroup
				related.ReportingBasis				= ReportingBasis
				related.ReportingBasisYear			= ReportingBasisYear
				related.AccountingEntity			= AccountingEntity
			Instance Selection
				where (related.ReportingBasisYearEndJournal = ReportingBasisYearEndJournal)				

		AccountingEntitySecurityGroupMemberRel
			one-to-one relation to AccountingEntityGroupMember
			Field Mapping uses part of key
				related.FinanceEnterpriseGroup	= FinanceEnterpriseGroup
				related.AccountingEntityGroup   = actor.context.AccountingEntitySecurityGroup.FinanceDimensionStructure
				related.AccountingEntity        = AccountingEntity
					
	Actions
		Create is an Action
			restricted
			
			Exit Rules
				if(FinanceEnterpriseGroup.BODTrigger and (AccountingEntity.AddressCode.PostalAddress.Country = "FR" or AccountingEntity.AddressCode.PostalAddress.Country = "CL"))
					invoke TriggerBOD GeneralLedgerJournalControl
		
		Update is an Action
			restricted
			
		Delete is an Action
			restricted	

			
		
			
