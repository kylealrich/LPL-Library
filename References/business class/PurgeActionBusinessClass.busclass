PurgeActionBusinessClass is a BusinessClass
    owned by la
    prefix is PABC
    
    Ontology
    	symbolic key is PurgeActionBusinessClass
    
    Patterns
    	disable Auditing
		disable EffectiveDated
		implements CreateStamp
		implements UpdateStamp

    Persistent Fields
		Description is Text
			translatable
		
	Conditions
		WillArchive
			when (PurgeActionBusinessClass.PurgeBusinessClass.ImplementsArchivable and PurgeActionBusinessClass.PurgeBusinessClass.ArchivingEnabledForBusinessClass)
			
		DeveloperMode
			when (PurgeActionDefinition.DeveloperMode)
			
		ArchivingEnabled
			when (PurgeActionDefinition.ArchivingEnabled)
			
		DisplayArchiveAlert
			when (not PurgeActionDefinition.ArchivingEnabled or not PurgeActionBusinessClass.PurgeBusinessClass.ArchivingEnabledForBusinessClass)
			
		AllowEdits
			when (PurgeActionDefinition.AllowEdits)
			
	Derived Fields
		ArchivingDisabledMessage is a MessageField
			restricted
			"ArchivingIsNotEnabled"
		
		DoesNotImplementArchivableMessage is a MessageField
			restricted
			"BusinessClass<PurgeActionBusinessClass.PurgeBusinessClass>DoesNotImplementArchivable"
			
		BCArchivingDisabledMessage is a MessageField
			restricted
			"<PurgeActionBusinessClass.PurgeBusinessClass>IsArchivableButArchivingIsNotEnabledForThisBusinessClass"
		
		WillArchiveMouseOverMessage is a DerivedField
    		type is MessageField
    		
			if (not ArchivingEnabled)
				return ArchivingDisabledMessage
			else	
			if (not PurgeActionBusinessClass.PurgeBusinessClass.ImplementsArchivable)
				return DoesNotImplementArchivableMessage
			else
			if (not PurgeActionBusinessClass.PurgeBusinessClass.ArchivingEnabledForBusinessClass)
				return BCArchivingDisabledMessage
			
			return ""
			
			
	Create Rules
		constraint (not PurgeActionDefinition.Inactive)
			"PurgeActionDefinition<PurgeActionDefinition.BusinessView>,<PurgeActionDefinition.BusinessAction>Inactive"
	
	Actions
		Create is a Create Action
			valid when (AllowEdits)
			
		Update is an Update Action
			valid when (AllowEdits)
			
		Delete is a Delete Action
			valid when (AllowEdits)
		
	Sets
		ByPurgeActionBusClass
			indexed
			Sort Order
				PurgeActionBusinessClass.PurgeBusinessClass 
				BusinessView
				BusinessAction
				
				
		
