ReceivableGeneralLedgerCode is a BusinessClass
	owned by ar
	prefix is RCD
	classic name is ARCODE
	default label is "ReceivableGlobalLedgerCode"

	Ontology
		symbolic key is ReceivableGeneralLedgerCode
			classic set name is RCDSET1
			classic name is AR-CODE

	Patterns
		implements StaticJava
		disable AuditIndex


	Persistent Fields

		Description
			classic name is DESC
			disable Auditing	//IEFIN-66210 - Commenting out "disable Auditing" within Patterns.  Adding "disable Auditing" at the Persistent Field Level, except for Distribution Account to retain and audit history.

		UsedFlag			is Boolean
			classic name is USED-FL
			restricted	
			disable Auditing	//IEFIN-66210 - Commenting out "disable Auditing" within Patterns.  Adding "disable Auditing" at the Persistent Field Level, except for Distribution Account to retain and audit history.
		Active				is Boolean
			classic name is ACTIVE-STATUS
			disable Auditing	//IEFIN-66210 - Commenting out "disable Auditing" within Patterns.  Adding "disable Auditing" at the Persistent Field Level, except for Distribution Account to retain and audit history.
		DistributionAccount is a FinanceCodeBlockFull
			classic name for DistributionAccount.AccountingUnit is ACCT-UNIT
			classic name for DistributionAccount.GeneralLedgerChartAccount is ACCOUNT


	Transient Fields
		TransientCodeBlock		is a TransactionCodeBlock	
		BypassProjectDateEdit
		BypassStructureRelationEdit 
		TransientTransactionDate	is an ExchangeDate
		TransientGeneralLedgerDate	is a PostingDate

	Attach Rules
		constraint (Active)
			"ReceivableCodeIsNotActive"

	Field Rules
		Description
			required

		DistributionAccount
			required





			if (BillingInvoiceImportReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Billing_\Invoice_\ImportRecord;_\Distribution_\AccountCannotBeChanged."	


			if (ReceivableInvoiceImportReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Receivable_\Invoice_\ImportRecord;_\Distribution_\AccountCannotBeChanged."	


			if (BillingInvoiceReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Billing_\InvoiceRecord;_\Distribution_\AccountCannotBeChanged."	


			if (CustomerDraftReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Customer_\DraftAsA_\Receivable_\Global_\Ledger_\Code;_\Distribution_\AccountCannotBeChanged."	

			if (CustomerDraftBillOfExchangeAccrualCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Customer_\DraftAsA_\Bill_\Of_\Exchange_\Accrual_\Code;_\Distribution_\AccountCannotBeChanged."	

			if (CustomerDraftDishonorCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Customer_\DraftAsA_\Dishonor_\Code;_\Distribution_\AccountCannotBeChanged."	


			if (UnreleasedReceivablePaymentHeaderReceivedNotAppliedReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByAn_\Unreleased_\Receivable_\Payment_\HeaderRecordAsA_\Received_\Not_\AppliedRecord;_\Distribution_\AccountCannotBeChanged."	

			if (UnreleasedReceivablePaymentHeaderPrepaymentReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByAn_\Unreleased_\Receivable_\Payment_\HeaderRecordAsA_\PrepaymentRecord;_\Distribution_\AccountCannotBeChanged."	



			if (ReceivablePaymentReceivedNotAppliedReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Receivable_\PaymentByThis_\Receivable_\Company;_\Distribution_\AccountCannotBeChanged."	



			if (UnreleasedReceivableInvoiceBatchReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByAn_\Unreleased_\Receivable_\Invoice_\BatchByThis_\Receivable_\Company;_\Distribution_\AccountCannotBeChanged."	



			if (ReceivableInvoiceObligationReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Receivable_\Invoice_\ObligationByThis_\Receivable_\Company;_\Distribution_\AccountCannotBeChanged."	



			if (ReceivableInvoiceReceivableGeneralLedgerCodeRel exists)	
				cannot be changed	
					"CodeInUseByA_\Transaction(_\Invoice,_\Credit_\Memo,Or_\Debit_\Memo)ByThis_\Receivable_\Company;_\Distribution_\AccountCannotBeChanged."	//IEFIN-109736 removed "open", since this condition isn't tied to the status of the transaction	



			constraint (DistributionAccount.ToAccountingEntity = Company.GeneralLedgerCompany.AccountingEntity)
				"<Company.CustomerBusinessGroup.FinanceEnterpriseGroup.AccountingEntityLabel>MustBe<Company.GeneralLedgerCompany.AccountingEntity>"











		Active
			if (parentcontext.name = "ReceivableCompany")
				Active = true

			if (!Active)
				constraint	(ReceivableGeneralLedgerCode != Company.ReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.ReceivedNotAppliedReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.PrepaymentReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.FinanceChargeReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.BOEAcceptanceReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.BOEFinancialRevenueReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.BOEFinancialExpenseReceivableGeneralLedgerCode
				and			 ReceivableGeneralLedgerCode != Company.BOEDishonoredExpenseReceivableGeneralLedgerCode)
					"CannotChangeStatusToInactive;_\CompanyUsing_\G\LCode"   

				constraint (!ReceivableProcessLevelRel exists)
					"CannotChangeStatusToInactive;_\Process_\LevelUsingCode"	 






	Conditions
		IsValidForActorContext
			restricted
			when (GeneralLedgerCompanyRel.FinanceEnterpriseGroup = actor.context.FinanceEnterpriseGroup)	

		SecurityGroupAllowsAccess	
			when (actor.context.CompanySecurityGroup = blank
			or	  CompanySecurityGroupMemberRel exists)

	Relations

		ReceivableProcessLevelRel
			one-to-many relation to ReceivableProcessLevel
			Field Mapping uses symbolic key
				related.Company								= Company
			Instance Selection
				where	(ReceivableGeneralLedgerCode = Company.ReceivableGeneralLedgerCode
				or		 ReceivableGeneralLedgerCode = Company.ReceivedNotAppliedReceivableGeneralLedgerCode
				or		 ReceivableGeneralLedgerCode = Company.PrepaymentReceivableGeneralLedgerCode)

		GeneralLedgerCompanyRel	
			one-to-one relation to GeneralLedgerCompany
			Field Mapping uses symbolic key
				related.Company		= Company

		CompanySecurityGroupMemberRel	
			one-to-one relation to GeneralLedgerCompanyGroupMember
			Field Mapping uses symbolic key
				related.GeneralLedgerCompanyGroup	= actor.context.CompanySecurityGroup.FinanceDimensionStructure
				related.Company						= Company




		ReceivablePaymentReceivedNotAppliedReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivablePayment
			Field Mapping uses Set10	
				related.Company													 = Company
			Instance Selection
				where (related.ReceivedNotAppliedReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		ReceivableInvoiceReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivableInvoice
			Field Mapping uses symbolic key
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		UnreleasedReceivableInvoiceBatchReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivableInvoiceBatch
			Field Mapping uses Set2	
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		UnreleasedReceivablePaymentHeaderReceivedNotAppliedReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivablePaymentHeader
			Field Mapping uses Set2	
				related.Company													 = Company
			Instance Selection
				where (related.ReceivedNotAppliedReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		UnreleasedReceivablePaymentHeaderPrepaymentReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivablePaymentHeader
			Field Mapping uses Set2	
				related.Company													 = Company
			Instance Selection
				where (related.PrepaymentReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		ReceivableInvoiceObligationReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivableInvoiceObligation
			Field Mapping uses symbolic key
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		ReceivableInvoiceImportReceivableGeneralLedgerCodeRel	
			one-to-many relation to ReceivableInvoiceImport
			Field Mapping uses Set1
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)




		BillingInvoiceReceivableGeneralLedgerCodeRel	
			one-to-many relation to BillingInvoice
			Field Mapping uses symbolic key
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		BillingInvoiceImportReceivableGeneralLedgerCodeRel	
			one-to-many relation to BillingInvoiceImport
			Field Mapping uses ByPrefixandInvoiceNumber
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)




		CustomerDraftReceivableGeneralLedgerCodeRel	
			one-to-many relation to CustomerDraft
			Field Mapping uses symbolic key
				related.Company													 = Company
			Instance Selection
				where (related.ReceivableGeneralLedgerCode	 = this instance.ReceivableGeneralLedgerCode)

		CustomerDraftBillOfExchangeAccrualCodeRel	
			one-to-many relation to CustomerDraft
			Field Mapping uses symbolic key
				related.Company													 = Company
			Instance Selection
				where (related.BillOfExchangeAccrualCode	 = this instance.ReceivableGeneralLedgerCode)

		CustomerDraftDishonorCodeRel	
			one-to-many relation to CustomerDraft
			Field Mapping uses symbolic key
				related.Company													 = Company
			Instance Selection
				where (related.DishonorCode	 = this instance.ReceivableGeneralLedgerCode)


	Derived Fields	
		RepresentativeText is a StringField	
			type is Text
			default label is "ReceivableGlobalLedgerCode"
			ReceivableGeneralLedgerCode " - " Description
		
		DerivedReceivableGlobalLedgerCodeTitle	is a DerivedField	
			type is Alpha size up to 132
			default label is "ReceivableGlobalLedgerCode"
			return RepresentativeText




	Actions
		Create is a Create Action
			Action Rules
				Active = true

		Update is an Update Action

		Delete is a Delete Action

		EditCodeBlock is an Update Action
			restricted
			Action Rules
				TransientCodeBlock = DistributionAccount

		ValidateInvoiceFinanceCodeBlock is an Instance Action
			restricted
			Parameters
				PrmInvoiceFinanceCodeBlock is a FinanceCodeBlockFull
			Parameter Rules
				PrmInvoiceFinanceCodeBlock
					default to DistributionAccount
						default individual fields
					BypassStructureRelationEdit = false
			Action Rules

