ContractAndLineLocation is a BusinessClass
	owned by po
    prefix is CALL

	Ontology
		symbolic key is ContractAndLineLocation

	Patterns
		implements StaticJava

	Persistent Fields
        Taxable                         is Numeric size 1
            States
                NotApplicable           value is 0
                Yes                     value is 1
                No                      value is 2
        TaxCode    
		ClinicalSystemUse               is Boolean    
        LineLevel                       is Boolean 
            default label is "LineOnlyForClinicalSystemUse"    

    Local Fields 
        LocalContractGroup              is like ContractGroup 
        LocalContract                   is like Contract 
        LocalContractLine               is like ContractLine
        LocalComponentGroup             is like ContractCapitatedComponentGroup
        LocalCompany                    is like Company 
        LocalLocation                   is like InventoryLocation
        LocalRequestingLocation         is like RequestingLocation
        LocalComponentGroupItem         is like ContractCapitatedComponentGroupItem 
        LocalManufacturer               is like Manufacturer 
        LocalManufacturerNumber         is like ManufacturerNumber
        LocalConsolidatedItemMaster     is a ConsolidatedItemMaster

    Derived Fields 

        NumberOfLinesWithLocations		is a ComputeField
			type is Numeric 6
			(instance count of LineClinicalLocationRel)
    
    Conditions 
        CanMaintain 
            restricted
            when (Contract.NotClosed
            and  (ContractLine !entered
            or    ContractLine.LineNotClosed)
            and  ((Contract.EnableClinicalSystemUseLocations
            and   Contract.NonServicePurchaseType)
            or   (ContractGroup.EnableTaxOverrideLocations
            and   Contract.HasAPurchaseType)))
    
        CanChangeToLineLevel
            restricted
            when (ClinicalSystemUse
            and   !LineLevel
            and   Contract.NotClosed
            and   ContractLine !entered)

        CanChangeToHeaderLevel
            restricted
            when (ClinicalSystemUse
            and   LineLevel
            and   Contract.NotClosed
            and   ContractLine !entered)

        HasLinesWithoutThisLocation 
            restricted 
            when (Contract.NumberOfLinesQuickApproximation != NumberOfLinesWithLocations)
        
        DisplayLinesWithNoLocations 
            restricted 
            when (LineLevel
            and   HasLinesWithoutThisLocation)
        
        CanAddLocationsToAllLines
            restricted 
            when (LineLevel
            and   ClinicalSystemUse
            and   Contract.NotClosed
            and   ContractLine !entered
            and   HasLinesWithoutThisLocation)
        
        HasLineClinicalLocations 
            restricted 
            when (LineClinicalLocationRel exists)
        
        NoParticipantOrMember
            restricted 
            when (!Contract.ParticipantsOrMembersExist)

        CanMaintainClinical 
            restricted 
            when (ContractLineNotEntered
            and   Contract.NotClosed
            and   Contract.EnableClinicalSystemUseLocations)
        
        ContractLineNotEntered 
            restricted 
            when (ContractLine !entered)

        ContractLineEntered 
            restricted 
            when (ContractLine entered)
        
        TaxRelated 
            restricted 
            when (TaxCode entered
            or    Taxable > 0)
        
        NotTaxRelated 
            restricted 
            when (TaxCode !entered
            and   Taxable = 0)

        NotTaxOrClinical
            restricted 
            when (NotTaxRelated
            and   ClinicalSystemUse = false)

        TaxOrClinical
            restricted 
            when (TaxRelated
            or    ClinicalSystemUse)

        LocationOnly 
            restricted 
            when (ContractAndLineLocation.Location entered)

        ParOrCartLocation 
            restricted 
            when (LocationOnly
            and  !ContractAndLineLocation.Location.IsPerpetualLocation)
        
        LocationAndHeaderOnly 
            restricted 
            when (ContractAndLineLocation.Location entered
            and   ContractLineNotEntered)

        CompanyAndHeaderOnly 
            restricted 
            when (ContractLineNotEntered
            and   ContractAndLineLocation.Location !entered
            and   ContractAndLineLocation.RequestingLocation !entered)

        CompanyOnly 
            restricted 
            when (ContractAndLineLocation.Location !entered
            and   ContractAndLineLocation.RequestingLocation !entered)

        AllowTaxAndNotService
            restricted 
            when (!Contract.ServicePurchaseType
			and   ContractGroup.EnableTaxOverrideLocations)
        
        RequestingLocationOnly 
            restricted 
            when (ContractAndLineLocation.RequestingLocation entered)

        RequestingLocationAndHeaderOnly 
            restricted 
            when (ContractAndLineLocation.RequestingLocation entered
            and   ContractLineNotEntered)

        HasLocationsNotInUse 
            restricted 
            when (LocationsNotInUseRel exists)

        CrossLocationsDoNotExist 
            restricted 
            when ((RequestingLocationOnly
            and    RequestingLocForInventoryRel !exists)
            or    (LocationOnly
            and    InventoryForReqLocRel !exists))

    Relations 

        ContractLineLocationRel
            one-to-one relation to ContractAndLineLocation 
            Field Mapping uses symbolic key 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
                related.ContractLine                                = LocalContractLine 
                related.ContractAndLineLocation.Company             = ContractAndLineLocation.Company
                related.ContractAndLineLocation.Location            = ContractAndLineLocation.Location
                related.ContractAndLineLocation.RequestingLocation  = ContractAndLineLocation.RequestingLocation               

        HeaderLocationRel 
            one-to-one relation to ContractAndLineLocation 
            Field Mapping uses symbolic key 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
                related.ContractLine                                = 0 
                related.ContractAndLineLocation.Company             = LocalCompany
                related.ContractAndLineLocation.Location            = LocalLocation
                related.ContractAndLineLocation.RequestingLocation  = LocalRequestingLocation  

        LineClinicalLocationRel 
            one-to-many relation to ContractAndLineLocation 
            Field Mapping uses ByLocationAndLine 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
                related.ContractAndLineLocation.Company             = ContractAndLineLocation.Company
                related.ContractAndLineLocation.Location            = ContractAndLineLocation.Location
                related.ContractAndLineLocation.RequestingLocation  = ContractAndLineLocation.RequestingLocation 
            Instance Selection 
                where (related.ClinicalSystemUse = true
                and    related.ContractLine entered)

        ContractCapitatedComponentGroupItemRel 
            one-to-many relation to ContractCapitatedComponentGroupItem 
            Field Mapping uses symbolic key
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract               
                related.ContractLine                                = LocalContractLine 
                related.ContractCapitatedComponentGroup             = LocalComponentGroup 
        
        ContractCapitatedCompanyLocationItemRel
            one-to-many relation to ContractCapitatedCompanyLocationItem
            Field Mapping uses ByCapitatedItem 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract               
                related.ContractLine                                = LocalContractLine 
                related.ContractCapitatedComponentGroup             = LocalComponentGroup 
                related.ContractCapitatedComponentGroupItem         = LocalComponentGroupItem
                related.Company                                     = LocalCompany 
                related.Location                                    = LocalLocation             

        ContractCapitatedCompanyOnlyItemRel
            one-to-many relation to ContractCapitatedCompanyLocationItem
            Field Mapping uses ByCapitatedItem 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract               
                related.ContractLine                                = LocalContractLine 
                related.ContractCapitatedComponentGroup             = LocalComponentGroup 
                related.ContractCapitatedComponentGroupItem         = LocalComponentGroupItem
                related.Company                                     = LocalCompany 
                related.Location                                    = blank   
        LocationsNotInUseRel
            one-to-many relation to ContractAndLineLocation 
            Field Mapping uses symbolic key 
				related.ContractGroup                               = ContractGroup
				related.Contract                                    = Contract
            Instance Selection 
                where (related.NotTaxOrClinical)

		ConsolidatedItemLocationRel 
			one-to-many relation to ConsolidatedItemLocation 
			Field Mapping uses ByLocation
				related.ContractGroup			= ContractGroup
				related.InventoryCompany        = ContractAndLineLocation.Company
				related.InventoryLocation       = ContractAndLineLocation.Location		
				related.RequestingLocation      = ContractAndLineLocation.RequestingLocation
                related.ConsolidatedItemMaster  = LocalConsolidatedItemMaster
  
		ConsolidatedItemLocationForDeleteRel 
			one-to-many relation to ConsolidatedItemLocation 
			Field Mapping uses ByLocation
				related.ContractGroup			= LocalContractGroup
				related.InventoryCompany        = LocalCompany
				related.InventoryLocation       = LocalLocation		
				related.RequestingLocation      = LocalRequestingLocation
                related.ConsolidatedItemMaster  = LocalConsolidatedItemMaster

		RequestingLocForInventoryRel 
			one-to-many relation to ConsolidatedItemLocation 
			Field Mapping uses ByLocation
				related.ContractGroup			= ContractGroup
				related.InventoryCompany        = ContractAndLineLocation.Company
				related.InventoryLocation       = ContractAndLineLocation.RequestingLocation		
				related.RequestingLocation      = blank
                related.ConsolidatedItemMaster  = LocalConsolidatedItemMaster

        InventoryForReqLocRel
			one-to-many relation to ConsolidatedItemLocation 
			Field Mapping uses ByLocation
				related.ContractGroup			= ContractGroup
				related.InventoryCompany        = ContractAndLineLocation.Company
				related.InventoryLocation       = blank 	
				related.RequestingLocation      = ContractAndLineLocation.Location	
                related.ConsolidatedItemMaster  = LocalConsolidatedItemMaster
        
        ContractRel 
            one-to-one relation to Contract 
            Field Mapping uses symbolic key 
                related.ContractGroup                               = ContractGroup 
                related.Contract                                    = Contract             

        ContractLineRel 
            one-to-one relation to ContractLine 
            Field Mapping uses symbolic key 
                related.ContractGroup                               = ContractGroup 
                related.Contract                                    = Contract       
                related.ContractLine                                = ContractLine 
        ClinicalMayUseLinesRel 
            one-to-many relation to ContractLine 
            Field Mapping uses symbolic key 
                related.ContractGroup                               = ContractGroup 
                related.Contract                                    = Contract 
            Instance Selection 
                where (related.MayUseForConsolidated)

        ClinicalUseLinesRel 
            one-to-many relation to ContractLine 
            Field Mapping uses symbolic key 
                related.ContractGroup                               = ContractGroup 
                related.Contract                                    = Contract 
            Instance Selection 
                where (related.CanUseForConsolidated)

        ClinicalUseLinesForDeleteRel 
            one-to-many relation to ContractLine 
            Field Mapping uses symbolic key 
                related.ContractGroup                               = LocalContractGroup 
                related.Contract                                    = LocalContract 
            Instance Selection 
                where (related.CanUseForConsolidated)      

        ParticipantGroupContractRel 
            one-to-many relation to ParticipantGroupContract 
            Field Mapping uses ByContract 
                related.ProcurementGroup                            = ContractGroup 
                related.Contract                                    = Contract 
            Instance Selection 
                where (related.LocationOnly)


    Sets 
        ByContractOnly 
            Sort Order 
                ContractGroup 
                Contract 
                ContractAndLineLocation.Company 
                ContractAndLineLocation.Location 
                ContractAndLineLocation.RequestingLocation 
            Instance Selection 
                where (ContractLineNotEntered)

        ByLocationAndLine 
            Sort Order 
                ContractGroup 
                Contract 
                ContractAndLineLocation.Company 
                ContractAndLineLocation.Location 
                ContractAndLineLocation.RequestingLocation
                ContractLine

        ByLocationFirst
            Sort Order 
                ContractGroup 
                ContractAndLineLocation.Company 
                ContractAndLineLocation.Location 
                ContractAndLineLocation.RequestingLocation
                Contract
                ContractLine

        ByLineAndLocationOnly
            Sort Order 
                ContractGroup 
                Contract 
                ContractLine
                ContractAndLineLocation.Company 
                ContractAndLineLocation.Location 
            Instance Selection 
                where (LocationOnly)

        ByLineAndRequestingLocationOnly 
            Sort Order 
                ContractGroup 
                Contract 
                ContractLine
                ContractAndLineLocation.Company 
                ContractAndLineLocation.RequestingLocation 
            Instance Selection 
                where (RequestingLocationOnly)

        ByLineAndCompanyOnly 
            Sort Order 
                ContractGroup 
                Contract 
                ContractLine
                ContractAndLineLocation.Company 
            Instance Selection 
                where (CompanyOnly)            

        ByCompanyOnly 
            Sort Order 
                ContractGroup 
                Contract 
                ContractAndLineLocation.Company 
            Instance Selection 
                where (CompanyAndHeaderOnly)

        ByLocationOnly 
            Sort Order 
                ContractGroup 
                Contract 
                ContractAndLineLocation.Company 
                ContractAndLineLocation.Location 
            Instance Selection 
                where (LocationAndHeaderOnly)

        ByRequestingLocationOnly
            Sort Order 
                ContractGroup 
                Contract 
                ContractAndLineLocation.Company 
                ContractAndLineLocation.RequestingLocation 
            Instance Selection 
                where (RequestingLocationAndHeaderOnly)

    Actions 

        Create is a Create Action 
            valid when (CanMaintain)
            completion message is "LocationCreated"
            Action Rules
                if (Taxable = 0
                and TaxCode !entered)
                    ClinicalSystemUse = true 
                
                constraint (Taxable > 0 or TaxCode entered or ClinicalSystemUse)
                    "MustEnterEitherTaxableYesOrNo,OrATaxCode,OrSelectClinicalSystemUse(WhicheverIsAvailable)"

                if (ClinicalSystemUse)
                    if (!Contract.ServicePurchaseType)
                        constraint (ContractAndLineLocation.Location entered or ContractAndLineLocation.RequestingLocation entered)
                            "MustEnterALocationOrARequestingLocation"

                if (ContractAndLineLocation.Location entered)
                    constraint (ContractAndLineLocation.RequestingLocation !entered)
                        "CannotEnterBothLocationAndRequestingLocation" 

                if (ContractAndLineLocation.RequestingLocation entered)
                    constraint (ContractAndLineLocation.Location !entered)
                        "CannotEnterBothLocationAndRequestingLocation" 

            Exit Rules 

                if (ClinicalSystemUse
                and Contract.HasBeenActivated)
                    invoke ManageConsolidatedItemMaster

        CreateForLine is a Create Action 
            valid when (CanMaintain)

            completion message is "LocationCreated"
            Action Rules
                if (Taxable = 0
                and TaxCode !entered)
                    ClinicalSystemUse = true 
                
                constraint (Taxable > 0 or TaxCode entered or ClinicalSystemUse)
                    "MustEnterEitherTaxableYesOrNo,OrATaxCode,OrSelectClinicalSystemUse(WhicheverIsAvailable)"

                if (ContractAndLineLocation.Location entered)
                    constraint (ContractAndLineLocation.RequestingLocation !entered)
                        "CannotEnterBothLocationAndRequestingLocation" 

                if (ContractAndLineLocation.RequestingLocation entered)
                    constraint (ContractAndLineLocation.Location !entered)
                        "CannotEnterBothLocationAndRequestingLocation" 

                if (ClinicalSystemUse)
                    if (!Contract.ServicePurchaseType)
                        constraint (ContractAndLineLocation.Location entered or ContractAndLineLocation.RequestingLocation entered)
                            "MustEnterALocationOrARequestingLocation"

                    LocalCompany             = ContractAndLineLocation.Company
                    LocalLocation            = ContractAndLineLocation.Location
                    LocalRequestingLocation  = ContractAndLineLocation.RequestingLocation 
                    if (HeaderLocationRel exists)
                        constraint (HeaderLocationRel.LineLevel = true)
                            "LocationExistsAtHeaderLevel;CannotAlsoBeCreatedAtLineLevel"
                    else
                        confirmation required 
                            "LocationNotDefinedForContractHeader;LocationThatAllowsLineLevelLocationsWillBeCreated;DoYouWantToContinue?"        

            Exit Rules 

                if (ClinicalSystemUse
                and HeaderLocationRel !exists)
                    invoke Create 
                        invoked.ContractGroup                               = ContractGroup 
                        invoked.Contract                                    = Contract 
                        invoked.ContractAndLineLocation.Company             = ContractAndLineLocation.Company 
                        invoked.ContractAndLineLocation.Location            = ContractAndLineLocation.Location 
                        invoked.ContractAndLineLocation.RequestingLocation  = ContractAndLineLocation.RequestingLocation
                        invoked.LineLevel                                   = true 
                
                if (ClinicalSystemUse
                and Contract.HasBeenActivated
                and ContractLineRel.HasBeenActivated)
                    invoke ManageConsolidatedItemMaster

        Update is an Update Action 
            valid when (CanMaintain)
            Action Rules

                if (ClinicalSystemUse)
                    constraint (ContractAndLineLocation.Location entered or ContractAndLineLocation.RequestingLocation entered)
                        "MustEnterALocationOrARequestingLocation"

                if (!ClinicalSystemUse)
                    constraint (Taxable > 0 or TaxCode entered)
                        "MustEnterEitherTaxableYesOrNoOrATaxCode"

                if (ContractAndLineLocation.Location entered)
                    constraint (ContractAndLineLocation.RequestingLocation !entered)
                        "CannotEnterBothLocationAndRequestingLocation" 

                if (ContractAndLineLocation.RequestingLocation entered)
                    constraint (ContractAndLineLocation.Location !entered)
                        "CannotEnterBothLocationAndRequestingLocation" 

                if (Contract.HasBeenActivated)
                    if (ClinicalSystemUse changed)
                        invoke ManageConsolidatedItemMaster
                            if (ClinicalSystemUse = false)
                                invoked.ParmInactivate = true

        CreateCapitatedLocationItems is a Set Action 
            restricted
            Parameters 
				ParmContractGroup			is a ContractGroup 
				ParmContract            	is a Contract 
				ParmComponentGroup      	is a ContractCapitatedComponentGroup
				ParmContractLine        	is a ContractLine 
                CompanyOnly                 is Boolean 
				UseBaseCostForPrimary    	is Boolean

            Instance Selection 
                where (ParmContractGroup = ContractGroup
                and    ParmContract      = Contract
                and   !RequestingLocationOnly
                and   !ContractLine entered
                and    ClinicalSystemUse)

            Action Rules
                Instance Rules 
                    LocalComponentGroup     = ParmComponentGroup
                    LocalContractLine       = ParmContractLine 
                    LocalCompany            = ContractAndLineLocation.Company 
                    LocalLocation           = ContractAndLineLocation.Location 

                    for each ContractCapitatedComponentGroupItemRel 
                        LocalComponentGroupItem = each.ContractCapitatedComponentGroupItem
                        if (CompanyOnly)
                            if (ContractCapitatedCompanyOnlyItemRel !exists)
                                invoke Create ContractCapitatedCompanyLocationItem 
                                    invoked.ContractGroup                       = ParmContractGroup 
                                    invoked.Contract                            = ParmContract 
                                    invoked.ContractLine                        = ParmContractLine 
                                    invoked.ContractCapitatedComponentGroup     = ParmComponentGroup 
                                    invoked.ContractCapitatedComponentGroupItem = each.ContractCapitatedComponentGroupItem
                                    invoked.ComponentContractLine               = each.ComponentContractLine 
                                    invoked.Company                             = ContractAndLineLocation.Company 
                                    invoked.LocalUseBaseCostForPrimary          = UseBaseCostForPrimary
                        else 
                        if (!CompanyOnly)
                            if (ContractCapitatedCompanyLocationItemRel !exists)
                                invoke Create ContractCapitatedCompanyLocationItem 
                                    invoked.ContractGroup                       = ParmContractGroup 
                                    invoked.Contract                            = ParmContract 
                                    invoked.ContractLine                        = ParmContractLine 
                                    invoked.ContractCapitatedComponentGroup     = ParmComponentGroup 
                                    invoked.ContractCapitatedComponentGroupItem = each.ContractCapitatedComponentGroupItem
                                    invoked.ComponentContractLine               = each.ComponentContractLine 
                                    invoked.Company                             = ContractAndLineLocation.Company 
                                    invoked.Location                            = ContractAndLineLocation.Location 
                                    invoked.LocalUseBaseCostForPrimary          = UseBaseCostForPrimary

        ManageConsolidatedItemMaster is an Instance Action 
            restricted 
            Parameters 
                ParmInactivate  is Boolean 

            Action Rules

                if (ContractGroup.StoreConsolidatedItemAndLocation)
                    if (ContractLine !entered
                    and ContractRel.NumberOfLinesQuickApproximation > 50)
                        invoke ManageConsolidatedItemMasterBackground 
                            invoked.ParmInactivate = ParmInactivate 

                    else 
                    if (ContractLine entered)
                        invoke ManageConsolidatedItemMasterForeground 
                            invoked.ParmInactivate = ParmInactivate 

        ManageConsolidatedItemMasterBackground is an Instance Action 
            restricted 
            run in background

            Parameters 
                ParmInactivate  is Boolean 

            Action Rules 
                for each ClinicalUseLinesRel 
                    if (each.ConsolidatedItemMasterExists)
                        LocalConsolidatedItemMaster    = each.ConsolidatedItemMasterRel.ConsolidatedItemMaster
                        if (ConsolidatedItemLocationRel exists)
                            invoke Update first ConsolidatedItemLocationRel
                        else 
                        if (CrossLocationsDoNotExist
                        and ParmInactivate != true)
                            invoke Create ConsolidatedItemLocation 
                                invoked.ContractGroup           = ContractGroup 
                                invoked.InventoryCompany        = ContractAndLineLocation.Company
                                invoked.InventoryLocation       = ContractAndLineLocation.Location
                                invoked.RequestingLocation      = ContractAndLineLocation.RequestingLocation
                                invoked.ConsolidatedItemMaster  = LocalConsolidatedItemMaster
                                invoked.ContractOrItemLocation  = 2
        
        ManageConsolidatedItemMasterForeground is an Instance Action 
            restricted 

            Parameters 
                ParmInactivate  is Boolean 

            Action Rules     

                if (ContractLine !entered)
                    for each ClinicalUseLinesRel 
                        display "Steve;InForEach"
                        display "Steve;OnEachLine(each.ContractLine)"
                        display "Steve;LineConsolItemMasterExists<each.ConsolidatedItemMasterExists>"
                        if (each.ConsolidatedItemMasterExists)
                            LocalConsolidatedItemMaster    = each.ConsolidatedItemMasterRel.ConsolidatedItemMaster
                            if (ConsolidatedItemLocationRel exists)
                                invoke Update first ConsolidatedItemLocationRel
                            else 
                            if (CrossLocationsDoNotExist
                            and ParmInactivate != true)
                                invoke Create ConsolidatedItemLocation 
                                    invoked.ContractGroup           = ContractGroup 
                                    invoked.InventoryCompany        = ContractAndLineLocation.Company
                                    invoked.InventoryLocation       = ContractAndLineLocation.Location
                                    invoked.RequestingLocation      = ContractAndLineLocation.RequestingLocation
                                    invoked.ConsolidatedItemMaster  = LocalConsolidatedItemMaster
                                    invoked.ContractOrItemLocation  = 2
                else 
                if (ContractLine entered)
                    if (ContractLine.ConsolidatedItemMasterExists)
                        LocalConsolidatedItemMaster    = ContractLine.ConsolidatedItemMasterRel.ConsolidatedItemMaster
                        if (ConsolidatedItemLocationRel exists)
                            invoke Update first ConsolidatedItemLocationRel
                        else 
                        if (CrossLocationsDoNotExist
                        and ParmInactivate != true)
                            invoke Create ConsolidatedItemLocation 
                                invoked.ContractGroup           = ContractGroup 
                                invoked.InventoryCompany        = ContractAndLineLocation.Company
                                invoked.InventoryLocation       = ContractAndLineLocation.Location
                                invoked.RequestingLocation      = ContractAndLineLocation.RequestingLocation
                                invoked.ConsolidatedItemMaster  = LocalConsolidatedItemMaster
                                invoked.ContractOrItemLocation  = 2

        AddLocationsToAllLines is an Instance Action 
            default label is "AddThisLocationToAllLines"
            valid when (CanAddLocationsToAllLines)
            disable multiple instance selection
            Action Rules 

                invoke CreateClinicalLineLocations ContractLine 
                    invoked.ParmContractGroup           = ContractGroup 
                    invoked.ParmContract                = Contract 
                    invoked.ParmInventoryCompany        = ContractAndLineLocation.Company
                    invoked.ParmInventoryLocation       = ContractAndLineLocation.Location
                    invoked.ParmRequestingLocation      = ContractAndLineLocation.RequestingLocation                
        
        ChangeToLineLevelClinicalLocation is an Instance Action 
            valid when (CanChangeToLineLevel)
            disable multiple instance selection
            Parameters 
                CreateARecordForAllLines    is Boolean 

            Action Rules 

                LineLevel = true 

                if (CreateARecordForAllLines)

                    invoke CreateClinicalLineLocations ContractLine 
                        invoked.ParmContractGroup           = ContractGroup 
                        invoked.ParmContract                = Contract 
                        invoked.ParmInventoryCompany        = ContractAndLineLocation.Company
                        invoked.ParmInventoryLocation       = ContractAndLineLocation.Location
                        invoked.ParmRequestingLocation      = ContractAndLineLocation.RequestingLocation

            Exit Rules

                LocalContractGroup      = ContractGroup 
                LocalContract           = Contract 
		        LocalCompany            = ContractAndLineLocation.Company
                LocalLocation		    = ContractAndLineLocation.Location 
				LocalRequestingLocation = ContractAndLineLocation.RequestingLocation
                if (!CreateARecordForAllLines
                and ContractGroup.StoreConsolidatedItemAndLocation
                and Contract.HasBeenActivated)
                    for each ClinicalUseLinesForDeleteRel 
                        if (each.ConsolidatedItemMasterExists)
                            LocalConsolidatedItemMaster    = each.ConsolidatedItemMasterRel.ConsolidatedItemMaster
                            if (ConsolidatedItemLocationForDeleteRel exists)
                                invoke Update first ConsolidatedItemLocationForDeleteRel

        ChangeToHeaderLevelClinicalLocation is an Instance Action 
            valid when (CanChangeToHeaderLevel)

            Action Rules 

                LineLevel = false 
                invoke RemoveLineLevelClinicalLocations
                    invoked.ParmContractGroup           = ContractGroup 
                    invoked.ParmContract                = Contract 
                    invoked.ParmInventoryCompany        = ContractAndLineLocation.Company
                    invoked.ParmInventoryLocation       = ContractAndLineLocation.Location
                    invoked.ParmRequestingLocation      = ContractAndLineLocation.RequestingLocation

            Exit Rules 

                if (Contract.HasBeenActivated)
                    invoke ManageConsolidatedItemMaster

        RemoveLineLevelClinicalLocations is a Set Action 
            restricted 
            Parameters 
                ParmContractGroup                   is like ContractGroup 
                ParmContract                        is like Contract 
                ParmInventoryCompany                is like GeneralLedgerCompany 
                ParmInventoryLocation               is like InventoryLocation  
                ParmRequestingLocation              is like RequestingLocation 

            Instance Selection 
                where (ContractLineEntered 
                and    ClinicalSystemUse 
                and    ParmContractGroup        = ContractGroup
                and    ParmContract             = Contract 
                and    ParmInventoryCompany     = ContractAndLineLocation.Company 
                and    ParmInventoryLocation    = ContractAndLineLocation.Location 
                and    ParmRequestingLocation   = ContractAndLineLocation.RequestingLocation)


            Action Rules 

                Instance Rules 

                    if (NotTaxRelated)
                        invoke Delete 
                    else 
                        invoke Update 
                            invoked.ClinicalSystemUse = false 
        
        MaintainClinicalLocations is an Instance Action 
            default label is "MaintainAllLocations"
            restricted
            Action Rules 

        MaintainClinicalLocationsSet is a Set Action 
            restricted
            Parameters
                ParmContractGroup                 is a ContractGroup
                ParmContract                      is a Contract
                ClinicalLocationMaintenanceOption is Numeric 2 
                SetToLineLevel                    is Boolean 

            Instance Selection 
                where (ParmContractGroup = ContractGroup
                and   ParmContract = Contract
                and   ContractLineNotEntered)
            
            Sort Order 
                ContractGroup 
                Contract 

            Local Fields 
                ClinicalUseChange is Boolean
            
            Action Rules 
                Contract Set Rules

                    Exit Rules 

                        if (Contract.ContractLocationRel !exists)
                            for each Contract.LocationOnlyParticipantGroupContractRel
                                invoke Delete each  
            
                Instance Rules

                    if (ClinicalLocationMaintenanceOption = 1)
                        ClinicalSystemUse = true 
                        ClinicalUseChange = true 
                        if (SetToLineLevel)
                            LineLevel     = true 
                    if (ClinicalLocationMaintenanceOption = 2)
                        ClinicalSystemUse = false 
                        ClinicalUseChange = true 
                        LineLevel         = false 
                    if (ClinicalLocationMaintenanceOption = 3
                    and RequestingLocationOnly)
                        ClinicalSystemUse = true  
                        ClinicalUseChange = true                                            
                        if (SetToLineLevel)
                            LineLevel     = true 
                    if (ClinicalLocationMaintenanceOption = 4
                    and RequestingLocationOnly)
                        ClinicalSystemUse = false  
                        ClinicalUseChange = true     
                        LineLevel         = false                     
                    if (ClinicalLocationMaintenanceOption = 5
                    and LocationOnly)
                        ClinicalSystemUse = true 
                        ClinicalUseChange = true 
                        if (SetToLineLevel)
                            LineLevel     = true                                                                     
                    if (ClinicalLocationMaintenanceOption = 6
                    and LocationOnly)
                        ClinicalSystemUse = false 
                        ClinicalUseChange = true 
                        LineLevel         = false 
                    if (ClinicalLocationMaintenanceOption = 7)
                        invoke DeleteClinical
                    if (ClinicalLocationMaintenanceOption = 8
                    and RequestingLocationOnly)
                        invoke DeleteClinical
                    if (ClinicalLocationMaintenanceOption = 9
                    and LocationOnly)
                        invoke DeleteClinical     
                    if (ClinicalLocationMaintenanceOption = 10
                    and ParOrCartLocation)
                        invoke DeleteClinical 
                    if (ClinicalUseChange = true
                    and Contract.HasBeenActivated)
                        invoke ManageConsolidatedItemMaster                                        
        
        Delete is a Delete Action 
            valid when (CanMaintain)

            Entrance Rules 

                LocalContractGroup              = ContractGroup  
                LocalContract                   = Contract 
                LocalCompany                    = ContractAndLineLocation.Company     
                LocalLocation                   = ContractAndLineLocation.Location
                LocalRequestingLocation         = ContractAndLineLocation.RequestingLocation
            Exit Rules

                if (ContractGroup.StoreConsolidatedItemAndLocation
                and Contract.HasBeenActivated
                and ClinicalSystemUse = true)
                    for each ClinicalUseLinesForDeleteRel 
                        if (each.ConsolidatedItemMasterExists)
                            LocalConsolidatedItemMaster    = each.ConsolidatedItemMasterRel.ConsolidatedItemMaster
                            if (ConsolidatedItemLocationForDeleteRel exists)
                                invoke Update first ConsolidatedItemLocationForDeleteRel

        DeleteClinical is an Instance Action
            restricted
            Action Rules 

                if (TaxRelated)
                    ClinicalSystemUse = false
                else
                if (!TaxRelated)
                    invoke Delete 

            Exit Rules

                if (ContractGroup.StoreConsolidatedItemAndLocation
                and Contract.HasBeenActivated
                and ClinicalSystemUse = true)
                    for each ClinicalUseLinesForDeleteRel 
                        if (each.ConsolidatedItemMasterExists)
                            LocalConsolidatedItemMaster    = each.ConsolidatedItemMasterRel.ConsolidatedItemMaster
                            if (ConsolidatedItemLocationForDeleteRel exists)
                                invoke Update first ConsolidatedItemLocationForDeleteRel



