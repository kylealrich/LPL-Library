BillingInvoiceLineLotSerialBin is a BusinessClass
    owned by bl
    prefix is OID
    classic name is OEINVCDTL

    Ontology
        symbolic key is BillingInvoiceLineLotSerialBin
            classic set name is OIDSET1
            classic name for BillingInvoiceLineLotSerialBin.UnitOfMeasure is UOM
            classic name for BillingInvoice.InvoiceNumber is INVC-NUMBER
            classic name for BillingInvoiceLine is LINE-NBR

    Patterns
        implements ContextualParent
        implements StaticJava
        disable AuditIndex
        implements Archivable

    Persistent Fields

        Quantity
        InvoiceCatchWeightQuantity is a Quantity
            classic name is INVC-CW-QTY
        UnitOfMeasureMultiplier    is an UOMMultiplier
            classic name is UOM-MULT

    Derived Fields

        LotString    is a StringField
            type is Alpha size 30
            restricted
            BillingInvoiceLineLotSerialBin.Lot
            blank
            BillingInvoiceLineLotSerialBin.Sublot
            blank
            BillingInvoiceLineLotSerialBin.Bin
            blank
            BillingInvoiceLineLotSerialBin.UnitOfMeasure

        BinString    is a StringField
            type is Alpha size 30
            restricted
            blank
            BillingInvoiceLineLotSerialBin.Bin
            blank
            BillingInvoiceLineLotSerialBin.UnitOfMeasure

        SerialString is a StringField
            type is Alpha size 30
            restricted
            BillingInvoiceLineLotSerialBin.Bin
            blank
            BillingInvoiceLineLotSerialBin.Serial
            blank
            BillingInvoiceLineLotSerialBin.UnitOfMeasure

        UomString    is a StringField
            type is Alpha size 30
            restricted
            blank
            BillingInvoiceLineLotSerialBin.UnitOfMeasure

        PrintString  is a ConditionalField
            type is Alpha size 30
            restricted
            if (IsSerialItem)
                SerialString
            else
            if (IsLotTracked)
                LotString
            else
            if (IsBinTracked)
                BinString
            else
                UomString

    Conditions

        IsBinTracked
        	restricted
            when (ItemlocRel.BinTracked)

        IsLotTracked
        	restricted
            when (ItemmastRel.LotTracked = "Y")

        IsSerialItem
        	restricted
            when (ItemmastRel.SerialTracked.SerialRequiredAtIssue
            or    ItemmastRel.SerialTracked.SerialRequiredAtReceipt)

    Relations

        IccompanyRel
            one-to-one relation to InventoryCompany
            required
            Field Mapping uses symbolic key
                related.Company = Company

        OeinvclineRel
            one-to-one relation to BillingInvoiceLine
            required
            Field Mapping uses symbolic key
                related.Company                      = Company
                related.BillingInvoice.InvoicePrefix    = BillingInvoice.InvoicePrefix
                related.BillingInvoice.InvoiceNumber = BillingInvoice.InvoiceNumber
                related.BillingInvoiceLine           = BillingInvoiceLine

        ColineRel
            one-to-one relation to CustomerOrderLine
            required
            Field Mapping uses symbolic key
                related.Company           = Company
                related.CustomerOrder     = OeinvclineRel.CustomerOrder
                related.CustomerOrderLine = OeinvclineRel.BillingInvoiceLine

        ItemlocRel
            one-to-one relation to ItemLocation
            required
            Field Mapping uses symbolic key
                related.Company           = Company
                related.InventoryLocation = OeinvclineRel.Location
                related.Item              = OeinvclineRel.Item

        ItemmastRel
            one-to-one relation to Item
            required
            Field Mapping uses symbolic key
                related.ItemGroup = IccompanyRel.ItemGroup
                related.Item      = OeinvclineRel.Item

	Actions
		
		Create is a Create Action
		
		Update is an Update Action
		
		Delete is a Delete Action
		
		Purge is a Purge Action
			restricted

